.***************************************************************************
.* System    :   Patient                                                   *
.* Program   :   IBAPMS72                                                  *
.* Desc      :                                                             *
.***************************************************************************
.* Date      :   25/03/2002                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :   This program will loop patwr1af and validate the admission*
.*               and standby patients for this ward/bed                    *
.*                                                                         *
.*  Mods     :                                                             *
.***************************************************************************
.* V12.00.01 22/05/2025 Thanh T         TSK 0955096                        *
.*           Changed for alphanumeric visit number                         *
.***************************************************************************
.* V10.03.01 24/01/2013 Jill Parkinson CAR 279239                          *
.*           Added check that last tran record matches current locaiton    *
.* V10.01.01 03/02/2011 Jill Parkinson CAR 233088                          *
.*           Added pmsvx1af                                                *
.*  V9.02.01    21/04/2002  Jill Habasque                                  *
.*              Fixed key before reading patnobef                          *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATWR1FD/INC
          INC       PATMI1FD/INC
          INC       PATTRNFD/INC
          INC       PMSVX1FD/INC
          INC       PATVISFD/INC
          INC       PATNOBFD/INC
          INC       PATONLFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
RECNUM    FORM      8
DISPDATE  DIM       11
STATDESC  DIM       5
MTHNAM3   DIM       3
JAN       INIT      "Jan"
FEB       INIT      "Feb"
MAR       INIT      "Mar"
APR       INIT      "Apr"
MAY       INIT      "May"
JUN       INIT      "Jun"
JUL       INIT      "Jul"
AUG       INIT      "Aug"
SEP       INIT      "Sep"
OCT       INIT      "Oct"
NOV       INIT      "Nov"
DEC       INIT      "Dec"
SAVKEY20  DIM       20
.
PRGID     INIT      "IBAPMS72"
PRGNAM    INIT      "Check Ward File      "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML1000  * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process patwr1af
          CALL      NOBE0000               * process patnobef
          CALL      ONLF0000               * process patonlvf
          CALL      CADM0000               * process current admissions
          CALL      ONLV0000               * process onleave
          PRINT     *N,"*** End Of Report ***"
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          MOVE      ZERO,RECNUM
.
          DISPLAY   *P56:24,"Opening patwr1af"
          OPEN      PATWR1A1,"patwr1af" 
          OPEN      PATWR1A2,"patwr1af" 
          OPEN      PATWR1A4,"patwr1af" 
.
          DISPLAY   *P56:24,"Opening patvisaf"
          OPEN      PATVISA1,"patvisaf" 
.
          DISPLAY   *P56:24,"Opening patonlvf"
          OPEN      PATONLV1,"patonlvf" 
          OPEN      PATONLV2,"patonlvf" 
.
          DISPLAY   *P56:24,"Opening patmi1af"
          OPEN      PATMI1A1,"patmi1af" 
          OPEN      PATMI1A2,"patmi1af" 
          OPEN      PMSVX1A1,"pmsvx1af" 
.
          DISPLAY   *P56:24,"Opening patnobef"
          OPEN      PATNOBE2,"patnobef" 
          OPEN      PATNOBE3,"patnobef" 
.
          DISPLAY   *P56:24,"Opening pattranf"
          OPEN      PATTRAN2,"pattranf" 
.
          DISPLAY   *P56:24,"Opening patmi1af"
          OPEN      PATMI1A2,"patmi1af" 
          OPEN      PMSVX1A1,"pmsvx1af" 
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Report "
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  CLOCK     TIME,CTIMEIS
          CALL      HEAD80
          PRINT     *20,"** Ward File **"
          CALL      HEAD0000
          PACK      KEY6,SP70
          CALL      RDSWARD1
PROC1000  CALL      RDKWARD1
          BRANCH    OVRCD,PROC9999
.
          MATCH     ZEROVISN,WADMNO
          GOTO      PROC2000 IF EQUAL
.
          PACK      KEY8,DWADMNO,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,PROC9000
.
          COMPARE   TWO,ASTAT
          GOTO      PROC9000 IF NOT EQUAL
.
          MATCH     AWARD,WWARD
          GOTO      PROC9000 IF NOT EQUAL
.
          MATCH     ABED,WBED
          GOTO      PROC9000 IF NOT EQUAL
.
PROC2000  MATCH     ZEROVISN,WSTBY
          GOTO      PROC1000 IF EQUAL
.
          PACK      KEY8,WSTBY,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,PROC2100
.
          COMPARE   TWO,ASTAT
          GOTO      PROC1000 IF EQUAL
.
PROC2100  MOVE      WSTBY,DWADMNO
.
PROC9000  PACK      KEY8,DWADMNO,SP70
          CALL      RDVISA1
          IF        OVRCD=1
            MOVE      SP70,PVIURNO
            MOVE      ZEROVISN,PVIBILL
          ENDIF
.
          CALL      PRIN0000                    * Print record details
.
          GOTO      PROC1000
.
PROC9999  CALL      UND80
          RETURN
+
.**************************************************************************
.*                               NOBE0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
NOBE0000  PRINT     *N,*20,"** No Bed File **"
          CALL      HEAD0000
          PACK      KEY16,SP70
          CALL      RDSNOBE2
NOBE1000  CALL      RDKNOBE2
          BRANCH    OVRCD,NOBE9999
.
          MATCH     ZEROVISN,NBADMNO
          GOTO      NOBE1000 IF EQUAL
.
          PACK      KEY8,NBADMNO,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,NOBE9000
.
          COMPARE   TWO,ASTAT
          GOTO      NOBE1000 IF EQUAL
.
NOBE9000  PACK      KEY8,NBADMNO,SP70
          CALL      RDVISA1
          IF        OVRCD=1
            MOVE      SP70,PVIURNO
          ENDIF
          MOVE      NBADMNO,DWADMNO
          MOVE      NBWARD,WWARD
          MOVE      NBROOM,WBED
.
          CALL      PRIN0000                    * Print record details
.
          GOTO      NOBE1000
.
NOBE9999  CALL      UND80
          RETURN
+
.**************************************************************************
.*                               ONLF0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
ONLF0000  PRINT     *N,*20,"** On Leave File **"
          CALL      HEAD0000
          PACK      KEY14,SP70
          CALL      RDSONLV1
ONLF1000  CALL      RDKONLV1
          BRANCH    OVRCD,ONLF9999
.
          MATCH     ZEROVISN,OADMNO
          GOTO      ONLF1000 IF EQUAL
.
          PACK      KEY8,OADMNO,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,ONLF9000
.
          COMPARE   FOUR,ASTAT
          GOTO      ONLF1000 IF EQUAL
.
.
ONLF9000  PACK      KEY8,OADMNO,SP70
          CALL      RDVISA1
          IF        OVRCD=1
            MOVE      SP70,PVIURNO
          ENDIF
.
          MOVE      OADMNO,DWADMNO
          MOVE      OWARD,WWARD
          MOVE      OBED,WBED
          CALL      PRIN0000                    * Print record details
.
          GOTO      ONLF1000
.
ONLF9999  CALL      UND80
          RETURN
+
.**************************************************************************
.*                               CADM0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
CADM0000  PRINT     *N,*20,"** Current Admissions **"
          CALL      HEAD0000
          PACK      KEY9,TWO,SP70
          CALL      RDSMISS2
CADM1000  CALL      RDKMISS2
          BRANCH    OVRCD,CADM9999
.
          COMPARE   TWO,ASTAT
          GOTO      CADM9999 IF NOT EQUAL
.
          MATCH     SP70,ABED
          GOTO      CADM2000 IF EQUAL
.
          MATCH     SP70,AWARD
          GOTO      CADM2000 IF EQUAL
.
          PACK      KEY14,DAADMNO,SP70
          CALL      RDSWARD2
          CALL      RDKWARD2
          BRANCH    OVRCD,CADM2000
.
          MATCH     AADMNO,WADMNO
          IF        @EQUAL
            MATCH     AWARD,WWARD
            GOTO      CADM9000 IF NOT EQUAL
.
            MATCH     ABED,WBED
            GOTO      CADM9000 IF NOT EQUAL
          ENDIF
.
          PACK      KEY30,AADMNO,Z70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,CADM9100
.
          MATCH     DTADMN,DAADMNO
          GOTO      CADM9100 IF NOT EQUAL
.
          MATCH     AWARD,TWARD
          GOTO      CADM9100 IF NOT EQUAL
.
          MATCH     ABED,TBED
          GOTO      CADM9100 IF NOT EQUAL
.
          GOTO      CADM1000
.
CADM2000  PACK      KEY14,DAADMNO,SP70
          CALL      RDSWARD4
          CALL      RDKWARD4
          BRANCH    OVRCD,CADM3000
.
          MATCH     AADMNO,WSTBY
          GOTO      CADM3000 IF NOT EQUAL
.
. **** award and abed should be blank for standby patients *****
          MATCH     AADMNO,WSTBY
          IF        @EQUAL
            MATCH     SP70,AWARD
            GOTO      CADM9000 IF NOT EQUAL
.
            MATCH     SP70,ABED
            GOTO      CADM9000 IF NOT EQUAL
          ENDIF
.
          PACK      KEY30,AADMNO,Z70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,CADM9100
.
          MATCH     DTADMN,DAADMNO
          GOTO      CADM9100 IF NOT EQUAL
.
          MATCH     WWARD,TWARD
          GOTO      CADM9100 IF NOT EQUAL
.
          MATCH     WBED,TBED
          GOTO      CADM9100 IF NOT EQUAL
.
          GOTO      CADM1000
.
CADM3000  PACK      KEY13,DAADMNO,SP70
          CALL      RDSNOBE3
          CALL      RDKNOBE3
          BRANCH    OVRCD,CADM9000
.
          MATCH     NBADMNO,AADMNO
          GOTO      CADM9000 IF NOT EQUAL
.
          MATCH     AWARD,NBWARD
          GOTO      CADM9000 IF NOT EQUAL
.
          PACK      KEY30,AADMNO,Z70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,CADM9100
.
          MATCH     DTADMN,DAADMNO
          GOTO      CADM9100 IF NOT EQUAL
.
          MATCH     AWARD,TWARD
          GOTO      CADM9100 IF NOT EQUAL
.
          GOTO      CADM1000
.

CADM9000  PACK      KEY8,DAADMNO,SP70
          CALL      RDVISA1
          IF        OVRCD=1
            MOVE      SP70,PVIURNO
            MOVE      SP70,PVIBILL
          ENDIF
          MOVE      SP70,WWARD
          MOVE      SP70,WBED
.
          MOVE      DAADMNO,DWADMNO
          CALL      PRIN0000                    * Print record details
.
          GOTO      CADM1000
.
CADM9100  PACK      KEY8,DAADMNO,SP70
          CALL      RDVISA1
          IF        OVRCD=1
            MOVE      SP70,PVIURNO
            MOVE      SP70,PVIBILL
          ENDIF
          MOVE      SP70,WWARD
          MOVE      SP70,WBED
.
          MOVE      DAADMNO,DWADMNO
          CALL      PRTRN000                    * Print record details
.
          GOTO      CADM1000
.
CADM9999  CALL      UND80
          RETURN
+
.**************************************************************************
.*                               ONLV0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
ONLV0000  PRINT     *N,*20,"** On Leave Admissions **"
          CALL      HEAD0000
          PACK      KEY9,FOUR,SP70
          CALL      RDSMISS2
ONLV1000  CALL      RDKMISS2
          BRANCH    OVRCD,ONLV9999
.
          COMPARE   FOUR,ASTAT
          GOTO      ONLV9999 IF NOT EQUAL
.
          PACK      KEY14,DAADMNO,SP70
          CALL      RDSONLV2
          CALL      RDKONLV2
          BRANCH    OVRCD,ONLV9000
.
          MATCH     AADMNO,OADMNO
          GOTO      ONLV1000 IF EQUAL
.
.
ONLV9000  PACK      KEY8,OADMNO,SP70
          CALL      RDVISA1
          IF        OVRCD=1
            MOVE      SP70,PVIURNO
            MOVE      SP70,PVIBILL
          ENDIF
.
          MOVE      OADMNO,DWADMNO
          MOVE      SP70,WWARD
          MOVE      SP70,WBED
          CALL      PRIN0000                    * Print record details
.
          GOTO      ONLV1000
.
ONLV9999  CALL      UND80
          RETURN
+
.**************************************************************************
.*                               HEAD0000              Called by:PROC0000  *
.**************************************************************************
HEAD0000  CALL      UND80
          PRINT     *1,"| U/R":
                    *15,"| Admission Number":
                    *48,"| Ward":
                    *55,"| Bed":
                    *80,"|"
          CALL      UND80

HEAD9999  RETURN
+
.**************************************************************************
.*                               PRIN0000              Called by:PROC0000  *
.**************************************************************************
PRIN0000  PRINT     *2,"|",PVIURNO:
                    *15,"| ",DWADMNO:
                    *48,"| ",WWARD:
                    *55,"| ",WBED:
                    *80,"|"
PRIN9999  RETURN
+
.**************************************************************************
.*                               PRIN0000              Called by:PROC0000  *
.**************************************************************************
PRTRN000  PRINT     *2,"|",PVIURNO:
                    *15,"| ",DWADMNO:
                    *48,"|Invalid Transfer/Ward file rec":
                    *80,"|"
PRTRN999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATWR1IO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       PMSVX1IO/INC
          INC       PATNOBIO/INC
          INC       PATONLIO/INC
          INC       PATVISIO/INC

. *****************************************************************************
. *                                                                           *
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     Program Name  :   IBARCP62                                            *
. *                                                                           *
. *     Function      :   Receipt Audit Report                                *
. *                                                                           *
. *     Modifications :                                                       *
. *       V10.14.01 21/03/2019  J.Tan            TSK 0857392                  *
. *                 Changed RCP Transaction count from DIM3 to DIM5           *
. *       V10.11.01 22/11/2017 Ania P    TSK 0261630                          *
. *                 Moved TFILNAME to CFNAMEDP in INIT0000                    *
. *       V10.07.02 29/01/2016  J.Tan    CAR 303942                           *
. *                 Mods for Payment types                                    *
. *       V10.07.01 16/12/2015  J.Tan    CAR 303942                           *
. *                 Mods for new payment types                                *
. *       V10.02.01 28/07/2010  J.Tan    CAR 244159                           *
. *                 Mods for Credit Card Surcharge Register 89                *
. *       V9.12.01  06/10/2009  WeeLee Koo    CAR 205680                      *
. *                 Added From Date and To Date                               *
. *       V9.09.02  26/03/2008  Ebon Clements CAR 162993                      *
. *                 Added cash drawer filter                                  *
. *       V9.09.01  07/09/2007  Ebon Clements CAR 149260                      *
. *                 Changed Payee to Payer                                    *
. *       V9.03.06  01/06/2004  Lina Vo CAR 50222                             *
. *                 Fixed to display cancelled receipts                       *
. *       V9.03.05  31/05/2004  J.Tan   CAR 50273                             *
. *                 Fixed printing the frist range of receipt number          *
. *       V9.03.04  12/05/2004  Lina Vo      CAR 49813                        *
. *                 Changed Money Order to use same field as Cheque Number    *
. *       V9.03.03  03/03/2004  Lina Vo      CAR 47921                        *
. *                 Removed use of PRIDBTFD & IO.                             *
. *       V9.03.02  06/11/2003  Lina Vo      CAR 44084                        *
. *                 Changed program to use new receipting tables.             *
. *                 Changed layout of audit report.                           *
. *       V9.03.01  06/11/2003  Lina Vo      CAR 44084                        *
. *                 Changed Status from OK to Un-released.                    *
. *                 Added extra Reconciled column to the report.              *
. *       V9.02.02  25/02/2003  Dean Cramer                                   *
. *                 Recompiled for RCPCHQFD - CHQPTYPE removed                *
. *       V9.02.01  14/02/2003  Dean Cramer  CAR 22238                        *
. *                 Recompiled for RCPTRNFD & RCPCHQFD                        *
. *       V5.08.01  29/08/2000  Caleb Sharman                                 *
. *                 Changed Health Fund variables to be 8 chars               *
. *       V5.07.07  30/09/1999  Andrew Peel  SRF  631904                      *
. *                 Fixed printing of report - page breaks not correct        *
. *       V5.07.06  09/06/1999  Greg Horvat                                   *
. *                 Recompiled for PRIINVIO - Fixed problem with direct read  *
. *       V5.07.05  31/03/1999 Jill Habasque                                  *
. *                 Fixed display of System & Payee on report                 *
. *       V5.07.04  31/03/1999 Jill Habasque                                  *
. *                 Changed to use HEAD132                                    *
. *       V5.07.03  30/03/1999 Jill Habasque                                  *
. *                 Fixed line up of report                                   *
. *       V5.07.B02 07/01/1999  Jim Stathopoulos                              *
. *                 Modified KTDAT                                            *
. *       V5.07.B01 02/11/1998  Davin                                         *
. *                 Mods for 507                                              *
. *****************************************************************************
. *     V5.05.02  24/04/1998  Sandra Barcham    HUT                           *
. *               Only open patient files if required                         *
. *     V5.05.01  20/04/1998  Steve Armstrong   STH                           *
. *               Recompiled for mods to PATDPTHV &                           *
. *               PATDPATH                                                    *
. *****************************************************************************
. *                       V5.01.00  20/01/92  DIG                             *
. *                       Ported from release 5                               *
. *     V5.01.01  19/01/94 Matt Surridge                                      *
. *               Put in check before opening Private                         *
. *               Practice Files.                                             *
.*      V5.01.02  02/06/94  ROD                                               *
.*                Access invoices across multi-databases                      *
.*      V5.02.01  18/04/1995  Steve Armstrong                                 *
.*                Recompiled for changes to RCPTRNFD                          *
.*                Mods to use PRI instead of PPP                              *
. *****************************************************************************
+
          INC       STD001FD
.
          INC       DEBDBTFD/INC
          INC       PATDPTHV/INC
          INC       PATHOSFD/INC
          INC       RCPCONFD/INC
          INC       RCPBNKFD/INC
          INC       RCPBDTFD/INC
          INC       RCPDTEFD/INC
          INC       RCPCIDFD/INC
          INC       RCPREGFD/INC
          INC       PATCODFD/INC
          INC       PATINVFD/INC
          INC       PATMA1FD/INC
          INC       PRIINVFD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
BRANCH    DIM      13
.
CASHDRAW  DIM      6
CHG       FORM     1
CMDLINE   DIM      50
CODECR    INIT     "CR"
DISCOUNT  FORM     8.2
DRAWERNM  DIM      40
DRNAME    DIM      10
DISPNAME  DIM       60
DIM10     DIM       10        * From Date Title
DIM10A    DIM       10        * To   Date Title
DOWNCASE  INIT      "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz"
.
ENDDIM12  DIM      12
ENDRECNO  FORM     12
ENDDAY    FORM     2
ENDMON    FORM     2
ENDYER    FORM     2
ENDDATE   DIM      8
.
FIELD     FORM     1
FLAG      DIM      1
FRSTFLG   FORM     1
FROMDATE  DIM       8        * FROMdate ccyymmdd
FRCEN     FORM      2
FRDAY     FORM      2
FRMON     FORM      2
FRYER     FORM      2
.
.
LINCNT    FORM     2
.
MRCNNOHS  FORM     2
.
NAME35    DIM       35
.
OLDRECNO  DIM      12
.
PAGENO    FORM     3
PAYTYPE   FORM     1
PNAME     DIM      20
PRNTFLAG  FORM     1
PATFNAME  DIM       60
.
RECONCIL  DIM      3
RDD       DIM      2
RMM       DIM      2
RYY       DIM      2
RCC       DIM      2
.
SDESC     DIM      9
SP18      INIT     "                  "
SP19      INIT     "                   "
STATUS0   INIT     "Not Bankd"
STATUS1   INIT     "Cancelled"
STATUS2   INIT     "Banked   "
STRECNO   FORM     12
SYDESC    DIM      14
SYST0     INIT     "N/A   "
SYST1     INIT     "Pat Billing"
SYST2     INIT     "Pri Practice"
SYST5     INIT     "A/C Receivable"
SYST89    INIT     "Surcharge     "
SYST90    INIT     "EMR Deposit"
SYST91    INIT     "OUT Deposit"
SYST92    INIT     "GST Cash Sales"
SYST94    INIT     "Sund Debtr Rec"
SYST95    INIT     "Sund Cash Sale"
SYST96    INIT     "Pri Deposit"
SYST97    INIT     "Pat Deposit"
SYST98    INIT     "Internal Recpt"
SYST99    INIT     "Suspense"
.
TODATE    DIM       8        * TOdate ccyymmdd
TOCEN     FORM      2
TODAY     FORM      2
TOMON     FORM      2
TOYER     FORM      2
.
PDESC    DIM      20
TIME     DIM      8
TOTAMT   FORM     10.2
THIRTEEN FORM     "13"
XXDPATH  DIM      120
.
.-----------------------------------------------------------------------------
PRGID     INIT      "IBARCP62"
PRGNAM    INIT      "Receipt Audit Report"
VERSION   INIT      "V12.01.00"
.-----------------------------------------------------------------------------
+
.-----------------------------------------------------------------------------
.  MAIN0000 - Start Program 
.-----------------------------------------------------------------------------
.
MAIN0000  CALL      INIT0000            * display screen header & open file
.
MAIN1000  CALL      OPTN0000            * ask for option
          BRANCH    EXIT,MAIN9999
.
          CALL      PROC0000            * process & print report
          GOTO      MAIN1000
.
MAIN9999  CHAIN     PGM
          STOP
+
.-----------------------------------------------------------------------------
.  INIT0000 - Initialization
.-----------------------------------------------------------------------------
.
INIT0000  CALL      TFILENAM
          MOVE      TFILNAME,CFNAMEDP            * Must preceed CALL PATDPATH
          CALL      PATDPATH
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening rcpbnkaf";
          OPEN      RCPBNKA1,"rcpbnkaf"
.
          DISPLAY   *P64:24,"rcpbnkaf";
          OPEN      RCPBNKA2,"rcpbnkaf"
.
          DISPLAY   *P64:24,"rcpbdtaf";
          OPEN      RCPBDTA1,"rcpbdtaf"
.
          DISPLAY   *P64:24,"rcpcidaf";
          OPEN      RCPCIDA1,"rcpcidaf"
.
          DISPLAY   *P64:24,"rcpdteaf";
          OPEN      RCPDTEA1,"rcpdteaf"
.
          DISPLAY   *P64:24,"rcpregaf";
          OPEN      RCPREGA1,"rcpregaf"
.
          DISPLAY   *P64:24,"debdbtaf";
          OPEN      DEBDBTA1,"debdbtaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,THIRTY6;*87,HSYS2,HSYS3
.
          IF        HSYS2=1
            DISPLAY   *P64:24,"patinvrf";
            OPEN      PATINVR1,"patinvrf"
            DISPLAY   *P64:24,"patma1af";
            OPEN      PATMA1A1,"patma1af"
            OPEN      PATMX1A1,"patmx1af"
          ENDIF
.
          IF        HSYS3=1
            DISPLAY   *P64:24,"priinvof";
            OPEN      PRIINVO1,"priinvof"
          ENDIF
.
INIT9999  RETURN
+
.-----------------------------------------------------------------------------
.  OPTN0000 - display user options
.-----------------------------------------------------------------------------
.
OPTN0000  MOVE      ZERO,EXIT
          MOVE      "           0",OLDRECNO
          MOVE      ZERO,FRSTFLG
          MOVE      ZERO,TOTAMT
          MOVE      "60",LINCNT
          HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF:
                    *P1:5,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:6,*V2LON,ONE,*HOFF,". Receipt Number Sequence":
                    *P1:7,*V2LON,TWO,*HOFF,". Cashier Sequence";
.
. ---- select option ----
.
          KEYIN     *P1:9,"Select Required Option : ",*V2LON,OPTION;
.
OPTN0100  BRANCH    OPTION,OPTN1000,OPTN2000
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN0000 IF NOT EQUAL
.
          MOVE      ONE,EXIT
          GOTO      OPTN9999
.
. ---- receipt range selected ----
.
OPTN1000  DISPLAY   *P50:2,*V2LON,"- Receipt Number Sequence "
.
. ---- input & validate start receipt number ----
.
         KEYIN     *P1:12,*EL,"Start Receipt Number : ",*V2LON,STRECNO;
.
         COMPARE   ZERO,STRECNO
         GOTO      OPTN0000 IF EQUAL
         GOTO      OPTN1000 IF LESS
.
. ---- input & validate end receipt number ----
.
         KEYIN     *P1:13,*EL,"End Receipt Number   : ",*V2LON,ENDRECNO;
.
         COMPARE   ZERO,ENDRECNO
         GOTO      OPTN1000 IF EQUAL
         GOTO      OPTN1000 IF LESS
         MOVE      ENDRECNO,ENDDIM12
.
         COMPARE   STRECNO,ENDRECNO
         GOTO      OPTN9000 IF NOT LESS
.
         DISPLAY   *P1:24,"End Receipt Number must be greater than ":
                          "Start Receipt Number ! ";
         CALL      HITENTER
         DISPLAY   *P1:24,*EL
         GOTO      OPTN1000
.
. ---- cashier sequence selected ----
.
OPTN2000  DISPLAY   *P50:2,*V2LON,"- Cashier Sequence "
.
. ---- input and validate start & end date ----
.
          CALL      DATE0000 
.
          CALL      CDRW0000                * Keyin Cash Drawer
          BRANCH    EXIT,OPTN0000
.
          GOTO      OPTN9000
.
. ---- ok to continue ----
.
OPTN9000  KEYIN     *P1:24,*EL,"Ok to Continue (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,"/":
                    *V2LON,"C",*HOFF,") ? ",*V2LON,ANS;
.
          MATCH     "Y",ANS
          GOTO      OPTN9999 IF EQUAL
.
          MATCH     "N",ANS
          GOTO      OPTN0100 IF EQUAL
.
          MATCH     "C",ANS
          GOTO      OPTN0000 IF EQUAL
.
          BEEP
          GOTO      OPTN9000
.
OPTN9999  RETURN
+
.-----------------------------------------------------------------------------
.  PROC0000 - process and print report
.-----------------------------------------------------------------------------
.
PROC0000  DISPLAY   *P1:15,*EF,*V2LON,"*** Processing Records ***",*HOFF:
                    *P1:17,"Receipt Number : ":
                    *P1:18,"Date           : ";
          BRANCH    OPTION,PROC0100,PROC0200
          GOTO      PROC9999
          
. ---- initialization for receipt option ----
.
PROC0100  PACK      KEY12,STRECNO,SP70
          CALL      RDRCBNK1
          BRANCH    OVRCD,PROC1000
          GOTO      PROC2000
.
. ---- initialization for date option ----
.
PROC0200  PACK      KEY26,FROMDATE,CASHDRAW,SP30
          CALL      RDRCBNK2 
          BRANCH    OVRCD,PROC1000
          GOTO      PROC2000
.
. ---- main processing loop ----
.
PROC1000  BRANCH    OPTION TO PROC1100,PROC1200
.  
. ---- get for receipt option ----
.
PROC1100  CALL      RKRCBNK1
          BRANCH    OVRCD,PROC9000
.
          MATCH     RCBNRECN,ENDDIM12
          GOTO      PROC9000 IF LESS
.
          GOTO      PROC2000
.
. ---- get for date option ----
.
PROC1200  CALL      RKRCBNK2
          BRANCH    OVRCD,PROC9000
.
          MATCH     RCBNTDAT,TODATE
          GOTO      PROC9000 IF LESS
.
          MATCH     SP70,CASHDRAW
          IF        !@EQUAL
            MATCH     RCBNCDRW,CASHDRAW
            GOTO      PROC9000 IF NOT EQUAL
          ENDIF
.
. ---- set up receipt bank header detail ----
.
PROC2000  ADD       RCBNTOTP,TOTAMT
.
          MOVE      STATUS0,SDESC            * get status type
          MOVE      ZERO,F1
          MOVE      RCBNSTAT,F1
          LOAD      SDESC,F1,STATUS1,STATUS2
.
          MATCH     RCBNRDAT,SP10
          IF        @EQUAL
            MOVE      "No",RECONCIL
          ELSE
            MOVE      "Yes",RECONCIL
          ENDIF
.
. ---- read of receipt bank details ----
.
          MOVE      ONE,PRNTFLAG
          PACK      KEY15,RCBNRECN,SP70
          CALL      RSRCBDT1
PROC2100  CALL      RKRCBDT1
          IF        OVRCD=1
            GOTO      PROC3000
          ENDIF
.
          MATCH     RCBNRECN,RCBDRECN
          GOTO      PROC3000 IF NOT EQUAL
.
          UNPACK    RCBNTDAT,RCC,RYY,RMM,RDD
          DISPLAY   *P20:17,*V2LON,RCBNRECN:
                    *P20:18,RDD,"/",RMM,"/",RCC,RYY;
. 
          GOTO      PROC6000
.
. ---- print receipt bank total ----
.
PROC3000  MOVE      TWO,PRNTFLAG
          PRINT     *73,"-------------":
                    *N,*71,RCBNTOTP:
                    *N,*73,"-------------",*N
          ADD       FOUR,LINCNT               * Increment line counter
.
. ---- read of receipt register details ----
.
          PACK      KEY17,RCBNRECN,SP70
          CALL      RSRCDTE1
PROC3100  CALL      RKRCDTE1
          IF        OVRCD=1
            GOTO      PROC3200
          ENDIF
.
          MATCH     RCBNRECN,RCDTRECN
          GOTO      PROC3200 IF NOT EQUAL
.
          UNPACK    RCBNTDAT,RCC,RYY,RMM,RDD
          DISPLAY   *P20:17,*V2LON,RCBNRECN:
                    *P20:18,RDD,"/",RMM,"/",RCC,RYY;
. 
          GOTO      PROC6000
.
. ---- print receipt register total ----
.
PROC3200  PRINT     *73,"-------------":
                    *N,*71,RCBNTOTP:
                    *N,*73,"-------------",*N
          ADD       FOUR,LINCNT               * Increment line counter
          MOVE      ZERO,PRNTFLAG
          GOTO      PROC1000
.
. ------- Print -------
.
PROC6000  COMPARE   "52",LINCNT
          CALL      HEAD0000 IF NOT LESS
.
          MATCH     RCBNRECN,OLDRECNO
          GOTO      PROC6200 IF EQUAL
.
          COMPARE   ZERO,FRSTFLG
          GOTO      PROC6100 IF EQUAL
          PRINT     *N;
          ADD       ONE,LINCNT               * Increment line counter
.
.      print receipt header details
.
PROC6100  MOVE      ONE,FRSTFLG
          PRINT     *1,RCBNRECN,*14,RDD,SLASH,RMM,SLASH,RYY,*23,RCBNCDRW:
                    *31,SDESC,*42,RECONCIL;
.
PROC6200  IF        PRNTFLAG=ONE
            CALL      PRTPAY00               * Print receipt payment details
          ELSE
            CALL      PRTREG00               * Print receipt register details
          ENDIF
.
          MOVE      RCBNRECN,OLDRECNO

          BRANCH    PRNTFLAG,PROC2100,PROC3100
          GOTO      PROC1000
.
PROC9000  PRINT     *N,*73,"-------------":
                    *N,*63,"Total",*73,TOTAMT:
                    *N,*73,"============="
.
PROC9999  RETURN
+
.-----------------------------------------------------------------------------
.  HEAD0000 - print heading             
.-----------------------------------------------------------------------------
.
HEAD0000  CLOCK     TIME,CTIMEIS
          ADD       ONE,PAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      "       0",OLDRECNO
          MOVE      ZERO,FRSTFLG
.
          CALL      HEAD132
.
          BRANCH    OPTION,HEAD1000,HEAD2000
.
HEAD1000  PRINT     *N,*50,"From : ",STRECNO," To : ",ENDRECNO:
                    *N,*50,"Receipt Sequence";
          GOTO      HEAD9000
.
HEAD2000  PACK      DIM10,FRDAY,SLASH,FRMON,SLASH,FRCEN,FRYER
          REP       " 0",DIM10
.
          PACK      DIM10A,TODAY,SLASH,TOMON,SLASH,TOCEN,TOYER
          REP       " 0",DIM10A
.
          MATCH     SP70,CASHDRAW
          IF        @EQUAL
            PRINT     *N,*50,"From Date : ",DIM10:
                      *N,*50,"To Date   : ",DIM10A:
                      *N,*50,"Cashier Sequence";
          ELSE
            PRINT     *N,*50,"From Date : ",DIM10:
                      *N,*50,"To Date   : ",DIM10A:
                      *N,*50,"Cash Drawer : ",DRAWERNM;
          ENDIF
.
HEAD9000  PRINT    *N,*N,*23," Cash ",*47,"Payment/":
                   *N,"     Receipt ",*14,"Date",*23,"Drawer":
                   *31,"Status",*42,"Recn",*47,"Register",*56,"System":
                   *71,"         Amount",*87,"Reference":
                   *N,"------------",*14,"--------",*23,"-------":
                   *31,"----------",*42,"----",*47,"--------":
                   *56,"--------------",*71,"---------------":
                   *87,"----------------------------------------------"
.
          MOVE      TEN,LINCNT
.
HEAD9999  RETURN
+
.-----------------------------------------------------------------------------
.  OTRF0000  :  Open all required visit based files 
.-----------------------------------------------------------------------------
.
OTRF0000  IF        FILENUM = 51
            CLOSE     PATINVR1
            OPEN      PATINVR1,PATH
          ENDIF
.
OTRF9999  RETURN
+
.-----------------------------------------------------------------------------
.  GETREG00  :  Get system code & desc                     
.-----------------------------------------------------------------------------
.
GETREG00  PACK      KEY3,RCDTREGI
          CALL      RDREGA1
          IF        OVRCD=1
            MOVE      ZERO,REGIBACD
          ENDIF
.
          MOVE      SYST0,SYDESC
          LOAD      SYDESC,REGIBACD,SYST1,SYST2,SYST0,SYST0,SYST5
.
          IF        REGIBACD>=90
            MOVE      ZERO,F2
            MOVE      REGIBACD,F2
            SUB       "90",F2
            MOVE      SYST90,SYDESC
            LOAD      SYDESC,F2,SYST91,SYST92,SYST0,SYST94,SYST95:
                                SYST96,SYST97,SYST98,SYST99
          ENDIF
          IF        REGIBACD=89
            PACK      SYDESC,SYST89,SP70
          ENDIF
.
GETREG99  RETURN
+
.-----------------------------------------------------------------------------
.  PRTPAY00  :  Print payment details - rcpbdtaf 
.-----------------------------------------------------------------------------
.
PRTPAY00  MOVE      SP70,PDESC               * get Payment type
          MOVE      ZERO,F1
          MOVE      RCBDPTYP,F1
          MOVE      F1,F2
.
          MOVE      "Py",KEY2
          PACK      KEY5,KEY2,RCBDPCOD
          CALL      RDCODE1
          IF        OVRCD=0
            SQUEEZE   PTCDNHCP
            MOVE      PTCDNHCP,F2
            PACK      PDESC,TDESC,SP70
          ENDIF
.
          PRINT     *47,PDESC,*71,RCBDPAMT;
          ADD       ONE,LINCNT               * Increment line counter
.
          BRANCH    F2,PRTPAY10,PRTPAY20,PRTPAY30,PRTPAY40,PRTPAY50:
                       PRTPAY60,PRTPAY40,PRTPAY40,PRTPAY10
          COMPARE   ZERO,F2
          GOTO      PRTPAY99 IF EQUAL
.
          PRINT     *87,"Payer:",RCBDPAYD:
                    *N,*87,"Ref:",RCBDSTRF
          ADD       ONE,LINCNT               * Increment line counter
          GOTO      PRTPAY99
.
PRTPAY10  PRINT     *87,"Payer:",RCBDPAYD
          GOTO      PRTPAY99
.
PRTPAY20  PRINT     *87,"Drawer:",RCBDDRAW,*115,"Cheque No:",RCBDCHQN:
                    *N,*87,"Bank:",RCBDBANK:
                    *N,*87,"Branch:",RCBDBRCH
          ADD       TWO,LINCNT               * Increment line counter
          GOTO      PRTPAY99
.
PRTPAY30  PACK      KEY5,CODECR,RCBDCRDT
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      RCBDCRDT,TDESC
          ENDIF
          PRINT     *87,"Payer:",RCBDPAYD:
                    *N,*87,"Card Type:",TDESC
          GOTO      PRTPAY99
.
PRTPAY40  PRINT     *87,"Payer:",RCBDPAYD:
                    *N,*87,"Statement Ref:",RCBDSTRF
          ADD       ONE,LINCNT               * Increment line counter
          GOTO      PRTPAY99
.
PRTPAY50  PRINT     *87,"Money Order:",RCBDCHQN
          GOTO      PRTPAY99
.
PRTPAY60  MATCH     "1",RCBDEFTT
          IF        @EQUAL
            PRINT     *87,"Payer:",RCBDPAYD," EFT Type:Cash":
                      *N,*87,"Reference:",RCBDSTRF
            ADD       ONE,LINCNT               * Increment line counter
          ELSE
            PACK      KEY5,CODECR,RCBDCRDT
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      RCBDCRDT,TDESC
            ENDIF
            PRINT     *87,"Payer:",RCBDPAYD," EFT Type:Credit":
                      *N,*87,"Card Type:",TDESC:
                      *N,*87,"Reference:",RCBDSTRF
            ADD       TWO,LINCNT               * Increment line counter
          ENDIF
          GOTO      PRTPAY99
.
PRTPAY99  RETURN
+
.-----------------------------------------------------------------------------
.  PRTREG00  :  Print register details - rcpdteaf
.-----------------------------------------------------------------------------
.
PRTREG00  CALL      GETREG00                 * Get System Code
.
          PRINT     *47,RCDTREGI,*56,SYDESC,*71,RCDTAMNT:
                    *87,"Reference:",RCDTREFR;
          ADD       ONE,LINCNT               * Increment line counter
.
          CALL      GETPNM00                 * Get Patient or Debtor Name
.
          COMPARE   ONE,REGIBACD             * Patient Billing
          GOTO      PRTREG61 IF EQUAL
.
          IF        REGIBACD=2 
            MATCH     "1",RCDTSFLG
            GOTO      PRTREG62 IF EQUAL      * Private Practice
            GOTO      PRTREG61
          ENDIF
.
          COMPARE   FIVE,REGIBACD            * Account Receivable 
          GOTO      PRTREG61 IF EQUAL
          COMPARE   EIGHTY9,REGIBACD         * Credit Card Surcharge
          GOTO      PRTREG65 IF EQUAL
          COMPARE   NINTY,REGIBACD           * Emergency Deposit
          GOTO      PRTREG63 IF EQUAL
          COMPARE   NINTY1,REGIBACD          * Outpatient Deposit
          GOTO      PRTREG63 IF EQUAL
          COMPARE   NINTY6,REGIBACD          * Private Practice Deposit
          GOTO      PRTREG64 IF EQUAL
          COMPARE   NINTY7,REGIBACD          * Patient Deposit
          GOTO      PRTREG63 IF EQUAL
          COMPARE   NINTY2,REGIBACD          * GST Sundry Cash Sales
          GOTO      PRTREG65 IF EQUAL
          COMPARE   NINTY4,REGIBACD          * Sundry Debtor Rec.
          GOTO      PRTREG65 IF EQUAL
          COMPARE   NINTY5,REGIBACD          * Sundry Cash Sales
          GOTO      PRTREG65 IF EQUAL
          COMPARE   NINTY8,REGIBACD          * Internal Receipts    
          GOTO      PRTREG65 IF EQUAL
          COMPARE   NINTY9,REGIBACD          * Suspense
          GOTO      PRTREG65 IF EQUAL
.
PRTREG61  PRINT     *N,*87,"Patient Name:",DISPNAME:
                    *N,*87,"Invoice:",RCDTINVN," Debtor:",RCDTURNO
          ADD       TWO,LINCNT               * Increment line counter
          GOTO    PRTREG99
.
PRTREG62  PRINT     *N,*87,"Patient Name:",DISPNAME:
                    *N,*87,"Invoice:",RCDTINVN," U/R:",RCDTURNO
          ADD       TWO,LINCNT               * Increment line counter
          GOTO    PRTREG99
.
PRTREG63  PRINT     *N,*87,"Patient Name:",DISPNAME:
                    *N,*87,"Visit:",RCDTVIST," U/R:",RCDTURNO
          ADD       TWO,LINCNT               * Increment line counter
          GOTO    PRTREG99
.
PRTREG64  PRINT     *N,*87,"Patient Name:",DISPNAME:
                    *N,*87,"U/R:",RCDTURNO
          ADD       TWO,LINCNT               * Increment line counter
          GOTO    PRTREG99
.
PRTREG65  PRINT     *N,*87,"Name:",RCDTNAME
          ADD       ONE,LINCNT               * Increment line counter
          GOTO    PRTREG99
.
PRTREG99  RETURN
+
.-----------------------------------------------------------------------------
.  GETPNM00  :  Get Patient or Debtor Name                  
.-----------------------------------------------------------------------------
GETPNM00  MOVE      SP70,DISPNAME
          COMPARE   ONE,REGIBACD
          GOTO      GETPNM10 IF EQUAL
          COMPARE   TWO,REGIBACD
          GOTO      GETPNM20 IF EQUAL
          COMPARE   FIVE,REGIBACD
          GOTO      GETPNM30 IF EQUAL
          COMPARE   NINTY,REGIBACD
          GOTO      GETPNM10 IF EQUAL
          COMPARE   NINTY1,REGIBACD
          GOTO      GETPNM10 IF EQUAL
          COMPARE   NINTY6,REGIBACD
          GOTO      GETPNM20 IF EQUAL
          COMPARE   NINTY7,REGIBACD
          GOTO      GETPNM10 IF EQUAL
          GOTO      GETPNM99
.
. ------- Patient -------
.
GETPNM10  MOVE      RCDTURNO,PURNO
          MOVE      PURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,GETPNM99

          CALL      PATNAA00
          GOTO      GETPNM99
.
. ------- Private practice Debtors -------
.
GETPNM20  MATCH     "1",RCDTSFLG
          GOTO      GETPNM10 IF EQUAL
.
...       MOVE      RCDTURNO,KEY8
...       CALL      RDPRDB1
...       BRANCH    OVRCD,GETPNM99
.
          MOVE      SP70,PTITL
...       MOVE      PRDBGNAM,PGNAME
...       MOVE      PRDBSNAM,PSNAME
          MOVE      "Unknown Debtor",PGNAME
          MOVE      SP70,PSNAME
          CALL      PATNAA00
          GOTO      GETPNM99
.
.
. ------- Acc Rec Debtors -------
.
GETPNM30  MOVE      RCDTURNO,KEY8
          CALL      RDDBDB1
          BRANCH    OVRCD,GETPNM99
.
          MOVE      SP70,PTITL
          MOVE      DBDBNA1,PGNAME
          MOVE      DBDBNA2,PSNAME
          CALL      PATNAA00
          GOTO      GETPNM99
.
GETPNM99  RETURN
+
.------------------------------------------------------------
. Format Patient Name in Title Text
.------------------------------------------------------------
.
PATNAA00  CLEAR     DISPNAME
          MOVE      SP70,PATFNAME
          PACK      NAME35,PTITL,SP70
          CALL      NAMSTR00
          PACK      NAME35,PGNAME,SP70
          CALL      NAMSTR00
          PACK      NAME35,PSNAME,SP70
          REP       UPPLOW,PSNAME
          APPEND    PSNAME,DISPNAME
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,PATFNAME
          STRIP     PATFNAME
          RETURN
.
.****************************************************************************
.* Key in cash drawer                                                       *
.****************************************************************************
CDRW0000  KEYIN     *P1:15,*EF,*P1:15,"Cash Drawer From: ",*V2LON,CASHDRAW
.
          PACK      CASHDRAW,CASHDRAW,SP70
          MOVE      SP70,DRAWERNM
.
          MATCH     SP70,CASHDRAW
          GOTO      CDRW9000 IF EQUAL
.
          PACK      KEY6,CASHDRAW,SP70
          CALL      RDCIDA1
          BRANCH    OVRCD,CDRW1000
.
          SQUEEZE   RCPSUR
          SQUEEZE   RCPGIVEN
          PACK      DRAWERNM,RCPSUR,SP1,RCPGIVEN,SP70
          RESET     RCPSUR
          RESET     RCPGIVEN
.
          GOTO      CDRW9000
.
CDRW1000  DISPLAY   *P1:24,"Invalid Cash Drawer";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      CDRW9999
.
CDRW9000  MOVE      ZERO,EXIT
.
CDRW9999  RETURN
.
.*****************************************************************************
.*                      Get user keyin Date                                  *
.*                                                                           *
.*****************************************************************************
.
DATE0000  DISPLAY   *P1:12,*EF:
                    *P1:12,"From  Date  :":
                    *P1:13,"To    Date  :"
.
          UNPACK    SP8,CCENT,CYEAR,CMON,CDAY
.
DATE1000  MOVE      TEN5,CCOL
          MOVE      TEN2,CVERT
.
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
          MOVE      CCC,CCENT
.
DATE2000  CALL      KEYDATE
          BRANCH    OVRCD,DATE9100
.
          MOVE      CDAY,FRDAY
          MOVE      CMON,FRMON
          MOVE      CYEAR,FRYER
          MOVE      CCENT,FRCEN
.
          PACK      FROMDATE,FRCEN,FRYER,FRMON,FRDAY
          REP       " 0",FROMDATE
.
DATE3000  UNPACK    SP8,CCENT,CYEAR,CMON,CDAY
.
          MOVE      TEN5,CCOL
          MOVE      TEN3,CVERT
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
.
          MOVE      CCC,CCENT
.
DATE4000  CALL      KEYDATE
          BRANCH    OVRCD,DATE1000
.
          MOVE      CDAY,TODAY
          MOVE      CMON,TOMON
          MOVE      CYEAR,TOYER
          MOVE      CCENT,TOCEN
.
          PACK      TODATE,TOCEN,TOYER,TOMON,TODAY
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      DATE9000 IF LESS
.
          MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9000  DISPLAY   *P43:24,*B,*V2LON:
                    "** FROM Date Greater Than TO Date **",*W2,*P1:24,*EL;
          GOTO      DATE0000
.
DATE9100  MOVE      ONE,EXIT
.
DATE9999  RETURN
.
.-----------------------------------------------------------------------------
.
          INC       DEBDBTIO/INC
.
          INC       RCPBNKIO/INC
          INC       RCPBDTIO/INC
          INC       RCPDTEIO/INC
          INC       RCPREGIO/INC
          INC       RCPCIDIO/INC
          INC       PATCODIO/INC
          INC       PATINVIO/INC
          INC       PATMA1IO/INC
          INC       PRIINVIO/INC
          INC       PATHOSIO/INC
          INC       PATDPATH
          INC       NAMSTRCD
          INC       STD001IO
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC


.******************************************************************************
.* System         : Patient                                                   *
.* Program        : IBAINP87.PBL                                              *
.* Name           : QLD Cancer Register Extract                               *
.******************************************************************************
.* Date           : 07/07/2004                                                *
.* Author         : Lina Vo                                                   *
.* Function       : This program extracts data in the format required for     *
.*                  the Queensland Cancer Register (Used by Private Hospitals)*
.*                                                                            *
.* Modifications  :                                                           *
.*                                                                            *
.******************************************************************************
.* V12.00.03 14/07/2025  Thanh T         TSK 0961623                          *
.*           Added OPEN IBAPOST2 index to fix issue with Read attempt on      *
.*           closed file                                                      * 
.* V12.00.02 02/06/2025  Don Nguyen      TSK 0955096                          *
.*           Alphanumeric visit number changes                                *
.* V12.00.01 30/05/2025  Don Nguyen      TSK 0955096                          *
.*           Alphanumeric visit number changes                                *
.******************************************************************************
.* V11.04.01 21/11/2023  Thanh T        TSK 0939233                           *
.*           Initialize PTCDFCPR field with 280 spaces intead of 90 spaces    *
.******************************************************************************
.* V10.14.01 05/03/2019  Nicole Torrisi                            TSK 0870326*
.*           Changed XTTLTDOC to limit DTITL to 4 characters (GCAD1500)       *
.******************************************************************************
.* V10.08.01 12/04/2016  Steve Armstrong                           TSK 0813958*
.*           Added missing PATCRDFD variables to CLRCRD00                     *
.******************************************************************************
.* V10.05.01 31/07/2014  Davin                                     CAR 304256 *
.*           Added hospital id keyin if multihospital=yes                     *
.*           Use pathspaf.pthshsid instead of cfileno if multihospital=yes    *
.******************************************************************************
.* V10.04.01 15/04/2014  Sandra Barcham                            CAR 299097 *
.*           Fix Indigenous status                                            *
.******************************************************************************
.* V10.03.02  28/12/2012 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
.* V10.03.01 18/11/2011  Mike Laming     CAR 240184                           *
.*           Changes to IBAPOSTF Post Code table - added State to Keys        *
.******************************************************************************
.* V10.02.03 13/07/2011  Steve Armstrong   CAR 240722                         *
.*           Further mods to cater for second given name as part of PATGSRFD  *
.*           keys                                                             *
.* V10.02.02 22/06/2011  Steve Armstrong   CAR 240722                         *
.*           Mods to cater for changes to PATGSRFD:                           *
.*                - GSRSNAM & GSRGNAM extended to 30 chars.                   *
.*                - PTGSGNM2 added.                                           *
.*                - all indexes changed in length.                            *
.* V10.02.01 28/03/2011  Mike Laming   CAR 240107                             *
.*           Mods to PATECDaf & PATECOaf variables & Keys - file change       *
.******************************************************************************
.* V10.00.01 Mike Laming   CAR 208570                                         *
.*           Recompiled for changes to PATCRDFD.                              *
.******************************************************************************
.* V9.09.01  14/11/2007  Mike Laming   CAR 154835                             *
.*           Mods as specified plus port 6.15 changes to 9.08/9.09            *
.******************************************************************************
.* V9.08.03  30/05/2007  Mike Laming   CAR 125991                             *
.*           Re-compiled for PATCRDFD/IO - New fields in file                 *
.* V9.08.02  25/05/2007  Mike Laming   CAR 125991                             *
.*           Re-compiled for PATCRDFD/IO - Record increased                   *
.* V9.08.01  31/01/2007  Peter Vela    CAR 127930                             *
.*           Recompled for MRTCONFD                                           *
.******************************************************************************
.* V9.07.01  06/09/2006  J.Tan            CAR 116050                          *
.*           Fixed extracting address and diagnosis at separation to Prim Dis.*
.******************************************************************************
.* V9.04.01  29/04/2005  Peter Vela       CAR 55214                           *
.*           Recompiled for MRTCONFD                                          *
.******************************************************************************
.* V9.03.00  07/07/2004  Lina Vo CAR 51071                                    *
.*           Created new                                                      *
.******************************************************************************
.
.         FD's required
.
          INC       STD001FD/PBL
          INC       IBAPOSFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       MRTCONFD/INC
          INC       PATCALAG/INC            * for calcage
          INC       PATCANFD/INC            * patient codes file
          INC       PATCODFD/INC            * patient codes file
          INC       PATCOMM/INC             * variables for surname search
          INC       PATCONFD/INC            * control file
          INC       PATCRDFD/INC            * Cancer Register Detail File
          INC       PATCRGFD/INC            * Cancer Register Header File 
          INC       PMSCDGFD/INC            * Cancer Reason for Clinical Diagn.
          INC       PATDADFD/INC            * Transfer file
          INC       PATDO1FD/INC            * Patient Doctors File
          INC       PATDSCFD/INC            * Discharge File
          INC       PATECDFD/INC            * Patient episode disease file
          INC       PATGSRFD/INC            * Patient name file           
          INC       PATHSPFD/INC            * Hospital file
          INC       PATICDFD/INC            * ICD-9 Codes
          INC       PATMA1FD/INC            * patient master file
          INC       PATMI1FD/INC            * admission file
          INC       PATVISFD/INC            * Visit File
          INC       PMIVARS/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
Z15       INIT      "zzzzzzzzzzzzzzz"
.
. ------- program variables -------
.
CATC      INIT      "C "
CATD      INIT      "D "
CATDD     INIT      "DD"
CATM      INIT      "M "
CATVA     INIT      "VA"
CATLY     INIT      "LY"
CODED     INIT      "Coding"
DISCHD    INIT      "Discharge"
DQTE      INIT      "#""
CODENM    INIT      "NM"
.
HDRFILE   INIT      "HDR"
CADFILE   INIT      "CAD"
CANFILE   INIT      "CAN"
CDXFILE   INIT      "CDX"
FANFILE   INIT      "FAN"
QCREXTN   INIT      ".QCR"
.
JAN       INIT      "JANUARY"
FEB       INIT      "FEBRUARY"
MAR       INIT      "MARCH"
APR       INIT      "APRIL"
MAY       INIT      "MAY"
JUN       INIT      "JUNE"
JUL       INIT      "JULY"
AUG       INIT      "AUGUST"
SEP       INIT      "SEPTEMBER"
OCT       INIT      "OCTOBER"
NOV       INIT      "NOVEMBER"
DEC       INIT      "DECEMBER"
.
ADD       DIM       2
ADMDATE   DIM       10
AMM       DIM       2
AYY       DIM       2
ACC       DIM       2
BASDIAGN  DIM       2
BJDAY     FORM      4
CONAME    DIM       35
CHG       FORM      1
CJDAY     FORM      4
CMDLINE   DIM       80
CODE      DIM       2
.
DIAGCODE  FORM      2
DIM3      DIM       3
DIM4      DIM       4
DIM5      DIM       5
DIM9      DIM       9 
DIM20     DIM       20
DIM45     DIM       45
DIM70     DIM       70
DOB       DIM       10
ENDDATE   DIM       8 
FNAME     DIM       8
FORM3     FORM      3
FORM12    FORM      12
FROMDATE  DIM       10
HEADHOSP  DIM       3
IBCNMHOS  FORM      1
KEY30A    DIM       30
KEY26A    DIM       30
LINENO    FORM      2
LINE460   DIM       460
MODE      FORM      1
MODE1     DIM       1
MONTH3    DIM       3
MULTCCNT  FORM      2
NAMEHOSP  DIM       50
REPET     FORM      2
SAVURNO   DIM       8
SECNDFLG  FORM      1
STRTDATE  DIM       8 
STRTFLAG  FORM      1
TODATE    DIM       10
THEDATE   DIM       10
.
PATHDESC  DIM       50
DIAGDESC  DIM       50
.
CADCOUNT  FORM      5        Number of CAD Records
CANCOUNT  FORM      5        Number of CAN Records
FANCOUNT  FORM      5        Number of FAN Records
CDXCOUNT  FORM      5        Number of CDX Records
GSRCOUNT  FORM      2                               
.
.  Header Details (HDR) File
.
EXTHDRFL  FILE      ASCII, FIXED=50     * Extract File 
.
. Field   Type      Size     Description
. -----   ----      ----     ----------------
XFACLCOD  DIM       5        Facility Code
XNOOFCAD  DIM       5        Number of CAD Records
XNOOFCAN  DIM       5        Number of CAN Records
XNOOFFAN  DIM       5        Number of FAN Records
XNOOFCDX  DIM       5        Number of CDX Records
. 
.
.  Cancer Admission Details (CAD) File
.
EXTCADFL  FILE      ASCII, FIXED=460     * Extract File 
.
. Field   Type      Size     Description
. -----   ----      ----     ----------------
XURNUMBR  DIM        8       U/R Number
XADMISNO  DIM       12       Admission Number
XPRSITCT  DIM        2       Multiple Primary Site Count
XMEDICAR  DIM       11       Medicare Number
XPSNAME   DIM       26       Patient Surname  (incl. quotation marks)
XPGFNAME  DIM       17       Patient First Name  (incl. quotation marks)
XPGSNAME  DIM       17       Patient Second Name  (incl. quotation marks)
XADDRESS  DIM       52       Address of Usual Residence (incl. quotation marks)
XLOCATN   DIM       42       Suburb, Town or Locality of Usual Residence
.                            (incl. quotation marks)
XPOSTCOD  DIM        4       Australian Postcode                        
.                            Suppl. Codes:0989=not stated/unknown
.                                         9301=Papua New Guinea  
.                                         9302=New Zealand       
.                                         9399=O/S - other(not PNG or NZ)
.                                         9799=At sea            
.                                         9899=Aust External Territories
.                                         9989=No fixed address
XDOB      DIM        9       Date of Birth DDMMMCCYY                    
XOCCUPTN  DIM       52       Occupation Description (incl. quotation marks)                    
XSEX      DIM        1       Sex                                       
.                            M=Male, F=Female, I=Indeterminate
XCOUNTRY  DIM        4       Country of Birth Code (ASCCSS)                    
XMSTATUS  DIM        2       Marital Status                            
.                            NM=Single/never married
.                            M=Married
.                            F=Defacto
.                            W=Widowed
.                            A=Separated
.                            N=Not stated/unknown
XINDIGST  DIM        2       Indigenous Status                            
.                            11=Indig-Aboriginal but not Torres Strait Islander
.                            12=Indig-Torres Strait Islander but not Aboriginal
.                            13=Indig-Aboriginal and Torres Strait Islander
.                            14=Not Indig-Not Aboriginal or Torres Strait Island
.                            19=Not Indig-Not Stated 
XADMSDTE  DIM        9       Admission Date DDMMMCCYY                     
XSEPTDTE  DIM        9       Separation Date DDMMMCCYY                     
XMODESEP  DIM        2       Mode of Separation                      
.                            01=Home/usual residence
.                            02=Other hospital-not contract
.                            03=Nursing Home
.                            04=Other Health care establishment
.                            05=Died in Hospital    
.                            06=Episode change        
.                            07=Discharge at own risk 
.                            09=Non return from leave 
.                            10=Contract to other hospital
.                            11=Return to other hospital following contract 
.                               at this Hospital
.                            12=Correctional facility    
.                            16=Transferred (Trans.to Facility is mandatory)
.                            19=Other    
.                            99=Unknown    
XTRFFACL  DIM        5       Transferring to Facility
XAUTOPSY  DIM        1       Autopsy Flag Y/N
XCSDEATH  DIM       52       Cause of Death (incl. quotation marks)
XTTLTDOC  DIM        6       Treating Doctor Title (incl. quotation marks)
XINTTDOC  DIM       11       Treating Doctor Initials (incl. quotation marks)
XGNMTDOC  DIM       57       Treating Doctor Give Names (incl. quotation marks)
XSNMTDOC  DIM       31       Treating Doctor Surname (incl. quotation marks)
XDIAGSEP  DIM        9       Diagnosis at Separation

.
.  Cancer Details (CAN) File
.
EXTCANFL  FILE      ASCII, FIXED=400     * Extract File 
.
. Field   Type      Size     Description
. -----   ----      ----     ----------------
.XURNUMBR  DIM        8       U/R Number
.XADMISNO  DIM       12       Admission Number
XPRSITNM  DIM        2       Multiple Primary Site Number
XPRSITCD  DIM        9       Primary Site of Cancer Code 
XPRSITDS  DIM       42       Primary Site of Cancer Description
.                            (incl. quotation marks)
XMORPHCD  DIM        9       Morphology Code                     
XFDIAGDT  DIM        9       Date of First Diagnosis             
XFDIAGFL  DIM        9       Date of First Diagnosis Flag            
XLOC1DGN  DIM       42       Suburb, Town or Locality of Usual Residence at
.                            time of first diagnosis of this cancer.
.                            (incl. quotation marks)
XPOS1DGN  DIM        4       Australian Postcode at 
.                            time of first diagnosis of this cancer
.                            Suppl. Codes:0989=not stated/unknown
.                                         9301=Papua New Guinea  
.                                         9302=New Zealand       
.                                         9399=O/S - other(not PNG or NZ)
.                                         9799=At sea            
.                                         9899=Aust External Territories
XLATRCNC  DIM        1       Laterality of Cancer                    
.                            R=Right
.                            L=Left
.                            B=Bilateral
.                            N=Not applicable
.                            U=Unknown
XBASDIAG  DIM        2       Basis of Diagnosis                      
.                            01=Unknown
.                            02=Clinical Only           --> requires CDX record
.                            03=Clinical Investigations --> requires CDX record
.                            04=Exploratory Surgery
.                            05=Specific Biochemical and immunological testing
.                            06=Cytology
.                            07=Histology of Metastasis
.                            08=Histology of Primary Site
.                            09=Autopsy and Histology 
XCOMMENT  DIM       52       Comments (incl. quotation marks) 
XLABFANO  DIM       2        Laboratory Facility No.                  *I-154835
XLABSPNO  DIM       52       Laboratory Specimen No.                  *I-154835
.
.  Reason for Clinical Diagnosis (CDX) File
.
EXTCDXFL  FILE      ASCII, FIXED=100     * Extract File 
.
. Field   Type      Size     Description
. -----   ----      ----     ----------------
.XURNUMBR  DIM        8       U/R Number
.XADMISNO  DIM       12       Admission Number
.XPRSITNM  DIM        2       Multiple Primary Site Number
XRSDIAGC  DIM        2        Reasons for clinical diagnosis code
.                             01=Pallative care admission
.                             02=Doctors notes/Referal 
.                             03=Pathology 
.                             04=Radiological investigations
.                             05=Other non invasive investigations
.                             06=Invasive investigation
.                             07=Non cancer admission 
.                             09=Other                 
XRSDIAGD  DIM        50       Reasons for clinical diagnosis text
.                             (incl. quotation marks)
.
.  Former/Alias Names (FAN) File
.
EXTFANFL  FILE      ASCII, FIXED=100     * Extract File 
.
. Field   Type      Size     Description
. -----   ----      ----     ----------------
.XURNUMBR  DIM        8       U/R Number
.XADMISNO  DIM       12       Admission Number
XFANNUMB  DIM        2       Former/Alias Name Indentifer
.XPSNAME   DIM       26       Patient Surname (incl. quotation marks)
.XPGFNAME  DIM       17       Patient First Name (incl. quotation marks)
.XPGSNAME  DIM       17       Patient Second Name (incl. quotation marks)
.
.
.
EXTHDRTM  DIM       8                   * Temp Extract File Name
EXTCADTM  DIM       8                   * Temp Extract File Name
EXTCANTM  DIM       8                   * Temp Extract File Name
EXTCDXTM  DIM       8                   * Temp Extract File Name
EXTFANTM  DIM       8                   * Temp Extract File Name
.
EXTHDRFN  DIM       40                   * Extract File Name
EXTCADFN  DIM       40                   * Extract File Name
EXTCANFN  DIM       40                   * Extract File Name
EXTCDXFN  DIM       40                   * Extract File Name
EXTFANFN  DIM       40                   * Extract File Name
.
.TABDELM   EQU       0x09                 * Tab Delimiter
.
TEMPFILE  IFILE SQL, FIXED=38, KEY="U1-8"
.
. NAME    TYPE   LENGTH   PHYSICAL  START LOC    DESCRIPTION
. ----    ----   ------   --------  ---------    --------------
TEMPADMN  DIM       8       8          1
TEMPSPAR  DIM       30      30         8
.EOR
.
. ------- program constants -------
PRGID     INIT      "IBAINP87"
PRGNAM    INIT      "QLD Cancer Register Extract"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.         mainline code
.*********************************************************************
ML0000    CALL      INIT0000                * initilisation
.
ML0500    CALL      OPTN0000                * Get Report Option 
          BRANCH    OPTION,ML1000,ML2000
.
          GOTO      ML9999
.
ML1000    CALL      DTRNG000                * get date range
          BRANCH    EXIT,ML0500
          CALL      CREG0000                * Registered Details
.
          IF        PTCNHSEM <> 1
            CALL      NCOD0000              * Use coding date
          ELSE
            CALL      NDIS0000              * Using Discharge Date
          ENDIF
          BRANCH    EXIT,ML0500             * =1 if no extract
.
          CALL      MVEXT000                * Move Extract for Web
          GOTO      ML0500
.
ML2000    CALL      DUMDTE00                * Set Dates for file names
          BRANCH    EXIT,ML0500
          CALL      CREG0000                * Create extract files
          CALL      NUNS0000                * extract un-sent registrations
          BRANCH    EXIT,ML0500             * =1 if no extract
.
          CALL      MVEXT000                * Move Extract to rename
          GOTO      ML0500
.
ML9999    CHAIN     PGM
.
+
.*********************************************************************
.         initilisation
.*********************************************************************
.
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening "
.
          DISPLAY   *P58:24,"ibapostf"
          OPEN      IBAPOST1,"ibapostf"
          OPEN      IBAPOST2,"ibapostf"
.
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P58:24,"patcrdaf"
          OPEN      PATCRDA1,"patcrdaf"
.
          DISPLAY   *P58:24,"patcrgaf"
          OPEN      PATCRGA1,"patcrgaf"
.
          DISPLAY   *P58:24,"pmscdgaf"
          OPEN      PMSCDGA1,"pmscdgaf"
.
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"     * files to open
.
          DISPLAY   *P58:24,"patecdaf"
          OPEN      PATECDA1,"patecdaf"
          OPEN      PATECDA2,"patecdaf"
.
          DISPLAY   *P58:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P58:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P58:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P58:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PATMI1A4,"patmi1af"
.
          DISPLAY   *P58:24,"patdocaf"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P58:24,"patcanaf"
          OPEN      PATCANA1,"patcanaf"
.
          DISPLAY   *P58:24,"patdadaf"
          OPEN      PATDADA1,"patdadaf"
.
          DISPLAY   *P58:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P58:24,"paticddf"
          CALL      OPICD1
.
          DISPLAY   *P58:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P58:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TWO;*4,CONAME
          READ      CONTROLF,EIGHTY8;*242,PNSWRACE
          READ      CONTROLF,EIGHTY8;*59,PTCNI10D
          READ      CONTROLF,NINTY7;*151,MRCNTDAD

          READ      CONTROLF,TEN;*54,CFILENO,*235,CMDISP
          READ      CONTROLF,TEN9;*75,CCNOTES
          READ      CONTROLF,TWENTY;*91,PTCNHSEM
          READ      CONTROLF,FIFTY9;*132,CALRDESC
          READ      CONTROLF,TWENTY1;*216,PTCNDTRF
.
          DISPLAY   *P58:24,"patgsrnf"
          OPEN      PATGSRN1,"patgsrnf"     * surname & given name
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
.         Set Printer Stuff 
.
          CLOCK     TIME,CTIMEIS
          MOVE      CODED,DIM4
          LOAD      DIM4,PTCNHSEM,DISCHD
          MOVE      CODED,DIM9
          LOAD      DIM9,PTCNHSEM,DISCHD
          STRIP     DIM9
          MOVE      ONE,CNOUNDLN
.
INIT9999  RETURN
+
.
.*********************************************************************
.         OPTN0000 Get Main Program Option   Called By ML0000
.                  Display and get U/R or Adno option.
.                  Returns : OPTION  1 = Register Details
.*********************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = Cancer Extract" :
                    *P1:6,*V2LON," 2",*HOFF," = Cancer Extract for unregist":
                    "ered cancers"
OPTN1000  KEYIN     *P1:8," Select Option : ",*V2LON,*P18:8,OPTION;
.
          BRANCH    OPTION,OPTN9999,OPTN9999
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL
.
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
.
+
.*********************************************************************
.         KHOS000 Get Main Program Option   Called By ML0000
.*********************************************************************
KHOS000 COMPARE    ONE,IBCNMHOS
        GOTO       KHOS999 IF NOT EQUAL
.
        DISPLAY    *P1:15,*EL,"Hospital Id : ";
        MOVE       TEN5,ECOL
        MOVE       "15",EVERT
        MOVE       SP3,CKYIDEF3
        MOVE       ZERO,CKYIMAND           * Not Mandatory
        OPEN       PATHSPA1,"pathspaf"
.
        CALL       PATHSPKY
        BRANCH     EXIT,KHOS100
        GOTO       KHOS900
.
KHOS100 DISPLAY    *P18:15,"All Hospitals";
        MOVE       SP10,PTHSHOSP
        MOVE       "All Hospitals",PTHSNAME
.
KHOS900 MOVE       PTHSHOSP,HEADHOSP
        MOVE       PTHSNAME,NAMEHOSP
KHOS999 RETURN
+
.**************************************************************************
. Routine to call the apopriate loop routine to get the Cancer redister 
. Details.
.**************************************************************************
.
CREG0000  MOVE      ZERO,EXIT
          CALL      CREXTR00                * create & open extract files
.
          MOVE      ZERO,CLNO  
          MOVE      ZERO,CPAGENO
          CALL      HEDC0000                * Print a header
          MOVE      SP10,SAVURNO
.
CREG9999  RETURN
+
.**************************************************************************
. Routine to get the from and to dates.
.
.**************************************************************************
.
DTRNG000  MOVE      ZERO,EXIT
          DISPLAY   *P1:4,*EF,*P2:4,"From Date : ":
                    *P2:6,"To Date : ";
.
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
DTRNG100  MOVE      TEN4,CCOL
          MOVE      FOUR,CVERT
          CALL      KEYDATE
          BRANCH    OVRCD,DTRNG100
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
.
          MOVE      CMM,F2
          MOVE      CMON,F2
          LOAD      DIM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      FROMDATE,CDAY,DIM3,CCENT,CYEAR
          REP       " 0",FROMDATE
.
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
DTRNG200  MOVE      TEN4,CCOL
          MOVE      SIX,CVERT
          CALL      KEYDATE
          BRANCH    OVRCD,DTRNG200
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
.
          MOVE      CMM,F2
          MOVE      CMON,F2
          LOAD      DIM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      TODATE,CDAY,DIM3,CCENT,CYEAR
          REP       " 0",TODATE
.
          CALL      KHOS000                 * Keyin Hospital id
.
          CALL      CONTQST
          BRANCH    CEXIT,DTRNG999,DTRNG000,DTRNG900
.
DTRNG900  MOVE      ONE,EXIT
.
DTRNG999  RETURN
+
.****************************************************************************
.*  DUMDTE00  :  set date range for "un-registered cancers"                 *
.****************************************************************************
DUMDTE00  MOVE      CFILENO,DIM5
.
          MOVE      CMM,F2
          LOAD      DIM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      FROMDATE,CDD,DIM3,CCC,CYY
          REP       " 0",FROMDATE
.
          MOVE      CMM,F2
          LOAD      DIM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      TODATE,CDD,DIM3,CCC,CYY
          REP       " 0",TODATE
.
          DISPLAY   *P1:23,"Extract files will be labeled with date range ":
                    "of ",*V2LON,FROMDATE,*HOFF," to ",*V2LON,TODATE,*HOFF
DUMDT800  MOVE      ZERO,EXIT
.
          CALL      KHOS000                 * Keyin Hospital id
.
          CALL      CONTQST
          BRANCH    CEXIT,DUMDT999,DUMDT900,DUMDT900
          BEEP
          GOTO      DUMDT800
.
DUMDT900  MOVE      ONE,EXIT
.
DUMDT999  DISPLAY   *P1:23,*EF
          RETURN
+
.****************************************************************************
.*  CREXTR00  :  Create extract files                                       *
.****************************************************************************
CREXTR00  MOVE      CFILENO,DIM5
.
          IF        IBCNMHOS=1
            PACK      DIM5,PTHSHSID,SP5
          ENDIF
          REPLACE   " 0",DIM5
          MOVE      DIM5,XFACLCOD
.
          PACK      EXTHDRFN,XFACLCOD,FROMDATE,TODATE,HDRFILE,QCREXTN,SP70
          PACK      EXTHDRTM,XFACLCOD,HDRFILE,SP70
          PREP      EXTHDRFL,EXTHDRTM
.
          PACK      EXTCADFN,XFACLCOD,FROMDATE,TODATE,CADFILE,QCREXTN,SP70
          PACK      EXTCADTM,XFACLCOD,CADFILE,SP70
          PREP      EXTCADFL,EXTCADTM
.
          PACK      EXTCANFN,XFACLCOD,FROMDATE,TODATE,CANFILE,QCREXTN,SP70
          PACK      EXTCANTM,XFACLCOD,CANFILE,SP70
          PREP      EXTCANFL,EXTCANTM
.
          PACK      EXTCDXFN,XFACLCOD,FROMDATE,TODATE,CDXFILE,QCREXTN,SP70
          PACK      EXTCDXTM,XFACLCOD,CDXFILE,SP70
          PREP      EXTCDXFL,EXTCDXTM
.
          PACK      EXTFANFN,XFACLCOD,FROMDATE,TODATE,FANFILE,QCREXTN,SP70
          PACK      EXTFANTM,XFACLCOD,FANFILE,SP70
          PREP      EXTFANFL,EXTFANTM
.
CREXTR99  RETURN
+
.**************************************************************************
.   Loop over files for coding date
.**************************************************************************
.
NCOD0000  MOVE      ZERO,EXIT
.
          PACK      KEY16,STRTDATE,SP20
          CALL      RDSMISS4
NCOD1000  CALL      RDKMISS4
          BRANCH    OVRCD,NCOD9000
.
          MATCH     ACODDTE,ENDDATE         * In Date range
          GOTO      NCOD9000 IF LESS
.
          UNPACK    ACODDTE,ACC,AYY,AMM,ADD         
          PACK      THEDATE,ADD,SLASH,AMM,SLASH,ACC,AYY
          MOVE      AURNO,SAVURNO
.
          PACK      KEY8,AADMNO,SP10    
          CALL      RDPTCRG1                * Get cancer registation record
          BRANCH    OVRCD,NCOD1000
.
          PACK      KEY8,AADMNO
          CALL      RDDSCH1                 * read discharge file
          BRANCH    OVRCD,NCOD1000
.
          PACK      KEY8,AADMNO
          CALL      RDPTDAD1                * read transfer file
.
          PACK      KEY8,AADMNO,SP10
          CALL      RDPMVX11                * read visit extension file
          BRANCH    OVRCD,NCOD1000
.
          IF        IBCNMHOS=1
            MATCH     PMVXMHOS,HEADHOSP
            GOTO      NCOD1000 IF NOT EQUAL
          ENDIF
.
          PACK      KEY8,AURNO,SP10
          CALL      RDMAST1                 * Get PMI Details
          BRANCH    OVRCD,NCOD1000
.
          PACK      KEY8,AURNO,SP10
          CALL      RDPMPX21
          BRANCH    OVRCD,NCOD1000
.
          CALL      GCAD0000                * Extract CAD details 
          CALL      GCAN0000                * Extract CAN details 
          CALL      GFAN0000                * Extract FAN details
          GOTO      NCOD1000
.
NCOD9000  CALL      GHDR0000                * Write HDR file
          PRINT     *1,"*** End of Report ***"
.
NCOD9999  RETURN
+
.*****************************************************************
. Rountine to loop over discharge file for cancer registration extract
.*****************************************************************
.
NDIS0000  MOVE      ZERO,EXIT
.
          PACK      KEY16,STRTDATE,SP10
          CALL      RDSDSCH2
NDIS1000  CALL      RDKDSCH2                * Loop over discharges
          BRANCH    OVRCD,NDIS9000
.
          MATCH     DDATE,ENDDATE           * In date range
          GOTO      NDIS9000 IF LESS
.
          UNPACK    DDATE,ACC,AYY,AMM,ADD         
          PACK      THEDATE,ADD,SLASH,AMM,SLASH,ACC,AYY
          MOVE      DURNO,SAVURNO
.
          PACK      KEY8,AADMNO
          CALL      RDPTDAD1                * read transfer file
.
          PACK      KEY8,DADMNO,SP70   
          CALL      RDPTCRG1                                            
          BRANCH    OVRCD,NDIS1000
.
          PACK      KEY8,DADMNO,SP10
          CALL      RDMISS1                 * Get admission details
          BRANCH    OVRCD,NDIS1000
.
          PACK      KEY8,DADMNO
          CALL      RDPMVX11                * read visit extension file
          BRANCH    OVRCD,NDIS1000
.
          IF        IBCNMHOS=1
            MATCH     PMVXMHOS,HEADHOSP
            GOTO      NDIS1000 IF NOT EQUAL
          ENDIF
.
          PACK      KEY8,DURNO,SP10
          CALL      RDMAST1                 * Get PMI Details
          BRANCH    OVRCD,NDIS1000
.
          PACK      KEY8,AURNO,SP10
          CALL      RDPMPX21
          BRANCH    OVRCD,NDIS1000
.
          CALL      GCAD0000                * Extract CAD details 
          CALL      GCAN0000                * Extract CAN details 
          CALL      GFAN0000                * Extract FAN details
          GOTO      NDIS1000
.
NDIS9000  CALL      GHDR0000                * Write HDR file
          PRINT     *1,"*** End of Report ***"
.
NDIS9999  RETURN
+
.*****************************************************************
.* Get and Write HDR Details
.*****************************************************************
GHDR0000  MOVE      CADCOUNT,XNOOFCAD
          REP       " 0",XNOOFCAD
          MOVE      CANCOUNT,XNOOFCAN
          REP       " 0",XNOOFCAN
          MOVE      CDXCOUNT,XNOOFCDX
          REP       " 0",XNOOFCDX
          MOVE      FANCOUNT,XNOOFFAN
          REP       " 0",XNOOFFAN
.
          WRITE     EXTHDRFL,SEQ;*+,XFACLCOD,COMMA,XNOOFCAD,COMMA:
                                    XNOOFCAN,COMMA,XNOOFFAN,COMMA,XNOOFCDX
.
GHDR9999  RETURN
+
.*****************************************************************
.* Clear Cancer Registration Details
.*****************************************************************
CLRCRD00  MOVE      ZEROVISN,PTCDDADN
          MOVE      ZERO,PTCDCNRG
          UNPACK    SP70,PTCDADTE,PTCDDDTE,PTCDTMRC,PTCDMTSP
          MOVE      ZERO,PTCDUKPR
          UNPACK    SP70,PTCDPRMM,PTCDHTYP,PTCDMET1,PTCDMET2,PTCDMET3
          UNPACK    SP70,PTCDLATR,PTCDSTAG
          UNPACK    SP70,PTCDBT01,PTCDBT02,PTCDBT03,PTCDBT04,PTCDBT05
          UNPACK    SP70,PTCDBT06,PTCDBT07,PTCDBT08,PTCDBT09,PTCDBT10
          MOVE      ZERO,PTCDRFLG
          UNPACK    SP70,PTCDOTDG,PTCDPATH,PTCDDGPR
          PACK      PTCDFCPR,SP70,SP70,SP70,SP70
          UNPACK    SP70,PTCDBO01,PTCDBO02,PTCDBO03,PTCDBO04,PTCDBO05
          UNPACK    SP70,PTCDBO06,PTCDBO07,PTCDBO08,PTCDBO09,PTCDDGOT
          MOVE      ZERO,PTCDAUTP
          MOVE      ZERO,PTCDBRLW
          MOVE      ZERO,PTCDBTSZ
          MOVE      ZERO,PTCDGLSC
          UNPACK    SP70,PTCDMLIN,PTCDRPLB,PTCDCKLV,PTCDCOMM         
          UNPACK    SP70,PTCDDTEX,PTCDDTRC,PTCDTMCR,PTCDUSCR         
          UNPACK    SP70,PTCDDTUP,PTCDTMUP,PTCDUSUP,PTCDBSDG         
.
          MOVE      SP70,PTCDCELT
          MOVE      SP70,PTCDDOCC
          MOVE      SP70,PTCDDOCN
          MOVE      SP70,PTCDNTDT
          MOVE      SP70,PTCDSNT1
          MOVE      SP70,PTCDSNT2
          MOVE      SP70,PTCDSNT3
          MOVE      SP70,PTCDBOD1
          MOVE      SP70,PTCDBOD2
          MOVE      SP70,PTCDBOD3
          MOVE      SP70,PTCDBOD4
          MOVE      SP70,PTCDBOD5
          MOVE      SP70,PTCDBOL5
          MOVE      SP70,PTCDBOD6
          MOVE      SP70,PTCDBOL6
          MOVE      SP70,PTCDBOD7
          MOVE      SP70,PTCDBOL7
          MOVE      SP70,PTCDGD01
          MOVE      SP70,PTCDGD02
          MOVE      SP70,PTCDGD03
          MOVE      SP70,PTCDGD04
          MOVE      SP70,PTCDGD05
          MOVE      SP70,PTCDGD06
          MOVE      SP70,PTCDGD07
          MOVE      SP70,PTCDGD08
          MOVE      SP70,PTCDGD09
          MOVE      SP70,PTCDPLOD
          MOVE      SP70,PTCDCOD1
          MOVE      SP70,PTCDCOD2
          MOVE      SP70,PTCDCSGT
          MOVE      SP70,PTCDCSGN
          MOVE      SP70,PTCDCSGM
          MOVE      SP70,PTCDCSSG
          MOVE      SP70,PTCDCSCM
          MOVE      SP70,PTCDTNON
          MOVE      SP70,PTCDTSUR
          MOVE      SP70,PTCDTSDT
          MOVE      SP70,PTCDTRAD
          MOVE      SP70,PTCDTRDT
          MOVE      SP70,PTCDTCHM
          MOVE      SP70,PTCDTCDT
          MOVE      SP70,PTCDTHOR
          MOVE      SP70,PTCDTHDT
          MOVE      SP70,PTCDTBIO
          MOVE      SP70,PTCDTBDT
          MOVE      SP70,PTCDRLDT
          MOVE      SP70,PTCDNDCL
          MOVE      SP70,PTCDTSAB
          MOVE      SP70,PTCDTOTH
          MOVE      SP70,PTCDTHOS
          MOVE      SP70,PTCDRST1
          MOVE      SP70,PTCDRST2
          MOVE      SP70,PTCDRST3
          MOVE      SP70,PTCDRST4
          MOVE      SP70,PTCDRST5
          MOVE      SP70,PTCDSURB
          MOVE      SP70,PTCDSTTE
          MOVE      SP70,PTCDPOST
          MOVE      SP70,PTCDMET4
          MOVE      SP70,PTCDMET5
          MOVE      SP70,PTCDMET6
          MOVE      SP70,PTCDECOG
          MOVE      SP70,PTCDRADI
          MOVE      SP70,PTCDSYST
          MOVE      SP70,PTCDSCFL
          MOVE      SP70,PTCDCSSY
          MOVE      SP70,PTCDBDIA
          MOVE      SP70,PTCDSPAR
.
CLRCRD99  RETURN
+
.*****************************************************************
.* Print the Cancer Details Report Records
.*****************************************************************
CPRT0000  MOVE      PTCRPRIM,KEY9
          CALL      RDPTICD1
          IF        OVRCD=0
            MOVE      DDESC,DIAGDESC           * Primary site text description
          ELSE
            MOVE      SP70,DIAGDESC
          ENDIF
.
          MOVE      PTCRHIST,KEY9
          CALL      RDPTICD1
          IF        OVRCD=0
            MOVE      DDESC,PATHDESC           * Primary site text description
          ELSE
            MOVE      SP70,PATHDESC
          ENDIF
.
          MOVE      TWO,PACFRMT
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,KEY28
.
          PRINT     *1,"| U/R Number",*18,": ",PURNO,*54,"| Admission No.":
                    *76,": ",DADMNO,*132,"|"
          PRINT     *1,"| Name",*18,": ",KEY28,*54,"| Admission Date ":
                    *76,": ",ADMDATE,*132,"|"
          PRINT     *1,"| Date of Birth",*18,": ",DOB,*54,"| ",DIM9," Date":
                    *76,": ",THEDATE,*132,"|"
          PRINT     *1,"| Sex",*18,": ",PSEX,*54,"| Diagnosis":
                    *76,": ",DIAGDESC,*132,"|"
          PRINT     *1,"| ",*54,"| Pathology",*76,": ",PATHDESC,*132,"|"
.
          CALL      UND132
          ADD       SEVEN,CLNO  
          COMPARE   CLNO,FIFTY2           * Check Lines and print header ?
          CALL      HEDC0000 IF LESS 
          CALL      HEDC0000 IF EQUAL
          MOVE      PURNO,SAVURNO
.
CPRT9999  RETURN
+
.*****************************************************************
.* Print a cancer details Header
.*****************************************************************
HEDC0000  CALL      HEAD132
.
          PRINT     *30,"Cancer Register Details"
          IF        OPTION = 1
            PRINT     *30,"From ",DIM9," Date : ",FROMDATE," To : ",TODATE
          ELSE
            PRINT     *30,"All un-registered or re-registered Cancers"
          ENDIF
.
          IF        IBCNMHOS =1
            PRINT     *30,"Hospital : ",HEADHOSP,"    ",NAMEHOSP
            MOVE      SIX,CLNO
          ELSE
            MOVE      FIVE,CLNO
          ENDIF
.
          CALL      UND132
.
HEDC9999  RETURN
+
.*****************************************************************
.* Get CAD details - Cancer Admission Details
.*****************************************************************
GCAD0000  MOVE      ZERO,EXIT
.
          CALL      INTCAD00          * initialise CAD extract fields
.
          MOVE      DURNO,XURNUMBR
          REP       " 0",XURNUMBR
.
          MOVE      DADMNO,FORM12  
          MOVE      FORM12,XADMISNO
          REP       " 0",XADMISNO
.
          UNPACK    DDATE,ACC,AYY,AMM,ADD         
          MOVE      AMM,F2
          LOAD      MONTH3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      XSEPTDTE,ADD,MONTH3,ACC,AYY
.
          UNPACK    ADATE,ACC,AYY,AMM,ADD
          MOVE      AMM,F2
          LOAD      MONTH3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      XADMSDTE,ADD,MONTH3,ACC,AYY
          PACK      ADMDATE,ADD,SLASH,AMM,SLASH,ACC,AYY  * for print report 
.
          MATCH     SP10,PMEDI
          IF        !@EQUAL
            SQUEEZE   PTMXMCCD
            PACK      XMEDICAR,PMEDI,PTMXMCCD        * Medicare number
          ENDIF
.
          MATCH     SP70,PBDATE
          IF        @EQUAL
...         MOVE      "Patient's DOB is unknown",XCOMMENT
...         PACK      KEY52,DQTE,XCOMMENT,DQTE
...         MOVE      KEY52,XCOMMENT
            MOVE      SP10,DOB                           * for print report
          ELSE
            UNPACK    PBDATE,ACC,AYY,AMM,ADD   * Date of Birth
            MOVE      AMM,F2
            LOAD      MONTH3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      XDOB,ADD,MONTH3,ACC,AYY
            PACK      DOB,ADD,SLASH,AMM,SLASH,ACC,AYY    * for print report
          ENDIF
.
          MATCH     SP70,PSNAME
          IF        !@EQUAL
            MOVE      PSNAME,XPSNAME
            STRIP     XPSNAME
            PACK      KEY26,DQTE,XPSNAME,DQTE * Patient Surname
            MOVE      KEY26,XPSNAME
          ENDIF
.
          MOVE      PGNAME,KEY25
          CALL      GNAM0000                 * Get given names
          MATCH     SP70,KEY30
          IF        !@EQUAL
            MOVE      KEY30,XPGFNAME
            STRIP     XPGFNAME
            PACK      KEY17,DQTE,XPGFNAME,DQTE
            MOVE      KEY17,XPGFNAME
          ENDIF
          MATCH     SP70,KEY30A
          IF        !@EQUAL
            MOVE      KEY30A,XPGSNAME
            STRIP     XPGSNAME
            PACK      KEY17,DQTE,XPGSNAME,DQTE
            MOVE      KEY17,XPGSNAME
          ENDIF
.
          CALL      GADDR000                 * get address
.
          MATCH     SP70,POCCUP
          IF        !@EQUAL
            MOVE      POCCUP,XOCCUPTN
            STRIP     XOCCUPTN
            PACK      KEY52,DQTE,XOCCUPTN,DQTE * Patient Surname
            MOVE      KEY52,XOCCUPTN
          ENDIF
.
          MOVE      PSEX,XSEX         
.
          MATCH     SP3,PCONT                * Country of birth
          IF        !@EQUAL
            PACK      KEY5,CATC,PCONT
            CALL      RDCODE1               
            IF        OVRCD=0
              UNPACK    TCFORM6,DIM2,XCOUNTRY
              REP       " 0",XCOUNTRY
            ENDIF
          ENDIF
.
          MOVE      ANSN,XMSTATUS           * Marital Stat not stated/unknown
          MATCH     SP3,PMSTAT
          IF        !@EQUAL
            PACK      KEY5,CATM,PMSTAT
            CALL      RDCODE1               
            IF        OVRCD=0 
              MOVE      THCSCOD,DIM1
              MOVE      DIM1,F1
              LOAD      XMSTATUS,F1,CODENM:  * single,never married
                                    ANSM:     * married
                                    ANSW:     * widowed
                                    ANSD:     * divorced
                                    ANSA:     * separated
                                    ANSN      * not stated/unknown
            ENDIF
          ENDIF
.
          MOVE      "19",XINDIGST         * Indigenous not stated 
          MATCH     SP3,PMVXABRG
          IF        !@EQUAL
            PACK      KEY5,CATVA,PMVXABRG,SP10
            CALL      RDCODE1              
            IF        OVRCD=0 
              CMATCH    "4",THCSCOD
              IF        @EQUAL
                MOVE      "14",XINDIGST   * not indigenous  
              ENDIF
              CMATCH    "1",THCSCOD
              IF        @EQUAL
                MOVE      "11",XINDIGST   * indigenous-Aboriginal 
              ENDIF
              CMATCH    "2",THCSCOD
              IF        @EQUAL
                MOVE      "12",XINDIGST   * indigenous-Torres Strait Islander
              ENDIF
              CMATCH    "3",THCSCOD
              IF        @EQUAL
                MOVE      "13",XINDIGST   * indigenous-Aboriginal & Torres Stait
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      "99",XMODESEP           * Unknown Mode of Separation
          BRANCH    PTCNDTRF,GCAD1000
.
          MATCH     SP3,DDEST               * read discharge destination
          IF        !@EQUAL
            PACK      KEY5,CATDD,DDEST
            CALL      RDCODE1               
            IF        OVRCD=0 
              MOVE      THCSCOD,XMODESEP 
              REP       " 0",XMODESEP        * Seperation mode
            ENDIF
          ENDIF
.
GCAD1000  MATCH     SP3,DSTAT               * read discharge status
          GOTO      GCAD1500 IF EQUAL
.
          PACK      KEY5,CATD,DSTAT
          CALL      RDCODE1                 * Cat."D "
          BRANCH    OVRCD,GCAD1500
.
          IF        PTCNDTRF=1
            MOVE      THCSCOD,XMODESEP 
            REP       " 0",XMODESEP         * Seperation mode
          ENDIF
.
          CMATCH    ANSD,TCINDC1            * discharge with deceased
          IF        @EQUAL
            MATCH     SP70,DDIAG
            GOTO      GCAD1500 IF EQUAL
            MOVE      DDIAG,XCSDEATH
            STRIP     XCSDEATH
            PACK      KEY52,DQTE,XCSDEATH,DQTE    * Cause of Death
            MOVE      KEY52,XCSDEATH
          ENDIF
.
...D1500  MOVE      PTDAADTS,KEY5           * Transfer from facility
GCAD1500  MOVE      PTDADCTS,KEY5           * Transfer to facility
          CALL      STRP0000                * Strip off blank char
          MOVE      KEY5,XTRFFACL
          REP       " 0",XTRFFACL           
          MATCH     "00000",XTRFFACL
          IF        @EQUAL
            MOVE      SP70,XTRFFACL
          ENDIF
.
          IF        PCEASE=1
            IF        PAUTOPY=1
              MOVE      "Y",XAUTOPSY        * Autopsy flag
            ELSE
              MOVE      "N",XAUTOPSY
            ENDIF
          ENDIF
.
          MOVE      DADMNO,KEY8
          PACK      KEY13,KEY8,SP70                                   *C-240107
          CALL      RSPTECD1
          CALL      RKPTECD1
          IF        OVRCD=0
            MATCH     KEY8,DPTEDADM
            IF        @EQUAL
              MATCH     "P",PTEDTYPE          * Primary?
              IF        @EQUAL
                MOVE      PTEDCODE,XDIAGSEP   * discharge diagnosis code
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP70,ADOCTA
          IF        !@EQUAL
            PACK      KEY6,ADOCTA,SP10
            CALL      RDDOCT1
            BRANCH    OVRCD,GCAD2000
.
            MATCH     SP70,DTITL
            IF        !@EQUAL
              PACK      KEY4,DTITL,SP70
              MOVE      KEY4,XTTLTDOC
              STRIP     XTTLTDOC
              PACK      KEY6,DQTE,XTTLTDOC,DQTE
              MOVE      KEY6,XTTLTDOC
            ENDIF
.
            MATCH    SP70,DGNAME
            IF        !@EQUAL
              MOVE      DGNAME,DIM1
              PACK      XINTTDOC,DQTE,DIM1,DQTE
              MOVE      DGNAME,XGNMTDOC
              STRIP     XGNMTDOC
              PACK      KEY57,DQTE,XGNMTDOC,DQTE
              MOVE      KEY57,XGNMTDOC
            ENDIF
.
            MATCH    SP70,DSNAME
            IF        !@EQUAL
              MOVE      DSNAME,XSNMTDOC
              STRIP     XSNMTDOC
              PACK      KEY31,DQTE,XSNMTDOC,DQTE
              MOVE      KEY31,XSNMTDOC
            ENDIF
          ENDIF
.
GCAD2000  MOVE      ZERO,MULTCCNT
          PACK      KEY25,DADMNO,SP10
          CALL      RSPTCRD1                * Get cancer register details
GCAD4000  CALL      RKPTCRD1                
          BRANCH    OVRCD,GCAD9000
.
          MATCH     DADMNO,PTCDDADN
          GOTO      GCAD9000 IF NOT EQUAL
          ADD       ONE,MULTCCNT
          GOTO      GCAD4000
.
GCAD9000  MOVE      MULTCCNT,XPRSITCT
          REP       " 0",XPRSITCT
          MATCH     "00",XPRSITCT
          IF        @EQUAL
            MOVE      SP5,XPRSITCT
          ENDIF
.
          STRIP     XMEDICAR
          STRIP     XPSNAME
          STRIP     XPGFNAME
          STRIP     XPGSNAME
          STRIP     XADDRESS
          STRIP     XLOCATN
          STRIP     XPOSTCOD
          STRIP     XDOB
          STRIP     XOCCUPTN
          STRIP     XSEX
          STRIP     XCOUNTRY
          STRIP     XMSTATUS
          STRIP     XINDIGST
          STRIP     XADMSDTE
          STRIP     XSEPTDTE
          STRIP     XMODESEP
          STRIP     XTRFFACL
          STRIP     XAUTOPSY
          STRIP     XCSDEATH
          STRIP     XTTLTDOC
          STRIP     XINTTDOC
          STRIP     XGNMTDOC
          STRIP     XSNMTDOC
          STRIP     XDIAGSEP
.
          ADD       ONE,CADCOUNT
          WRITE     EXTCADFL,SEQ;*+,XURNUMBR,COMMA,XADMISNO,COMMA:
                                    XPRSITCT,COMMA,XMEDICAR,COMMA:
                                    XPSNAME,COMMA,XPGFNAME,COMMA:
                                    XPGSNAME,COMMA,XADDRESS,COMMA:
                                    XLOCATN,COMMA,XPOSTCOD,COMMA:
                                    XDOB,COMMA,XOCCUPTN,COMMA:
                                    XSEX,COMMA,XCOUNTRY,COMMA:
                                    XMSTATUS,COMMA,XINDIGST,COMMA:
                                    XADMSDTE,COMMA,XSEPTDTE,COMMA:
                                    XMODESEP,COMMA,XTRFFACL,COMMA:
                                    XAUTOPSY,COMMA,XCSDEATH,COMMA:
                                    XTTLTDOC,COMMA,XINTTDOC,COMMA:
                                    XGNMTDOC,COMMA,XSNMTDOC,COMMA,XDIAGSEP
.
          CALL      CPRT0000     * print registered patient
.
GCAD9999  RETURN
+
.*****************************************************************
.* Get address
.*****************************************************************
GADDR000  CLEAR     XADDRESS
          MATCH     SP70,PADD1
          IF        !@EQUAL
            STRIP     PADD1
            APPEND    PADD1,XADDRESS
            SETLPTR   PADD1
          ENDIF
.
          MATCH     SP70,PADD2
          IF        !@EQUAL
            STRIP     PADD2
            APPEND    SP1,XADDRESS
            APPEND    PADD2,XADDRESS
            SETLPTR   PADD2
          ENDIF
.
          MATCH     SP70,PADD4
          IF        !@EQUAL
            STRIP     PADD4
            APPEND    SP1,XADDRESS
            APPEND    PADD4,XADDRESS
            SETLPTR   PADD4
          ENDIF
.
          RESET     XADDRESS
          MATCH     SP70,XADDRESS
          IF        !@EQUAL
            STRIP     XADDRESS
            PACK      KEY52,DQTE,XADDRESS,DQTE
            MOVE      KEY52,XADDRESS
          ENDIF
         
          PACK      KEY40,PSUBRB            * Suburb
          MATCH     SP15,PSUBRB
          IF        @EQUAL
            MATCH     SP4,PPOST
            IF        !@EQUAL
              PACK      KEY56,PPOST,SP70    * use postcode to get suburb
              CALL      RSIBPOS1
              CALL      RKIBPOS1
              IF        OVRCD=0          
                MATCH     PPOST,IBPOPCOD    * same postcode ?
                IF        @EQUAL   
                  PACK      KEY40,IBPOSUBR,SP70   * Suburb
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          MOVE      KEY40,XLOCATN
          MATCH     SP70,XLOCATN
          IF        !@EQUAL
            STRIP     XLOCATN
            PACK      KEY42,DQTE,XLOCATN,DQTE
            MOVE      KEY42,XLOCATN
          ENDIF
.
          MOVE      PPOST,XPOSTCOD
          MATCH     SP10,PPOST
          IF        @EQUAL
            MOVE      "0989",XPOSTCOD             * Not stated/unknown
            PACK      KEY56,PSUBRB,SP70           * Use suburb to get postcode
            CALL      RSIBPOS2
            CALL      RKIBPOS2
            IF        OVRCD=0         
              MATCH     PSUBRB,IBPOSUBR           * same postcode ?
              IF        @EQUAL   
                PACK      XPOSTCOD,IBPOPCOD,SP10  * Postcode
              ENDIF
            ENDIF
          ENDIF
.
GADDR999  RETURN
+
.******************************************************************************
.*                                 STRP0000                                   *
.*                       Strip off blank char.                                *
.******************************************************************************
STRP0000  STRIP     KEY5
          TYPE      KEY5
          GOTO      STRP9000 IF NOT EQUAL
          MOVE      KEY5,FORM5
          MOVE      FORM5,KEY5
          REP       " 0",KEY5
          GOTO      STRP9999
.
STRP9000  ENDSET    KEY5
          APPEND    SP10,KEY5
          RESET     KEY5
STRP9999  RETURN
+
.*****************************************************************
.* Get CAN details - Cancer Details
.*****************************************************************
GCAN0000  PACK      KEY25,DADMNO,SP70
          CALL      RSPTCRD1
GCAN1000  CALL      RKPTCRD1
          BRANCH    OVRCD,GCAN9999
.
          MATCH     DADMNO,PTCDDADN
          GOTO      GCAN9999 IF NOT EQUAL
.
          CALL      INTCAN00                * initialise CAN extract fields
.
          IF        PTCDCNRG>99
            GOTO      GCAN9999
          ENDIF
.
          MOVE      PTCDCNRG,F2
          MOVE      F2,XPRSITNM
          REP       " 0",XPRSITNM
.
          MOVE      PTCDPRMM,XPRSITCD
          MOVE      PTCDPRMM,KEY9
          CALL      RDPTICD1
          IF        OVRCD=0
            MOVE      DDESC,KEY40
            MOVE      KEY40,XPRSITDS        * Primary site text description
            STRIP     XPRSITDS
            PACK      KEY42,DQTE,XPRSITDS,DQTE
            MOVE      KEY42,XPRSITDS
          ENDIF
.
          MOVE      PTCDHTYP,XMORPHCD
.
          MOVE      "Y",XFDIAGFL
          MATCH     SP70,PTCDDDTE
          IF        @EQUAL 
            MOVE      "15JUN1900",XFDIAGDT
          ELSE
            UNPACK    PTCDDDTE,ACC,AYY,AMM,ADD         
            MOVE      AMM,F2
            LOAD      MONTH3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      XFDIAGDT,ADD,MONTH3,ACC,AYY
            MATCH     "0",PTCDDGPR
            IF        @EQUAL
              MOVE      "N",XFDIAGFL
            ENDIF
          ENDIF
.
.                                                                     *I-154835
GCAN1100  MOVE      ZERO,FORM1
          MOVE      PTCDDGPR,FORM1          * Diagnosed prior to admission
.                           yes     unknown
          BRANCH    FORM1,GCAN1200,GCAN1200
          GOTO      GCAN1300       * no
.
GCAN1200  CLEAR     XLOC1DGN                * PTCDDGPR = 0 or 2
....      MATCH     SP30,PTCDSURB
....      GOTO      GCAN1300 IF EQUAL
....      APPEND    PTCDSURB,XLOC1DGN
....      APPEND    SP1,XLOC1DGN
....      APPEND    PTCDSTTE,XLOC1DGN
....      PACK      XPOS1DGN,PTCDPOST,SP10  * post code
....      GOTO      GCAN1400
.
GCAN1300  CLEAR     XLOC1DGN                * PTCDDGPR = 1
          MATCH     SP35,PADD2
          IF        @EQUAL
            APPEND    PSUBRB,XLOC1DGN
          ELSE
            APPEND    PADD2,XLOC1DGN
          ENDIF
          PACK      XPOS1DGN,PPOST,SP10     * post code
          GOTO      GCAN1400
.                                           * add Quotes each end
GCAN1400  RESET     XLOC1DGN
          MOVE      XLOC1DGN,KEY40
          PACK      XLOC1DGN,KEY40,SP70
          MATCH     SP70,XLOC1DGN
          IF        !@EQUAL
            STRIP     XLOC1DGN
            PACK      KEY42,DQTE,XLOC1DGN,DQTE
            MOVE      KEY42,XLOC1DGN
          ENDIF
.
GCAN1500  MOVE      ANSU,XLATRCNC           * Unknown laterality           
          PACK      KEY5,CATLY,PTCDLATR,SP10
          CALL      RDCODE1                 * Cat."LY"
          IF        OVRCD=0
            CMATCH    SP1,THCSCOD
            IF        !@EQUAL
              UNPACK    THCSCOD,XLATRCNC,DIM5
            ENDIF
          ENDIF
.
.-------------------------------------------  start of deletion       *D-154835
....        CMATCH    "1",THCSCOD
....        IF        @EQUAL
....          MOVE      ANSR,XLATRCNC       * Right              
....        ENDIF
....        CMATCH    "2",THCSCOD
....        IF        @EQUAL
....          MOVE      ANSL,XLATRCNC       * Left              
....        ENDIF
....        CMATCH    "4",THCSCOD
....        IF        @EQUAL
....          MOVE      ANSB,XLATRCNC       * Bilateral              
....        ENDIF
....        CMATCH    "8",THCSCOD
....        IF        @EQUAL
....          MOVE      ANSN,XLATRCNC       * Not applicable              
....        ENDIF
....      ENDIF
.-------------------------------------------    end of deletion       *D-154835
.
          MATCH     "1",PTCDBT10
          IF        @EQUAL
            MOVE      "09",XBASDIAG   * Autopsy & Histology
            MOVE      " 9",BASDIAGN
            GOTO      GCAN1700
          ENDIF
          MATCH     "1",PTCDBT08
          IF        @EQUAL
            MOVE      "08",XBASDIAG   * Histology of Primary Site
            MOVE      " 8",BASDIAGN
            GOTO      GCAN1700
          ENDIF
          MATCH     "1",PTCDBT07
          IF        @EQUAL
            MOVE      "07",XBASDIAG   * Histology of Metastasis 
            MOVE      " 7",BASDIAGN
            GOTO      GCAN1700
          ENDIF
          MATCH     "1",PTCDBT06
          IF        @EQUAL
            MOVE      "06",XBASDIAG   * Cytology
            MOVE      " 6",BASDIAGN
            GOTO      GCAN1700
          ENDIF
          MATCH     "1",PTCDBT02
          IF        @EQUAL
            MOVE      "05",XBASDIAG   * Specific Biochemical & 
            GOTO      GCAN1700        *          immunological testing
          ENDIF
          MATCH     "1",PTCDBT05
          IF        @EQUAL
            MOVE      "04",XBASDIAG   * Exploratory Surgery
            GOTO      GCAN1700
          ENDIF
          MATCH     "1",PTCDBT04      * Endoscopy
          IF        @EQUAL
            MOVE      "03",XBASDIAG   * Clinical Investigations
            MOVE      " 4",BASDIAGN
            GOTO      GCAN1700
          ENDIF
          MATCH     "1",PTCDBT03      * Imaging
          IF        @EQUAL
            MOVE      "03",XBASDIAG   * Clinical Investigations
            MOVE      " 3",BASDIAGN
            GOTO      GCAN1700
          ENDIF
          MATCH     "1",PTCDBT01
          IF        @EQUAL
            MOVE      "02",XBASDIAG   * Clinical Only
            MOVE      " 1",BASDIAGN
            GOTO      GCAN1700
          ENDIF
.
          MOVE      "01",XBASDIAG       * Unknown
.
GCAN1700  MOVE      ZERO,DIAGCODE
          MOVE      XBASDIAG,DIAGCODE
.
          BRANCH    DIAGCODE,GCAN2000:      * "01" - Unknown
                             GCAN1800:      * "02" - Clinical Only
                             GCAN1800:      * "03" - Imaging/Endoscopy
                             GCAN2000:      * "04" - Explor.Surgery
                             GCAN2000:      * "05" - Spec.Biochen or Immunology
                             GCAN1900:      * "06" - Cytology or Haematology
                             GCAN1900:      * "07" - Histology of Metastasis
                             GCAN1900:      * "08" - Histology of Prim.Lab.
                             GCAN1900       * "09" - Autopsy or Histology
          GOTO      GCAN2000
.
GCAN1800  CALL      GCDX0000                * create a "CDX" record
          GOTO      GCAN2000
.                                           * now for DIAGCODE = "06" to "09"
GCAN1900  MOVE      SP70,XCOMMENT
          PACK      KEY26,PTCDDADN,PTCDCNRG,PTCDPRMM,BASDIAGN,SP70
          PACK      KEY29,PTCDDADN,PTCDCNRG,PTCDPRMM,BASDIAGN,SP70
          CALL      RSPMCDG1
GCAN1910  CALL      RKPMCDG1
          BRANCH    OVRCD,GCAN2000
.
          PACK      KEY26A,PMCGADNO,PMCGCNRG,PMCGPRMM,PMCGBSDG,SP70
          MATCH     KEY26,KEY26A
          GOTO      GCAN2000 IF NOT EQUAL
.
          MOVE      DPMCGCDI,XLABFANO
          REP       " 0",XLABFANO           * Laboratory Facility Code
          MOVE      PMCGCDDS,KEY50
          STRIP     KEY50
          PACK      XLABSPNO,DQTE,KEY50,DQTE  * Specimen No.
          GOTO      GCAN2000
.
.
GCAN2000  STRIP     XPRSITCD
          STRIP     XPRSITDS
          STRIP     XMORPHCD
          STRIP     XLOC1DGN
          STRIP     XPOS1DGN
          STRIP     XCOMMENT
          STRIP     XLABFANO                                          *I-154835
          STRIP     XLABSPNO                                          *I-154835
.
          ADD       ONE,CANCOUNT
          WRITE     EXTCANFL,SEQ;*+,XURNUMBR,COMMA,XADMISNO,COMMA:
                                    XPRSITNM,COMMA,XPRSITCD,COMMA:
                                    XPRSITDS,COMMA,XMORPHCD,COMMA:
                                    XFDIAGDT,COMMA,XFDIAGFL,COMMA:
                                    XLOC1DGN,COMMA,XPOS1DGN,COMMA:
                                    XLATRCNC,COMMA,XBASDIAG,COMMA:
                                    XCOMMENT,COMMA,XLABFANO,COMMA:
                                    XLABSPNO
.
GCAN3000  PACK      PTCDDTEX,CCC,CYY,CMM,CDD,SP10
          REP       " 0",PTCDDTEX
          CALL      UPPTCRD1
.
          GOTO      GCAN1000
.
GCAN9999  RETURN
+
.*****************************************************************
.* Get & Write Reason for Clinical Diagnosis (CDX) details
.*****************************************************************
GCDX0000  MOVE      ZERO,EXIT
          PACK      KEY26,PTCDDADN,PTCDCNRG,PTCDPRMM,BASDIAGN,SP70
          PACK      KEY29,PTCDDADN,PTCDCNRG,PTCDPRMM,BASDIAGN,SP70
          CALL      RSPMCDG1
GCDX1000  CALL      RKPMCDG1
          BRANCH    OVRCD,GCDX9999
.
          PACK      KEY26A,PMCGADNO,PMCGCNRG,PMCGPRMM,PMCGBSDG,SP70
          MATCH     KEY26,KEY26A
          GOTO      GCDX9999 IF NOT EQUAL
.
          CALL      INTCDX00           * initialise CDX extract variables
.
          MOVE      PMCGCDIG,XRSDIAGC
          REP       " 0",XRSDIAGC
.
          MATCH     SP70,PMCGCDDS
          IF        !@EQUAL
            MOVE      PMCGCDDS,XRSDIAGD
            STRIP     XRSDIAGD
            PACK      KEY50,DQTE,XRSDIAGD,DQTE
            MOVE      KEY50,XRSDIAGD
          ENDIF
.
          STRIP     XRSDIAGD
.
          ADD       ONE,CDXCOUNT
          WRITE     EXTCDXFL,SEQ;*+,XURNUMBR,COMMA,XADMISNO,COMMA:
                                    XPRSITNM,COMMA,XRSDIAGC,COMMA,XRSDIAGD
.
          GOTO      GCDX1000
.
GCDX9999  RETURN
+
.------------------------------------------------------------
. Get & Write Former/Alias Names (FAN) details
.------------------------------------------------------------
GFAN0000  MOVE      ZERO,GSRCOUNT
          MOVE      AURNO,GSRURNO
          PACK      KEY115,GSRURNO,SP70,SP70
          CALL      RSPTGSR1
GFAN1000  CALL      RKPTGSR1
          BRANCH    OVRCD,GFAN9999
.
GFAN4000  MATCH     AURNO,GSRURNO              * check U/R
          GOTO      GFAN9999 IF NOT EQUAL
.
          MOVE      SP70,XPSNAME
          MOVE      SP70,XPGFNAME
          MOVE      SP70,XPGSNAME
.
          MATCH     GSRSNAM,PTMASNAM           * check surname
          GOTO      GFAN5000 IF NOT EQUAL
.
          MATCH     GSRGNAM,PMPXFNAM           * check first given name
          GOTO      GFAN5000 IF NOT EQUAL
.
          MATCH     PTGSGNM2,PMPXSNAM          * check second given name
          GOTO      GFAN5000 IF NOT EQUAL
.
          MOVE      ZERO,GSRURNO
          GOTO      GFAN1000
.
GFAN5000  MATCH     GSRSNAM,SP70
          IF        !@EQUAL
            MOVE      GSRSNAM,XPSNAME
            STRIP     XPSNAME
            PACK      KEY26,DQTE,XPSNAME,DQTE * Patient Surname
            MOVE      KEY26,XPSNAME
          ENDIF
.
          MATCH     GSRGNAM,SP70
          IF        !@EQUAL
            MOVE      GSRGNAM,XPGFNAME
            STRIP     XPGFNAME
            PACK      KEY17,DQTE,XPGFNAME,DQTE
            MOVE      KEY17,XPGFNAME
          ENDIF
.
          MATCH     PTGSGNM2,SP70
          IF        !@EQUAL
            MOVE      PTGSGNM2,XPGSNAME
            STRIP     XPGSNAME
            PACK      KEY17,DQTE,XPGSNAME,DQTE
            MOVE      KEY17,XPGSNAME
          ENDIF
.
          ADD       ONE,GSRCOUNT
          MOVE      GSRCOUNT,XFANNUMB
          REP       " 0",XFANNUMB
.
          STRIP     XPSNAME
          STRIP     XPGFNAME
          STRIP     XPGSNAME
.
          ADD       ONE,FANCOUNT
          WRITE     EXTFANFL,SEQ;*+,XURNUMBR,COMMA,XADMISNO,COMMA:
                                    XFANNUMB,COMMA,XPSNAME,COMMA:
                                    XPGFNAME,COMMA,XPGSNAME
          GOTO      GFAN1000
.
GFAN9999  RETURN
+
.------------------------------------------------------------
. Initialise variables - for CAD file
.------------------------------------------------------------
INTCAD00  UNPACK   SP70,XURNUMBR,XADMISNO,XPRSITCT,XMEDICAR
          UNPACK   SP70,XPSNAME,XPGFNAME,XPGSNAME
          UNPACK   SP70,XADDRESS
          UNPACK   SP70,XLOCATN,XPOSTCOD,XDOB
          UNPACK   SP70,XOCCUPTN
          UNPACK   SP70,XSEX,XCOUNTRY,XMSTATUS,XINDIGST
          UNPACK   SP70,XADMSDTE,XSEPTDTE,XMODESEP,XTRFFACL
          UNPACK   SP70,XAUTOPSY,XCSDEATH
          UNPACK   SP70,XTTLTDOC,XINTTDOC,XGNMTDOC
          UNPACK   SP70,XSNMTDOC,XDIAGSEP
          UNPACK   SP70,XCOMMENT
          UNPACK   SP70,XLABFANO,XLABSPNO                             *I-154835
.
INTCAD99  RETURN
+
.------------------------------------------------------------
. Initialise variables - for CAN file
.------------------------------------------------------------
INTCAN00  UNPACK   SP70,XPRSITNM,XPRSITCD,XPRSITDS
          UNPACK   SP70,XMORPHCD,XFDIAGDT,XFDIAGFL
          UNPACK   SP70,XLOC1DGN,XPOS1DGN
          UNPACK   SP70,XLATRCNC,XBASDIAG
.
INTCAN99  RETURN
+
.------------------------------------------------------------
. Initialise variables - for CDX file
.------------------------------------------------------------
INTCDX00  UNPACK   SP70,XRSDIAGC,XRSDIAGD
.
INTCDX99  RETURN
+
.------------------------------------------------------------
. Initialise variables - for FAN file
.------------------------------------------------------------
INTFAN00  UNPACK   SP70,XFANNUMB,XPSNAME,XPGFNAME,XPGSNAME
.
INTFAN99  RETURN
+
.------------------------------------------------------------
. Get first given name
.------------------------------------------------------------
GNAM0000  MOVE     ZERO,SECNDFLG
          UNPACK   SP70,KEY30,KEY30A
          CLEAR    KEY30
          CLEAR    KEY30A
          RESET    KEY25
          CMATCH   SP1,KEY25
          GOTO     GNAM9000 IF EQUAL
          GOTO     GNAM1200
.
GNAM1000  BUMP     KEY25
          GOTO     GNAM9000 IF EOS
.
          CMATCH   SP1,KEY25
          GOTO     GNAM1200 IF NOT EQUAL
          COMPARE  ONE,SECNDFLG
          GOTO     GNAM2000 IF NOT EQUAL
.
GNAM1200  MOVE     KEY25,ANS
          IF       SECNDFLG=0
            APPEND   ANS,KEY30     * first given name
          ELSE
            APPEND   ANS,KEY30A    * Second given name
          ENDIF
          GOTO     GNAM1000
.
GNAM2000  MOVE     ONE,SECNDFLG    * Flag for second given name
          GOTO     GNAM1000
.
GNAM9000  APPEND   SP30,KEY30
          RESET    KEY30
          APPEND   SP30,KEY30A
          RESET    KEY30A
GNAM9999  RETURN
+
.------------------------------------------------------------
. Move Extract to Web Directory
.------------------------------------------------------------
MVEXT000  MOVE      ZERO,EXIT
.
          CLEAR     CMDLINE                 * header file
          APPEND    "ibainp87.us1 ",CMDLINE
          APPEND    EXTHDRTM,CMDLINE
          APPEND    ".txt ",CMDLINE
          APPEND    EXTHDRFN,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE                 * cancer admission file
          APPEND    "ibainp87.us1 ",CMDLINE
          APPEND    EXTCADTM,CMDLINE
          APPEND    ".txt ",CMDLINE
          APPEND    EXTCADFN,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE                 * cancer details file
          APPEND    "ibainp87.us1 ",CMDLINE
          APPEND    EXTCANTM,CMDLINE
          APPEND    ".txt ",CMDLINE
          APPEND    EXTCANFN,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE                 * reason for clinical diagnosis file
          APPEND    "ibainp87.us1 ",CMDLINE
          APPEND    EXTCDXTM,CMDLINE
          APPEND    ".txt ",CMDLINE
          APPEND    EXTCDXFN,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE                 * former/alias names file
          APPEND    "ibainp87.us1 ",CMDLINE
          APPEND    EXTFANTM,CMDLINE
          APPEND    ".txt ",CMDLINE
          APPEND    EXTFANFN,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
MVEXT999  RETURN
+
.*****************************************************************
.                               NUNS0000
.           Loop thru Cancer Reg Details for un-sent registrations
.*****************************************************************     
.
NUNS0000  MOVE      ZERO,EXIT
.
          CALL      CRTM0000                * initialise the temp file
.         
          MOVE      SP30,TEMPSPAR
          MOVE      SP8,TEMPADMN
          PACK      KEY25,SP20,SP10
          CALL      RSPTCRD1                * Get cancer register details
NUNS1000  CALL      RKPTCRD1
          BRANCH    OVRCD,NUNS2000
.           
          MATCH     SP8,PTCDDTEX            * blank Extract Date field ?
          GOTO      NUNS1000 IF NOT EQUAL
.
          MATCH     TEMPADMN,DPTCDDAD       * same admission no ?
          GOTO      NUNS1000 IF EQUAL
.
          MOVE      DPTCDDAD,TEMPADMN
          MOVE      DPTCDDAD,KEY8
          CALL      WRTEMP1
          GOTO      NUNS1000
.
.
NUNS2000  MATCH     SP8,TEMPADMN
          GOTO      NUNS3000 IF NOT EQUAL
.
          DISPLAY   *P1:24,"No un-sent Cancer Registrations found - ";
          CALL      HITENTER
          MOVE      ONE,EXIT                                           *I-59999
          GOTO      NUNS9999
.
.
NUNS3000  MOVE      SP8,KEY8
          CALL      RSTEMP1
.
NUNS4000  CALL      RKTEMP1
          BRANCH    OVRCD,NUNS9000
.
NUNS5000  MOVE      TEMPADMN,DADMNO
.
          PACK      KEY8,DADMNO
          CALL      RDPTDAD1                * read transfer destination file
.
          PACK      KEY8,DADMNO,SP70
          CALL      RDPTCRG1
          BRANCH    OVRCD,NUNS4000
.
          PACK      KEY8,DADMNO,SP10
          CALL      RDMISS1                 * Get admission details
          BRANCH    OVRCD,NUNS4000
.
.....     PACK      KEY8,DADMNO
.....     CALL      RDPMVX11                * read visit extension file
.....     BRANCH    OVRCD,NUNS4000
.
          IF        IBCNMHOS=1
            MATCH     PMVXMHOS,HEADHOSP
            GOTO      NUNS4000 IF NOT EQUAL
          ENDIF
.
          PACK      KEY8,AURNO,SP10
          CALL      RDMAST1                 * Get PMI Details
          BRANCH    OVRCD,NUNS4000
.
          PACK      KEY8,AURNO,SP10
          CALL      RDPMPX21
          BRANCH    OVRCD,NUNS4000
.
          PACK      KEY8,AADMNO
          CALL      RDDSCH1                 * read discharge file
          BRANCH    OVRCD,NUNS4000
.
          UNPACK    DDATE,ACC,AYY,AMM,ADD
          PACK      THEDATE,ADD,SLASH,AMM,SLASH,ACC,AYY
.
          CALL      GCAD0000                * Extract CAD details
          CALL      GCAN0000                * Extract CAN details
          CALL      GFAN0000                * Extract FAN details
          GOTO      NUNS4000
.
NUNS9000  CALL      GHDR0000                * Write HDR file
.
NUNS9999  PRINT     *1,"*** End of Report ***"
          CALL      CLTM0000
          RETURN
+
.******************************************************************************
.*                             CRTM0000                                       *
.*                 Open and create the temporary file                         *
.******************************************************************************
CRTM0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME
.
          CALL      CLTM0000                * Close and erase tempfile
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    " 38 U1-8",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,FNAME
CRTM9999  RETURN
+
.******************************************************************************
.*                             CLTM0000                                       *
.*                 Close and erase the temporary file                         *
.******************************************************************************
CLTM0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
CLTM9999  RETURN
+
.******************************************************************************
.*                             I/O for Temporary file                         *
.******************************************************************************
RSTEMP1   RESET     KEY8
          READ      TEMPFILE,KEY8;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;TEMPADMN,TEMPSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY4
          READ      TEMPFILE,KEY8;TEMPADMN,TEMPSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMP1   MOVE      ZERO,OVRCD
          READKP    TEMPFILE;TEMPADMN,TEMPSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     TEMPFILE,KEY8;TEMPADMN,TEMPSPAR
          RETURN
.
DETEMP1   RESET     KEY8
          DELETE    TEMPFILE,KEY8
          RETURN
+
.******************************************************************************
.
.         I/O includes needed
.
          INC       STD001IO/PBL
.
          INC       CLPATMAS                * 
          INC       TFILENAM                     * Generate Temp File Name
          INC       PATHSPKY                * Keyin hospital id
          INC       IBAPOSIO/INC
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCANIO/INC            * Cancer Register Detail File
          INC       PATCRDIO/INC            * Cancer Register Detail File
          INC       PATCRGIO/INC            * Cancer Register Header File
          INC       PMSCDGIO/INC            * Cancer Reason for Clinical Diagn.
          INC       PATCODIO/INC            * patient codes file
          INC       PATDADIO/INC            * Transfer file
          INC       PATDO1IO/INC            * Patient Doctors File
          INC       PATDSCIO/INC            * Discharge File
          INC       PATECDIO/INC            * Patient episode disease file
          INC       PATGSRIO/INC            * Patient name file           
          INC       PATHSPIO/INC            * Hospital file
          INC       PATICDIO/INC            * ICD-9 File
          INC       PATMA1IO/INC            * patient master file
          INC       PATMI1IO/INC            * admission file
          INC       PATVISIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
..............................................................................

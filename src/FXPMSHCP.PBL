.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   FXPMSHCP                                                  *
.* Desc      :                                                             *
.***************************************************************************
.* Date      :   08/08/2001                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will loop the(patdo1af)doctors file and      *
.*               create records in the hcp doctors file (pmshcpaf) that    *
.*               are missing                                               *
.* Mods      :                                                             *
.*               V10.11.01 09/08/2017 Tracey Nguyen    TSK 0299206         *
.*                        Recompiled for PATDKIPR/PMSHKIPR                 *
.*               V10.05.01 05/10/2014 Jill Parkinson   CAR 306905          *
.*                        Added option 2 - copy hcp to doctor file         *
.*               V9.09.01 15/08/2007 Jill Habasque  CAR 146699             *
.*                        Added provider numbers 2,3,4 and 5               *
.*               V9.00.00 10/08/2001 Ebon Clements                         *
.*                        Created Fixit                                    *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATDO1FD/INC
          INC       PMSHCPFD/INC
          INC       PATCODFD/INC
.
. VARIABLES
. -------------------------
COUNTR    FORM      8
CURRDATE  DIM       8
.
PRGID     INIT      "FXPMSHCP"
PRGNAM    INIT      "Fixit for HCP doctors codes"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML1000  * proceed with fixit
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    IF        COPTION=1 
            CALL      PROC0000               * process doctor to hcp
          ENDIF
.
          IF        COPTION=2 
            CALL      DOCT0000               * process hcp to doctor
          ENDIF
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          OPEN      PATCODE1,"patcodes"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                              *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run HCP Fixit":
                    *P1:6,*V2LON,TWO,*HOFF,". Run Doctor Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.**************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing: ";
          MOVE      ZERO,COUNTR                  * initialise record counter
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          PACK      KEY6,SP70
          CALL      RDSDOCT1
PROC1000  CALL      RDKDOCT1
          BRANCH    OVRCD,PROC9000
.
          PACK      KEY10,DCODE,SP70          * Check if record is in HCP file
          CALL      RDPMHCP1
          COMPARE   ZERO,OVRCD        
          GOTO      PROC1000 IF EQUAL
.
          ADD       ONE,COUNTR
          CALL      CLPMSHCP
.
          PACK      PMHCHCPC,DCODE,SP70          * Doctor Code
          PACK      PMHCTITL,DTITL,SP70          * Doctor Title
          PACK      PMHCGNAM,DGNAME,SP70         * Given Name
          PACK      PMHCSNAM,DSNAME,SP70         * Surname
          MOVE      ANSU,PMHCGNDR                * Sex
          MOVE      DBIRTH,PMHCDTOB              * Date of birth
          MOVE      DRTYPE,PMHCSPC1              * Speciality
          PACK      PMHCRCPT,DRNAME,SP70         * Receptionist name
          PACK      PMHCADR1,DSADD1,SP70         * Address line 1
          PACK      PMHCADR2,DSADD2,SP70         * Address line 2
          PACK      PMHCADR3,DSADD3,SP70         * Address line 3
          PACK      PMHCADR4,DSADD4,SP70         * Address line 4
          PACK      PMHCADR5,SP70                * Address line 5
          PACK      PMHCADR6,SP70                * Address line 6
          MOVE      DSPOST,PMHCPOST              * Post code
          MOVE      PTDOSEML,PMHCSEML            * E-Mail
          MOVE      DTELES,PMHCSTEL              * Surgers Telephone
          MOVE      DTELEP,PMHCPAGR              * Page Telephone
          MOVE      DPAGER,PMHCPAGN              * Pager Number
          MOVE      DASSOC1,PMHCAHP1             * Associated HCP 1
          MOVE      DASSOC2,PMHCAHP2             * Associated HCP 2
          MOVE      DASSOC3,PMHCAHP3             * Associated HCP 3
          MOVE      DASSOC4,PMHCAHP4             * Associated HCP 4
          MOVE      DASSOC5,PMHCAHP5             * Associated HCP 5
          MOVE      DPROV,PMHCPRV1               * Provider number 1
          MOVE      PTDOPRO2,PMHCPRV2            * Provider number 2
          MOVE      PTDOPRO3,PMHCPRV3            * Provider number 3
          MOVE      PTDOPRO4,PMHCPRV4            * Provider number 4
          MOVE      PTDOPRO5,PMHCPRV5            * Provider number 5
          MOVE      DTELEH,PMHCHTEL              * Home Telephone
          PACK      PMHCHAD1,DHADD1,SP70         * Address line 1
          PACK      PMHCHAD2,DHADD2,SP70         * Address line 2
          PACK      PMHCHAD3,DHADD3,SP70         * Address line 3
          PACK      PMHCHAD4,DHADD4,SP70         * Address line 4
          PACK      PMHCHAD5,SP70                * Address line 5
          PACK      PMHCHAD6,SP70                * Address line 6
          MOVE      DHPOST,PMHCHPCD              * Post code
          MOVE      PTDOHEML,PMHCHEML            * E-Mail
          MOVE      DAFIRST,PMHCDFAC             * Date first accred
          MOVE      DALAST,PMHCDLAC              * Date last accred
          MOVE      DAYEARS,PMHCYACC             * Accred years
          MOVE      DACCRED,PMHCATYP             * Accred type
          MOVE      DHCSCOD,PMHCHHCP             * H.C.S HCP Code
          MOVE      DALEVEL,PMHCOSLV             * Service level
.
          COMPARE   ZERO,DRSTAT                   * Status
          IF        @EQUAL
            MOVE      TWO,PMHCHCST
            MOVE      ZERO,PMHCSTTS               * Active / Inactive
          ENDIF
.
          COMPARE   ONE,DRSTAT                   * Status
          IF        @EQUAL
            MOVE      TWO,PMHCHCST
            MOVE      ONE,PMHCSTTS               * Active / Inactive
          ENDIF
.
          COMPARE   TWO,DRSTAT                   * Status
          IF        @EQUAL
            MOVE      ONE,PMHCHCST
            MOVE      ZERO,PMHCSTTS               * Active / Inactive
          ENDIF
.
          PACK      PMHCFAXN,PTDOFAXN,SP70       * Fax number
          PACK      PMHCHFXN,PTDOHFAX,SP70       * Home Fax
          PACK      PMHCMOBN,PTDOMOBL,SP70       * Mobile
          MOVE      PTDOGLCC,PMHCCRDC            * Creditor code
          PACK      PMHCWAHC,PTDOWHDP,SP70       * WA HDP HCP code
          MOVE      PTDOMREG,PMHCMRBN            * Med Reg Board No
          MOVE      PTDONHSN,PMHCNHSN            * N.H.S Pros Number
          MOVE      PTDOUDF1,PMHCUDF1            * User Def Field 1
          MOVE      PTDOUDF2,PMHCUDF2            * User Def Field 2
          MOVE      PTDOUDF3,PMHCUDF3            * User Def Field 3
          MOVE      PTDOUDF4,PMHCUDF4            * User Def Field 4
          MOVE      PTDOUDF5,PMHCUDF5            * User Def Field 5
          MOVE      ZERO,PMHCUYN1                * User Def Y/N 1
          MOVE      ZERO,PMHCUYN2                * User Def Y/N 2
          MOVE      ZERO,PMHCUYN3                * User Def Y/N 3
          MOVE      ZERO,PMHCUYN4                * User Def Y/N 4
          MOVE      ZERO,PMHCUYN5                * User Def Y/N 5
          MOVE      PTDOINSC,PMHCINSC            * Insurance code
          MOVE      PTDOINSN,PMHCIPLN            * Insurance number
          MOVE      PTDOINSF,PMHCIDTF            * Insurance date from
          MOVE      PTDOINST,PMHCIDTT            * Insurance date to
          MOVE      PTDOREGN,PMHCMPGN            * MPG Cert No
          MOVE      ZERO,PMHCAPCI                * APC
          MOVE      PTDOREGF,PMHCRGDF            * Reg Date From
          MOVE      PTDOREGT,PMHCREGT            * Reg Date To
          MOVE      PTDOVISA,PMHCVISD            * Visa date
          MOVE      PTDOCOMT,PMHCPRFC            * Pref Comm
          MOVE      PTDOPREF,PMHCPRFN            * Pref Name
          MOVE      PTDOABNN,PMHCABNN            * ABN Number
          MOVE      ANSM,PMHCUPDF                * Upddate Flag
          MOVE      ZERO,PMHCPRMK                * Processor mark
          MOVE      CURRDATE,PMHCCDTE            * Date created
          MOVE      CTIMEIS,PMHCCTIM             * Time created        
.
          PACK      KEY10,DCODE,SP70
          CALL      WRPMHCP1        * New data written to HCP file
          PROC      PMSHKIUP        * Updates keyword index
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P1:24,*EL,"Fixit completed.  ",COUNTR," records updated.":
                    "  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
.**************************************************************************
.*                               DOCT0000              Called by: ML0000  *
.**************************************************************************
.
DOCT0000  DISPLAY   *P1:24,*EL,"Processing: ";
          MOVE      ZERO,COUNTR                  * initialise record counter
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          PACK      KEY10,SP70
          CALL      RSPMHCP1
DOCT1000  CALL      RKPMHCP1
          BRANCH    OVRCD,DOCT9000
.
. *** ignore GPs ****
          COMPARE   "3",PMHCHCST
          GOTO      DOCT1000 IF EQUAL
.
. *** ignore others ****
          COMPARE   "7",PMHCHCST
          GOTO      DOCT1000 IF EQUAL
.
          PACK      KEY6,PMHCHCPC,SP70   * Check if record is in doctor file
          CALL      RDDOCT1
          COMPARE   ZERO,OVRCD
          GOTO      DOCT1000 IF EQUAL
.
          ADD       ONE,COUNTR
          CALL      CLPATDOC
.
          PACK      DCODE,PMHCHCPC,SP70          * Doctor Code
          PACK      DTITL,PMHCTITL,SP70          * Doctor Title
          PACK      DGNAME,PMHCGNAM,SP70         * Given Name
          PACK      DSNAME,PMHCSNAM,SP70         * Surname
          MOVE      PMHCDTOB,DBIRTH              * Date of birth
          MOVE      PMHCSPC1,DRTYPE              * Speciality
          PACK      DRNAME,PMHCRCPT,SP70         * Receptionist name
          PACK      DSADD1,PMHCADR1,SP70         * Address line 1
          PACK      DSADD2,PMHCADR2,SP70         * Address line 2
          PACK      DSADD3,PMHCADR3,SP70         * Address line 3
          PACK      DSADD4,PMHCADR4,SP70         * Address line 4
          MOVE      DSPOST,PMHCPOST              * Post code
          PACK      PTDOSEML,PMHCSEML            * E-Mail
          PACK      DTELES,PMHCSTEL              * Surgers Telephone
          PACK      DTELEP,PMHCPAGR              * Page Telephone
          PACK      DPAGER,PMHCPAGN              * Pager Number
          PACK      DASSOC1,PMHCAHP1             * Associated HCP 1
          PACK      DASSOC2,PMHCAHP2             * Associated HCP 2
          PACK      DASSOC3,PMHCAHP3             * Associated HCP 3
          PACK      DASSOC4,PMHCAHP4             * Associated HCP 4
          PACK      DASSOC5,PMHCAHP5             * Associated HCP 5
          PACK      DPROV,PMHCPRV1               * Provider number 1
          PACK      PTDOPRO2,PMHCPRV2            * Provider number 2
          PACK      PTDOPRO3,PMHCPRV3            * Provider number 3
          PACK      PTDOPRO4,PMHCPRV4            * Provider number 4
          PACK      PTDOPRO5,PMHCPRV5            * Provider number 5
          PACK      DTELEH,PMHCHTEL              * Home Telephone
          PACK      DHADD1,PMHCHAD1,SP70         * Address line 1
          PACK      DHADD2,PMHCHAD2,SP70         * Address line 2
          PACK      DHADD3,PMHCHAD3,SP70         * Address line 3
          PACK      DHADD4,PMHCHAD4,SP70         * Address line 4
          PACK      DHPOST,PMHCHPCD              * Post code
          PACK      PTDOHEML,PMHCHEML            * E-Mail
          PACK      DAFIRST,PMHCDFAC             * Date first accred
          PACK      DALAST,PMHCDLAC              * Date last accred
          MOVE      PMHCYACC,DAYEARS             * Accred years
          PACK      DACCRED,PMHCATYP             * Accred type
          MOVE      PMHCHHCP,DHCSCOD             * H.C.S HCP Code
          PACK      DALEVEL,PMHCOSLV             * Service level
.
          MOVE      PMHCSTTS,DRSTAT               * Active / Inactive
          COMPARE   "1",DRSTAT
          GOTO      DOCT2000 IF EQUAL      * leave as inactive
.
          COMPARE   "2",PMHCHCST
          IF        @EQUAL
            MOVE      "2",DRSTAT
          ENDIF
.
          COMPARE   "3",PMHCHCST
          IF        @EQUAL
            MOVE      "2",DRSTAT
          ENDIF
.
          COMPARE   "12",PMHCHCST
          IF        @EQUAL
            MOVE      "2",DRSTAT
          ENDIF
.
DOCT2000  PACK      PTDOFAXN,PMHCFAXN,SP70       * Fax number
          PACK      PTDOHFAX,PMHCHFXN,SP70       * Home Fax
          PACK      PTDOMOBL,PMHCMOBN,SP70       * Mobile
          PACK      PTDOGLCC,PMHCCRDC            * Creditor code
          PACK      PTDOWHDP,PMHCWAHC,SP70       * WA HDP HCP code
          PACK      PTDOMREG,PMHCMRBN            * Med Reg Board No
          PACK      PTDONHSN,PMHCNHSN            * N.H.S Pros Number
          PACK      PTDOUDF1,PMHCUDF1            * User Def Field 1
          PACK      PTDOUDF2,PMHCUDF2            * User Def Field 2
          PACK      PTDOUDF3,PMHCUDF3            * User Def Field 3
          PACK      PTDOUDF4,PMHCUDF4            * User Def Field 4
          PACK      PTDOUDF5,PMHCUDF5            * User Def Field 5
          PACK      PTDOINSC,PMHCINSC            * Insurance code
          PACK      PTDOINSN,PMHCIPLN            * Insurance number
          PACK      PTDOINSF,PMHCIDTF            * Insurance date from
          PACK      PTDOINST,PMHCIDTT            * Insurance date to
          PACK      PTDOREGN,PMHCMPGN            * MPG Cert No
          PACK      PTDOREGF,PMHCRGDF            * Reg Date From
          PACK      PTDOREGT,PMHCREGT            * Reg Date To
          PACK      PTDOVISA,PMHCVISD            * Visa date
          PACK      PTDOCOMT,PMHCPRFC            * Pref Comm
          PACK      PTDOPREF,PMHCPRFN            * Pref Name
          PACK      PTDOABNN,PMHCABNN            * ABN Number
.
          PACK      KEY6,DCODE,SP70
          CALL      RDADOCT1
          IF        OVRCD=1
            CALL      WRDOCT1         * New data written to Doctor file
          ENDIF
          PROC      PATDKIUP        * Updates keyword index
          GOTO      DOCT1000
.
DOCT9000  DISPLAY   *P1:24,*EL,"Fixit completed.  ",COUNTR," records updated.":
                    "  ";
          CALL      HITENTER
.
DOCT9999  RETURN
+

. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PMSHKIPR
          INC       PATDKIPR
          INC       CLPMSHCP
          INC       CLPATDOC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PMSHCPIO/INC

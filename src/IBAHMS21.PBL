.******************************************************************************
.* System         : PATIENT MANAGEMENT                                        *
.* Program        : IBAHMS21                                                  *
.* Desc           : Hospital Occupancy Report                                 *
.******************************************************************************
.* Date           : 15/11/1991                                                *
.* Author         : Justin Coates (Adapted from IBAPMS21)                     *
.* Modifications  :                                                           *
.*                                                                            *
.*        ********  Programmers Note:  When making changes in this program,   *
.*                  check the following programs to see if the change is also *
.*                  required there:  IBAHOS82, IBAPMS21, IBAPMS22.            *
.*                                                                            *
.*        V12.00.01 22/05/2025  Alina Bhari   TSK 0955096                     *
.*                  Added alpanumeric visit number generation -PTRN1000       *
.*        V11.01.01 13/04/2021  J.Tan           TSK 0863287                   *
.*                  Recompiled for STEPDOWN -Patient Invoice new functionality*
.*        V11.00.01 10/02/2021  J.Tan          TSK 0896392                    *
.*                  Fixed no of adm.for Daycase with a Change record on Adm   .
.*        V10.15.01 26/11/2019  Peter Vela     TSK 0878592                    *
.*                  Added validation for Change record if TOTTDAY = 0 in      *
.*                  CALC00                                                    *
.*        V10.13.01 05/12/2018  Don Nguyen     TSK 0838558                    *
.*                  Deleted temp file (PSTROL) after processing               *
.*        V10.10.01 13/06/2017  J.Tan          TSK 0839991                    *
.*                  Recompiled for PATDBTYP (PATCMSTP)                        *
.*        V10.09.01 17/02/2017  Ania P         CAR 261630                     *
.*                  Removed use of PORT                                       *
.*        V10.08.01 19/07/2016  J.Tan          TSK 0821393                    *
.*                  Fixed No of Admission for Daycase patient went on leave   *
.*        V10.04.01 27/05/2014  J.Tan          CAR 300784                     *
.*                  Recompiled for STEPDOWN - force stepdown prior inv.fromdat*
.*        V10.03.04 28/06/2013  J.Tan          CAR 287554                     *
.*                  Changed FROMDATE to KFRDATE                               *
.*        V10.03.03 06/05/2013  J.Tan          CAR 284902                     *
.*                  Recompiled for STEPDOWN - force stepdown prior Inv.Fromdat*
.*        V10.03.02 17/10/2012  Jill Parkinson CAR 273375                     *
.*                  Recompiled for DATECONV - first day of year calculation   *
.*        V10.03.01 19/03/2012  Peter McMullen CAR  259587                    *
.*                  remove all references to redundant file payears           *
.*        V10.02.02 27/04/2011  Saroeun Soeur CAR 241405                      *
.*                  fixed non multi hospital                                  *
.*        V10.02.01 15/04/2011 Sareun Soeur   CAR 241405                      *
.*                  added hospital filter for multi hospital                  *
.*        V10.01.01 23/02/2011  Peter Vela    CAR 233034                      *
.*                  Removed PATBF2FD and PATBF2IO                             *
.*        V10.00.01 14/07/2010  J.Tan         CAR 226158                      *
.*                  Recompiled for STEPDOWN - bed fees update                 *
.*        V9.12.03  11/03/2010  J.Tan         CAR 216044                      *
.*                  Mods to include Leave days in stepdown count for TAC      *
.*        V9.12.02  19/01/2010  J.Tan         CAR 166439                      *
.*                  Recompiled for PATCMSTP - C/P additional charge stepdown  *
.*        V9.12.01  21/09/2009  J.Tan         CAR 204390                      *
.*                  Recompiled for STEPDOWN - mods to old bed fees file       *
.*        V9.10.01  18/06/2008  Mike Laming      CAR 168125                   *
.*                  Correct alignment of headings in report at HEAD7100       *
.*                  Change to use V6.16 "On Leave" check at CALC00            *
.*        V9.09.02  25/10/2007  Peter Vela CAR 142394                         *
.*                  Fixed addition of bed days if todate = discharge date     *
.*        V9.09.01  17/08/2007  Peter Vela CAR 139461                         *
.*                  Fixed addition of bed days after admission tran record    *
.*                  @ PTRN5000                                                *
.*        V9.08.01  02/02/2007  Ebon Clements CAR 120001                      *
.*                  Added nz private billing files                            *
.*        V9.07.01  27/07/2006  Peter Vela    CAR 112261                      *
.*                  Modified to date in PTRN7000                              *
.*                  Added TMOVE=D check PTRN5000                              *
.*        V9.04.04  31/12/2004  J.Tan         CAR 56034                       *
.*                  Fixed looping when no report generated                    *
.*        V9.04.03  29/11/2004  Mike Laming   CAR 54534                       *
.*                  Re-comple for PATCMSTP - fix Addit.Charge Description     *
.*        V9.04.02  27/10/2004  Lina Vo       CAR 54095                       *
.*                  Fixed report to display all wards even ones that are      *
.*                  invalid.  This is so the report bed days and amount       *
.*                  matches.                                                  *
.*                  Fixed bed days and amounts to be similar to IBAPMS20       *
.*        V9.04.01  22/10/2004  Lina Vo       CAR 54146                       *
.*                  Fixed looping in PTRN0000 after STEPDOWN                  *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Code "R" - "Intersex"                           *
.*        V9.02.07  01/12/2002  J.Tan                                         *
.*                  Mods checking for todate with adate                       *
.*        V9.02.06  30/10/2002  J.Tan                                         *
.*                  Recompiled for PATCMSTP                                   *
.*        V9.02.05  05/06/2002  J.Tan  defect 18011                           *
.*                  Mods not to use for hospital type for stepdown            *
.*        V9.02.03  06/06/2002  J.Tan                                         *
.*                  Removed checking for hospital types                       *
.*        V9.02.04  07/05/2002  J.Tan                                         *
.*                  Recompiled for release 6.09 changes                       *
.*        V9.02.03  06/12/2001  Tonii                                         *
.*                  Removed episode type for casemix payment                  *
.*        V9.02.02  07/11/2001  Dean Cramer                                   *
.*                  Change "ZZZ" start load to "~~~" due to infinite loop     *
.*        V9.02.01  22/09/2001  Tonii  SRF 13075                              *
.*                  Recompiled for PATDINVS                                   *
.******************************************************************************
.*       V5.08.03  11/09/2000  Tonii                                          *
.*                 - Mods to accept Unknown and Indeterminate as valid        *
.*                   patient sex description                                  *
.*                 - Mods to print 2 extra columns for the above changes when *
.*                   running Hospital occupancy report                        *
.*        V5.08.02  16/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*       V5.08.B01  04/04/2000  Steve Armstrong   srf 638181 & 638413         *
.*                  Mods to save TODATE prior to calling PATCMSTP, then       *
.*                  restore after call returns.                               *
.******************************************************************************
.*       V5.07.06   23/11/1999  Davin                                         *
.*                  Changed for 4 character drgs                              *
.*       V5.07.05   04/08/1999  Steve Downing                                 *
.*                  Recompiled for PATCMSTP                                   *
.*       V5.07.04   27/07/1999  Steve Downing                                 *
.*                  Simplified CALC00 & CDAY0000 routines, century included   *
.*                  in to-date calculations                                   *
.*       V5.07.03   22/06/1999  J.Tan                                         *
.*                  Fixed the Total admissions                                *
.*       V5.07.02   10/05/1999  J.Tan                                         *
.*                  Fixed the report layout                                   *
.*       V5.07.01   12/03/1999  J.Tan                                         *
.*                  Mods for casemix funding                                  *
.*       V5.07.B03  25/01/1999  Nicole Harrington 507 rebounds                *
.*                  Mods to print century on date ranges                      *
.*       V5.07.B02  13/01/1999  Jim Stathopoulos                              *
.*                  Fixed Global Compile Errors                               *
.*       V5.07.B01  16/11/98 Jim Stathopoulos                                 *
.*                  507 Changes                                               *
.******************************************************************************
.*        V6.03.10  23/05/96 J.Tan     SRF 615301                             *
.*                  Recompiled for STEPDOWN -problem with new date in the past*
.*        V6.03.09  15/05/96 J.Tan     SRF 615270                             *
.*                  Fixed to read master record for daycases                  *
.*        V6.03.08  10/05/96 J.Tan     SRF 615219                             *
.*                  Mods to set starting date of next invoice for stepdown    *
.*        V6.03.07  21/12/1995  Steve Armstrong   SRF 612667                  *
.*                  Mods to use WDACTVDY to calculate active days for a ward  *
.*                  instead of using CHAC0000 (to balance with INP82/HOS82).  *
.*                  Fixed bugs caused by "TODATE" being set to day after      *
.*                  required end date.                                        *
.*                  Removed save of ward variable prior to call to STEPDOWN   *
.*                  and also removed reload of ward after STEPDOWN.           *
.*                  Mods to use SAVWWARD instead of SAVWARD which is altered  *
.*                  by STEPDOWN.                                              *
.*                  Added read on CDLSTEP.                                    *
.*                  Fixed printing of Ward instead of Room Type in Ward       *
.*                  Summary at end of report.                                 *
.*                  Added use of PTCNH82W.                                    *
.*        V6.03.06  09/11/1995  Whirlpool       SRF 612207                    *
.*                  Fixeds problem with tward being reset by stepdown         *
.*        V6.03.05  27/10/1995  Greg Horvat      SRF 612064 612205 612206     *
.*                  Recompiled for changes to STEPDOWN                        *
.*        V6.03.04  13/10/1995  Greg Horvat      SRF 611811 (Rebound)         *
.*                  Recompiled for PATDINVS                                   *
.*        V6.03.03  11/10/1995  Greg Horvat      SRF 611811                   *
.*                  Changed to use CALCDAYS instead of CALC when calculating  *
.*                  the difference between two dates                          *
.*        V6.03.02  04/09/1995  Greg Horvat      SRF 611419                   *
.*                  Recompiled for PATDINVS                                   *
.*        V6.03.01  31/03/1995  Greg Horvat      SRF 135807                   *
.*                  Changed the variable used for saving the bed. It          *
.*                  conflicted with the STEPDOWN routine, which was also using*
.*                  the SAVBED variable for a different purpose. This affected*
.*                  the check for inactive beds.                              *
.*        V6.02.01  13/10/1994  Jeni Tan         SRF 132145                   *
.*                  Recompiled for STEPDOWN                                   *
.*        V6.01.03  15/06/1994  Greg Horvat      V6.02 Upgrade (Rebound)      *
.*                  Changed to allow automatic stepdowns in the report        *
.*        V6.01.02  30/05/1994  Greg Horvat      V6.02 Upgrade                *
.*                  Changed to allow automatic stepdowns in the report        *
.*                  V5.02   13/04/92    J.Tan          SRF 112447             *
.*                          Fixed number of admissions                        *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       NZPBFEFD/INC
          INC       PATAFEFD/INC
          INC       PATASDFD/INC
          INC       PATHDFFD/INC
          INC       PATHLFFD/INC
          INC       PATLDFFD/INC
          INC       PATLSDFD/INC
          INC       PATFN1FD/INC
          INC       PATFX1FD/INC
.
          INC       PATBFEFD/INC
          INC       PATCODFD/INC
          INC       PATCFAFD/INC
          INC       PATDAYFD/INC
          INC       PATDINVS/INC
          INC       PATDSCFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC                                       *I-49016
          INC       PATMI1FD/INC
          INC       PATRCAUD/INC
          INC       PATTRNFD/INC
          INC       PATWBHFD/INC
          INC       PATWR1FD/INC
          INC       PMSCIDFD/INC
          INC       PMSVX1FD/INC
          INC       WDACTVVR/INC
          INC       WEBSECFD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.         Temporary Indexed files
.
PATFNAME  DIM       8
PATMP1XX  IFILE     SQL, FIXED=9, KEY="u1-8"
PATOCCP1  IFILE     SQL, FIXED=145, KEY="U1-9"              * was 102  *C-49016
PATOCCP2  IFILE     SQL, FIXED=145, KEY="U1-9"              * was 102  *C-49016
.                     (*** THE ISI IS ACTUALLY PATOCCP1.ISI ***)
.                     (*** PATOCCP2.ISI DOES NOT EXIST      ***)
.
. NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----     ----   ------   --------  ----------   -----------
OCWARD    DIM       3           3         1      OCCUPANCY WARD NUMBER
OCROOM    DIM       3           3         4      OCCUPANCY ROOM TYPE
.                                                 "ZZZ" = TOTAL ALL WARD
OCTYPE    DIM       3           3         7      OCCUPANCY ADMISSION TYPE
.                                                 "ZZZ" = TOTAL ALL BED TYPES
.
OCBEDS    FORM      7           5        10      OCCUPANCY No BEDS ROOM TYPES
.
OCDAYM    FORM      7           5        15      OCCUPANCY BED DAYS - MALE
OCDAYF    FORM      7           5        20      OCCUPANCY BED DAYS - FEMALE
OCDAYA    FORM      7           5        25      OCCUPANCY BED DAYS - ADULT
OCDAYC    FORM      7           5        30      OCCUPANCY BED DAYS - CHILD
OCDAYD    FORM      7           5        35      OCCUPANCY BED DAYS - DAY CASES
OCDAYT    FORM      7           5        40      OCCUPANCY BED DAYS - TOTAL
.
. USED BY TABBING ON READ PATOCCP2     PRETOTAL = OCDAYT
.
PRETOTAL  FORM      7    TAB POS   40  *** TOTAL BED DAYS FOR PAT MIX CALC
.
OCAMTM    FORM     12.2         8        45      OCCUPANCY AMOUNT - MALE
OCAMTF    FORM     12.2         8        53      OCCUPANCY AMOUNT - FEMALE
OCAMTA    FORM     12.2         8        61      OCCUPANCY AMOUNT - ADULT
OCAMTC    FORM     12.2         8        69      OCCUPANCY AMOUNT - CHILD
OCAMTD    FORM     12.2         8        77      OCCUPANCY AMOUNT - DAY CASES
OCAMTT    FORM     12.2         8        85      OCCUPANCY AMOUNT - TOTAL
.
ADMADLT   FORM      5           4        92      ADMISSIONS AMOUNT - ADULT
ADMCHIL   FORM      5           4        96      ADMISSIONS AMOUNT - CHILD
ADMTOTL   FORM      8           5       100      ADMISSIONS AMOUNT - TOTAL
OCCHNG    FORM      1           2       105      OCCUPANCY NO. BEDS CHANGE FLAG
.
OCDAYI    FORM      7           5       107      OCCUPANCY BED DAYS - INDETER
OCDAYR    FORM      7           5       112      OCCUPANCY BED DAYS - INTERSEX
OCDAYU    FORM      7           5       117      OCCUPANCY BED DAYS - OTHER  
.
OCAMTI    FORM      9.2         7       124      OCCUPANCY AMOUNT - INDETER
OCAMTR    FORM      9.2         7       132      OCCUPANCY AMOUNT - INTERSEX
OCAMTU    FORM      9.2         7       139      OCCUPANCY AMOUNT - OTHER  
.End of Record                          145
+
.
.         VARIABLES DEFINITION
.
ADMFLG    FORM      1
ADMADD    FORM      3
AMTCODE   FORM     12.2
ASDATE    DIM       8
.
BEDCODE   FORM      5
BEDOCCP   FORM      3.2
BOAMT     FORM      7.2
BOBED     FORM      5
.
CAAMT     FORM      6.2
CADAYS    FORM      4
CALDAYS   FORM      4
CHG       FORM      1
CHNGFLAG  FORM      1
CMDLINE   DIM       50
CODE      DIM       2
CUDATE    DIM       8
.
DAYANLA   FORM      3
DAYANLC   FORM      3
DAYANLF   FORM      3
DAYANLM   FORM      3
DAYANLI   FORM      3
DAYANLU   FORM      3
DAYANLT   FORM      3
DAYPERD   FORM      3.2
DAYPERI   FORM      3.2
DAYWRK1   FORM      9.4             * working variables
DAYWRK2   FORM      9.4
DDROOM    DIM       23
DDTYPE    DIM       16
DIM4      DIM       4
DIM18     DIM       18
DIM18A    DIM       18
DIM30     DIM       30
DISCFLG   FORM      1
DNAME     DIM       30
.
EFTDATE   DIM       8         * Effective date (ccyymmdd)
FEAMT     FORM     12.2
FEDAYS    FORM      4
FNAMEI    DIM       8
FNAMER    DIM       8
FNAMEU    DIM       8
FORM3A    FORM      3
FRWARD    DIM       3
FRWRD     DIM       6
FWRDDESC  DIM       20
.
GATOTAL   FORM     12.2
GBTOTAL   FORM      5
.
HEADFLG   FORM      1             * Header flag
.                                     0 = Normal Header
.                                     1 = Ward Summary Header
.
INDAY     FORM      2
INMON     FORM      2
INYEAR    FORM      2
INCENT    FORM      2
.
JDAYS     FORM      3
JYEAR     FORM      2
.
KFRDATE   DIM       8
KTTDATE   DIM       8
LSTDATE   DIM       8
LYR       FORM      1
.
MAAMT     FORM     12.2
MADAYS    FORM      4
.
OPNFLAG   FORM      "0"
.
PATMIXP   FORM      3.2
PSTROL    FILE      ASCII, FIXED=256
PTCNH82W  FORM      1
PTCNDCLM  DIM       3
PTCNIDES  FORM      1
PTCNADES  FORM      1
PTCNXCHS  DIM       1
PTCNPPIN  DIM       1
.
QUESTFLG  FORM      1
.
ROOMFLAG  FORM      1              * 1=Room Type (Room) heading has been printed
RYEAR     DIM       4
.
SAVALLW   FORM     12.2
SAVAMT    FORM     12.2
SAVBEND   FORM      3
SAVDISC   FORM     12.2
SAVEDATE  DIM       8
SAVROOM   DIM       3
SAVTYPE   DIM       3
SAVWWARD  DIM       3
SAVWBED   DIM       3
SDAYMON   DIM       4
SRBDAY    DIM       4
SRYEAR    FORM      4
STATUS    FORM      1
SUMFLG    DIM       1
SYEAR     DIM       4
SVOCAMTM  FORM      9.2         * Resize OCCUPANCY AMOUNT - MALE
SVOCAMTF  FORM      9.2         * Resize OCCUPANCY AMOUNT - FEMALE
SVOCAMTA  FORM      9.2         * Resize OCCUPANCY AMOUNT - ADULT
SVOCAMTC  FORM      9.2         * Resize OCCUPANCY AMOUNT - CHILD
SVOCAMTD  FORM      9.2         * Resize OCCUPANCY AMOUNT - DAY CASES
.
TDAYMON   DIM       4
TMPADMN   DIM       8
TOCBEDS   FORM      7               * Room Type total number of beds
TOTTDAY   FORM      8
TOWARD    DIM       3
TOWRD     DIM       6
TTOTAL    FORM     12.2
TWRDDESC  DIM       20
.
TADMADLT  FORM      5               * Admissions Amount - ADULT
TADMCHIL  FORM      5               * Admissions Amount - CHILD
TADMTOTL  FORM      8               * Admissions Amount - TOTAL
.
TOCAMTM   FORM     12.2             * Room Type Total Amount - MALE
TOCAMTF   FORM     12.2             * Room Type Total Amount - FEMALE
TOCAMTI   FORM     12.2             * Room Type Total Amount - INDETERMINATE
TOCAMTR   FORM     12.2             * Room Type Total Amount - INTERSEX
TOCAMTU   FORM     12.2             * Room Type Total Amount - OTHER
TOCAMTA   FORM     12.2             * Room Type Total Amount - ADULT
TOCAMTC   FORM     12.2             * Room Type Total Amount - CHILD
TOCAMTD   FORM     12.2             * Room Type Total Amount - DAY CASES
TOCAMTT   FORM     12.2             * Room Type Total Amount - TOTAL
.
TOCDAYM   FORM      7               * Room Type Total Bed Days - MALE
TOCDAYF   FORM      7               * Room Type Total Bed Days - FEMALE
TOCDAYI   FORM      7               * Room Type Total Bed Days - INDETERMINATE
TOCDAYR   FORM      7               * Room Type Total Bed Days - INTERSEX
TOCDAYU   FORM      7               * Room Type Total Bed Days - OTHER
TOCDAYA   FORM      7               * Room Type Total Bed Days - ADULT
TOCDAYC   FORM      7               * Room Type Total Bed Days - CHILD
TOCDAYD   FORM      7               * Room Type Total Bed Days - DAY CASES
TOCDAYT   FORM      7               * Room Type Total Bed Days - TOTAL
.
UNKNWARD  FORM      1
.
WARDFLG   FORM      1
WDATE     DIM       8
WDATE1    DIM       8
WDATE2    DIM       8
WSTAT     FORM      1
WTOTAL    FORM     12.2
.
XDATE1    DIM       8
XDAY2     DIM       2
XMON2     DIM       2
XYEAR2    DIM       2
XCENT2    DIM       2
.
.         SYS. PARA.
.
CAGECOFF  FORM      2
CAUDQ     FORM      1
CDLSTEP   FORM      1
CHOSTYP   FORM      1
CSTDOWN   FORM      1
PTCNCNDY  FORM      1
PTRESDAY  FORM      1
PTCNCLEV  DIM       3
.
DIM19A    DIM       19
SAVEXTRA  FORM     12.2
PTCNBCUT  DIM       4
CFEETYP   FORM      1

.
.         CONSTANTS DEFINITION
.
CODEA     INIT      "A "
CODEBT    INIT      "BT"
CODEC     INIT      "C "
CODECC    INIT      "CC"
CODECL    INIT      "CL"
CODEDC    INIT      "DC"
CODEM     INIT      "M "
CODEP     INIT      "P "
CODER     INIT      "R "
CODERT    INIT      "RT"
CODES     INIT      "S "
CODET     INIT      "T "
CODEUNK   INIT      "UNK"
.
FINISH    INIT      "Finish"                   * added for V5.53 - SRF 105741
FIVE9     FORM      "59"
GRSUM     FORM      "0"
HPCENT    FORM      "100.00"
INVLWARD  INIT      "Invalid Ward "
INVLTYPE  INIT      "Invalid Code "
LYEAR     FORM      "366"
.
PCENT     INIT      "%"
START     INIT      "Start"                    * added for V5.53 - SRF 105741
SP14      INIT      "              "
ZEIGHT    INIT      "ZZZZZZZZ"
ZZZ       INIT      "~~~"
HOSNAME   DIM       50
HOSCODE   DIM       3
.
PRGID     INIT      "IBAHMS21"
PRGNAM    INIT      "Hospital Occupancy Report"
VERSION   INIT      "V12.01.00"
+
.*********************************************************************
.                   ML0000
.         Start of Program
.*********************************************************************
ML0000    CALL      INIT0000                * initialization
.
ML1000    CALL      OPTN0000                * get the option
          BRANCH    EXIT,ML9999             * exit selected
.
ML2000    CALL      SCRA0000                * display screen
          MOVE      ONE,WARDFLG             * initialize ward range flag
          MOVE      ZERO,HEADFLG
.
ML3000    CALL      DATE0000                * enter date range
          BRANCH    EXIT,ML1000             * nothing entered
.
ML4000    CALL      GSUM0000                * enter grand summary only
          CALL      CDAY0000                * get number of days
.
ML4050    CALL      KHOS0000
          BRANCH    EXIT,ML4000
.
          CALL      CONTQST                 * OK to continue
          BRANCH    CEXIT,ML5000,ML2000,ML1000
.                         Yes    No     Cancel
.
.         start processing
.
ML5000    CALL      INTA0000                * initialize working vars
          CALL      INTB0000                * initialize occupancy file
          BRANCH    EXIT,ML1000             * no occupancy file
.
          CALL      CTPA0000                * create temp file
          CALL      DAYA0000                * load admissions from patdayaf
.
          CALL      EXTA0000                * loop over all current admissions
          CALL      CBED0000                * calculate the number of beds
.
          COMPARE   TWO,MOPTION             * just print summary ?
          GOTO      REPORT IF NOT EQUAL     * no
.
PSUM      CALL      WSUM0000                * print summary
.
ENDP      IF        RECFLAG=0
            PRINT     *N,"***  No Report Generated ***",*N
          ELSE
            PRINT     *N,"***  End of Report  ***",*N
          ENDIF
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **";
          CLOSE     PATOCCP1
          CLOSE     PATOCCP2
          CALL      DTPB0000
          CALL      DTPA0000
          GOTO      ML2000
.
ML9999    CHAIN     PGM
+
.------------------------------------------------------------------------------
.         KHOS0000
.         Enter The Hospital
.------------------------------------------------------------------------------
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:21,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TWENTY1,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:21,*V2LON,PTHSNAME;
          PACK      HOSNAME,PTHSNAME,SP70
          PACK      HOSCODE,PTHSHOSP,SP70
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
.
KHOS9999  RETURN
+
.*********************************************************************
.         CALCULATE DATES DIFFERENCE
.         Para's  : WDATE2        start date
.                   WDATE1        end   date
.*********************************************************************
CALC00    DAYSEP    WDATE2,WDATE1,TOTTDAY
.
.         READ PATIENT MASTER FILE
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          COMPARE   ONE,OVRCD
          RETURN    IF EQUAL
.
          SUB       STALLW,SAVAMT
          SUB       STDISC,SAVAMT
          MULT      TOTTDAY,SAVAMT
.
.                                       * removed and replaced by v6.16 code
....      COMPARE   ZERO,TOTTDAY
....      RETURN    IF EQUAL
....      GOTO      NEXTDX IF NOT LESS
....
....      DISPLAY   *P1:23,*EL,"*** Transfer Record Missing for ":
....                "Admission : ",*V2LON,AADMNO,*HOFF," Contact I.B.A. ***";
.
.                                       * code taken from V6.16       *C-168125
          IF        TOTTDAY = 0
            COMPARE   ZERO,ADMADD
            GOTO      ENDOC99 IF EQUAL
            CMATCH    "L",TMOVE
            GOTO      CALC10 IF EQUAL
            CMATCH    "C",TMOVE
            GOTO      CALC10 IF EQUAL
            GOTO      ENDOC99
.
CALC10      MATCH     TDATE,ADATE
            GOTO      ENDOC99 IF NOT EQUAL
            MOVE      ZERO,SAVAMT
            GOTO      NEXTDX
          ELSE
            IF        TOTTDAY < 0
              DISPLAY   *P1:23,*EL,"*** Transfer Record Missing for ":
                       "Admission : ",*V2LON,AADMNO,*HOFF," Contact I.B.A. ***";
            ENDIF
          ENDIF
.
NEXTDX    BRANCH    WARDFLG,NEXTDX0
          MATCH     FRWARD,OCWARD               * Ignore writing to temp file
          RETURN    IF LESS                       if ward is not in Ward range
          MATCH     OCWARD,TOWARD
          RETURN    IF LESS
.
NEXTDX0   BRANCH    UNKNWARD,NEXTDX2
          MOVE      OCWARD,SAVWWARD
          PACK      KEY6,SAVWWARD,SAVWBED
          CALL      RDWARD1
          BRANCH    OVRCD,NEXTDX2
.
          MOVE      ONE,ACTVWFLG                * don't get weekend days
          PROC      WDACTVDY                    * check if ward was active
.
          COMPARE   ZERO,ACTVDAYS               * ward/bed active in period ?
          RETURN    IF EQUAL                    * no - ignore
.
.       --- --- ---   normal bed record
.
.       --- --- ZZZ   Total Bed record
.       --- ZZZ ---   Grand total Ward rec for each Type
.       --- ZZZ ZZZ   Grand total for the complete Ward
.
.       GRAND SUMMARY ON LAST PAGE IS AS ABOVE BUT WARD = ZZZ (ALL WARDS)
.
.       ZZZ --- ---   Total Line record
.       ZZZ --- ZZZ   Total Bed record
.       ZZZ ZZZ ---   Grand total Ward rec for each Type
.       ZZZ ZZZ ZZZ   Grand total for the complete Ward
.
NEXTDX2   PACK      KEY9,OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          CALL      AOCP0000 IF NOT EQUAL
.
          MOVE      ZERO,TOCBEDS
.
          MOVE      ZERO,TOCDAYM            * clear day counters
          MOVE      ZERO,TOCDAYF
          MOVE      ZERO,TOCDAYI
          MOVE      ZERO,TOCDAYR                                       *I-49016
          MOVE      ZERO,TOCDAYU
          MOVE      ZERO,TOCDAYA
          MOVE      ZERO,TOCDAYC
          MOVE      ZERO,TOCDAYD
          MOVE      ZERO,TOCDAYT
.
          MOVE      ZERO,TOCAMTM            * clear amount counters
          MOVE      ZERO,TOCAMTF
          MOVE      ZERO,TOCAMTI
          MOVE      ZERO,TOCAMTR                                       *I-49016
          MOVE      ZERO,TOCAMTU
          MOVE      ZERO,TOCAMTA
          MOVE      ZERO,TOCAMTC
          MOVE      ZERO,TOCAMTD
          MOVE      ZERO,TOCAMTT
.
          MOVE      ZERO,TADMADLT           * clear admissions counters
          MOVE      ZERO,TADMCHIL
          MOVE      ZERO,TADMTOTL
.
. ******* see if a day case *******
.
          MOVE      ONE,ADMFLG
          MATCH     "D",STMOVE
          GOTO      NODAYCC IF NOT EQUAL    * not a discharge record
.
          MATCH     ADATE,DDATE
          GOTO      NODAYCC IF NOT EQUAL    * not a day case patient
.
.         DAY CASES NOT INCLUDED IN OTHER STATS
.
          MOVE      ADATE,ASDATE
          CALL      PATAGED                 * calculate age using admission date
.
          COMPARE   CAGECOFF,PSAGE
          GOTO      DYCHILD IF LESS
.
.         have an adult daycase
.
          MOVE      ONE,TOCDAYD
          ADD       ADMADD,TADMADLT          * add day cases to adult admission
          ADD       SAVAMT,TOCAMTD
          GOTO      ENDOCCP
.
.         have a child daycase
.
DYCHILD   MOVE      ONE,TOCDAYD
          ADD       ADMADD,TADMCHIL          * add day cases to child admission
          ADD       SAVAMT,TOCAMTD
          GOTO      ENDOCCP
.
. ******* see if male or female *******
.
NODAYCC   CMATCH    "M",PSEX
          GOTO      MALOCC IF EQUAL
.
          CMATCH    "F",PSEX
          GOTO      FELOCC IF EQUAL
.
..          CMATCH    "I",PSEX
..          GOTO      INLOCC IF EQUAL
.
..          CMATCH    "R",PSEX                * "Intersex"             *I-49016
..          GOTO      RALOCC IF EQUAL                                  *I-49016
.
          ADD       TOTTDAY,TOCDAYU
          ADD       SAVAMT,TOCAMTU
          GOTO      OCAGE
.
.         have a male
.
MALOCC    ADD       TOTTDAY,TOCDAYM
          ADD       SAVAMT,TOCAMTM
          GOTO      OCAGE
.
.         have a female
.
FELOCC    ADD       TOTTDAY,TOCDAYF          * amount of days
          ADD       SAVAMT,TOCAMTF
          GOTO      OCAGE
.
.         patient sex is indeterminate
.
..INLOCC    ADD       TOTTDAY,TOCDAYI          * amount of days
..          ADD       SAVAMT,TOCAMTI
.
.         patient sex is intersex
.
..RALOCC    ADD       TOTTDAY,TOCDAYR          * amount of days        *I-49016
..          ADD       SAVAMT,TOCAMTR                                   *I-49016
.
.
. ******* see if child or adult *******
.
OCAGE     MOVE      ADATE,ASDATE
          CALL      PATAGED                 * calculate age using admission date
.
          COMPARE   CAGECOFF,PSAGE
          GOTO      CHILD IF LESS
.
.         have an adult
.
          ADD       TOTTDAY,TOCDAYA
          ADD       SAVAMT,TOCAMTA
          ADD       ADMADD,TADMADLT         * set adult counter
          GOTO      ENDOCCP
.
.         have a child
.
CHILD     ADD       TOTTDAY,TOCDAYC
          ADD       SAVAMT,TOCAMTC
          ADD       ADMADD,TADMCHIL         * set child counter
.
. ******* set the total variables *******
.
ENDOCCP   MOVE      TOCDAYM,TOCDAYT
          ADD       TOCDAYF,TOCDAYT
..          ADD       TOCDAYI,TOCDAYT         * bed days total
..          ADD       TOCDAYR,TOCDAYT                                  *I-49016
          ADD       TOCDAYU,TOCDAYT
          ADD       TOCDAYD,TOCDAYT
.
          MOVE      TOCAMTM,TOCAMTT
          ADD       TOCAMTF,TOCAMTT
..          ADD       TOCAMTI,TOCAMTT
..          ADD       TOCAMTR,TOCAMTT                                  *I-49016
          ADD       TOCAMTU,TOCAMTT
          ADD       TOCAMTD,TOCAMTT         * amount total
.
          MOVE      TADMADLT,TADMTOTL
          ADD       TADMCHIL,TADMTOTL       * admissions total
.
          CALL      ABED0000
.
.         --- --- ---   Normal Record
.
          CALL      UPOCCP1
.
.         save the current values
.
          MOVE      OCWARD,SAVWWARD
          MOVE      OCROOM,SAVROOM
          MOVE      OCTYPE,SAVTYPE
.
.         --- --- ZZZ   Total Bed record
.
          MOVE      SAVWWARD,OCWARD
          MOVE      SAVROOM,OCROOM
          MOVE      ZZZ,OCTYPE
          PACK      KEY9,OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          CALL      AOCP0000 IF NOT EQUAL
.
          CALL      ABED0000
          CALL      UPOCCP1
.
.         --- ZZZ ---   Grand total Ward rec for each Type
.
          MOVE      SAVWWARD,OCWARD
          MOVE      ZZZ,OCROOM
          MOVE      SAVTYPE,OCTYPE
          PACK      KEY9,OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          CALL      AOCP0000 IF NOT EQUAL
.
          CALL      ABED0000
          CALL      UPOCCP1
.
.         --- ZZZ ZZZ   Grand total for the complete Ward
.
          MOVE      SAVWWARD,OCWARD
          MOVE      ZZZ,OCTYPE
          MOVE      ZZZ,OCROOM
          PACK      KEY9,OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          CALL      AOCP0000 IF NOT EQUAL
.
          CALL      ABED0000
          CALL      UPOCCP1
.
.         GRAND TOTAL FOR LAST PAGE RECORDS
.
.         ZZZ --- ---   Total Line record
.
          MOVE      ZZZ,OCWARD
          MOVE      SAVROOM,OCROOM
          MOVE      SAVTYPE,OCTYPE
          PACK      KEY9,OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          CALL      AOCP0000 IF NOT EQUAL
.
          CALL      ABED0000
          CALL      UPOCCP1
.
.         ZZZ --- ZZZ   Total Bed record
.
          MOVE      ZZZ,OCWARD
          MOVE      SAVROOM,OCROOM
          MOVE      ZZZ,OCTYPE
          PACK      KEY9,OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          CALL      AOCP0000 IF NOT EQUAL
.
          CALL      ABED0000
          CALL      UPOCCP1
.
.         ZZZ ZZZ ---   Grand total Ward rec for each Type
.
          MOVE      ZZZ,OCWARD
          MOVE      ZZZ,OCROOM
          MOVE      SAVTYPE,OCTYPE
          PACK      KEY9,OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          CALL      AOCP0000 IF NOT EQUAL
.
          CALL      ABED0000
          CALL      UPOCCP1
.
.         ZZZ ZZZ ZZZ   Grand total for the complete Ward
.
          MOVE      ZZZ,OCWARD
          MOVE      ZZZ,OCTYPE
          MOVE      ZZZ,OCROOM
          PACK      KEY9,OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          CALL      AOCP0000 IF NOT EQUAL
.
          CALL      ABED0000
          CALL      UPOCCP1
ENDOC99   RETURN
.
.*********************************************************************
.         PRINT REPORT
.*********************************************************************
REPORT    DISPLAY   *P1:24,*EL,*P29:24,*V2LON,"** Generating Report **",*HOFF:
                    *P1:24,*EL," Printing Ward :":
                    *P39:24,"Room Type :",*P60:24,"Admn Type :";
.
          MOVE      ZERO,GRSUM
.
.         PRINT OCCUPANCY FILE
.         OPTION 1 IS DAYS ......OPTION 2 IS AMOUNT
.
PRTCODE   MOVE      ONE,OPTION
          MOVE      SP3,OCWARD
          MOVE      SP3,OCTYPE
          MOVE      SP3,OCROOM
          MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
          MOVE      ZERO,ROOMFLAG
          MOVE      ZERO,RECFLAG 
.
REPOSO    PACK      KEY9,OCWARD,OCROOM,OCTYPE
          MOVE      SP6,WWARD               * USED AS DUMMY VARIABLES
          MOVE      SP6,WBED
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          GOTO      NEXTO1 IF EQUAL
.
NEXTO     CALL      RDKOCCP1
.         BRANCH    OVRCD,LASTPRT
.
          COMPARE   ZERO,OVRCD
          GOTO      NEXTO1 IF EQUAL
          BRANCH    RECFLAG,LASTPRT         * found a record
          GOTO      ENDP
.
NEXTO1    MOVE      ONE,RECFLAG
          COMPARE   ZERO,CPAGENO
          GOTO      NOTGTOT IF EQUAL
.
          MATCH     ZZZ,OCROOM
          GOTO      SAMEWRD IF NOT EQUAL
.
          MATCH     ZZZ,OCTYPE
          GOTO      SAMEWRD IF NOT EQUAL
.
.         CHANGED WARDS
.
LASTPRT   COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          BRANCH    OPTION,ENDL1,ENDL2
.
ENDL1     MOVE      ZERO,PATMIXP
          MOVE      ZERO,BEDOCCP
.
          COMPARE   ZERO,OCDAYT
          GOTO      PRTWTS IF EQUAL
.
          MOVE      HPCENT,PATMIXP          * Patient Mix (Tot = 100.00)
.
PRTWTS    BRANCH    GRSUM,PRT10
.
          MATCH     "Y",SUMFLG
          GOTO      PRT20 IF EQUAL
.
PRT10     CALL      PAGASK1
          PRINT     *1,WBDESC,*25,OCBEDS;
          IF        OCCHNG = 1
            PRINT     *32,"*";                   * No. of beds changed
          ENDIF
.
          MOVE      ADMADLT,ADMTOTL
          ADD       ADMCHIL,ADMTOTL
          PRINT     *37,OCDAYM,*44,OCDAYF,*52,OCDAYU:
                    *64,OCDAYA,*71,OCDAYC,*79,OCDAYD,*88,OCDAYT:
                    *97,ADMADLT,SP2,ADMCHIL,SP2,ADMTOTL
.
          ADD       ONE,CLNO
          CALL      PAGASK1
.
.         PRINT BED DAY ANAYSIS
.
          MOVE      ZERO,DAYANLM
          MOVE      ZERO,DAYANLF
          MOVE      ZERO,DAYANLA
          MOVE      ZERO,DAYANLC
          MOVE      ZERO,DAYANLT
          MOVE      ZERO,DAYPERI
          MOVE      ZERO,DAYPERD
.
.         Check if there is no patients in ward
.
          COMPARE   ZERO,OCDAYT
          GOTO      PRTSTS IF EQUAL
.
          MOVE      OCDAYM,DAYWRK1
          MOVE      OCDAYM,DAYWRK2
          ADD       OCDAYF,DAYWRK2
..          ADD       OCDAYI,DAYWRK2
          ADD       OCDAYU,DAYWRK2
          DIV       DAYWRK2,DAYWRK1
          MULT      HPCENT,DAYWRK1
          MOVE      DAYWRK1,DAYANLM         * male percentage
.
          MOVE      OCDAYF,DAYWRK1
          MOVE      OCDAYF,DAYWRK2
          ADD       OCDAYM,DAYWRK2
....          ADD       OCDAYI,DAYWRK2
          ADD       OCDAYU,DAYWRK2
          DIV       DAYWRK2,DAYWRK1
          MULT      HPCENT,DAYWRK1
          MOVE      DAYWRK1,DAYANLF         * female percentage
.
..          MOVE      OCDAYI,DAYWRK1
..          MOVE      OCDAYI,DAYWRK2
..          ADD       OCDAYF,DAYWRK2
..          ADD       OCDAYM,DAYWRK2
..          ADD       OCDAYU,DAYWRK2
..          DIV       DAYWRK2,DAYWRK1
..          MULT      HPCENT,DAYWRK1
..          MOVE      DAYWRK1,DAYANLI         * indterminate percentage
.
          MOVE      OCDAYU,DAYWRK1
          MOVE      OCDAYU,DAYWRK2
          ADD       OCDAYM,DAYWRK2
          ADD       OCDAYF,DAYWRK2
..          ADD       OCDAYU,DAYWRK2
          DIV       DAYWRK2,DAYWRK1
          MULT      HPCENT,DAYWRK1
          MOVE      DAYWRK1,DAYANLU         * unknown percentage
.
          MOVE      OCDAYA,DAYWRK1
          MOVE      OCDAYA,DAYWRK2
          ADD       OCDAYC,DAYWRK2
          DIV       DAYWRK2,DAYWRK1
          MULT      HPCENT,DAYWRK1
          MOVE      DAYWRK1,DAYANLA         * adult percentage
          MOVE      HPCENT,DAYANLC
          SUB       DAYANLA,DAYANLC         * child percentage
.
          MOVE      HPCENT,DAYANLT          * total always 100%
.
          MOVE      OCDAYA,DAYWRK1
          ADD       OCDAYC,DAYWRK1
          DIV       OCDAYT,DAYWRK1
          MULT      HPCENT,DAYWRK1
          MOVE      DAYWRK1,DAYPERI         * IN-PATIENTS %
.
          MOVE      HPCENT,DAYPERD
          SUB       DAYPERI,DAYPERD
.
PRTSTS    PRINT     *N,"Bed Day Analysis",*40,DAYANLM,PCENT:
                    *47,DAYANLF,PCENT,*55,DAYANLU,PCENT:
                    *67,DAYANLA,PCENT,*74,DAYANLC,PCENT,*91,DAYANLT,PCENT:
                    *N,*N,*73,"In-Patients",*88,DAYPERI,PCENT,*N:
                    *73,"Day Cases",*88,DAYPERD,PCENT
          ADD       FIVE,CLNO
          IF        OCCHNG = 1
            MATCH     ZZZ,OCWARD
            IF        @EQUAL
              PRINT     *1,"* - Some Wards ";
            ELSE
              PRINT     *1,"* - Ward ";
            ENDIF
            PRINT     "changed number of beds during period."
            ADD       ONE,CLNO
          ENDIF
.
PRT20     MOVE      TWO,OPTION
.
.         Set for reposition to top of Ward
.
          MOVE      SP3,OCROOM
          MOVE      SP3,OCTYPE
          GOTO      ENDLST
.
ENDL2     BRANCH    GRSUM,ENDL3
.
          MATCH     "Y",SUMFLG
          GOTO      ENDL4 IF EQUAL
.
ENDL3     CALL      PAGASK2
          MOVE      OCAMTF,SVOCAMTF
          MOVE      OCAMTA,SVOCAMTA
          MOVE      OCAMTC,SVOCAMTC
          MOVE      OCAMTD,SVOCAMTD
          PRINT     *1,WBDESC,*31,OCAMTM,*46,SVOCAMTF,*60,OCAMTU:
                    *80,SVOCAMTA,*93,SVOCAMTC,*106,SVOCAMTD,*119,OCAMTT
          ADD       ONE,CLNO
          CALL      PAGASK2
.
ENDL4     MOVE      ONE,OPTION
.
.         Program finishes at this point
.
          MATCH     ZZZ,OCWARD
          GOTO      PSUM IF EQUAL
.
ENDLST    MOVE      ZERO,ROOMFLAG
          MOVE      "60",CLNO
.
          BRANCH    OPTION,NEXTO,REPOSO
.
.    SAMEWRD
.
.    Check if the Room Type title has been printed yet (ROOMFLAG=1)
.    If it has, and this is the Room Type summary record (OCTYPE=ZZZ)
.    then print the summary
.
SAMEWRD   COMPARE   ZERO,ROOMFLAG
          GOTO      NOTGTOT IF EQUAL
.
          MATCH     ZZZ,OCTYPE
          GOTO      NOTGTOT IF NOT EQUAL
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          BRANCH    OPTION,LINTOT1,LINTOT2
.
LINTOT1   MOVE      ZERO,PATMIXP
          MOVE      ZERO,BEDOCCP
.
          COMPARE   ZERO,OCDAYT
          GOTO      PRTBTS IF EQUAL
.
          MOVE      HPCENT,PATMIXP          * Patient Mix (Tot = 100.00)
.
PRTBTS    BRANCH    GRSUM,PRTB10
.
          MATCH     "Y",SUMFLG
          GOTO      LINTOT4 IF EQUAL
.
PRTB10    CALL      PAGSEP1
          MOVE      ADMADLT,ADMTOTL
          ADD       ADMCHIL,ADMTOTL
          PRINT     *9,"TOTAL :",*25,OCBEDS:
                    *37,OCDAYM,*44,OCDAYF,*52,OCDAYU:
                    *64,OCDAYA,*71,OCDAYC,*79,OCDAYD,*88,OCDAYT:
                    *97,ADMADLT,SP2,ADMCHIL,SP2,ADMTOTL
.
          CALL      PAGSEP1
          GOTO      LINTOTST
.
LINTOT2   BRANCH    GRSUM,LINTOT3
.
          MATCH     "Y",SUMFLG
          GOTO      LINTOT4 IF EQUAL
.
LINTOT3   CALL      PAGSEP2
          MOVE      OCAMTF,SVOCAMTF
          MOVE      OCAMTA,SVOCAMTA
          MOVE      OCAMTC,SVOCAMTC
          MOVE      OCAMTD,SVOCAMTD
          PRINT     *9,"TOTAL :":
                    *31,OCAMTM,*46,SVOCAMTF,*60,OCAMTU:
                    *80,SVOCAMTA,*93,SVOCAMTC,*106,SVOCAMTD,*119,OCAMTT
.
          CALL      PAGSEP2
.
LINTOTST  ADD       ONE,CLNO
LINTOT4   MOVE      ZERO,ROOMFLAG
          GOTO      NEXTO
.
.         NEW WARD
.
NOTGTOT   MOVE      OCWARD,WWARD
.
          DISPLAY   *P52:24,*V2LON,OCROOM;
.
          MATCH     SP3,OCROOM
          GOTO      NOROOM IF EQUAL
.
          PACK      KEY5,CODERT,OCROOM
          CALL      RDCODE1
          BRANCH    OVRCD,UNKROOM
          MOVE      TDESC,DDROOM
          GOTO      OKROOM
.
NOROOM    MOVE      "NO ROOM TYPE GIVEN          ",DDROOM
          GOTO      OKROOM
.
UNKROOM   CLEAR     DDROOM
          APPEND    "UNKNOWN ROOM TYPE: ",DDROOM
          APPEND    OCROOM,DDROOM
          RESET     DDROOM
.
OKROOM    COMPARE   ZERO,CPAGENO
          GOTO      NRMLIN IF EQUAL
.
          IF        ROOMFLAG = 1 & CLNO < FIFTY5
            GOTO      SAMERM
          ENDIF
.         BRANCH    ROOMFLAG,SAMERM
.
          MATCH     ZZZ,OCROOM
          GOTO      NRMLIN IF NOT EQUAL
.
          MOVE      "TOTALS :            ",DDROOM
.
NRMLIN    COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          BRANCH    GRSUM,NRML10
          MATCH     "Y",SUMFLG
          GOTO      NRML20 IF EQUAL
.
NRML10    COMPARE   "53",CLNO
          CALL      HEAD0000 IF NOT LESS
          PRINT     *N,*2,DDROOM
          ADD       TWO,CLNO
.
NRML20    MOVE      ONE,ROOMFLAG
.
SAMERM    COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
.         Check if this is the summary record, and if so then print summary
.         This test will only succeed when there is no admission types for
.         this room type/ward combination, and the ROOMFLAG=0 test at
.         SAMEWRD therefore succeeded.
.
          MATCH     ZZZ,OCTYPE
          GOTO      SAMEWRD IF EQUAL
.
          MATCH     SP3,OCTYPE
          GOTO      BLNKTYPE IF EQUAL
.
          PACK      KEY5,CODEA,OCTYPE
          CALL      RDCODE1
          BRANCH    OVRCD,UNKTYPE
          MOVE      TDESC,DDTYPE
          GOTO      OKTYPE
.
BLNKTYPE  MOVE      "Blank Adm. Code   ",DDTYPE
          GOTO      OKTYPE
.
UNKTYPE   PACK      DDTYPE,INVLTYPE,OCTYPE,SP70
.
OKTYPE    DISPLAY   *P73:24,*V2LON,OCTYPE;
.
          BRANCH    OPTION,PDAYS,PAMTS
.
PDAYS     COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
.         GET PRETOTAL VALUE FROM FILE
.
          PACK      KEY9,OCWARD,OCROOM,ZZZ
          CALL      RDOCCP2
          MOVE      ZERO,PATMIXP
.
          COMPARE   ZERO,OCDAYT
          GOTO      PRTATS IF EQUAL
.
          COMPARE   ZERO,PRETOTAL
          GOTO      PRTATS IF EQUAL
.
          MOVE      OCDAYT,DAYWRK1
          MULT      HPCENT,DAYWRK1
          DIV       PRETOTAL,DAYWRK1
          MOVE      DAYWRK1,PATMIXP         * Patient Mix
.
PRTATS    BRANCH    GRSUM,TATS10
          MATCH     "Y",SUMFLG
          GOTO      NEXTO IF EQUAL
.
TATS10    MOVE      ADMADLT,ADMTOTL
          ADD       ADMCHIL,ADMTOTL
          PRINT     *9,DDTYPE,*37,OCDAYM,*44,OCDAYF,*52,OCDAYU:
                    *64,OCDAYA,*71,OCDAYC,*79,OCDAYD,*88,OCDAYT:
                    *97,ADMADLT,SP2,ADMCHIL,SP2,ADMTOTL
          ADD       ONE,CLNO
          GOTO      NEXTO
.
PAMTS     COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          BRANCH    GRSUM,TATS20
          MATCH     "Y",SUMFLG
          GOTO      NEXTO IF EQUAL
.
TATS20    MOVE      OCAMTF,SVOCAMTF
          MOVE      OCAMTA,SVOCAMTA
          MOVE      OCAMTC,SVOCAMTC
          MOVE      OCAMTD,SVOCAMTD
          PRINT     *9,DDTYPE,*31,OCAMTM,*46,SVOCAMTF,*60,OCAMTU:
                    *80,SVOCAMTA,*93,SVOCAMTC,*106,SVOCAMTD,*119,OCAMTT
          ADD       ONE,CLNO
          GOTO      NEXTO
.
ENDLN     RETURN
.
.         LAST LINE ON THE PAGE
.
PAGEND1   PRINT     "-----------------------  ------      ------ ------":
                    " -------      ------ ------ ------- --------":
                    "  -----  -----  --------"
          ADD       ONE,CLNO
          RETURN
.
.         PAGE SEPERATOR
.
PAGSEP1   PRINT     "-----------------------  ------      ------ ------":
                    " -------      ------ ------ ------- --------":
                    "  -----  -----  --------"
          ADD       ONE,CLNO
          RETURN
.
.         PAGE ASTERISKS
.
PAGASK1   PRINT     "***********************  ******      ****** ******":
                    " *******      ****** ****** ******* ********":
                    "  *****  *****  ********"
          ADD       ONE,CLNO
          RETURN
.
.         LAST LINE ON THE PAGE
.
PAGEND2   PRINT     "----------------------- ":
                    "          -----------  -----------  -----------        ":
                    "  -----------  -----------  -----------  -------------"
          ADD       ONE,CLNO
          RETURN
.
.         PAGE SEPERATOR
.
PAGSEP2   PRINT     "----------------------- ":
                    "          -----------  -----------  -----------        ":
                    "  -----------  -----------  -----------  -------------"
          ADD       ONE,CLNO
          RETURN
.
.         PAGE ASTERISKS
.
PAGASK2   PRINT     "*********************** ":
                    "          ***********  ***********  ***********        ":
                    "  ***********  ***********  ***********  *************"
          ADD       ONE,CLNO
          RETURN
+
.**********************************************************************
.*                            DAYA0000                                *
.*     Process patdayaf Data writing to temp indexed file patmp1xx    *
.**********************************************************************
DAYA0000  UNPACK    ACTVTDTE,CCENT,CYEAR,TDAYMON
          PACK      RYEAR,CCENT,CYEAR
          REP       " 0",RYEAR
.
          UNPACK    KFRDATE,CCENT,CYEAR,SDAYMON
          PACK      SYEAR,CCENT,CYEAR
          REP       " 0",SYEAR
.
          DISPLAY   *P1:24,*EL,*P21:24,*V2LON,*BLINKON:
                    "** Initialising the Temp Indexed File **";
.
.------ close previous day file and set up the name of the next one ------
.
DAYA1000  CLOSE     PATDAYA1
          PACK      CFNAME,FILDAYA1,SYEAR
.
          TRAP      DAYA4900 IF IO        * trap if file does not exist
          OPEN      PATDAYA1,CFNAME
          TRAPCLR   IO
.
          PACK      KEY12,SDAYMON,SP10    * position on daily patient list file
          CALL      RSPTDAY1
.
.------ read through the patient daily list file ------
.
DAYA2000  CALL      RKPTDAY1
          BRANCH    OVRCD,DAYA5000
.
          MATCH     RYEAR,SYEAR            * see if the current file is the one
          GOTO      DAYA3000 IF NOT EQUAL    for the last year in the date range
.
          MATCH     PTDYDATE,TDAYMON       * if so, make sure the month and day
          GOTO      DAYA9999 IF LESS         are still in range
.
.------ Only need to write to the temporary file if the admission number -----
.------ is not already there ------
.
DAYA3000  MOVE      PTDYADMN,KEY8
          DISPLAY   *P65:24,KEY8;
          CALL      RDTMPR1                * read the temp file
          BRANCH    OVRCD,DAYA4000
          GOTO      DAYA2000
.
.------ write a new record to the temp file ------
.
DAYA4000  CALL      WRTMPR1
          GOTO      DAYA2000
.
.------ need to pop the return address off the stack if I/O error trapped -----
.
DAYA4900  NORETURN
.
.------ either file does not exist or we have reached the end of the ------
.------ current file ------
.
DAYA5000  MATCH     RYEAR,SYEAR             * test if we have reached the end 
          GOTO      DAYA9999 IF NOT LESS      year in the date range
.
          MOVE      SYEAR,FORM4             * if not, increment the start year
          ADD       ONE,FORM4                 to the next year and process
          MOVE      FORM4,SYEAR               the file for this year
          REP       " 0",SYEAR
          MOVE      "0101",SDAYMON
          GOTO      DAYA1000
.
DAYA9999  RETURN
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialisaton and open files                               *
.*********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSPX2A1,"pmspx2af"                                *I-49016
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"pmscidaf";
          OPEN      PMSCIDA1,"pmscidaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A2,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
.
          DISPLAY   *P64:24,"patwbhaf";
          OPEN      PATWBHA1,"patwbhaf"
.
          DISPLAY   *P64:24,"nzpbfeaf";
          OPEN      NZPBFEA1,"nzpbfeaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*94,CAGECOFF,*217,CAUDQ
          READ      CONTROLF,TEN3;*245,CHOSTYP
          READ      CONTROLF,TEN5;*247,CSTDOWN
          READ      CONTROLF,FIVE9;*1,CDLSTEP
          READ      CONTROLF,TWENTY;*164,PTRESDAY,*199,PTCNBCUT,*211,PTCNCNDY
          READ      CONTROLF,EIGHTY8;*233,PTCNADES
          READ      CONTROLF,TEN9;*212,CFEETYP
          READ      CONTROLF,HUND29;*89,PTCNPPIN
.
          PACK      CUDATE,CCC,CYY,CMM,CDD
          REP       " 0",CUDATE
          MOVE      ONE,CNEWDS
          MOVE      ONE,CNOUNDLN            * no underlines
.
          CALL      TFILENAM
          PACK      PATFNAME,TFILNAME
.
          CALL      TFILENAM
          PACK      FNAMEI,TFILNAME
.
          CALL      TFILENAM
          PACK      FNAMER,TFILNAME
.
INIT9999  RETURN
+
.*********************************************************************
.                   SCRA0000
.         Date parameteres screen
.*********************************************************************
SCRA0000  DISPLAY   *P1:4,*EF,"Keyin From Date : ":
                    *P1:6,"Keyin To   Date : ":
                    *P1:8,"Grand Summary Only (",*V2LON,"Y",*HOFF,"/":
                                                 *V2LON,"N",*HOFF,") : ";
SCRA9999  RETURN
+
.*********************************************************************
.                   SCRB0000
.         Ward parameters screen
.*********************************************************************
SCRB0000  DISPLAY   *P1:12,*EF,"From Ward :":
                        *P1:14,"To   Ward :";
SCRB9999  RETURN
+
.*********************************************************************
.*                  DATE0000                    Called by : ML3000   *
.*        Keyin the date range                                       *
.*        Returns : EXIT = 1      exit selected                      *
.*********************************************************************
DATE0000  MOVE      FOUR,CVERT              * FROM date
          MOVE      "19",CCOL
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE
          BRANCH    CQUEST,DATE0000         * invalid
          BRANCH    OVRCD,DATE9100          * exit selected
.
          PACK      KFRDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KFRDATE
          UNPACK    KFRDATE,XCENT1,XYEAR1,XMON1,XDAY1
.
          MATCH     KFRDATE,CUDATE
          GOTO      DATE3100 IF LESS
.
.         Keyin TO date
.
DATE2000  MOVE      SIX,CVERT
          MOVE      KFRDATE,KTTDATE
          MOVE      ONE,CDEFDTE
          CALL      KEYDATE
          BRANCH    CQUEST,DATE2000
          BRANCH    OVRCD,DATE0000          * nothing entered
.
          PACK      KTTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KTTDATE
          UNPACK    KTTDATE,XCENT2,XYEAR2,XMON2,XDAY2
.
          MATCH     KTTDATE,CUDATE
          GOTO      DATE3200 IF LESS
.
          MATCH     KFRDATE,KTTDATE
          GOTO      DATE3000 IF LESS
.
          UNPACK    KTTDATE,XCENT2,XYEAR2,XMON2,XDAY2
          MOVE      KFRDATE,ACTVFDTE
          MOVE      KTTDATE,ACTVTDTE
          GOTO      DATE4000
.
DATE3000  DISPLAY   *P1:24,*B,*EL,*V2LON:
                    "** To Date must be greater than or = to From Date **  ";
          CALL      HITENTER
          GOTO      DATE3300
.
DATE3100  DISPLAY   *P1:24,*B,*EL,*V2LON:
                    "** From Date must be less than or = today's date **   ";
          CALL      HITENTER
          GOTO      DATE0000
.
DATE3200  DISPLAY   *P1:24,*B,*EL,*V2LON:
                    "** To Date must be less than or = today's date **   ";
          CALL      HITENTER
.
DATE3300  MOVE      XDAY1,INDAY
          MOVE      XMON1,INMON
          MOVE      XYEAR1,INYEAR
          MOVE      XCENT1,INCENT
          GOTO      DATE2000
.
.         Add one to the KTTDATE. This is because initially the program did not
.         include the end date, so it looks at all the data upto but including
.         the KTTDATE.
.
DATE4000  MOVE      CDAY,DD
          MOVE      CMON,MM
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          CALL      DMYCON
.
          MOVE      JULDAY,FORM4
          SUB       OYEAR,FORM4
          SUB       LEAPYR,FORM4
          COMPARE   ZERO,FORM4
          GOTO      DATE4500 IF LESS
.
          ADD       ONE,JULYR
          IF        JULYR = ZERO
            ADD       ONE,JULCC             *Account for change in century
          ENDIF
          MOVE      ZERO,JULDAY
.
DATE4500  ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
.
          PACK      KTTDATE,CC,YY,MM,DD
          REP       " 0",KTTDATE
.
          MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9100  MOVE      ONE,EXIT
.
DATE9999  RETURN
+
.*********************************************************************
.*                  GSUM0000                    Called by : ML4000   *
.*        Enter response to Grand SUmmary Only question              *
.*********************************************************************
GSUM0000  KEYIN     *P28:8,*DV,UNDLN1,*P28:8,*V2LON,SUMFLG;
.
          RESET     SUMFLG
          GOTO      GSUM0100 IF NOT EOS
.
          MOVE      "Y",SUMFLG
          DISPLAY   *P28:8,*V2LON,SUMFLG;
          GOTO      GSUM9999
.
GSUM0100  REP       UPPLOW,SUMFLG
          DISPLAY   *P28:8,*V2LON,SUMFLG;
.
          MATCH     "Y",SUMFLG
          GOTO      GSUM9999 IF EQUAL
          MATCH     "N",SUMFLG                 * If No, keyin Ward range
          GOTO      GSUM0500 IF EQUAL
          BEEP
          GOTO      GSUM0000
.
GSUM0500  MOVE      ZERO,WARDFLG
.
.         Keyin Ward range if answer to "Grand Summary Only" question is (N)o
.
.         From Ward
.         ---------
.
GSUM1000  CALL      SCRB0000                    * display Ward range screen
          CALL      CLRA0000                   * initialise variables
.
          MOVE      "12",VERT                  * initialize row position
          MOVE      ZERO,QUESTFLG              * initialize "?" flag
.
GSUM1100  DISPLAY   *P22:VERT,*EL:
                    *P25:VERT,"( Hit <",*V2LON,"ENTER",*HOFF,"> for ":
                              *V2LON,START,*HOFF," )";
.
GSUM1200  KEYIN     *P13:VERT,*DV,UNDLN3,*P13:VERT,*V2LON,FRWARD;
          ENDSET    FRWARD
          APPEND    SP3,FRWARD
          RESET     FRWARD
.
          MATCH     SP3,FRWARD                 * null From Ward ?
          GOTO      GSUM1300 IF NOT EQUAL      * no
          MOVE      START,FRWRD                * yes
.
          BRANCH    QUESTFLG,GSUM1600
          DISPLAY   *P13:12,*EL,*V2LON,START;
          GOTO      GSUM3000
.
GSUM1300  DISPLAY   *P13:VERT,*V2LON,FRWARD;
          CMATCH    QUEST,FRWARD               * "?" routine requested ?
          GOTO      GSUM1700 IF EQUAL           * yes
.
          PACK      KEY6,FRWARD,SP3            * validate From Ward
          CALL      RDWARD1
          COMPARE   ZERO,OVRCD
          GOTO      GSUM1500 IF EQUAL
.
          CALL      INVA0000                    * invalid From Ward
          BRANCH    QUESTFLG,GSUM1400
          GOTO      GSUM1200
.
GSUM1400  DISPLAY   *P1:24,*EL,"From Ward :";
          GOTO      GSUM1100
.
GSUM1500  MOVE      WBDESC,FWRDDESC            * valid From Ward
          MOVE      FRWARD,FRWRD
          BRANCH    QUESTFLG,GSUM1600
          DISPLAY   *P22:12,*EL,FWRDDESC;
          GOTO      GSUM3000
.
GSUM1600  CALL      RDSA0000                     * re-display screen after "?"
          GOTO      GSUM3000
.
GSUM1700  CALL      PATWRDDS                   * Wards scan
          MOVE      "24",VERT
          MOVE      ONE,QUESTFLG
          GOTO      GSUM1400
.
.         To Ward
.         -------
.
GSUM3000  MOVE      "14",VERT                  * initialize row position
          MOVE      ZERO,QUESTFLG              * initialize "?" flag
.
GSUM3100  DISPLAY   *P22:VERT,*EL:
                    *P25:VERT,"( Hit <",*V2LON,"ENTER",*HOFF,"> for ":
                              *V2LON,FINISH,*HOFF," )";
.
GSUM3200  KEYIN     *P13:VERT,*DV,UNDLN3,*P13:VERT,*V2LON,TOWARD;
          ENDSET    TOWARD
          APPEND    SP3,TOWARD
          RESET     TOWARD
.
          MATCH     SP3,TOWARD                 * null To Ward ?
          GOTO      GSUM3300 IF NOT EQUAL       * no
          MOVE      ZZZ,TOWARD                 * yes
          MOVE      FINISH,TOWRD
.
          BRANCH    QUESTFLG,GSUM3600
          DISPLAY   *P13:14,*EL,*V2LON,FINISH;
          GOTO      GSUM5000                    * go to check Ward range entered
.
GSUM3300  DISPLAY   *P13:VERT,*V2LON,TOWARD;
          CMATCH    QUEST,TOWARD               * "?" routine requested ?
          GOTO      GSUM3700 IF EQUAL           * yes
.
          PACK      KEY6,TOWARD,SP3            * validate To Ward
          CALL      RDWARD1
          COMPARE   ZERO,OVRCD
          GOTO      GSUM3500 IF EQUAL
.
          CALL      INVA0000                    * invalid To Ward
          BRANCH    QUESTFLG,GSUM3400
          GOTO      GSUM3200
.
GSUM3400  DISPLAY   *P1:24,*EL,"  To Ward :";
          GOTO      GSUM3100
.
GSUM3500  MOVE      WBDESC,TWRDDESC            * valid To Ward
          MOVE      TOWARD,TOWRD
          BRANCH    QUESTFLG,GSUM3600
          DISPLAY   *P22:14,*EL,TWRDDESC;
          GOTO      GSUM5000
.
GSUM3600  CALL      RDSA0000                   * re-display screen after "?"
          GOTO      GSUM5000
.
GSUM3700  CALL      PATWRDDS                   * Wards scan
          MOVE      "24",VERT
          MOVE      ONE,QUESTFLG
          GOTO      GSUM3400
.
.         check if Ward range is valid or not
.
GSUM5000  MATCH     FRWARD,TOWARD
          GOTO      GSUM9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** To Ward must be greater than or = to From Ward **  ";
          CALL      HITENTER
          GOTO      GSUM1000
.
GSUM9999  RETURN
+
.*********************************************************************
.                   INVA0000
.         Invalid Ward selected 
.*********************************************************************
INVA0000  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Ward **    ";
          CALL      HITENTER
INVA9999  RETURN
+
.*********************************************************************
.                   CLRA0000
.         Initialize Ward range variables
.*********************************************************************
CLRA0000  MOVE      SP3,FRWARD
          MOVE      SP3,TOWARD
          MOVE      SP20,FWRDDESC
          MOVE      SP20,TWRDDESC
CLRA9999  RETURN
+
.*********************************************************************
.                   RDSA0000
.         Re-display screen after Wards scan
.*********************************************************************
RDSA0000  CALL      SCRA0000
.
          DISPLAY   *P20:4,*V2LON,XDAY1,SLASH,XMON1,SLASH,XYEAR1:
                    *P20:6,*V2LON,XDAY2,SLASH,XMON2,SLASH,XYEAR2:
                    *P28:8,*V2LON,SUMFLG;
.
          CALL      SCRB0000
.
          MATCH     SP3,FRWARD
          GOTO      RDSA1000 IF EQUAL
          DISPLAY   *P13:12,*V2LON,FRWARD,*HOFF,*P22:12,FWRDDESC;
          GOTO      RDSA2000
.
RDSA1000  DISPLAY   *P13:12,*V2LON,START;
.
RDSA2000  MATCH     ZZZ,TOWARD
          GOTO      RDSA3000 IF EQUAL
          DISPLAY   *P13:14,*V2LON,TOWARD,*HOFF,*P22:14,TWRDDESC;
          GOTO      RDSA9999
.
RDSA3000  DISPLAY   *P13:14,*V2LON,FINISH;
+
RDSA9999  RETURN
+
.*********************************************************************
.                   CDAY0000
.         Calculate nodays for percentage calculation on bed occupancy
.*********************************************************************
CDAY0000  DAYSEP    KFRDATE,KTTDATE,TOTTDAY
          MOVE      TOTTDAY,NODAYS       *** number days covered
.
CDAY9999  RETURN
+
.*********************************************************************
.*                  CTPA0000                    Called by : xxxx0000 *
.*        Create the temp file                                       *
.*********************************************************************
CTPA0000  DISPLAY   *P1:24,*EL,*P23:24,*V2LON,*BLINKON:
                    "** Creating the Temp Indexed File **";
.
.         Kill the Tempfile if it already exists
.
          CALL      DTPA0000
.
.         Build new temp indexed file 
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    PATFNAME,CMDLINE
          APPEND    " 9 u1-8",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      PATMP1XX,PATFNAME
.
CTPA9999  RETURN
+
.*********************************************************************
.                   INTA0000
.         Initialize working variables
.*********************************************************************
INTA0000  MOVE      ZERO,AMTCODE
          MOVE      ZERO,BEDCODE
          MOVE      ZERO,CADAYS
          MOVE      ZERO,CAAMT
          MOVE      ZERO,SAVAMT
          MOVE      SP6,STDATE
          MOVE      ZERO,CPAGENO            * clear page counter
INTA9999  RETURN
+
.*********************************************************************
.                   INTB0000
.         Initialize Occupancy File
.*********************************************************************
INTB0000  DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Initialising Occupancy File **";
          CALL      CTPB0000
          BRANCH    OPCND,INTB9100
.
          MOVE      ZERO,EXIT
          GOTO      INTB9999
.
INTB9100  MOVE      ONE,EXIT
.
INTB9999  RETURN
+
.*********************************************************************
.                   HEAD0000
.         Headings for Output
.*********************************************************************
HEAD0000  MATCH     "Y",SUMFLG
          GOTO      HEAD1000 IF NOT EQUAL   * doing a ward range
.
          MATCH     ZZZ,WWARD
          GOTO      HEAD9999 IF NOT EQUAL
.
HEAD1000  CLOCK     TIME,CTIMEIS
.
          CALL      HEAD132
          ADD       ONE,CLNO
          IF        IBCNMHOS=1
            PRINT     *35,"Hospital Id : ",HOSNAME
            ADD       ONE,CLNO
          ENDIF
.
          PRINT     *35,"FROM : ",XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1:
                    *55,"TO : ",XDAY2,SLASH,XMON2,SLASH,XCENT2,XYEAR2
          MOVE      FOUR,CLNO
.
          MATCH     ZZZ,WWARD
          GOTO      HEAD5000 IF NOT EQUAL
.
          MOVE      ONE,GRSUM
          BRANCH    WARDFLG,HEAD2000
          PRINT     *N,*31,"*** GRAND SUMMARY for Wards : ",*+,FRWRD," to ":
                           TOWRD," ***",*N
          GOTO      HEAD3000
.
HEAD2000  PRINT     *N,*35,"***  GRAND SUMMARY OF ALL WARDS  ***",*N
.
HEAD3000  ADD       THREE,CLNO
.
          BRANCH    WARDFLG,HEAD4000
          MOVE      "WARDS TOTAL",WBDESC
          GOTO      HEAD6000
.
HEAD4000  MOVE      "ALL WARDS TOTAL",WBDESC
          GOTO      HEAD6000
.
HEAD5000  PACK      KEY6,OCWARD,WBED
          CALL      RDWARD1
          IF        OVRCD<>0
            GOTO      ENDP
          ENDIF
.
          BRANCH    GRSUM,HEAD5200
          MATCH     "Y",SUMFLG
          GOTO      HEAD6000 IF EQUAL
.
HEAD5200  PRINT     *35,"WARD : ",WWARD,*55,WBDESC,*N
          ADD       TWO,CLNO
.
HEAD6000  DISPLAY   *P18:24,*V2LON,WBDESC;
          MOVE      ZERO,ROOMFLAG
.
          BRANCH    OPTION,HEAD7100,HEAD7200
.
HEAD7100  IF        HEADFLG = 0
            PRINT     *26,"No of    --------------- Inpatient Bed Days ------":
                      "----------  - All -":
                      *97,"----- Admissions -----":
                      *N,"Room Type              ":
                      *26,"Beds        Male   Female Other        ":
                      "Adult  Child  D Cases  ":
                      " Total ",*97,"Adult  Child   Total"
          ELSE
            PRINT     *26,"No of    --------------- Inpatient Bed Days ------":
                      "----------  - All -":
                      *95,"----- Admissions -----":
                      *N,"Ward                   ":
                      *26,"Beds        Male   Female Other        ":
                      "Adult  Child  D Cases  ":
                      " Total ",*97,"Adult  Child   Total"
          ENDIF
          ADD       TWO,CLNO
          CALL      PAGSEP1
          GOTO      HEAD9999
.
HEAD7200  PRINT     *28,"  --------------------  I N P A T I E N T S -------":
                    "  ---------------------- A L L ------------------------":
                    *N,*1,"Room Type          ":
                    "                  Male        Female   ":
                    "    Other             ":
                    "    Adult        Child   ":
                    "  Day Cases       Total"
          ADD       TWO,CLNO
          CALL      PAGSEP2
.
HEAD9999  RETURN
+
.*********************************************************************
.*                  EXTA0000                    Called by : ML5000   *
.*        Produce Part one of the report                             *
.*********************************************************************
EXTA0000  DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Processing Admission Numbers **",*HOFF,*W2:
                    *P1:24,*EL,*P20:24,"Admission No :",*P56:24,"Scanning :";
.
          MOVE      SP8,KEY8
          CALL      RSTMPR1
.
.         read through the indexed temp file and process each admission
.
EXTA1000  CALL      RKTMPR1
          BRANCH    OVRCD,EXTA9999          * end of temp file
.
          MOVE      TMPADMN,AADMNO
          DISPLAY   *P68:24,AADMNO;
          MOVE      AADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,EXTA1000          * invalid admission number
.
.         MATCH     KTTDATE,ADATE
.         GOTO      EXTA1000 IF NOT LESS    * admission date too big
.
          MATCH     ADATE,KTTDATE
          GOTO      EXTA1000 IF LESS        * admission date too big
.
          IF        IBCNMHOS=1
            MATCH     SP70,HOSCODE      * All Hospitals
            IF        !@EQUAL
              PACK      KEY8,AADMNO,SP70
              CALL      RDPMVX11
              IF        OVRCD <> 1
                MATCH     PMVXMHOS,HOSCODE    * Correct hospital
                GOTO      EXTA1000 IF NOT EQUAL
              ELSE
                GOTO EXTA1000
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      SP6,DDATE
          BRANCH    ASTAT,EXTA1000,EXTA3000,EXTA2000,EXTA3000,EXTA1000
.
EXTA2000  MOVE      AADMNO,KEY8
          CALL      RDDSCH1
          BRANCH    OVRCD,EXTA1000          * invalid discharge number
.
          MATCH     KFRDATE,DDATE
          GOTO      EXTA1000 IF LESS        * discharge date too early
.
.------ we have a valid admission so extract details ------
.------ and write to summary file ------
.
EXTA3000  DISPLAY   *P35:24,*V2LON,AADMNO;
          CALL      PTRN0000
          GOTO      EXTA1000
.
EXTA9999  RETURN
+
.*********************************************************************
.                   PTRN0000                    Called by : EXTA3000
.         Loop over transfer file for stats for the admission
.*********************************************************************
PTRN0000  MOVE      SP8,STDATE
          MOVE      ZERO,SAVAMT
          MOVE      SP6,STMOVE
          MOVE      ZERO,STDISC
          MOVE      ZERO,STALLW
          MOVE      ZERO,ADMFLG
          MOVE      ZERO,DISCFLG
          MOVE      ZERO,UNKNWARD           * Clear Unknown ward flag
.
          MATCH     ANSY,PTMICMXP
          IF        @EQUAL
            IF        ASTAT=3
              MATCH     ADATE,DDATE
              IF        @EQUAL
                MOVE      ONE,ACDAYCS     * set one to daycase flag
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      SP10,SIDATET
          MOVE      SP10,LSTDATE
          MOVE      ZERO,ENDFLAG
          MOVE      ALACDTE,IDATEF           * set starting date for next inv.
          MOVE      IDATEF,SIDATET           * set starting date for next inv.
.
          CALL      GTAB0000                 * get health fund table
          PACK      CMDRG,PTMIPCMX,SP20      * Default to provisional DRG code
          UNPACK    SP10,KEY4,KEY5
          UNPACK    PTMIPCMX,KEY4,KEY5
          MATCH     SP5,KEY5
          IF        @EQUAL
            IF        ASTAT = 3
              MATCH     SP4,PTDSDDRG
              IF        !@EQUAL
                PACK      CMDRG,PTDSDDRG,SP20    * Use discharge DRG Code
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2
PTRN1000  CALL      RDKTRAN2
          BRANCH    OVRCD,PTRN5000
.
          MATCH     TADMN,AADMNO
          GOTO      PTRN5000 IF NOT EQUAL
.
          IF        IBCNMHOS=1
            MATCH     SP70,HOSCODE      * All Hospitals
            IF        !@EQUAL
              PACK      KEY8,TADMN,SP70
              CALL      RDPMVX11
              IF        OVRCD <> 1
                MATCH     PMVXMHOS,HOSCODE    * Correct hospital
                GOTO      PTRN1000 IF NOT EQUAL
              ELSE
                GOTO      PTRN1000
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      TMOVE,STMOVE
          RESET     TDATE
          REP       " 0",TDATE
          RESET     TDATE
.
          MOVE      TDATE,XDATE1
          MOVE      TDATE,LSTDATE
.
          MATCH     ANSY,PTMICMXP
          IF        @EQUAL
            BRANCH    ACDAYCS,PTRN1100    * sameday, no stepdown
            MOVE      KTTDATE,SAVEDATE   * save todate as PATCMSTP changes it
            CALL      PATCMSTP
            MOVE      SAVEDATE,KTTDATE
            GOTO      PTRN1100
          ENDIF
.
          IF         CSTDOWN = 1
            CALL      STEPDOWN
          ENDIF
.
PTRN1100  COMPARE   ZERO,ENDFLAG
          GOTO      PTRN1200 IF EQUAL
.
          MATCH     LSTDATE,TDATE
          GOTO      PTRN2100 IF EQUAL
.
PTRN1200  CMATCH    "A",TMOVE
          GOTO      PTRN3500 IF EQUAL
.
          MATCH     ADATE,DDATE
          IF        !@EQUAL
            CMATCH    "R",TMOVE
            GOTO      PTRN3500 IF EQUAL
            CMATCH    "L",TMOVE
            GOTO      PTRN2500 IF EQUAL
          ENDIF
.
          CMATCH    "C",TMOVE
          GOTO      PTRN2500 IF EQUAL
.
          MATCH     ADATE,DDATE
          GOTO      PTRN2000 IF NOT EQUAL
.
.         Daycase
.
          SUB       STDISC,SAVAMT
          SUB       STALLW,SAVAMT
.
          MOVE      ONE,TOTTDAY
.
          MOVE      ZERO,UNKNWARD           * Clear Unknown ward flag
          MOVE      TATYPE,OCTYPE
          MOVE      SP6,OCWARD              * OCCUPANCY WARD
          MOVE      SP6,OCROOM              * OCCUPANCY ROOM TYPE
          MOVE      SP3,SAVWBED
.
          PACK      KEY6,TWARD,TBED
          CALL      RDWARD1
          IF        OVRCD=1
            MOVE      ONE,UNKNWARD          * Set Unknown ward flag
            MOVE      TWARD,OCWARD          * OCCUPANCY WARD
            MOVE      SP10,OCROOM           * OCCUPANCY ROOM TYPE
            MOVE      TBED,SAVWBED
            GOTO      PTRN1500
          ENDIF
.
          PROC      GTRMTYPE                * get latest room type
.
          MOVE      WWARD,OCWARD            * OCCUPANCY WARD
          MOVE      ACTVRTYP,OCROOM         * OCCUPANCY ROOM TYPE
          MOVE      WBED,SAVWBED
.
.      include DAY CASES IN FIGURES
.
.       DISPLAY  *P1:20,*B," DAYCASE FOR ADMISSION ",*V2LON,AADMNO;
.       KEYIN    ANS,*P1:20,*EL;
.
PTRN1500  MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PTRN9999
          CALL      NEXTDX
          GOTO      PTRN9999
.
.         Discharged Record
.
PTRN2000  MATCH     KFRDATE,TDATE
          GOTO      PTRN9999 IF LESS
          MOVE      ONE,DISCFLG
          MATCH     KTTDATE,TDATE
          GOTO      PTRN5000 IF LESS
          MOVE      KTTDATE,TDATE
          GOTO      PTRN5000
.
PTRN2100  MOVE      TATYPE,STATYPE
          MOVE      TRBTYP,STRBTYP
          MOVE      TRBEND,STRBEND
          MOVE      PTTRAEND,SPTTRAEN
          MOVE      PTTREPSD,SPTTREPS
          MOVE      TDATE,SIDATET
          GOTO      PTRN1000
.
.         End of a period - calculate statistics
.         On-leave and Change Record
.
PTRN2500  MATCH     KFRDATE,TDATE
          GOTO      PTRN3500 IF LESS
.
          MATCH     KTTDATE,TDATE
          GOTO      PTRN3000 IF LESS
.
          MOVE      KTTDATE,WDATE1           * end date
          MOVE      STDATE,WDATE2           * start date
          GOTO      PTRN9000
.
PTRN3000  MOVE      TDATE,WDATE1            * end date
          MOVE      STDATE,WDATE2           * start date
          CALL      CALC00
.
.         Start of a period - save variables
.         Admission and On-Return Record
.
PTRN3500  CALL      ADMN0000                * see if an admitted in range
          MATCH     KTTDATE,TDATE
          GOTO      PTRN9999 IF NOT LESS      * after end date
.
          MATCH     KFRDATE,TDATE
          GOTO      PTRN4000 IF NOT LESS
          MOVE      KFRDATE,STDATE
          GOTO      PTRN4500
.
PTRN4000  MOVE      TDATE,STDATE
.
PTRN4500  MOVE      TDISC,STDISC
          MOVE      TALLW,STALLW
          MOVE      TRATEF,SAVAMT
          ADD       TRATEH,SAVAMT
.
          MOVE      TATYPE,OCTYPE
          MOVE      SP6,OCWARD              * OCCUPANCY WARD
          MOVE      SP6,OCROOM              * OCCUPANCY ROOM TYPE
          MOVE      SP3,SAVWBED
.
          PACK      KEY6,TWARD,TBED
          CALL      RDWARD1
          BRANCH    OVRCD,PTRN2100
.
          PROC      GTRMTYPE                * get latest room type
.
          MOVE      WWARD,OCWARD            * OCCUPANCY WARD
          MOVE      ACTVRTYP,OCROOM         * OCCUPANCY ROOM TYPE
          MOVE      WBED,SAVWBED
          GOTO      PTRN2100
.
.         End of Transfer File
.
PTRN5000  MATCH     "A",STMOVE
          IF        @EQUAL
            MATCH     STDATE,KTTDATE
            GOTO      PTRN9999 IF EQUAL
.
            MATCH     KFRDATE,STDATE
            IF        @LESS
              MOVE      KFRDATE,WDATE2
              MOVE      KTTDATE,WDATE1
            ELSE
              MOVE      STDATE,WDATE2
              MOVE      KTTDATE,WDATE1
            ENDIF
            CALL      CALC00
            GOTO      PTRN9999
          ENDIF
          MATCH     "L",STMOVE
          GOTO      PTRN9999 IF EQUAL
          MATCH     " ",STMOVE
          GOTO      PTRN9999 IF EQUAL
          MATCH     "D",STMOVE
          IF        @EQUAL
            MATCH     STDATE,DDATE
            GOTO      PTRN9999 IF EQUAL
          ENDIF
.
          IF        DISCFLG = 1
            MOVE      TDATE,XDATE1
          ELSE
            PACK      XDATE1,CCC,CYY,CMM,CDD
          ENDIF
          REP       " 0",XDATE1
.
          MATCH     ANSY,PTMICMXP
          IF        @EQUAL
            BRANCH    ACDAYCS,PTRN7000
            MOVE      XDATE1,TDATE
            MOVE      SP1,TMOVE
            LOAD      TMOVE USING ASTAT OF ANSC,ANSC,ANSC,ANSR
            MOVE      STATYPE,TATYPE
            MOVE      STRBTYP,TRBTYP
            MOVE      STRBEND,TRBEND
            MOVE      SPTTRAEN,PTTRAEND
            MOVE      SPTTREPS,PTTREPSD
            MOVE      KTTDATE,SAVEDATE   * save todate as PATCMSTP changes it
            CALL      PATCMSTP
            MOVE      SAVEDATE,KTTDATE
            MOVE      ONE,ENDFLAG
            GOTO      PTRN6500
          ENDIF
.
.         Check if stepdown is required
.
          COMPARE   ONE,CSTDOWN
          GOTO      PTRN7000 IF NOT EQUAL
.
          MOVE      XDATE1,TDATE
          MOVE      SP1,TMOVE
          LOAD      TMOVE,ASTAT,ANSC,ANSC,ANSC,ANSR
          MOVE      STATYPE,TATYPE
          MOVE      STRBTYP,TRBTYP
          MOVE      STRBEND,TRBEND
          CALL      STEPDOWN
          BRANCH    NOFEE,PTRN7000
          MOVE      ONE,ENDFLAG
.
.         Check if a stepdown was indicated = shown by a new TDATE
.
PTRN6500  MATCH     TDATE,XDATE1
          GOTO      PTRN7000 IF EQUAL
.
          MATCH     TDATE,LSTDATE
          GOTO      PTRN1100 IF LESS
          GOTO      PTRN2100
.
PTRN7000  MATCH     "D",STMOVE
          GOTO      PTRN8000 IF NOT EQUAL
.
          MATCH     KTTDATE,LSTDATE
          GOTO      PTRN8000 IF NOT LESS
.
          MOVE      LSTDATE,WDATE1            * end date
          MOVE      STDATE,WDATE2           * start date
          GOTO      PTRN9000
.
PTRN8000  MOVE      KTTDATE,WDATE1           * end date
          MOVE      STDATE,WDATE2           * start date
.
PTRN9000  CALL      CALC00
.
PTRN9999  RETURN
+
.*********************************************************************
.                   AOCP0000
.         Add a new record to the occupancy file
.*********************************************************************
AOCP0000  CALL      ZBED0000
          UNPACK    KEY9,OCWARD,OCROOM,OCTYPE
          CALL      WROCCP1
          PACK      KEY9,OCWARD,OCROOM,OCTYPE
          CALL      RDOCCP1
AOCP9999  RETURN
+
.*********************************************************************
.                   ZBED0000
.         Clear BED values 
.*********************************************************************
ZBED0000  MOVE      ZERO,OCBEDS
          MOVE      ZERO,OCCHNG
          MOVE      ZERO,OCDAYM
          MOVE      ZERO,OCDAYF
          MOVE      ZERO,OCDAYA
          MOVE      ZERO,OCDAYC
          MOVE      ZERO,OCDAYD
          MOVE      ZERO,OCDAYT
          MOVE      ZERO,OCAMTM
          MOVE      ZERO,OCAMTF
          MOVE      ZERO,OCAMTI
          MOVE      ZERO,OCAMTU
          MOVE      ZERO,OCAMTA
          MOVE      ZERO,OCAMTC
          MOVE      ZERO,OCAMTD
          MOVE      ZERO,OCAMTT
          MOVE      ZERO,ADMADLT
          MOVE      ZERO,ADMCHIL
          MOVE      ZERO,ADMTOTL
.
ZBED9999  RETURN
+
.*********************************************************************
.                   ABED0000
.         Add up BED Totals
.*********************************************************************
ABED0000  ADD       TOCBEDS,OCBEDS
          ADD       TOCDAYM,OCDAYM
          ADD       TOCDAYF,OCDAYF
..          ADD       TOCDAYI,OCDAYI
..          ADD       TOCDAYR,OCDAYR                                   *I-49016
          ADD       TOCDAYU,OCDAYU
          ADD       TOCDAYA,OCDAYA
          ADD       TOCDAYC,OCDAYC
          ADD       TOCDAYD,OCDAYD
          ADD       TOCDAYT,OCDAYT
.
          ADD       TOCAMTM,OCAMTM
          ADD       TOCAMTF,OCAMTF
..          ADD       TOCAMTI,OCAMTI
..          ADD       TOCAMTR,OCAMTR                                   *I-49016
          ADD       TOCAMTU,OCAMTU
          ADD       TOCAMTA,OCAMTA
          ADD       TOCAMTC,OCAMTC
          ADD       TOCAMTD,OCAMTD
          ADD       TOCAMTT,OCAMTT
.
          ADD       TADMADLT,ADMADLT
          ADD       TADMCHIL,ADMCHIL
          ADD       TADMTOTL,ADMTOTL
.
ABED9999  RETURN
+
.*********************************************************************
.*                  WSUM0000                    Called by : xxxx0000 *
.*        Print a ward summary                                       *
.*********************************************************************
WSUM0000  MOVE      ONE,OPTION              * set to print correct heading
          MOVE      ONE,HEADFLG             * set to print ward instead of Room
          MOVE      ZZZ,WWARD               * set so prints heading
          CALL      HEAD0000                * print new heading
.
          PACK      KEY9,SP3,ZZZ,ZZZ
          CALL      RDOCCP1
          COMPARE   ZERO,OVRCD
          GOTO      WSUM1100 IF EQUAL
.
WSUM1000  CALL      RDKOCCP1
          BRANCH    OVRCD,WSUM3000          * end of file
.
WSUM1100  MATCH     ZZZ,OCROOM
          GOTO      WSUM1000 IF NOT EQUAL   * not a total record
.
          MATCH     ZZZ,OCTYPE
          GOTO      WSUM1000 IF NOT EQUAL   * not a total record
.
          MOVE      "GRAND TOTAL : ",WBDESC
          MATCH     ZZZ,OCWARD
          GOTO      WSUM1500 IF EQUAL       * the grand total record
.
          MATCH     SP10,OCWARD
          IF        @EQUAL
            MOVE      "Blank Ward           ",WBDESC
          ELSE
            PACK      KEY6,OCWARD,SP6         * get the ward description
            CALL      RDWARD1
            IF        OVRCD=1                                 
              PACK      WBDESC,INVLWARD,OCWARD
            ENDIF
          ENDIF
          GOTO      WSUM2000
.
WSUM1500  CALL      PAGSEP1
.
WSUM2000  MOVE      ADMADLT,ADMTOTL
          ADD       ADMCHIL,ADMTOTL
          PRINT     *1,WBDESC,*25,OCBEDS:
                    *37,OCDAYM,*44,OCDAYF,*52,OCDAYU,*64,OCDAYA:
                    *71,OCDAYC,*79,OCDAYD,*88,OCDAYT:
                    *97,ADMADLT,SP2,ADMCHIL,SP2,ADMTOTL
          ADD       ONE,CLNO
.
          MATCH     ZZZ,OCWARD
          GOTO      WSUM1000 IF NOT EQUAL   * more to print
.
WSUM3000  CALL      PAGSEP1
.
WSUM9999  RETURN
+
.*********************************************************************
.*                  OPTN0000                    Called by : ML1000   *
.*        Get which option to perform                                *
.*********************************************************************
OPTN0000  HLINE     *G37,2,53,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*P1:5,"1",*P1:6,"2",*HOFF:
                    *P3:4,"= Exit":
                    *P3:5,"= Full Ward Report":
                    *P3:6,"= Ward Summary":
                    *P1:8,"Select Option :";
.
OPTN1000  KEYIN     *P17:8,*EL,*DV,UNDLN1,*P17:8,*V2LON,MOPTION;
.
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9000 IF EQUAL       * Exit selected
.
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,OPTN2000,OPTN3000
          BEEP
          GOTO      OPTN1000
.
OPTN2000  DISPLAY   *P53:2,*V2LON,"- Full Ward Report ";
          GOTO      OPTN9999
.
OPTN3000  DISPLAY   *P53:2,*V2LON,"- Ward Summary ";
          GOTO      OPTN9999
.
OPTN9000  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*********************************************************************
.*                  CBED0000                    Called by : ML5000   *
.*        Calculate the number of beds in each ward                  *
.*********************************************************************
CBED0000  DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "*** Processing No of Beds ***"
          DISPLAY   *P1:24,*EL,*P20:24,"Ward/Bed :";
.
          BRANCH    WARDFLG,CBED0500        * using all wards
          MATCH     SP3,FRWARD
          GOTO      CBED0500 IF EQUAL       * from start ward
.
          PACK      KEY6,FRWARD,SP3             * no - use From Ward as part of
          CALL      RDWARD1                            Key to read first record
          BRANCH    OVRCD,CBED9999
          GOTO      CBED1500
.
.         start at first ward
.
CBED0500  MOVE      SP6,KEY6
          CALL      RDSWARD1
.
.         loop over ward file and calculate number of beds
.
CBED1000  CALL      RDKWARD1
          BRANCH    OVRCD,CBED9999
.
CBED1500  DISPLAY   *P32:24,*V2LON,WWARD,"/",WBED;
.
          BRANCH    WARDFLG,CBED2000        * using all wards
          MATCH     WWARD,TOWARD 
          GOTO      CBED9999 IF LESS        * finished range
.
CBED2000  MOVE      WWARD,SAVWWARD
          MOVE      WBED,SAVWBED
.
.         Check if the ward was active during the period.  If not, then
.         ignore all records for the ward and position on the ward header
.         record for the next ward.
.
          MATCH     SP3,WBED                 * ward header record ?
          IF        @EQUAL
            MOVE      ONE,ACTVWFLG           * don't get weekend days
            PROC      WDACTVDY               * get days ward was active
            COMPARE   ZERO,ACTVDAYS          * ward active during period ?
            IF        @EQUAL
              PACK      KEY6,WWARD,ZZZ       * no
              CALL      RDSWARD1             * posn. on next ward header rec
              GOTO      CBED1000
            ENDIF
            MOVE      ACTVBCHG,CHNGFLAG
          ELSE
.
.           This is a ward/bed record, so if the ward has changed the number
.           of beds in the period, then use the room type from WDACTVDY, 
.           otherwise call GTRMTYPE to get the latest room type.
.
            IF        CHNGFLAG = 1
              MOVE      ONE,ACTVWFLG         * don't get weekend days
              PROC      WDACTVDY             * get latest room type
            ELSE
              PROC      GTRMTYPE             * get latest room type
              BRANCH    EXIT,CBED1000        * bed inactive - ignore
            ENDIF
            MOVE      ONE,TOTBED             * set for single bed
            GOTO      CBED3000
          ENDIF
.
.         We are processing a ward header record (WBED = spaces)
.         If this is a ward/bed ward, then get next record and process beds.
.
          COMPARE   ONE,WINPUT              * ward only input ?
          IF        !@EQUAL
            GOTO      CBED1000              * no
          ENDIF
.
.         If we aren't printing zero stats wards, check if ward has stats first
.
CBED3000  IF        PTCNH82W = 0
            PACK      KEY9,WWARD,ZZZ,ZZZ
            CALL      RDOCCP1
            IF        OVRCD = 1
              PACK      KEY6,WWARD,ZZZ
              CALL      RDSWARD1
              GOTO      CBED1000
            ENDIF
          ENDIF
.
.         We have a ward/bed record.
.         Firstly, update grand total for room type for all wards
.
          PACK      KEY9,ZZZ,ACTVRTYP,ZZZ
          CALL      RDOCCP1                 * read grand total record
          COMPARE   ZERO,OVRCD              * record on file ?
          CALL      AOCP0000 IF NOT EQUAL   * no - write new record
.
          ADD       TOTBED,OCBEDS           * update total
          CALL      UPOCCP1
.
.         Update total for room type for a given ward
.
          PACK      KEY9,WWARD,ACTVRTYP,ZZZ
          CALL      RDOCCP1                 * read grand total record
          COMPARE   ZERO,OVRCD              * record on file ?
          CALL      AOCP0000 IF NOT EQUAL   * no - write new record
.
          ADD       TOTBED,OCBEDS           * update total
          CALL      UPOCCP1
.
.         Update total beds for all room types in a given ward
.
          PACK      KEY9,WWARD,ZZZ,ZZZ
          CALL      RDOCCP1                 * read grand total record
          COMPARE   ZERO,OVRCD              * record on file ?
          CALL      AOCP0000 IF NOT EQUAL   * no - write new record
.
          ADD       TOTBED,OCBEDS           * update total
          MOVE      CHNGFLAG,OCCHNG
          CALL      UPOCCP1
.
.         Update total beds for all room types for all wards
.
          PACK      KEY9,ZZZ,ZZZ,ZZZ
          CALL      RDOCCP1                 * read grand total record
          COMPARE   ZERO,OVRCD              * record on file ?
          CALL      AOCP0000 IF NOT EQUAL   * no - write new record
.
          ADD       TOTBED,OCBEDS           * update total
          IF        CHNGFLAG = 1
            MOVE      ONE,OCCHNG
          ENDIF
          CALL      UPOCCP1
.
          MATCH     SP3,WBED                     * ward header record
          IF        @EQUAL
            PACK      KEY6,WWARD,ZZZ             * yes
            CALL      RDSWARD1                   * posn. on next ward header rec
          ENDIF
          GOTO      CBED1000                     * get next ward record
.
CBED9999  RETURN
+
.*********************************************************************
.*                  ADMN0000                    Called by : ARITYPE  *
.*        Work out if admitted in period                             *
.*********************************************************************
ADMN0000  MOVE      ZERO,ADMADD             * clear the add variable
          CMATCH    "C",TMOVE
          GOTO      ADMN1000 IF NOT EQUAL
.
          MATCH     ADATE,DDATE
          GOTO      ADMN9999 IF EQUAL      * daycase
.
          MATCH     TDATE,ADATE
          GOTO      ADMN1000 IF NOT EQUAL
.
          BRANCH    ADMFLG,ADMN9999
          GOTO      ADMN2000
.
ADMN1000  MATCH     ANSA,TMOVE
          GOTO      ADMN9999 IF NOT EQUAL   * not an admission record
.
ADMN2000  MATCH     KFRDATE,TDATE
          GOTO      ADMN9999 IF LESS        * date too early
.
          MATCH     KTTDATE,TDATE
          GOTO      ADMN9999 IF NOT LESS    * date too late
.
          MOVE      ONE,ADMADD              * set the add variable
.
ADMN9999  RETURN
+
.*********************************************************************
.                   CTPB0000
.         Create the Occupancy temp file
.*********************************************************************
CTPB0000  MOVE      ZERO,STATUS
.
          CALL      DTPB0000                * delete old file
.
          PREP      PSTROL,FNAMER
          WRITE     PSTROL,SEQ;"isbuild ",FNAMEI," 145 u1-9" 
          WEOF      PSTROL,SEQ
.
          MOVE      ONE,STATUS
.
          CLEAR     CMDLINE
          APPEND    "sh ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
.         delete rollout file
.
          CLOSE     PSTROL,DELETE
.
          MOVE      ZERO,OPCND
          TRAP      CTPB3000 IF IO
          OPEN      PATOCCP1,FNAMEI
          TRAPCLR   IO
          OPEN      PATOCCP2,FNAMEI
          GOTO      CTPB9999
.
.         isbuild failed
.
CTPB3000  MOVE      ONE,OPCND               * set exit flag
          TRAPCLR   IO
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Temporary Index File ",FNAMEI," not found **    ";
          CALL      HITENTER
.
CTPB9999  RETURN
+
.*********************************************************************
.                   DTPB0000
.         Delete Occupancy temp indexed file (b)
.*********************************************************************
DTPB0000  MOVE      ZERO,STATUS
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
DTPB9999  RETURN
+
.*********************************************************************
.                   DTPA0000
.         Delete temp indexed file (a)
.*********************************************************************
DTPA0000  CLEAR     CMDLINE
          CLOSE     PATMP1XX
          APPEND    "iserase ",CMDLINE
          APPEND    PATFNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
DTPA9999  RETURN
+
.*********************************************************************
.                   CALC
.              Calculate Dates Difference
.*********************************************************************
.
CALC      MOVE      WDATE2,CDYSFDTE
          MOVE      WDATE1,CDYSTDTE
          CALL      CALCDAYS
          MOVE      CDYSDAYS,CALDAYS
          SUB       ONE,CALDAYS        * Only want the diff between the 2 dates
          ADD       CALDAYS,NBRDAYS
          RETURN
.
.*******************************************************************************
.         Get the default charging claim code for multi hospital
.*******************************************************************************
DCLM0000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          IF        IBCNMHOS=1
            OPEN      PATHSPA1,"pathspaf"
            MOVE      PMVXMHOS,KEY3
            CALL      RDPTHSP1
            IF        OVRCD=0
              MOVE      PTHSDCLM,PTCNDCLM     * default claim code charging
            ENDIF
          ENDIF
DCLM9999  RETURN
+
.*********************************************************************
.
.         I/O routines for Temp file (a) - admissions for date range
.
RATMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;;
          RETURN
.
RDTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READKS    PATMP1XX;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READKP    PATMP1XX;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          WRITE     PATMP1XX,KEY8;KEY8
          RETURN
.
UPTMPR1   UPDATE    PATMP1XX;TMPADMN
          RETURN
.
DETMPR1   RESET     KEY8
          DELETE    PATMP1XX,KEY8
          RETURN
+
.
.         I/O for Occupancy temp file (b)
.
WROCCP1   RESET     KEY9
          WRITE     PATOCCP1,KEY9;KEY9,OCBEDS:
                    OCDAYM,OCDAYF,OCDAYA,OCDAYC,OCDAYD,OCDAYT:
                    OCAMTM,OCAMTF,OCAMTA,OCAMTC,OCAMTD,OCAMTT:
                    ADMADLT,ADMCHIL,ADMTOTL,OCCHNG:
                    OCDAYI,OCDAYR,OCDAYU,OCAMTI,OCAMTR,OCAMTU          *C-49016
          RETURN
.
RDOCCP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      PATOCCP1,KEY9;OCWARD,OCROOM,OCTYPE,OCBEDS:
                    OCDAYM,OCDAYF,OCDAYA,OCDAYC,OCDAYD,OCDAYT:
                    OCAMTM,OCAMTF,OCAMTA,OCAMTC,OCAMTD,OCAMTT:
                    ADMADLT,ADMCHIL,ADMTOTL,OCCHNG:
                    OCDAYI,OCDAYR,OCDAYU,OCAMTI,OCAMTR,OCAMTU          *C-49016
          GOTO      OVERCOND IF OVER
          RETURN
.
RDKOCCP1  MOVE      ZERO,OVRCD
          READKS    PATOCCP1;OCWARD,OCROOM,OCTYPE,OCBEDS:
                    OCDAYM,OCDAYF,OCDAYA,OCDAYC,OCDAYD,OCDAYT:
                    OCAMTM,OCAMTF,OCAMTA,OCAMTC,OCAMTD,OCAMTT:
                    ADMADLT,ADMCHIL,ADMTOTL,OCCHNG:
                    OCDAYI,OCDAYR,OCDAYU,OCAMTI,OCAMTR,OCAMTU          *C-49016
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSOCCP1  MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      PATOCCP1,KEY9;;
          GOTO      OVERCOND IF OVER
          RETURN
.
DEOCCP1   RESET     KEY9
          DELETE    PATOCCP1,KEY9
          RETURN
.
UPOCCP1   UPDATE    PATOCCP1;OCWARD,OCROOM,OCTYPE,OCBEDS:
                    OCDAYM,OCDAYF,OCDAYA,OCDAYC,OCDAYD,OCDAYT:
                    OCAMTM,OCAMTF,OCAMTA,OCAMTC,OCAMTD,OCAMTT:
                    ADMADLT,ADMCHIL,ADMTOTL,OCCHNG:
                    OCDAYI,OCDAYR,OCDAYU,OCAMTI,OCAMTR,OCAMTU          *C-49016
          RETURN
.
.         INDEX 2 DOES NOT EXIST IT IS A COPY OF INDEX 1
.
RDOCCP2   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      PATOCCP2,KEY9;*40,PRETOTAL
          GOTO      OVERCOND IF OVER
          RETURN
+
.==============================================================================
.
          INC       STD001IO
.
          INC       GETBFEES
          INC       GTRMTYPE
          INC       DATECONV
          INC       CALCDAYS
          INC       PATAGED
          INC       STEPDOWN
          INC       WDACTVDY
          INC       PATCMSTP
          INC       PATHSPKY
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
          INC       NZPBFEIO/INC
          INC       PATFN1IO/INC
          INC       PATFX1IO/INC
          INC       PATAFEIO/INC       * for casemix
          INC       PATASDIO/INC
          INC       PATHDFIO/INC
          INC       PATHLFIO/INC
          INC       PATHSPIO/INC
          INC       PATLDFIO/INC
          INC       PATLSDIO/INC
.
          INC       PATBFEIO/INC
          INC       PATCODIO/INC
          INC       PATCFAIO/INC
          INC       PATDAYIO/INC
          INC       PATDSCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATRCAIO/INC
          INC       PATTRNIO/INC
          INC       PATWBHIO/INC
          INC       PATWR1IO/INC
          INC       PMSCIDIO/INC
          INC       PMSVX1IO/INC
          INC       PATWRDDS                * Wards scan
          INC       GETCNEFF
...............................................................................

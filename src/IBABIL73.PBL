.******************************************************************************
.*               P R O G R A M  S U M M A R Y                                 *
.*               -------------  -------------                                 *
.*                                                                            *
.*      PROGRAM NAME:     IBABIL73                                            *
.*                                                                            *
.*      FUNCTION:         PATIENT REGISTER                                    *
.*                                                                            *
.*      DATE WRITTEN:     09/08/85                                            *
.*                                                                            *
.*      AUTHOR:           J.TAN         (I.B.A)                               *
.*                                                                            *
.*      MODIFICATIONS:                                                        *
.******************************************************************************
.*        V12.00.01 22/05/2025  Alina Bhari   TSK 0955096                     *
.*                  Added alpanumeric visit number generation -FPRINTB,CHKWAD * 
.*        V11.04.01 17/09/2024  Thanh T       TSK 0949713                     *
.*                  Added Run By Ward functionality for Ward/Bed Sequence     *
.******************************************************************************
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added include SEXDSCIO                                    *
.*        V10.13.01 05/12/2018  Don Nguyen    TSK 0838558                     *
.*                  Deleted temp file (PSTROL) after processing               *
.*        V10.05.01 02/02/2015  Ania P        CAR 261630                      *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V9.02.01  Jill Habasque  SRF 17573                                  *
.*                  Changed to use RTIODAYS instead of NHOSPDAY               *
.*        V5.08.03  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.02  28/08/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.******************************************************************************
.*        V5.08.B01 10/04/2000  Greg Horvat                                   *
.*                  Removed MATADAFD MATADBFD & MATADBIO                      *
.*        V5.07.B02 27/01/1999  Nicole Harrington                             *
.*                  507 rebounds                                              *
.*        V5.07.B01 12/11/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.******************************************************************************
.*        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
.*                  Changed temp file name "pstidx" to standard format        *
.******************************************************************************
. *                       W2.01 30/09/87 G.Williams (I.B.A)                   *
. *                             Fix Religion on report                        *
. *                       W2.02 03.11.87 S.Barcham (I.B.A.)                   *
. *                             Use NHOPSDAY                                  *
. *                       W2.03 09/05/88 M.HAYSE   (I.B.A.)                   *
. *                             MATERNITY                                     *
. *                       W2.04 08/07/88 M.HAYSE   (I.B.A.)                   *
. *                             MATERNITY FIX                                 *
. *                       W2.05 14/07/88 G.Williams (I.B.A)                   *
. *                             Added new file - PATNOBFD                     *
. *                       W2.06 25/07/88 M.HAYSE    (I.B.A)                   *
. *                             ADDRESS & SURNAME OF BABY                     *
. *                       V5.01 29.04.89 S.Barcham  (I.B.A)                   *
. *                             Fix 2353 I * I                                *
. *                       V5.02 15/05/89 J.Tan      (I.B.A)                   *
. *                             Fix I*D       SRF 100543                      * 
. *                       V5.03 16/05/89 DIG                                  *
. *                             Changed to standard and                       *
. *                             modified print for 8 digit                    *
. *                             admission numbers                             *
. *                       V5.04 27/05/89 J.Tan                                * 
. *                             Fixed Mother Adm. to form8                    *
. *                             (PATMI1FD).     SRF 100750                    *
. *                       V5.05 06/06/89 J.Tan                                *
. *                             Mods Locking master file                      *
. *                             (PATIOMAS)     SRF 100809                     *
. *                       V5.06 09.06.89 S.Barcham                            *
. *                             Fixed MATADM includes                         *
. *                             SRF 100916                                    *
. *                       V5.07 17.06.89 S.Barcham                            *
. *                             Compiled for new standbys                     *
. *                             SRF 101101                                    *
. *                    V5.01.01 06/07/89 K.Peak                               *
. *                             Changed Adm & U/R to 8 chrs                   *
. *                    V5.01.02 03/11/89 S.Barcham                            *
. *                             Fix Option 2                                  *
. *                   V5.01.121 27/11/90 Glen Hobby                           *

. *                                                                           *
. *        V5.01.122 03.03.93 Neeriem "I Hate Support" Dye                    *
. *                  Modify to print full address                             *
. *        V5.01.123 09/06/93 J.Tan     SRF 121660                            *
. *                  Mods to check for ward/bed active ind.                   *
. *        V5.01.124 10/02/94 Rob Leonard  SRF 600705                         *
. *                  Fix Ward sequence picking up extra wards                 *
. *                                                                           *
. *****************************************************************************
.
.
          INC       STD001FD
          INC       PATCODFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATHSPFD/INC
          INC       PATWR1FD/INC
          INC       PATNOBFD/INC
          INC       PATDO1FD/INC
          INC       PATTRNFD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.         Temporary index file for report
.
TEMPFILE IFILE         SQL, FIXED=349, KEY="u1-31"
.
.Name        Type      Size   Physical   Start
.----        ----      ----   --------   -----
.PTWDHOSN    DIM        3        3          1      
.PSNAME      DIM        20       20         4
.UNIQUEX     DIM        8        8         24
.WADMNO      DIM        8        8         32
.PSAGE       FORM       3        3         40
.PSEX        DIM        6        6         43
.PADMDATE    DIM        10       10        49
.PHDAYS      FORM       4        4         59
.WWARD       DIM        3        3         63
.WBED        DIM        3        3         66
.DNAME       DIM        30       30        69
.ADIAG40     DIM        40       40        99
.PNAME       DIM        30       30       139
.PADD1       DIM        35       35       169
.PPENNO29    DIM        29       29       204
.PADD2       DIM        35       35       233
.PSUBRB      DIM        35       35       268
.PPOST       DIM        8        8        303
.PREG        DIM        3        3        311
.PADD4       DIM        35       35       314
.                                EOR      349
.
.               WORK AREA
.
DIM4     DIM      4
.
PSKIP    FORM    1
DPSKIP   DIM     3
.
DALPHA   DIM     6
DNAME    DIM     30
DNAMEDIS DIM     13
.
FRSTTIME FORM    1 
.
RTDAYS   FORM     4
SAVDATE  DIM      8
SAVTYPE  DIM      1
SAHOSP   DIM      3
SAWARD   DIM      3
SKEY30   DIM      30
.
CODE     DIM     2
CODEC    INIT    "C "
CODEM    INIT    "M "
.
ADIAG40  DIM     40
PPENNO29 DIM     29
.
.
PADD120  DIM     20
PADD220  DIM     20
PNAME22  DIM     22
PNAME    DIM     30
.
PSTROL   FILE      ASCII, FIXED=256
FNR      INIT    "pstrol"
FNT      INIT    "bil73a"
FNAMET   DIM       8
FNAMER   DIM       8
.
OPCND    FORM      1
STATUS   FORM      1
CMDLINE  DIM       50
UNIQUE   FORM      8
UNIQUEX  DIM       8
.
FIVE5    FORM    "55"
.
.      EXTRA VARIABLES
.
WRDESC   DIM     20
CDESC    DIM     20
PHDAYS   FORM     4
DHHMM    DIM      5
MIN1     FORM     1
.
.        WORK AREA
.
CALDAYS  FORM    4
CALDAY   FORM    3
CJDAY    FORM    3           * JULIAN CURRENT DAY
AJDAY    FORM    3           * JULIAN ADMISSION DAY
FROMDATE DIM     8
JDAYS    FORM    3
JYEAR    FORM    2
LYEAR    FORM    "366"
NBRDAYS  FORM    4
OYEAR    FORM    "365"
TODATE   DIM     8
WDATE1   DIM     8
WDATE2   DIM     8
PADMDATE DIM     10
.
NAME     DIM     22         * MFNAME.MSNAME
DMSTAT   DIM     9
.
DSEX     DIM     8                                            * was 6  *C-49016
.
SHOSP    DIM     3
SHOSPNAM DIM     50
SWARD    DIM     3
SBED     DIM     3
INHOSP   DIM     3
INWARD   DIM     3
.
IBCNMHOS  FORM      1                  * Multi hospital indicator from controlf
.
DDATE    INIT    "      "
.
ZTHREE    INIT      "ZZZ"     * For positioning at start of next ward
.
PRGID     INIT      "IBABIL73"
PRGNAM    INIT      "Patient Register"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
          OPEN      PATWR1A5,"patwr1af"
.
          DISPLAY   *P64:24,"patnobef";
          OPEN      PATNOBE1,"patnobef"
          DISPLAY   *P64:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMET
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
+
......... START OF PROGRAM
.
......... SELECT OPTION
.
START     MOVE      ZERO,CPAGENO
          MOVE      0,FRSTTIME
.
OPT2      DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Patient Surname Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Ward/Bed Sequence"
.
          KEYIN     *P1:8,"Option : ",*EL,*V2LON,OPTION
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
BRNCH     BRANCH    OPTION OF SURNAM,KWARD
          BEEP
          GOTO      OPT2
+
.
......... ACCESS VIA PATIENT SURNAME
.
SURNAM    MOVE      SP70,SAHOSP
          MOVE      SP70,SAWARD
          MOVE      SP70,INHOSP
          MOVE      SP70,INWARD
.
          CALL      KHOS0000                * Check which hospital
          BRANCH    EXIT,SURNAM
.
          PACK      INHOSP,KEY3,SP70
          IF        IBCNMHOS = 1
            PACK      KEY9,INHOSP,SP70
          ELSE
            PACK      KEY6,SP70
          ENDIF
.
          CALL      KILLIDX
          CALL      CREAIDX
.
          OPEN      TEMPFILE,FNAMET
          MOVE      ONE TO UNIQUE
.
          GOTO      ENDS1Y
+
.
......... PRINT VIA WARD NUMBER
.
KWARD     MOVE      SP70,SAHOSP
          MOVE      SP70,SAWARD
          MOVE      SP70,INHOSP
          MOVE      SP70,INWARD 
.
          CALL      KHOS0000                * Check which hospital
          BRANCH    EXIT,KWARD
.
          PACK      INHOSP,KEY3,SP70
.
          IF        IBCNMHOS = 1
            MATCH     SP70,INHOSP
            GOTO      ALLWS IF EQUAL
          ENDIF
.
KWARD1    KEYIN     *P1:18,"Enter Ward Number : ":
                    *V2LON,INWARD;
.
          PACK      INWARD,INWARD,SP70
          PACK      KEY9,INHOSP,INWARD,SP70
.
          MATCH     SP70,INWARD
          GOTO      ONEHWARD IF EQUAL          
.
          IF        IBCNMHOS = 1
            PACK      KEY9,INHOSP,INWARD,SP70
            CALL      RDWARD5
          ELSE
            PACK      KEY6,INWARD,SP70
            CALL      RDWARD1
          ENDIF
          IF        OVRCD = 0
            GOTO      KWARD2
          ENDIF
.
          IF        IBCNMHOS = 1
            CALL      RDSWARD5
            CALL      RDKWARD5                * read first ward record
          ELSE
            CALL      RDSWARD1
            CALL      RDKWARD1               * read first ward record
          ENDIF
          BRANCH    OVRCD,NOTEXIST
.
          MATCH     INHOSP,PTWDHOSN
          GOTO      NOTEXIST IF NOT EQUAL
.
          MATCH     INWARD,WWARD
          GOTO      NOTEXIST IF NOT EQUAL
.
KWARD2    BRANCH    WACTIVE,NOTACTIV             * Igonore non-active wards
.
          MOVE      WBDESC,WRDESC
          DISPLAY   *P40:18,*EL,*V2LON,WRDESC
.
......... If this is a Ward Only Ward, then loop through the No-Bed file
.
          BRANCH    WINPUT,GOTONE
          GOTO      ENDS1Y
.
NOTEXIST  KEYIN     *P1:24,*EL,*B,*V2LON,"** Ward does not exist **   ";
          CALL      HITENTER
          GOTO      KWARD
.
NOTACTIV  KEYIN     *P1:24,*EL,*B,*V2LON,"** Ward is not active **   ";
          CALL      HITENTER
          GOTO      KWARD
.
. All hospitals and all wards
.
ALLWS     CALL      KNEWPA00
          IF        IBCNMHOS = 1
            MOVE      SP70,KEY9
            CALL      RDSWARD5
          ELSE
            MOVE      SP70,KEY6
            CALL      RDSWARD1
          ENDIF
          GOTO      NEXTA
.
. One oospital & all wards
.
ONEHWARD  CALL      KNEWPA00     
.
ENDS1Y    MOVE      "60",CLNO
.
          IF        IBCNMHOS = 1
            CALL      RDWARD5
          ELSE
            CALL      RDWARD1
          ENDIF
          IF        OVRCD = 0
            GOTO      NEXTA2
          ENDIF
.
NEXTA     IF        IBCNMHOS = 1
            CALL      RDKWARD5
          ELSE
            CALL      RDKWARD1
          ENDIF
          BRANCH    OVRCD OF SORTD
.
NEXTA2    MATCH     SP70,INHOSP
          IF        !@EQUAL
            MATCH     INHOSP,PTWDHOSN
            GOTO      SORTD IF NOT EQUAL           
          ENDIF
.
NEXTA3    MATCH     SP70,INWARD
          IF        !@EQUAL
            MATCH     INWARD,WWARD
            GOTO      SORTD IF NOT EQUAL
          ENDIF 
.
          BRANCH    WACTIVE,NXTWARD          * Ignore non-active wards
.
          MATCH     SP70,SAWARD
          GOTO      ALLWRDS IF EQUAL
.
          MATCH     SAWARD,WWARD
          GOTO      SORTD IF NOT EQUAL
.
ALLWRDS   MATCH     SP3,WBED
          GOTO      CHKWAD IF NOT EQUAL
.
          MOVE      WBDESC,WRDESC
.
GOTONE    PACK      KEY13 WITH WWARD,SP20
          CALL      RDSNOBE1
NOBLP     CALL      RDKNOBE1
          BRANCH    OVRCD OF NEXTA
.
          MATCH     WWARD,NBWARD
          GOTO      NEXTA IF NOT EQUAL
.
          MOVE      NBADMNO,WADMNO
          MOVE      NBPLUR,WPLUR
          CALL      GETDATA
          GOTO      NOBLP
.
ENDREG    MOVE      WWARD,SWARD
          GOTO      NEXTA
.
CHKWAD    MATCH     ZEROVISN,WADMNO
          GOTO      MTBED IF EQUAL
.
          CALL      GETDATA
          GOTO      NEXTA
.
......... Ward read was inactive, so skip bed records and get next ward
.
NXTWARD   MATCH     SP3,WBED                 * Is this a bed record ?
          GOTO      NEXTA IF NOT EQUAL       * Yes - do not skip to next ward
          IF        IBCNMHOS = 1
            PACK      KEY9,PTWDHOSN,WWARD,Z70  * Position after last bed
            CALL      RDSWARD5                 * record for this ward
          ELSE
            PACK      KEY6,WWARD,Z70  * Position after last bed
            CALL      RDSWARD1
          ENDIF  
          GOTO      NEXTA                    * Read next ward
.
MTBED     BRANCH    OPTION OF NEXTA,PRTMTY
.
PRTMTY    CALL      NRMWRD
          GOTO      NEXTA
.
......... Subroutine to process an admission number
.
GETDATA   MOVE      WADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF LOSTDAT1
.
          MOVE      AURNO,KEY8            *** ADDRESS & SURNAME OF MUM
          CALL      RDMAST1
.
          MOVE      PTITL,DIM4
          PACK      PNAME WITH DIM4,SP1,PGNAME
.
          MOVE      ADIAG1,ADIAG40
.
GPENNO    MOVE      SP30,PPENNO29
          MATCH     SP6,PPENNO
          GOTO      KEYDOC IF EQUAL
.
          CLEAR     PPENNO29
          APPEND    "PENSION ## : ",PPENNO29
          APPEND    PPENNO,PPENNO29
          RESET     PPENNO29
.
KEYDOC    MOVE      SP30,DNAME
          MOVE      SP30,DNAMEDIS
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF NODOCA
.
          MOVE      DGNAME,ANS
          PACK      DNAME,ANS,DOT,DSNAME,SP70
          MOVE      DNAME,DNAMEDIS
          MOVE      DSNAME,DALPHA
.
......... RELIGION OPTION TEST
.
NODOCA    MOVE      SP9,DMSTAT
          PACK      KEY5 WITH CODEM,PMSTAT
          CALL      RDCODE1
          BRANCH    OVRCD OF GETHSP
          MOVE      TDESC,DMSTAT
.
GETHSP    CALL      HOSPDAY
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DSEX
.
ENDSEX    DISPLAY   *P27:24,*V2LON,WWARD,*P46:24,WBED
          BRANCH    OPTION OF SRTNAME,NRMWRD
.
SRTNAME   PACK      KEY31,PTWDHOSN,PSNAME,UNIQUE,SP70
          MOVE      UNIQUE,UNIQUEX
          WRITE     TEMPFILE,KEY31;PTWDHOSN,PSNAME,UNIQUEX,WADMNO,PSAGE,PSEX:
                             PADMDATE,PHDAYS,WWARD,WBED,DNAME:
                             ADIAG40,PNAME,PADD1,PPENNO29,PADD2:
                             PSUBRB,PPOST,PREG,PADD4
          ADD       ONE TO UNIQUE
.
          RETURN
.
NRMWRD    COMPARE   FIVE5,CLNO
          CALL      HEAD IF NOT LESS
.
          MATCH     WWARD,SWARD
          GOTO      FPRINTA IF EQUAL
.
........ WE MUST CHECK IF A WARD PER PAGE
.
          BRANCH    PSKIP OF FPRINT0
FPRINT    CALL      HEAD
          GOTO      FPRINTA
.
FPRINT0   COMPARE   "49" TO CLNO
          GOTO      FPRINT IF NOT LESS
.
          CALL      HEAD12
.
......... ZERO ADMISSION NUMBER INDICATES BED IS EMPTY
.
FPRINTA   CALL      DISPDA1
          RETURN
.
......... GET RID OF TRAILING BLANKS
.
BUMPPN    CMATCH    SP1,PNAME            *** PATIENT
          RETURN    IF NOT EQUAL
          BUMP      PNAME BY -1
          RETURN    IF EOS
          GOTO      BUMPPN
.
SORTD     BRANCH    OPTION OF SORTIT,ENDP
.
SORTIT    DISPLAY   *P1:24,*EL,*P20:24,"** PROCESSING STATISTICS **",*W;
.
          CLOSE     TEMPFILE
          GOTO      SORTFI
.
NOOPEN    MOVE      ONE,OPCND
          DISPLAY   *P1:24,*B,*EL,"** TEMPORARY STATISTICS  FILE ":
                                 FNAMET," NOT FOUND **",*W2;
          RETURN
.
SORTFI    TRAP      NOOPEN IF IO
          OPEN      TEMPFILE,FNAMET
          TRAPCLR   IO
.
          DISPLAY   *P1:7,*EF
.
          BRANCH    OPCND OF ENDP
.
          DISPLAY   *P1:24,*EL,*P20:24,"Patient Name :";
          MOVE      SP70,KEY31
          READ      TEMPFILE,KEY31;;
.
READLOP   READKS    TEMPFILE;PTWDHOSN,PSNAME,UNIQUEX,WADMNO,PSAGE,PSEX:
                             PADMDATE,PHDAYS,WWARD,WBED,DNAME:
                             ADIAG40,PNAME,PADD1,PPENNO29,PADD2:
                             PSUBRB,PPOST,PREG,PADD4
          GOTO      ENDP IF OVER
.
          MOVE      DNAME,DNAMEDIS
          DISPLAY   *P40:24,*EL,*V2LON,PSNAME
.
          CALL      DISPDA2
          GOTO      READLOP
.
NOWARD    DISPLAY   *P20:24,*EL,*B,*V2LON:
                    "** No Available Data on File **",*W2;
          GOTO      START
.
INVWARD   DISPLAY   *P20:24,*EL,*B,*V2LON:
                    "** Ward does not Exist **",*W2;
          GOTO      START
+
.
......... OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NOWARD IF EQUAL
          CALL      PAGEND
          PRINT     *N,"  ***  END OF REPORT  ***"
          DISPLAY   *P40:24,*EL,*V2LON:
                    "** Report Generation Completed **",*W2;
          BRANCH    OPTION TO ENDP1,START
          GOTO      START
.
ENDP1     CALL      KILLIDX
          GOTO      START
.
LOSTDAT1  DISPLAY   *P1:23,*EL,*V2LON:
                    "*** Ward/Bed ",*V2LON,WWARD,SLASH,WBED,*HOFF:
                    " has invalid Admission ",*V2LON,WADMNO,*HOFF," ***"
.
          IF        FRSTTIME = 0
            CALL      HEAD
          ELSE    
            COMPARE   FIVE5,CLNO
            CALL      HEAD IF NOT LESS
          ENDIF
.
          PRINT     *N,"*** Ward/Bed ",WWARD,SLASH,WBED:
                    " has invalid Admission ",WADMNO," ***":
                    " Contact I.B.A Immediately  ***",*N
          ADD       THREE,CLNO
          RETURN
.
+
......... PRINT ACTUAL DATA
.
DISPDA1   COMPARE   FIVE5,CLNO
          CALL      HEAD IF NOT LESS
.
          MATCH     WWARD,SWARD
          GOTO      FPRINTB IF EQUAL
.
          CALL      HEAD12
.
FPRINTB   MATCH     ZEROVISN,WADMNO
          GOTO      EMPBED1 IF EQUAL
.
          CALL      PRLINE
          MOVE      WWARD,SWARD
          MOVE      PTWDHOSN,SHOSP
          RETURN
+
.
......... PRINT ACTUAL DATA VIA SURNAME
.
DISPDA2   COMPARE   FIVE5,CLNO
          CALL      HEAD2 IF NOT LESS
.
          IF        IBCNMHOS = 1
            MATCH     SHOSP,PTWDHOSN
            CALL      HEAD2 IF NOT EQUAL
          ENDIF    
.
PRLINE    CALL      PAGEND
          PRINT     *1,"|",WADMNO,*10,"|",PREG,*18,"|",PSNAME:
                    *63,"|",PSAGE:
                    *67,"|",SP1,WWARD,SLASH,WBED:
                    *76,"|",DNAMEDIS:
                    *90,"|",SP1,ADIAG40,*132,"|":
                    *N,"|",*10,"|",*18,"|",PNAME:
                    *63,"| ",PSEX:
                    *67,"| ",PHDAYS:
                    *76,"| ",PADMDATE:
                    *90,"| ",*132,"|":
                    *N,"|",*10,"|",*18,"|",PADD1:
                    *63,"|",*67,"|",*76,"|":
                    *90,"| ",PPENNO29,*132,"|":
                    *N,"|",*10,"|",*18,"|",PADD2:
                    *63,"|",*67,"|",*76,"|":
                    *90,"| ",*132,"|":
                    *N,"|",*10,"|",*18,"|",PSUBRB:
                    *63,"|",*67,"|",*76,"|":
                    *90,"| ",*132,"|":
                    *N,"|",*10,"|",*18,"|",PADD4," ",PPOST:
                    *63,"|",*67,"|",*76,"|":
                    *90,"| ",*132,"|"
          ADD       "6",CLNO
          RETURN
+
......... EMPTY BED PRINTOUT
.
EMPBED1   CALL     PAGEND
          PRINT    *1,"|",*10,"|",*18,"| ":
                   *41,"|",*45,"|",*49,"| ",WWARD,SLASH,WBED:
                   *58,"|",*70,"|":
                   *76,"|",*90,"| ** EMPTY BED **",*132,"|":
                   *N,"|",*10,"|",*18,"| ":
                   *41,"|",*45,"|",*49,"|":
                   *58,"|",*70,"|":
                   *76,"|",*90,"| ",*132,"|"
.
          MOVE      WWARD,SWARD
          MOVE      PTWDHOSN,SHOSP
          ADD       THREE,CLNO
          RETURN
+
.
......... LAST LINE ON THE CODE
.
PAGEND    PRINT     *1,"*-----------------------------------------":
                       "-------------------------------------------":
                       "----------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
......... HEADINGS FOR OUTPUT
.
HEAD      BRANCH    OPTION OF HEAD2,HEAD1
.
HEAD1     COMPARE   ZERO,CPAGENO
          CALL      PAGEND IF NOT EQUAL
          MOVE      ONE,CNOUNDLN
          MOVE      "WARD/BED SEQUENCE",CPHDROPT
          CALL      IBACLOCK
          CALL      HEAD132
.
          MOVE      ONE,FRSTTIME 
.
          MOVE      FOUR,CLNO
          GOTO      HEAD13
.
HEAD12    CALL      PAGEND
          ADD       ONE,CLNO
.
HEAD13    COMPARE   ONE,IBCNMHOS
          IF        @EQUAL
            MATCH     SHOSP,PTWDHOSN
            IF        !@EQUAL
              PACK      KEY3,PTWDHOSN,SP70
              CALL      RDPTHSP1
              IF        OVRCD = 1
                MOVE      KEY3,PTHSNAME      
              ENDIF
              MOVE      PTHSNAME,SHOSPNAM  
            ENDIF
            PRINT     *N,*40,"Hospital : ",PTWDHOSN,*55,SHOSPNAM
            ADD       ONE,CLNO
          ENDIF
.
          PRINT     *40,"Ward     : ",WWARD,*55,WRDESC,*N
          ADD       THREE,CLNO
.
          CALL      PAGEND
.
          PRINT     *1,"| Admiss",*10,"| Relig.":
                    *18,"|Patients Name",*63,"|Age":
                    *67,"|Ward/Bed",*76,"| Att. Doctor":
                    *90,"| Diagnosis / Pension Number",*132,"|":
                    *N,"| Number",*10,"|":
                    *18,"|",*63,"|Sex":
                    *67,"|Days Hos",*76,"| Admiss Date",*90,"|",*132,"|"
          ADD       TWO,CLNO
.
          MOVE      WWARD,SWARD
          MOVE      PTWDHOSN,SHOSP
          RETURN
+
.
......... HEADINGS FOR OUTPUT
.
HEAD2     MOVE      ONE,FRSTTIME
          COMPARE   ZERO,CPAGENO
          CALL      PAGEND IF NOT EQUAL
          MOVE      ONE,CNOUNDLN
          MOVE      "SURNAME SEQUENCE",CPHDROPT
          CALL      IBACLOCK
          CALL      HEAD132
.
          MOVE      FOUR,CLNO
.
          IF        IBCNMHOS = 1
            MATCH     SHOSP,PTWDHOSN
            IF        !@EQUAL
              PACK      KEY3,PTWDHOSN,SP70
              CALL      RDPTHSP1
              IF        OVRCD = 1
                MOVE      KEY3,PTHSNAME
              ENDIF
              MOVE      PTHSNAME,SHOSPNAM
              MOVE      PTWDHOSN,SHOSP  
            ENDIF
            PRINT     *N,*40,"Hospital : ",PTWDHOSN,*55,SHOSPNAM
            ADD       ONE,CLNO
          ENDIF
.
          CALL      PAGEND
.
          PRINT     *1,"| Admiss",*10,"| Relig.":
                    *18,"|Patients Name",*63,"|Age":
                    *67,"|Ward/Bed",*76,"| Att. Doctor":
                    *90,"| Diagnosis / Pension Number",*132,"|":
                    *N,"| Number",*10,"|":
                    *18,"|",*63,"|Sex":
                    *67,"|Days Hos",*76,"| Admiss Date",*90,"|",*132,"|"
          ADD       TWO,CLNO
          RETURN
+
......... READ TRANSFER FILE
.
HOSPDAY   MOVE      SP8,SAVDATE
          MOVE      SP1,SAVTYPE
          MOVE      ZERO TO PHDAYS
          UNPACK    ADATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      PADMDATE WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          PACK      FROMDATE WITH XCENT,XYEAR,XMON,XDAY
          REP       " 0" IN FROMDATE
          PACK      TODATE WITH CCC,CYY,CMM,CDD
          REP       " 0" IN TODATE
          CALL      RTIODAYS
          MOVE      NBRDAYS TO PHDAYS
          RETURN
+
......... Deleting  AN INDEXED FILE BASED ON PORT
.
KILLIDX   MOVE      ZERO,STATUS
.
          CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P1:9,*EF,*P1:9
          RETURN
.
CREAIDX   PREP      PSTROL,FNAMER
          WRITE     PSTROL,SEQ;"isbuild ",FNAMET," 349 u1-31"
          WEOF      PSTROL,SEQ
.
          MOVE      ONE,STATUS
          DISPLAY   *P1:13,*EF,*P1:13
.
          CLEAR     CMDLINE
          APPEND    "sh ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          BRANCH    STATUS TO INDOK
.
          MOVE      ONE,OPCND
          DISPLAY   *P20:24,*B,*V2LON:
                    "** The Index File Not Created **",*W3;
.
INDOK     DISPLAY   *P1:13,*EF,*P1:13
.
          CLOSE     PSTROL,DELETE
          RETURN
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:13,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TEN3,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  DISPLAY   *P20:13,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
.
KNEWPA00  MOVE      ZERO,PSKIP
          KEYIN     *P1:20,"Would you like each Ward ":
                    "on a separate Page (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS;
.
          CMATCH    ANSY,ANS
          GOTO      KNEWPA99 IF EQUAL    
.
          MOVE      ONE,PSKIP
.
KNEWPA99  RETURN
.
          INC       STD001IO
          INC       SEXDSCIO
          INC       PATHSPKY
.
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATDO1IO/INC
          INC       PATHSPIO/INC
          INC       PATWR1IO/INC
          INC       PATNOBIO/INC
          INC       PATTRNIO/INC
          INC       PATWRDKY
          INC       RTIODAYS
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.
ENDIT     CHAIN     PGM
          STOP

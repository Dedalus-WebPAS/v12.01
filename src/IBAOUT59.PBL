.**********************************************************************
.* System   :  OUTPATIENTS                                            *
.* Program  :  IBAOUT59                                               *
.* Desc     :  CANCELLED CLINIC REPORT                                *
.**********************************************************************
.* Date     :  22/05/90                                               *
.* Author   :  Graeme Williams                                        *
.* Comments :  Provide a report showing all sessions within a specific*
.*          :  date range that have been cancelled.....               *
.*          :                                                         *
.* Changes  :                                                         *
.*        V10.07.01 30/10/2015  Ebon Clements  CAR 268970             *
.*                  Change to use OUTCLIFD index 1 instead of index 2 *
.*        V5.08.01  19/09/2000  Steve Armstrong   srf 647263          *
.*                  Recompiled for changes to OUTDSCLN                *
.*        V5.08.B01 14/03/2000  Steve Armstrong   5.08 Mods           *
.*                  Mods for changes to OUTCLIFD (effective date)     *
.*                                                                    *
.**********************************************************************
.
.$$$$$
. Cancelled Clinic Report (IBAOUT59)
.___________________________________
.
. - Initialization
.        - Display Header
.        - Open Files 
.               PATCODE1
.               PATDO1A1
.               CONTROLF
.               OUTCLIA1
.               OUTCSLA1
.               OUTCSLA2
.               OUTCTYP1
.
. - Processing Options
.        - 0. Exit
.        - 1. All clinics for a given date range
.        - 2. Single clinic for a given date range
.
.        - Determine Report Headers By Option
.
. - Processing Loop : Option 1
.
.        - Ask for and confirm a range of dates
.        - Loop through the 2nd index of OUTCSLFD
.        - For each record found, display the applicable data
.        - When the screen is full, ask if they want the next page, first page,
.          exit, print, or different date range
.        - If they want to print, call a routine to loop through the 2nd index
.          of OUTCSLFD, and for each record found, print the same details that
.          were being displayed.
.
. - Processing Loop : Option 2
.
.        - Ask for and confirm a range of dates and a clinic id.
.        - Loop through the 1st index of OUTCSLFD
.        - For each record found, display the applicable data
.        - When the screen is full, ask if they want the next page, first page,
.          exit, print, or different date range
.        - If they want to print, call a routine to loop through the 1st index
.          of OUTCSLFD, and for each record found, print the same details that
.          were being displayed.
.
.$$$$$
.
          INC       STD001FD
          INC       PATCODFD/INC        * patient codes file
          INC       PATDO1FD/INC        * patient doctor file
          INC       OUTCLIFD/INC        * outpatients clinic file
          INC       OUTCSLFD/INC        * outpatients cancelled sessions file
          INC       OUTCTYFD/INC        * outpatients clinic type file
.
.**********************************************************************
.*                         CONSTANTS                                  *
.**********************************************************************
.
CODECB    INIT      "CB"
.
.**********************************************************************
.*                         GLOBAL VARIABLES                           *
.**********************************************************************
.
CLINIC    DIM       6
.
DIM40     DIM       40
DSPDESC   DIM       23
.
FROMDATE  DIM       8 
.
.
NUMRECS   FORM      5
.
TODATE    DIM       8  
.
UNKCLIN   INIT      "Unknown Clinic : "
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAOUT59"
PRGNAM    INIT      "Cancelled Clinic Report"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.*                      controlling logic                             *
.**********************************************************************
ML0000    CALL      INIT0000                      * intialization
.
.         Select the option they wish to run
.
.            Option 1 : All cancelled sessions
.
.            Option 2 : Cancelled sessions for a single clinic id
.
ML0100    CALL      OPTN0000                      * get the option
          BRANCH    EXIT,ML9999                   * exit program
          BRANCH    OPTION,ML1000,ML2000
.
. ------  Option 1: All cancelled sessions in a given date range  ------
.
ML1000    CALL      DATE0000                      * Date Ranges 
          BRANCH    EXIT,ML0100                   * Nothing input
.
          CALL      CONTQST                       * ok to continue
          BRANCH    CEXIT,ML1100,ML1000,ML0100
.
.         Display the selected sessions
.
ML1100    CALL      DALL0000                      * display cancelled sessions
          BRANCH    EXIT,ML1000,ML0100,ML1200
.
.         Print the selected sessions
.
ML1200    CALL      PALL0000                      * print cancelled sessions
          GOTO      ML1100
.
. ------  Option 2: All cancelled sessions for a given clinic id  ------
.
ML2000    CALL      DATE0000                      * Date Ranges 
          BRANCH    EXIT,ML0100                   * Nothing input
.
ML2100    CALL      CLIN0000                      * Get the clinic id
          BRANCH    EXIT,ML2000                   * nothing input
.
          CALL      CONTQST                       * ok to continue
          BRANCH    CEXIT,ML2200,ML2000,ML0100
.
.         Display the selected sessions
.
ML2200    CALL      DCLI0000                      * display cancelled sessions
          BRANCH    EXIT,ML2000,ML0100,ML2300
.
.         Print the selected sessions
.
ML2300    CALL      PCLI0000                      * print cancelled sessions
          GOTO      ML2200
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*                   call header routine and open files               *
.**********************************************************************
INIT0000  CALL      DISPHEAD                        * display header
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes" 
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af" 
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"             
.
          PACK      CFNAME,UID,FILCLIA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME,UID,FILCSLA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCSLA1,CFNAME
.
          PACK      CFNAME,UID,FILCSLA2
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCSLA2,CFNAME
.
          PACK      CFNAME,UID,FILCTYA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCTYA1,CFNAME
.
INIT9999  RETURN
+
.**********************************************************************
.*                           OPTN0000                                 *
.*                    select option screen                            *
.**********************************************************************
OPTN0000  HLINE     *G37,2,52,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = All clinics for a date range":
                    *P1:6,*V2LON,TWO,*HOFF," = Single clinic for a date range":
                    *P1:8,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*DV,UNDLN2:
                    *P17:8,*V2LON,OPTION:
                    *P17:8,*DV,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
.
          BRANCH    OPTION,OPTN1100,OPTN1200
          BEEP
          GOTO      OPTN1000
.
.-------- determine header -----
.
OPTN1100  DISPLAY   *P52:2,*V2LON,"- All clinics ";
          MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN1200  DISPLAY   *P52:2,*V2LON,"- Single clinic ";
          MOVE      ZERO,EXIT
          GOTO      OPTN9999
. 
.         Exit option selected
.
OPTN9000  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.***************************************************************************
.*                               DATE0000                                  *
.*                          accept date range                              *
.***************************************************************************
.
.---------- keyin first date range -------
.
DATE0000  MOVE      TEN7,CCOL                       * set up keyin date
          MOVE      FOUR,CVERT
          MOVE      ZERO,CDEFDTE
.
          DISPLAY   *P1:4,*EF,"Date Range    :":
                    *P27:4," to ";
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
.
          CALL      KEYDATE                        * call keyin date routine
          BRANCH    OVRCD,DATE9000                 * nothing input
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
.---------- keyin second date range -------
.
          MOVE      "31",CCOL                       * set up keyin date 
.
          MOVE      CDD,CYEAR
          MOVE      CMM,CYEAR
          MOVE      CYY,CYEAR
          MOVE      CCC,CCENT
.
          CALL      KEYDATE                         * call keyin date routine
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
.         Check for a valid range
.
          MATCH     FROMDATE,TODATE
          GOTO      DATE8000 IF NOT LESS
.
          DISPLAY   *P1:24,*B,"Invalid date range. ";
          CALL      HITENTER
          GOTO      DATE0000
.
.         Valid date range input
.
DATE8000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
.         Nothing input
.
DATE9000  MOVE      ONE,EXIT
.
DATE9999  RETURN
.
.***************************************************************************
.*                               CLIN0000                                  *
.*                          accept clinic id                               *
.***************************************************************************
.
CLIN0000  DISPLAY   *P1:5,*EF,"Clinic id     :"
.
CLIN1000  KEYIN     *P17:5,*DV,UNDLN6:
                    *P17:5,*V2LON,CLINIC:
                    *P17:5,*DV,CLINIC
.
          CALL      VCLI0000                 * validate the clinic id
          BRANCH    EXIT,CLIN1000:           * invalid code input
                         CLIN9000:           * nothing input
                         CLIN4000            * "?" entered
.
.         Valid code input
.
          CALL      CDSC0000                 * Get the clinic description
.
          DISPLAY   *P31:5,OCADESC
          MOVE      ZERO,EXIT
          GOTO      CLIN9999
.
.         "?" option entered
.
CLIN4000  MOVE      ONE,CNEWDS
          CALL      OUTDSCLN                 * Display available clinic's
.
          DISPLAY   *P1:24,*EL,"Clinic id     :"
.
CLIN5000  KEYIN     *P17:24,*DV,UNDLN6:
                    *P17:24,*V2LON,CLINIC:
                    *P17:24,*DV,CLINIC
.
          CALL      VCLI0000                 * validate the clinic id
          BRANCH    EXIT,CLIN5000:           * invalid code input
                         CLIN7000:           * nothing input
                         CLIN4000            * "?" entered
.
.         Valid code input. Re-display the screen
.
          CALL      CDSC0000                 * Get the clinic description
          CALL      RDSP0000                 * redisplay the screen
          MOVE      ZERO,EXIT
          GOTO      CLIN9999
.
.         Nothing input after a "?"
.
CLIN7000  CALL      RDSP0000                 * redisplay the screen
          MOVE      ONE,EXIT
          GOTO      CLIN9999
.
.         Nothing input
.
CLIN9000  MOVE      ONE,EXIT
.
CLIN9999  RETURN
.
.***************************************************************************
.*                               VCLI0000                                  *
.*                          validate clinic id                             *
.***************************************************************************
.
VCLI0000  ENDSET    CLINIC
          APPEND    SP6,CLINIC
          RESET     CLINIC
.
          MATCH     SP6,CLINIC
          GOTO      VCLI8000 IF EQUAL        * nothing input
.
          CMATCH    QUEST,CLINIC
          GOTO      VCLI8500 IF EQUAL        * "?" input
.
          PACK      KEY20,IDDD,CLINIC,FROMDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     IDDD,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     CLINIC,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          BRANCH    OVRCD,VCLI9000           * invalid input
.
          MOVE      ZERO,EXIT                * valid code
          GOTO      VCLI9999
.
VCLI8000  MOVE      TWO,EXIT                 * nothing input
          MOVE      SP30,OCADESC             * clear description
          GOTO      VCLI9999
.
VCLI8500  MOVE      THREE,EXIT               * "?" input
          GOTO      VCLI9999
.
VCLI9000  DISPLAY   *P1:24,*B,*EL,"Invalid clinic id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT                 * invalid input
.
VCLI9999  RETURN
.
.***************************************************************************
.*                               CDSC0000                                  *
.*                          get clinic description                         *
.***************************************************************************
.
CDSC0000  MATCH     SP30,OCADESC
          GOTO      CDSC9999 IF NOT EQUAL
.
          MOVE      OCADOCT,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,CDSC9999
.
          MOVE      DTITL,PACTITLE
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      TWO,PACFRMT
.
          CALL      PACNAME
.
          MOVE      PACFNAME,OCADESC
.
CDSC9999  RETURN
.
.***************************************************************************
.*                               RDSP0000                                  *
.*                          redisplay after "?" option                     *
.***************************************************************************
.
RDSP0000  UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P1:4,*EF,"Date Range    :":
                    *P17:4,*V2LON,CPCDATE,*HOFF," to ";
.
          UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P31:4,*V2LON,CPCDATE,*HOFF:
                    *P1:5,"Clinic id     :",*P17:5,*V2LON,CLINIC,*HOFF:
                    *P31:5,OCADESC
.
RDSP9999  RETURN
.
.***************************************************************************
.*                               DALL0000                                  *
.*                Display all the cancelled sessions in the date range     *
.***************************************************************************
.
DALL0000  PACK      KEY27,IDDD,FROMDATE,SP30
          CALL      RSOTCSL2
          CALL      RKOTCSL2
          BRANCH    OVRCD,DALL9000           * nothing found
.
          MATCH     IDDD,OTCSSITE            * Correct site ?
          GOTO      DALL9000 IF NOT EQUAL
.
          MATCH     OTCSDATE,TODATE          * Correct date ?
          GOTO      DALL9000 IF LESS
.
.         We have at least one record
.
          DISPLAY   *P1:6,*EF,*ULON,"Clinic Id                     ":
                    *P32:6,"Clinic Date":
                    *P44:6,"Clinic Type":
                    *P56:6,"Reason for Cancellation"
          MOVE      SEVEN,CVERT
          MOVE      ZERO,CPAGENO             * current page number
.
.         Loop through the file and display the records
.
          PACK      KEY27,IDDD,FROMDATE,SP30
          CALL      RSOTCSL2
DALL1000  CALL      RKOTCSL2
          BRANCH    OVRCD,DALL8000           * finished file
.
          MATCH     IDDD,OTCSSITE            * Correct site ?
          GOTO      DALL8000 IF NOT EQUAL
.
          MATCH     OTCSDATE,TODATE          * Correct date ?
          GOTO      DALL8000 IF LESS
.
.         Check if there is enough room on the current page
.
          COMPARE   TWENTY3,CVERT
          GOTO      DALL1500 IF LESS
.
.         Check what the user wants to do
.            EXIT=0 means display the next page
.            EXIT=1 means try another date range
.            EXIT=2 means try another option
.            EXIT=3 means print the data found
.            EXIT=4 means display first page again
.
          CALL      QSTM0000
          BRANCH    EXIT,DALL9999,DALL9999,DALL9999,DALL0000
.
.         Display the next page
.
          ADD       ONE,CPAGENO              * increment the page count
          DISPLAY   *P1:7,*EF;
          MOVE      SEVEN,CVERT
.
.         Set up descriptions ready to display
.
DALL1500  PACK      KEY20,OTCSSITE,OTCSCLIN,OTCSDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OTCSSITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OTCSCLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ELSE
            CALL      CDSC0000                 * get the clinic description
          ENDIF
          MOVE      OCADESC,DSPDESC
.
          UNPACK    OTCSDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      SP20,TDESC
          MATCH     SP3,OTCSREAS
          GOTO      DALL2000 IF EQUAL
.
          PACK      KEY5,CODECB,OTCSREAS
          CALL      RDCODE1
.
.         Display this data record
.
DALL2000  DISPLAY   *P1:CVERT,*V2LON,OTCSCLIN,*HOFF:
                    *P8:CVERT,DSPDESC:
                    *P33:CVERT,*V2LON,CPCDATE:
                    *P46:CVERT,OTCSCTYP:
                    *P56:CVERT,TDESC
          ADD       ONE,CVERT
          GOTO      DALL1000
.
.         End of the requested date range encountered
.         Check what the user wants to do
.            EXIT=1 means try another date range
.            EXIT=2 means try another option
.            EXIT=3 means print the data found
.            EXIT=4 means display first page again
.
DALL8000  CALL      QSTL0000                 * Question for last page
          BRANCH    EXIT,DALL9999,DALL9999,DALL9999,DALL0000
          GOTO      DALL9999
.
.         Nothing found for the selected criteria
.
DALL9000  DISPLAY   *P1:24,*EL,*B,"No cancelled sessions found in the":
                    " request range. ";
          CALL      HITENTER
          MOVE      ONE,EXIT                 * Get another date range
.
DALL9999  RETURN
.
.***************************************************************************
.*                               QSTM0000                                  *
.*     Ask (E)xit, (D)ifferent search, (N)ext page, (F)irst page, (P)rint  *
.*           EXIT=0 means display the next page                            *
.*           EXIT=1 means try another date range                           *
.*           EXIT=2 means try another option                               *
.*           EXIT=3 means print the data found                             *
.*           EXIT=4 means display first page again                         *
.***************************************************************************
.
QSTM0000  DISPLAY   *P1:24,"(",*V2LON,"E",*HOFF,")xit, (":
                               *V2LON,"P",*HOFF,")rint, (":
                               *V2LON,"D",*HOFF,")ifferent search, (":
                               *V2LON,"N",*HOFF,")ext page";
.
          COMPARE   ZERO,CPAGENO
          GOTO      QSTM1000 IF EQUAL
.
          DISPLAY   ", (",*V2LON,"F",*HOFF,")irst page";
.
QSTM1000  KEYIN     " ? ",*V2LON,*EL,ANS;
.
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          TYPE      ANS
          GOTO      QSTM5000 IF EQUAL
.
          REP       "D1d1E2e2P3p3F4f4N0n0",ANS
.
          TYPE      ANS
          GOTO      QSTM5000 IF NOT EQUAL
.
          MOVE      ANS,EXIT
          BRANCH    EXIT,QSTM9999,QSTM9999,QSTM9999,QSTM2000
          GOTO      QSTM9999
.
.         First page option is only valid if we are not on the first page
.
QSTM2000  COMPARE   ZERO,CPAGENO
          GOTO      QSTM9999 IF NOT EQUAL
.
.         Invalid option
.
QSTM5000  BEEP
          GOTO      QSTM0000
.
QSTM9999  RETURN
.
.***************************************************************************
.*                               QSTL0000                                  *
.*     Ask (E)xit, (D)ifferent search, (F)irst page, (P)rint               *
.*           EXIT=1 means try another date range                           *
.*           EXIT=2 means try another option                               *
.*           EXIT=3 means print the data found                             *
.*           EXIT=4 means display first page again                         *
.***************************************************************************
.
QSTL0000  DISPLAY   *P1:24,"(",*V2LON,"E",*HOFF,")xit, (":
                               *V2LON,"P",*HOFF,")rint, (":
                               *V2LON,"D",*HOFF,")ifferent search";
.
          COMPARE   ZERO,CPAGENO
          GOTO      QSTL1000 IF EQUAL
.
          DISPLAY   ", (",*V2LON,"F",*HOFF,")irst page";
.
QSTL1000  KEYIN     " ? ",*V2LON,*EL,ANS;
.
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          TYPE      ANS
          GOTO      QSTL5000 IF EQUAL
.
          REP       "D1d1E2e2P3p3F4f4",ANS
.
          TYPE      ANS
          GOTO      QSTL5000 IF NOT EQUAL
.
          MOVE      ANS,EXIT
          BRANCH    EXIT,QSTL9999,QSTL9999,QSTL9999,QSTL2000
          GOTO      QSTL9999
.
.         First page option is only valid if we are not on the first page
.
QSTL2000  COMPARE   ZERO,CPAGENO
          GOTO      QSTL9999 IF NOT EQUAL
.
.         Invalid option
.
QSTL5000  BEEP
          GOTO      QSTL0000
.
QSTL9999  RETURN
.
.***************************************************************************
.*                               PALL0000                                  *
.*                Print all the cancelled sessions in the date range       *
.***************************************************************************
.
PALL0000  MOVE      ZERO,NUMRECS
          MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          CALL      HALL0000                 * Print the heading
.
          DISPLAY   *P1:24,*EL,*P50:24,"Printing: ";
.
.         Loop through the file and print the records
.
          PACK      KEY27,IDDD,FROMDATE,SP30
          CALL      RSOTCSL2
PALL1000  CALL      RKOTCSL2
          BRANCH    OVRCD,PALL8000           * finished file
.
          MATCH     IDDD,OTCSSITE            * Correct site ?
          GOTO      PALL8000 IF NOT EQUAL
.
          MATCH     OTCSDATE,TODATE          * Correct date ?
          GOTO      PALL8000 IF LESS
.
.         Check if there is enough room on the current page
.
          COMPARE   "55",CLNO
          CALL      HALL0000 IF NOT LESS
.
.         Set up descriptions ready to display
.
          PACK      KEY20,OTCSSITE,OTCSCLIN,OTCSDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OTCSSITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OTCSCLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ELSE
            CALL      CDSC0000                 * get the clinic description
          ENDIF
          MOVE      OCADESC,DSPDESC
.
          UNPACK    OTCSDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      SP30,OCTDESC
          PACK      KEY12,IDDD,OTCSCTYP
          CALL      RDCTYA1
.
          MOVE      SP20,TDESC
          MATCH     SP3,OTCSREAS
          GOTO      PALL2000 IF EQUAL
.
          PACK      KEY5,CODECB,OTCSREAS
          CALL      RDCODE1
.
.         Print this data record
.
PALL2000  PRINT     *1,"|",*3,OTCSCLIN,*10,OCADESC:
                    *41,"|",*43,CPCDATE:
                    *54,"|",*56,OTCSCTYP,*63,OCTDESC:
                    *94,"|",*96,OTCSREAS,*100,TDESC:
                    *132,"|"
          ADD       ONE,CLNO
          ADD       ONE,NUMRECS
.
.         Display the record on the screen so the user can see something.
.
          DISPLAY   *P60:24,*V2LON,OTCSCLIN,SP1,CPCDATE;
.
          GOTO      PALL1000
.
.         End of the requested date range encountered
.
PALL8000  CALL      UND132
          PRINT     *N,*1,"Number of records printed = ",NUMRECS,*N:
                    *N,"*** End of Report ***"
.
PALL9999  RETURN
.
.***************************************************************************
.*                               HALL0000                                  *
.*                Print the heading for option 1                           *
.***************************************************************************
.
HALL0000  CALL      HEAD132
.
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *40,"From date : ",CPCDATE
.
          UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *40,"To date   : ",CPCDATE,*N
.
          CALL      UND132
.
          PRINT     *1,"| Clinic Id":
                    *41,"| Clin. Date":
                    *54,"| Clinic Type":
                    *94,"| Reason for Cancellation":
                    *132,"|"
.
          CALL      UND132
          ADD       FOUR,CLNO
.
HALL9999  RETURN
.
.***************************************************************************
.*                               DCLI0000                                  *
.*                Display all the cancelled sessions in the date range     *
.***************************************************************************
.
DCLI0000  PACK      KEY27,IDDD,CLINIC,FROMDATE,SP30
          CALL      RSOTCSL1
          CALL      RKOTCSL1
          BRANCH    OVRCD,DCLI9000           * nothing found
.
          MATCH     IDDD,OTCSSITE            * Correct site ?
          GOTO      DCLI9000 IF NOT EQUAL
.
          MATCH     CLINIC,OTCSCLIN          * Correct clinic ?
          GOTO      DCLI9000 IF NOT EQUAL
.
          MATCH     OTCSDATE,TODATE          * Correct date ?
          GOTO      DCLI9000 IF LESS
.
.         We have at least one record
.
          DISPLAY   *P1:6,*EF,*ULON,"Clinic Id                     ":
                    *P32:6,"Clinic Date":
                    *P44:6,"Clinic Type":
                    *P56:6,"Reason for Cancellation"
          MOVE      SEVEN,CVERT
          MOVE      ZERO,CPAGENO             * current page number
.
.         Loop through the file and display the records
.
          PACK      KEY27,IDDD,CLINIC,FROMDATE,SP30
          CALL      RSOTCSL1
DCLI1000  CALL      RKOTCSL1
          BRANCH    OVRCD,DCLI8000           * finished file
.
          MATCH     IDDD,OTCSSITE            * Correct site ?
          GOTO      DCLI8000 IF NOT EQUAL
.
          MATCH     CLINIC,OTCSCLIN          * Correct clinic ?
          GOTO      DCLI8000 IF NOT EQUAL
.
          MATCH     OTCSDATE,TODATE          * Correct date ?
          GOTO      DCLI8000 IF LESS
.
.         Check if there is enough room on the current page
.
          COMPARE   TWENTY3,CVERT
          GOTO      DCLI1500 IF LESS
.
.         Check what the user wants to do
.            EXIT=0 means display the next page
.            EXIT=1 means try another date range
.            EXIT=2 means try another option
.            EXIT=3 means print the data found
.            EXIT=4 means display first page again
.
          CALL      QSTM0000
          BRANCH    EXIT,DCLI9999,DCLI9999,DCLI9999,DCLI0000
.
.         Display the next page
.
          ADD       ONE,CPAGENO              * increment the page count
          DISPLAY   *P1:7,*EF;
          MOVE      SEVEN,CVERT
.
.         Set up descriptions ready to display
.
DCLI1500  PACK      KEY20,OTCSSITE,OTCSCLIN,OTCSDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OTCSSITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OTCSCLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ELSE
            CALL      CDSC0000                 * get the clinic description
          ENDIF
          MOVE      OCADESC,DSPDESC
.
          UNPACK    OTCSDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      SP20,TDESC
          MATCH     SP3,OTCSREAS
          GOTO      DCLI2000 IF EQUAL
.
          PACK      KEY5,CODECB,OTCSREAS
          CALL      RDCODE1
.
.         Display this data record
.
DCLI2000  DISPLAY   *P1:CVERT,*V2LON,OTCSCLIN,*HOFF:
                    *P8:CVERT,DSPDESC:
                    *P33:CVERT,*V2LON,CPCDATE:
                    *P46:CVERT,OTCSCTYP:
                    *P56:CVERT,TDESC
          ADD       ONE,CVERT
          GOTO      DCLI1000
.
.         End of the requested date range encountered
.         Check what the user wants to do
.            EXIT=1 means try another date range
.            EXIT=2 means try another option
.            EXIT=3 means print the data found
.            EXIT=4 means display first page again
.
DCLI8000  CALL      QSTL0000                 * Question for last page
          BRANCH    EXIT,DCLI9999,DCLI9999,DCLI9999,DCLI0000
          GOTO      DCLI9999
.
.         Nothing found for the selected criteria
.
DCLI9000  DISPLAY   *P1:24,*EL,*B,"No cancelled sessions found in the":
                    " request range. ";
          CALL      HITENTER
          MOVE      ONE,EXIT                 * Get another date range
.
DCLI9999  RETURN
.
.***************************************************************************
.*                               PCLI0000                                  *
.*                Print all the cancelled sessions in the date range       *
.***************************************************************************
.
PCLI0000  MOVE      ZERO,NUMRECS
          MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          CALL      HCLI0000                 * Print the heading
.
          DISPLAY   *P1:24,*EL,*P50:24,"Printing: ";
.
.         Loop through the file and print the records
.
          PACK      KEY27,IDDD,CLINIC,FROMDATE,SP30
          CALL      RSOTCSL1
PCLI1000  CALL      RKOTCSL1
          BRANCH    OVRCD,PCLI8000           * finished file
.
          MATCH     IDDD,OTCSSITE            * Correct site ?
          GOTO      PCLI8000 IF NOT EQUAL
.
          MATCH     CLINIC,OTCSCLIN          * Correct clinic ?
          GOTO      PCLI8000 IF NOT EQUAL
.
          MATCH     OTCSDATE,TODATE          * Correct date ?
          GOTO      PCLI8000 IF LESS
.
.         Check if there is enough room on the current page
.
          COMPARE   "55",CLNO
          CALL      HCLI0000 IF NOT LESS
.
.         Set up descriptions ready to display
.
          PACK      KEY20,OTCSSITE,OTCSCLIN,OTCSDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OTCSSITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OTCSCLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ELSE
            CALL      CDSC0000                 * get the clinic description
          ENDIF
          MOVE      OCADESC,DSPDESC
.
          UNPACK    OTCSDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      SP30,OCTDESC
          PACK      KEY12,IDDD,OTCSCTYP
          CALL      RDCTYA1
.
          MOVE      SP20,TDESC
          MATCH     SP3,OTCSREAS
          GOTO      PCLI2000 IF EQUAL
.
          PACK      KEY5,CODECB,OTCSREAS
          CALL      RDCODE1
.
.         Print this data record
.
PCLI2000  PRINT     *1,"|",*3,OTCSCLIN,*10,OCADESC:
                    *41,"|",*43,CPCDATE:
                    *54,"|",*56,OTCSCTYP,*63,OCTDESC:
                    *94,"|",*96,OTCSREAS,*100,TDESC:
                    *132,"|"
          ADD       ONE,CLNO
          ADD       ONE,NUMRECS
.
.         Display the record on the screen so the user can see something.
.
          DISPLAY   *P60:24,*V2LON,OTCSCLIN,SP1,CPCDATE;
.
          GOTO      PCLI1000
.
.         End of the requested date range encountered
.
PCLI8000  CALL      UND132
          PRINT     *N,*1,"Number of records printed = ",NUMRECS,*N:
                    *N,"*** End of Report ***"
.
PCLI9999  RETURN
.
.***************************************************************************
.*                               HCLI0000                                  *
.*                Print the heading for option 2                           *
.***************************************************************************
.
HCLI0000  CALL      HEAD132
.
          PACK      KEY20,IDDD,CLINIC,FROMDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     IDDD,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     CLINIC,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ELSE
            CALL      CDSC0000                 * get the clinic description
          ENDIF
.
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *40,"Clinic    : ",CLINIC,*60,OCADESC,*N:
                    *40,"From date : ",CPCDATE
.
          UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *40,"To date   : ",CPCDATE,*N
.
          CALL      UND132
.
          PRINT     *1,"| Clinic Id":
                    *41,"| Clin. Date":
                    *54,"| Clinic Type":
                    *94,"| Reason for Cancellation":
                    *132,"|"
.
          CALL      UND132
          ADD       FIVE,CLNO
.
HCLI9999  RETURN
+
.
.-------- io subroutine includes ------
.
          INC       STD001IO
          INC       OUTDSCLN
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       OUTCLIIO/INC
          INC       OUTCSLIO/INC
          INC       OUTCTYIO/INC

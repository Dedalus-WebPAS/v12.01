.-------------------------------------------------------------------------------
. Program      : PATWEB84
.
. Function     : Patient Download Server 
.
. Modifications : 
.
.-------------------------------------------------------------------------------
. V11.02.01 10/02/2022  Thanh T          TSK 0889899
.           Recompiled as WATTR1FD changes
. V10.11.02 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.01 28/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
.-------------------------------------------------------------------------------
. V10.04.02 07/04/2014  Peter Vela      CAR 286258
.           Recompiled for PATMASTM
. V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.-------------------------------------------------------------------------------
. V9.12.01  17/12/2009  Peter McMullen CAR 210130
.           Convert to newer Dr name format routine DOCNM000
.-------------------------------------------------------------------------------
. V9.08.01  31/01/2007  Peter Vela     CAR 127930
.           Recompiled for MRTMASFD
.-------------------------------------------------------------------------------
. V9.05.01  09/03/2006  Ebon Clements CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.05.B01 30/01/2006  Ebon Clements CAR 93186
.           Mods for REDIRECT length change. 
.           Recompiled for IBAWEBDF
.-------------------------------------------------------------------------------
. V9.03.01  26/03/2004 Peter Vela CAR 13038
.           Recompiled for PATDOCCD - Doctor Name format Parameter
. V9.02.03  07/02/2003 Tonii CAR 35992
.           Recompiled for PATMISTM - inactive codes in patfundf
. V9.02.02  09/10/2002 Peter Vela               SRF 17693
.           Recompiled for PATMISTM - Admitting Point
. V9.02.01  04/12/2001 Katie Nelson/Harvinder   SRF 648628(nz)
.           Recompiled for NHI tags added to PATMASTM
. V9.01.02  16.08.2001 J.Tan
.           Recompiled for PATMASTM
. V9.00.01  14.08.2001 J.Tan
.           Recompiled for PATMASTM
. V9.00.B01 13.07.2001 Sandra Barcham
.           Replace GP with HCP
.------------------------------------------------------------------
.                 V5.09.01  15/01/2001 Bronko Sosic
.                           Recompiled for PATMASTM
.------------------------------------------------------------------
.                 V5.08.03  28/09/2000 Bronko Sosic
.                           Recompiled for PATMASTM
.                 V5.08.02  16/08/2000  Caleb Sharman
.                           Changed Health Fund variables to be 8 chars
.                 V5.08.01  07/06/2000 Bronko Sosic  
.                           Recompiled for PATMASTM 
.-------------------------------------------------------------------
.                 V5.07.02  02/02/2000 Steven Watkins
.                           Recompiled for PATMASTM / PATMISTM
. Modifications : V5.07.01  28.01.2000  B.G.Ackland
.                           Recompile in Work
.                 V5.07.00  14.01.2000 Pat Dirito 
.                           Created Server                
.
.------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
.
          INC       PATDHEAD/INC
.
          INC       APSMASFD/INC      * A/P Master File
          INC       BOKRC1FD/INC      * Booking File
          INC       CORTYPFD/INC      * Correspondence Download Types
          INC       CORDTTFD/INC      * Correspondence Details 
          INC       EMRUNKFD/INC      * Unknowns Patient File
          INC       EMRVISFD/INC      * Emergency Visits File 
          INC       OUTRF1FD/INC      * Outpatient Referral Letter File
          INC       OUTBOAFD/INC      * OUT - Clinic Session Booking File A
          INC       OUTBB1FD/INC      * OUT - Clinic Session Booking File B
          INC       OUTSITFD/INC      * Outpatient Site File
          INC       PATMA1FD/INC      * Patient Master File
          INC       PMSPX2FD/INC      * Patient Master File
          INC       PMSVX1FD/INC      * Visit Ext. File
          INC       PATDSCFD/INC      * Patient Discharge File
          INC       PATPR1FD/INC      * Pre-admission master file
          INC       PATIN1FD/INC      * Insurance file
          INC       PATVISFD/INC      * Patient Visit File  
          INC       PATNIDFD/INC      * National ID Number File
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATCONFD/INC      * Patient Control File
          INC       PMSHCPFD/INC      * National HCP File
          INC       PMSHCGFD/INC      * HCP Practice File
          INC       PMSHCLFD/INC      * HCP Practice File
          INC       PMSHCPTD/INC      * National HCP File
          INC       PMSHCGTD/INC      * HCP Practice File
          INC       PMSHCLTD/INC      * HCP Practice File
          INC       PATFN1FD/INC      * Patient health Fund File
          INC       PATHSPFD/INC      * Hospital File
          INC       PATCALAG/INC
          INC       MRTMASFD/INC  
.
          INC       PATALRFD/INC      * Patient Alerts Code File
          INC       PATMI1FD/INC      * Patient Admissions File
          INC       PATWR1FD/INC      * Ward file
          INC       PATDO1FD/INC      * Doctors File
          INC       RESLNKFD/INC      * Result Linking Table
          INC       WATTR1FD/INC      * Waiting List Patient Proc File
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
.
.----------------------------------------------------------------------------
. CGI Variables
.----------------------------------------------------------------------------
NEXTPAGE  FORM      1     * Nextpage parameter CGI parameter
DOWNTYPE  DIM       5     * Download Type
DOCTCODE  DIM       6     * Doctor Code 
.----------------------------------------------------------------------------
. Program Variables 
.----------------------------------------------------------------------------
EXPORTFL  FILE      ASCII, FIXED=400     * Export File (Adjust Length to Suit)
EXPORTFN  DIM       16                   * Export File Name
EXPORTEX  INIT      ".iba"               * Export File Name
.
CFILEPRE  DIM       3
CHARCR    INIT      0x0D
FORM8     FORM      8
DSCHFLAG  FORM      1
BRECFLAG  FORM      1
EMRFLAG   FORM      1
EMRVFLAG  FORM      1
OUTVAR    DIM       127
HEALTFND  DIM       6    
NOTAVAIL  INIT      "N/A"
ZERO8     INIT      "0000    "   
.
.----------------------------------------------------------------------------
PRGID     INIT      "PATWEB84"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Patient Download Server"
.----------------------------------------------------------------------------
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "INVALID OPTION",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      DLOAD000
          CALL      LOCDIR00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------------------------
.  Redirect URL
.------------------------------------------------------------------------------
LOCDIR00  CALL      OPENHTML
          BRANCH    EXIT,LOCDIR90
          WRITE     HTMLFILE;"<html><head>":
                             "</head>":
                             "<frameset rows=0,* border=0>":
                             " <frame src=#"/",EXPORTFN,EXPORTEX,"#" >":
                             " <frame src=#"/DownLoad.html#" >":
                             "</frameset> "
          CALL      CLOSHTML
          GOTO      LOCDIR99
LOCDIR90  DISPLAY   "*** Fifo Not Found ***"
LOCDIR99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      INTMAS00  * Open routine in PATMASTM
.
          OPEN      APSMASA1,"apsmasaf"
.
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSHCGA1,"pmshcgaf"
          OPEN      CORTYPA1,"cortypaf"
          OPEN      CORDTTA2,"cordttaf"
          OPEN      PATNIDA1,"patnidaf"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          OPEN      OUTRF1A1,"outrf1af"
          OPEN      PATFN1A1,"patfn1af"
          OPEN      PATIN1A1,"patin1af"
          OPEN      WATTR1A1,"wattr1af"
          OPEN      BOKRC1A1,"bokrc1af"
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EMRVISA1,"emrvisaf"
          TRAPCLR   IO
          MOVE      OVRCD,EMRVFLAG
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EMRUNKA1,"emrunkaf"
          TRAPCLR   IO
          MOVE      OVRCD,EMRFLAG
.
          OPEN      OUTSITA1,"outsitaf"     * Open the site file
          MOVE      "out",CFILEPRE          * Save Current File Prefix
          CALL      OPNOUT00                * Open Outpatient Files 
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
INIT9999  RETURN
.------------------------------------------------------------
. Open Outpatient Files
.------------------------------------------------------------
OPNOUT00  PACK      CFNAME,CFILEPRE,FILBB1A1  * Get the name of the FILBB file
          OPEN      OUTBB1A1,CFNAME           * Open the file
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,URNUMBER
          MOVE      SP70,ADMISSNO
          MOVE      ZERO,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,DOWNTYPE
          MOVE      SP70,DOCTCODE
          MOVE      SP70,PMHCP001
          MOVE      SP70,PMHCG001
          MOVE      ZERO,PMHCG002
          RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "doctcode=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,DOCTCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "downtype=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,DOWNTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmhcp001=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            PACK      PMHCP001,PMHCP001,SP70 
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmhcg001=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            PACK      PMHCG001,PMHCG001,SP70 
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmhcg002=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,PMHCG002
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.------------------------------------------------------------------
. Get Name from Visit Based Tables
.------------------------------------------------------------------
FINVIS00  MOVE      ADMISSNO,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,FINVIS80
.
          BRANCH    PVITYPE,FINVIS10,FINVIS20,FINVIS20
          MOVE      ONE,EXIT
          GOTO      FINVIS99
.
FINVIS10  BRANCH    EMRFLAG,FINVIS90
          MOVE      PVIBILL,KEY8
          CALL      RDEMUNK1
          BRANCH    OVRCD,FINVIS90
          MOVE      SP70,PTITL
          MOVE      EMUNSNAM,PSNAME
          MOVE      EMUNGNAM,PGNAME
          MOVE      EMUNBDAT,PBDATE
          MOVE      EMUNSEX,PSEX
          MOVE      EMUNAGE,PSAGE
          MATCH     SP70,PSNAME
          IF        @EQUAL
            MOVE     EMUNDET1,PSNAME
          ENDIF
          MATCH     SP70,PGNAME
          IF        @EQUAL
            MOVE     EMUNDET2,PGNAME
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      FINVIS99
.
FINVIS20  MOVE      PVIBILL,KEY8
          CALL      RDPRAM1
          BRANCH    OVRCD,FINVIS90
          MOVE      ZERO,EXIT
          GOTO      FINVIS99
.
FINVIS30  MOVE      PVIBILL,KEY8
          CALL      RDPRAM1
          BRANCH    OVRCD,FINVIS90
          MOVE      ZERO,EXIT
          GOTO      FINVIS99
.
FINVIS80  MOVE      ADMISSNO,KEY8
          CALL      RDPRAM1
          BRANCH    OVRCD,FINVIS85
          MOVE      ZERO,EXIT
          GOTO      FINVIS99
.
FINVIS85  BRANCH    EMRFLAG,FINVIS90
          MOVE      ADMISSNO,KEY8
          CALL      RDEMUNK1
          BRANCH    OVRCD,FINVIS90
          MOVE      SP70,PTITL
          MOVE      EMUNSNAM,PSNAME
          MOVE      EMUNGNAM,PGNAME
          MOVE      EMUNBDAT,PBDATE
          MOVE      EMUNSEX,PSEX
          MOVE      EMUNAGE,PSAGE
          MATCH     SP70,PSNAME
          IF        @EQUAL
            MOVE     EMUNDET1,PSNAME
          ENDIF
          MATCH     SP70,PGNAME
          IF        @EQUAL
            MOVE     EMUNDET2,PGNAME
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      FINVIS99
.
FINVIS90  MOVE      ONE,EXIT
.
FINVIS99  RETURN
.---------------------------------------------------------------------------
.  Download Routine
.---------------------------------------------------------------------------
DLOAD000  CALL      IBACLOCK
          CALL      CLPATMAS         * Clear Master File Variables
          CALL      CLPATMIS         * Clear Admission File Variables  
          CALL      CLPATDSC         * Clear Discharge File Variables
          MOVE      ZERO,MISSFLAG
.
          PACK      KEY5,DOWNTYPE,SP70  * Check If Download Type Exists
          CALL      RDCOTY1
          BRANCH    OVRCD,DLOAD900
.
          MATCH     "       0",URNUMBER
          IF        @EQUAL
            CALL      FINVIS00
            BRANCH    EXIT,DLOAD910
          ELSE
            MOVE      URNUMBER,KEY8
            CALL      RDMAST1
            BRANCH    OVRCD,DLOAD910
          ENDIF
.
          MOVE      ADMISSNO,KEY8
          MATCH     SP70,KEY8
          IF        @EQUAL
            MOVE      ONE,MISSFLAG
            GOTO      DLOAD100
          ENDIF
          CALL      RDMISS1
          MOVE      OVRCD,MISSFLAG
          CALL      PATNAA00      * Format Patient Name Without Bold
.
DLOAD100  CALL      MAKEXP00      * Create Download File
          CALL      EXPHED00      * Write Heading Record to File
          CALL      EXPDET00      * Write Export Details to File
          GOTO      DLOAD999
.
DLOAD900  MOVE      "Download Type does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      DLOAD999
.
DLOAD910  MOVE      "Can't find patient master details",WEBTITLE
          APPEND    URNUMBER,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00          
          GOTO      DLOAD999
.
DLOAD999  RETURN
.------------------------------------------------------------
. Create Export File
.------------------------------------------------------------
MAKEXP00  CLEAR     EXPORTFN
          APPEND    "UR",EXPORTFN
          APPEND    URNUMBER,EXPORTFN
          APPEND    "-",EXPORTFN
          APPEND    DOWNTYPE,EXPORTFN
          APPEND    SP70,EXPORTFN
          RESET     EXPORTFN
          REP       " 0",EXPORTFN
          MOVE      "   91",AUDTSECT
          READ      CONTROLF,AUDTSECT;*99,CWEBRDIR,CWEBRCFG,CWEBROOT
          PACK      KEY80,CWEBROOT,EXPORTFN,EXPORTEX
          SQUEEZE   KEY80
          PREP      EXPORTFL,KEY80
.
MAKEXP99  RETURN
.------------------------------------------------------------
. Export Patient Details
.------------------------------------------------------------
EXPHED00  WRITE     EXPORTFL,SEQ;QUOTE,"Download Type",QUOTE,COMMA:
                                 QUOTE,"Download Name",QUOTE,COMMA:
                                 QUOTE,"U/R No",QUOTE,COMMA:
                                 QUOTE,"Visit No",QUOTE,COMMA: 
                                 QUOTE,"Formatted Name",QUOTE,COMMA; 
          PACK      KEY13,DOWNTYPE,SP70
          CALL      RSCODT2
.
EXPHED10  CALL      RKCODT2
          BRANCH    OVRCD,EXPHED90
          MATCH     CODTCTYP,DOWNTYPE
          GOTO      EXPHED90 IF NOT EQUAL
.
          WRITE     EXPORTFL,SEQ;QUOTE,CODTHEAD,QUOTE,COMMA;
          GOTO      EXPHED10
.
EXPHED90  WRITE     EXPORTFL,SEQ;QUOTE,"<EOR>",QUOTE,CHARCR;
EXPHED99  RETURN
.------------------------------------------------------------
. Write Export Details
.------------------------------------------------------------
EXPDET00  PACK      KEY5,DOWNTYPE,SP70  * Check If Download Type Exists
          REP       " 0",KEY5
          WRITE     EXPORTFL,SEQ;QUOTE,KEY5,QUOTE,COMMA:
                                 QUOTE,COTYCDSC,QUOTE,COMMA:
                                 QUOTE,URNUMBER,QUOTE,COMMA:
                                 QUOTE,ADMISSNO,QUOTE,COMMA:
                                 QUOTE,PATFNAME,QUOTE,COMMA;
          PACK      KEY13,DOWNTYPE,SP70
          CALL      RSCODT2
.
EXPDET10  CALL      RKCODT2
          BRANCH    OVRCD,EXPDET90
          MATCH     CODTCTYP,DOWNTYPE
          GOTO      EXPDET90 IF NOT EQUAL
.
          CALL      GVAR0000     * Get output variable
          WRITE     EXPORTFL,SEQ;QUOTE,OUTVAR,QUOTE,COMMA;
.
          GOTO      EXPDET10
.            
EXPDET90  WRITE     EXPORTFL,SEQ;QUOTE,"<EOR>",QUOTE,CHARCR;
EXPDET99  RETURN
.------------------------------------------------------------
GVAR0000  PACK      VAR,SP70,SP70
          SFORMAT   OUTVAR,127
          UNPACK    CODTFLNO,KEY2,KEY3
          MOVE      KEY2,FLDSETNO
          MOVE      KEY3,FLDITMNO
          BRANCH    FLDSETNO,GVAR0100,GVAR0200,GVAR0300,GVAR0400,GVAR0500:
                             GVAR0600,GVAR0700,GVAR0800,GVAR0900,GVAR1000: 
                             GVAR1100,GVAR1200,GVAR1300,GVAR1400,GVAR1500:
                             GVAR1600,GVAR1700
          SFORMAT   OUTVAR,20
          MOVE      "Invalid Field No.",OUTVAR
          GOTO      GVAR9999
.
GVAR0100  CALL      MASLOD00    * Load Master File Variable
          GOTO      GVAR9000
.
GVAR0200  CALL      MX1LOD00    * Load Master File Extension Variable
          GOTO      GVAR9000
.
GVAR0300  CALL      DOCLOD00    * Selected Doctor, DOCTCODE already set as
          GOTO      GVAR9000    * a CGI Parameter
.
GVAR0400  BRANCH    MISSFLAG,GVAR8000
          CALL      REFDOC00    * Gets the Refering Doc & sets DOCTCODE 
          CALL      DOCLOD00    * Load Doctor File Variable
          GOTO      GVAR9000
.
GVAR0500  BRANCH    MISSFLAG,GVAR8000  
          CALL      ATTDOC00    * Gets the Attending Doc & sets DOCTCODE 
          CALL      DOCLOD00    * Load Doctor File Variable
          GOTO      GVAR9000
.
GVAR0600  BRANCH    MISSFLAG,GVAR8000   
          CALL      TREDOC00    * Gets the Treating Doc & sets DOCTCODE 
          CALL      DOCLOD00    * Load Doctor File Variable
          GOTO      GVAR9000
.
GVAR0700  CALL      GPPLOD00    * Selected HCP, PMHCP001,PMHCG001 & PMHCG002 
          GOTO      GVAR9000    * already set as CGI Parameters
.
GVAR0800  PACK      PMHCP001,PMPXRHC1,SP70   * Set Variables with Master File  
          PACK      PMHCG001,PMPXRH1G,SP70   * extension Variables,HCP,Practice 
          MOVE      PMPXR1GC,PMHCG002   * Code & Practice Count
          CALL      GPPLOD00    * Registered HCP 
          GOTO      GVAR9000    
.
GVAR0900  BRANCH    MISSFLAG,GVAR8000  
          CALL      REFGPP00    * Gets the Referring HCP&sets PMCHP001,PMHCG001
.                               * and PMHCG002 
          CALL      GPPLOD00    * Load HCP File Variable
          GOTO      GVAR9000
.
GVAR1000  CALL      HOSLOD00    * Load Hospital Variable                   
          GOTO      GVAR9000    
. 
GVAR1100  BRANCH    MISSFLAG,GVAR8000 
          CALL      HLTFND00    * Gets Health Fund & Sets HEALTFND
          CALL      HLTLOD00    * Load Health Fund Variable                     
          GOTO      GVAR9000    
.
GVAR1200  BRANCH    MISSFLAG,GVAR8000
          BRANCH    EMRVFLAG,GVAR8000     * No EMR Visit Table    
          CALL      EMRLOD00              * Load Emergency Variable             
          GOTO      GVAR9000    
.
GVAR1300  BRANCH    MISSFLAG,GVAR8000
          CALL      OUTLOD00    * Load Outpatient Variable                   
          GOTO      GVAR9000    
.
GVAR1400  BRANCH    MISSFLAG,GVAR8000
          CALL      REFLOD00    * Load Referral Variable                   
          GOTO      GVAR9000    
.
GVAR1500  BRANCH    MISSFLAG,GVAR8000
          CALL      MISLOD00    * Load Admission File Variable
          GOTO      GVAR9000
.
GVAR1600  CALL      WATLOD00    * Load Waiting List Variable                   
          GOTO      GVAR9000    
.
GVAR1700  BRANCH    MISSFLAG,GVAR8000 
          CALL      BOKLOD00    * Load Booking Variable                   
          GOTO      GVAR9000    
.
GVAR8000  PACK      VAR,SP70,SP70
.
GVAR9000  STRIP     VAR
          MOVELPTR  VAR,LENGTH
          COMPARE   ZERO,LENGTH
          IF        @EQUAL
            SFORMAT   OUTVAR,ONE
            MOVE      SP1,OUTVAR
          ELSE
            SFORMAT   OUTVAR,LENGTH
            MOVE      VAR,OUTVAR
          ENDIF
.
          MATCH     "1",CODTCNVT
          IF        @EQUAL     
            MOVE      OUTVAR,NAME35
            CALL      NAMSTR00            * Convert to Title Text
            MOVE      DISPNAME,OUTVAR
            GOTO      GVAR9999
          ENDIF   
          MATCH     "2",CODTCNVT
          IF        @EQUAL     
            REP       UPPLOW,OUTVAR       * Convert to Upper Case
            GOTO      GVAR9999
          ENDIF
          MATCH     "3",CODTCNVT
          IF        @EQUAL     
            REP       DOWNCASE,OUTVAR     * Convert to Lower Case
            GOTO      GVAR9999
          ENDIF
          GOTO      GVAR9999
.
GVAR9999  RETURN
.------------------------------------------------------------------------
.                             REFDOC00 
.   This routine gets the Reffering doctor by reading the visit file
.   and then reading the appropriate Inpatient, Outpatient or EMR File       
.   Parameters: DOCTCODE
.------------------------------------------------------------------------
REFDOC00  MOVE      SP70,DOCTCODE
          PACK      KEY8,ADMISSNO,SP70      
          CALL      RDVISA1
          BRANCH    OVRCD,REFDOC99
.
          BRANCH    PVITYPE,REFDOC10,REFDOC20,REFDOC30
          GOTO      REFDOC99          
.
REFDOC10  BRANCH    EMRVFLAG,REFDOC99      * No EMR Visit Table
          PACK      KEY8,ADMISSNO,SP70     * Read Emergency Visit File 
          CALL      RDEMVIS1
          BRANCH    OVRCD,REFDOC99
          MOVE      EMVIREFD,DOCTCODE      * Referral Doctor Set 
          GOTO      REFDOC99
.
REFDOC20  PACK      KEY8,ADMISSNO,SP70     * Read Outpatitent Referral File 
          CALL      RDOTRFL1       
          BRANCH    OVRCD,REFDOC99
          MOVE      OTRLRDOC,DOCTCODE      * Referral Doctor Set 
          GOTO      REFDOC99
.
REFDOC30  PACK      KEY8,ADMISSNO,SP70     * Read INP - Admission File 
          CALL      RDMISS1       
          BRANCH    OVRCD,REFDOC99
          MOVE      ADOCTR,DOCTCODE        * Referral Doctor Set 
          GOTO      REFDOC99
.
REFDOC99  RETURN
.------------------------------------------------------------------------
.                             ATTDOC00 
.   This routine gets the Attending doctor by reading the visit file
.   and then reading the appropriate Inpatient, Outpatient or EMR File       
.   Parameters: DOCTCODE
.------------------------------------------------------------------------
ATTDOC00  MOVE      SP70,DOCTCODE
          PACK      KEY8,ADMISSNO,SP70      
          CALL      RDVISA1
          BRANCH    OVRCD,ATTDOC99
.
          BRANCH    PVITYPE,ATTDOC10,ATTDOC20,ATTDOC30
          GOTO      ATTDOC99          
.
ATTDOC10  BRANCH    EMRVFLAG,ATTDOC99      * No EMR Visit Table 
          PACK      KEY8,ADMISSNO,SP70     * Read Emergency Visit File 
          CALL      RDEMVIS1
          BRANCH    OVRCD,ATTDOC99
          MOVE      EMVIDOCT,DOCTCODE      * Attending Doctor Set 
          GOTO      ATTDOC99
.
ATTDOC20  PACK      KEY8,ADMISSNO,SP70     * Read OUT - Session Booking File B 
          CALL      RDBOKB1       
          BRANCH    OVRCD,ATTDOC99
          MOVE      OBADOCT,DOCTCODE       * Attending Doctor Set 
          GOTO      ATTDOC99
.
ATTDOC30  PACK      KEY8,ADMISSNO,SP70     * Read INP - Admission File 
          CALL      RDMISS1       
          BRANCH    OVRCD,ATTDOC99
          MOVE      ADOCTA,DOCTCODE        * Attending Doctor Set 
          GOTO      ATTDOC99
.
ATTDOC99  RETURN
.------------------------------------------------------------------------
.                             TREDOC00 
.   This routine gets the Treating doctor by reading the visit file
.   and then reading the appropriate Inpatient, Outpatient or EMR File       
.   Parameters: DOCTCODE
.------------------------------------------------------------------------
TREDOC00  MOVE      SP70,DOCTCODE
          PACK      KEY8,ADMISSNO,SP70      
          CALL      RDVISA1
          BRANCH    OVRCD,TREDOC99 
.
          BRANCH    PVITYPE,TREDOC10,TREDOC20,TREDOC30
          GOTO      TREDOC99          
.
TREDOC10  
          GOTO      TREDOC99
.
TREDOC20  PACK      KEY8,ADMISSNO,SP70     * Read OUT - Session Booking File B 
          CALL      RDBOKB1       
          BRANCH    OVRCD,TREDOC99
          MOVE      OTBBADCS,DOCTCODE      * Treatment Doctor Set 
          GOTO      TREDOC99
.
TREDOC30  PACK      KEY8,ADMISSNO,SP70     * Read INP - Admission File 
          CALL      RDMISS1       
          BRANCH    OVRCD,TREDOC99
          MOVE      ADOCTT,DOCTCODE        * Treatment Doctor Set 
          GOTO      TREDOC99
.
TREDOC99  RETURN
.------------------------------------------------------------------------
.                             REFGPP00 
.   This routine gets the Reffering HCP by reading the visit file
.   and then reading the appropriate Inpatient, Outpatient or EMR File       
.   Parameters: PMHCP001,PMHCG001,PMHCG002
.------------------------------------------------------------------------
REFGPP00  MOVE      SP70,PMHCP001
          MOVE      SP70,PMHCG001
          MOVE      ZERO,PMHCG002
.
          PACK      KEY8,ADMISSNO,SP70      
          CALL      RDVISA1
          BRANCH    OVRCD,REFGPP99
.
          BRANCH    PVITYPE,REFGPP10,REFGPP20,REFGPP30
          GOTO      REFGPP99          
.
REFGPP10  BRANCH    EMRVFLAG,REFGPP99      * No EMR Visit Table 
          PACK      KEY8,ADMISSNO,SP70     * Read Emergency Visit File 
          CALL      RDEMVIS1
          BRANCH    OVRCD,REFGPP99
          PACK      PMHCP001,EMVIREFG,SP70
          MOVE      SP70,PMHCG001          * No Practice Code Set
          MOVE      ZERO,PMHCG002          * No Practice Count Set
          GOTO      REFGPP99
.
REFGPP20  PACK      KEY8,ADMISSNO,SP70     * Read OUT - Session Booking File B
          CALL      RDBOKB1
          BRANCH    OVRCD,REFGPP99
          PACK      PMHCP001,OTBBRFGP,SP70      * Referal HCP Set 
          PACK      PMHCG001,OTBBGPPC,SP70      * Practice Code Set  
          MOVE      OTBBGPPT,PMHCG002           * Practice Count Set  
          GOTO      REFGPP99
.
REFGPP30  PACK      KEY8,ADMISSNO,SP70     * Read INP - Admission File 
          CALL      RDMISS1       
          BRANCH    OVRCD,REFGPP99
.
          PACK      KEY8,ADMISSNO,SP20
          CALL      RDPMVX11
          IF        OVRCD=1
            CALL      CLPMSVX1
          ELSE
            PACK      PMHCP001,PMVXRHC1,SP70      * Referal HCP Set 
            PACK      PMHCG001,PMVXRH1G,SP70      * Practice Code Set  
            MOVE      PMVXRH1C,PMHCG002           * Practice Count Set  
          ENDIF
          GOTO      REFGPP99
.
REFGPP99  RETURN
.------------------------------------------------------------------------
.                             HLTFND00 
.   This routine gets the Health Fund Table by reading the visit file
.   and then reading the appropriate Inpatient, Outpatient or EMR File       
.   Parameters: HEALTFND     
.------------------------------------------------------------------------
HLTFND00  MOVE      SP70,HEALTFND 
.
          PACK      KEY8,ADMISSNO,SP70      
          CALL      RDVISA1
          BRANCH    OVRCD,HLTFND99
.
          BRANCH    PVITYPE,HLTFND10,HLTFND20,HLTFND30
          GOTO      HLTFND99          
.
HLTFND10  BRANCH    EMRVFLAG,HLTFND99      * No EMR Visit Table
          PACK      KEY8,ADMISSNO,SP70     * Read Emergency Visit File 
          CALL      RDEMVIS1
          BRANCH    OVRCD,HLTFND99
          MOVE      EMVIFUND,HEALTFND          * Health Fund Set  
          GOTO      HLTFND99
.
HLTFND20  PACK      KEY8,ADMISSNO,SP70     * Read OUT - Session Booking File B
          CALL      RDBOKB1
          BRANCH    OVRCD,HLTFND99
          MOVE      OTBBFUND,HEALTFND      * Health Fund Set 
          GOTO      HLTFND99
.
HLTFND30  MOVE      PFUNDH,HEALTFND        * Master File Var (Already read)
          GOTO      HLTFND99
.
HLTFND99  RETURN
.--------------------------------------------------------------------
. Load Master File Variable              
.       Note NOTAVAIL is a dummy variable = its an empty FLDITMNO 
.       Note position 20 & 21 Will contain PTMAMCRN & PTMANCED in 6.06
.--------------------------------------------------------------------
MASLOD00  MOVE      "Invalid Field No.",VAR
          LOAD      VAR,FLDITMNO,PSNAME,PGNAME,PTITL,PPSNAME,PADD1:        * 5 
                            PADD2,PSUBRB,PADD4,PPOST,PTELEP:               *10
                            PTELEB,PSEX,PBDATE,PSAGE,PCONT:                *15 
                            PREG,PTYPE,POCCUP,PMEDI,NOTAVAIL:              *20
                            NOTAVAIL,PPENNO,PPNDTE,PREPAT,PSMOK:           *25
                            PMICRO,PLOCDOC,PFUNDH,PFNDTB,PFNDMM:           *30
                            PFNDYY,PFNDCG,PMSTAT,PYRRES,PCASE:             *35
                            PDIET,PCEASE,PDECDTE,PAUTOPY,PHIGH1:           *40
                            PHIGH2,PHIGH3,PUSR1,PUSR2,PUSR3:               *45
                            PUSR4,PUSR5,PUSR6,PUYN1,PUYN2:                 *50
                            PUYN3,PTNINMPI,PHCNOTES,PINITHOS,PTMAEMPC:     *55
                            PTMANOKO,PTMANOKE,PTMADCNO,PTMAUKDB,PTMAUKDD:  *60
                            PNKNAME,PNKADD1,PNKADD2,PNKSUBR,NOTAVAIL:      *65
                            PNKPOST,PNKTELP,PNKTELB,PNKRELP                *70
.
MASLOD99  RETURN
.--------------------------------------------------------------------
. Load Master File Extension Variable              
.       Note NOTAVAIL is a dummy variable = its an empty FLDITMNO 
.--------------------------------------------------------------------
MX1LOD00  MOVE      "Invalid Field No.",VAR
          LOAD      VAR,FLDITMNO,PTMXRDAT,PTMXMEAL,PTMXLGA,PTMXLS,PTMXLSDT: *5
                            PTMXFHMD,PTMXMHU1,PTMXMHU2,PTMXMHU3,PTMXECNM:  *10
                            PTMXECA1,PTMXECA2,PTMXECA3,PTMXECA4,PTMXECPC:  *15
                            PTMXECPP,PTMXECBP,PTMXECRE,PTMXNRNM,PTMXNRA1:  *20
                            PTMXNRA2,PTMXNRA3,PTMXNRA4,PTMXNRPC,PTMXNRPP:  *25
                            PTMXNRBP,PTMXNRRE,PTMX3BEX,PTMXCARD,PTMXCEXP:  *30 
                            PTMXCXMP,PTMXFMLY,PTMXDOFR,PTMXSPID,PTMXOSDT:  *35
                            PTMXUKDO,PTMXCELL,PTMXADD1,PTMXADD2,PTMXADD3:  *40
                            PTMXADD4,PTMXPOST,PTMXMCCD                     *45
.
MX1LOD99  RETURN
.--------------------------------------------------------------------
. Load Doctor File Variable              
.   Parameters: DOCTCODE     
.--------------------------------------------------------------------
DOCLOD00  MATCH     SP70,DOCTCODE
          GOTO      DOCLOD90 IF EQUAL
.
          PACK      KEY6,DOCTCODE,SP70    * Read Doctor File
          CALL      RDDOCT1
          BRANCH    OVRCD,DOCLOD90
          MOVE      DTITL,FMTTITLE
          MOVE      DGNAME,FMTGNAME
          MOVE      DSNAME,FMTSNAME
          CALL      DOCNM000   * Generates Doctor Formatted Name
          MOVE      "Invalid Field No.",VAR
          LOAD      VAR,FLDITMNO,DOCFNAME,DTITL,DGNAME,DSNAME,DRNAME:  *5
                            DSADD1,DSADD2,DSADD3,DSADD4,DSPOST:        *10
                            DTELES,DTELEP,DPAGER,DPROV,DTELEH:         *15
                            DHADD1,DHADD2,DHADD3,DHADD4,DHPOST         *20
.
          GOTO      DOCLOD99
.
DOCLOD90  PACK      VAR,SP70,SP70
.
DOCLOD99  RETURN
.--------------------------------------------------------------------
. Load HCP File Variable              
.   Parameters: PMHCP001, PMHCG001, PMHCG002   
.--------------------------------------------------------------------
GPPLOD00  MATCH     SP70,PMHCP001
          GOTO      GPPLOD90 IF EQUAL
.         
          PACK      KEY10,PMHCP001,SP70      * Read HCP File
          CALL      RDPMHCP1 
          BRANCH    OVRCD,GPPLOD90
.
          PACK      KEY12,PMHCG001,PMHCG002,SP70  * Read HCP Practice File     
          CALL      RDPMHCG1 
          CALL      HCPNAM00   * Generates Formatted HCP Name
          MOVE      "Invalid Field No.",VAR
          LOAD      VAR,FLDITMNO,DOCFNAME,PMHCTITL,PMHCSNAM,PMHCGNAM,PMHCHAD1:
                            PMHCHAD2,PMHCHAD3,PMHCHAD4,PMHCHPCD,PMHCHTEL:  *10
                            PMHCHFXN,PMHGPNAM,PMHGADD1,PMHGADD2,PMHGADD3:   *15
                            PMHGADD4,PMHGPOST,PMHGPTEL                     *20
.
          GOTO      GPPLOD99
.
GPPLOD90  PACK      VAR,SP70,SP70
.
GPPLOD99  RETURN
.------------------------------------------------------------
. Load Admission File Variable              
.------------------------------------------------------------
MISLOD00  MOVE      ADMISSNO,KEY8      * Read Admission File
          CALL      RDMISS1
          BRANCH    OVRCD,MISLOD90
.
          MOVE      "Invalid Field No.",VAR
          LOAD      VAR,FLDITMNO,AADMNO,ADATE,ATIME,ASTAT,AWARD:           * 5 
                               ABED,ADOCTL,ASRCE,ATYPE,ACLSS:              *10
                               ACARE,AFUNDH,AFNDTB,AFNDMM,AELIG:           *15 
                               AVISIT,AALERG,AILLN,ADIAG1,ADIAG2:          *20
                               ACLSSFT,AMBS,ACLAIM,ADIET,APLOCCR:          *25
                               ASTAY,ADYHOSP,ACOMM1,ACOMM2,AUSR1:          *30
                               AUSR2,AUSR3,AUSR4,AUSR5,AUSR6:              *35
                               AUSR7,PTMIPDRG                              *40
.
          GOTO      MISLOD99
.
MISLOD90  PACK      VAR,SP70,SP70
.
MISLOD99  RETURN 
.--------------------------------------------------------------------
. Load Hospital File Variable              
.   Parameters:   
.--------------------------------------------------------------------
HOSLOD00  PACK      KEY3,SP70   * Hospital File
          CALL      RSPTHSP1
          CALL      RKPTHSP1
          BRANCH    OVRCD,HOSLOD90
.
          MOVE      "Invalid Field No.",VAR
          LOAD      VAR,FLDITMNO,PTHSNAME,PTHSADD1,PTHSADD2,PTHSADD3,PTHSADD4:
                                 PTHSPCOD,PTHSTELE,PTHSFAXN                *10
.
          GOTO      HOSLOD99
.
HOSLOD90  PACK      VAR,SP70,SP70
.
HOSLOD99  RETURN
.--------------------------------------------------------------------
. Load Health Fund File Variable              
.   Parameters: HEALTFND   
.--------------------------------------------------------------------
HLTLOD00  MATCH     SP70,HEALTFND
          GOTO      HLTLOD90 IF EQUAL  
.
          PACK      KEY10,HEALTFND,ZERO8,SP70   * Health Fund File
          CALL      RDFUND1 
          BRANCH    OVRCD,HLTLOD90
.
          MOVE      "Invalid Field No.",VAR
          LOAD      VAR,FLDITMNO,HFNAME,HFADD1,HFADD2,HFADD3,HFADD4:       * 5
                                 HFPCODE,HFTELE,HFFAXN,PTHFHGRP            *10
.
          GOTO      HLTLOD99
.
HLTLOD90  PACK      VAR,SP70,SP70
.
HLTLOD99  RETURN
.--------------------------------------------------------------------
. Load Emergency File Variable              
.       Note NOTAVAIL is a dummy variable = its an empty FLDITMNO 
.--------------------------------------------------------------------
EMRLOD00  PACK      KEY8,ADMISSNO,SP70     * Read Emergency Visit File 
          CALL      RDEMVIS1
          BRANCH    OVRCD,EMRLOD90
.
          MOVE      "Invalid Field No.",VAR
          LOAD      VAR,FLDITMNO,EMVISTAT,EMVIDATE,EMVITIME,EMVITRIG,EMVICOM1: 
                            EMVICOM2,EMVILOCN,EMVICOMP,EMVICLAS,EMVIINSR:  *10
                            EMVISRCE,EMVISITN,EMVIMODE,EMVIADMT,EMVIDURN:  *15 
                            EMVIEMPL,EMVIWAIT,EMVIPREV,EMVITABL,EMVIPLIN:  *20
                            EMVIINIT,EMVIRTAS,EMVIDDAT,EMVIDTIM,EMVIDSTA:  *25
                            EMVIUC01,EMVIUC02,EMVIUC03,EMVIUC04,EMVIUC05:  *30
                            EMVIUC06,EMVIUC07,EMVIUC08,EMVIUC09,EMVIUC10:  *35
                            EMVIUC11,EMVIUC12,EMVIUC13,EMVIUC14,EMVIUC15:  *40
                            EMVIUC16,EMVIUC17,EMVIUC18,EMVIUC19,EMVIUC20:  *45
                            EMVIUC21,EMVIUC22,EMVIUC23,EMVIUC24,EMVIUC25:  *50
                            EMVIUC26,EMVIUC27,EMVIUC28,EMVIUC29,EMVIUC30:  *55
                            EMVIUD01,EMVIUD02,EMVIUD03,EMVIUD04,EMVIUD05:  *60
                            EMVIUD06,EMVIUD07,EMVIUD08,EMVIUD09,EMVIUD10:  *65
                            EMVIUT01,EMVIUT02,EMVIUT03,EMVIUT04,EMVIUT05:  *70
                            EMVIUT06,EMVIUT07,EMVIUT08,EMVIUT09,EMVIUT10:  *75
                            EMVIYN01,EMVIYN02,EMVIYN03,EMVIYN04,EMVIYN05:  *80
                            EMVIYN06,EMVIYN07,EMVIYN08,EMVIYN09,EMVIYN10:  *85
                            EMVITXT1,EMVITXT2,EMVITXT3,EMVITXT4,EMVITXT5:  *90
                            EMVIUN01,EMVIUN02,EMVIUN03,EMVIUN04,EMVIUN05:  *95
                            EMVIELAP,EMVINSDT,EMVINSTM,EMVIDRDT,NOTAVAIL:  *100
                            EMVIDRTM,EMVIPATT,EMVITSRC,EMVIDEST,EMVIAMBL:  *105
                            EMVIREGN,EMVIPADM,EMVIEWRD,EMVIEBED,EMVIHFMN:  *110
                            EMVIACLS,EMVICCLS,EMVITRNS,EMVITRDT,EMVITRTM:  *115
                            EMVIATNS,EMVIEXLC,EMVISITE                     *120
.
          GOTO      EMRLOD99
.
EMRLOD90  PACK      VAR,SP70,SP70
.
EMRLOD99  RETURN
.--------------------------------------------------------------------
. Load Outpatient File Variable              
.       Note NOTAVAIL is a dummy variable = its an empty FLDITMNO 
.--------------------------------------------------------------------
OUTLOD00  PACK      KEY8,ADMISSNO,SP70     * Read OUT - Session Booking File B
          CALL      RDBOKB1
          BRANCH    OVRCD,OUTLOD90
.
          MOVE      "Invalid Field No.",VAR
          LOAD      VAR,FLDITMNO,OBASRCR,OBACOMP,OBACLASS,OBAINS,OBAATTN:  * 5 
                            OBACOMM,OTBBFUND,OTBBTBLE,DOTBBPVI,OTBBCLAL:   *10
                            OBADATE,OBACAT,OBAPRTY,OBARSCHD,OBBBOOK:       *15 
                            OBBCTYP,OBOUSR1,OBOUSR2,OBOUSR3,OBOUSR4:       *20
                            OTBBDEPT,OTBBTRAN,OTBBLNGI,OTBBTMFS,OTBBTMBO:  *25
                            OTBBGPFA,OTBBECRA,OTBBADCS,OTBBCITM,OTBBASTM:  *30
                            OTBBDPTM,OTBBOUTC,OTBBSNDL,OTBBDOFR,OBOUSR5:   *35
                            OBOUSR6,OBOUSR7,OBOUSR8,OTBBRANK               *40
.
          GOTO      OUTLOD99
.
OUTLOD90  PACK      VAR,SP70,SP70
.
OUTLOD99  RETURN
.--------------------------------------------------------------------
. Load Referral File Variable              
.       Note NOTAVAIL is a dummy variable = its an empty FLDITMNO 
.--------------------------------------------------------------------
REFLOD00  PACK      KEY8,ADMISSNO,SP70     * Read Outpatitent Referral File
          CALL      RDOTRFL1
          BRANCH    OVRCD,REFLOD90
.
          MOVE      "Invalid Field No.",VAR
          LOAD      VAR,FLDITMNO,OTRLDATE,OTRLDEPT,OTRLPRIO,OTRLDIAG,OTRLUSD1:  
                            OTRLUSD2,OTRLUSD3,OTRLUSD4,OTRLBSIT,OTRLBCLI:  *10
                            OTRLBDTE,OTRLBSTR,OTRLCDTE,OTRLREAS,OTRLSPFC:  *15 
                            OTRLCAD1,OTRLCAD2,OTRLCAD3,OTRLCAD4,OTRLCPC:   *20
                            OTRLECRN,OTRLGPFH,OTRLCTYP,OTRLSITE,OTRLSRCE:  *25
                            OTRLRHCP,OTRLDREC,OTRLDPRI,OTRLPURC,OTRLTREF:  *30
                            OTRLRANK                                       *35
.
          GOTO      REFLOD99
.
REFLOD90  PACK      VAR,SP70,SP70
.
REFLOD99  RETURN
.--------------------------------------------------------------------
. Load Waiting List File Variable              
.       Note NOTAVAIL is a dummy variable = its an empty FLDITMNO 
.--------------------------------------------------------------------
WATLOD00  PACK      KEY19,SP70     * Read Waiting List File
          CALL      RDTREA1 
          BRANCH    OVRCD,WATLOD90
.
          MOVE      "Invalid Field No.",VAR
          LOAD      VAR,FLDITMNO,WMCODE,WMTIME,WMDESC,WMREASON,DWMSTAT:    * 5 
                            WMDATE1,WMDATE2,WMDATE3,WMDATE4,WMREMOVE:      *10
                            WMUNIT,WMSTAY,WMPTY,WMNOTICE,WMPTYPE:          *15 
                            WMPCAT,WMUDEF1,WMUDEF2,WMUDEF3,WMUDEF4:        *20
                            WMKEYDT,WTWMREAD,WTWMDEDA,WTWMCLSS,WTWMDTAD:   *25
                            WTWMACAT,WTWMGADD,WTWMDABD,WTWMEATY,WTWMADMC:  *30
                            WTWMECRA,WTWMGPFA,WTWMNAFR,WTWMNATO,WTWMDOFR:  *35
                            WTWMPRO2,WTWMPRO3,WTWMTRPA,WMUDEF5,WMUDEF6:    *40
                            WMUDEF7,WMUDEF8,WTWMRANK                       *45
.
          GOTO      WATLOD99
.
WATLOD90  PACK      VAR,SP70,SP70
.
WATLOD99  RETURN
.--------------------------------------------------------------------
. Load Booking File Variable              
.       Note NOTAVAIL is a dummy variable = its an empty FLDITMNO 
.--------------------------------------------------------------------
BOKLOD00  PACK      KEY8,ADMISSNO,SP70     * Read Booking File
          CALL      RDBKREC1
          BRANCH    OVRCD,BOKLOD90
.
          MOVE      "Invalid Field No.",VAR
          LOAD      VAR,FLDITMNO,BKREEDAT,BKREATIM,BKREPDAT,BKREPTIM,BKREADAT:  
                            BKRETYPE,BKRECANC,BKRESTAT,BKREOMEM,BKREPREV:  *10
                            BKREACCM,BKREWARD,BKREEBED,BKRECLSS,BKREATYP:  *15 
                            BKREDEPT,BKRECLMT,BKRERESI,BKREEATY,BKREECRA:  *20
                            BKREGPFA,BKREDOFR,DBKREELO,BKREBKDT            *25
.
          GOTO      BOKLOD99
.
BOKLOD90  PACK      VAR,SP70,SP70
.
BOKLOD99  RETURN
.--------------------------------------------------------------------
. Process Fields called from WEBBOD00
.-------------------------------------------------------------------------------
PROFLD00  
.BRANCH    REPORTNO,PROFLD10
.          GOTO      PROFLD99
.
.      Patient Alert Details
.
.PROFLD10  BRANCH    FLDSETNO
.          GOTO      PROFLD99
.
.
PROFLD99  RETURN
.------------------------------------------------------------
.   
          INC       IBAWEBCD
          INC       WEBDATCD
.
          INC       APSMASIO/INC      * A/P Master File
          INC       BOKRC1IO/INC      * Booking File
          INC       EMRUNKIO/INC      * Unknowns Patient File IO
          INC       EMRVISIO/INC      * Emergency Visits File 
          INC       CORTYPIO/INC      * Correspondence Download Types
          INC       CORDTTIO/INC      * Correspondence Details 
          INC       PATPR1IO/INC      * Pre-admission master file           
          INC       WATTR1IO/INC      * Waiting List Patient Proc File
          INC       PATMA1IO/INC      
          INC       PMSPX2IO/INC      
          INC       PMSVX1IO/INC      
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATNIDIO/INC      * National ID Number File
          INC       PMSHCPIO/INC      * National HCP File
          INC       PMSHCGIO/INC      * HCP Practice File
          INC       PATFN1IO/INC      * Patient health Fund File
          INC       PATHSPIO/INC      * Hospital File
          INC       PATIN1IO/INC      * Insurance File
          INC       PATVISIO/INC
          INC       PATDSCIO/INC
          INC       OUTRF1IO/INC      * Outpatient Referral Letter File
          INC       OUTBB1IO/INC      * OUT - Clinic Session Booking File B
.
          INC       PATALRIO/INC      * Patient Alerts Code File
          INC       RESLNKIO/INC
          INC       PATMI1IO/INC
          INC       PATWR1IO/INC
          INC       OUTSITIO/INC      * Outpatient Site File
          INC       PATDO1IO/INC
          INC       MRTMASIO/INC
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
          INC       CLNHIMAS                   * Clear nhimasaf file variables
          INC       CALCAGE
          INC       DAYOFWEK
          INC       PATMASTM
          INC       PATDOCTM
          INC       PMSHCPTM
          INC       PMSHCGTM
          INC       PMSHCLTM
          INC       NAMSTRCD
          INC       CLPATMIS
          INC       CLPATDSC
          INC       CLPATMAS
          INC       CLPMSVX1
.
          INC       PATDOCCD

.******************************************************************************
.*              P R O G R A M  S U M M A R Y                                  *
.*              -------------  -------------                                  *
.*                                                                            *
.*     PROGRAM NAME:    IBAEMR08                                              *
.*                                                                            *
.*     FUNCTION:        DIVERTED PATIENTS REPORT                              *
.*                                                                            *
.*     DATE WRITTEN:    23/06/09                                              *
.*                                                                            *
.*     AUTHOR:          WeeLee Koo (I.B.A)                                    *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*        V9.12.03  15/09/2009 WEELEE KOO       CAR 197930                    *
.*                  Added Reason Code Description to Report Body              * 
.*        V9.12.01  03/09/2009 WEELEE KOO       CAR 197930                    *
.*                  Edited the Start and End Time                             *
.*        V9.12.00  23/06/2009 WEELEE KOO       CAR 197930                    *
.*                  Created new report for Diverted Patients                  *
.******************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. --------------------------
.
          INC       EMRDVPFD/INC          * diverted patients file 
          INC       PATCODFD/INC          * patcodes
          INC       WEBSECFD/INC          * web security file
.
. LOCAL VARIABLE DEFINITION
. --------------------------.
.
COMMENTS  DIM       500      * All Free Format Lines
CSEC      DIM       2
CURRDATE  DIM       8
DESCOPT   DIM       23       * option description in heading
DESCPT    DIM       20       * Reason Code Description (Header)
ENDDATE   DIM       8
ENDTIME   DIM       8
RCODE     DIM       3        * Reason Code Heading
RDESC     DIM       20       * Reason Code Description (Body)
RSONCODE  DIM       3        * Reason for Diversion
SITECODE  DIM       3
STRTDATE  DIM       8 
STRTTIME  DIM       8
TOTPAT    FORM      5        * Total number of patients
.
. CONSTANT DEFINITION
. -------------------
.
PRGID     INIT      "IBAEMR08"
PRGNAM    INIT      "Diverted Patients Report"
VERSION   INIT      "V12.01.00"
.
.*****************************************************************************
.*                              MAIN0000                                     *
.*                       Controlling Logic (Mainline code)                   *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN1000  CALL      INPT0000               * get user input
          BRANCH    EXIT,MAIN9999
.          
MAIN2000  CALL      SITE0000               * get site code
          BRANCH    EXIT,MAIN2000
.
          CALL      SDAT0000               * get start date
          BRANCH    EXIT,MAIN1000          * nothing entered
.
          CALL      EDAT0000               * get end date
          BRANCH    EXIT,MAIN1000          * nothing entered
.
          CALL      GDVC0000               * get Reason for diversion code
          BRANCH    EXIT,MAIN1000
.
          CALL      CONTQST                * ok to continue ?
          BRANCH    CEXIT,MAIN5000:        * Yes
                          MAIN1000:        * No
                          MAIN1000         * Cancel
.
MAIN5000  CALL      RRUN0000               * load records for printing
          CALL      GRNT0000               * print grand total
.
MAIN9999  CHAIN     PGM
+
.*****************************************************************************
.*                       Initialization                                      *
.*                                                                           *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"emrdvpaf";
          OPEN      EMRDVPA1,"emrdvpaf"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"websecaf";
          OPEN      WEBSECA1,"websecaf"
.
.
INIT9999  RETURN
.
.*****************************************************************************
.*                      Get User Option                                      *
.*                                                                           *
.*****************************************************************************
.
INPT0000  MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
          MOVE      ZERO,TOTPAT
          MOVE      ZERO,EXIT
.
          HLINE     *G37,2,50,80
          DISPLAY   *P39:4,*EF:
                    *P1:5,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:6,*V2LON,ONE,*HOFF," = Run Report"
.
INPT0500  KEYIN     *P1:10,"Select Option : ",*V2LON,OPTION
.                    
          COMPARE   ZERO,OPTION
          GOTO      INPT9000 IF EQUAL
.
          BRANCH    OPTION,INPT1000
.
          BEEP
          GOTO      INPT0500
.
INPT1000  MOVE      ZERO,EXIT
          GOTO      INPT9999
.
INPT9000  MOVE      ONE,EXIT
.  
INPT9999  RETURN
.
.****************************************************************************
.*                             SITE0000                Called by: MAIN0000  *
.*          EXIT  0 = valid start date entered                              *
.*                1 = nothing entered                                       *
.****************************************************************************
.
SITE0000  MOVE      SP3,SITECODE
          KEYIN     *P1:12,*EF,"Site Code :",SITECODE
          MOVE      TEN2,CVERT
          MOVE      TEN2,CCOL
.
          PACK      KEY10,SP20
          CALL      RSWBSE1
SITE1000  CALL      RKWBSE1
          BRANCH    OVRCD,SITE9000

          MATCH     SP3,SITECODE
          GOTO      SITE9100 IF EQUAL
.
          MATCH     SITECODE,WBSEESC
          GOTO      SITE1000 IF NOT EQUAL

          MOVE      ZERO,EXIT
          GOTO      SITE9999

SITE9000  DISPLAY   *P1:24,*EL,*B,"Invalid Site Code ";
          CALL      HITENTER
          GOTO      SITE0000
.
SITE9100  MOVE      ONE,EXIT
.
SITE9999  RETURN
.
.****************************************************************************
.*                             SDAT0000                Called by: MAIN0000  *
.*                     Get starting date and time                           *
.* Returns: STRTDATE: starting date (ccyymmdd)                              *
.*          STRTTIME: starting time (hh:mm:ss)                              *
.*          EXIT  0 = valid start date/time entered                         *
.*                1 = nothing entered                                       *
.****************************************************************************
.
SDAT0000  DISPLAY   *P1:13,*EF,"From Date :"
          MOVE      TEN3,CVERT
          MOVE      TEN3,CCOL
.
          CALL      IBACLOCK                     * default to current date
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          UNPACK    CURRDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,SDAT9100
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     STRTDATE,CURRDATE            * date in future ?
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date cannot be in the future. ";
            CALL      HITENTER
            GOTO      SDAT0000
          ENDIF
.
SDAT1000  KEYIN     *P1:14,*EF,"From Time (HH:MM:SS) :",STRTTIME
          MOVE      TEN4,CVERT
          MOVE      TEN4,CCOL
.
          MATCH     SP8,STRTTIME
          GOTO      SDAT9100 IF EQUAL

          MOVE      ZERO,EXIT
          GOTO      SDAT9999
.
SDAT9100  DISPLAY   *P1:24,*EL,*B,"Start Time is a mandatory field ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
SDAT9999  RETURN
+
.****************************************************************************
.*                             EDAT0000                Called by: MAIN0000  *
.*                    Get ending date and time                              *
.* Requires: ENDDATE: ending date (ccyymmdd)                                *
.*           ENDTIME: ending time (HH:MM:SS)                                    *
.* Returns : EXIT  0 = valid end date entered                               *
.*                 1 = nothing entered                                      *
.****************************************************************************
.
EDAT0000  DISPLAY   *P1:16,*EL,"To Date   :"
          MOVE      TEN6,CVERT
          MOVE      TEN6,CCOL
.
          UNPACK    CURRDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,EDAT9100
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     STRTDATE,ENDDATE             * start date > end date ?
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Start date greater than end date.  ";
            CALL      HITENTER
            GOTO      EDAT0000
          ENDIF
.
          MATCH     ENDDATE,CURRDATE             * date in future ?
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date cannot be in the future.  ";
            CALL      HITENTER
            GOTO      EDAT0000
          ENDIF
.
EDAT1000  KEYIN   *P1:17,*EF,"To Time (HH:MM:SS) :",ENDTIME
          MOVE      TEN7,CVERT
          MOVE      TEN7,CCOL
.
          MATCH     SP8,ENDTIME
          GOTO      EDAT9100 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      EDAT9999
.
EDAT9100  DISPLAY   *P1:24,*EL,*B,"End Time is a mandatory field ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
EDAT9999  RETURN
+
.****************************************************************************
.*                             GDVC0000                Called by: MAIN0000  *
.*                          Get division code                               *
.****************************************************************************
.
GDVC0000  MOVE      SP3,RSONCODE
	KEYIN     *P1:19,*EL,"Reason for Division Code :",RSONCODE
          MOVE      TEN9,CVERT
          MOVE      TEN3,CCOL
          RESET     RSONCODE

GDVC1000  MOVE      "el",KEY2
          PACK      KEY5,KEY2,SP5
          CALL      RDSCODE1
GDVC2000  CALL      RDKCODE1
          BRANCH    OVRCD,GDVC9999
.
          MATCH     KEY2,TCODE
          GOTO      GDVC9999 IF NOT EQUAL
.
          MATCH     TDESC,SP20
          GOTO      GDVC2000 IF EQUAL
.
          MATCH     RSONCODE,ACODE
          IF        @EQUAL
            MOVE      TDESC,DESCPT
          ENDIF
.
          GOTO      GDVC2000          
.
GDVC9999  RETURN   
.
.*****************************************************************************
.*           Get Records from Diversion Date Range                           *
.*                                                                           *
.*****************************************************************************
.
RRUN0000  PACK      KEY19,SITECODE,STRTDATE,STRTTIME,SP20
          CALL      RDEMDVP1
RRUN1000  CALL      RKEMDVP1
          BRANCH    OVRCD,RRUN9999
.
          MATCH     EMDVDDTE,ENDDATE
          GOTO      RRUN2000 IF EQUAL
          GOTO      RRUN9999 IF LESS
.
          GOTO      RRUN3000
.
RRUN2000  MATCH     EMDVDTIM,ENDTIME
          GOTO      RRUN1000 IF LESS
.
RRUN3000  MATCH     SP3,RSONCODE
          GOTO      RRUN9000 IF EQUAL

          MATCH     RSONCODE,EMDVRDVN
          GOTO      RRUN1000 IF NOT EQUAL
.
RRUN9000  CALL      PRNT0000        * Print Record
          GOTO      RRUN1000
.
RRUN9999  RETURN
.
.*****************************************************************************
.*                       Print Record Lines                                  *
.*                                                                           *
.*****************************************************************************
.
PRNT0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          UNPACK    EMDVDDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          STRIP     EMDVFFL1
          STRIP     EMDVFFL2
          STRIP     EMDVFFL3
          STRIP     EMDVFFL4
          STRIP     EMDVFFL5
.
          PACK      COMMENTS,EMDVFFL1,SP1,EMDVFFL2,SP1,EMDVFFL3,SP1:
                    EMDVFFL4,SP1,EMDVFFL5
.
          MOVE      SP20,RDESC
          MOVE      "el",KEY2
          PACK      KEY5,KEY2,EMDVRDVN,SP5
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT9000
.
          MOVE      TDESC,RDESC
.
PRNT9000  PRINT     *1,"|",CPDATE," at ",EMDVDTIM,*30,"|",EMDVRDVN:
                    *36,"|",RDESC,*60,"|",COMMENTS,*131,"|"
          ADD       ONE,CLNO
          ADD       ONE,TOTPAT
.
PRNT9999  RETURN
.
.*****************************************************************************
.*                       Print Grand Totals                                  *
.*                                                                           *
.*****************************************************************************
.
GRNT0000  COMPARE   ZERO,CPAGENO
          GOTO      GRNT9000 IF EQUAL
.
          CALL      PDOT0000
.
          PRINT     *N,"Total number of Patients : ",TOTPAT:
                    *N,*N,"** END OF REPORT **"
          DISPLAY   *P1:24,*EL:
                    *P40:24,*V2LON,"*** Generation Completed ***",*W2;
          GOTO      GRNT1000

GRNT1000  MOVE      ONE,EXIT
          GOTO      GRNT9999
.
GRNT9000  DISPLAY   *P1:24,*EL,*B:
                    *P40:24,*V2LON,"*** No Available Data ***",*W2;
          GOTO      GRNT1000
.
GRNT9999  RETURN
.
.*****************************************************************************
.*                       Display Heading                                     *
.*                                                                           *
.*****************************************************************************
.
HEAD0000  COMPARE   ZERO,CPAGENO
          CALL      PDOT0000 IF NOT EQUAL
.
          MOVE      ONE,CNOUNDLN
          MOVE      DESCOPT,CPHDROPT
          CALL      IBACLOCK
          CALL      HEAD132
.
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          UNPACK    STRTTIME,CHOUR,DIM1,CMIN,DIM1,CSEC
.
          PRINT     *25,"Diverted Patients From    : ",CPCDATE,SP1,CHOUR,":":
                    CMIN,":",CSEC,SP2;

          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          UNPACK    ENDTIME,CHOUR,DIM1,CMIN,DIM1,CSEC
.
          PRINT     "To: ",CPCDATE,SP1,CHOUR,":",CMIN,":",CSEC
.         
          MATCH     SP3,RSONCODE 
          IF        @EQUAL
            MOVE      "All",RCODE
          ELSE
            MOVE      RSONCODE,RCODE
          ENDIF
.           
          PRINT     *25,"Reason for Diversion Code : ",RCODE,SP3,DESCPT
.
          PRINT     *N
          CALL      PDOT0000
.
          PRINT     *1,"|Date/Time",*30,"|Code":
                    *36,"|Description",*60,"|Comments",*132,"|"
.
          CALL      PDOT0000
          MOVE      TEN2,CLNO
.
HEAD9999  RETURN
.
.*****************************************************************************
.*                       Print dotted lines                                  *
.*                                                                           *
.*****************************************************************************
.
PDOT0000  PRINT     *1,"*--------------------------------------------":
                    "--------------------------------------------":
                    "------------------------------------------*"
          ADD       ONE,CLNO
.
PDOT9999  RETURN
.
.*****************************************************************************
.*                        I/O's Includes                                     *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001IO
.
          INC       EMRDVPIO/INC         * diverted patients report
          INC       PATCODIO/INC         * patcodes
          INC       WEBSECIO/INC         * web security file

.
.

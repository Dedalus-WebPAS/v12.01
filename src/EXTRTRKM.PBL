.*****************************************************************************
.* System    :   Medical Records Tracking                                    *
.* Program   :   EXTRTRKM                                                    *
.* Desc      :   Medical Records Master Data Extraction Program              *
.*****************************************************************************
.* Date      :   06/03/2014                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will extract the Medical Records Master        *
.*               details into a pipe delimited file in a format that will    *
.*               feed into CONVTRKM.                                         *
.* Mods:                                                                     *
.*        V10.08.01 18/04/2016  Steve Armstrong  CAR 0312150                 *
.*                  Mods to allow extraction for a specific campus           *
.*****************************************************************************
.*        V10.04.00 27/02/2014  Steve Armstrong  CAR 296128                  *
.*                  Created program.                                         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       IBACONFD/INC
          INC       MRTMASFD/INC
          INC       PATHSPFD/INC
.
.
TRAKMEXT  FILE      ASCII,FIXED=4096
.
.         MRT Master upload file layout - convtrkm.txt
.         (as per CONVTRKM.PBL)
.
.Name     Type    Length Description
.----     ----    ------ -----------
MASTURNO  DIM       8    U/R Number                             (mrmaurno)
MASTHHOS  DIM       3    Home Hospital (pathspaf)               (mrmahhos)
MASTHLOC  DIM       4    Home Location (coded - mrtlocaf)       (mrmahloc)
MASTDOTY  DIM       3    Type of document (Cat TY)              (mrmadoty)
MASTVOLN  DIM       2    Volume                                 (mrmavoln)
MASTSTAT  DIM       3    Record Status (Cat RS)                 (mrmastat)
MASTFILM  DIM       25   MicroFilm Address                      (mrmafilm)
MASTEPDT  DIM       8    'Episode To' Date (ccyymmdd)           (mrmaepdt)
MASTOHOS  DIM       3    Originating Hospital (pathspaf)        (mrmaohos)
MASTCOMM  DIM       20   Comments                               (mrmacomm)
MASTCHOS  DIM       3    Current Hospital (pathspaf)            (mrmachos)
MASTCLOC  DIM       4    Current Location (coded - mrtlocaf)    (mrmacloc)
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
EXTCOUNT  FORM      10
HOSPCODE  DIM       3
RECCOUNT  FORM      10
.
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
.
PRGID     INIT      "EXTRTRKM"
PRGNAM    INIT      "MR Tracking Master Data Extraction"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * MRT Master extract
.
MAIN1000  CALL      GHOS0000               * get hospital
          BRANCH    EXIT,MAIN2000:         * nothing entered (All)
                         MAIN0100          * exitchar entered
.
MAIN2000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:        * yes
                          MAIN1000:        * no
                          MAIN0100         * cancel
.
MAIN5000  CALL      CFIL0000               * create file
          BRANCH    EXIT,MAIN0100          * file exists
.
          CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"mrtmasaf";
          OPEN      MRTMASA1,"mrtmasaf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run data extraction                     *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". MR Tracking Master Data ":
                    "Extraction"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run extraction
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                                  GHOS0000       Called by: MAIN0000       *
.*       Get the hospital code for extraction (Multi-hospital only)          *
.* Returns: EXIT  0 = valid code entered                                     *
.*                1 = spaces entered (select ALL hospitals)                  *
.*                2 = exitchar entered                                       *
.*          HOSPCODE - Hospital code (pathspaf)                              *
.*****************************************************************************
.
GHOS0000  COMPARE   ONE,IBCNMHOS                 * using multi-hospital
          GOTO      GHOS9000 IF NOT EQUAL        * no - finished
.
          MOVE      TEN7,ECOL
          MOVE      TEN,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          MOVE      ZERO,CKYIMODE
.
GHOS1000  DISPLAY   *P1:EVERT,*EF,"Home Hospital :":
                    *P1:24,"Enter spaces to select ALL"
          CALL      PATHSPKY
          BRANCH    EXIT,GHOS9100:               * nothing input
                         GHOS9200:               * exitchar input
                         GHOS9100                * spaces input
.
GHOS9000  DISPLAY   *P17:EVERT,*V2LON,PTHSHOSP,*HOFF:
                    *P25:EVERT,PTHSNAME
          MOVE      PTHSHOSP,HOSPCODE
          MOVE      ZERO,EXIT
          GOTO      GHOS9999
.
GHOS9100  MOVE      SP3,HOSPCODE
          DISPLAY   *P17:EVERT,*V2LON,"All"
          MOVE      ONE,EXIT
          GOTO      GHOS9999
.
GHOS9200  MOVE      TWO,EXIT
.
GHOS9999  RETURN
+
.*****************************************************************************
.*                                  CFIL0000       Called by: MAIN0000       *
.*                         Create the extract file                           *
.*****************************************************************************
.
CFIL0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TRAKMEXT,"convtrkm"     * extract file exists ?
          TRAPCLR   IO
          BRANCH    OVRCD,CFIL5000          * no - create it
.
CFIL1000  KEYIN     *P1:23,*B,*EF,"The extract file convtrkm.txt already ":
                    "exists.":
                    *P1:24,"Overwrite it (",*V2LON,*DV,ANSY,*HOFF,"/":
                    *V2LON,*DV,ANSN,*HOFF,")?":
                    *P21:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS                * "Y" entered ?
          GOTO      CFIL2000 IF EQUAL       * yes - continue
.
          MATCH     ANSN,ANS                * "N" entered ?
          GOTO      CFIL9100 IF EQUAL       * no
.
          BEEP
          GOTO      CFIL1000
.
.         Delete the existing file
.
CFIL2000  CLOSE     TRAKMEXT,DELETE
.
.         Create a new extract file
.
CFIL5000  PREP      TRAKMEXT,"convtrkm"
.
          MOVE      ZERO,EXIT
          GOTO      CFIL9999
.
CFIL9100  MOVE      ONE,EXIT
.
CFIL9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*        Loop through the MR Master file and extract each record            *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,RECCOUNT                * initialise record count
          MOVE      ZERO,EXTCOUNT                * initialise extract count
          DISPLAY   *P1:24,*EL,"Processing:";
.
          MOVE      SP20,KEY20
          CALL      RSMRMAS1                     * position at start of file
PROC0500  CALL      RKMRMAS1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          DISPLAY   *P13:24,*V2LON,RECCOUNT
.
          COMPARE   ONE,IBCNMHOS                 * using multi-hospital ?
          GOTO      PROC1000 IF NOT EQUAL        * no
.
          MATCH     SP3,HOSPCODE                 * all hospitals selected ?
          GOTO      PROC1000 IF EQUAL            * yes
.
          MATCH     HOSPCODE,MRMAHHOS            * same hospital as selected ?
          GOTO      PROC0500 IF NOT EQUAL        * no - ignore record
.
PROC1000  ADD       ONE,RECCOUNT                 * increment record counter
.
          MOVE      MRMAURNO,MASTURNO
          SQUEEZE   MASTURNO
          MOVE      MRMAHHOS,MASTHHOS
          SQUEEZE   MASTHHOS
          MOVE      MRMAHLOC,MASTHLOC
          SQUEEZE   MASTHLOC
          MOVE      MRMADOTY,MASTDOTY
          SQUEEZE   MASTDOTY
          MOVE      MRMAVOLN,MASTVOLN
          SQUEEZE   MASTVOLN
          MOVE      MRMASTAT,MASTSTAT
          SQUEEZE   MASTSTAT
          MOVE      MRMAFILM,MASTFILM
          STRIP     MASTFILM
          MOVE      MRMAEPDT,MASTEPDT
          SQUEEZE   MASTEPDT
          MOVE      MRMAOHOS,MASTOHOS
          SQUEEZE   MASTOHOS
          MOVE      MRMACOMM,MASTCOMM
          STRIP     MASTCOMM
          MOVE      MRMACHOS,MASTCHOS
          SQUEEZE   MASTCHOS
          MOVE      MRMACLOC,MASTCLOC
          SQUEEZE   MASTCLOC
.
          WRITE     TRAKMEXT,SEQ;*+,MASTURNO,PIPE,MASTHHOS,PIPE,MASTHLOC,PIPE:
                                    MASTDOTY,PIPE,MASTVOLN,PIPE,MASTSTAT,PIPE:
                                    MASTFILM,PIPE,MASTEPDT,PIPE,MASTOHOS,PIPE:
                                    MASTCOMM,PIPE,MASTCHOS,PIPE,MASTCLOC,*-
.
          ADD       ONE,EXTCOUNT                 * increment extract count
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:23,*EF,"Extraction complete.":
                    *P1:24,*V2LON,EXTCOUNT,*HOFF," records extracted.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATHSPKY
.
          INC       MRTMASIO/INC
          INC       PATHSPIO/INC

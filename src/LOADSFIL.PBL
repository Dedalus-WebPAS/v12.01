.******************************************************************************
.* System         : General                                                   *
.* Program        : LOADSFIL                                                  *
.* Description    : Load data from a text file to a SFILE                     *
.******************************************************************************
.* Author         : Graeme Williams  29/12/1995                               *
.* Modifications  :                                                           *
.*        V10.00.01  Steve Armstrong    CAR 224098                            *
.*                   Removed TRAP on RANGE error when reading through the     *
.*                   SFILE when extracting data to a FILE.  Replaced with     *
.*                   check of OVER flag.                                      *
.******************************************************************************
.
          INC       STD001FD
.
. ------- Program Variables ---------------------------------------------
.
FILENAME  DIM       8
RECLEN    FORM      3
SECCNT    FORM      10
SFILE     SFILE     ASCII, FIXED=RECLEN
TFILE     FILE      ASCII, FIXED=RECLEN
V0        DIM       100
V1        DIM       100
V2        DIM       100
V3        DIM       100
V4        DIM       100
V5        DIM       100
V6        DIM       100
V7        DIM       100
V8        DIM       100
V9        DIM       100
.
. ------- Program Constants ---------------------------------------------
.
PRGID     INIT      "LOADSFIL"
PRGNAM    INIT      "Load data into an SFILE"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.* ML0000   : Mainline Code                                                   *
.******************************************************************************
ML0000    CALL      INIT0000                 * program initialisation
.
ML1000    CALL      OPTN0000                 * select sub-option
          BRANCH    OPTION,ML1100:           * Load data
                           ML1200            * Extract data
          GOTO      ML9999
.
.         Load data
.
ML1100    CALL      KFIL0000                 * get filename
          BRANCH    EXIT,ML1000              * nothing input
          CALL      CONTQST                  * okay to continue?
          BRANCH    CEXIT,ML1150,ML1100,ML1000
.
ML1150    CALL      LOAD0000                 * load new details
          CALL      END0000                  * end of processing
          GOTO      ML1100
.
.         Extract data
.
ML1200    CALL      KFIL0000                 * keyin run number to be extracted
          BRANCH    EXIT,ML1000              * nothing input
          CALL      CONTQST                  * okay to continue?
          BRANCH    CEXIT,ML1250,ML1200,ML1000
.
ML1250    CALL      EXTR0000                 * extract existing details
          CALL      END0000                  * end of processing
          CALL      ML1200
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.* INIT0000   : Program initialisation                                        *
.* Called by  : ML0000                                                        *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
INIT9999  RETURN
.******************************************************************************
.* OPTN0000   : Select program option                                         *
.* Called by  : ML0000                                                        *
.* Returns    : OPTION   0 = Exit                                             *
.*                       1 = Load Data                                        *
.*                       2 = Extract Data                                     *
.******************************************************************************
OPTN0000  HLINE     *G37,2,55,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Load data (from FILE to SFILE)":
                    *P1:6,*V2LON,TWO,*HOFF," = Extract data (from SFILE to FILE)":
                    *P1:8,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL
.
          DISPLAY   *P17:8,*V2LON,OPTION
.
          BRANCH    OPTION,OPTN1100,OPTN1200
          BEEP
          GOTO      OPTN1000
.
OPTN1100  DISPLAY   *P55:2,*V2LON,"- Load "
          GOTO      OPTN9999
.
OPTN1200  DISPLAY   *P55:2,*V2LON,"- Extract "
.
OPTN9999  RETURN
.******************************************************************************
.* KFIL0000 : Keyin file name and record length                               *
.* Returns  : EXIT     - 0 = Data input, 1 = Nothing input                    *
.******************************************************************************
KFIL0000  IF        OPTION = 1
            DISPLAY     *P1:20,*EF,"If converting controlf.txt to controlf.dat":
                        ", the following applies:":
                        *P1:21,"Record length = 256":
                        *P1:22,"Set TB7SFILE=TRUE; export TB7SFILE":
                        *P1:23,"Run from command line ":
                        "eg: RUN <switch> LOADSFIL":
                        *P1:24;
            CALL        HITENTER
          ENDIF
.
KFIL0100  DISPLAY   *P1:10,"Enter record length : ",*EF;
.
          KEYIN     *P23:10,*V2LON,*EL,RECLEN
.
          IF        RECLEN = 0
            GOTO      KFIL9000
          ENDIF
          IF        RECLEN < 0
            DISPLAY   *P1:24,*EL,*B,"Invalid record length.  ";
            CALL      HITENTER
            GOTO      KFIL0100
          ENDIF
.
          DISPLAY   *P1:12,"Enter file name     :",*EF;
.
KFIL1000  KEYIN     *P23:12,*V2LON,*EL,FILENAME
.
          PACK      FILENAME,FILENAME,SP8
          MATCH     SP8,FILENAME
          GOTO      KFIL0100 IF EQUAL
.
          STRIP     FILENAME
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      SFILE,FILENAME
          TRAPCLR   IO
.
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Cannot open an index file of this name. ";
            CALL      HITENTER
            CLOSE     TFILE
            GOTO      KFIL1000
          ENDIF
.
          IF        OPTION = 2
            PREP      TFILE,FILENAME
            MOVE      ZERO,EXIT
            GOTO      KFIL9999
          ENDIF
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TFILE,FILENAME
          TRAPCLR   IO
.
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Cannot open a text file of this name. ";
            CALL      HITENTER
            GOTO      KFIL1000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      KFIL9999
.
KFIL9000  MOVE      ONE,EXIT
.
KFIL9999  RETURN
+
.******************************************************************************
.* END0000  : Completed a run                                                 *
.******************************************************************************
END0000   DISPLAY   *P1:22,*EF:
                    *P1:23,"No. Records: ",*V2LON,SECCNT
.
          DISPLAY   *P1:24,*EL,"Processing Completed. ";
          CALL      HITENTER
.
END9999   RETURN
+
.******************************************************************************
.* LOAD0000   : Load details from existing file                               *
.* Called by  : ML0000                                                        *
.******************************************************************************
LOAD0000  DISPLAY   *P1:24,*EL,"Processing: ";
          MOVE      ZERO,SECCNT
LOAD1000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF RANGE
          READ      TFILE,SECCNT;*1,V0,V1,V2,V3,V4,V5,V6,V7,V8,V9
          TRAPCLR   RANGE
          BRANCH    OVRCD,LOAD9000
.
          DISPLAY   *P13:24,SECCNT;
.
          WRITE     SFILE,SECCNT;V0,V1,V2,V3,V4,V5,V6,V7,V8,V9
          ADD       ONE,SECCNT
          GOTO      LOAD1000
.
LOAD9000  CLOSE     SFILE
          CLOSE     TFILE
.
LOAD9999  RETURN
+
.******************************************************************************
.* EXTR0000   : Load details into a new file                                  *
.* Called by  : ML0000                                                        *
.******************************************************************************
EXTR0000  DISPLAY   *P1:24,*EL,"Processing: ";
          MOVE      ZERO,SECCNT
EXTR1000  READ      SFILE,SECCNT;*1,V0,V1,V2,V3,V4,V5,V6,V7,V8,V9
          GOTO      EXTR9000 IF OVER
.
          DISPLAY   *P13:24,SECCNT;
.
          WRITE     TFILE,SECCNT;V0,V1,V2,V3,V4,V5,V6,V7,V8,V9
          ADD       ONE,SECCNT
          GOTO      EXTR1000
.
EXTR9000  CLOSE     SFILE
          CLOSE     TFILE
.
EXTR9999  RETURN
.
          INC       STD001IO

.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   FIXWC1MR                                                  *
.* Desc      :   Fix ur numbers on patwc1af and patwmabf for merged patients*
.***************************************************************************
.* Date      :   13/07/2006                                                *
.* Author    :   Jill Habasque                                             *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATWC1FD/INC
          INC       PATVISFD/INC
          INC       PATWMAFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
PRGID     INIT      "FIXWC1MR"
PRGNAM    INIT      "Fix patwc1af for merged patients"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML1000  * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process .
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PATWC1A1,"patwc1af"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATWMAB1,"patwmabf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Print Only":
                    *P1:6,*V2LON,TWO,*HOFF,". Print and Update"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  PACK      KEY8,SP70
          CALL      RDSWCOM1
PROC1000  CALL      RDKWCOM1
          BRANCH    OVRCD,PROC9999
.
          PACK      KEY8,DWCADMNO,SP70
          CALL      RDVISA1
          BRANCH    OVRCD,PROC1000
.
          MATCH     PVIURNO,PTWCURNO
          GOTO      PROC1000 IF EQUAL
.
          PRINT     *1,"Visit ",DWCADMNO," will be updated from UR ",PTWCURNO," to ",PVIURNO
.
          PACK      PTWCURNO,PVIURNO,SP70
.
          COMPARE   TWO,COPTION
          GOTO      PROC1000 IF NOT EQUAL
.
          CALL      UPWCOM1
          GOTO      PROC1000
.
PROC9999  RETURN
+
.**************************************************************************
.*                               PROT0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROT0000  PACK      KEY8,SP70
          CALL      RDSWMAB1
PROT1000  CALL      RDKWMAB1
          BRANCH    OVRCD,PROT9999
.
          PACK      KEY8,DMADMNO,SP70
          CALL      RDVISA1
          BRANCH    OVRCD,PROT1000
.
          MATCH     PVIURNO,PTWMURNO
          GOTO      PROT1000 IF EQUAL
.
          PRINT     *1,"Visit ",DMADMNO," will be updated from UR ",PTWMURNO," to ",PVIURNO
.
          PACK      PTWMURNO,PVIURNO,SP70
.
          COMPARE   TWO,COPTION
          GOTO      PROT1000 IF NOT EQUAL
.
          CALL      UPWMAB1
          GOTO      PROT1000
.
PROT9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATWC1IO/INC
          INC       PATWMAIO/INC
          INC       PATVISIO/INC

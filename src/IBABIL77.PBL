.******************************************************************************
.*              P R O G R A M  S U M M A R Y                                  *
.*              -------------  -------------                                  *
.*                                                                            *
.*     PROGRAM NAME:     IBABIL77                                             *
.*                                                                            *
.*     FUNCTION:         CROSS INDEX OF OPERATIONS                            *
.*                                                                            *
.*     DATE WRITTEN:     28/05/87                                             *
.*                                                                            *
.*     AUTHOR:           PAUL LO                                              *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                                                                            *
.******************************************************************************
.*       V12.00.01  16/05/2025  Ebon Clements TSK 0955096                     *
.*                  Alphanueric visit number changes                          *
.*       V11.05.02  24/03/2025  Davin         TSK 0956210                     *
.*                  Add ICD10 Ed.13 - Go-live Date PTCNGDX3 (Sect.130 Pos.86) *
.*       V11.05.01  17.12.2024  DA Sarkies    TSK 0951086                     *
.*                  Increased size of NBRDAYS                                 *
.*       V11.02.01  31/03/2022  Davin         TSK 0917793                     *
.*                  Add ICD10 Ed.12 - Go-live Date PTCNGDX2 (Sect.128 Pos.231)*
.*       V11.00.01  28/01/2021  Thanh T       TSK 0887203                     *
.*                  Added PROC2501 section for Billed DRG functionality       *
.******************************************************************************
.*       V10.10.01  17/03/2017  Davin          TSK 0833093     HDP 2017/2018  *
.*                  Add ICD10 Ed.10 - Go-live Date PTCNGDVX (Sect.110 Pos.85) *
.*       V10.06.02  26/08/2015  Ebon Clements CAR 320443                      *
.*                  Commented out using ICD 9 switch                          *
.*       V10.06.01  17/03/2015  Davin         CAR 311669      HDP 2015/16     *
.*                  Add PTCNGDV9 for ICD10 Ed.9 Go-live Date (Sect.110 Pos.77)*
.*       V10.05.01  02/02/2015  Ania P        CAR 261630                      *
.*                  Removed use of PORT for temp file naming                  *
.*       V10.03.02  27/09/2013  Patrick Adair CAR 290003                      *
.*                  Replaced call to RDPT10O1 with RDPTICO1 in SETOPR00       *
.*                  Removed duplicate RDPTIC01 call in PRNT3000               *
.*       V10.03.01  23/04/2013  Davin         CAR 284420      HDP 2013/14     *
.*                  Add PTCNGDV8 for ICD10 Ed.8 Go-live Date (Sect.110 Pos.69)*
.*       V10.02.01  23/03/2011  Mike Laming   CAR 240107                      *
.*                  Remove PTCNUCEP PATMRDFD/IO and PATMROFD/IO               *
.*                  Mods for Key changes in PATECDFD & PATECOFD               *
.*       V10.00.03  27/10/2010  Mike Laming   CAR 176738                      *
.*                  Add "Sub-Total Primary" line & correct "Sub-Total Patient"*
.*       V10.00.02  03/06/2010  Mike Laming   CAR 216427                      *
.*                  Add "Print Principal Condition Only" option, add "Totals  *
.*                  for Operation Code" as per v6.17.(major re-work & cleanup)*
.*       V10.00.01  07/04/2010  Mike Laming   CAR 219246      HDP 2010        *
.*                  Add PTCNGDV7 for ICD10 Ed.7 Go-live Date (Sect.110 Pos.61)*
.*        V9.11.01  13/02/2009  Mike Laming   CAR 167192                      *
.*                  Add "Doctor Codes" to "Doctor Type" option (as per v6.16) *
.*        V9.09.01  20/03/2008  Mike Laming   CAR 163918    HDP 2008          *
.*                  Add PTCNGDV6 for ICD10 Ed.6 Go-live                       *
.*        V9.06.01  15/05/2006  Mike Laming   CAR 105244      2006 HDP        *
.*                  ADD PTCNGDV5 for ICD10 Ed.5 Go-live                       *
.*        V9.04.06  29/11/2005  Peter Vela    CAR 85461                       *
.*                  Fixed validation of three digit block numbers             *
.*                  LOOP4 NEXT9                                               *
.*        V9.04.05  10/08/2005  Mike Laming   CAR 65685                       *
.*                  Second attempt (RJUSTIFY doesn't work)                    *
.*        V9.04.04  02/08/2005  Mike Laming   CAR 65685                       *
.*                  Changes to right-align Block codes (both keyin & input)   *
.*                  Correct Headings for Block selection                      *
.*        V9.04.03  08/07/2005  Mike Laming   CAR 63036                       *
.*                  Re-compiled for PATICOKY - use RDPTICO1 instead of RDICO1 *
.*        V9.04.02  25/11/2004  Peter Vela       CAR 55193                    *
.*                  Fixed Block specified range                               *
.*        V9.04.01  23/08/2004  Lina Vo CAR 52901                             *
.*                  Added Multi-Hospital processing                           *
.*        V9.03.04  23/06/2004  Henry Son        CAR 49037                    *
.*                  ICD10 Ed.4 changes - add PTCNGDV4.                        *
.*        V9.03.03  22/01/2004  Davin          CAR 45361                      *
.*                  Added sequence option (admno,doctor,disch.date & u/r)     *
.*                  Added new filter (unit)                                   *
.*                  Added unit, disch.destination & DRG to report             *
.*        V9.03.02  31/12/2003  Sylvek Litewka CAR 20222                      *
.*                  Replaced calls to RDPT10O1 (ICD10 Procedure V2 file read) *
.*                  with RDPTICO1 (latest ICD10 version read).                *
.*        V9.03.01  28/11/2003  Davin          CAR 45361                      *
.*                  Removed duplication of info from report                   *
.*        V9.02.06  02/04/2003  Lina Vo        CAR 37660                      *
.*                  Fixed validation of operation code to use start & end date*
.*                  to determine whether validating for ICD9 or ICD10 codes.  *
.*        V9.02.05  29/08/2002  Jill Habasque  SRF 17573                      *
.*                  Changed to use RTIODAYS instead of NHOSPDAY               *
.*        V9.02.04  14/01/2002  Raghunandan Surve, HPS    SRF13724            *
.*                  Fixed number of operation codes, sub-bed days primary     *
.*                  Removed KEYDATEX, using KEYDATE instead                   *
.*        V9.02.03  09/01/2002  Raghunandan Surve, HPS    SRF13724            *
.*                  Display items 3 and 4 by default as From and To           *
.*        V9.02.02  08/01/2002  Raghunandan Surve, HPS    SRF13724            *
.*                  Fixed field numbers, fixed summary report                 *
.*        V9.02.01  27/08/2001  Dean Cramer                                   *
.*                  Fix I*L on paticdgf                                       *
.*        V5.09.B02 07/02/2001  Dean Cramer                                   *
.*                  Include process notes at end of report                    *
.*        V5.09.B01 12/12/2000  Dean Cramer                                   *
.*                  Include logic for ranging by block number.                *
.*        V5.08.06  07/12/2000  Dean Cramer                                   *
.*                  Include logic for print & sub totals grouping by operation*
.*                  block number.                                             *
.*                  Rework variables - remove obsolete variables and tests.   *
.*                  Alter to 8 admission variables of length 6.               *
.*                  Replace old isbuild and execute logic with current        *
.*                  standard.                                                 *
.*        V5.08.05  29/11/2000  Dean Cramer                                   *
.*                  Alter so subtotaling.                                     *
.*                  Also make cosmetic changes to the report layout as        *
.*                  directed,                                                 *
.*        V5.08.04  25.10.2000 J.Tan                                          *
.*                  Recompiled for WEB Report Scheduler                       *
.*        V5.08.03  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.02  27/06/2000  Greg Horvat                                   *
.*                  Recompiled for PATICOKY - Validating ICD10 version 2 codes*
.*        V5.08.B04 05/04/2000  J Habasque                                    *
.*                  Recompiled for PATICOKY                                   *
.*        V5.08.B03 15/03/2000  Greg Horvat                                   *
.*                  Changed to get the coding from the episode coding files   *
.*        V5.08.B02 06/03/2000  Jill Habasque                                 *
.*                  Recompiled for STDKWSDF & STDKWSCD                        *
.*        V5.07.03  30/09/1999  Jill Habasque SRF 634728                      *
.*                  Changed operation heading to be a dim 5 (not dim 2)       *
.*        V5.07.02  27/07/1999  Caleb Sharman  SRF 632440                     *
.*                  Mods to validate op. codes and use ICD9 or ICD10          *
.*        V5.07.01  19/02/1999  Davin  SRF 629934                             *
.*                  Don't validate op. codes on keyin (=> allow ICD9 or ICD10)*
.*        V5.07.B02 20/01/1999  Nicole Harrington  507 rebound 121            *
.*                  Mods to print century on date                             *
.*        V5.07.B01 12/11/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.******************************************************************************
.*        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
.*                  Changed temp file name "pstidx" to standard format        *
.******************************************************************************
.*        V5.00     20/03/89 J.Tan                                            *
.*                  Fixed PATIOMDI                                            *
.*        V5.01     16/05/89 DIG                                              *
.*                  Changed to standard and                                   *
.*                  modified print for 8 digit U/R's etc                      *
.*        V5.02     27/05/89 J.Tan                                            *
.*                  Fixed Mother Adm. to form8                                *
.*                  (PATMISFD).     SRF 100750                                *
.*        V5.03     06/06/89 J.Tan                                            *
.*                  Mods Locking master file                                  *
.*                  (PATIOMAS)     SRF 100809                                 *
.*        V5.01.01  06/07/89 K.Peak                                           *
.*                  Changed Adm & U/R 8 chrs                                  *
.*        V5.01.02  24/11/89 K.Peak                                           *
.*                  Fixed spelling of separate     SRF 103155                 *
.*        V5.01.03  20/02/90 J.Tan  SRF 103139                                *
.*                  Fixed the description of not existing disease codes       *
.*        V5.01.04  01/03/90 J.Tan  SRF 104092                                *
.*                  Fixed sub-operation code                                  *
.*        V5.01.121 27/11/90 Glen Hobby                                       *
.*                  Recompiled for changes to PATMSXFD                        *
.*        V5.01.122 20/05/91 Steve O'Gorman                                   *
.*                  Fixed Errors from New Comp.                               *
.*        V5.01.123 28/01/93 ROD                                              *
.*                  Added patmrdaf Cons Episodesi                             *
.*        V5.01.124 04.03.93 Neeriem "I Hate Support" Dye                     *
.*                  Modify to use standard routine KEYDATE                    *
.*        V5.01.125 16.03.93 Neeriem "I Hate Support" Dye                     *
.*                  Made BIL76,77,78 all handle dates the same                *
.*        V5.01.126 07/05/93  ROD                                             *
.*                  Replace PTCNACPS with PTCNUCEP                            * 
.*        V5.01.127 21/06/93  WHIRLPOOL   SRF 123343                          *
.*                  Fixed KEY on read for PATMRDFD                            * 
.*                  Made program use PATMRO file instead                      * 
.*                  of PATMOP for uk - you spastic chuck                      * 
.*        V5.01.128 18/02/94  I.Rutt                                          *
.*                  Standards upgrade                                         *
.*        V5.01.129 07/04/94  Ian Rutt                                        *
.*                  Added new keyins PATCODKY, PATICOKY                       *
.******************************************************************************
.
          INC       STD001FD
          INC       IBACONFD/INC
          INC       PATCODFD/INC

          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATECDFD/INC
          INC       PATECOFD/INC
          INC       PATHSPFD/INC
          INC       PATICGFD/INC
          INC       PATICOFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATOICFD/INC
          INC       PATTRNFD/INC
          INC       PMSVX1FD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
          INC       PMSEDGFD/INC
          INC       PATFN1FD/INC
          INC       PATPGRFD/INC
          INC       PATFX1FD/INC
          INC       PATCFAFD/INC
.
.        1st Temporary index file for report
.
GNDFILE  IFILE          SQL, FIXED=9, KEY="u1-8"
.
.Name    Type      Size    Physical    Start
.----    ----      ----    --------    -----
.KEY8     DIM        8         8          1        Admission Number
.                             EOR        9
.
.        2nd Temporary index file for report
.
SORTSTR   DIM       20
SORT1     INIT      "uc1-29"                 * admission sequence  (key29)
SORT2     INIT      "uc1-13,101-120,22-29"   * doctor sequence     (key41)
SORT3     INIT      "uc1-13,93-100,22-29"    * disch.date sequence (key29)
SORT4     INIT      "uc1-13,52-59,22-29"     * u/r number sequence (key29)
TEMPFILE IFILE     SQL, FIXED=121, KEY=SORTSTR
.
.Name    Type      Size    Physical    Start
.----    ----      ----    --------    -----
.PT0OBLKN DIM        4         4          1
.PTEOCODE DIM        9         9          5
.DADMNOX  DIM        8         8         14
UNIQUEX   DIM        8         8         22
PNAME12   DIM        12        12        30
.ACLSSFT  DIM        3         3         42      * unit
.DDEST    DIM        3         3         45      * discharge destination
.PTDSDDRG DIM        4         4         48      * drg code
.PURNO    DIM        8         8         52
.PSEX     DIM        1         1         60
.PSAGE    FORM       3         3         61
DNAME18   DIM        18        18        64
.NBRDAYS  FORM       3         3         82
FROMDATE  DIM        8         8         85
TODATE    DIM        8         8         93
.DSNAME   DIM        20        20       101
.                              EOR      121
.
.        3rd Temporary index file for report
.
ADMFILE  IFILE          SQL, FIXED=9, KEY="u1-8"
.
.Name    Type      Size    Physical    Start
.----    ----      ----    --------    -----
.KEY8     DIM        8         8          1        Admission Number
.                             EOR        9
+
.               WORK AREA
.
BLKSTART  DIM       4
BLKENDS   DIM       4
.
CALDAYS   FORM      3
CBOT      FORM      2
CHECKSUS  FORM      1
CLEF      FORM      3
CMDLINE   DIM       50
CODE      DIM       3
COUNT     FORM      8
CRIG      FORM      3
CSCRSIZ   FORM      1
CSEARCH   FORM      1
CSECFEE   FORM      1 
CTOP      FORM      2
CURROPR   DIM       9
CURRBLK   DIM       4
CURRADM   DIM       8
.
DDRGCODE  DIM       4
DMDCCODE  DIM       4
DRGVER    DIM       3
.
PTCNDGED  DIM       1                 * Enable DRG Effective Dates Functionality
.                                       0 or blank = No
.                                       1 = Yes
.
DADMNOX   DIM       8
DASH132   DIM       132
DASH126   DIM       126
DIM1A     DIM       1
DIM1B     DIM       1
DIM1C     DIM       1
DIM3      DIM       4
DIM4      DIM       4
DIM4A     DIM       4
DIM5      DIM       5
DIM70     DIM       70
DIM70A    DIM       70
DIM70B    DIM       70
DIM9      DIM       9
DIM9A     DIM       9
DITEM     FORM      2
DISPVAR   DIM       127
DNAME18A  DIM       18
DOAGAIN   FORM      1
DOCCODE   DIM       6
DOCNAME   DIM       22
DROW      FORM      2
.
ENDATE    DIM       8
ENDFLG    FORM      "0"
.
FIELD     FORM      2
FIRST     DIM       1
..FIVE5     FORM      "55"
FLAGCOL   FORM      3
FLAGDESC  DIM       127
FLAGROW   FORM      3
FRSTLINE  DIM       1
FNAMEA    DIM       8
FNAMET    DIM       8
FNAMER    DIM       8
FNAMEG    DIM       8
FORM5     FORM      5
FSTTIM    DIM       1
.
GNDDAY    FORM      8
GNDLOS    FORM      3.2
GNDPAT    FORM      6
GNDPRI    FORM      6
.
FIRSTHED  FORM      1   
HDFLG     FORM      1
.
ICDFLAG   FORM      1                *DETERMINES WHETHER TO USE ICD9 OR ICD10
.
JDAYS     FORM      3
JYEAR     FORM      2
.
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
KEYWRDXX  DIM       15
KEYWRDNO  FORM      2
. 
LNO       FORM      3
LYEAR     FORM      "366"
LASTBLK   DIM       4
.
MAXITEM   FORM      2
MINITEM   FORM      2
MODE      DIM       1
.
NBRDAYS   FORM      4          /*  NHOSPDAY  VARIABLES  */
RTDAYS    FORM      4          /*  NHOSPDAY  VARIABLES  */
SKEY30    DIM       30         /*  NHOSPDAY  VARIABLES  */
NEWDITEM  FORM      2          /*  IC9KYWSR             */
NODIS     DIM       1
NOOPR     DIM       1
.
OPRSTART  DIM       9
OPRENDS   DIM       9
OYEAR     FORM      "365"
ONEWPAGE  FORM      1
.
PADATE    DIM       10
PAGENO    FORM      3
PDDATE    DIM       10
PFDATE    DIM       10
PDOCONLY  DIM       1
PCONONLY  FORM      1
PREVADM   DIM       8
PREVOPR   DIM       9
PREVBLK   DIM       4
PTCNI10D  DIM       8 
PTCNGDV2  DIM       8 
PTCNGDV3  DIM       8 
PTCNGDV4  DIM       8                       * Ed4 Golive date          *I-49037
PTCNGDV5  DIM       8                       * Ed5 golive date         *I-105244
PTCNGDV6  DIM       8                       * Ed.6 Go-live date       *I-163918
PTCNGDV7  DIM       8                       * Ed.7 Go-live date       *I-219246
PTCNGDV8  DIM       8                       * Ed.8 Go-live date       *I-284420
PTCNGDV9  DIM       8                       * Ed.9 Go-live date       *I-311669
PTCNGDVX  DIM       8                       * Ed.10 Go-live date      *I-0833093
PTCNGDX1  DIM       8                       * Go live date for ICD10 Ed.11
PTCNGDX2  DIM       8                       * Go live date for ICD10 Ed.12
PTCNGDX3  DIM       8                       * Go live date for ICD10 Ed.13
PTDATE    DIM       10
.
.*******************************************************************************
.*        PATCONFD.INC : Sector 119                                            *
.*******************************************************************************
.          Grouper Effective Dates
PTCNED41  DIM       8        183      Effective Date for DRG 4.1      *C-284669
PTCNED42  DIM       8        191      Effective Date for DRG 4.2      *C-284669
PTCNED50  DIM       8        199      Effective Date for DRG 5.0      *C-284669
PTCNED51  DIM       8        207      Effective Date for DRG 5.1      *C-284669
PTCNED52  DIM       8        215      Effective Date for DRG 5.2      *C-284669
PTCNED60  DIM       8        223      Effective Date for DRG 6.0      *C-284669
PTCNED62  DIM       8        231      Effective Date for DRG 6.2      *C-284669
PTCNED70  DIM       8        239      Effective Date for DRG 7.0      *C-284669
PTCNED80  DIM       8        232      Effective Date for DRG 8.0
PTCNED90  DIM       8        232      Effective Date for DRG 9.0
PTCNE100  DIM       8        224      Effective Date for DRG 10.0
PTCNE110  DIM       8        239      Effective Date for DRG 11.0
PTCNE120  DIM       8         94      Effective Date for DRG 12.0
 .
PTCNDGPV  DIM       3        158      Default Grouper Version (031/041/042)
.
CFPAYCOD  DIM       3        * 3M Codefinder PAY Code
.
RANGFLG   DIM       1
.
SAVKEY24  DIM       24
SAVKEY9   DIM       9
SBOT      FORM      2
SCRNFLAG  FORM      1
SEARCHTY  FORM      1
SELITMNO  FORM      2
SELUNIT   DIM       3
PTSUBHED  FORM      1  
SHOWSUSP  FORM      1
SLEF      FORM      3
SRIG      FORM      3
STARTD    DIM       8
DRTYPCOD  DIM       3
STATUS    FORM      1
STDCOL00  FORM      3
STDCOL01  FORM      3
STDCOL02  FORM      3
STDCOL03  FORM      3
STDOCT    DIM       6
STDROW    FORM      2
STOP      FORM      2
.
ST1       DIM       50       * save keys for "?" options
ST2       DIM       50
ST3       DIM       50
ST4       DIM       50
ST5       DIM       50
ST6       DIM       50
ST7       DIM       50
ST8       DIM       50
ST9       DIM       50
ST10      DIM       50
ST11      DIM       50
ST12      DIM       50
ST13      DIM       50
ST14      DIM       50
ST15      DIM       50
ST16      DIM       50
ST17      DIM       50
.
SUBDAY    FORM      8
SUBLOS    FORM      3.2
SUBOPR    FORM      5
SUBPAT    FORM      5
SUBPRI    FORM      5
SUMMONLY  FORM      1
.
SUODAY    FORM      8
SUOLOS    FORM      3.2
SUOOPR    FORM      5
SUOPAT    FORM      5
SUOPRI    FORM      5
SUBLINE1  DIM       132
SUBLINE2  DIM       132
SDASHS    DIM       132
.
TMPCAL    FORM      8.2
TYPDESC   DIM       25
.
UNIQUE    FORM      8
.
VERT      FORM      2
.
WDATE1    DIM       8
WDATE2    DIM       8
.
BILDRGFL  DIM       1
.
XDIM4     DIM       4
XDIM6     DIM       6
XDIM7     DIM       7
XDIM9     DIM       9
.
YDIM6     DIM       6
YDIM7     DIM       7
YDIM9     DIM       9
.
.      EXTRA VARIABLES
.
.      WORK AREA
.
ALLREC    INIT      "ALL      "
CODEAC    INIT      "AC"
CODEDT    INIT      "DT"
FSTREC    INIT      "Start    "
GINVALID  INIT      "Block number not on group file (paticdgf)"
OINVALID  INIT      "Operation code not on group file (paticdgf)"
LSTREC    INIT      "Finish   "
SP22      INIT      "                      "
.
.                    ....*....1....*....2....*....3....*....4....*....5
SUBH101   INIT      "                                                  "
SUBH102   INIT      "                                        Dis.      " 
SUBH103   INIT      "Bed                       Gross                   "
SUBH201   INIT      "Patient Name      U/R   Adm No  Sex Age  Operation"
SUBH202   INIT      "s  Diseases     Attending Doctor   Unit Dest DRG  "
SUBH203A  INIT      "Days Admission  Discharge   Fee "
SUBH203B  INIT      "Days Admission  Discharge       "
DASH50    INIT      "--------------------------------------------------"
DASH30    INIT      "--------------------------   "
.
PRGNAM    INIT      "Cross Index Of Operations"
PRGID     INIT      "IBABIL77"
VERSION   INIT      "V12.01.00"
+
. ****************************************************************************
.         START OF PROGRAM
. ****************************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      MENU0000
          BRANCH    EXIT,ML9999
.
          CALL      PROC0000    
          CALL      PRNT0000

          CALL      ENDIT000
.
          CALL      KILLIDX
.
          GOTO      ML1000
.
ML9999    CHAIN     PGM
          STOP
.
. ****************************************************************************
.                                INIT0000
.                               OPEN FILES
. ****************************************************************************
INIT0000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,EIGHTY8;*59,PTCNI10D
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"paticdgf";
          OPEN      PATICDG1,"paticdgf"
.
          DISPLAY   *P64:24,"paticdof";
          CALL      OPICO1
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
INIT2000  DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN;*125,CSECFEE
.
          DISPLAY   *P64:24,"patecdaf";
          OPEN      PATECDA2,"patecdaf"
.
          DISPLAY   *P64:24,"patecoaf";
          OPEN      PATECOA2,"patecoaf"
.
INIT3000  CALL      TFILENAM
          MOVE      TFILNAME,FNAMET
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEG
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEA
.
          MOVE      SP9,OPICDDTE
INIT9999  RETURN
+
. ****************************************************************************
.             SELECT OPTION
. ****************************************************************************
MENU0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"1",*HOFF,". Starting Discharge Date       :":
                    *P1:5,*V2LON,"2",*HOFF,". Ending   Discharge Date       :":
                    *P1:7,*V2LON,"3",*HOFF,". Operation or Block Range (O/B):":
                    *P1:8,*V2LON,"4",*HOFF,". From                          :":
                    *P1:9,*V2LON,"5",*HOFF,". To                            :":
                    *P1:11,*V2LON,"6",*HOFF,". Keyin Doctor Type/Doctor Code :":
                    *P55:11,"   <RETURN> for All ":
                    *P1:13,*V2LON,"7",*HOFF,". Primary Doctor Type Only (Y/N):":
                    *P1:14,*V2LON,"8",*HOFF,". Keyin Unit                    :":
                    *P1:15,*V2LON,"9",*HOFF,". Summary Details Only     (Y/N):":
                    *P1:16,*V2LON,"10",*HOFF,".New page for each Oper.  (Y/N):":
                    *P1:17,*V2LON,"11",*HOFF:
                                            ".Print Principal Condition Only:"
.
          IF        IBCNMHOS=1
            DISPLAY   *P1:18,*V2LON,"12",*HOFF,".Hospital Id":
                      *P34:18,":"
          ENDIF
.
MENU1000  MOVE      ZERO,PAGENO
          MOVE      ZERO,ENDFLG
          MOVE      ZERO,PTSUBHED
          MOVE      "A",MODE
          MOVE      "60",LNO
          CALL      ZEROS
          CALL      ZEROS1 
          MOVE      ZERO,GNDDAY
          MOVE      ZERO,GNDLOS
          MOVE      ZERO,GNDPAT
          MOVE      ZERO,GNDPRI 
          CALL      CLRA0000
.
MENU2000  CALL      KSDAT000                * keyin Start Date
          BRANCH    EXIT,MENU9999
          CALL      KEDAT000                * keyin End Date
          CALL      KYRNG000                * Opr/Block Type for Range
          MATCH     "B",RANGFLG
          IF        @EQUAL
            CALL      KYBLK000              * Block Range
          ENDIF 
          MATCH     "O",RANGFLG
          IF        @EQUAL
            CALL      KYOPR000              * Operation Range
          ENDIF 
          CALL      KYDOC000                * Doctor Code/Type
          CALL      KYDTYP00                * Primary Doc Type only
          CALL      KUNIT000                * Unit
          CALL      KSUMM000                * Summary Details only
          CALL      KSPAGE00                * Seperate pages
          CALL      KYPPC000                * Print Principal Condition Only
          CALL      KSHOSP00                * Hospital Id
.
.                       SELECT THE FIELD TO UPDATE
.
MENU3000  CALL      MAINQST
          MOVE      ZERO,EXIT
.
          COMPARE   ZERO,CCITEM
          GOTO      MENU5000 IF EQUAL
          GOTO      MENU7000 IF LESS
          MOVE      "C",MODE
.
          PERFORM   CCITEM,KSDAT000,KEDAT000,KYRNG000,KYRBO000,KYRBO000:
                           KYDOC000,KYDTYP00,KUNIT000,KSUMM000,KSPAGE00:
                           KYPPC000,KSHOSP00
.
          BEEP
          GOTO      MENU3000

.
MENU5000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Admission Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Attending Doctor Sequence":
                    *P1:7,*V2LON,THREE,*HOFF," = Discharge Date Sequence":
                    *P1:8,*V2LON,FOUR,*HOFF," = U/R Sequence"
.
          KEYIN     *P1:10,"Option : ",*EL,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      MENU9000 IF EQUAL
.
          IF        OPTION > 0 & OPTION < 5
            GOTO      MENU5500
          ENDIF
.
          BEEP
          GOTO      MENU5000
.
MENU5500  MOVE      SP1,BILDRGFL
          DISPLAY   *P1:23,"Display Billed DRG in DRG Column? (Y/N): ";
          KEYIN     *P42:23,*EL,*V2LON,BILDRGFL;
          REP       UPPLOW,BILDRGFL
          DISPLAY   *P42:23,*EL,*V2LON,BILDRGFL
.
          MATCH     "Y",BILDRGFL
          GOTO      MENU6000 IF EQUAL
          MATCH     "N",BILDRGFL
          GOTO      MENU6000 IF EQUAL
          GOTO      MENU5500 
.
.
MENU6000  MOVE      ZERO,EXIT
          KEYIN     *P1:24,*EL,"Ok to continue (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF:
                    "/",*V2LON,"C",*HOFF:
                    ") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
.
          CMATCH    "N",ANS
          GOTO      MENU5000 IF EQUAL
          CMATCH    "C",ANS
          GOTO      MENU9000 IF EQUAL
          CMATCH    "Y",ANS
          GOTO      MENU9999 IF EQUAL
          BEEP
          GOTO      MENU6000
.
.                        Clear Screen 
. 
MENU7000  DISPLAY   *P36:4,*EL,*P36:5,*EL,*P36:7,*EL,*P36:8,*EL:
                    *P36:10,*EL,*P36:11,*EL,*P36:12,*EF
          GOTO      MENU0000
.
MENU9000  MOVE      ONE,EXIT
.
MENU9999  RETURN
.
. *****************************************************************************
.                           * enter Start Date
. *****************************************************************************
KSDAT000  MOVE      ZERO,EXIT 
          MOVE      FOUR,CVERT
          MOVE      "36",CCOL
.
KSDAT100  UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    CQUEST,KSDAT100
          BRANCH    OVRCD,KSDAT900
.
KSDAT200  PACK      STARTD,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STARTD
          CALL      PACDATE
          MOVE      CPCDATE,PFDATE 
          REP       " 0",PFDATE
          RESET     STARTD
.
          MATCH     "C",MODE
          GOTO      KSDAT999 IF NOT EQUAL
.
          MATCH     STARTD,ENDATE
          GOTO      KSDAT999 IF NOT LESS
          DISPLAY   *P1:24,*B,*EL,"Ending Date Can't be Less than":
                    " Starting Date. ";
          CALL      HITENTER
          GOTO      KSDAT000
.
KSDAT900  MOVE      ONE,EXIT
.
KSDAT999  RETURN
.
. *****************************************************************************
.                           * enter End Date
. *****************************************************************************
KEDAT000  MOVE      FIVE,CVERT
          MOVE      CDD,CDAY
          MOVE      CMM,CMON
          MOVE      CYY,CYEAR
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    CQUEST,KEDAT000
          BRANCH    OVRCD,KEDAT900
.
          PACK      ENDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDATE
          CALL      PACDATE
          MOVE      CPCDATE,PTDATE
          REP       " 0",PTDATE 
.
          MATCH     STARTD,ENDATE
          GOTO      KEDAT600 IF NOT LESS
          DISPLAY   *P1:24,*B,*EL,"Ending Date Can't be Less than":
                    " Starting Date. ";
          CALL      HITENTER
          GOTO      KEDAT000
.
KEDAT600  MOVE      ZERO,ICDFLAG
.
.....     MATCH     PTCNI10D,STARTD        *IS START DATE AFTER GO-LIVE DATE ?
.....     IF        @LESS
.....       MATCH     PTCNI10D,ENDATE      *IS END DATE AFTER GO-LIVE DATE ?
.....       IF        @LESS
.....       ELSE
.....          DISPLAY   *P1:23,*B,*EF,"Both the start & end dates ":
.....                    "need to be either above or below the go-live date"
.....         CALL      HITENTER
.....         DISPLAY   *P36:5,*EL,*P1:23,*EF
.....         GOTO      KEDAT000
.....       ENDIF
.....     ELSE
.....       GOTO      KEDAT999              *DONT SET FLAG TO USE ICD9 CODES
.....     ENDIF
.....     MOVE      ONE,ICDFLAG             *SET FLAG TO USE ICD9 CODES
.....
.....     MATCH     STARTD,ENDATE
.....     GOTO      KEDAT999 IF NOT LESS
.....     DISPLAY   *P1:24,*B,*EL,"Ending Date Can't be Less Than":
.....                                    " Starting Date. ";
.....     CALL      HITENTER
.....     GOTO      KEDAT000
.
          GOTO      KEDAT999
.
KEDAT900  MOVE      ONE,EXIT
.
KEDAT999  RETURN
.
. *****************************************************************************
.              enter B for range by Block or O for range by Operation
. *****************************************************************************
KYRNG000  MOVE      SP1,RANGFLG
          DISPLAY   *P37:7,*V2LON,RANGFLG;
          KEYIN     *P37:7,*EL,*V2LON,RANGFLG;
          RESET     RANGFLG
          REP       UPPLOW,RANGFLG
          DISPLAY   *P37:7,*V2LON,RANGFLG;
          MATCH     "B",RANGFLG
          IF        @EQUAL
            DISPLAY  *P1:8,*V2LON,"4",*HOFF,". From Block Code               :":
                     *P1:9,*V2LON,"5",*HOFF,". To   Block Code               :";
            GOTO      KYRNG999
          ENDIF
          MATCH     "O",RANGFLG
          IF        @EQUAL
            DISPLAY  *P1:8,*V2LON,"4",*HOFF,". From Operation Code           :":
                     *P1:9,*V2LON,"5",*HOFF,". To   Operation Code           :";
            GOTO      KYRNG999
          ENDIF
          BEEP
          GOTO      KYRNG000
.
KYRNG999  RETURN
.
.*****************************************************************************
.                    logic for Items 4 and 5
.*****************************************************************************
.
KYRBO000  MATCH     "B",RANGFLG
          IF        @EQUAL
            DISPLAY  *P1:8,*V2LON,"4",*HOFF,". From Block Code               :":
                     *P1:9,*V2LON,"5",*HOFF,". To   Block Code               :";
            CALL      KYBLK000
            GOTO      KYRBO999
          ENDIF
.
          MATCH     "O",RANGFLG
          IF        @EQUAL
            DISPLAY  *P1:8,*V2LON,"4",*HOFF,". From Operation Code           :":
                     *P1:9,*V2LON,"5",*HOFF,". To   Operation Code           :";
            CALL      KYOPR000
            GOTO      KYRBO999
          ENDIF
.
KYRBO999  RETURN
.
. ****************************************************************************
.                    * enter block code range
. ****************************************************************************
KYBLK000  DISPLAY   *P1:8,*V2LON,"4",*HOFF,". From Block Code               :":
                    *P1:9,*V2LON,"5",*HOFF,". To   Block Code               :";
          KEYIN     *P36:8,*EL,*V2LON,*JR,BLKSTART; 
          RESET     BLKSTART
          GOTO      KYBLK200 IF NOT EOS
.
          MOVE      SP4,BLKSTART
          MOVE      FSTREC,XDIM7    
          DISPLAY   *P36:8,*V2LON,FSTREC;
.
          MATCH     "C",MODE
          GOTO      KYBLK999 IF EQUAL
          GOTO      KYBLK300
.
KYBLK200  PACK      XDIM7,BLKSTART,SP10    
.
          MATCH     "C",MODE
          GOTO      KYBLK300 IF NOT EQUAL
          MATCH     SP4,BLKENDS
          GOTO      KYBLK999 IF EQUAL
          MATCH     BLKSTART,BLKENDS
          GOTO      KYBLK999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** FROM Code must be less than or equal to TO Code **   ";
          CALL      HITENTER
          GOTO      KYBLK000
.
.
KYBLK300  KEYIN     *P36:9,*EL,*V2LON,*JR,BLKENDS;   
          RESET     BLKENDS
          GOTO      KYBLK400 IF NOT EOS
.
          MOVE      SP4,BLKENDS
          MOVE      LSTREC,YDIM7        
          DISPLAY   *P36:9,*V2LON,LSTREC;
          GOTO      KYBLK999
.
KYBLK400  PACK      YDIM7,BLKENDS,SP10    
.
          MATCH     BLKSTART,BLKENDS
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** TO Code must be greater than or equal FROM Code **   ";
            CALL      HITENTER
            GOTO      KYBLK300
          ENDIF
.
KYBLK999  RETURN
.
. *****************************************************************************
.                         * enter operation code range
. *****************************************************************************
KYOPR000  DISPLAY   *P1:8,*V2LON,"4",*HOFF,". From Operation Code           :":
                    *P1:9,*V2LON,"5",*HOFF,". To   Operation Code           :";
          CALL      STRTCODE
          BRANCH    EXIT,KYOPR200,KYOPR000
          MOVE      OPCODE,OPRSTART
          RESET     OPRSTART
          GOTO      KYOPR300 IF NOT EOS
.
KYOPR200  MOVE      SP9,OPRSTART
          DISPLAY   *P36:8,*V2LON,FSTREC
          MATCH     "C",MODE
          GOTO      KYOPR999 IF EQUAL
          GOTO      KYOPR400
.
KYOPR300  MATCH     "C",MODE
          GOTO      KYOPR400 IF NOT EQUAL
          MATCH     SP9,OPRENDS
          GOTO      KYOPR999 IF EQUAL
          MATCH     OPRSTART,OPRENDS
          GOTO      KYOPR999 IF NOT LESS
.
          DISPLAY   *P1:24,*B,*EL,"FROM Code Can't be Greater than ":
                    "TO Code. ";
          CALL      HITENTER
          GOTO      KYOPR000
.
KYOPR400  CALL      FINICODE                
          BRANCH    EXIT,KYOPR500,MENU9999
          MOVE      OPCODE,OPRENDS
          RESET     OPRENDS
          GOTO      KYOPR600 IF NOT EOS
.
KYOPR500  MOVE      SP9,OPRENDS
          DISPLAY   *P36:9,*EL,*V2LON,LSTREC
          MATCH     "C",MODE
          GOTO      KYOPR999 IF EQUAL
          GOTO      KYOPR999
.
KYOPR600  MATCH     OPRSTART,OPRENDS
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"FROM Code Can't be Greater than ":
                      "TO Code. ";
            CALL      HITENTER
            GOTO      KYOPR400
          ENDIF
.
KYOPR999  RETURN
.         
. *****************************************************************************
.         keyin Primary Doctor Type Only or ALL types selection
. *****************************************************************************
KYDTYP00  KEYIN     *P36:13,*EL,*V2LON,PDOCONLY
          RESET     PDOCONLY
          IF        @EOS
            MOVE      "N",PDOCONLY
          ENDIF
.
KYDTYP20  REP       UPPLOW,PDOCONLY
          DISPLAY   *P36:13,*EL,*V2LON,PDOCONLY 
          MATCH     "Y",PDOCONLY
          GOTO      KYDTYP30 IF EQUAL
.
          MATCH     "N",PDOCONLY
          GOTO      KYDTYP40 IF EQUAL
          BEEP
          GOTO      KYDTYP00
.
KYDTYP30  MOVE      "PRIMARY DOCTOR TYPE ONLY",TYPDESC 
          GOTO      KYDTYP99
.
KYDTYP40  MOVE      "ALL DOCTOR TYPES",TYPDESC
.
.
KYDTYP99  RETURN
.
. *****************************************************************************
.                                 keyin Unit
. *****************************************************************************
KUNIT000  DISPLAY   *P36:14,*EL;
          MOVE      "36",ECOL
          MOVE      "14",EVERT
          MOVE      CODEAC,CODE
          CALL      PATCODKY
          BRANCH    EXIT,KUNIT100,KUNIT100
.
          MOVE      ACODE,SELUNIT
          DISPLAY   *P42:14,*V2LON,TDESC
          GOTO      KUNIT999
.
KUNIT100  MOVE      SP3,SELUNIT
          DISPLAY   *P36:14,*V2LON,ALLREC
.
KUNIT900  MOVE      ONE,EXIT
KUNIT999  RETURN
.
. *****************************************************************************
.         keyin Summary Only indicator
. *****************************************************************************
KSUMM000  MOVE      ZERO,SUMMONLY
          KEYIN     *P36:15,*EL,*V2LON,DIM1A;
          IF        @EOS
            MOVE      "N",DIM1A
          ENDIF
          RESET     DIM1A
.
          REP       UPPLOW,DIM1A
          DISPLAY   *P36:15,*V2LON,DIM1A
.
          MATCH     "N",DIM1A
          GOTO      KSUMM999 IF EQUAL
          MATCH     "Y",DIM1A
          IF        @EQUAL
            MOVE      ONE,SUMMONLY
            GOTO      KSUMM999
          ENDIF
.
          BEEP
          GOTO      KSUMM000
.
KSUMM999  RETURN
.
. *****************************************************************************
.         Keyin for separate page option
. *****************************************************************************
KSPAGE00  MOVE      ZERO,ONEWPAGE
          KEYIN     *P36:16,*EL,*V2LON,*RV,DIM1B;
          RESET     DIM1B
          IF        @EOS
            MOVE      "N",DIM1B
          ENDIF
.
          REP       UPPLOW,DIM1B
          DISPLAY   *P36:16,*V2LON,*EL,DIM1B
.
          MATCH     "Y",DIM1B
          IF        @EQUAL
            MOVE      ONE,ONEWPAGE
            GOTO      KSPAGE99
          ENDIF
          MATCH     "N",DIM1B
          GOTO      KSPAGE99 IF EQUAL
          BEEP
          GOTO      KSPAGE00
.
KSPAGE99  RETURN
.
. *****************************************************************************
.                    Keyin Print Principal Condition Only             *I-216427
. *****************************************************************************
KYPPC000  MOVE      ZERO,PCONONLY
          KEYIN     *P36:17,*V2LON,*RV,DIM1C;
          IF        @EOS
            MOVE      "N",DIM1C
          ENDIF
          RESET     DIM1C
          REP       UPPLOW,DIM1C
          DISPLAY   *P36:17,*V2LON,DIM1C;
.
          MATCH     "Y",DIM1C
          IF        @EQUAL
            MOVE      ONE,PCONONLY
            GOTO      KYPPC999
          ENDIF
          MATCH     "N",DIM1C
          GOTO      KYPPC999 IF EQUAL         * No
          BEEP
          GOTO      KYPPC000
.
KYPPC999  RETURN
.
. *****************************************************************************
.                        Keyin for Hospital id 
. *****************************************************************************
KSHOSP00  COMPARE   ONE,IBCNMHOS
          GOTO      KSHOSP99 IF NOT EQUAL
.
          MOVE      "36",ECOL
          MOVE      "18",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
.
          CALL      PATHSPKY
          BRANCH    EXIT,KSHOSP20
          DISPLAY   *P42:17,*V2LON,PTHSNAME;
          GOTO      KSHOSP99
.
KSHOSP20  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          DISPLAY   *P36:18,*V2LON,"ALL";
.
KSHOSP99  RETURN
.
. **************************************************************************
.                             Process
. **************************************************************************
.                           Admn    Att.Doc  Disc.Dt  UR.No.
PROC0000  BRANCH    OPTION,PROC0010,PROC0020,PROC0030,PROC0040
.
PROC0010  DISPLAY   *P52:2,*V2LON,"- Admission Sequence "
          MOVE      SORT1,SORTSTR
          GOTO      PROC0500
.
PROC0020  DISPLAY   *P52:2,*V2LON,"- Attending Doctor Sequence "
          MOVE      SORT2,SORTSTR
          GOTO      PROC0500
.
PROC0030  DISPLAY   *P52:2,*V2LON,"- Discharge Date Sequence "
          MOVE      SORT3,SORTSTR
          GOTO      PROC0500
.
PROC0040  DISPLAY   *P52:2,*V2LON,"- U/R Sequence "
          MOVE      SORT4,SORTSTR
          GOTO      PROC0500
.
.
PROC0500  CALL      KILLIDX                 * create Temp file
          CALL      CREAIDX
.
          OPEN      TEMPFILE,FNAMET
          MOVE      ONE TO UNIQUE
.
          OPEN      GNDFILE,FNAMEG
.
          OPEN      ADMFILE,FNAMEA
.
.                                           * main process loop
          DISPLAY   *P1:24,*EL;                
          PACK      KEY16 WITH STARTD,SP8
          CALL      RDSDSCH2
PROC1000  CALL      RDKDSCH2
          BRANCH    OVRCD OF PROC9999
.
          UNPACK    DDATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      XDATE WITH XCENT,XYEAR,XMON,XDAY
.
          MATCH     SP8,ENDATE
          GOTO      PROC1010 IF EQUAL
.
          MATCH     XDATE,ENDATE
          GOTO      PROC9999 IF LESS
.
PROC1010  COMPARE   ONE,IBCNMHOS            * check Multi-Hospital Processing
          IF        @EQUAL
            MATCH     SP10,PTHSHOSP
            IF        !@EQUAL
              MOVE      DADMNO,KEY8
              CALL      RDPMVX11
              BRANCH    OVRCD,PROC1000
.
              MATCH     PMVXMHOS,PTHSHOSP
              GOTO      PROC1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
PROC1020  DISPLAY   *P28:24,*V2LON,XDAY,SLASH,XMON,SLASH,XYEAR;
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF PROC1000
.
          MATCH     DADMNO,AADMNO
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     DURNO,AURNO
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     SP3,SELUNIT             * check Unit
          IF        !@EQUAL
            MATCH     SELUNIT,ACLSSFT
            GOTO      PROC1000 IF NOT EQUAL
          ENDIF
.
PROC1050  MATCH     SP3,DRTYPCOD              * check ??? 
          GOTO      PROC2000 IF EQUAL
.
          MATCH     SP6,STDOCT              * check for Doctor code
          IF        !@EQUAL
            MATCH     ADOCTA,STDOCT         * Same doctor ?
            GOTO      PROC1000 IF NOT EQUAL
          ENDIF
.
PROC1060  MOVE      ADOCTA,KEY6  
          CALL      RDDOCT1
          BRANCH    OVRCD OF PROC1000
.
          MATCH     DRTYPE,DRTYPCOD
          GOTO      PROC2000 IF EQUAL
.
          MATCH     "Y",PDOCONLY             * check primary doctor only
          GOTO      PROC1000 IF EQUAL
.
          MATCH     DRTYPCOD,DRTYPE2
          GOTO      PROC2000 IF EQUAL
.
          MATCH     DRTYPCOD,DRTYPE3
          GOTO      PROC2000 IF EQUAL
.
          MATCH     DRTYPCOD,DRTYPE4
          GOTO      PROC2000 IF EQUAL
.
          MATCH     DRTYPCOD,DRTYPE5
          GOTO      PROC2000 IF EQUAL
.
          GOTO      PROC1000
.
PROC2000  MOVE      DURNO,KEY8      
          CALL      RDMAST1
          MOVE      SP30,PNAME12
          MOVE      PGNAME,ANS
          PACK      PNAME12,ANS,DOT,PSNAME
.
          MOVE      SP30,DNAME18
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF PROC2010
          MOVE      DGNAME,ANS
          PACK      DNAME18,ANS,DOT,DSNAME
.
.         Check Operations Code range
.
PROC2010  UNPACK    ADATE INTO XCENT,XYEAR,XMON,XDAY   
          PACK      FROMDATE WITH XCENT,XYEAR,XMON,XDAY
          UNPACK    DDATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      TODATE WITH XCENT,XYEAR,XMON,XDAY
          CALL      RTIODAYS
          IF        NBRDAYS = 0
            MOVE      ONE,NBRDAYS        * sub-bed days 1,for same day case 
          ENDIF
.
          PACK      KEY16,DADMNO,SP20   
          CALL      RSPTECO2
.                                    
PROC2100  CALL      RKPTECO2
          BRANCH    OVRCD,PROC1000
.
          MATCH     DADMNO,PTEOADMN
          GOTO      PROC1000 IF NOT EQUAL
.
          MOVE      PTEOCODE,KEY9
          CALL      RDPTICO1
          IF        OVRCD = 1
            MOVE      SP4,PT0OBLKN
          ENDIF
.
          MATCH     "O",RANGFLG     
          GOTO      PROC2300 IF EQUAL
.
          MATCH     SP4,BLKSTART
          GOTO      PROC2200 IF EQUAL
.
          MATCH     SP70,PT0OBLKN
          IF        @EQUAL
           MATCH     BLKSTART,PT0OBLKN
           GOTO      PROC2100 IF LESS
           GOTO      PROC2200
          ENDIF
.
          MOVE      ZERO,FORM4A   
          TYPE      BLKSTART
          IF        @EQUAL
            MOVE      BLKSTART,DIM4
            RESET     DIM4
            MOVE      DIM4,FORM4
            MOVE      PT0OBLKN,DIM4A
            RESET     DIM4A
            STRIP     DIM4A      
            MOVE      DIM4A,FORM4A
            COMPARE   FORM4,FORM4A
          ELSE
            MATCH     BLKSTART,PT0OBLKN
          ENDIF
          GOTO      PROC2100 IF LESS
.
PROC2200  MATCH     SP4,BLKENDS 
          GOTO      PROC2400 IF EQUAL
.
          MATCH     SP70,PT0OBLKN
          IF        @EQUAL
            MATCH     BLKENDS,PT0OBLKN
            GOTO      PROC2250 
          ENDIF
.
          MOVE      ZERO,FORM4A    
          TYPE      BLKENDS
          IF        @EQUAL
            MOVE      BLKENDS,DIM4
            RESET     DIM4
            MOVE      DIM4,FORM4
            MOVE      PT0OBLKN,DIM4A
            RESET     DIM4A
            STRIP     DIM4A         
            MOVE      DIM4A,FORM4A
            COMPARE   FORM4,FORM4A
          ELSE
            MATCH     BLKENDS,PT0OBLKN
          ENDIF
PROC2250  GOTO      PROC2400 IF LESS
          GOTO      PROC2100 IF NOT EQUAL
.
          GOTO      PROC2400
.
.
PROC2300  MATCH     SP9,OPRSTART 
          IF        !@EQUAL
            MATCH     OPRSTART,PTEOCODE
            GOTO      PROC2100 IF LESS
          ENDIF
.
          MATCH     SP9,OPRENDS
          GOTO      PROC2400 IF EQUAL
          MATCH     OPRENDS,PTEOCODE
          GOTO      PROC2400 IF LESS
          GOTO      PROC2100 IF NOT EQUAL
.
.                                           * check principal condition only
PROC2400  IF        PCONONLY = 1
            COMPARE   ONE,PTEOUNQN
            GOTO      PROC2100 IF NOT EQUAL      * finished for patient
          ENDIF
.
          DISPLAY   *P61:24,*V2LON,PTEOCODE;
          MOVE      DADMNO,KEY8
          READ      GNDFILE,KEY8;KEY8
          GOTO      PROC2500 IF NOT OVER
.
          ADD       ONE,GNDPAT
          ADD       NBRDAYS,GNDDAY
          WRITE     GNDFILE,KEY8;KEY8
.
PROC2500  MOVE      UNIQUE,UNIQUEX   
          MOVE      DADMNO,DADMNOX
.
          MATCH     "Y",BILDRGFL
          GOTO      PROC2505 IF NOT EQUAL
.
. Billed DRG - This will override the PTDSDDRG with the billed DRG code
.
PROC2501  CALL      GETDRG
          MOVE      DDRGCODE,PTDSDDRG
.
.                           Admn    Att.Doc  Disc.Dt  UR.No.
PROC2505  BRANCH    OPTION,PROC2510,PROC2511,PROC2512,PROC2513
.
PROC2510  PACK      KEY29,PT0OBLKN,PTEOCODE,DADMNOX,UNIQUEX
          WRITE     TEMPFILE,KEY29;PT0OBLKN,PTEOCODE,DADMNOX,UNIQUEX,PNAME12:
                    ACLSSFT,DDEST,PTDSDDRG,PURNO,PSEX,PSAGE,DNAME18,NBRDAYS:
                    FROMDATE,TODATE,DSNAME
          GOTO      PROC2600
.
PROC2511  PACK      KEY41,PT0OBLKN,PTEOCODE,DSNAME,UNIQUEX
          WRITE     TEMPFILE,KEY41;PT0OBLKN,PTEOCODE,DADMNOX,UNIQUEX,PNAME12:
                    ACLSSFT,DDEST,PTDSDDRG,PURNO,PSEX,PSAGE,DNAME18,NBRDAYS:
                    FROMDATE,TODATE,DSNAME
          GOTO      PROC2600
.
PROC2512  PACK      KEY29,PT0OBLKN,PTEOCODE,TODATE,UNIQUEX
          WRITE     TEMPFILE,KEY29;PT0OBLKN,PTEOCODE,DADMNOX,UNIQUEX,PNAME12:
                    ACLSSFT,DDEST,PTDSDDRG,PURNO,PSEX,PSAGE,DNAME18,NBRDAYS:
                    FROMDATE,TODATE,DSNAME
          GOTO      PROC2600
.
PROC2513  PACK      KEY29,PT0OBLKN,PTEOCODE,PURNO,UNIQUEX
          WRITE     TEMPFILE,KEY29;PT0OBLKN,PTEOCODE,DADMNOX,UNIQUEX,PNAME12:
                    ACLSSFT,DDEST,PTDSDDRG,PURNO,PSEX,PSAGE,DNAME18,NBRDAYS:
                    FROMDATE,TODATE,DSNAME
          GOTO      PROC2600

PROC2600  ADD       ONE TO UNIQUE
          GOTO      PROC2100
.
.
PROC9999  RETURN
+
. **************************************************************************
.                                Print Report
. **************************************************************************
PRNT0000  MOVE      ONE,FIRSTHED
          MOVE      ZERO,PTSUBHED          
          MOVE      "69",LNO                * force Page Header
          MOVE      ZERO,COUNT
          MOVE      ZEROVISN,PREVADM
          UNPACK    SP20,PREVBLK,PREVOPR
.
PRNT1000  SUB       ONE,UNIQUE
          DISPLAY   *P1:23,*EL,"Total Records       : ",*V2LON,UNIQUE,*HOFF:
                    *P1:24,*EL,"Printing Record No. :",*P35:24,"Operation :":
                    *P60:24,"Admission :"
.
.
.                           Admn    Att.Doc  Disc.Dt  UR.No.
          BRANCH    OPTION,PRNT1001,PRNT1002,PRNT1003,PRNT1004
.                                           * select correct heading
PRNT1001  PACK      KEY29,SP70
          READ      TEMPFILE,KEY29;;
          GOTO      PRNT2000
.
PRNT1002  PACK      KEY41,SP70
          READ      TEMPFILE,KEY41;;
          GOTO      PRNT2000
.
PRNT1003  PACK      KEY29,SP70
          READ      TEMPFILE,KEY29;;
          GOTO      PRNT2000
.
PRNT1004  PACK      KEY29,SP70
          READ      TEMPFILE,KEY29;;
          GOTO      PRNT2000
.
.                                           * read tempfile record
PRNT2000  READKS    TEMPFILE;PT0OBLKN,PTEOCODE,DADMNOX,UNIQUEX,PNAME12:
                    ACLSSFT,DDEST,PTDSDDRG,PURNO,PSEX,PSAGE,DNAME18,NBRDAYS:
                    FROMDATE,TODATE,DSNAME
          GOTO      PRNT9999 IF OVER
.
          ADD       ONE,COUNT               * count records
.
          MOVE      UNIQUEX,UNIQUE
          MOVE      DADMNOX,DADMNO
          MOVE      DADMNOX,CURRADM
          MOVE      PTEOCODE,CURROPR        * save current Operation code
          MOVE      PT0OBLKN,CURRBLK        * save current Bloke code
.
          MATCH     DADMNO,PREVADM
          IF        @EQUAL
.                                           * same Admis & same OpCode? -bypass
            MATCH     PTEOCODE,PREVOPR
            GOTO      PRNT2000 IF EQUAL
          ENDIF
.
          DISPLAY   *P23:24,*V2LON,COUNT,*P47:24,PTEOCODE,*P72:24,DADMNO;
.
          IF        FIRSTHED = 1
            MOVE      "69",LNO              * force Page Header
            MOVE      ONE,PTSUBHED          * flag sub-heading is required
            GOTO      PRNT3000
          ENDIF
.
.                                           * check for change of Block/Oper
PRNT2500  MATCH     PREVOPR,PTEOCODE
          IF        !@EQUAL
            CALL      PTOPR000              * print previous Operation
            MOVE      ONE,PTSUBHED          * flag sub-heading is required
          ELSE
            MATCH     PREVBLK,PT0OBLKN  ***** odddball condition added later
            IF        !@EQUAL
              CALL      PTOPR000            * print previous Operation
              CALL      PTBLK000            * print previous Block
              MOVE      ONE,PTSUBHED          
            ENDIF
          ENDIF
.
          MATCH     PREVBLK,PT0OBLKN
          IF        !@EQUAL
            CALL      PTBLK000              * print previous Block
            MOVE      ONE,PTSUBHED          * flag sub-heading is required
          ENDIF
.
.
.                                           * process the current temp record
PRNT3000  CALL      SETOPR00                * get description of current Oper
          CALL      SETBLK00                * read this Block record
.
.       Make sure only the first operation code is counted for number of
.       patients and bed days
.                                           * position on required table
          PACK      KEY16,DADMNO,SP20  
          CALL      RSPTECO2
.                                           * read records from required table
PRNT4000  CALL      RKPTECO2
          BRANCH    OVRCD,PRNT4500
.
          MATCH     PTEOCODE,CURROPR  
          GOTO      PRNT4000 IF NOT EQUAL
.
          MOVE      DADMNO,KEY8
          READ      ADMFILE,KEY8;KEY8
          IF        @OVER = 1
            WRITE     ADMFILE,KEY8;KEY8
          ENDIF
.
.
PRNT4500  MOVE      CURROPR,PTEOCODE  * Restore operation code from temp record
.
.                                     * finish printing reqts. for Blocks
          COMPARE   "56",LNO
          CALL      HEDA0000 IF NOT LESS    * print Heading if required
.
          COMPARE   ONE,PTSUBHED
          CALL      HEDB0000 IF EQUAL       * print sub heading if required
.
.
PRNT8000  CALL      GETDET00                * process Oper & Disease details
.
PRNT9000  MOVE      CURROPR,PREVOPR         * move key fields to "previous"
          MOVE      CURRBLK,PREVBLK
          MOVE      CURRADM,PREVADM
          GOTO      PRNT2000                * return for next record
.
PRNT9999  RETURN
.
.******************************************************************************
.                       set Block sub-heading
.******************************************************************************
SETBLK00  PACK      DIM70A,SP30,SP30,SP10
          PACK      KEY5,ANSO,PT0OBLKN
          CALL      RDICG1
          BRANCH    OVRCD,SETBLK70
          MOVE      GDESC,DIM70A
          GOTO      SETBLK99
.
SETBLK70  MOVE      GINVALID,DIM70A
.
SETBLK99  RETURN
.
.******************************************************************************
.                    set Procedure sub-heading
.******************************************************************************
SETOPR00  PACK      DIM70B,SP30,SP30,SP10
          MOVE      PTEOCODE,KEY9
          CALL      RDPTICO1
          BRANCH    OVRCD,SETOPR70
          MOVE      ODESC,DIM70B
          GOTO      SETOPR99
.
SETOPR70  MOVE      OINVALID,DIM70B
.
SETOPR99  RETURN
.
. ****************************************************************************
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
. ****************************************************************************
ENDIT000  MOVE      ZERO,EXIT
          COMPARE   ZERO,PAGENO
          GOTO      ENDIT400 IF EQUAL
          CALL      PTOPR000 
          CALL      PTBLK000
.
          MOVE      ONE,ENDFLG
          COMPARE   "1",SUMMONLY
          GOTO      ENDIT100 IF NOT EQUAL
.
          COMPARE   "40",LNO
          CALL      HEDA0000 IF NOT LESS
          GOTO      ENDIT200
.
ENDIT100  CALL      HEDA0000
ENDIT200  CALL      PRTGND00                * print Grand total
          CALL      PRTNTE00                * 
.
          PRINT     *N,*N,"  ***  END OF REPORT  ***"
.
          DISPLAY   *P1:12,*EF,*P1:12
          GOTO      ENDIT999
.
.
ENDIT400  MOVE      SP1,KEY1
          DISPLAY   *P1:24,*EL,*B:
                   "No Record Selected. ";
          CALL      HITENTER 
.
.
ENDIT999  RETURN
.
. *****************************************************************************
.                         Get Operation & Disease Codes
. *****************************************************************************
GETDET00  MOVE      "Y",FRSTLINE
          ADD       ONE,SUBOPR
          ADD       ONE,SUOOPR
.
          PACK      KEY16,DADMNO,SP20 
          MOVE      SP9,PTEDCODE
          MOVE      SP1,PTEDTYPE
          MOVE      "N",NODIS
          MOVE      "N",NOOPR 
          CALL      RSPTECO2
          CALL      RSPTECD2
.
GETDET10  MATCH     "N",NOOPR
          GOTO      GETDET30 IF NOT EQUAL
.
          CALL      RKPTECO2
          BRANCH    OVRCD,GETDET20
.
          MATCH     DADMNO,PTEOADMN
          GOTO      GETDET20 IF NOT EQUAL
.
.                                            * check principal condition only
          IF        PCONONLY = 1
            COMPARE   ONE,PTEOUNQN
            GOTO      GETDET20 IF NOT EQUAL
          ENDIF
.
          MATCH     "N",NODIS
          GOTO      GETDET30 IF EQUAL
          CALL      PRTDET00                * print detail line
          GOTO      GETDET10
.
GETDET20  MOVE      "Y",NOOPR
          MOVE      SP9,PTEOCODE
.
GETDET30  MATCH     "N",NODIS
          GOTO      GETDET70 IF NOT EQUAL
.
GETDET40  CALL      RKPTECD2
          BRANCH    OVRCD,GETDET50
          MOVE      SP20,DNAME18A
.
          MATCH     DADMNO,PTEDADMN
          GOTO      GETDET50 IF NOT EQUAL
.
.                                           * check principal condition only
          IF        PCONONLY = 1
            MATCH     "P",PTEDTYPE
            GOTO      GETDET40 IF NOT EQUAL
          ENDIF
.
          CALL      PRTDET00                * print detail line
          MATCH     "N",NOOPR
          GOTO      GETDET10 IF EQUAL
          GOTO      GETDET40
.
.
GETDET50  MOVE      "Y",NODIS
          MOVE      SP9,PTEDCODE
          MOVE      SP1,PTEDTYPE
          MOVE      SP20,DNAME18A
.
GETDET70  MATCH     "N",NOOPR
          GOTO      GETDET80 IF NOT EQUAL
          CALL      PRTDET00                * print detail line
          GOTO      GETDET10
.
GETDET80  MATCH     "Y",FRSTLINE            * (in case it hasn't been printed)
          CALL      PRTDAT00 IF EQUAL
.
GETDET99  RETURN
.
. *****************************************************************************
.                               Print Detail Line
. *****************************************************************************
PRTDET00  MATCH     "Y",FRSTLINE
          IF        @EQUAL
            CALL      PRTDAT00
            GOTO      PRTDET99
          ENDIF
.
          MATCH     SP9,PTEOCODE
          GOTO      PRTDET10 IF EQUAL       * print detail line
.
          ADD       ONE,SUBOPR              * Count primary and assoc operations
          ADD       ONE,SUOOPR              *
.
PRTDET10  COMPARE   "1",SUMMONLY
          GOTO      PRTDET99 IF EQUAL
.
          COMPARE   "56",LNO
          CALL      HEDA0000 IF NOT LESS
          COMPARE   "7",LNO
          CALL      HEDB0000 IF LESS        * print sub heading if required
.          
          PRINT     *42,PTEOCODE,*54,PTEDTYPE,*56,PTEDCODE,*67,DNAME18A
          ADD       ONE,LNO
.
PRTDET99  RETURN
.
.******************************************************************************
.
.******************************************************************************
PRTDAT00  MOVE      ZERO,EXIT
          MOVE      "N",FRSTLINE
.
          BRANCH    SUMMONLY,PRTDAT20
.
          COMPARE   "56",LNO
          CALL      HEDA0000 IF NOT LESS
          COMPARE   "7",LNO
          CALL      HEDB0000 IF LESS        * print sub heading if required
.
          UNPACK    FROMDATE,XCENT,XYEAR,XMON,XDAY
          PACK      PADATE,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          UNPACK    TODATE,XCENT,XYEAR,XMON,XDAY
          PACK      PDDATE,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
          COMPARE   CSECFEE,SECLEV          * check security level
          IF        !@LESS
            MOVE      OFEE,FORM5
            MOVE      FORM5,KEY5
          ELSE
            MOVE    SP5,KEY5
          ENDIF
.
          PRINT     *1,PNAME12,*15,PURNO,*24,DADMNO,*34,PSEX,*37,PSAGE:
                    *42,PTEOCODE,*54,PTEDTYPE,*56,PTEDCODE,*67,DNAME18:
                    *86,ACLSSFT,*91,DDEST,*96,PTDSDDRG,*101,NBRDAYS:
                    *106,PADATE,*117,PDDATE,*127,KEY5
          ADD       ONE,LNO
.
.                                           * count total patients (all)
          ADD       ONE,SUOPAT
          ADD       ONE,SUBPAT
.                                           * capture totals for Principal Opr.
PRTDAT20  MATCH     PTEOCODE,CURROPR
          GOTO      PRTDAT99 IF NOT EQUAL
.
          ADD       ONE,SUOPRI
          ADD       ONE,SUBPRI
          ADD       NBRDAYS,SUODAY
          ADD       NBRDAYS,SUBDAY
.
PRTDAT99  RETURN
.
. **************************************************************************
.                 HEADINGS FOR OUTPUT
. **************************************************************************
HEDA0000  IF        FIRSTHED = 1
            CLOCK     TIME,CTIMEIS
            CALL      HEADSET
          ENDIF
          ADD       ONE,PAGENO
.
          BRANCH    OPTION,HEDA1000,HEDA1001,HEDA1002,HEDA1003
.
HEDA1000  MOVE      "- Admission Sequence",CPHDROPT
          GOTO      HEDA1100
.
HEDA1001  MOVE      "- Attending Doctor Sequence",CPHDROPT
          GOTO      HEDA1100
.
HEDA1002  MOVE      "- Discharge Date Sequence",CPHDROPT
          GOTO      HEDA1100
.
HEDA1003  MOVE      "- U/R Sequence",CPHDROPT
          GOTO      HEDA1100
.
HEDA1100  MOVE      ZERO,FORM5
.
HEDA2000  CALL      HEAD132                 * print End-of-page line & Page-head
          MOVE      "3",LNO
.
.                                           * Report info - first page only
          IF        FIRSTHED = 1
            IF        IBCNMHOS =1
              PRINT     *40,"Hospital : ",PTHSNAME
              ADD       ONE,LNO
            ENDIF
.
            PRINT     *N,*35,"***  Discharge Date From  ",PFDATE:
                      "   To  ",PTDATE,"   ***";
            MATCH     "B",RANGFLG
            IF        @EQUAL
            PRINT     *N,*35,"***  Block Code From      ",XDIM7:
                      "    To  ",YDIM7,"    ***",*N;
            ELSE
              PRINT     *N,*35,"***  Operation Code From  ",XDIM9:
                        "  To  ",YDIM9,"  ***",*N;
            ENDIF
.
            MATCH     SP6,YDIM6
            IF        @EQUAL
              PRINT     *35,"***  Doctor Type          ",XDIM6,*93,"***":
                        *100,TYPDESC,*N;
            ELSE
              PRINT     *35,"***  Doctor Type  From    ",XDIM6:
                        "     To  ",YDIM6,"     ***",*100,TYPDESC,*N; 
            ENDIF
.
            ADD       "5",LNO
          ENDIF
.
HEDA3000  MATCH     "Y",BILDRGFL
          IF        @EQUAL
            PRINT     *35," DRG",SP30,SP5,"Billed DRG ",*N
          ELSE
            PRINT     *35," DRG",SP30,SP5,"Grouper DRG ",*N
          ENDIF
          ADD       ONE,LNO
.
.                                           * reset print controls
          MOVE      ZERO,FIRSTHED
          MOVE      "zzzz",LASTBLK
.
HEDA9999  RETURN
.
. *****************************************************************************
.                             Sub heading
. *****************************************************************************
HEDB0000  COMPARE   "50",LNO
          CALL      HEDA0000 IF NOT LESS
.
          MATCH     LASTBLK,CURRBLK
          IF        !@EQUAL
            PRINT     *N,"Operation Block Number : ",CURRBLK,*35,DIM70A
            ADD       "2",LNO
            MOVE      CURRBLK,LASTBLK
.
            IF        SUMMONLY = 0
              PRINT     SUBLINE1,*N,SUBLINE2   * print sub-sub-heading
              ADD       "2",LNO
            ENDIF
          ELSE
            PRINT     *N
            ADD       ONE,LNO
          ENDIF
.
          PRINT     SDASHS,*N:
                    *1,"Operation Code   : ",CURROPR,*35,DIM70B,*N:
                    SDASHS
          ADD       "3",LNO
.
.                                           * reset print controls
          MOVE      ZERO,PTSUBHED
.
HEDB9999  RETURN
.
. *****************************************************************************
.                             Sub Totals for Operation Codes
. *****************************************************************************
PTOPR000  COMPARE   "52",LNO
          CALL      HEDA0000 IF NOT LESS
.
          MOVE      SUODAY,TMPCAL
          DIV       SUOPAT,TMPCAL
          MOVE      TMPCAL,SUOLOS
.
PTOPR100  PRINT     *N,*37,"-----Totals For Operation Code ",PREVOPR,"--------":
                    *N,*14,"Sub-Total Patients                :     ",SUOPAT:
                       *66,"Sub-Total Operations              :     ",SUOOPR:
                    *N,*14,"Sub-Total Primary                 :     ",SUOPRI:
                       *66,"Sub-Bed Days Primary              :  ",SUODAY:
                    *N,*66,"Sub-Average Length Of Stay Primary:    ",SUOLOS:
                    *N
          ADD       "4",LNO
          CALL      ZEROS1
.
          IF        SUMMONLY = 0 & ONEWPAGE = 1
            MATCH     CURRBLK,PREVBLK
            IF        @EQUAL
              MOVE      "69",LNO
            ENDIF
          ENDIF
.
.
PTOPR999  RETURN
.
. *****************************************************************************
.                             Sub Totals for Block Numbers
. *****************************************************************************
PTBLK000  COMPARE   "50",LNO
          CALL      HEDA0000 IF NOT LESS
.
          MOVE      SUBDAY,TMPCAL
          DIV       SUBPAT,TMPCAL
          MOVE      TMPCAL,SUBLOS
.
PTBLK100  PRINT     *N,*37,"-----Totals For Block Number ",PREVBLK,"--------":
                    *N,*14,"Sub-Total Patients                :     ",SUBPAT:
                       *66,"Sub-Total Operations              :     ",SUBOPR:
                    *N,*14,"Sub-Total Primary                 :     ",SUBPRI:
                       *66,"Sub-Bed Days Primary              :  ",SUBDAY:
                    *N,*66,"Sub-Average Length Of Stay Primary:    ",SUBLOS:
                    *N
          ADD       "4",LNO
          CALL      ZEROS
.
          IF        SUMMONLY = 0 & ONEWPAGE = 1
            MOVE      "69",LNO
            GOTO      PTBLK999
          ENDIF
.
PTBLK200  PRINT     *1,SDASHS
          ADD       "1",LNO
.
          MOVE      ONE,PTSUBHED
.
PTBLK999  RETURN
.
. *****************************************************************************
.                             Grand Totals                  
. *****************************************************************************
PRTGND00  MOVE      GNDDAY,TMPCAL
          DIV       GNDPAT,TMPCAL
          MOVE      TMPCAL,GNDLOS
.
          COMPARE   "1",SUMMONLY
          GOTO      PRTGND10 IF NOT EQUAL
.
          PRINT     *N,*N,*N,*N;
.
PRTGND10  PRINT     *N,*N,*N,*N:
                    *37,"-------   G R A N D    T O T A L S   ---------",*N,*N:
                    *41,"Total Patients          :     ",GNDPAT,*N:
                    *41,"Bed Days                :   ",GNDDAY,*N:
                    *41,"Average Length Of Stay  :     ",GNDLOS,*N
PRTGND99  RETURN
.
. *****************************************************************************
.                             Process Notes
. *****************************************************************************
PRTNTE00  PRINT     *N,*N:
                    *3,"*** Process Notes ***":
                    *N,*N:
                    *3,"This report is sequenced by the Operation Block Number":
                    *57," held in the ICD10 Operation Table.",*N:
                    *3,"Within each Operation Block Number operation details":
                    *55," appear in Operation Code sequence. This code is",*N:
                    *3,"also held in the ICD10 Operation Table.",*N,*N,*N:
                    *3,"With regards to Sub Totals please note the following -":
                    *N,*N,*3,"Sub-Total Patients - is the total of patients ":
                    *49,"having undergone the procedures listed for the",*N:
                    *3,"particular Operation Block Number. This is true if, ":
                    *55,"and only if, a procedure is a primary procedure.",*N:
                    *3,"If a procedure is an associated procedure performed ":
                    *55,"on the same patient during the same admission, it",*N:
                    *3,"will be detailed under the relevant Operation Block ":
                    *55,"Number but the patient will not be added to that",*N:
                    *3,"Operation Block Number sub total, as the patient must":
                    *56," not be added more than once.",*N,*N:
                    *3,"Sub-Bed Days Primary - is the total bed days ":
                    *48,"accumulated by patients listed for the particular",*N:
                    *3,"Operation Block Number. Bed days are arrived at for ":
                    *55,"each admission by using the admission date and the",*N:
                    *3,"discharge date as parameters. The Patient Bed Transfer":
                    *57," File is scrutinized for all bed days between the",*N:
                    *3,"parameter dates for the admission. Although a bed days":
                    *58,"figure may appear where ever the primary procedure",*N:
                    *3,"is referenced, it will only be accumulated when the ":
                    *55,"primary procedure operation code heads the detail.",*N:
                    *3,"Like sub total patients, this is to ensure that the ":
                    *55,"bed days are accumulated only once.",*N,*N:
                    *3,"Sub-Average Length of Stay - is the sub total bed days":
                    *57," divided by the sub total patients.",*N,*N:
                    *3,"Sub-Total Operations - is the total number of ":
                    *49,"operations listed for a particular Operation Block",*N:
                    *3,"Number, primary and associated.",*N,*N:
                    *3,"Grand Totals - that appear on this page are the totals":
                    *57," of the Operation Block Number sub totals.",*N,*N;
.
PRTNTE99  RETURN
+
. *****************************************************************************
.                       RESET AND CLEARANCE ROUTINES
.                Deleting temporary index files based on PORT
. *****************************************************************************
KILLIDX   CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEG,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
CREAIDX   CLEAR     KEY99
          APPEND    "isbuild ",KEY99
          APPEND    FNAMET,KEY99
          APPEND    " 121 ",KEY99
          APPEND    SORTSTR,KEY99
          APPEND    SP70,KEY99
          RESET     KEY99
          EXECUTE   KEY99,TASKID
.
          CLEAR     KEY99
          APPEND    "isbuild ",KEY99
          APPEND    FNAMEG,KEY99
          APPEND    " 9 uc1-8",KEY99
          APPEND    SP70,KEY99
          RESET     KEY99
          EXECUTE   KEY99,TASKID
.
          CLEAR     KEY99
          APPEND    "isbuild ",KEY99
          APPEND    FNAMEA,KEY99
          APPEND    " 9 uc1-8",KEY99
          APPEND    SP70,KEY99
          RESET     KEY99
          EXECUTE   KEY99,TASKID
          RETURN
+
. *****************************************************************************
.                        
. *****************************************************************************
HEADSET   MOVE      SP9,XDIM9
          MOVE      SP9,YDIM9
.
          MOVE      OPRSTART,XDIM9
          MOVE      OPRENDS,YDIM9
.
          MATCH     SP9,OPRSTART
          GOTO      HEADS100 IF NOT EQUAL
          MOVE      FSTREC,XDIM9
.
HEADS100  MATCH     SP9,OPRENDS
          GOTO      HEADS200 IF NOT EQUAL
          MOVE      LSTREC,YDIM9
.
HEADS200  MOVE      SP6,XDIM6
          MOVE      SP6,YDIM6
.
          MOVE      DRTYPCOD,XDIM6
.
          MATCH     SP3,DRTYPCOD
          GOTO      HEADS999 IF NOT EQUAL
.
          MOVE      FSTREC,XDIM6
          MOVE      LSTREC,YDIM6

.                                           * set up Sub-Heading lines
          COMPARE   CSECFEE,SECLEV          * check security level
          IF        !@LESS
            PACK      SDASHS,DASH50,DASH50,DASH50
            PACK      SUBLINE1,SUBH101,SUBH102,SUBH103
            PACK      SUBLINE2,SUBH201,SUBH202,SUBH203A
          ELSE
            PACK      SDASHS,DASH50,DASH50,DASH30
            PACK      SUBLINE1,SUBH101,SUBH102,SUBH101
            PACK      SUBLINE2,SUBH201,SUBH202,SUBH203B
          ENDIF
.
HEADS999  RETURN
.
. *****************************************************************************
.                        
. *****************************************************************************
ZEROS     MOVE      ZERO,SUBPAT
          MOVE      ZERO,SUBPRI
          MOVE      ZERO,SUBDAY
          MOVE      ZERO,SUBLOS
          MOVE      ZERO,SUBOPR
          RETURN
.
ZEROS1    MOVE      ZERO,SUOPAT
          MOVE      ZERO,SUOPRI
          MOVE      ZERO,SUODAY
          MOVE      ZERO,SUOLOS
          MOVE      ZERO,SUOOPR
          RETURN
.
. *****************************************************************************
.
. *****************************************************************************
STRTCODE  MOVE      EIGHT,EVERT
          MOVE      THIRTY6,ECOL
          MOVE      ONE,CNEWDS
          MOVE      SP9,CKYIDEF9
          MOVE      ZERO,CKYIMAND
          MOVE      ZERO,CKYIMODE
          UNPACK    STARTD,XCENT,XYEAR,XMON,XDAY 
          MOVE      XCENT,CCC
          MOVE      XYEAR,CYY
          MOVE      XMON,CMM
          MOVE      XDAY,CDD
          COMPARE   ICDFLAG,ONE             *USING ICD9 OR ICD10 ?
          IF        @EQUAL
            CALL      PATIC9KY              *USING ICD9
          ELSE
            MOVE      STARTD,ICDRDDTE             
            CALL      PATICOKY              *USING ICD10
          ENDIF
.
          RETURN 
.
FINICODE  MOVE      NINE,EVERT
          MOVE      THIRTY6,ECOL 
          MOVE      ONE,CNEWDS
          MOVE      SP9,CKYIDEF9
          MOVE      ZERO,CKYIMAND
          MOVE      ZERO,CKYIMODE
          UNPACK    ENDATE,XCENT,XYEAR,XMON,XDAY   
          MOVE      XCENT,CCC
          MOVE      XYEAR,CYY
          MOVE      XMON,CMM
          MOVE      XDAY,CDD
          COMPARE   ICDFLAG,ONE              *USING ICD9 OR ICD10 ?
          IF        @EQUAL
            CALL      PATIC9KY               *USING ICD9
          ELSE
            CALL      PATICOKY               *USING ICD10
          ENDIF
.
          RETURN 
+
.*********************************************************************
.         enter the Doctor Code or Doctor Type
.*********************************************************************
KYDOC000  MOVE      "36",CCOL
          MOVE      "11",CVERT
          MOVE      ONE,SCRNFLAG
          UNPACK    SP30,DOCCODE,KEY6,STDOCT
.
KYDOC100  KEYIN     *PCCOL:CVERT,*DV,UNDLN6,*PCCOL:CVERT,*V2LON,KEY6:
                    *PCCOL:CVERT,*DV,KEY6;
          PACK      KEY6,KEY6,SP3
          MATCH     SP6,KEY6
          GOTO      KYDOC400 IF EQUAL       * set for all codes
          MATCH     "ALL   ",KEY6
          GOTO      KYDOC400 IF EQUAL       * set for all codes
.
          CMATCH    QUEST,KEY6
          GOTO      KYDOC200 IF NOT EQUAL   * validate the code
.
. display valid doctors and types
.
          MOVE      CODEDT,CODE
          CALL      PATCODDS
.
          KEYIN     *P1:24,*EL,"Do you want to see Doctor Code (Y/N) ? ",ANS;
          MOVE      ZERO,SCRNFLAG
          MOVE      "24",CVERT
          MOVE      "18",CCOL
          REP       UPPLOW,ANS
          CMATCH    "N",ANS
          GOTO      KYDOC150 IF EQUAL
.
KYDOC120  CALL      PATDOCDS
          MOVE      "24",CVERT
          MOVE      "18",CCOL
          MATCH     SP6,DOCCODE
          GOTO      KYDOC150 IF EQUAL
          MOVE      DOCCODE,KEY6
          GOTO      KYDOC200
.
KYDOC150  DISPLAY   *P1:24,*EL,"Enter the Code : ";
          GOTO      KYDOC100
.
. validate code
.
KYDOC200  MOVE      SP10,DIM3
          UNPACK    KEY6,KEY3,DIM3
          MATCH     SP3,DIM3
          GOTO      KYDOC220 IF NOT EQUAL
.
          PACK      KEY5,ANSD,ANST,KEY3
          CALL      RDCODE1
          COMPARE   ZERO,OVRCD
          GOTO      KYDOC300 IF EQUAL       * valid code
.
KYDOC220  CALL      RDDOCT1
          BRANCH    OVRCD,KYDOC210          * invalid code
.
          MOVE      KEY6,STDOCT
          MOVE      DRTYPE,ACODE
          PACK      KEY5,CODEDT,ACODE
          CALL      RDCODE1
          COMPARE   ZERO,OVRCD
          GOTO      KYDOC300 IF EQUAL       * valid code
.
KYDOC210  DISPLAY   *P1:24,*EL:
                    *B,*V2LON,"** Invalid Doctor Type/Doctor Code **    ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,KYDOC100
          GOTO      KYDOC150
.
. have a valid code
.
KYDOC300  MOVE      ACODE,DRTYPCOD
          BRANCH    SCRNFLAG,KYDOC310
          CALL      RDIS0000
.
KYDOC310  MATCH     SP6,STDOCT
          GOTO      KYDOC320 IF EQUAL
          MOVE      "Y",PDOCONLY
          MOVE      DGNAME,ANS
          PACK      DOCNAME,ANS,DOT,DSNAME
          DISPLAY   *P36:11,*EL,*V2LON,STDOCT,*HOFF,*P46:11,DOCNAME:
                    *P36:12,*EL,*V2LON,DRTYPCOD,*HOFF,*P46:12,TDESC:
                    *P36:13,*EL,*V2LON,PDOCONLY;
          GOTO      KYDOC999
.
KYDOC320  DISPLAY   *P36:11,*EL,*V2LON,DRTYPCOD,*HOFF,*P46:11,TDESC;
          GOTO      KYDOC999
.
KYDOC400  BRANCH    SCRNFLAG,KYDOC410
          CALL      RDIS0000
.
KYDOC410  MOVE      SP3,DRTYPCOD
          DISPLAY   *P36:11,*V2LON,ALLREC,*EL;
.
KYDOC999  RETURN
+
.*********************************************************************
.         redisplay the current data
.*********************************************************************
RDIS0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"1",*HOFF,". Starting Discharge Date       : ":
                    *V2LON,PFDATE,*HOFF:
                    *P1:5,*V2LON,"2",*HOFF,". Ending   Discharge Date       : ":
                    *V2LON,PTDATE,*HOFF:
                    *P1:7,*V2LON,"3",*HOFF,". Operation or Block Range (O/B): ":
                    *V2LON,RANGFLG,*HOFF;
          MATCH     "O",RANGFLG
          GOTO      RDIS5000 IF EQUAL
.
          DISPLAY   *P1:8,*V2LON,"4",*HOFF,". From Block Code               : ":
                    *V2LON,BLKSTART,*HOFF:
                    *P1:9,*V2LON,"5",*HOFF,". To   Block Code               : ":
                    *V2LON,BLKENDS,*HOFF;
.
          MATCH     SP4,BLKSTART
          GOTO      RDIS1000 IF NOT EQUAL
          DISPLAY   *P37:8,*V2LON,FSTREC;
.
RDIS1000  MATCH     "Y",FSTTIM
          GOTO      RDIS8000 IF EQUAL
.
          MATCH     SP4,BLKENDS
          GOTO      RDIS8000 IF NOT EQUAL
          DISPLAY   *P37:9,*V2LON,LSTREC;
.
RDIS5000  DISPLAY   *P1:8,*V2LON,"4",*HOFF,". From Operation Code           : ":
                    *V2LON,OPRSTART,*HOFF:
                    *P1:9,*V2LON,"5",*HOFF,". To   Operation Code           : ":
                    *V2LON,OPRENDS,*HOFF;
.
          MATCH     SP9,OPRSTART
          GOTO      RDIS6000 IF NOT EQUAL
          DISPLAY   *P37:8,*V2LON,FSTREC;
.
RDIS6000  MATCH     "Y",FSTTIM
          GOTO      RDIS8000 IF EQUAL
.
          MATCH     SP9,OPRENDS
          GOTO      RDIS8000 IF NOT EQUAL
.
          DISPLAY   *P37:9,*V2LON,LSTREC;
.
RDIS8000  DISPLAY   *P1:11,*V2LON,"6",*HOFF,". Keyin Doctor Type/Doctor Code :":
                    SP1,*V2LON,DRTYPCOD,*HOFF:
                    *P55:11,"   <RETURN> for All ":
                    *P1:13,*V2LON,"7",*HOFF,". Primary Doctor Type Only (Y/N):":
                    SP1,*V2LON,PDOCONLY,*HOFF:
                    *P1:14,*V2LON,"8",*HOFF,". Keyin Unit                    :":
                    SP1,*V2LON,SELUNIT,*HOFF:
                    *P1:15,*V2LON,"9",*HOFF,". Summary Details Only     (Y/N):":
                    SP1,*V2LON,DIM1A,*HOFF:
                    *P1:16,*V2LON,"10",*HOFF,".New page for each Oper.  (Y/N):":
                    SP1,*V2LON,DIM1B,*HOFF:
                    *P1:17,*V2LON,"11",*HOFF:
                                            ".Print Principal Condition Only:":
                    SP1,*V2LON,DIM1C,*HOFF;
.
          IF        IBCNMHOS=1
            DISPLAY   *P1:18,*V2LON,"12",*HOFF,".Hospital Id":
                      *P34:18,":"
          ENDIF
.
RDIS9999  RETURN
+
.*********************************************************************
.         clear the variables
.*********************************************************************
CLRA0000  UNPACK    SP20,OPRSTART,OPRENDS
          UNPACK    SP20,BLKSTART,BLKENDS
          UNPACK    SP20,STARTD,ENDATE
          UNPACK    SP20,PFDATE,PTDATE
          UNPACK    SP20,SELUNIT
          UNPACK    SP10,DRTYPCOD,PDOCONLY
          UNPACK    SP10,STDOCT
          UNPACK    SP30,DOCNAME
          UNPACK    SP30,PTHSNAME
CLRA9999  RETURN
+
.*********************************************************************
          INC       STD001IO
          INC       ICDHLPSC
          INC       RTIODAYS
          INC       PATCODKY
          INC       PATHSPKY
          INC       PATICODS
          INC       PATICOKY      
          INC       PATIC9KY
          INC       PATDOCDS
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATECDIO/INC
          INC       PATECOIO/INC
          INC       PATHSPIO/INC
          INC       PATICGIO/INC
          INC       PATICOIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATDO1IO/INC
          INC       PATTRNIO/INC
          INC       PATOICIO/INC
          INC       PMSVX1IO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.
          INC       GETDRG
          INC       GETEFDRG
          INC       PATPGRIO/INC
          INC       PMSEDGIO/INC
          INC       PATCFAIO/INC
          INC       PATFX1IO/INC
          INC       PATFN1IO/INC


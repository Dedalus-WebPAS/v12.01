.******************************************************************************
.* System    :   Patient Management System                                    *
.* Program   :   FXTAUMAS                                                     *
.* Desc      :   Create NHI records for all PMI master file records           *
.******************************************************************************
.* Date      :   22/06/1999                                                   *
.* Author    :   Steve Armstrong                                              *
.* Mods      :                                                                *
.******************************************************************************
+
          INC       STD001FD
.
          INC       NHIMASFD/INC            * NHI Master file
          INC       PATMA1FD/INC            * Master file
          INC       PATNIDFD/INC            * National Id File
.         INC       PATNMPFD/INC            * NZ NHI stuff
.
. LOCAL VARIABLES
. ---------------
CHARNUM   FORM      2
COUNT     FORM      6                       * Record counter
.
DIM20     DIM       20
.
FOUNDCHR  FORM      1                       * found character flag
.                                              0 = valid character not found
.                                              1 = valid character found
.
LENVAR    FORM      2
.
PTCNANMA  FORM      1
.
UPDFLAG   FORM      1                       * update flag
.                                               0 = update record
.                                               1 = write new record
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
.
.
PRGNAM    INIT      "Convert PMI Data"
PRGID     INIT      "FXTAUNHI"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
.
ML0000    CALL      INIT0000                * Initialise variables & open files
.
          CALL      OPTN0000                * Ask proceed question
          BRANCH    EXIT,ML9999
.
          CALL      PMST0000                * Process the master file
.
ML9999    CHAIN     PGM                     * Chain back to appropriate menu
+
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                      Initialise Variables & Open Files                     *
.******************************************************************************
.
INIT0000  CALL      DISPHEAD                     * Display screen header
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"            
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"            
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"            
.
          DISPLAY   *P64:24,"nhimasaf";
          OPEN      NHIMASA1,"nhimasaf"
.
          DISPLAY   *P64:24,"patnidaf";
          OPEN      PATNIDA1,"patnidaf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,EIGHTY;*140,PTCNANMA
          IF        PTCNANMA = 1
            OPEN      NHIAUDMA,"nhiaudma"
          ENDIF
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                            Ask Proceed Question                            *
.******************************************************************************
.
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,"Note : This program creates nhimasaf ":
                    "records for all patma1af records"
.
          DISPLAY   *P1:24,*EL,*HOFF,"Ok to Proceed (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ";
.
OPTN1000  KEYIN     *P23:24,*V2LON,ANS
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          CMATCH    ANSY,ANS
          GOTO      OPTN9000 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      OPTN9500 IF EQUAL
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                  PMST0000              Called by: ML0000   *
.*                           Process The Master File                          *
.******************************************************************************
.
PMST0000  DISPLAY   *P1:23,"Processing: ",*P50:23,"Record: ";
.
          MOVE      ZERO,COUNT                   * initialise count
          MOVE      ZERO,CNOUNDLN                * initialise print variables
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,CLNO
          CALL      IBACLOCK
          CALL      HEAD0000
.
.         Loop through the patma1af file
.
          MOVE      SP8,KEY8
          CALL      RDSMAST1                     * position at start of file
PMST1000  CALL      RDKMAST1                     * read next record
          BRANCH    OVRCD,PMST9000               * eof - finished
.
          COMPARE   ZERO,PSTAT                   * valid U/R ?
          GOTO      PMST1000 IF NOT EQUAL        * n0  -ignore
.
          ADD       ONE,COUNT                    * increment count
          IF        (COUNT%1000)=0|COUNT=1
            DISPLAY   *P13:23,*V2LON,PURNO:
                      *P58:23,COUNT;
          ENDIF
.
.         Get the national number for the patient from the patnidaf file
.
....      PACK      KEY10,CHOSINDX,PURNO
....      CALL      RDPTNID1                     * record on file ?
....      BRANCH    OVRCD,PMST8000               * no - print error line
.
          MOVE      PURNO,DIM20               * load nhi number
          SQUEEZE   DIM20                        * remove spaces from nat. no.
          MOVELPTR  DIM20,LENVAR
.
          IF        LENVAR < 1 | LENVAR > 7
            GOTO      PMST8000                   * invalid NHI number
          ENDIF
.
          BRANCH    LENVAR,PMST1010:
                           PMST1020:
                           PMST1030:
                           PMST1040:
                           PMST1050:
                           PMST1060:
                           PMST1070
.
PMST1010  PACK      NHMANMPI,SP6,DIM20
          GOTO      PMST2000
.
PMST1020  PACK      NHMANMPI,SP5,DIM20
          GOTO      PMST2000
.
PMST1030  PACK      NHMANMPI,SP4,DIM20
          GOTO      PMST2000
.
PMST1040  PACK      NHMANMPI,SP3,DIM20
          GOTO      PMST2000
.
PMST1050  PACK      NHMANMPI,SP2,DIM20
          GOTO      PMST2000
.
PMST1060  PACK      NHMANMPI,SP1,DIM20
          GOTO      PMST2000
.
PMST1070  MOVE      DIM20,NHMANMPI
PMST2000  CALL      WNHI0000                     * write nhi record
.
          GOTO      PMST1000                     * get next record
.
PMST8000  CALL      PRNT0000                     * no - print an error report
          GOTO      PMST1000                     * get next record
.
PMST9000  CALL      LINE0000                     * print line
          PRINT     *N,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,"Conversion completed.  ";
          CALL      HITENTER
.
PMST9999  RETURN
+
.****************************************************************************
.*                             WNHI0000                Called by: PMST0000  *
.*                 Write a record to the nhimasaf file                      *
.****************************************************************************
.
WNHI0000  MOVE      NHMANMPI,KEY7
          CALL      RANHMAS1                * record on file already ?
          COMPARE   ZERO,OVRCD
          GOTO      WNHI9999 IF EQUAL       * yes - ignore
.
          CALL      GIVN0000                * get first given name
.
          MOVE      PURNO,NHMAURNO          * load other file variables
          PACK      NHMASURN,PSNAME,SP30
          MOVE      SP20,NHMAGIV2
          MOVE      SP20,NHMAGIV3
          MOVE      ONE,NHMAPREI
          MOVE      PADD1,NHMAADD1
          MOVE      PADD2,NHMAADD2
          MOVE      PSUBRB,NHMAADD3
          MOVE      PADD4,NHMAADD4
          MOVE      SP30,NHMAADD5
          MOVE      PBDATE,NHMADOB
          IF        PCEASE = 1
            MOVE      PDECDTE,NHMADDTH
          ELSE
            MOVE      SP8,NHMADDTH
          ENDIF
          MOVE      PPOST,NHMADOMC
          MOVE      SP1,NHMARES
          MOVE      SP2,NHMAETH
          MOVE      PSEX,NHMASEX
          MOVE      SP8,NHMADAT
          MOVE      SP8,NHMATIM
          MOVE      SP2,NHMAETH2
          MOVE      SP2,NHMAETH3
          PACK      NHMASPA,SP20,SP6
.
          CALL      WRNHMAS1
.
.         Check if nhimasaf audit record has to be written
.
          MOVE      ONE,AUDTTYPE          * set audit flag for "write"
          CALL      WANHMA00
.
WNHI9999  RETURN
+
.******************************************************************************
.*                                  GIVN0000              Called by: WNHI0000 *
.*                           Get the first given name                         *
.******************************************************************************
.
GIVN0000  CLEAR     NHMAGIV1
          MATCH     SP25,PGNAME                  * given name blank ?
          IF        @EQUAL
            MOVE      PGNAME,NHMAGIV1            * yes
            GOTO      GIVN9999
          ENDIF
.
          MOVE      ZERO,FOUNDCHR                * initialise found char. flag
          MOVE      ZERO,CHARNUM                 * initialise charcater count
          WHILE     CHARNUM < 25
            CMATCH    SP1,PGNAME                 * character blank ?
            IF        @EQUAL
              BRANCH    FOUNDCHR,GIVN9000        * yes - 
            ENDIF
.
            CMOVE     PGNAME,ANS                 * save character
            ENDSET    NHMAGIV1 
            APPEND    ANS,NHMAGIV1               * add char. to nhi field
            RESET     NHMAGIV1
.
            MOVE      ONE,FOUNDCHR               * set char. found flag
            BUMP      PGNAME,1                   * position on next character
            ADD       ONE,CHARNUM                * increment char. count
          DO
.
GIVN9000  RESET     PGNAME
          PACK      NHMAGIV1,NHMAGIV1,SP30
.
GIVN9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: PRNT0000 *
.*                           Print The Report Header                          *
.******************************************************************************
.
HEAD0000  CALL      HEAD80                       * print report header
          PRINT     *20,"U/R's with no National No."
.
          CALL      LINE0000                     * print line
.
          PRINT     *1,PIPE,*3,"U/R Number",*14,PIPE,*16,"Surname",*37,PIPE:
                    *39,"Given Names",*65,PIPE,*67,"Date of Birth",*80,PIPE
.
          CALL      LINE0000                     * print line
.
          MOVE      SEVEN,CLNO                   * set line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by: HEAD0000  *
.*                      draw line across page                     PMST0000  *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "-------------------------------*"
.
          ADD       ONE,CLNO
.
LINE9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: PMST0000 *
.*                            Print An Error Report                           *
.******************************************************************************
.
PRNT0000  COMPARE   FIFTY6,CLNO                  * page full ?
          CALL      HEAD0000 IF NOT LESS         * yes
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *1,PIPE,*4,PURNO,*14,PIPE,*16,PSNAME,*37,PIPE,*39,PGNAME:
                    *65,PIPE,*68,CPCDATE,*80,PIPE
          ADD       ONE,CLNO                     * increment line count
.
PRNT9999  RETURN
+
.*****************************************************************************
.         I/O Includes                                                       *
.*****************************************************************************
.
          INC       STD001IO
.
          INC       NHIMASIO/INC            * NHI master file
          INC       PATMA1IO/INC            * Master file
          INC       PATNIDIO/INC
+

.*****************************************************************************
.*  System       :  Outpatient                                               *
.*  Program      :  IBAOUT80                                                 *
.*  Function     :  Outpatient Attendance by Hospital                        *
.*****************************************************************************
.*  Date         :  19/05/93                                                 *
.*  Author       :  Whirlpool                                                *
.*  Modifactions :                                                           *
.*****************************************************************************
.*        V11.02.01 09/02/2022  Thanh T          TSK 0905641                 *
.*                  Recompiled as OUTMA1FD changes                           *
.*        V10.07.01 29/10/2015  Ebon Clements  CAR 268970                    *
.*                  Change to use OUTCLIFD index 1 instead of index 2        *
.*        V10.05.01 18/12/2014  Ebon Clements  CAR 261630                    *
.*                  Removed port number from temp file name                  *
.*        V10.03.01 17/10/2012  Jill Parkinson CAR 273375                    *
.*                  Recompiled for DATECONV - first day of year calculation  *
.*        V9.02.01  23/10/2001  Dean Cramer                                  *
.*                  Fix so all session details print.                        *
.*        V5.08.B01 15/03/2000  Steve Armstrong     5.08 Mods                *
.*                  Mods for changes to OUTCLIFD (effective date)            *
.*****************************************************************************
.*        V5.01.02  21/09/1993    LOFTY                                      *
.*                  fix temp file name and delete it when finished           *
.*        V5.01.03  24/05/1995  Matt Surridge                                *
.*                  Fixed bugs for global recompile.                         *
.*        V5.01.04  20/07/1994  Rob Leonard                                  *
.*                  Fixed hospital keyin                                     * 
.*---------------------------------------------------------------------------*
.*        V5.03.01  12/04/1995  Max White       SRF 441212                   *
.*                  1. Added Grand Totals.                                   * 
.*                  2. Calculate sessions held as in IBAOUT84 (i.e. count    * 
.*                     any number of sessions in the same hour as a single   * 
.*                     session).                                             * 
.*                  3. Any visit where indicator 1 not = 'N' to be counted   *
.*                     as a follow-up attendance.                            *
.*****************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATHSPFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTOSFFD/INC
          INC       OUTSITFD/INC
          INC       OUTCTYFD/INC
          INC       OUTCLIFD/INC
          INC       OUTMA1FD/INC
          INC       OUTCSLFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
DOL       DIM       10
CODE      DIM       2
CONS      DIM       15
CMDLINE   DIM       60
CFNAMEA   DIM       8
CFNAMEB   DIM       8
DIM3      DIM       3
DIM4      DIM       4
DIM6ARRY  DIM       6[34]
DIM8      DIM       8
DIM25     DIM       25
DIM60     DIM       60
DIM75     DIM       75
DIM80     DIM       80
EHSP      DIM       6
EHSPDESC  DIM       35
EDOL      DIM       10
ENDDATE   DIM       8
PERCDNA   FORM      3.2
PERCWALK  FORM      3.2
SAVSITE   DIM       6
SAVSPEC   DIM       6
SDOL      DIM       10
SHSP      DIM       6
SHSPDESC  DIM       35
SFORM1    FORM      1
STRTDATE  DIM       8
.
START     INIT      "Start "
FINISH    INIT      "Finish"
ZEDS      INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
GRANDTOT  FORM      5
.
TODAY     DIM       8
.
TMPFILEA  IFILE  SQL, FIXED=70, KEY="U1-12"
KEYA      INIT   " 70 U1-12"                
.
.Name     Type      Length   Phys.  Start  Description
.-------  ----      ------   -----  -----  -------------------------------
XXSPEC    DIM        6           6      1
XXCONS    DIM        6           6      7
XXSESSH   FORM       4           3     13
XXSESSC   FORM       4           3     16
XXNEWSEE  FORM       4           3     19
XXNEWDNA  FORM       4           3     22
XXFUPSEE  FORM       4           3     25
XXFUPDNA  FORM       4           3     28
XXTOTSEE  FORM       5           4     31
XXTOTDNA  FORM       5           4     35
XXNBOOKA  FORM       4           3     39
XXSPECD   DIM       13          13     42
XXCONSD   DIM       15          15     55
.                                      70
.
TMPFILEB  IFILE     SQL, FIXED=32, KEY="U1-3,4-9,10-15,16-21,22-29,30-31"
KEYB      INIT      " 32 U1-3,4-9,10-15,16-21,22-29,30-31"
.
.Name     Type      Length   Phys.  Start  Description
.-------  ----      ------   -----  -----  -------------------------------
XBHOSP     DIM       3           3      1  Hospital code
XBSITE     DIM       6           6      4  Outpatients Site Code 
XBSESS     DIM       6           6     10  Session Id
XBCTYPE    DIM       6           6     16  Clinic Type          
XBSDATE    DIM       8           8     22  Session Date
XBSHR      DIM       2           2     30  Session Hour
.                                      32
. Grand Totals
.
GTSESSH   FORM       4
GTSESSC   FORM       4
GTNEWSEE  FORM       4
GTNEWDNA  FORM       4
GTFUPSEE  FORM       4
GTFUPDNA  FORM       4
GTTOTSEE  FORM       5
GTTOTDNA  FORM       5
GTNBOOKA  FORM       4
.
PRGID     INIT      "IBAOUT80"
PRGNAM    INIT      "Outpatient Attendance By Hospital"                       
VERSION   INIT      "V12.01.00"
.
.*****************************************************************************
.*                              ML0000                                       *
.*                             Mainline                                      *
.*****************************************************************************
.
ML0000    CALL      INIT0000                * initialistion
.
ML1000    CALL      MENU0000                * Program Menu
          BRANCH    EXIT,ML9995
.
ML2000    CALL      DSCR0000
          CALL      KDAT0000                * Keyin the date range
          BRANCH    EXIT,ML9995         
.
ML3000    CALL      KHOS0000                * Keyin the Hospital range
          BRANCH    EXIT,ML9995         
.
          CALL      CONTQST
          BRANCH    CEXIT,ML5000,ML2000,ML1000
.
ML5000    CALL      CTPA0000                * create the 1st tempfile
          CALL      CTPB0000                * create the 2nd tempfile

          CALL      EXTR0000                * Extract the data
          BRANCH    EXIT,ML1000
.
          CALL      PRNT0000                * Print the report by speciality
          GOTO      ML1000
.
ML9995    CALL      DTPA0000                * delete the 1st tempfile
          CALL      DTPB0000                * delete the 2nd tempfile
.
ML9999    CHAIN     PGM
.
.*****************************************************************************
.*                              INIT0000                                     *
.*                        Initialisation section                             *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
          OPEN      PATCODE1,"patcodes"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      OUTSITA1,"outsitaf"
          OPEN      OUTOSFA3,"outosfaf"
          OPEN      PATDO1A1,"patdo1af"
          
.
          MOVE      ONE,CNEWDS
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,CFNAMEA
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,CFNAMEB
.
INIT9999  RETURN
.
.*****************************************************************************
.*                              MENU0000                                     *
.*                        Program Menu Routine                               *
.*****************************************************************************
.
MENU0000  MOVE      ZERO,EXIT
.
          HLINE     *G37,2,57,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Print the Report":
                    *P1:7,"Select Option ? "

MENU1000  KEYIN     *P17:7,*V2LON,MOPTION:
                    *P17:7,*DV,MOPTION
.
          BRANCH    MOPTION,MENU9999            
.
          COMPARE   ZERO,MOPTION
          GOTO      MENU9000 IF EQUAL
.
          BEEP
          GOTO      MENU1000
.
MENU9000  MOVE      ONE,EXIT
.
MENU9999  RETURN
                   
.
.*****************************************************************************
.*                              DSCR0000                                     *
.*                        Display the screen                                 *
.*****************************************************************************
.
DSCR0000  DISPLAY   *P1:10,*EF:
                    *P1:10,"Date     from : ":
                    *P1:11,"           to : ":
                    *P1:13,"Hospital from : ":
                    *P1:14,"           to : "
.
DSCR9999  RETURN
.*****************************************************************************
.*                              KDAT0000                                     *
.*                        Keyin the date on list range                       *
.*****************************************************************************
.
KDAT0000  MOVE      ZERO,EXIT
          MOVE      CCC,CCENT
          UNPACK    SP30,CDAY,CMON,CYEAR
          MOVE      "18",CCOL
          MOVE      TEN,CVERT
          MOVE      ONE,CCANLDTE
          MOVE      ZERO,CHIGHLT
.
          DISPLAY   *P18:10,*EL,*P18:11,*EL
          CALL      KEYDATE
.
          MATCH     SP2,CDAY
          IF        @EQUAL
            BEEP 
            GOTO      KDAT0000
          ENDIF
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
.
          MATCH     STRTDATE,TODAY
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date cannot be into the future. ";
            CALL      HITENTER
            GOTO      KDAT0000
          ENDIF
          PACK      SDOL,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
KDAT5000  MOVE      TEN1,CVERT
          CALL      KEYDATE
.
          MATCH     SP2,CDAY
          IF        @EQUAL
            BEEP
            GOTO      KDAT5000
          ENDIF
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          MATCH     ENDDATE,TODAY
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date cannot be into the future. ";
            CALL      HITENTER
            GOTO      KDAT5000
          ENDIF
          PACK      EDOL,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          MATCH     STRTDATE,ENDDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Dates must be in sequence. ";
            CALL      HITENTER
            GOTO      KDAT0000
          ENDIF
.
KDAT9999  RETURN
.
.
.*****************************************************************************
.*                           KHOS0000                                        *
.*                      Keyin the Hospital Range                             *
.*****************************************************************************
.
KHOS0000  MOVE      ZERO,CKYIMAND
          MOVE      SP3,CKYIDEF3
          MOVE      "18",ECOL
          MOVE      TEN3,EVERT
          MOVE      ZERO,EXIT
.
          CALL      PATHSPKY
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KHOS9999
          ENDIF
.
          IF        EXIT = 1
            MOVE      ZERO,EXIT
            MOVE      SP3,SHSP
            PACK      SHSPDESC,START,SP30
            DISPLAY   *PECOL:EVERT,*V2LON,START
            GOTO      KHOS5000
          ENDIF
.
          MOVE      KEY3,SHSP
          PACK      SHSPDESC,KEY3,SP2,PTHSNAME
          DISPLAY   *PECOL:EVERT,*V2LON,SHSP,SP2,*HOFF,PTHSNAME  
          
.
KHOS5000  MOVE      ZERO,EXIT
          MOVE      TEN4,EVERT
          MOVE      ZERO,EXIT
.
          CALL      PATHSPKY
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KHOS9999
          ENDIF
.
          IF        EXIT = 1
            MOVE      ZERO,EXIT
            MOVE      ZEDS,EHSP
            PACK      EHSPDESC,FINISH,SP30
            DISPLAY   *PECOL:EVERT,*V2LON,FINISH
            GOTO      KHOS9999
          ENDIF
.
          MOVE      KEY3,EHSP
          PACK      EHSPDESC,KEY3,SP2,PTHSNAME
          DISPLAY   *PECOL:EVERT,*V2LON,EHSP,SP2,*HOFF,PTHSNAME  
.
          MATCH     SHSP,EHSP
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Hospitals must be in sequence. ";
            CALL      HITENTER
            DISPLAY   *P18:13,*EL,*P18:14,*EL
            GOTO      KHOS0000
          ENDIF
.         
KHOS9999  RETURN
.
.*****************************************************************************
.*                           EXTR0000                                        *
.*                        Extract the relevant data                          *
.*****************************************************************************
.
EXTR0000  DISPLAY   *P1:24,*EL,"Extracting - "
.
          MOVE      ONE,FORM1
          MOVE      SP8,DIM8
          MOVE      SP3,SAVSITE
.
          PACK      KEY34,STRTDATE,SP30
          CALL      RSOTOSF3
EXTR1000  CALL      RKOTOSF3
          BRANCH    OVRCD,EXTR9000 
.
.-------- Test if within date range -------
.
          MATCH     OTOSDATE,ENDDATE
          GOTO      EXTR9000 IF LESS
.
          MATCH     OTOSHOSP,EHSP
          IF        @LESS
            PACK      KEY34,OTOSDATE,OTOSHOSP,ZEDS
            CALL      RSOTOSF3
            GOTO      EXTR1000
          ENDIF
.
.-------- Open correct files ------
.
          MATCH     SAVSITE,OTOSSITE
          IF        !@EQUAL
            PACK      KEY6,OTOSSITE
            CALL      RDSITA1
            BRANCH    OVRCD,EXTR1000
.
            MOVE      OTOSSITE,SAVSITE
            PACK      CFNAME,OSTFILE,FILBOKA1
            OPEN      OUTBOKA1,CFNAME
            PACK      CFNAME,OSTFILE,SP10
            CALL      OPOTBB11
            PACK      CFNAME,OSTFILE,FILCTYA1
            OPEN      OUTCTYA1,CFNAME
            PACK      CFNAME,OSTFILE,FILCLIA1
            OPEN      OUTCLIA1,CFNAME
          ENDIF
.
.-------- Check if held session has already been counted --------
.
          UNPACK    OTOSTIME,XBSHR
          PACK      KEY31,OTOSHOSP,OTOSSITE,OTOSSESS,OTOSCTYP,OTOSDATE,XBSHR
          CALL      RDTEMPB
          IF        OVRCD = 1
            UNPACK    KEY31,XBHOSP,XBSITE,XBSESS,XBCTYPE,XBSDATE,XBSHR
            CALL      WRTEMPB
          ELSE
            MOVE      OTOSCTYP,XXSPEC
            MOVE      OTOSSESS,XXCONS
            GOTO      EXTR1900              * same session as before
          ENDIF
.
          MOVE      OTOSCTYP,XXSPEC
          MOVE      OTOSSESS,XXCONS
          PACK      KEY12,XXSPEC,XXCONS
          CALL      RDXXOAH1
          IF        OVRCD = 1
            CALL      CLER0000
            ADD       ONE,XXSESSH
            CALL      GDES0000              * get description
            CALL      WRXXOAH1
          ELSE
            ADD       ONE,XXSESSH
            CALL      UPXXOAH1
          ENDIF
.
EXTR1900  PACK      KEY28,OTOSSITE,OTOSCLID,OTOSDATE,OTOSTIME,SP30
          CALL      RDSBOKA1
EXTR2000  CALL      RDKBOKA1
          BRANCH    OVRCD,EXTR1000
.
.-------- Test for correct session ------
.
          PACK      DIM25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     DIM25,KEY28
          GOTO      EXTR1000 IF NOT EQUAL
.
          COMPARE   FOUR,OBASTAT
          GOTO      EXTR2000 IF LESS
.
          PACK      KEY5,ANSC,ANSV,OBAVISIT
          CALL      RDCODE1
          BRANCH    OVRCD,EXTR2000
.
          PACK      KEY12,XXSPEC,XXCONS
          CALL      RDXXOAH1
.
          MATCH     ANSN,TCINDC1
          IF        @EQUAL
            IF        OBASTAT = 4
              ADD       ONE,XXNEWSEE
              ADD       ONE,XXTOTSEE
            ELSE
              ADD       ONE,XXNEWDNA
              ADD       ONE,XXTOTDNA
            ENDIF
          ELSE
            IF        OBASTAT = 4
              ADD       ONE,XXFUPSEE
              ADD       ONE,XXTOTSEE
            ELSE
              ADD       ONE,XXFUPDNA
              ADD       ONE,XXTOTDNA
            ENDIF
          ENDIF
.
          MOVE      SP1,OBBBOOK
          PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1
.
          MATCH     ANSN,OBBBOOK
          IF        @EQUAL
            ADD       ONE,XXNBOOKA
          ELSE
            MATCH     ANSS,OBBBOOK
            IF        @EQUAL
              ADD       ONE,XXNBOOKA
            ENDIF
          ENDIF
          MOVE      ZERO,FORM1
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P14:24,*EL,*V2LON,CPCDATE,SP2,OBASTRT,SP2,OBACLIN
.
          CALL      UPXXOAH1
.
          GOTO      EXTR2000
.
EXTR9000  MOVE      ZERO,SFORM1
          MOVE      FORM1,SFORM1
          CALL      ECAN0000                * Extract cancellation data
          ADD       SFORM1,FORM1
.          IF        FORM1 = 2
.            DISPLAY   *P1:24,*B,*EL,"No data found. "; 
.            CALL      HITENTER
.            MOVE      ONE,EXIT
.          ENDIF
        
EXTR9999  RETURN
+
.*****************************************************************************
.*                           CLER0000                                        *
.*                        Clear temporary file variables                     *
.*****************************************************************************
.
CLER0000  MOVE      ZERO,XXSESSH   
          MOVE      ZERO,XXSESSC   
          MOVE      ZERO,XXNEWSEE  
          MOVE      ZERO,XXNEWDNA  
          MOVE      ZERO,XXFUPSEE  
          MOVE      ZERO,XXFUPDNA  
          MOVE      ZERO,XXTOTSEE  
          MOVE      ZERO,XXTOTDNA  
          MOVE      ZERO,XXNBOOKA 
.
CLER9999  RETURN
+
.*****************************************************************************
.*                           ECAN0000                                        *
.*                    Extract the number of cancelled sessions               *
.*****************************************************************************
.
ECAN0000  CALL      CTPB0000                * re-create 2nd tempfile
          MOVE      ONE,FORM1
          PACK      KEY6,SP6
          CALL      RDSSITA1
ECAN1000  CALL      RDKSITA1
          BRANCH    OVRCD,ECAN9999
.
          PACK      CFNAME,OSTFILE,SP20
          CALL      OPOTMA11
          PACK      CFNAME,OSTFILE,FILCSLA2
          OPEN      OUTCSLA2,CFNAME
          PACK      CFNAME,OSTFILE,FILCTYA1
          OPEN      OUTCTYA1,CFNAME
          PACK      CFNAME,OSTFILE,FILCLIA1
          OPEN      OUTCLIA1,CFNAME
.
          PACK      KEY27,OSTSITE,STRTDATE,SP30
          CALL      RSOTCSL2
ECAN2000  CALL      RKOTCSL2
          BRANCH    OVRCD,ECAN1000
.
          MATCH     OSTSITE,OTCSSITE
          GOTO      ECAN1000 IF NOT EQUAL
.
          MATCH     OTCSDATE,ENDDATE
          GOTO      ECAN1000 IF LESS
.
          UNPACK    OTCSDATE,CCENT,CYEAR,CMON,CDAY
          CALL      DATECONV
          BRANCH    EXIT,ECAN2000
.
          PACK      KEY18,OTCSSITE,OTCSCLIN,WEEKDAY,OTCSSTRT
          CALL      RDMASA1
          BRANCH    OVRCD,ECAN2000
.
          MATCH     SHSP,OTMAHOSP
          GOTO      ECAN2000 IF LESS
.
          MATCH     OTMAHOSP,EHSP
          GOTO      ECAN2000 IF LESS
.
          UNPACK    OTCSDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P14:24,*EL,*V2LON,CPCDATE,SP2,OTCSSTRT,SP2,OTCSCLIN
.
.-------- Check if session has already been cancelled --------
.
          UNPACK    OTCSSTRT,XBSHR
          PACK      KEY31,OTMAHOSP,OTCSSITE,OTMACLSE,OTCSCTYP,OTCSDATE,XBSHR
          CALL      RDTEMPB
          IF        OVRCD = 1
            UNPACK    KEY31,XBHOSP,XBSITE,XBSESS,XBCTYPE,XBSDATE,XBSHR
            CALL      WRTEMPB
          ELSE
            GOTO      ECAN9000              * same session as before
          ENDIF
.
          MOVE      OMATYP,XXSPEC
          MOVE      OTMACLSE,XXCONS
          PACK      KEY12,XXSPEC,XXCONS      
          CALL      RDXXOAH1
          IF        OVRCD = 1
            CALL      CLER0000
            ADD       ONE,XXSESSC
            CALL      GDES0000              * get description
            CALL      WRXXOAH1
          ELSE
            ADD       ONE,XXSESSC
            CALL      UPXXOAH1
          ENDIF
.
ECAN9000  MOVE      ZERO,FORM1
          GOTO      ECAN2000
.
ECAN9999  RETURN
.
.*****************************************************************************
.*                           GDES0000                                        *
.*                        Get the consultant description                     *
.*****************************************************************************
.
GDES0000  UNPACK    SP20,XXCONSD
.
          MOVE      SP30,OCTDESC
          PACK      KEY12,OTOSSITE,XXSPEC
          CALL      RDCTYA1
          MOVE      OCTDESC,XXSPECD
.
          PACK      KEY20,OTOSSITE,XXCONS,OTOSDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OTOSSITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     XXCONS,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
          BRANCH    OVRCD,GDES9999
.
          MATCH     SP6,OCADOCT
          IF        !@EQUAL
            CALL      FRMT0000
            GOTO      GDES9999
          ENDIF 
.
          MOVE      OCADESC,XXCONSD
GDES9999  RETURN
.
.*****************************************************************************
.*                           PRNT0000                                        *
.*                        Print the report                                   *
.*****************************************************************************
PRNT0000  MOVE      ZERO,CPAGENO
          MOVE      ZERO,CNOUNDLN
          MOVE      "60",CLNO
.
          MOVE      ZERO,GTSESSH
          MOVE      ZERO,GTSESSC
          MOVE      ZERO,GTNEWSEE
          MOVE      ZERO,GTNEWDNA
          MOVE      ZERO,GTFUPSEE
          MOVE      ZERO,GTFUPDNA
          MOVE      ZERO,GTTOTSEE
          MOVE      ZERO,GTTOTDNA
          MOVE      ZERO,GTNBOOKA
.
          DISPLAY   *P1:24,*EL,"Printing....."
.
          UNPACK    SP20,KEY12
          CALL      RSXXOAH1
PRNT1000  CALL      RKXXOAH1
          BRANCH    OVRCD,PRNT9000
.
          IF        CLNO > 58 
            CALL      HEAD0000        
            MOVE      SP6,SAVSPEC
          ENDIF
.
          CALL      CALC0000
.
          MATCH     XXSPEC,SAVSPEC
          IF        @EQUAL
            PRINT     *1,"|",*24,"|",*26,XXCONS,*33,XXCONSD:
                      *49,"|",*51,XXSESSH,*57,XXSESSC,*62,"|",*64,XXNEWSEE:
                      *70,XXNEWDNA,*75,"|",*77,XXFUPSEE,*82,XXFUPDNA:
                      *87,"|",*89,XXTOTSEE,*96,XXTOTDNA,*102,"|":
                      *104,PERCDNA,*112,"|",*115,XXNBOOKA,*124,PERCWALK,*132,"|"
            ADD       ONE,CLNO
          ELSE
            PRINT   *1,"|",*3,XXSPEC,*10,XXSPECD,*24,"|",*26,XXCONS,*33,XXCONSD:
                      *49,"|",*51,XXSESSH,*57,XXSESSC,*62,"|",*64,XXNEWSEE:
                      *70,XXNEWDNA,*75,"|",*77,XXFUPSEE,*82,XXFUPDNA:
                      *87,"|",*89,XXTOTSEE,*96,XXTOTDNA,*102,"|":
                      *104,PERCDNA,*112,"|",*115,XXNBOOKA,*124,PERCWALK,*132,"|"
            ADD       ONE,CLNO
            MOVE      XXSPEC,SAVSPEC
          ENDIF
.
. Accumulate Grand Totals
.
          ADD       XXSESSH,GTSESSH
          ADD       XXSESSC,GTSESSC
          ADD       XXNEWSEE,GTNEWSEE
          ADD       XXNEWDNA,GTNEWDNA
          ADD       XXFUPSEE,GTFUPSEE
          ADD       XXFUPDNA,GTFUPDNA
          ADD       XXTOTSEE,GTTOTSEE
          ADD       XXTOTDNA,GTTOTDNA
          ADD       XXNBOOKA,GTNBOOKA
.
          GOTO      PRNT1000
.
PRNT9000  IF        CLNO > 58 
            CALL      HEAD0000        
          ENDIF
.
. Print Grand Totals
.
          MOVE      GTSESSH,XXSESSH
          MOVE      GTSESSC,XXSESSC
          MOVE      GTNEWSEE,XXNEWSEE
          MOVE      GTNEWDNA,XXNEWDNA
          MOVE      GTFUPSEE,XXFUPSEE
          MOVE      GTFUPDNA,XXFUPDNA
          MOVE      GTTOTSEE,XXTOTSEE
          MOVE      GTTOTDNA,XXTOTDNA
          MOVE      GTNBOOKA,XXNBOOKA
.
          CALL      CALC0000
.
          CALL      UND132
          PRINT     *1,"| Grand Total":
                     *49,"|",*51,XXSESSH,*57,XXSESSC,*62,"|",*64,XXNEWSEE:
                     *70,XXNEWDNA,*75,"|",*77,XXFUPSEE,*82,XXFUPDNA:
                     *87,"|",*89,XXTOTSEE,*96,XXTOTDNA,*102,"|":
                     *104,PERCDNA,*112,"|",*115,XXNBOOKA,*124,PERCWALK,*132,"|"
.
          CALL      UND132
          PRINT     *N:
                    "***** End of Report *****"

PRNT9999  RETURN
+
.*****************************************************************************
.*                           CALC0000                                        *
.*                     Calculate percentages to print                        *
.*****************************************************************************
CALC0000  MOVE      ZERO,GRANDTOT
          MOVE      ZERO,PERCDNA
          MOVE      ZERO,PERCWALK
          
          ASSIGN    (XXTOTSEE + XXTOTDNA),GRANDTOT
          IF        GRANDTOT > 0
            ASSIGN    ((XXTOTDNA / GRANDTOT) * 100),PERCDNA
          ENDIF
          IF        XXTOTSEE > 0
            ASSIGN    ((XXNBOOKA / XXTOTSEE) *100),PERCWALK
          ENDIF
.
CALC9999  RETURN
+
.*****************************************************************************
.*                           FRMT0000                                        *
.*                     Format all details to print                           *
.*****************************************************************************
.
FRMT0000  MOVE      ZERO,EXIT
.
          MOVE      SP20,XXCONSD
          PACK      KEY6,OCADOCT
          CALL      RDDOCT1
          BRANCH    OVRCD,FRMT9999
.
          MOVE      DTITL,PACTITLE
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,XXCONSD
.
FRMT9999  RETURN
.
.*****************************************************************************
.*                           HEAD0000                                        *
.*                        Print the report heading                           *
.*****************************************************************************
.
HEAD0000  
          CALL      HEAD132
.
          PRINT     *N,*40,"Period    from : ",SDOL:
                    *N,*40,"            to : ",EDOL
          PRINT     *N,*40,"Hospitals from : ",SHSPDESC:
                    *N,*40,"            to : ",EHSPDESC
          PRINT     " "
.
          CALL      UND132
          PRINT     *1,"|",*24,"|":
                    *49,"|",*51,"-SESSIONS-",*62,"|",*64,"-- NEW --",*75,"|":
                    *77,"FOLLOW-UP",*87,"|",*89,"-- TOTAL --",*102,"|":
                    *104,"% DNA",*112,"|",*114,"--- WALK-INS ---",*132,"|"
          PRINT     *1,"|",*3,"SPECIALITY",*24,"|",*26,"CONSULTANT":
                    *49,"|",*51,"Held  Canc",*62,"|",*64,"Seen  DNA",*75,"|":
                    *77,"Seen  DNA",*87,"|",*89,"Seen   DNA",*102,"|":
                    *104,"of Total",*112,"|",*114,"Number      %",*132,"|"

          CALL      UND132
.
          ADD       NINE,CLNO  
.
HEAD9999  RETURN
+
.*****************************************************************************
.*                           CTPA0000                                        *
.*                        Create the 1st Tempfile                            *
.*****************************************************************************
CTPA0000  CALL      DTPA0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    CFNAMEA,CMDLINE
          APPEND    KEYA,CMDLINE
          APPEND    SP30,SP30,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      TMPFILEA,CFNAMEA
.
CTPA9999  RETURN
+
.*****************************************************************************
.*                           CTPB0000                                        *
.*                        Create the 2nd Tempfile                            *
.*****************************************************************************
CTPB0000  CALL      DTPB0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    CFNAMEB,CMDLINE
          APPEND    KEYB,CMDLINE
          APPEND    SP30,SP30,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      TMPFILEB,CFNAMEB
.
CTPB9999  RETURN
+
.*****************************************************************************
.*                           DTPA0000                                        *
.*                        Kill the 1st Tempfile                              *
.*****************************************************************************
DTPA0000  CLOSE     TMPFILEA
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    CFNAMEA,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
DTPA9999  RETURN
+
.*****************************************************************************
.*                           DTPB0000                                        *
.*                        Kill the 2nd Tempfile                              *
.*****************************************************************************
DTPB0000  CLOSE     TMPFILEB
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    CFNAMEB,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
DTPB9999  RETURN
+
. ==========================
. Temporary file IO routines
. ==========================
RDXXOAH1  RESET     KEY12
          MOVE      ZERO,OVRCD
          READ      TMPFILEA,KEY12;XXSPEC,XXCONS,XXSESSH,XXSESSC,XXNEWSEE:
                                   XXNEWDNA,XXFUPSEE,XXFUPDNA,XXTOTSEE,XXTOTDNA:
                                   XXNBOOKA,XXSPECD,XXCONSD
          GOTO      OVERCOND IF OVER
          RETURN
+
RSXXOAH1  RESET     KEY12
          READ      TMPFILEA,KEY12;;         
          RETURN
+
RKXXOAH1  MOVE      ZERO,OVRCD
          READKS    TMPFILEA;XXSPEC,XXCONS,XXSESSH,XXSESSC,XXNEWSEE:
                             XXNEWDNA,XXFUPSEE,XXFUPDNA,XXTOTSEE,XXTOTDNA:
                             XXNBOOKA,XXSPECD,XXCONSD
          GOTO      OVERCOND IF OVER
          RETURN
+
RPXXOAH1  MOVE      ZERO,OVRCD
          READKP    TMPFILEA;XXSPEC,XXCONS,XXSESSH,XXSESSC,XXNEWSEE:
                             XXNEWDNA,XXFUPSEE,XXFUPDNA,XXTOTSEE,XXTOTDNA:
                             XXNBOOKA,XXSPECD,XXCONSD
          GOTO      OVERCOND IF OVER
          RETURN
+
WRXXOAH1  RESET     KEY12
          WRITE     TMPFILEA,KEY12;XXSPEC,XXCONS,XXSESSH,XXSESSC,XXNEWSEE:
                             XXNEWDNA,XXFUPSEE,XXFUPDNA,XXTOTSEE,XXTOTDNA:
                             XXNBOOKA,XXSPECD,XXCONSD
          RETURN
+
UPXXOAH1  UPDATE    TMPFILEA;XXSPEC,XXCONS,XXSESSH,XXSESSC,XXNEWSEE:
                              XXNEWDNA,XXFUPSEE,XXFUPDNA,XXTOTSEE,XXTOTDNA:
                              XXNBOOKA,XXSPECD,XXCONSD
          RETURN
+
RDTEMPB   MOVE      ZERO,OVRCD
          READ      TMPFILEB,KEY31;XBHOSP,XBSITE,XBSESS,XBCTYPE,XBSDATE,XBSHR
          GOTO      OVERCOND IF OVER
          RETURN
+
WRTEMPB   WRITE     TMPFILEB,KEY31;XBHOSP,XBSITE,XBSESS,XBCTYPE,XBSDATE,XBSHR
          RETURN
+
.*****************************************************************************
.*                            I/O Section                                    *
.*****************************************************************************
.
          INC       STD001IO
          INC       IBASEQIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATHSPIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTOSFIO/INC
          INC       OUTSITIO/INC
          INC       OUTCTYIO/INC
          INC       OUTCLIIO/INC
          INC       OUTMA1IO/INC
          INC       OUTCSLIO/INC
          INC       WEBERRIO/INC
.
          INC       PATHSPKY
          INC       TFILENAM
          INC       DATECONV
................................................................................

.*********************************************************************
.* System        :  Operating Rooms                                  *
.* Program       :  IBAOPR62.PBL                                     *
.* Name          :  Clinic Activity Analysis                         *
.***********************************************************************
.* Date          :  07/01/1991                                         *
.* Author        :  Justin Coates                                      *
.* Function      :  To display the utilization statistics              *
.*                  for each clinic/surgeon                            *
.* Modifications :                                                     *
.*        V10.04.01 15/04/2014  J.Tan          CAR 261630              *
.*                  Removed port number from temp file name            *
.*        V9.07.01  20/09/2006 Peter Vela CAR 118228                   *
.*                  Added Reallocated Sessions to Report               *
.* *********************************************************************
.*                  06/02/91  ROD                                      *
.*                  Print Booked sessions & Total booked time for      *
.*                  Extra sessions.                                    *
.*                  11/02/91  ROD                                      *
.*                  Fixed Single clinic/butcher display bug            *
.*        V5.01.02  06/11/91  D.Di.Paola      SRF 111638               * 
.*                  Fixed to use correct time booked for "extra".      *
.*        V5.01.03  07/11/91  D.Di.Paola      SRF 111638               * 
.*                  Fixed to use correct total varriables for "EXTRA"  *
.*        V5.01.04  18/06/92  ROD                                      *   
.*                  Print/Display date for period                      *
.*        V5.01.05  21/07/92  ROD                                      *   
.*                  Print cancelled session message at end of report   *
.*        V5.01.06  17/11/92 J.Tan    SRF 118759                       *
.*                  Fixed to print total of surgeon with booked only   *
.*        V5.01.07  29/12/92 Graeme Williams      SRF 119688           *
.*                  Changed comment at the end of the report           *
.*        V5.01.08  07/06/1993  Glenn Berry      SRF 120247            *
.*                  Added Report Layout 5                              *
.*                  (same as layout 2 with multiple surgeons per page) *
.*        V5.01.09  23/08/1993  Gabrielle        SRF 120247            *
.*                  Fixed layout 4 & 5 to check that there are enough  *
.*                  lines left to print a table and the table info.    *
.*        V5.01.10  24/05/1995  Matt                                   *
.*                  Fixed for Global Recompile.                        *
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.***********************************************************************
.
.$$$$$
.         IBAOPR62.PBL        Clinic/Surgeon Activity Analysis
.         ============        ================================
.         INIT0000 - Initialization
.         OPTN0000 - Enter which way to do the report
.         KPER0000 - Enter the period range
.          OPTION 1 -> CLSU0000 - Enter the clinic/surgeon code
.          OPTION 2 -> KGRP0000 - Enter a range of groups
.         REPT0000 - Produce the report
.             LCOU0000 - loop thru OPRCOUFD and write to temp files
.             LCSU0000 - loop thru OPRCSUFD and write to temp files
.             LCSC0000 - loop thru OPRCSCFD and write to temp files
.             loop through required temp file to produce the report
.$$$$$
.
. ------- FD includes needed -------
.
          INC       STD001FD                * standard variables
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       OPRCLIFD/INC            * clinic file
          INC       OPRCONFD/INC            * control file
          INC       OPRCOUFD/INC            * clinic operation usage file
          INC       OPRCSCFD/INC            * clinic session cancellation file
          INC       OPRCSUFD/INC            * clinic session usage file
.
          INC       PATCODFD/INC            * codes file
          INC       PATCONFD/INC            * control file
          INC       PATDO1FD/INC            * doctors file
          INC       PATDRGFD/INC            * date range file
.
. ------- program variables -------
.
CFNAMEA   DIM       8         * file name for temp file A
CFNAMEB   DIM       8         * file name for temp file B
CFNAMEC   DIM       8         * file name for temp file C
CFNAMED   DIM       8         * file name for temp file D
.kkk
.
CLSUCODE  DIM       6         * clinic/surgeon code entered
CLSUDESC  DIM       40        * clinic/surgeon code description
CMDLINE   DIM       50        * command line for 'execute' command
CODE      DIM       2         * category for PATDSCOD
CPTDATE   DIM       8         * for PATCOMN3
CURRCLSU  DIM       6         * current clinic/surgeon printing details for
CURRDATE  DIM       8         * current date ccyymmdd
CURRTYPE  DIM       1         * the current type cancel/delay/overtime
.
DIM3      DIM       3         * for patdsdoc
DIM7A     DIM       7         * starting period as mm/ccyy
DIM7B     DIM       7         * ending period   as mm/ccyy
DIM9      DIM       9         * description for the code type
DIM20A    DIM       20        * starting period description
DIM20B    DIM       20        * ending period   description
DIM20C    DIM       20        * starting group description
DIM20D    DIM       20        * ending group   description
.
DOCCODE   DIM       6         * Doctor Code
ENCODE    DIM       3         * entered code for KCOD0000
FORM6A    FORM      6         * counts total for booked and extra
FORM6B    FORM      6         * counts total for each code type
FORM6P5   FORM      6.5       * used in percentage calculation
.
GROPSTRT  DIM       3         * starting group code
GROPSTOP  DIM       3         * ending   group code
.
.
PCENTA    FORM      3.2       * percentage value
PCENTB    FORM      3.2       * percentage value
PCENTC    FORM      3.2       * percentage value
PRFDATE   DIM       10
PRTDATE   DIM       10
.
PERDSTRT  DIM       6         * starting period
PERDSTOP  DIM       6         * ending period
SAVEEND   DIM       6         * ending period (financial)
SAVEMON1  DIM       2         * start period financial month
SAVEMON2  DIM       2         * end period financial month
SAVEYR1   DIM       4         * start period financial year
SAVEYR2   DIM       4         * end period financial year
SCRNFLAG  FORM      1         * ? performed flag
STOPLDAT  DIM       8         * ending   period from date
STOPHDAT  DIM       8         * ending   period end  date
STRTLDAT  DIM       8         * starting period from date
STRTHDAT  DIM       8         * starting period end  date
.
TLNBOOK   FORM      6         * total Number of sessions booked
TLTBOOK   FORM      6         * total Time booked for sessions
TLNUSED   FORM      6         * total Number of sessions used
TLTUSED   FORM      6         * total Time used for sessions
TLNCASE   FORM      6         * total Number of cases seen
TLNOPER   FORM      6         * total Number of operations performed
TLPCASE   FORM      6         * total Number of private cases seen
TLPTIME   FORM      6         * total Time used for private patients
TLOCASE   FORM      6         * total Number of other users
TLOTIME   FORM      6         * total Time used for other users
TLSHARE   FORM      6         * total time shared
.
TLOTYPE   FORM      6         * total Operations            Report type 2 only
TLTTYPE   FORM      6         * total time for operations   Report type 2 only
.
TLNCANC   FORM      6         * total Sessions cancelled    Report Type 4 only
TLNDLAY   FORM      6         * total Sessions delayed      Report Type 4 only
TLNOVER   FORM      6         * total Sessions overtime     Report Type 4 only
.
.         temp file A
.
TEMPA     IFILE     SQL, FIXED=52, KEY="U1-6,7-7"
.
.Name     Type      Length Phys Start  Description
.-------  ----      ------ ---- -----  ---------------------------------------
ACLIN     DIM       6        6     1   Clinic/Surgeon code
.                                        "zzzzzz" = Grand totals
ABFLAG    DIM       1        1     7   Booked/Extra flag
.                                        "B" = Booked
.                                        "E" = Extra
ANBOOK    FORM      6        4     8   Number of sessions booked
ATBOOK    FORM      6        4    12   Time booked for sessions
ANUSED    FORM      6        4    16   Number of sessions used
ATUSED    FORM      6        4    20   Time used for sessions
ANCASE    FORM      6        4    24   Number of cases seen
ANOPER    FORM      6        4    28   Number of operations performed
APCASE    FORM      6        4    32   Number of private cases seen
APTIME    FORM      6        4    36   Time used for private patients
AOCASE    FORM      6        4    40   Number of other users
AOTIME    FORM      6        4    44   Time used for other users
ASHARE    FORM      6        4    48   Shared time
.                                 52   End of Record
.
.         temp file B
.
TEMPB     IFILE     SQL, FIXED=19, KEY="U1-6,7-7,8-10"
.
.Name     Type      Length Phys Start  Description
.-------  ----      ------ ---- -----  ---------------------------------------
BCLIN     DIM       6        6     1   Clinic/Surgeon code
.                                        "zzzzzz" = Grand totals
BTYPE     DIM       1        1     7   Type of Code
.                                        "1" = Cancellation
.                                        "2" = Delay sessions
.                                        "3" = Overtime sessions
BCODE     DIM       3        3     8   Code (Cancel/delay/overtime)
BBOOK     FORM      6        4    11   Times code used for booked sessions
BEXTRA    FORM      6        4    15   Times code used for extra sessions
.                                 19   End of Record
.
.         temp file C
.
TEMPC     IFILE     SQL, FIXED=19, KEY="U1-6,7-7,8-10"
.
.Name     Type      Length Phys Start  Description
.-------  ----      ------ ---- -----  ---------------------------------------
CCLIN     DIM       6        6     1   Clinic/Surgeon code
.                                        "zzzzzz" = Grand totals
CBFLAG    DIM       1        1     7   Booked/Extra flag
.                                        "B" = Booked
.                                        "E" = Extra
COTYPE    DIM       3        3     8   Operation Type
CNOPER    FORM      6        4    11   Number of operations performed
CTUSED    FORM      6        4    15   Time in theatre for operations
.                                 19   End of Record
.
.         temp file D
.
TEMPD     IFILE     SQL, FIXED=48, KEY="U1-6,7-7"
.
.Name     Type      Length Phys Start  Description
.-------  ----      ------ ---- -----  ---------------------------------------
DCLIN     DIM       6        6     1   Clinic/Surgeon code
                                         "zzzzzz" = Grand totals
DBFLAG    DIM       1        1     7   Booked/Extra flag
                                         "B" = Booked
                                         "E" = Extra
DNBOOK    FORM      6        4     8   Number of sessions booked
DTBOOK    FORM      6        4    12   Time booked for sessions
DNUSED    FORM      6        4    16   Number of sessions used
DTUSED    FORM      6        4    20   Time used for sessions
DNCASE    FORM      6        4    24   Number of cases seen
DNOPER    FORM      6        4    28   Number of operations performed
DNCANC    FORM      6        4    32   Number of sessions cancelled
DNDLAY    FORM      6        4    36   Number of sessions delayed
DNOVER    FORM      6        4    40   Number of sessions overtime
DSHARE    FORM      6        4    44   Total time shared
.                                 48   End of Record
. ------- program constants -------
.
CATDT     INIT      "DT"      * doctor group
CATOC     INIT      "OC"      * cancelled sessions
CATOD     INIT      "OD"      * delayed sessions
CATOO     INIT      "OO"      * overtime sessions
CATYG     INIT      "YG"      * clinic group
.
CANCEL    INIT      "Cancelled"
DELAY     INIT      "Delayed  "
.
INDEXA    INIT      " 52 U1-6,7-7"
INDEXB    INIT      " 19 U1-6,7-7,8-10"
INDEXC    INIT      " 19 U1-6,7-7,8-10"
INDEXD    INIT      " 48 U1-6,7-7"
.
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
OVER      INIT      "Overtime "
ZED6      INIT      "zzzzzz"
.
PRGID     INIT      "IBAOPR62"
PRGNAM    DIM       50
PRGNAM1   INIT      " Activity Analysis"
VERSION   INIT      "V12.01.00"
+
.*********************************************************************
.*                  ML0000                                           *
.*        Mainline Code / Controlling Logic                          *
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000                * get the option type
          BRANCH    EXIT,ML9999             * exit selected
.
ML2000    CALL      KPER0000                * enter the period range
          BRANCH    EXIT,ML1000             * exit selected
.
          BRANCH    MOPTION,ML3100,ML3200
.
.         enter either clinic or surgeon code
.
ML3100    CALL      CLSU0000                * enter clinic/surgeon code
          BRANCH    EXIT,ML2000             * nothing entered
          GOTO      ML4000
.
.         enter a range of groups
.
ML3200    CALL      KGRP0000                * enter the range of groups
.
.         OK to Continue (Y/N/C)
.
ML4000    CALL      CONTQST
          BRANCH    CEXIT,ML5000,ML2000,ML1000
.
.         produce the report
.
ML5000    CALL      REPT0000                * produce the report
          GOTO      ML1000 
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialization and open files                              *
.*********************************************************************
INIT0000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,FORTY;*96,OPCNCDSC,*111,OPCNCLSU
.
          MOVE      OPCNCDSC,PRGNAM
          ENDSET    PRGNAM
.
INIT0100  MATCH     SP1,PRGNAM
          GOTO      INIT0200 IF NOT EQUAL   * have a letter
.
          BUMP      PRGNAM,-1
          GOTO      INIT0100 IF NOT EOS
.
INIT0200  APPEND    PRGNAM1,PRGNAM
          RESET     PRGNAM
.          REP       UPPLOW,PRGNAM
          CALL      DISPHEAD                * program header
          DISPLAY   *P1:3,*EF               * clear the screen
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE           * current date
          DISPLAY   *P1:3,*EF               * clear the screen
          MOVE      ONE,CNEWDS              * new ? mark option
          MOVE      ONE,CDEFDTE             * once to accept default
          MOVE      ONE,CNOUNDLN            * no underlines
          MOVE      ZERO,CHIGHLT            * use highlighting
.
          DISPLAY   *P50:24,"Opening "
          READ      CONTROLF,FORTY2;*54,OPCNR62A
          READ      CONTROLF,FIFTY9;*27,CPERMTH
.
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"     * codes file
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"     * doctor file
          OPEN      PATDO1A2,"patdo1af"     * doctor file
          DISPLAY   *P58:24,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"     * date range file
          DISPLAY   *P58:24,"oprcliaf"
          OPEN      OPRCLIA1,"oprcliaf"     * clinic file
          OPEN      OPRCLIA2,"oprcliaf"     * clinic file
          DISPLAY   *P58:24,"oprcouaf"
          OPEN      OPRCOUA1,"oprcouaf"     * clinic operation usage file
          DISPLAY   *P58:24,"oprcsuaf"
          OPEN      OPRCSUA1,"oprcsuaf"     * clinic session usage file
          DISPLAY   *P58:24,"oprcscaf"
          OPEN      OPRCSCA1,"oprcscaf"     * clinic session cancellation file
.
.         remove trailing blanks 
.
          ENDSET    CPERMTH
.
INIT9000  CMATCH    SP1,CPERMTH
          GOTO      INIT9010 IF NOT EQUAL   * found a letter
.
          BUMP      CPERMTH,-1
          GOTO      INIT9000 IF NOT EOS     * more letters to go
.
INIT9010  LENSET    CPERMTH                 * set logical length
          RESET     CPERMTH
.
          ENDSET    OPCNCDSC
.
INIT9020  CMATCH    SP1,OPCNCDSC
          GOTO      INIT9030 IF NOT EQUAL   * found a letter
.
          BUMP      OPCNCDSC,-1
          GOTO      INIT9020 IF NOT EOS     * more letters to go
.
INIT9030  LENSET    OPCNCDSC                * set logical length
          RESET     OPCNCDSC
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,CFNAMEA
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,CFNAMEB
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,CFNAMEC
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,CFNAMED
.
INIT9999  RETURN
+
.*********************************************************************
.*                  OPTN0000                    Called by : ML1000   *
.*        See if to produce report                                   *
.*        Returns : EXIT = 1      exit chosen                        *
.*                  MOPTION = 1   single clinic/surgeon              *
.*                  MOPTION = 2   range of clinics/surgeons          *
.*********************************************************************
OPTN0000  MOVE      PRGNAM,KEY60
          CALL      DISPLIN2
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*P1:5,ONE,*P1:6,TWO,*HOFF:
                    *P3:4,"= Exit":
                    *P3:5,"= Single ",*+,OPCNCDSC:
                    *P3:6,"= Range of ",*+,OPCNCDSC," Groups":
                    *P1:8,"Select Option : "
.
OPTN1000  KEYIN     *P17:8,*DV,UNDLN1:
                    *P17:8,*V2LON,MOPTION:
                    *P17:8,*DV,MOPTION
.
          BRANCH    MOPTION,OPTN8100,OPTN8200
.
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9000 IF EQUAL       * exit selected
.
          BEEP
          GOTO      OPTN1000
.
OPTN8100  DISPLAY   *P59:2,*V2LON," Single ",*+,OPCNCDSC,SP1;
          GOTO      OPTN8900
.
OPTN8200  DISPLAY   *P59:2,*V2LON,SP1,*+,OPCNCDSC," Groups ";
.
OPTN8900  MOVE      FALSE,EXIT              * valid answer
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT               * exit selected
.
OPTN9999  RETURN
+
.*********************************************************************
.*                  KPER0000                    Called by : ML1500   *
.*        Enter the period ranges                                    *
.*        Returns : EXIT = 1      exit chosen                        * 
.*                  PERDSTRT      starting period                    *
.*                  PERDSTOP      ending period                      *
.*********************************************************************
KPER0000  DISPLAY   *P1:9,*EF:
                    *P5:10,"Starting ",*+,CPERMTH,*P21:10,":":
                    *P5:11,"Ending ",*+,CPERMTH,*P21:11,":"
.
.         enter starting period
.
KPER1000  MOVE      "10",CVERT              * row
          MOVE      "23",CCOL               * column
          UNPACK    SP4,CYEAR,CMON          * no defaults
          MOVE      CCC,CCENT
.
          CALL      KEYPER  
          BRANCH    OVRCD,KPER9000          * nothing entered
          BRANCH    CQUEST,KPER1000         * ? entered
.
          PACK      PERDSTRT,ICENT,IYEAR,IMON
          REP       " 0",PERDSTRT
.
          MOVE      PERDSTRT,KEY6           * period is the key
          CALL      RDDRGA3                 * read date range file
          BRANCH    OVRCD,KPER1500          * invalid period
.
          PACK      DIM7A,IMON,SLASH,ICENT,IYEAR
          REP       " 0",DIM7A
          MOVE      DRGLDSC,DIM20A          * long description
          MOVE      DRGFDTE,STRTLDAT        * from date of period
          MOVE      DRGTDTE,STRTHDAT        * to date of period
          MOVE      DRGYR,SAVEYR1           * save financial yr
          MOVE      DRGNUM,SAVEMON1         * save financial month
.
          MATCH     STRTHDAT,CURRDATE
          GOTO      KPER2000 IF NOT LESS    * to date is <= current date
.
          DISPLAY   *P1:24,*B,"Date must not be after current date.  ",*EL;
          CALL      HITENTER 
          GOTO      KPER1000
.
KPER1500  DISPLAY   *P1:24,*B,*+,CPERMTH," not on file.  ",*EL;
          CALL      HITENTER 
          GOTO      KPER1000
.
.         enter ending period (default to starting period)
.
KPER2000  UNPACK    STRTLDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P37:10,DIM20A,SP3,CPCDATE          * display description
          MOVE      CPCDATE,PRFDATE
          MOVE      "11",CVERT              * row
          MOVE      "23",CCOL               * column
          UNPACK    PERDSTRT,CCENT,CYEAR,CMON
.
          CALL      KEYPER 
          BRANCH    OVRCD,KPER2000          * nothing entered
          BRANCH    CQUEST,KPER2000         * ? entered
.
          PACK      PERDSTOP,ICENT,IYEAR,IMON
          REP       " 0",PERDSTOP           * set up end period
.
          MOVE      PERDSTOP,KEY6           * ending period as key
          CALL      RDDRGA3                 * read date range file
          BRANCH    OVRCD,KPER2500          * invalid period
.
          PACK      DIM7B,IMON,SLASH,ICENT,IYEAR
          REP       " 0",DIM7B
          MOVE      DRGLDSC,DIM20B          * long description
          MOVE      DRGFDTE,STOPLDAT        * from date of period
          MOVE      DRGTDTE,STOPHDAT        * to date of period
          MOVE      DRGYR,SAVEYR2           * save financial yr
          MOVE      DRGNUM,SAVEMON2         * save financial month
          PACK      SAVEEND,DRGYR,DRGNUM
.
          MATCH     PERDSTRT,PERDSTOP
          GOTO      KPER3000 IF NOT LESS    * ending period is >= start period
.
          DISPLAY   *P1:24,*B,"Ending ",*+,CPERMTH," must be after Starting ":
                    *+,CPERMTH,".  ",*EL;
          CALL      HITENTER 
          GOTO      KPER2000
.
KPER2500  DISPLAY   *P1:24,*B,*+,CPERMTH," not on file.  ",*EL;
          CALL      HITENTER 
          GOTO      KPER2000
.
.         check end date of end period is before current date
.
KPER3000  MATCH     STOPHDAT,CURRDATE
          GOTO      KPER8000 IF NOT LESS    * date is <= current date
.
          DISPLAY   *P1:24,*B,"Date must not be after current date.  ",*EL;
          CALL      HITENTER 
          GOTO      KPER2000
.
KPER8000  UNPACK    STOPHDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P37:11,DIM20B,SP3,CPCDATE          * display description
          MOVE      CPCDATE,PRTDATE
          MOVE      FALSE,EXIT              * valid dates
          GOTO      KPER9999
.
KPER9000  MOVE      TRUE,EXIT               * exit selected
.
KPER9999  RETURN
+
.*********************************************************************
.*                  CLSU0000                    Called by : ML3100   *
.*        Enter either the clinic or surgeon code                    *
.*        Returns : EXIT = 1      nothing entered                    * 
.*                  CLSUCODE      the code                           *
.*                  CLSUDESC      the description                    *
.*********************************************************************
CLSU0000  MOVE      ONE,SCRNFLAG            * set redisplay flag
          MOVE      SP6,CLSUCODE            * clear code
          PACK      CLSUDESC,SP20,SP20      * clear desc
          UNPACK    SP30,GROPSTRT,DIM20C    * clear starting vars
          UNPACK    SP30,GROPSTOP,DIM20D    * clear ending vars
          MOVE      "23",CCOL               * column
          MOVE      "13",CVERT              * row
          DISPLAY   *P5:13,*+,OPCNCDSC,*P21:13,":",*EL;
.
CLSU1000  KEYIN     *PCCOL:CVERT,*DV,UNDLN6,*EL:
                    *PCCOL:CVERT,*V2LON,CLSUCODE:
                    *PCCOL:CVERT,*DV,CLSUCODE
.
          ENDSET    CLSUCODE
          APPEND    SP6,CLSUCODE
          RESET     CLSUCODE
.
          MATCH     SP6,CLSUCODE
          GOTO      CLSU4000 IF EQUAL       * nothing entered
.
          MATCH     QUEST,CLSUCODE
          GOTO      CLSU2000 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          BRANCH    OPCNCLSU,CLSU1100       * using doctors
.
          CALL      OPRCLIDS                * ? for clinics
          GOTO      CLSU1200
.
CLSU1100  CALL      GETSCR00  
          MOVE      ONE,SCRNFLAG
          MOVE      SP10,CLSUCODE
          CALL      PATDOCDS
          CALL      PUTSCR00                      * restore screen
          BRANCH    EXIT,CLSU4000
          MOVE      DOCCODE,CLSUCODE
          GOTO      CLSU2000 
.
CLSU1200  DISPLAY   *P1:24,"Enter the code : ",*EL;
          MOVE      ZERO,SCRNFLAG
          MOVE      "18",CCOL
          MOVE      "24",CVERT
          GOTO      CLSU1000
.
.         validate what was entered
.
CLSU2000  MOVE      CLSUCODE,KEY6           * set up key
          BRANCH    OPCNCLSU,CLSU2100       * read doctors file
.
.         using a clinic code
.
          CALL      RDOPCLI1
          BRANCH    OVRCD,CLSU3000          * invalid code
.
          MATCH     SP6,OPCLDOCT
          GOTO      CLSU2200 IF EQUAL       * no doctor code
.
          MOVE      OPCLDOCT,KEY6           * use the doctor code as key
.
.         using a doctor code
.
CLSU2100  CALL      RDDOCT1
          BRANCH    OVRCD,CLSU3000          * invalid code
.
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,OPCLDESC       * set up name
.
CLSU2200  MOVE      OPCLDESC,CLSUDESC       * set up description
          MOVE      FALSE,EXIT              * valid item entered
          GOTO      CLSU5000                * check for redisplay
.
.         ERROR : Invalid code entered
.
CLSU3000  DISPLAY   *P1:24,*B,"Invalid ",*+,OPCNCDSC," code.  ",*EL;
          CALL      HITENTER 
          BRANCH    SCRNFLAG,CLSU1000
.
          DISPLAY   *P1:24,"Enter the code : ",*EL;
          GOTO      CLSU1000
.
.         nothing entered so exit
.
CLSU4000  MOVE      TRUE,EXIT               * indicate nothing entered
.
.         check for redisplay
.
CLSU5000  BRANCH    SCRNFLAG,CLSU8000       * no redisplay needed
.
          CALL      SCRN0000                * redisplay screen
          GOTO      CLSU9999
.
.         no redisplay needed
.
CLSU8000  BRANCH    EXIT,CLSU9999           * nothing entered
.
          DISPLAY   *P37:13,CLSUDESC        * display description
.
CLSU9999  RETURN
+
.*********************************************************************
.*                  KGRP0000                    Called by : ML3200   *
.*        Enter the range of groups                                  *
.*        Returns : GROPSTRT      starting group code                *
.*                  GROPSTOP      ending   group code                *
.********************************************************************* 
KGRP0000  MOVE      CATYG,CODE              * default to clinic
          LOAD      CODE,OPCNCLSU,CATDT     * if doctor codes
.
          DISPLAY   *P5:13,"Starting Group  : ",*EL:
                    *P5:14,"Ending Group    : ",*EL
.
          UNPACK    SP30,GROPSTRT,DIM20C    * clear starting vars
          UNPACK    SP30,GROPSTOP,DIM20D    * clear ending vars
          UNPACK    SP30,CLSUCODE,CLSUDESC  * clear clinic/surgeon data
.
KGRP1000  CALL      FGRP0000                * enter from group
KGRP2000  CALL      TGRP0000                * enter to grooup
.
          MATCH     "zzz",GROPSTOP
          GOTO      KGRP9999 IF EQUAL       * valid range
.
          MATCH     SP3,GROPSTRT
          GOTO      KGRP9999 IF EQUAL       * valid range
.
          MATCH     GROPSTRT,GROPSTOP
          GOTO      KGRP9999 IF NOT LESS    * valid code range
.
          DISPLAY   *P1:24,*B,"End group must be after Start group.  ",*EL;
          CALL      HITENTER 
          GOTO      KGRP2000
.
KGRP9999  RETURN
+
.*********************************************************************
.*                  FGRP0000                    Called by : KGRP1000 *
.*        Enter the starting code                                    *
.*        Returns : GROPSTRT      the starting code                  * 
.*                  DIM20C        the starting code description      * 
.*********************************************************************
FGRP0000  MOVE      "23",CCOL               * Column for keyin
          MOVE      "13",CVERT              * Line for keyin
          MOVE      "***",GROPSTRT          * set the start code
          MOVE      SP20,DIM20C             * clear the field
.
          CALL      KCOD0000                * Keyin the code
.
          MOVE      ENCODE,GROPSTRT         * the code
          MOVE      TDESC,DIM20C            * the description
.
          MATCH     SP3,ENCODE
          GOTO      FGRP8000 IF NOT EQUAL   * something entered
.
          DISPLAY   *P23:13,*V2LON,"Start",*EL;
          GOTO      FGRP9999
.
FGRP8000  DISPLAY   *P23:13,*V2LON,GROPSTRT,*HOFF,*P37:13,DIM20C
.
FGRP9999  RETURN
+
.*********************************************************************
.*                  TGRP0000                    Called by : KGRP2000 *
.*        Enter the ending code                                      *
.*        Returns : GROPSTOP      the ending code                    * 
.*                  DIM20D        the ending code description        * 
.*********************************************************************
TGRP0000  MOVE      "23",CCOL               * Column for keyin
          MOVE      "14",CVERT              * Line for keyin
          MOVE      SP3,GROPSTOP            * set the start code
          MOVE      SP20,DIM20D             * clear the field
.
          CALL      KCOD0000                * Keyin the code
.
          MOVE      ENCODE,GROPSTOP         * the code
          MOVE      TDESC,DIM20D            * the description
.
          MATCH     SP3,ENCODE
          GOTO      TGRP8000 IF NOT EQUAL   * something entered
.
          DISPLAY   *P23:14,*V2LON,"Finish",*EL;
          MOVE      "zzz",GROPSTOP
          GOTO      TGRP9999
.
TGRP8000  DISPLAY   *P23:14,*V2LON,GROPSTOP,*HOFF,*P37:14,DIM20D
.
TGRP9999  RETURN
+
.********************************************************************
.*                  KCOD0000                   Called by : FGRP0000 *
.*        Keyin a codes file entry                         TGRP0000 *
.*        Para's  : CCOL          column for input                  *
.*                  CVERT         row for input                     *
.*                  CODE          codes file category for item      *
.*        Returns : TDESC         the description                   *
.*                  ENCODE        the entered code                  *
.********************************************************************
KCOD0000  MOVE      ONE,SCRNFLAG            * set ? as not performed
.
KCOD1000  KEYIN     *PCCOL:CVERT,*DV,UNDLN3,*EL:
                    *PCCOL:CVERT,*V2LON,ENCODE:
                    *PCCOL:CVERT,*DV,ENCODE;
.
.         check what was entered
.
          ENDSET    ENCODE
          APPEND    SP3,ENCODE
          RESET     ENCODE
.
          MATCH     SP3,ENCODE 
          GOTO      KCOD7900 IF EQUAL       * nothing entered
.
          MATCH     QUEST,ENCODE
          GOTO      KCOD2000 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          CALL      PATCODDS                * ? routine
          MOVE      TWENTY4,CVERT           * row for input
          MOVE      TEN8,CCOL               * column for input
          MOVE      ZERO,SCRNFLAG           * set ? as performed
          DISPLAY   *P1:24,"Input the code : ",*EL
          GOTO      KCOD1000
.
.         validate what was entered
.
KCOD2000  PACK      KEY5,CODE,ENCODE        * key of entered value
          CALL      RDCODE1                 * read code file
          BRANCH    OVRCD,KCOD7000          * invalid input
.
          GOTO      KCOD8000                * valid input
.
.         ERROR: invalid code entered
.
KCOD7000  DISPLAY   *P1:24,*EL,*B,"Invalid code.  ",*EL;
          CALL      HITENTER
.
          BRANCH    SCRNFLAG,KCOD1000       * ? not entered
.
          DISPLAY   *P1:24,"Input the code : ",*EL
          GOTO      KCOD1000    
.
.         nothing was entered
.
KCOD7900  MOVE      SP20,TDESC              * clear the description
          MOVE      SP3,ENCODE              * clear the code
.
.         valid code entered
.
KCOD8000  MOVE      FALSE,EXIT              * valid input
          BRANCH    SCRNFLAG,KCOD9999       * ? not entered
.
          CALL      SCRN0000                * redisplay rest of screen
.
KCOD9999  RETURN
+
.********************************************************************* 
.*                  SCRN0000                    Called by : KCOD8000 *
.*        Redisplay Screen after a ? mark                            *
.*********************************************************************
SCRN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*P1:5,ONE,*P1:6,TWO,*HOFF:
                    *P3:4,"= Exit":
                    *P3:5,"= Single ",*+,OPCNCDSC:
                    *P3:6,"= Range of ",*+,OPCNCDSC," Groups":
                    *P1:8,"Select Option : ",*V2LON,MOPTION,*HOFF:
                    *P5:10,"Starting ",CPERMTH,*P21:10,": ",*V2LON,DIM7A,*HOFF:
                    *P37:10,DIM20A:
                    *P5:11,"Ending ",CPERMTH,*P21:11,": ",*V2LON,DIM7B,*HOFF:
                    *P37:11,DIM20B;
.
          BRANCH    MOPTION,SCRN1000,SCRN2000
.
SCRN1000  DISPLAY   *P5:13,*+,OPCNCDSC,*P21:13,": ",*V2LON,CLSUCODE,*HOFF:
                    *P37:13,CLSUDESC
          GOTO      SCRN9999
.
SCRN2000  DISPLAY   *P5:13,"Starting Group  : ",*V2LON,GROPSTRT,*HOFF:
                    *P37:13,DIM20C:
                    *P5:14,"Ending Group    : ",*V2LON,GROPSTOP,*HOFF:
                    *P37:14,DIM20D
.
          MATCH     SP3,GROPSTRT
          GOTO      SCRN9999 IF NOT EQUAL   * no value to display
.
          DISPLAY   *P23:13,*V2LON,"Start",*EL;
.
SCRN9999  RETURN
+
.*********************************************************************
.*                  REPT0000                    Called by : ML4000   *
.*        Produce the report for the required data                   *
.*********************************************************************
REPT0000  DISPLAY   *P1:24,*EL,"Producing the report now - ",*V2LON,*BLINKON:
                    "Please wait";
          MOVE      ZERO,CPAGENO            * initialize page counter
          MOVE      SP6,CURRCLSU            * clear current clinic/surg
          CALL      CTMP0000                * create temp files
.
.         obtain the information and write to temp files
.
REPT1000  CALL      LCOU0000                * loop through OPRCOUFD
          CALL      LCSU0000                * loop through OPRCSUFD
          CALL      LCSC0000                * loop through OPRCSCFD
.
.         loop through the temp files and produce the report
.
          BRANCH    OPCNR62A,REPT2000,REPT3000,REPT4000,REPT5000,REPT6000
          GOTO      REPT9999
.
. ------- report layout 1 -------
.
REPT2000  MOVE      SP7,KEY7
          DISPLAY   *P50:24,*EL,"Processing - "
.
REPT2050  READ      TEMPA,KEY7;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
.
REPT2100  READKS    TEMPA;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
          GOTO      REPT8000 IF OVER        * end of file
.
          DISPLAY   *P63:24,*V2LON,ACLIN
          MOVE      ACLIN,CURRCLSU          * set the current clinic
          CALL      PAGE0000                * print page header
          CALL      PTPA0000                * print temp file A
          CALL      PTPB0000                * print temp file B
          PACK      KEY7,CURRCLSU,ANSZ      * position at last item for current
          GOTO      REPT2050                * positional read
.
. ------- report layout 2 -------
.
REPT3000  MOVE      SP7,KEY7
          DISPLAY   *P50:24,*EL,"Processing - "
.
REPT3050  READ      TEMPA,KEY7;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
.
REPT3100  READKS    TEMPA;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
          GOTO      REPT8000 IF OVER        * end of file
.
          DISPLAY   *P63:24,*V2LON,ACLIN
          MOVE      ACLIN,CURRCLSU          * set the current clinic
          CALL      PAGE0000                * print page header
          CALL      PTAP0000                * print temp file A
          CALL      PTPB0000                * print temp file B
          PACK      KEY7,CURRCLSU,ANSZ      * position at last item for current
          GOTO      REPT3050                * positional read
.
. ------- report layout 3 -------
.
REPT4000  MOVE      SP7,KEY7
          DISPLAY   *P50:24,*EL,"Processing - "
          CALL      PAGE0000                * print the page heading
.
REPT4050  READ      TEMPA,KEY7;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
.
REPT4100  READKS    TEMPA;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
          GOTO      REPT8000 IF OVER        * end of file
.
          DISPLAY   *P63:24,*V2LON,ACLIN
          MOVE      ACLIN,CURRCLSU          * set the current clinic
.
          COMPARE   "55",CLNO
          GOTO      REPT4200 IF LESS        * just print table
.
          CALL      PAGE0000                * print new page
          GOTO      REPT4300
.
REPT4200  CALL      TBLC0000                * print table
.
REPT4300  CALL      PTPA0000                * print temp file A
          PACK      KEY7,CURRCLSU,ANSZ      * position at last item for current
          GOTO      REPT4050
.
. ------- report layout 4 -------
.
REPT5000  MOVE      SP7,KEY7
          DISPLAY   *P50:24,*EL,"Processing - "
          CALL      PAGE0000                * print the page heading
.
REPT5050  READ      TEMPD,KEY7;DCLIN,DBFLAG,DNBOOK,DTBOOK,DNUSED,DTUSED:
                    DNCASE,DNOPER,DNCANC,DNDLAY,DNOVER,DSHARE
.
REPT5100  READKS    TEMPD;DCLIN,DBFLAG,DNBOOK,DTBOOK,DNUSED,DTUSED:
                    DNCASE,DNOPER,DNCANC,DNDLAY,DNOVER,DSHARE
          GOTO      REPT8000 IF OVER        * end of file
.
          DISPLAY   *P63:24,*V2LON,DCLIN
          MOVE      DCLIN,CURRCLSU          * set the current clinic
.
          COMPARE   "55",CLNO
          GOTO      REPT5200 IF LESS        * just print table
.
REPT5110  CALL      PAGE0000                * print new page
          GOTO      REPT5300
.
REPT5200  ADD       "7",CLNO                * New table fit & not just heading ?
          COMPARE   "55",CLNO                 
          GOTO      REPT5110 IF NOT LESS    * No. Print new page
          SUB       "7",CLNO 
          CALL      TBLD0000                * Yes. Print table
.
REPT5300  CALL      PTPD0000                * print temp file D
          PACK      KEY7,CURRCLSU,ANSZ      * position at last item for current
          GOTO      REPT5050                * positional read
.
. ------- report layout 5 -------
.
REPT6000  MOVE      SP7,KEY7
          DISPLAY   *P50:24,*EL,"Processing - "
          CALL      PAGE0000                * print page heading
.
REPT6050  READ      TEMPA,KEY7;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
.
REPT6100  READKS    TEMPA;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
          GOTO      REPT8000 IF OVER        * end of file
.
          DISPLAY   *P63:24,*V2LON,ACLIN
          MOVE      ACLIN,CURRCLSU          * set the current clinic
.
          COMPARE   "55",CLNO
          GOTO      REPT6200 IF LESS        
.
REPT6110  CALL      PAGE0000
          GOTO      REPT6300
.
REPT6200  ADD       "7",CLNO                * New table fit & not just heading ?
          COMPARE   "55",CLNO
          GOTO      REPT6110 IF NOT LESS    * No. Print new page
          SUB       "7",CLNO 
          CALL      TBLE0000                * Yes. Print Table
.
REPT6300  CALL      PTAP0000                * print temp file A
          CALL      PTPB0000                * print temp file B
          PACK      KEY7,CURRCLSU,ANSZ      * position at last item for current
          GOTO      REPT6050                * positional read
.
.
.         end of report section
.
REPT8000  CALL      SUMM0000                * summary details
          CALL      DTMP0000                * delete temp files
.
REPT9999  RETURN
+
.********************************************************************* 
.*                  CTMP0000                    Called by : REPT0000 *
.*        Create the required temp files for the report              *
.*********************************************************************
CTMP0000  CALL      DTMP0000                * delete temp file
.
          BRANCH    OPCNR62A,CTMP1000,CTMP1000,CTMP1000,CTMP4000,CTMP1000
          GOTO      CTMP9999
.
.         temp file for report layout 1 and 2 and 3 and 5
.
CTMP1000  CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,CFNAMEA,INDEXA
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * create temp file
          OPEN      TEMPA,CFNAMEA           * open temp file
.
          BRANCH    OPCNR62A,CTMP2000,CTMP2000,CTMP9999,CTMP9999,CTMP2000
.
.         temp file for report layout 1 and 2 and 5
.
CTMP2000  CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,CFNAMEB,INDEXB
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * create temp file index 1
          OPEN      TEMPB,CFNAMEB           * open temp file
.
          BRANCH    OPCNR62A,CTMP9999,CTMP3000,CTMP9999,CTMP9999,CTMP3000
.
.         temp file for report layout 2 and 5
.
CTMP3000  CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,CFNAMEC,INDEXC
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * create temp file
          OPEN      TEMPC,CFNAMEC           * open temp file
          GOTO      CTMP9999
.
.         create temp file for layout 4
.
CTMP4000  CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,CFNAMED,INDEXD
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * create temp file
          OPEN      TEMPD,CFNAMED           * open temp file
.
CTMP9999  RETURN
+
.*********************************************************************
.*                  DTMP0000                    Called by : CTMP0000 *
.*        Delete the temp files used for the report         REPT0000 *
.********************************************************************* 
DTMP0000  BRANCH    OPCNR62A,DTMP1000,DTMP1000,DTMP1000,DTMP4000,DTMP1000
          GOTO      DTMP9999
.
.         delete temp files for report layout 1 and 2 and 3 and 5
.         delete temp file A for layout 1 and 2 and 3 and 5
.
DTMP1000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO          * call overcond if doesn't exist
          OPEN      TEMPA,CFNAMEA           * open tempfile
          TRAPCLR   IO                      * clear the IO trap
          BRANCH    OVRCD,DTMP1500          * no file to delete
.
          CLOSE     TEMPA                   * close temp file
          CLEAR     CMDLINE
          PACK      CMDLINE,ISERASE,CFNAMEA
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
DTMP1500  BRANCH    OPCNR62A,DTMP2000,DTMP2000,DTMP9999,DTMP9999,DTMP2000
.
.         delete temp file B for layout 1 and 2 and 5
.
DTMP2000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO          * call overcond if doesn't exist
          OPEN      TEMPB,CFNAMEB           * open tempfile
          TRAPCLR   IO                      * clear the IO trap
          BRANCH    OVRCD,DTMP2500          * no file to delete
.
          CLOSE     TEMPB                   * close temp file 
          CLEAR     CMDLINE
          PACK      CMDLINE,ISERASE,CFNAMEB
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
DTMP2500  BRANCH    OPCNR62A,DTMP9999,DTMP3000,DTMP9999,DTMP9999,DTMP3000
.
.         delete temp file C for layout 2 and 5
.
DTMP3000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO          * call overcond if doesn't exist
          OPEN      TEMPC,CFNAMEC           * open tempfile
          TRAPCLR   IO                      * clear the IO trap
          BRANCH    OVRCD,DTMP9999          * no file to delete
.
          CLOSE     TEMPC                   * close temp file 
          CLEAR     CMDLINE
          PACK      CMDLINE,ISERASE,CFNAMEC
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          GOTO      DTMP9999
.
.         delete temp file D for layout 4
.
DTMP4000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO          * call overcond if doesn't exist
          OPEN      TEMPD,CFNAMED           * open tempfile
          TRAPCLR   IO                      * clear the IO trap
          BRANCH    OVRCD,DTMP9999          * no file to delete
.
          CLOSE     TEMPD                   * close temp file 
          CLEAR     CMDLINE
          PACK      CMDLINE,ISERASE,CFNAMED
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
DTMP9999  RETURN
+
.*********************************************************************
.*                  LCOU0000                    Called by : REPT1000 *
.*        Loop through OPRCOUFD and write to temp files              *
.*********************************************************************
LCOU0000  DISPLAY   *P50:24,"Searching OPRCOUFD",*EL;
          PACK      KEY21,SAVEYR1,SAVEMON1,CLSUCODE,SP9
          CALL      RSOPCOU1                * positional read
.
LCOU1000  CALL      RKOPCOU1                * next read
          BRANCH    OVRCD,LCOU9999          * finished
.
          MATCH     OPCODATE,SAVEEND
          GOTO      LCOU9999 IF LESS        * out of range
.
.         do required validation according to the report order, MOPTION
.
          BRANCH    MOPTION,LCOU1100,LCOU1200
.
.         option 1 - have a single clinic/surgeon
.
LCOU1100  MATCH     OPCODOCT,CLSUCODE
          GOTO      LCOU1000 IF NOT EQUAL   * incorrect clinic/doctor
.
          GOTO      LCOU2000                * valid clinic/surgeon
.
.         option 2 - have a range of codes
.
LCOU1200  MOVE      OPCODOCT,KEY6           * set up doctor code
          CALL      VGRP0000                * validate the group type
          BRANCH    EXIT,LCOU1000           * invalid group type
.
.         have a valid item of data, update required temp file
.
LCOU2000  BRANCH    OPCNR62A,LCOU3000,LCOU4000,LCOU3000,LCOU6000,LCOU4000
          GOTO      LCOU9999
.
. -------------------------------------
. ------- report layout 1 and 3 -------
. -------------------------------------
.
LCOU3000  PACK      KEY7,OPCODOCT,OPCOTYPE
.
LCOU3010  UNPACK    KEY7,ACLIN,ABFLAG
          MOVE      ZERO,ANBOOK             * number of sessions booked
          MOVE      ZERO,ATBOOK             * time booked for sessions
          MOVE      ZERO,ANUSED             * number of sessions used
          MOVE      ZERO,ATUSED             * time used for sessions
          MOVE      ZERO,ANCASE             * number of cases seen
          MOVE      ZERO,ANOPER             * number of operations performed
          MOVE      ZERO,APCASE             * number of private cases seen
          MOVE      ZERO,APTIME             * time used for private cases
          MOVE      ZERO,AOCASE             * number of other users
          MOVE      ZERO,AOTIME             * time used for other users
.
          MOVE      ZERO,OVRCD
          RESET     KEY7
          READ      TEMPA,KEY7;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
          CALL      OVERCOND IF OVER        * if OVRCD = 1, write a record
.
          ADD       OPCONOPR,ANOPER         * number of operations
          ADD       OPCONCAS,ANCASE         * number of cases
.
          COMPARE   "2",OPCOATYP
          GOTO      LCOU3100 IF NOT EQUAL   * not a private patient
.
          ADD       OPCONCAS,APCASE         * private cases
          ADD       OPCOTIME,APTIME         * time for private cases
.
LCOU3100  COMPARE   TWO,OPCOOWN
          GOTO      LCOU3200 IF NOT EQUAL   * the same oper. surg
.
          ADD       OPCONCAS,AOCASE         * other cases
          ADD       OPCOTIME,AOTIME         * time for other cases
.
.         have update values, now see if to write or to update temp file
.
LCOU3200  BRANCH    OVRCD,LCOU3300          * write a record
.
          UPDATE    TEMPA;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
          GOTO      LCOU3400
.
LCOU3300  RESET     KEY7
          WRITE     TEMPA,KEY7;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
.
.         now update the grand totals if it hasnt been done
.
LCOU3400  MATCH     ZED6,KEY7
          GOTO      LCOU1000 IF EQUAL       * get next clinic/surgeon
.
.         set clinic to 'zzzzzz' for the grand totals
.
          PACK      KEY7,ZED6,OPCOTYPE
          GOTO      LCOU3010                * update the grand totals
.
. -------------------------------------
. ------- report layout 2 and 5 -------
. -------------------------------------
.
LCOU4000  PACK      KEY7,OPCODOCT,OPCOTYPE
.
LCOU4010  UNPACK    KEY7,ACLIN,ABFLAG
          MOVE      ZERO,ANBOOK             * number of sessions booked
          MOVE      ZERO,ATBOOK             * time booked for sessions
          MOVE      ZERO,ANUSED             * number of sessions used
          MOVE      ZERO,ATUSED             * time used for sessions
          MOVE      ZERO,ANCASE             * number of cases seen
          MOVE      ZERO,ANOPER             * number of operations performed
          MOVE      ZERO,APCASE             * number of private cases seen
          MOVE      ZERO,APTIME             * time used for private cases
          MOVE      ZERO,AOCASE             * number of other users
          MOVE      ZERO,AOTIME             * time used for other users
.
          MOVE      ZERO,OVRCD
          RESET     KEY7
          READ      TEMPA,KEY7;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
          CALL      OVERCOND IF OVER        * if OVRCD = 1, write a record
.
          ADD       OPCONOPR,ANOPER         * number of operations
          ADD       OPCONCAS,ANCASE         * number of cases
.
.         have update values, now see if to write or to update temp file
.
          BRANCH    OVRCD,LCOU4100          * write a record
.
          UPDATE    TEMPA;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
          GOTO      LCOU4200
.
LCOU4100  RESET     KEY7
          WRITE     TEMPA,KEY7;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
.
.         now update the grand totals if it hasnt been done
.
LCOU4200  MATCH     ZED6,KEY7
          GOTO      LCOU4500 IF EQUAL       * update other temp file now
.
.         set clinic to 'zzzzzz' for the grand totals
.
          PACK      KEY7,ZED6,OPCOTYPE
          GOTO      LCOU4010                * update the grand totals
.
. ------- update temp file C -------
.
LCOU4500  MATCH     SP3,OPCOOTYP
          GOTO      LCOU4530 IF NOT EQUAL   * have an operation type
.
          MOVE      "***",OPCOOTYP          * set type to non-blank
.
LCOU4530  PACK      KEY10,OPCODOCT,OPCOTYPE,OPCOOTYP
.
LCOU4560  UNPACK    KEY10,CCLIN,CBFLAG,COTYPE
          MOVE      ZERO,CNOPER             * clear number of operations
          MOVE      ZERO,CTUSED             * clear time in theatre
.
          MOVE      ZERO,OVRCD
          RESET     KEY10
          READ      TEMPC,KEY10;CCLIN,CBFLAG,COTYPE,CNOPER,CTUSED
          CALL      OVERCOND IF OVER        * if OVRCD = 1, write a record
.
          ADD       OPCONOPR,CNOPER         * add number of operations
          ADD       OPCOTIME,CTUSED         * add to theatre time used
.
.         have update values, now see if to write or to update temp file
.
          BRANCH    OVRCD,LCOU4600          * write a record
.
          UPDATE    TEMPC;CCLIN,CBFLAG,COTYPE,CNOPER,CTUSED
          GOTO      LCOU4700
.
LCOU4600  RESET     KEY10
          WRITE     TEMPC,KEY10;CCLIN,CBFLAG,COTYPE,CNOPER,CTUSED
.
.         now update the grand totals if it hasnt been done
.
LCOU4700  MATCH     ZED6,KEY10
          GOTO      LCOU1000 IF EQUAL       * get next clinic/surgeon
.
.         set clinic to 'zzzzzz' for the grand totals
.
          PACK      KEY10,ZED6,OPCOTYPE,OPCOOTYP
          GOTO      LCOU4560                * update the grand totals
.
. -------------------------------
. ------- report layout 4 -------
. -------------------------------
.
LCOU6000  PACK      KEY7,OPCODOCT,OPCOTYPE
.
LCOU6010  UNPACK    KEY7,DCLIN,DBFLAG
          MOVE      ZERO,DNBOOK             * number of sessions booked
          MOVE      ZERO,DTBOOK             * time booked for sessions
          MOVE      ZERO,DNUSED             * number of sessions used
          MOVE      ZERO,DTUSED             * time used for sessions
          MOVE      ZERO,DNCASE             * number of cases seen
          MOVE      ZERO,DNOPER             * number of operations performed
          MOVE      ZERO,DNCANC             * number of sessions cancelled 
          MOVE      ZERO,DNDLAY             * number of sessions delayed 
          MOVE      ZERO,DNOVER             * number of sessions overtime 
          MOVE      ZERO,DSHARE             * total time shared
.
          MOVE      ZERO,OVRCD
          RESET     KEY7
          READ      TEMPD,KEY7;DCLIN,DBFLAG,DNBOOK,DTBOOK,DNUSED,DTUSED:
                    DNCASE,DNOPER,DNCANC,DNDLAY,DNOVER,DSHARE
          CALL      OVERCOND IF OVER        * if OVRCD = 1, write a record
.
          ADD       OPCONOPR,DNOPER         * number of operations
          ADD       OPCONCAS,DNCASE         * number of cases
.
.         have update values, now see if to write or to update temp file
.
LCOU6200  BRANCH    OVRCD,LCOU6300          * write a record
.
          UPDATE    TEMPD;DCLIN,DBFLAG,DNBOOK,DTBOOK,DNUSED,DTUSED:
                    DNCASE,DNOPER,DNCANC,DNDLAY,DNOVER,DSHARE
          GOTO      LCOU6400
.
LCOU6300  RESET     KEY7
          WRITE     TEMPD,KEY7;DCLIN,DBFLAG,DNBOOK,DTBOOK,DNUSED,DTUSED:
                    DNCASE,DNOPER,DNCANC,DNDLAY,DNOVER,DSHARE
.
.         now update the grand totals if it hasnt been done
.
LCOU6400  MATCH     ZED6,KEY7
          GOTO      LCOU1000 IF EQUAL       * get next clinic/surgeon
.
.         set clinic to 'zzzzzz' for the grand totals
.
          PACK      KEY7,ZED6,OPCOTYPE
          GOTO      LCOU6010                * update the grand totals
.
LCOU9999  RETURN
+
.*********************************************************************
.*                  LCSU0000                    Called by : REPT1000 *
.*        Loop through OPRCSUFD and write to temp files              *
.*********************************************************************
LCSU0000  DISPLAY   *P50:24,"Searching OPRCSUFD",*EL;
          PACK      KEY16,SAVEYR1,SAVEMON1,CLSUCODE,SP4
          CALL      RSOPCSU1                * positional read
.
LCSU1000  CALL      RKOPCSU1                * next read
          BRANCH    OVRCD,LCSU9999          * finished
.
          MATCH     OPCSDATE,SAVEEND
          GOTO      LCSU9999 IF LESS        * out of range
.
.         do required validation according to the report order, MOPTION
.
          BRANCH    MOPTION,LCSU1100,LCSU1200
.
.         option 1 - have a single clinic/surgeon
.
LCSU1100  MATCH     OPCSDOCT,CLSUCODE
          GOTO      LCSU1000 IF NOT EQUAL   * incorrect clinic/doctor
.
          GOTO      LCSU2000                * valid clinic/surgeon
.
.         option 2 - have a range of codes
.
LCSU1200  MOVE      OPCSDOCT,KEY6           * set up doctor code
          CALL      VGRP0000                * validate the group type
          BRANCH    EXIT,LCSU1000           * invalid group type
.
.         have a valid item of data, update required temp file
.
LCSU2000  BRANCH    OPCNR62A,LCSU3000,LCSU3000,LCSU3000,LCSU6000,LCSU3000
          GOTO      LCSU9999
.
. -------------------------------------------
. ------- report layout 1, 2, 3 and 5 -------
. -------------------------------------------
.
LCSU3000  PACK      KEY7,OPCSDOCT,OPCSTYPE
.
LCSU3010  UNPACK    KEY7,ACLIN,ABFLAG
          MOVE      ZERO,ANBOOK             * number of sessions booked
          MOVE      ZERO,ATBOOK             * time booked for sessions
          MOVE      ZERO,ANUSED             * number of sessions used
          MOVE      ZERO,ATUSED             * time used for sessions
          MOVE      ZERO,ANCASE             * number of cases seen
          MOVE      ZERO,ANOPER             * number of operations performed
          MOVE      ZERO,APCASE             * number of private cases seen
          MOVE      ZERO,APTIME             * time used for private cases
          MOVE      ZERO,AOCASE             * number of other users
          MOVE      ZERO,AOTIME             * time used for other users
          MOVE      ZERO,ASHARE             * time shared
.
          MOVE      ZERO,OVRCD
          RESET     KEY7
          READ      TEMPA,KEY7;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
          CALL      OVERCOND IF OVER        * if OVRCD = 1, write a record
.
          ADD       OPCSBSES,ANBOOK         * number of booked sessions
          ADD       OPCSBTIM,ATBOOK         * time for booked
          ADD       OPCSUSES,ANUSED         * number of used sessions
          ADD       OPCSUTIM,ATUSED         * time for used
          ADD       OPCSSTIM,ASHARE         * time shared
.
.         have update values, now see if to write or to update temp file
.
LCSU3200  BRANCH    OVRCD,LCSU3300          * write a record
.
          UPDATE    TEMPA;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
          GOTO      LCSU3400
.
LCSU3300  RESET     KEY7
          WRITE     TEMPA,KEY7;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
.
.         now update the grand totals if it hasnt been done
.
LCSU3400  MATCH     ZED6,KEY7
          GOTO      LCSU1000 IF EQUAL       * have finished this clinic/surg
.
.         set clinic to 'zzzzzz' for the grand totals
.
          PACK      KEY7,ZED6,OPCSTYPE
          GOTO      LCSU3010                * update the grand totals
.
. -------------------------------
. ------- report layout 4 -------
. -------------------------------
.
LCSU6000  PACK      KEY7,OPCSDOCT,OPCSTYPE
.
LCSU6010  UNPACK    KEY7,DCLIN,DBFLAG
          MOVE      ZERO,DNBOOK             * number of sessions booked
          MOVE      ZERO,DTBOOK             * time booked for sessions
          MOVE      ZERO,DNUSED             * number of sessions used
          MOVE      ZERO,DTUSED             * time used for sessions
          MOVE      ZERO,DNCASE             * number of cases seen
          MOVE      ZERO,DNOPER             * number of operations performed
          MOVE      ZERO,DNCANC             * number of sessions cancelled 
          MOVE      ZERO,DNDLAY             * number of sessions delayed 
          MOVE      ZERO,DNOVER             * number of sessions overtime 
          MOVE      ZERO,DSHARE             * total time shared
.
          MOVE      ZERO,OVRCD
          RESET     KEY7
          READ      TEMPD,KEY7;DCLIN,DBFLAG,DNBOOK,DTBOOK,DNUSED,DTUSED:
                    DNCASE,DNOPER,DNCANC,DNDLAY,DNOVER,DSHARE
          CALL      OVERCOND IF OVER        * if OVRCD = 1, write a record
.
          ADD       OPCSBSES,DNBOOK         * number of booked sessions
          ADD       OPCSBTIM,DTBOOK         * time for booked
          ADD       OPCSUSES,DNUSED         * number of used sessions
          ADD       OPCSUTIM,DTUSED         * time for used
          ADD       OPCSSTIM,DSHARE         * shared time
.
.         have update values, now see if to write or to update temp file
.
LCSU6200  BRANCH    OVRCD,LCSU6300          * write a record
.
          UPDATE    TEMPD;DCLIN,DBFLAG,DNBOOK,DTBOOK,DNUSED,DTUSED:
                    DNCASE,DNOPER,DNCANC,DNDLAY,DNOVER,DSHARE
          GOTO      LCSU6400
.
LCSU6300  RESET     KEY7
          WRITE     TEMPD,KEY7;DCLIN,DBFLAG,DNBOOK,DTBOOK,DNUSED,DTUSED:
                    DNCASE,DNOPER,DNCANC,DNDLAY,DNOVER,DSHARE
.
.         now update the grand totals if it hasnt been done
.
LCSU6400  MATCH     ZED6,KEY7
          GOTO      LCSU1000 IF EQUAL       * have finished this clinic/surg
.
.         set clinic to 'zzzzzz' for the grand totals
.
          PACK      KEY7,ZED6,OPCSTYPE
          GOTO      LCSU6010                * update the grand totals
.
LCSU9999  RETURN
+
.*********************************************************************
.*                  LCSC0000                    Called by : REPT1000 *
.*        Loop through OPRCSCFD and write to temp files              *
.*********************************************************************
LCSC0000  COMPARE   THREE,OPCNR62A
          GOTO      LCSC9999 IF EQUAL       * dont do this for report three
.
          DISPLAY   *P50:24,"Searching OPRCSCFD",*EL;
          PACK      KEY20,SAVEYR1,SAVEMON1,CLSUCODE,SP8
          CALL      RSOPCSC1                * positional read
.
LCSC1000  CALL      RKOPCSC1                * next read
          BRANCH    OVRCD,LCSC9999          * end of data
.
          MATCH     OPCCDATE,SAVEEND
          GOTO      LCSC9999 IF LESS        * out of range
.
.         do required validation according to the report order, MOPTION
.
          BRANCH    MOPTION,LCSC1100,LCSC1200
.
.         option 1 - have a single clinic/surgeon
.
LCSC1100  MATCH     OPCCDOCT,CLSUCODE
          GOTO      LCSC1000 IF NOT EQUAL   * incorrect clinic/doctor
.
          GOTO      LCSC2000                * valid clinic/surgeon
.
.         option 2 - have a range of codes
.
LCSC1200  MOVE      OPCCDOCT,KEY6           * set up doctor code
          CALL      VGRP0000                * validate the group type
          BRANCH    EXIT,LCSC1000           * invalid group type
.
.         have a valid item of data, update required temp file
.
LCSC2000  BRANCH    OPCNR62A,LCSC3000,LCSC3000,LCSC9999,LCSC6000,LCSC3000
          GOTO      LCSC9999
.
. -------------------------------------
. ------- report layout 1, 2 and 5 ----
. -------------------------------------
.
LCSC3000  PACK      KEY10,OPCCDOCT,OPCCCTYP,OPCCCODE
.
LCSC3010  UNPACK    KEY10,BCLIN,BTYPE,BCODE
          MOVE      ZERO,BBOOK              * clear booking amount
          MOVE      ZERO,BEXTRA             * clear extra amount
.
          MOVE      ZERO,OVRCD
          RESET     KEY10
          READ      TEMPB,KEY10;BCLIN,BTYPE,BCODE,BBOOK,BEXTRA
          CALL      OVERCOND IF OVER        * if OVRCD = 1, write a record
.
          MATCH     ANSB,OPCCTYPE
          GOTO      LCSC3100 IF NOT EQUAL   * have an extra session
.
          ADD       OPCCNUMB,BBOOK          * add to booked amount
          GOTO      LCSC3200
.
LCSC3100  ADD       OPCCNUMB,BEXTRA         * add to extra amount
.
.         have update values, now see if to write or to update temp file
.
LCSC3200  BRANCH    OVRCD,LCSC3300          * write a record
.
          UPDATE    TEMPB;BCLIN,BTYPE,BCODE,BBOOK,BEXTRA
          GOTO      LCSC3400
.
LCSC3300  RESET     KEY10
          WRITE     TEMPB,KEY10;BCLIN,BTYPE,BCODE,BBOOK,BEXTRA
.
.         now update the grand totals if it hasnt been done
.
LCSC3400  MATCH     ZED6,KEY10
          GOTO      LCSC1000 IF EQUAL       * have finished this clinic/surg
.
.         set clinic to 'zzzzzz' for the grand totals
.
          PACK      KEY10,ZED6,OPCCCTYP,OPCCCODE
          GOTO      LCSC3010                * update the grand totals
.
. -------------------------------
. ------- report layout 4 -------
. -------------------------------
.
LCSC6000  PACK      KEY7,OPCCDOCT,OPCCTYPE
.
LCSC6010  UNPACK    KEY7,DCLIN,DBFLAG
          MOVE      ZERO,DNBOOK             * number of sessions booked
          MOVE      ZERO,DTBOOK             * time booked for sessions
          MOVE      ZERO,DNUSED             * number of sessions used
          MOVE      ZERO,DTUSED             * time used for sessions
          MOVE      ZERO,DNCASE             * number of cases seen
          MOVE      ZERO,DNOPER             * number of operations performed
          MOVE      ZERO,DNCANC             * number of sessions cancelled 
          MOVE      ZERO,DNDLAY             * number of sessions delayed 
          MOVE      ZERO,DNOVER             * number of sessions overtime 
          MOVE      ZERO,DSHARE             * total time shared
.
          MOVE      ZERO,OVRCD
          RESET     KEY7
          READ      TEMPD,KEY7;DCLIN,DBFLAG,DNBOOK,DTBOOK,DNUSED,DTUSED:
                    DNCASE,DNOPER,DNCANC,DNDLAY,DNOVER,DSHARE
          CALL      OVERCOND IF OVER        * if OVRCD = 1, write a record
.
          MOVE      OPCCCTYP,FORM1          * get as a form
          BRANCH    FORM1,LCSC6100,LCSC6130,LCSC6160
.
LCSC6100  ADD       OPCCNUMB,DNCANC         * add to number cancelled
          GOTO      LCSC6200
.
LCSC6130  ADD       OPCCNUMB,DNDLAY         * add to number delayed
          GOTO      LCSC6200
.
LCSC6160  ADD       OPCCNUMB,DNOVER         * add to number overtime
.
.         have update values, now see if to write or to update temp file
.
LCSC6200  BRANCH    OVRCD,LCSC6300          * write a record
.
          UPDATE    TEMPD;DCLIN,DBFLAG,DNBOOK,DTBOOK,DNUSED,DTUSED:
                    DNCASE,DNOPER,DNCANC,DNDLAY,DNOVER,DSHARE
          GOTO      LCSC6400
.
LCSC6300  RESET     KEY7
          WRITE     TEMPD,KEY7;DCLIN,DBFLAG,DNBOOK,DTBOOK,DNUSED,DTUSED:
                    DNCASE,DNOPER,DNCANC,DNDLAY,DNOVER,DSHARE
.
.         now update the grand totals if it hasnt been done
.
LCSC6400  MATCH     ZED6,KEY7
          GOTO      LCSC1000 IF EQUAL       * have finished this clinic/surg
.
.         set clinic to 'zzzzzz' for the grand totals
.
          PACK      KEY7,ZED6,OPCCTYPE
          GOTO      LCSC6010                * update the grand totals
.
LCSC9999  RETURN
+
.*********************************************************************
.*                  VGRP0000                    Called by : LCOU1200 *
.*        Validate that the clinic/surgeon is in correct    LCSU1200 *
.*        group range                                       LCSC1200 *
.*        Para's  : KEY6          the clinic/surgeon code            *
.*        Returns : EXIT = 1      invalid clinic/surgeon             * 
.********************************************************************* 
VGRP0000  BRANCH    OPCNCLSU,VGRP3000       * using doctors
.
.         read the clinic file
.
          CALL      RDOPCLI1
          BRANCH    OVRCD,VGRP9000          * invalid code
.
          MATCH     SP6,OPCLDOCT
          GOTO      VGRP2900 IF NOT EQUAL   * have a doctor code so check it
.
          MOVE      OPCLCAT,DIM3            * set up the group code
          GOTO      VGRP5000                * check that in required range
.
VGRP2900  MOVE      OPCLDOCT,KEY6           * set up key
.
.         read the doctor file
.
VGRP3000  CALL      RDDOCT1
          BRANCH    OVRCD,VGRP9000          * invalid code
.
          MOVE      DRTYPE,DIM3             * set up the group code
.
VGRP5000  MATCH     GROPSTRT,DIM3
          GOTO      VGRP9000 IF LESS        * value is too low
.
          MATCH     DIM3,GROPSTOP
          GOTO      VGRP9000 IF LESS        * value is too high
.
VGRP8000  MOVE      FALSE,EXIT              * valid code
          GOTO      VGRP9999
.
VGRP9000  MOVE      TRUE,EXIT               * invalid code
.
VGRP9999  RETURN
+
.*********************************************************************
.*                  PAGE0000                    Called by : REPT0000 *
.*        Print the page heading                                     *
.********************************************************************* 
PAGE0000  CALL      HEAD132
          PRINT     *40,"Start Date : ",DIM7A,*65,DIM20A,SP3,PRFDATE:
                    *N,*40,"End   Date : ",DIM7B,*65,DIM20B,SP3,PRTDATE
.
          BRANCH    MOPTION,PAGE1000,PAGE1100
.
PAGE1000  PRINT     *N,*40,OPCNCDSC," : ",CLSUCODE,*65,CLSUDESC
          GOTO      PAGE1200
.
PAGE1100  PRINT     *N,*40,*+,OPCNCDSC," Groups from : ";
.
          MATCH     SP3,GROPSTRT
          GOTO      PAGE1120 IF NOT EQUAL   * have a starting code
.
          PRINT     "Start to ";
          GOTO      PAGE1130
.
PAGE1120  PRINT     GROPSTRT," to ";        * print starting code
.
PAGE1130  MATCH     "zzz",GROPSTOP
          GOTO      PAGE1140 IF NOT EQUAL   * have a ending code
.
          PRINT     "Finish"
          GOTO      PAGE1200
.
PAGE1140  PRINT     GROPSTOP                * print ending code
.
.         set line counter
.
PAGE1200  MOVE      EIGHT,CLNO
.
.         now print the required table headings
.
          BRANCH    OPCNR62A,PAGE2000,PAGE3000,PAGE2000,PAGE4000,PAGE3000
          GOTO      PAGE9999
.
. ------- table heading 1 and 3 -------
.
PAGE2000  MATCH     SP6,CURRCLSU
          GOTO      PAGE9999 IF EQUAL       * no table needed
.
          CALL      GDES0000                * print clinic/surgeon name
          PRINT     *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*":
.
                    *N,"|",*10,"| Number| Total | Number |  Total| Shared|":
                    *60,"| Number | Number |---- Private patients ----|":
                    "------ Other Users -------|":
.
                  *N,"|",*10,"| Booked| Booked|Sessions|   Time| Sessns|  Util":
                    *60,"|   of   |   of   | No. of |   Time |  Util  |":
                    " No. of |   Time |  Util  |":
.
                  *N,"|",*10,"|Session|  Time |  Used  |   Used| Mins. |   %":
                    *60,"|Patients| Oper's |Patients|   Used |   %    |":
                    "Patients|   Used |   %    |":
.
                    *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*";
.
          ADD       SIX,CLNO                * update line counter
          GOTO      PAGE9999
.
. ------- table heading 2 and 5------
.
PAGE3000  MATCH     SP6,CURRCLSU
          GOTO      PAGE9999 IF EQUAL       * no table needed
.
          CALL      GDES0000                * print clinic/surgeon name
          PRINT     *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*":
.
                    *N,"|",*13,"| Number |  Total | Number |  Total |  Total |":
                    *67,"| Number | Number |",*113,"| Number ":
                    "|  Total  |":
.
                    *N,"|",*13,"| Booked | Booked |Sessions|   Time ":
                    "|   Time |  Util":
                    *67,"|   of   |   of   |",*113,"|   of   ":
                    "|  Time   |":
.         
                    *N,"|",*13,"|Sessions|  Time  |  Used  |   Used ":
                    "| Shared |   %":
                    *67,"| Cases  | Proc's | Operation Type ":
                    *113,"| Proc's |  Mins.  |":
.
                    *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*";
.
          ADD       SIX,CLNO                * update line counter
          GOTO      PAGE9999
.
. ------- table heading 4 -------
.
PAGE4000  MATCH     SP6,CURRCLSU
          GOTO      PAGE9999 IF EQUAL       * no table needed
.
          CALL      GDES0000                * print clinic/surgeon name
          PRINT     *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*":
.
                    *N,"|",*27,"| Number |  Total | Number |  Total |  Total |":
                    *81,"| Number | Number |  No. of  |  No. of  ":
                    "|  No. of  |":
.
                    *N,"|",*27,"| Booked | Booked |Sessions|   Time |  Time  ":
                    "|  Util":
                    *81,"|   of   |   of   | Sessions | Sessions |":
                    " Sessions |":
.
                    *N,"|",*27,"|Sessions|  Time  |  Used  |   Used | Shared ":
                    "|   %",*81,"| Cases  | Proc's | Cancelld |  Delayed |":
                    " Overtime |":
.
                    *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*";
.
          ADD       SIX,CLNO                * update line counter
.
PAGE9999  RETURN
+
.********************************************************************* 
.*                  GDES0000                    Called by : REPT0000 *
.*        Get the clinic/surgeon description                         *
.*        Para's  : CURRCLSU      current clinic/surgeon             * 
.*        Returns : OPCLDESC      the name/description               * 
.********************************************************************* 
GDES0000  MOVE      CURRCLSU,KEY6           * set up key
          MOVE      "Invalid Code",OPCLDESC * incase of errors
.
          MATCH     "zzzzzz",CURRCLSU
          GOTO      GDES9000 IF EQUAL       * using grand totals
.
          BRANCH    OPCNCLSU,GDES5000       * using doctors
.
.         using clinics
.
          CALL      RDOPCLI1
          BRANCH    OVRCD,GDES8000          * invalid clinic code
.
          MATCH     SP6,OPCLDOCT
          GOTO      GDES8000 IF EQUAL       * no doctor code
.
          MOVE      OPCLDOCT,KEY6           * use doctor code as key
.
.         using doctors
.
GDES5000  CALL      RDDOCT1
          BRANCH    OVRCD,GDES8000          * invalid code
.
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,OPCLDESC       * set up the name
.
GDES8000  PRINT     *N,OPCNCDSC," : ",CURRCLSU,SP2,OPCLDESC;
          GOTO      GDES9999
.
.         up to printing grand totals
.
GDES9000  PRINT     *N,"******* Grand Totals *******";
.
GDES9999  RETURN
+
.*********************************************************************
.*                  TBLC0000                    Called by : REPT4200 *
.*        Print just a table heading for report type C               *
.*        Para's  : CURRCLSU      the current clinic/surgeon         * 
.********************************************************************* 
TBLC0000  MATCH     SP6,CURRCLSU
          GOTO      TBLC9999 IF EQUAL       * no table needed
.
          CALL      GDES0000                * print clinic/surgeon name
          PRINT     *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*":
.
                    *N,"|",*10,"|Number | Total |  No.   | Total ":
                    "|  Total|        |  No.   |  No.   |":
                    "---- Private Patients ----|":
                    "------- Other Users ------|":
.                   
                    *N,"|",*10,"|Booked |Booked |Sess's  | Time  ":
                    "| Time  | Util   |  of    |  of    |":
                    "   No.of| Time   | Util   |":
                    "   No.of| Time   | Util   |":
.                   
                    *N,"|",*10,"|Sess's | Time  | Used   | Used  ":
                    "|Shared |   %    | Cases  |Proc's  |": 
                    "  Proc's| Used   |   %    |":
                    "  Proc's| Used   |   %    |":
.
                    *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*";
.
          ADD       SIX,CLNO                * update line counter
.
TBLC9999  RETURN
+
.*********************************************************************
.*                  TBLD0000                    Called by : REPT5200 *
.*        Print just the table for report type 4                     *
.********************************************************************* 
TBLD0000  MATCH     SP6,CURRCLSU
          GOTO      TBLD9999 IF EQUAL       * no table needed
.
          CALL      GDES0000                * print clinic/surgeon name
          PRINT     *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*":
.
                    *N,"|",*27,"| Number |  Total | Number |  Total |  Total |":
                    *81,"| Number | Number |  No. of  |  No. of  ":
                    "|  No. of  |":
.
                    *N,"|",*27,"| Booked | Booked |Sessions|   Time |  Time  ":
                    "|  Util":
                    *81,"|   of   |   of   | Sessions | Sessions |":
                    " Sessions |":
.
                    *N,"|",*27,"|Sessions|  Time  |  Used  |   Used | Shared ":
                    "|   %",*81,"| Cases  | Proc's | Cancelld |  Delayed |":
                    " Overtime |":
.
                    *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*";
.
          ADD       SIX,CLNO                * update line counter
.
TBLD9999  RETURN
+
.*********************************************************************
.*                  TBLE0000                    Called by : REPT6200 *
.*        Print just a table heading for report type E               *
.*        Para's  : CURRCLSU      the current clinic/surgeon         * 
.********************************************************************* 
TBLE0000  MATCH     SP6,CURRCLSU
          GOTO      TBLE9999 IF EQUAL       * no table needed
.
          CALL      GDES0000                * print clinic/surgeon name
          PRINT     *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*":
.
                    *N,"|",*13,"| Number |  Total | Number |  Total |  Total |":
                    *67,"| Number | Number |",*105,"        |  Number":
                    "|  Total |":
.
                    *N,"|",*13,"| Booked | Booked |Sessions|   Time ":
                    "|   Time |  Util":
                    *67,"|   of   |   of   |",*105,"        |    of  ":
                    "|  Time  |":
.
                    *N,"|",*13,"|Sessions|  Time  |  Used  |   Used ":
                    "| Shared |   %":
                    *67,"| Cases  | Proc's | Operation Type ":
                    *105,"        |  Proc's|  Mins. |":
.
                    *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*";
.
          ADD       SIX,CLNO                * update line counter
.
TBLE9999  RETURN
+
.*********************************************************************
.*                  PTPA0000                    Called by : REPT2100 *
.*        Print the contents of temp file A for a clin/surg REPT4300 *
.*        Para's  : CURRCLSU      the current clinic/surgeon         * 
.********************************************************************* 
PTPA0000  PACK      KEY7,CURRCLSU,SP1       * positional read at start of clin
          MOVE      ZERO,TLNBOOK            * number of sessions booked
          MOVE      ZERO,TLTBOOK            * time booked for sessions
          MOVE      ZERO,TLNUSED            * number of sessions used
          MOVE      ZERO,TLTUSED            * time used for sessions
          MOVE      ZERO,TLNCASE            * number of cases seen
          MOVE      ZERO,TLNOPER            * number of operations performed
          MOVE      ZERO,TLPCASE            * number of private cases seen
          MOVE      ZERO,TLPTIME            * time used for private cases
          MOVE      ZERO,TLOCASE            * number of other users
          MOVE      ZERO,TLOTIME            * time used for other users
          MOVE      ZERO,TLSHARE            * total shared time
.
          READ      TEMPA,KEY7;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
.
PTPA2100  READKS    TEMPA;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
          GOTO      PTPA9999 IF OVER        * end of file
.
          MATCH     ACLIN,CURRCLSU 
          GOTO      PTPA9999 IF NOT EQUAL   * finished for this clin
.
.         see if any booked data
.
          MATCH     ANSB,ABFLAG
          GOTO      PTPA2200 IF NOT EQUAL   * no booked data
.
.         display booked data
.
          MOVE      ATUSED,FORM6P5          * time used
          DIV       ATBOOK,FORM6P5          * divide by time booked
          MULT      "100",FORM6P5           * get as a percentage
          MOVE      FORM6P5,PCENTA          * util %
.
          MOVE      APTIME,FORM6P5          * time used
          DIV       ATUSED,FORM6P5          * divide by time used
          MULT      "100",FORM6P5           * get as a percentage
          MOVE      FORM6P5,PCENTB          * private patient util %
.
          MOVE      AOTIME,FORM6P5          * time used
          DIV       ATUSED,FORM6P5          * divide by time used
          MULT      "100",FORM6P5           * get as a percentage
          MOVE      FORM6P5,PCENTC          * other user util %
.
          COMPARE   "55",CLNO
          CALL      PAGE0000 IF NOT LESS
.
          PRINT     *N,"|Booked",*10,"|",ANBOOK,*18,"|",ATBOOK,*26,"| ":
           ANUSED,*35,"|",ATUSED,*43,"|",ASHARE,*51,"| ",PCENTA,*60,"| ",ANCASE:
                    *69,"| ",ANOPER,*78,"| ",APCASE,*87,"| ",APTIME,*96,"| ":
                    PCENTB,*105,"| ",AOCASE,*114,"| ",AOTIME,*123,"| ",PCENTC:
                    *132,"|";
          ADD       ONE,CLNO                * add to line counter
          MOVE      ANBOOK,TLNBOOK          * total number of sessions booked
          MOVE      ATBOOK,TLTBOOK          * total time booked for sessions
          ADD       ANUSED,TLNUSED          * total number of sessions used
          ADD       ATUSED,TLTUSED          * total time used for sessions
          ADD       ANCASE,TLNCASE          * total number of cases seen
          ADD       ANOPER,TLNOPER          * total number of oper's performed
          ADD       APCASE,TLPCASE          * total number of private cases seen
          ADD       APTIME,TLPTIME          * total time used for private cases
          ADD       AOCASE,TLOCASE          * total number of other users
          ADD       AOTIME,TLOTIME          * total time used for other users
          ADD       ASHARE,TLSHARE          * total time shared
.
.         get next record
.
          READKS    TEMPA;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
          GOTO      PTPA2300 IF OVER        * end of file
.
          MATCH     ACLIN,CURRCLSU
          GOTO      PTPA2300 IF NOT EQUAL   * different clinic
.
.         check if any 'extra' data
.
PTPA2200  MATCH     ANSE,ABFLAG
          GOTO      PTPA9999 IF NOT EQUAL   * no 'extra' data
.
.         display extra data
.
          MOVE      ATUSED,FORM6P5          * time used
          DIV       ATBOOK,FORM6P5         * divide by time booked
          MULT      "100",FORM6P5           * get as a percentage
          MOVE      FORM6P5,PCENTA          * util %
.
          MOVE      APTIME,FORM6P5          * time used
          DIV       ATUSED,FORM6P5          * divide by time used
          MULT      "100",FORM6P5           * get as a percentage
          MOVE      FORM6P5,PCENTB          * private patient util %
.
          MOVE      AOTIME,FORM6P5          * time used
          DIV       ATUSED,FORM6P5          * divide by time used
          MULT      "100",FORM6P5           * get as a percentage
          MOVE      FORM6P5,PCENTC          * other user util %
.
          COMPARE   "55",CLNO
          CALL      PAGE0000 IF NOT LESS
.
          PRINT     *N,"| Extra",*10,"|",*18,"|",*26,"| ":
           ANUSED,*35,"|",ATUSED,*43,"|",ASHARE,*51,"| ",PCENTA,*60,"| ",ANCASE:
                    *69,"| ",ANOPER,*78,"| ",APCASE,*87,"| ",APTIME,*96,"| ":
                    PCENTB,*105,"| ",AOCASE,*114,"| ",AOTIME,*123,"| ",PCENTC:
                    *132,"|";
          ADD       ONE,CLNO                * add to line counter
          ADD       ANUSED,TLNUSED          * total number of sessions used
          ADD       ATUSED,TLTUSED          * total time used for sessions
          ADD       ANCASE,TLNCASE          * total number of cases seen
          ADD       ANOPER,TLNOPER          * total number of oper's performed
          ADD       APCASE,TLPCASE          * total number of private cases seen
          ADD       APTIME,TLPTIME          * total time used for private cases
          ADD       AOCASE,TLOCASE          * total number of other users
          ADD       AOTIME,TLOTIME          * total time used for other users
          ADD       ASHARE,TLSHARE          * total time shared
.
.         print the total for the clinic/surgeon
.
PTPA2300  MOVE      TLTUSED,FORM6P5         * total time used
          DIV       TLTBOOK,FORM6P5         * divide by total time booked
          MULT      "100",FORM6P5           * get as a percentage
          MOVE      FORM6P5,PCENTA          * total util %
.
          MOVE      TLPTIME,FORM6P5         * total time used privates
          DIV       TLTUSED,FORM6P5         * divide by total time used
          MULT      "100",FORM6P5           * get as a percentage
          MOVE      FORM6P5,PCENTB          * private patient util %
.
          MOVE      TLOTIME,FORM6P5         * total time used for others
          DIV       TLTUSED,FORM6P5         * divide by total time used
          MULT      "100",FORM6P5           * get as a percentage
          MOVE      FORM6P5,PCENTC          * other user util %
.
          COMPARE   "55",CLNO
          CALL      PAGE0000 IF NOT LESS
.
          PRINT     *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*":
                    *N,"| Total",*10,"|",TLNBOOK,*18,"|",TLTBOOK,*26,"| ":
      TLNUSED,*35,"|",TLTUSED,*43,"|",TLSHARE,*51,"| ",PCENTA,*60,"| ",TLNCASE:
                    *69,"| ",TLNOPER,*78,"| ",TLPCASE,*87,"| ",TLPTIME,*96,"| ":
                    PCENTB,*105,"| ",TLOCASE,*114,"| ",TLOTIME,*123,"| ",PCENTC:
                    *132,"|":
                    *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*"
          ADD       "4",CLNO                * add to line counter
.
PTPA9999  RETURN
+
.*********************************************************************
.*                  PTPB0000                    Called by : REPT2100 *
.*        Print the contents of temp file B for a clin/surg REPT3100 *
.*        Para's  : CURRCLSU      the current clinic/surgeon         * 
.********************************************************************* 
PTPB0000  MOVE      ZERO,CURRTYPE           * set as no type
          MOVE      ZERO,FORM6A             * total for each 'bcode'
          MOVE      ZERO,FORM6B             * total for each 'btype'
.
          PACK      KEY10,CURRCLSU,SP4
          READ      TEMPB,KEY10;BCLIN,BTYPE,BCODE,BBOOK,BEXTRA
.         
PTPB1000  READKS    TEMPB;BCLIN,BTYPE,BCODE,BBOOK,BEXTRA
          GOTO      PTPB8000 IF OVER        * finished temp file B
.
          MATCH     BCLIN,CURRCLSU
          GOTO      PTPB8000 IF NOT EQUAL   * different clinic
.
          MATCH     BTYPE,CURRTYPE
          GOTO      PTPB2000 IF EQUAL       * same code type
.
.         have a new code type, so print totals for previous one
.
          MATCH     "0",CURRTYPE
          GOTO      PTPB1500 IF EQUAL       * first item, so just print heading
.
.         print the summary for the previous type
.
          COMPARE   "55",CLNO
          CALL      PAGE0000 IF NOT LESS
.
          PRINT     *N,*51,"------":
                    *N,*6,"Total Sessions ",DIM9,*51,FORM6B
          MOVE      ZERO,FORM6B             * clear 'btype' counter
          ADD       "3",CLNO                * add to line counter
.
.         print the heading for a new code type
.
PTPB1500  MOVE      BTYPE,FORM1             * get the type as a form
          MOVE      BTYPE,CURRTYPE          * set up current type
          LOAD      DIM9,FORM1,CANCEL,DELAY,OVER
.
          COMPARE   "55",CLNO
          CALL      PAGE0000 IF NOT LESS
.
          PRINT     *N,"Sessions ",DIM9,*31,"Booked",*40,"Extra",*52,"Total":
                    *N,"------------------------",*31,"------",*39,"------":
                    *51,"------";
          ADD       "2",CLNO                * add to line counter
.
.         print a code
.
PTPB2000  MOVE      BBOOK,FORM6A            * get total booked
          ADD       BEXTRA,FORM6A           * total for each 'bcode'
          ADD       FORM6A,FORM6B           * add to 'btype' total
.
          MOVE      BTYPE,FORM1             * get as a form
          LOAD      CODE,FORM1,CATOC,CATOD,CATOO * get correct category
.
          MOVE      "Invalid Code",TDESC
          PACK      KEY5,CODE,BCODE
          CALL      RDCODE1
.
          PRINT     *N,BCODE,SP2,TDESC,*31,BBOOK,*39,BEXTRA,*51,FORM6A;
          ADD       "1",CLNO                * add to line counter
          GOTO      PTPB1000
.
.         end of file reached, finish off last summary
.
PTPB8000  MATCH     "0",CURRTYPE
          GOTO      PTPB9999 IF EQUAL       * no summary to print
.
          PRINT     *N,*51,"------":
                    *N,*6,"Total Sessions ",DIM9,*51,FORM6B
          ADD       "3",CLNO                * add to line counter
.
PTPB9999  RETURN
+
.*********************************************************************
.*                  PTAP0000                    Called by : REPT3100 *
.*        Print the contents of temp file A for a clin/surg  TYPE 2  *
.*        Para's  : CURRCLSU      the current clinic/surgeon         * 
.********************************************************************* 
PTAP0000  PACK      KEY7,CURRCLSU,SP1       * positional read at start of clin
          MOVE      ZERO,TLNBOOK            * number of sessions booked
          MOVE      ZERO,TLTBOOK            * time booked for sessions
          MOVE      ZERO,TLNUSED            * number of sessions used
          MOVE      ZERO,TLTUSED            * time used for sessions
          MOVE      ZERO,TLNCASE            * number of cases seen
          MOVE      ZERO,TLNOPER            * number of operations performed
          MOVE      ZERO,TLOTYPE            * total Operations
          MOVE      ZERO,TLTTYPE            * total time for operations
          MOVE      ZERO,TLSHARE
.
          READ      TEMPA,KEY7;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
.
PTAP2100  READKS    TEMPA;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
          GOTO      PTAP9999 IF OVER        * end of file
.
          MATCH     ACLIN,CURRCLSU 
          GOTO      PTAP9999 IF NOT EQUAL   * no data for this clinic
.
.         see if any booked data
.
          MATCH     ANSB,ABFLAG
          GOTO      PTAP2200 IF NOT EQUAL   * no booked data
.
.         display booked data
.
          MOVE      ATUSED,FORM6P5          * time used
          DIV       ATBOOK,FORM6P5          * divide by time booked
          MULT      "100",FORM6P5           * get as a percentage
          MOVE      FORM6P5,PCENTA          * util %
.
          COMPARE   "55",CLNO
          CALL      PAGE0000 IF NOT LESS
.
          PRINT     *N,"| Booked",*13,"| ",ANBOOK,*22,"| ",ATBOOK,*31,"| ":
                    ANUSED,*40,"| ",ATUSED,*49,"| ",ASHARE," | ",PCENTA:
                    *67,"| ",ANCASE,*76,"| ",ANOPER;
          MOVE      ANBOOK,TLNBOOK          * total number of sessions booked
          MOVE      ATBOOK,TLTBOOK          * total time booked for sessions
          ADD       ANUSED,TLNUSED          * total number of sessions used
          ADD       ATUSED,TLTUSED          * total time used for sessions
          ADD       ANCASE,TLNCASE          * total number of cases seen
          ADD       ANOPER,TLNOPER          * total number of oper's performed
          ADD       ASHARE,TLSHARE          * total time shared
.
          MOVE      ABFLAG,CURRTYPE         * get the current type
          CALL      PTPC0000                * print temp file C details
.
.         get next record
.
          READKS    TEMPA;ACLIN,ABFLAG,ANBOOK,ATBOOK,ANUSED,ATUSED:
                    ANCASE,ANOPER,APCASE,APTIME,AOCASE,AOTIME,ASHARE
          GOTO      PTAP2300 IF OVER        * end of file
.
          MATCH     ACLIN,CURRCLSU
          GOTO      PTAP2300 IF NOT EQUAL   * different clinic
.
.         check if any 'extra' data
.
PTAP2200  MATCH     ANSE,ABFLAG
          GOTO      PTAP9999 IF NOT EQUAL   * no 'extra' data and so no data
.
.         display extra data
.
          MOVE      ATUSED,FORM6P5          * time used
          DIV       ATBOOK,FORM6P5          * divide by time booked
.....     DIV       TLTBOOK,FORM6P5         * divide by time booked
          MULT      "100",FORM6P5           * get as a percentage
          MOVE      FORM6P5,PCENTA          * util %
.
          COMPARE   "55",CLNO
          CALL      PAGE0000 IF NOT LESS
.
          PRINT     *N,"| Extra",*13,"| ",ANBOOK,*22,"| ",ATBOOK,*31,"| ":
                    ANUSED,*40,"| ",ATUSED,*49,"| ",ASHARE," | ",PCENTA:
                    " | ",ANCASE," | ",ANOPER;
          ADD       ANBOOK,TLNBOOK          * total number of sessions booked
          ADD       ATBOOK,TLTBOOK          * total time booked for sessions
          ADD       ANUSED,TLNUSED          * total number of sessions used
          ADD       ATUSED,TLTUSED          * total time used for sessions
          ADD       ANCASE,TLNCASE          * total number of cases seen
          ADD       ANOPER,TLNOPER          * total number of oper's performed
          ADD       ASHARE,TLSHARE          * total time shared
.
          MOVE      ABFLAG,CURRTYPE         * set the current type
          CALL      PTPC0000                * print temp file C details
.
.         print the total for the clinic/surgeon
.
PTAP2300  MOVE      TLTUSED,FORM6P5         * total time used
          DIV       TLTBOOK,FORM6P5         * divide by total time booked
          MULT      "100",FORM6P5           * get as a percentage
          MOVE      FORM6P5,PCENTA          * total util %
.
          COMPARE   "55",CLNO
          CALL      PAGE0000 IF NOT LESS
.
          PRINT     *N,"| Total",*13,"| ",TLNBOOK," | ",TLTBOOK," | ",TLNUSED:
                    " | ",TLTUSED," | ",TLSHARE," | ",PCENTA:
                    " | ",TLNCASE," | ",TLNOPER:
                    *85,"|",*113,"| ",TLOTYPE:
                    " |  ",TLTTYPE," |":
                    *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*"
          ADD       "3",CLNO                * add to line counter
.
PTAP9999  RETURN
+
.*********************************************************************
.*                  PTPC0000                    Called by : PTAP0000 *
.*        Print the details from temp file C                         *
.*        Para's  : CURRCLSU      current clinic/surgeon             * 
.*********************************************************************
PTPC0000  MOVE      ONE,FORM1               * indicate first op. type
.
          PACK      KEY10,CURRCLSU,CURRTYPE,SP3  * get at start of clinic
          READ      TEMPC,KEY10;CCLIN,CBFLAG,COTYPE,CNOPER,CTUSED
.
.         read next item on temp file B
.
PTPC1000  READKS    TEMPC;CCLIN,CBFLAG,COTYPE,CNOPER,CTUSED
          GOTO      PTPC9000 IF OVER        * end of file
.
          MATCH     CBFLAG,CURRTYPE
          GOTO      PTPC9000 IF NOT EQUAL   * different type
.
          MATCH     CCLIN,CURRCLSU
          GOTO      PTPC9000 IF NOT EQUAL   * different clinic/surgeon
.
.         get operation type description
.
          MOVE      "Unknown",TDESC
          PACK      KEY5,ANSO,ANSY,COTYPE
          CALL      RDCODE1
.
.         print the details
.
          BRANCH    FORM1,PTPC2000          * in first item
.
          COMPARE   "55",CLNO
          CALL      PAGE0000 IF NOT LESS    * need a new page
.
.         print when not first op. type -> print from start of line
.
       PRINT     *N,"|",*13,"|",*22,"|",*31,"|",*40,"|",*49,"|",*58,"|",*67,"|":
                    *76,"|",*85,"| ",COTYPE,SP2,TDESC,*113,"| ",CNOPER:
                    *122,"|  ",CTUSED,*132,"|";
          ADD       ONE,CLNO
          GOTO      PTPC3000
.
.         print for the first operation type -> print from column 85
.
PTPC2000  MOVE      ZERO,FORM1              * signal done different print
          PRINT     *85,"| ",COTYPE,SP2,TDESC,*113,"| ",CNOPER:
                    *122,"|  ",CTUSED,*132,"|";
          ADD       ONE,CLNO
.
.         add to the totals
.
PTPC3000  ADD       CNOPER,TLOTYPE          * add to op types
          ADD       CTUSED,TLTTYPE          * add to op times
          gOTO      PTPC1000
.
.         end of file
.
PTPC9000  COMPARE   ONE,FORM1
          GOTO      PTPC9500 IF NOT EQUAL   * have printed something
.
          PRINT     *85,"|",*113,"|",*122,"|",*132,"|";
          ADD       ONE,CLNO
.
PTPC9500  PRINT     *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*";
          ADD       ONE,CLNO                * add to line counter
.
PTPC9999  RETURN
+
.********************************************************************* 
.*                  PTPD0000                    Called by : REPT5300 *
.*        Print the details from temp file D for layout 4            *
.*        Para's  : CURRCLSU      current clinic/surgeon             * 
.********************************************************************* 
PTPD0000  PACK      KEY7,CURRCLSU,SP1       * positional read at start of clin
          MOVE      ZERO,TLNBOOK            * number of sessions booked
          MOVE      ZERO,TLTBOOK            * time booked for sessions
          MOVE      ZERO,TLNUSED            * number of sessions used
          MOVE      ZERO,TLTUSED            * time used for sessions
          MOVE      ZERO,TLNCASE            * number of cases seen
          MOVE      ZERO,TLNOPER            * number of operations performed
          MOVE      ZERO,TLNCANC            * number of sessions cancelled
          MOVE      ZERO,TLNDLAY            * number of sessions delayed
          MOVE      ZERO,TLNOVER            * number of sessions overtime
.
          READ      TEMPD,KEY7;DCLIN,DBFLAG,DNBOOK,DTBOOK,DNUSED,DTUSED:
                    DNCASE,DNOPER,DNCANC,DNDLAY,DNOVER,DSHARE
.
PTPD2100  READKS    TEMPD;DCLIN,DBFLAG,DNBOOK,DTBOOK,DNUSED,DTUSED:
                    DNCASE,DNOPER,DNCANC,DNDLAY,DNOVER,DSHARE
          GOTO      PTPD9999 IF OVER        * end of file
.
          MATCH     DCLIN,CURRCLSU 
          GOTO      PTPD9999 IF NOT EQUAL   * finished for this clin
.
.         see if any booked data
.
          MATCH     ANSB,DBFLAG
          GOTO      PTPD2200 IF NOT EQUAL   * no booked data
.
.         display booked data
.
          MOVE      DTUSED,FORM6P5          * time used
          DIV       DTBOOK,FORM6P5          * divide by time booked
          MULT      "100",FORM6P5           * get as a percentage
          MOVE      FORM6P5,PCENTA          * util %
.
          COMPARE   "55",CLNO
          CALL      PAGE0000 IF NOT LESS
.
          PRINT     *N,"| Booked",*27,"| ",DNBOOK," | ",DTBOOK," | ":
                    DNUSED," | ",DTUSED," | ",DSHARE," | ",PCENTA," | ",DNCASE:
                    " | ",DNOPER," |  ",DNCANC,"  |  ",DNDLAY,"  |  ":
                    DNOVER,*132,"|";
          ADD       ONE,CLNO                * add to line counter
          MOVE      DNBOOK,TLNBOOK          * total number of sessions booked
          MOVE      DTBOOK,TLTBOOK          * total time booked for sessions
          ADD       DNUSED,TLNUSED          * total number of sessions used
          ADD       DTUSED,TLTUSED          * total time used for sessions
          ADD       DNCASE,TLNCASE          * total number of cases seen
          ADD       DNOPER,TLNOPER          * total number of oper's performed
          ADD       DNCANC,TLNCANC          * total number of sessions cancelled
          ADD       DNDLAY,TLNDLAY          * total number of sessions delayed
          ADD       DNOVER,TLNOVER          * total number of sessions overtime
          ADD       DSHARE,TLSHARE          * total time shared
.
.         get next record
.
          READKS    TEMPD;DCLIN,DBFLAG,DNBOOK,DTBOOK,DNUSED,DTUSED:
                    DNCASE,DNOPER,DNCANC,DNDLAY,DNOVER,DSHARE
          GOTO      PTPD2300 IF OVER        * end of file
.
          MATCH     DCLIN,CURRCLSU
          GOTO      PTPD2300 IF NOT EQUAL   * different clinic
.
          MATCH     ANSE,DBFLAG
          GOTO      PTPD2300 IF NOT EQUAL   * no 'extra' data
          GOTO      PTPD2210
.
.         check if any 'extra' data
.
PTPD2200  MATCH     ANSE,DBFLAG
          GOTO      PTPD9999 IF NOT EQUAL   * no 'extra' data
.
.         display extra data
.
PTPD2210  MOVE      DTUSED,FORM6P5          * time used
          DIV       DTBOOK,FORM6P5         * divide by time booked
          MULT      "100",FORM6P5           * get as a percentage
          MOVE      FORM6P5,PCENTA          * util %
.
          COMPARE   "55",CLNO
          CALL      PAGE0000 IF NOT LESS
.
          PRINT     *N,"| Extra ",*27,"| ",DNBOOK," | ",DTBOOK," | ":
                    DNUSED," | ",DTUSED," | ",DSHARE," | ",PCENTA," | ",DNCASE:
                    " | ",DNOPER," |  ",DNCANC,"  |  ",DNDLAY,"  |  ":
                    DNOVER,*132,"|";
          ADD       ONE,CLNO                * add to line counter
          ADD       DNBOOK,TLNBOOK          * total number of sessions booked
          ADD       DTBOOK,TLTBOOK          * total time booked for sessions
          ADD       DNUSED,TLNUSED          * total number of sessions used
          ADD       DTUSED,TLTUSED          * total time used for sessions
          ADD       DNCASE,TLNCASE          * total number of cases seen
          ADD       DNOPER,TLNOPER          * total number of oper's performed
          ADD       DNCANC,TLNCANC          * total number of sessions cancelled
          ADD       DNDLAY,TLNDLAY          * total number of sessions delayed
          ADD       DNOVER,TLNOVER          * total number of sessions overtime
          ADD       DSHARE,TLSHARE          * total time shared
.
.         print the total for the clinic/surgeon
.
PTPD2300  MOVE      TLTUSED,FORM6P5         * total time used
          DIV       TLTBOOK,FORM6P5         * divide by total time booked
          MULT      "100",FORM6P5           * get as a percentage
          MOVE      FORM6P5,PCENTA          * total util %
.
          COMPARE   "55",CLNO
          CALL      PAGE0000 IF NOT LESS
.
          PRINT     *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*":
                    *N,"| Total ",*27,"| ",TLNBOOK," | ",TLTBOOK," | ":
                    TLNUSED," | ",TLTUSED," | ",TLSHARE," | ",PCENTA," | ":
                    TLNCASE," | ",TLNOPER," |  ",TLNCANC,"  |  ",TLNDLAY:
                    "  |  ",TLNOVER,*132,"|":
                    *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*"
          ADD       "4",CLNO                * add to line counter
.
PTPD9999  RETURN
+
.*********************************************************************
.*                  SUMM0000                    Called by : REPT8000 *
.*        Print the summary of the columns at the end of the report  *
.********************************************************************* 
SUMM0000  COMPARE   "45",CLNO
          CALL      PAGE0000 IF NOT LESS    * will need a new page
.
          BRANCH    OPCNR62A,SUMM1000,SUMM2000,SUMM1000,SUMM2000,SUMM2000
          GOTO      SUMM9999
.
.         summary for layout 1 and 3
.
SUMM1000  PRINT     *N,"Number Booked Sessions":
                    *29,"= Number of sessions booked":
                    *N,"Total Booked Time (Mins)":
                    *29,"= Total booked time for sessions - time for breaks":
                    *N,"Number sessions Used":
                    *29,"= Number of sessions that have usage data input":
                    *N,"Total Time Used (Mins)":
                    *29,"= Total duration for sessions used":
                    *N,"Util %":
                    *29,"= Total Time Used / Total Booked Time * 100":
                    *N,"Number of Patients":
                    *29,"= Number of cases in the sessions":
                    *N,"Number of Oper's":
                    *29,"= Number of operations performed in the sessions":
                    *N,"Number of Private pat's":
                    *29,"= Number of private patients operated on":
                    *N,"Time Used (Private)":
                    *29,"= ",*+,OPCNCDSC," time used by private patients":
                    *N,"Private Patients Util %":
                    *29,"= Private patient time used / Total time used * 100":
                    *N,"Other Users":
                    *29,"= Patients whose operating surgeon was not the ":
                    "attending doctor":
                    *N,"Time Used (Other users)":
                    *29,"= Time used by Other users ":
                    *N,"Other Users Util %":
                    *29,"= Other users time used / Total time used * 100":
                    *N,"Total Time Shared":
                    *29,"= Total shared/reallocation time (booked & extra)"
          GOTO      SUMM7000
.
.         summary for layout 2 and 4 and 5
.
SUMM2000  PRINT     *N,"Number Booked Sessions":
                    *29,"= Number of sessions booked":
                    *N,"Total Booked Time (Mins)":
                    *29,"= Total booked time for sessions - time for breaks":
                    *N,"Number sessions Used":
                    *29,"= Number of sessions that have usage data input":
                    *N,"Total Time Used (Mins)":
                    *29,"= Total duration for sessions used":
                    *N,"Total Time Shared":
                    *29,"= Total shared/reallocation time (booked & extra)":
                    *N,"Util %":
                    *29,"= Total Time Used / Total Booked Time * 100":
                    *N,"Number of Patients":
                    *29,"= Number of cases in the sessions":
                    *N,"Number of Oper's":
                    *29,"= Number of operations performed in the sessions":
                    *N,"Total Time Shared":
                    *29,"= Total shared/reallocation time (booked & extra)"
.
SUMM7000  BRANCH    OPCNR62A,SUMM7500,SUMM7500,SUMM9000,SUMM7500,SUMM7500
          GOTO      SUMM9999
.
SUMM7500  PRINT     *N,"NB. Sessions Cancelled will always appear on the ":
                    "Booked line"
.
SUMM9000  PRINT     *N,"*** End of Report ***"
.
SUMM9999  RETURN
+
.
. ------- I/O includes needed -------
.
          INC       STD001IO                * all standard routines
          INC       TFILENAM                * Generate Temp File Name
.
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
          INC       OPRCLIDS                * ? for clinic file
          INC       OPRCLIIO/INC            * clinic file
          INC       OPRCOUIO/INC            * clinic operation usage file
          INC       OPRCSCIO/INC            * clinic session cancellation file
          INC       OPRCSUIO/INC            * clinic session usage file
.
          INC       PATCOMN3                * for KEYPERD
          INC       PATCODKY                * ? for codes
          INC       PATDOCDS                * ? for doctors
          INC       PATDOCKY                * ? for doctors
          INC       PATCODIO/INC            * codes file
          INC       PATDO1IO/INC            * doctors file
          INC       PATDRGIO/INC            * date range file
................................................................................

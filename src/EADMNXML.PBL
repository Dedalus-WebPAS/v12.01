.*******************************************************************************
.* System    :   Inpatient                                                     *
.* Program   :   EADMNXML                                                      *
.* Desc      :   Process an eAdmission XML File                                *
.*******************************************************************************
.* Date      :   28/03/2013                                                    *
.* Author    :   Saroeun Soeur                                                 *
.* Function  :   This program will open an eAdmission XML File and             *
.*               load the details into pmsehbaf and pmsedbaf                   *
.* Version   :   v10.03.00                                                     *
.* Modification :                                                              *
.*                                                                             *
.* V12.01.03     16/12/2025 Ebon clements    TSK 0970614                       *
.*               Process ""space"" as a clear field value and write record to  *
.*               pmsedbaf - WRDD0000                                           *
.* V12.01.02     15/12/2025 Ebon clements    TSK 0970614                       *
.*               Process "" as a clear field value and write record to         *
.*               pmsedbaf - WRDD0000                                           *
.* V12.01.01     25/11/2025 Nikitha Bhaskar  TSK 0969892                       *
.*               Recompiled for PMSEDBXD                                       *
.* V11.04.01     11.01.2024 DA Sarkies       TSK 0941573                       *
.*               Added code to change " to '                                   *
.* V11.03.01     11.01.2024 DA Sarkies       TSK 0941573                       *
.*               Added code to change " to '                                   *
.* V11.01.01     24/06/2021 Peter Vela       TSK 0908102                       *
.*               Added blank validation to PMDBVALU before write to pmsedbaf   *
.* V11.00.01     04/06/2020 Don Nguyen       TSK 0881876                       *
.*               Recompiled for PMSEDBXD - added 'VerifiedAddress'             *
.* V10.11.03     04/12/2017 Peter Vela       TSK 0850270                       *
.*               Recompiled for PMSEDBXD                                       *
.* V10.11.02     04/12/2017 Peter Vela       TSK 0849236                       *
.*               Recompiled for PMSEDBXD                                       *
.* V10.11.01     5/11/2017  Peter Vela       TSK 0846923                       *
.*               Recompiled for PMSEDBXD                                       *
.* v10.03.05     18.11.2013 Patrick Adair    CAR 292748                        *
.*               default PTCNNEID to 1 if it has not been pre-populated        *
.* v10.03.04     01.08.2013 Patrick Adair    CAR 289389                        *
.*               recompiled for changes to PMSXMLFD/INC                        *
.* v10.03.03      23.04.2013  Saroeun Soeur       CAR 280425                   *
.*                change to work with command line                             *
.* v10.03.02      18.04.2013  Saroeun Soeur       CAR 280425                   *
.* v10.03.01      28.03.2013  Brian Ackland                                    *
.*                Change to execute shell script eadmnxml.us1 (config as php)  *
.*                Change relationship ehb/edb to use UNIQ NOT Portal ID        *
.*******************************************************************************
          INC       STD002FD            * command line
          INC       PATCONFD/INC
          INC       PMSEDBXD/INC
          INC       PMSEDBFD/INC        *eAdmission pending detail table
          INC       PMSEERFD/INC        *eAdmission pending error table
          INC       PMSEHBFD/INC        *eAdmission pending header table
          INC       TFILEVAR/INC 
          INC       WEBERRFD/INC 
          INC       PMSXMLFD/INC
.
. eAdmission header file
. ------------------------------------------------------------------------------
EAHDSTXT  FILE      HL7,FIXED=2000
.
.Name     Type      Size      Start
XMHBADAT  DIM       8         Exp Admission Date CCYYMMDD
XMHBHOSP  DIM       3         Hospital (pathspaf)
XMHBBTYP  DIM       3         Booking Type (cat BK)
XMHBAVMO  DIM       100       Admitting VMO
XMHBEAID  DIM       20        eAdmission Id
XMHBUNIQ  DIM       20        eAdmission Unique
XMHBTIME  DIM       8         eAdmission Time
.
. eAdmission detail file
. ------------------------------------------------------------------------------
EADDSTXT  FILE      HL7,FIXED=2000
.
.Name     Type      Size      Start
XMDBUNIQ  DIM       20        Unique ID eAdmission Id
XMDBPRID  DIM       8         Property ID
XMDBWBID  DIM       8         webPAS ID (cgi name)
XMDBVALU  DIM       600       Value
.
. Program variables
. ------------------------------------------------------------------------------
CMDLINE   DIM       200
DIM8      DIM       8
FORM20    FORM      20
TODAY     DIM       8
.
HEADERFN  DIM       8     * Header Temp File Name
DETAILFN  DIM       8     * Detail Temp File Name
.
FILENAM1  DIM       20
FILENAM2  DIM       20
XMLFLNAM  DIM       80
.
SP600     INIT      "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  ":
                    "                                                  "
CNVQUOTE  INIT      "#"'"
.
. Program constants
. ------------------------------------------------------------------------------
PRGID     INIT      "EADMNXML"
PRGNAM    INIT      "eAdmission XML load"
VERSION   INIT      "V12.01.03"
+
.*******************************************************************************
.*                              MAIN0000                                       *
.*                      Controlling Logic (Mainline code)                      *
.*******************************************************************************
.
MAIN0000  CALL      SETX0000
          CALL      INIT0000               * initialisation and open files
.
MAIN0500  CALL      GFIL0000               * get filename 
          BRANCH    EXIT,MAIN9999          * nothing input
.
          CALL      CFIL0000               * copy file
          BRANCH    EXIT,MAIN9999          * unable to copy
.
          CALL      OPEN0000               * open text file
          BRANCH    EXIT,MAIN9999          * unable to open .txt file
.
          CALL      PROC0000               * process
.
MAIN9999  STOP                             * chain back to program
+
.*******************************************************************************
.*                                INIT0000               Called by: MAIN0000   *
.*  Initialisation routine                                                     *
.*******************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          OPEN      PMSXMLA1,"pmsxmlaf"
          OPEN      PMSEHBA1,"pmsehbaf"
          OPEN      PMSEDBA1,"pmsedbaf"
.
          OPEN      CONTROLF,"controlf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,HEADERFN
          REP       " 0",HEADERFN
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,DETAILFN
          REP       " 0",DETAILFN
.
INIT9999  RETURN
+
.*******************************************************************************
.*                               OPEN0000                Called by: MAIN0000   *
.* Open the eAdmission temporary files for processing                          *
.*******************************************************************************
.
OPEN0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EAHDSTXT,HEADERFN            * open text file
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Header File not found.  ";
            CALL      HITENTER
            GOTO      OPEN9100
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EADDSTXT,DETAILFN           * open text file
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"File not found.  ";
            CALL      HITENTER
            GOTO      OPEN9100
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      OPEN9999
.
OPEN9100  MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9200  DISPLAY   *P1:24,"Invalid Field";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9300  DISPLAY   *P1:24,"Field not found for this message id";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9999  RETURN
+
.*******************************************************************************
.*                               PROC0000                Called by: MAIN0000   *
.* Loop through the eAdmission temporary files and store data in tables        *
.*******************************************************************************
.
PROC0000  CALL      WRHD0000
          CALL      WRDD0000
          CLOSE     EAHDSTXT,DELETE        * Delete Temporary File
          CLOSE     EADDSTXT,DELETE        * Delete Temporary File
.
PROC9999  RETURN
+
.*******************************************************************************
.*                                WRDD0000               Called by: PROC0000   *
.* Write eAdmission detail table                                               *
.*******************************************************************************
WRDD0000  
.
WRDD1000  READ      EADDSTXT,SEQ;XMDBPRID,XMDBWBID,XMDBVALU
          REP       CNVQUOTE,XMDBVALU            * replace " with '
          GOTO      WRDD9999 IF OVER
.
          MOVELPTR  XMDBVALU,F3
          IF        F3=2
            MATCH     "''",XMDBVALU              * "" mapped to ''
            IF        @EQUAL
              MOVE      SP70,XMDBVALU            * clear field value
              GOTO      WRDD2000                 * and load
            ENDIF
          ENDIF
.
          IF        F3=5
            MATCH     "'' ''",XMDBVALU           * "" mapped to '' x two fields
            IF        @EQUAL
              MOVE      SP70,XMDBVALU            * clear field value
              GOTO      WRDD2000                 * and load
            ENDIF
          ENDIF
.
          MATCH     SP600,XMDBVALU               * don't load blank values
          GOTO      WRDD1000 IF EQUAL
          GOTO      WRDD1000 IF EOS
.
WRDD2000  PACK      KEY28,PMHBUNIQ,XMDBPRID,SP70
          CALL      RDPMEDB1
          IF        OVRCD=1
            PACK      PMDBUNIQ,PMHBUNIQ,SP70
            PACK      PMDBPRID,XMDBPRID,SP70
            PACK      PMDBWBID,XMDBWBID,SP70
            PACK      PMDBVALU,XMDBVALU,SP70
            MOVE      ZERO,PMDBNVAL
            PACK      PMDBDVAL,SP70
            CALL      WRPMEDB1
          ELSE
            DISPLAY   *R,"Duplicate Found in Load on ",PMHBUNIQ,XMDBPRID
          ENDIF
          GOTO      WRDD1000
.
WRDD9999  RETURN
+
.*******************************************************************************
.*                                WRHD0000               Called by: PROC0000   *
.* Write eAdmission header table                                               *
.*******************************************************************************
WRHD0000  READ      EAHDSTXT,SEQ;XMHBUNIQ,XMHBEAID,XMHBADAT,XMHBTIME,XMHBHOSP:
                                 XMHBBTYP,XMHBAVMO
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
          MOVE      DIM8,TODAY
          CLOCK     TIME,CTIMEIS
          PACK      PMHBDATE,TODAY,SP70
          PACK      PMHBTIME,CTIMEIS,SP70
          PACK      PMHBSTAT,ONE,SP70
          PACK      PMHBADAT,XMHBADAT,SP70
          PACK      PMHBURNO,SP70
          PACK      PMHBVISN,SP70
          PACK      PMHBHOSP,XMHBHOSP,SP70
          PACK      PMHBBTYP,XMHBBTYP,SP70
          PACK      PMHBAVMO,XMHBAVMO,SP70
          PACK      PMHBEAID,XMHBEAID,SP70
          REP       " 0",PMHBEAID
          PACK      PMHBLDDT,SP70
          PACK      PMHBLDTY,SP70
          PACK      PMHBUTIM,SP70
          PACK      PMHBWEBU,SP70         
.
          MOVE      ZERO,FORM20
.
          MOVE      "  0",PRXCODE                 * System Lock Sector 0
          CALL      GETSLK00                      * Get System Lock-Sector 0
          READ      CONTROLF,HUND16;*227,PTCNNEID    * next message id
.
          TYPE      PTCNNEID                      * If not numeric default to 1
          GOTO      WRHD8000 IF NOT EQUAL
            
          MOVE      PTCNNEID,FORM20
.
WRHD8000  ADD       ONE,FORM20
          MOVE      FORM20,PTCNNEID
.         
          MOVE      PTCNNEID,PMHBUNIQ
.         
          WRITAB    CONTROLF,HUND16;*227,PTCNNEID
          CALL      RELSLK00  
          CALL      WRPMEHB1
.
WRHD9999  RETURN
.*******************************************************************************
.*                          CFIL0000                   Called by: MAIN0000     *
.* Copy the XML file to a ".txt" file so TBL can open it                       *
.* Requires: XMLFLNAM - eAdmission XML Response File name                      *
.* Returns : TEMPFILE - temporary ".txt" filename                              *
.*           EXIT  0 = file successfully copied                                *
.*                 1 = unable to copy file                                     *
.*******************************************************************************
.
CFIL0000  STRIP     XMLFLNAM
          CLEAR     CMDLINE
          APPEND    "eadmnxml.us1 ",CMDLINE
          APPEND    HEADERFN,CMDLINE
          APPEND    " ",CMDLINE
          APPEND    DETAILFN,CMDLINE
          APPEND    " ",CMDLINE
          APPEND    XMLFLNAM,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          MATCH     "0       ",TASKID
          IF        @EQUAL
            MOVE      ZERO,EXIT
          ELSE
            DISPLAY   *P1:24,*EL,*B,"Unable to convert XML file.  ";
            CALL      HITENTER
            MOVE      ONE,EXIT
          ENDIF
.
CFIL9999  RETURN
+
.*******************************************************************************
.*                          GFIL0000                 Called by: MAIN0000       *
.* Keyin the eAdmission XML file name                                          *
.*******************************************************************************
.
GFIL0000  KEYIN     *P1:10,*EL,"Filename (including path): ":
                    *P28:10,XMLFLNAM
.
          PACK      XMLFLNAM,XMLFLNAM,SP30
          MATCH     SP30,XMLFLNAM
          GOTO      GFIL9100 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      GFIL9999
.
GFIL9100  MOVE      ONE,EXIT
.
GFIL9999  RETURN
.*******************************************************************************
.* I/O Includes                                                                *
.*******************************************************************************
          INC       STD002IO
          INC       TFILENAM     
          INC       PMSEDBXM/INC
          INC       PMSEERIO/INC        *eAdmission pending error table
          INC       WEBERRIO/INC
          INC       PMSXMLIO/INC
          INC       PMSEHBIO/INC
          INC       PMSEDBIO/INC
.

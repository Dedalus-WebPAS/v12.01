.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAPAT55                                             *
.*                                                                            *
.*     FUNCTION:         HEALTH FUND REGISTER                                 *
.*                                                                            *
.*     DATE WRITTEN:     08/08/1985                                           *
.*                                                                            *
.*     AUTHOR:           MALCOLM HAYSE                                        *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*        V11.02.01 11/04/2022  J.Tan         TSK 0814566                     *
.*                  Added Hospital and Eff.todate to HF MV hours              *
.*        V10.01.01 20/12/2010  Mike Laming   CAR 233046                      *
.*                  Mods to Misc.Charges PATMCHFD - Key change, logic changes *
.*        V10.00.01 30/04/2010  J.Tan     CAR 220887                          *
.*                  Mods checking for Active/Inactive of Misc.charge          *
.*        V9.03.02  26/05/2004  Pat Dirito        CAR 50035                   *
.*                  Added PTFNMVRT, PTFNBMVH,PTHFNRMV & PTFNMVBC              *
.*        V9.03.01  08/04/2004  Steve Armstrong     HosClaims  CAR 48280      *
.*                  Added new EDI fields                                      *
.******************************************************************************
.*        V9.02.02  20/11/2001  Mike Laming                                   *
.*                  Add PTHFUCMX "Use Casemix Matrix"                         *
.*                  Modify print routine to handle ALL lines of Health Fund   *
.*        V9.02.01  13/10/2001  J.Tan                                         *
.*                  Add new "Use Matrix Default" indicator into report (5.09) *
.*        V5.08.03  15/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.02  04/08/2000  Jill Habasque                                 *
.*                  Added Grouper Version                                     *
.*        V5.08.01  06/06/2000  Tonii                                         *
.*                  Changed from KEY10 to KEY14 for the health fund file      *
.* ************************************************************************** *
.*        V5.07.02  18/03/1999  Jill Habasque                                 *
.*                  Added active/inactive codes                               *
.*        V5.07.01  16/03/99 J.Tan                                            *
.*                  Copied from 6.06 for hf table type (casemix funding)      *
.*        V6.06.01  11/02/1999  Jill Habasque                                 *
.*                  Fixed DISPHEAD display                                    *
.*       V6.03.03  20/08/96 J.Tan                                             *
.*                 Change billing category to table type                      *
.*       V6.03.02  02/04/1996  Greg Horvat       SRF 614674                   *
.*                 Added HCP Eq. to the report                                *
.*       V6.03.01  09/10/1995  Greg Horvat       SRF 611799                   *
.*                 Added health fund group to report                          *
.*       V6.03.B3  09/02/95 Gabrielle    Release 6.03 Modifications           *
.*                 Billing Category is supposed to be on each health fund     *
.*       V6.03.00  23/01/95 Gabrielle    Release 6.03 Modifications           *
.*                 Add the new Billing Category variable to keyin/display etc *
.*                                                                            *
.* ************************************************************************** *
.*                                                                            *
.*                       V5.01  05/09/88 G.Williams                           *
.*                              Add Theatre Fee Charging                      *
.*                       V5.51  25/07/89 J.Tan            SRF 100263          *
.*                              Add Health Fund Comments                      *
.*                       V5.52  15/08/89 J.Tan                                *
.*                              System option for above Modification          *
.*                       V5.53  18/05/90 J.Tan            SRF 104901          *
.*                              Added 8 new theatre class.                    *
.*                       V5.54  25/05/90 Glen Hobby                           *
.*                              Added PRS2 capability                         *
.*                       V5.55  20/11/90 Glen Hobby                           *
.*                              Fixed bug in display of table HDP equivalents *
.*                       V5.56  20/11/90 Glen Hobby                           *
.*                              Fixed bug in print of table HDP equivalents   *
.*                       V5.57  07/03/91 i chung          SRF 107755          *
.*                              Fixed only 1st 3 Tables' HDP EQV being printed*
.*                              problem                                       *
.*                       V5.58  06/01/92 i chung          SRF 112296          *
.*                              Fixed repeated Address 2 problem              *
.*                                                                            *
.*                       V6.00  07/03/91 ROD                                  *
.*                              Release 6. Health Fund Table now 8 characters *
.*                              and added Fax Number to Fund                  *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATHSPFD/INC            * Hospital File
          INC       PATADHFD/INC
          INC       PATCONFD/INC
          INC       PATFCMFD/INC
          INC       PATFN1FD/INC
          INC       PATFN2FD/INC
          INC       PATFX1FD/INC
          INC       PATMCHFD/INC
+
.
.         VARIABLES
.
ACTIVE    DIM       4
CASEMX    DIM       4
CURRDATE  DIM       8
MATRIX    DIM       4
DIM11     DIM       11  
DIM12     DIM       12
DIM17     DIM       17
DIM30     DIM       30
DIM40     DIM       40
DIM3YN    DIM       3
DTCLSS    DIM       17
DATALN    DIM       78[8]
DATANO    FORM      2
.
FIRIND    FORM      1
FIRST     FORM      1
HOSPCODE  DIM       3
HOSPNAME  DIM       30
KEY21A    DIM       21
IBCNMHOS  FORM      1
LINENO    FORM      2
.
MENUOPTN  FORM      1
.
OPTCVERT  FORM      2
.
PHFHDPE   DIM       6                        * Print Var for Health Fund HDP
.
SACODE    DIM       6
SAVHDPE   DIM       4
SHFADD3   DIM       20
SHFADD4   DIM       20
SKEY34    DIM       34
.
.         CONSTANTS
DASH      INIT      "-"
BRALEFT   INIT      "("
BRARIGHT  INIT      ")"
.
DPROD     INIT      "Production"
DTCLS0    INIT      "MBS Fee Ranges  "
DTCLS1    INIT      "Classification 1"
DTCLS2    INIT      "Classification 2"
DTCLS3    INIT      "Classification 3"
DTCLS4    INIT      "Classification 4"
DTCLS5    INIT      "Classification 5"
DTCLS6    INIT      "Classification 6"
DTCLS7    INIT      "Classification 7"
DTCLS8    INIT      "Classification 8"
DTCLS9    INIT      "Classification 9"
DTCLS10   INIT      "Classification 10"
DTCLS11   INIT      "Classification 11"
DTCLS12   INIT      "Classification 12"
DTCLS13   INIT      "Classification 13"
DTCLS14   INIT      "Classification 14"
DTCLS15   INIT      "Classification 15"
DTCLS16   INIT      "Classification 16"
DTEST     INIT      "Test"
.
CMXLGN    INIT      "Casemix matrx="
MATLGN    INIT      "Matrix def'lt="
MAXLINE   FORM      "55"
.
FIVE9     FORM      "59"
.
SAVGRPV   DIM       3
SP12      INIT      "            "
.
VRT       INIT      "| "
ZERO4     INIT      "0000    "
.
PRGID     INIT      "IBAPAT55"
PRGNAM    INIT      "Health Fund Register"
VERSION   INIT      "V12.01.00"
+
.
.         DISPLAY SCREEN HEADER AND OPEN FILES
.
          CALL      DISPHEAD
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          DISPLAY   *P56:24,"Opening patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patfcmmf";
          OPEN      PATFCMM1,"patfcmmf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
          READ      CONTROLF,FIVE9;*61,CHFCOMM,*222,PTCNFNHD
          READ      CONTROLF,HUND10;*115,PTCNUEDI,*229,PTCNMVCP
.
          COMPARE   ONE,PTCNUEDI
          IF        @EQUAL
            DISPLAY   *P64:24,"patfx1af";
            OPEN      PATFX1A1,"patfx1af"
          ENDIF
.
          COMPARE   ONE,PTCNMVCP
          IF        @EQUAL
            DISPLAY   *P56:24,"Opening patfn2af";
            OPEN      PATFN2A1,"patfn2af"
            DISPLAY   *P56:24,"Opening patmchgf";
            OPEN      PATMCHG1,"patmchgf"
          ENDIF
+
.
.         START OF PROGRAM
.
.           SELECT OPTION
.
START     MOVE      ZERO,CPAGENO
.
          CALL      DISPHEAD
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Health Fund Sequence";
.
          MOVE      FIVE,CVERT                   * Initialise option line number
          MOVE      ONE,MENUOPTN                 * Initialise menu option
.
          IF         CHFCOMM = 1
            ADD       ONE,CVERT                  * increment line number
            ADD       ONE,MENUOPTN               * increment option number
            DISPLAY   *P1:6,*V2LON,TWO,*HOFF," = Health Fund Comments";
          ENDIF
.
          IF        PTCNUEDI = 1
            ADD       ONE,CVERT                  * increment line number
            ADD       ONE,MENUOPTN               * increment option number
            DISPLAY   *P1:CVERT,*V2LON,MENUOPTN,*HOFF," = Health Fund EDI"
          ENDIF
.
          IF        PTCNMVCP = 1
            ADD       ONE,CVERT                  * increment line number
            ADD       ONE,MENUOPTN               * increment option number
            DISPLAY   *P1:CVERT,*V2LON,MENUOPTN,*HOFF," = Health Fund Mech. ":
                      "Ventilation Billing"
          ENDIF
.
          ASSIGN    (CVERT + 2),OPTCVERT
.
START0    KEYIN     *P1:OPTCVERT,*EL,"Select Option :":
                    *P17:OPTCVERT,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      ENDIT IF EQUAL
.
          IF        OPTION > MENUOPTN
            BEEP
            GOTO      START0
          ENDIF
.
.         Convert menu selection to option selection
.
          IF        OPTION = 2
            IF        CHFCOMM = 1
              GOTO      START1
            ELSE
              IF        PTCNUEDI = 1
                MOVE      THREE,OPTION
                GOTO      START1
              ELSE
                MOVE      FOUR,OPTION
                GOTO      START1
              ENDIF
            ENDIF
          ENDIF
.
          IF        OPTION = 3
            IF        CHFCOMM = 1
              IF        PTCNUEDI = 1
                GOTO      START1
              ELSE
                MOVE      FOUR,OPTION
                GOTO      START1
              ENDIF
            ELSE
              MOVE      FOUR,OPTION
            ENDIF
          ENDIF
.
START1    BRANCH    OPTION OF FUNDNO:          * Health Fund Sequence
                              HFCOMM:          * Health Fund Comments
                              FUNDEDI:         * Health Fund EDI
                              MVCP0000         * Health Fund MV Billing
+
.*******************************************************************************
.*                                 FUNDNO                                      *
.*                       PRINT VIA FUND NUMBER                                 *
.*******************************************************************************
FUNDNO    DISPLAY   *P50:2,"-  ",*V2LON,"Health Fund Sequence";
          MOVE      "Health Fund Sequence",CPHDROPT
          MOVE      SP20,KEY14
          CALL      RDSFUND1
          MOVE      "60",CLNO
.
          DISPLAY   *P20:24,*EL,"Health Fund : ",*P48:24,"Table : ";
.
          MOVE      ZERO,CLNO
          CALL      HEAD0000
          CALL      PRNT0000
          GOTO      ENDP
.
NOFUND    BRANCH    OPTION OF NOFUND1,NOFUND2
.
NOFUND1   DISPLAY   *P1:24,*EL,*B,*V2LON,*P26:24:
                    "** No Health Funds on file **",*W2;
          GOTO      START
.
NOFUND2   DISPLAY   *P1:24,*EL,*B,*V2LON,*P22:24:
                    "** No Health Funds Comments on file **",*W2;
          GOTO      START
.*******************************************************************************
.*                              ENDP                                           *
.*        OVER Condition found..print last line of -- and display message      *
.*******************************************************************************
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NOFUND IF EQUAL
.
          CALL      PAGEND
          PRINT     *N,*N,"***  End of Report  ***"
          DISPLAY   *P1:24,*EL,*P23:24,*V2LON:
                    "** Register Generation Completed **",*W2;
          GOTO      START
+
.*******************************************************************************
.*                              PRNT0000                                       *
.*                        Print Health Fund Details                            *
.*******************************************************************************
PRNT0000  PACK      KEY14,SP20
          MOVE      ZERO,DATANO
          MOVE      ZERO,LINENO
          CALL      RDSFUND1                 * Positional Read
PRNT1000  CALL      RDKFUND1                 * Read next Record
          BRANCH    OVRCD,PRNT9000           * EOF, exit routine
.
          DISPLAY   *P35:24,*V2LON,HCODE,*P57:24,HFTABL;
.
          MATCH     ZERO4,HFTABL             * Same Health Fund Code
          IF        @EQUAL
            CALL      LINE0000               * No, setup Health Fund details
            GOTO      PRNT1000               * go back & read next
          ENDIF
.
          CALL      THDP0000
.
          MATCH     "1",DHFTACT
          IF        @EQUAL
            MOVE      DNO,ACTIVE
          ELSE
            MOVE      DYES,ACTIVE
          ENDIF
.
.                                           * Print the 2 Health Table lines
          ADD       ONE,LINENO
          UNPACK    HFNAME,KEY25
          PRINT     *1,DATALN[LINENO],*79,VRT,HFTABL,PHFHDPE,*95,VRT:
                    KEY25,*123,VRT,ACTIVE,*132,"|"
          ADD       ONE,LINENO
          PRINT     *1,DATALN[LINENO],*79,VRT,*95,VRT,PTHFBCAT,*123:
                    VRT,*132,"|"
.
          IF        LINENO > DATANO
            MOVE      DATANO,LINENO
          ENDIF
.
          ADD       TWO,CLNO
          COMPARE   "55",CLNO                * No. Lines > 55
          CALL      HEAD0000 IF NOT LESS     * Yes, print new page heading
.
          GOTO      PRNT1000
.
PRNT9000  COMPARE   DATANO,LINENO
          CALL      REST0000 IF LESS        * print rest of last Fund
.
PRNT9999  RETURN
.
.*******************************************************************************
.*                              LINE0000                                       *
.*                       setup Health Fund Code lines                          *
.*  Health Fund Code, Name, Telephone, Theatre Charging                        *
.*******************************************************************************
LINE0000  COMPARE   DATANO,LINENO
          CALL      REST0000 IF LESS        * print rest of last Fund
.
          CALL      PAGEND 
          MOVE      DTCLS0,DTCLSS 
          LOAD      DTCLSS,HFBAND,DTCLS1,DTCLS2,DTCLS3,DTCLS4,DTCLS5,DTCLS6:
                                  DTCLS7,DTCLS8,DTCLS9,DTCLS10,DTCLS11,DTCLS12:
                                  DTCLS13,DTCLS14,DTCLS15,DTCLS16
.
. ----------------------------------------- set up 1st line data       *I-50901
.
          PACK      DIM12,HCODE,SP12      * Health Fund code
          PACK      DIM30,HFNAME,SP30     * Health Fund name
          PACK      DIM11,HFTELE,SP12     * phone no.
          PACK      DIM17,DTCLSS,SP30     * theatre charging classification
          PACK      DATALN[1],VRT,DIM12,VRT,DIM30,VRT,DIM11,VRT,DIM17
.
. ----------------------------------------- set up 2nd line data       *I-50901
.
          IF        PTCNFNHD=1
            CALL      FHDP0000              * Setup the HDP for H.Fund Code
            PACK      DIM12,PHFHDPE,SP12    * HDP equivalent
          ELSE
            MOVE      SP12,DIM12
          ENDIF
          PACK      DIM30,HFADD1,SP30       * address line 1
          PACK      DIM11,HFFAXN,SP12       * fax no.
          PACK      DIM17,PTHFHGRP,SP3,PTHFHCPE
          PACK      DATALN[2],VRT,DIM12,VRT,DIM30,VRT,DIM11,VRT,DIM17
.
. ----------------------------------------- set up 3rd line data       *I-50901
.
          PACK      DIM12,PTHFGRPV,SP12     * Grouper version
          PACK      DIM30,HFADD2,SP30       * address line 2
          MOVE      SP12,DIM11
          MOVE      DNO,MATRIX
          LOAD      MATRIX,PTHFUMTD,DYES    * use matrix default
          PACK      DIM17,MATLGN,MATRIX
          PACK      DATALN[3],VRT,DIM12,VRT,DIM30,VRT,DIM11,VRT,DIM17
.
. ----------------------------------------- set up 4th line data       *I-50901
.
          MOVE      SP12,DIM12
          MOVE      HFADD3,DIM30            * address line 3
          MOVE      SP12,DIM11
          MOVE      DNO,CASEMX
          LOAD      CASEMX,PTHFUCMX,DYES    * use matrix default
          PACK      DIM17,CMXLGN,CASEMX
          PACK      DATALN[4],VRT,DIM12,VRT,DIM30,VRT,DIM11,VRT,DIM17
.
          MOVE      FOUR,DATANO
          MOVE      SP30,DIM30
          MOVE      SP30,DIM17
          PACK      DATALN[5],VRT,DIM12,VRT,DIM30,VRT,DIM11,VRT,DIM17
          PACK      DATALN[6],VRT,DIM12,VRT,DIM30,VRT,DIM11,VRT,DIM17
          PACK      DATALN[7],VRT,DIM12,VRT,DIM30,VRT,DIM11,VRT,DIM17
          PACK      DATALN[8],VRT,DIM12,VRT,DIM30,VRT,DIM11,VRT,DIM17
.
. ----------------------------------------- set up 5th line data?      *I-50901
.
          MATCH     SP30,HFADD4
          IF        !@EQUAL
            ADD       ONE,DATANO
            MOVE      SP12,DIM12
            MOVE      HFADD4,DIM30            * address line 4
            MOVE      SP12,DIM11
            PACK      DATALN[DATANO],VRT,DIM12,VRT,DIM30,VRT,DIM11,VRT,DIM17
          ENDIF
.
          MOVE      ZERO,LINENO
          GOTO      LINE9999
.
LINE9000  MOVE      TRUE,EXIT

LINE9999  RETURN
+
.*******************************************************************************
.*                              REST0000                             *I-90202  *
.*              Print to rest of the health fund info.               *I-90202  *
.*******************************************************************************
REST0000  COMPARE   DATANO,LINENO
          GOTO      REST9999 IF NOT LESS
.
          ADD       ONE,LINENO
          PRINT     *1,DATALN[LINENO],*79,VRT,*95,VRT,*123,VRT,*132,"|"
.
          ADD       ONE,CLNO
          COMPARE   "55",CLNO                * No. Lines > 55
          CALL      HEAD0000 IF NOT LESS     * Yes, print new page heading
.
          GOTO      REST0000
.
REST9999  RETURN
.
.*******************************************************************************
.*                              FHDP0000                                       *
.*              Setup the HDP Equivalent for Health Fund CODE                  *
.*******************************************************************************
FHDP0000  IF        PTCNFNHD = ONE
            PACK     PHFHDPE,BRALEFT,HFHDPE,BRARIGHT                   *C-90202
          ELSE
            MOVE     SP6,PHFHDPE
          ENDIF
FHDP9999  RETURN
+
.*******************************************************************************
.*                              THDP0000                                       *
.*              Setup the HDP Equivalent for Health Fund TABLE                 *
.*******************************************************************************
THDP0000  IF        PTCNFNHD = ONE
            PACK     PHFHDPE,SLASH,HFHDPE
          ELSE
            MOVE     SP6,PHFHDPE
          ENDIF
THDP9999  RETURN
+
.*******************************************************************************
.*                                   HFCOMM                                    *
.*                        Health Fund Comments option                          *
.*******************************************************************************
HFCOMM    DISPLAY   *P50:2,"-  ",*V2LON,"Health Fund Comments",*HOFF;
          MOVE      "Health Fund Comments",CPHDROPT
.
HFCOMM1   MOVE      SP6,HCODE
          KEYIN     *P1:13,*EF,"Health Fund : ______",*P15:13,*V2LON,HCODE;
          RESET     HCODE
          GOTO      START IF EOS
.
          ENDSET    HCODE
          APPEND    SP6,HCODE
          RESET     HCODE
.
          DISPLAY   *P15:13,*EL,*V2LON,HCODE;
          MOVE      HCODE,SACODE                 * save the Fund Code
          MOVE      ZERO4,HFTABL
.
          PACK      KEY14 WITH HCODE,HFTABL,SP10
          CALL      RDFUND1
          COMPARE   ZERO,OVRCD
          GOTO      OKCONT IF EQUAL
.
          DISPLAY   *P40:13,*B,*V2LON,"** Health Fund does not exist **",*W2;
          GOTO      HFCOMM1
.
OKCONT    DISPLAY   *P25:13,*V2LON,HFNAME,*HOFF:
                    *P1:24,*EL,"Ok to continue (",*V2LON,"Y",*HOFF:
                               "/",*V2LON,"N",*HOFF,") ?";
.
OKCONT1   KEYIN     *P24:24,*EL,"_",*P24:24,*V2LON,ANS;
.
          MATCH     "Y",ANS
          GOTO      PRCOMM IF EQUAL
          MATCH     "N",ANS
          GOTO      HFCOMM1 IF EQUAL
          BEEP
          GOTO      OKCONT1
.
.         Print comments
.
PRCOMM    DISPLAY   *P1:24,*EL,*P23:24,*V2LON,"** ",*BLINKON,"Printing ":
                               "Health Fund Comments",*HOFF,*V2LON," **";
          MOVE      "60",CLNO
          MOVE      ZERO,FIRST
.
          PACK      KEY8 WITH HCODE,SP2
          CALL      RDSFCMM1
PRCOM1    CALL      RDKFCMM1
          BRANCH    OVRCD OF ENDP
.
          MATCH     SACODE,FUNDCOD
          GOTO      ENDP IF NOT EQUAL
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          BRANCH    FIRST OF PRCOM2
          PRINT     *1,"| ",HCODE," ",HFNAME,*43,"| ",FTEXT,*132,"|"
          ADD       ONE,CLNO
          MOVE      ONE,FIRST
          GOTO      PRCOM1
.
PRCOM2    PRINT     *1,"|",*43,"| ",FTEXT,*132,"|"
          ADD       ONE,CLNO
          GOTO      PRCOM1
.
.          last line on the page
.
PAGEND    PRINT     "*---------------------------------------------------":
                    "----------------------------------------------------":
                    "---------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.*******************************************************************************
.*                                 FUNDEDI                                     *
.*                            Print Fund EDI Data                              *
.*******************************************************************************
FUNDEDI   DISPLAY   *P50:2,"-  ",*V2LON," EDI Data ";
          MOVE      " - EDI Data",CPHDROPT
          MOVE      ZERO,CLNO
          CALL      HEAD0000                     * Print Fund EDI Heading
          CALL      PEDI0000                     * Print Fund EDI Data
          GOTO      ENDP
+
.*******************************************************************************
.*                                 MVCP0000                                    *
.*             Print Mechanical Ventilation in Casepayment billing details     *
.*******************************************************************************
.
MVCP0000  DISPLAY   *P50:2,"-  ",*V2LON," MV Billing Data ";
          MOVE      " - MV Billing Data",CPHDROPT
          CALL      KHOS0000                 * Keyin Hospital
.
          MOVE      ZERO,CLNO
          CALL      HEAD0000                     * Print Fund EDI Heading
          CALL      PMVB0000                     * Print Fund EDI Data
          GOTO      ENDP
+
.*******************************************************************************
.*                              PEDI0000                                       *
.*                     Print Health Fund EDI Data                              *
.*******************************************************************************
PEDI0000  MOVE      SP6,KEY6
          CALL      RSPTFX11                     * Position key to start of file
PEDI1000  CALL      RKPTFX11                     * Read next Record
          BRANCH    OVRCD,PEDI9999               * EOF, exit routine
.
          MOVE      DNO,DIM3YN
          LOAD      DIM3YN,PTFXEXTR,DYES
.
          PACK      DIM11,SP10,SP1
          MATCH     PTFXTFLG,ANST
          IF        @EQUAL
            MOVE      DTEST,DIM11
            GOTO      PEDI2000
          ENDIF
.
          MATCH     PTFXTFLG,ANSP
          IF        @EQUAL
            MOVE      DPROD,DIM11
            GOTO      PEDI2000
          ENDIF
.
PEDI2000  PRINT     *1,"| ",PTFXHFND,*14,"| ",DIM3YN,*20,"| ",DIM11:
                    *38,"| ",PTFXSUBJ,*70,"| ",PTFXADDR,*132,"|"
.
          DISPLAY   *P20:24,*EL,"Printing Health Fund : ",PTFXHFND
          ADD       ONE,CLNO
          IF        CLNO >= MAXLINE
            CALL      HEAD0000
          ENDIF
.
          GOTO      PEDI1000
.
PEDI9999  RETURN
+
.*******************************************************************************
.*                              PMVB0000                                       *
.*             Print Health Fund Mechanical Ventilation Data                   *
.*******************************************************************************
.
PMVB0000  MOVE      SP70,KEY17
          CALL      RSPTFN21                     * Position key to start of file
PMVB1000  CALL      RKPTFN21                     * Read next Record
          BRANCH    OVRCD,PMVB9999               * EOF, exit routine
.
          MOVE      DNO,DIM3YN
          LOAD      DIM3YN,PTFNBMVH,DYES
.
          PACK      KEY14,PTFNHFND,ZERO4,SP70
          CALL      RDFUND1
          BRANCH    OVRCD,PMVB1000
          UNPACK    HFNAME,KEY30
.
          IF        IBCNMHOS=1
            MATCH     SP70,HOSPCODE
            IF        !@EQUAL
              MATCH     HOSPCODE,PTFNHOSP
              GOTO      PMVB1000 IF NOT EQUAL    * different hospital
            ENDIF
          ENDIF
.
          MOVE      SP70,DIM40
          MATCH     SP70,PTFNMVBC
          GOTO      PMVB3000 IF EQUAL
.
          PACK      KEY29,PTCNDCLM,SP6,SP3,PTFNMVBC,CURRDATE
          CALL      PATMCHRD                * read Misc.Charges file 
          COMPARE   ZERO,EXIT
          GOTO      PMVB2000 IF EQUAL
.
          MOVE      "INVALID CODE",MDESC
.
PMVB2000  PACK      KEY9,PTFNMVBC
          STRIP     KEY9
          PACK      DIM40,KEY9,SP1,DASH,SP1,MDESC
.
PMVB3000  COMPARE   ONE,IBCNMHOS
          GOTO      PMVB6000 IF NOT EQUAL
.
          MOVE      SP70,KEY18
          MATCH     SP70,PTFNHOSP
          IF        !@EQUAL
            MOVE      PTFNHOSP,KEY3
            CALL      RDPTHSP1
            IF        OVRCD=0
              UNPACK    PTHSNAME,KEY18
            ENDIF
          ENDIF
.
          MOVE      SP70,CPCDATE
          MATCH     SP70,PTFNEDAT
          IF        !@EQUAL
            UNPACK    PTFNEDAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
          ENDIF
.
          UNPACK    DIM40,KEY25
          PRINT     *1,"| ",HCODE,*14,"| ",KEY30,*47,"| ",PTFNHRMV:
                    *60,"| ",PTFNMVRT,*73,"| ",KEY18,*94,"|":
                    KEY25,*121,"|",CPCDATE,*132,"|"
          GOTO      PMVB7000
.
PMVB6000  UNPACK    DIM40,KEY35
          PRINT     *1,"| ",HCODE,*14,"| ",KEY30,*47,"| ",DIM3YN:
                    *60,"| ",PTFNMVRT,*73,"| ",PTFNHRMV,*94,"|":
                    KEY35,*132,"|"
.
PMVB7000  DISPLAY   *P20:24,*EL,"Printing Health Fund : ",HCODE
          ADD       ONE,CLNO
          IF        CLNO >= MAXLINE
            CALL      HEAD0000
          ENDIF
.
          GOTO      PMVB1000
.
PMVB9999  RETURN
.*******************************************************************************
.*                               HEAD0000          
.*                           Headings for Output
.*******************************************************************************
HEAD0000  CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
          COMPARE   ZERO,CPAGENO
          CALL      PAGEND IF NOT EQUAL
.
          CALL      HEAD132
.
          IF        IBCNMHOS=1
            PRINT     *40,"Hospital - ",HOSPCODE," ",HOSPNAME
          ENDIF
          PRINT     *N;
          CALL      PAGEND
.
          BRANCH    OPTION,HEAD1000:             * Heading for Health fund seq
                           HEAD2000:             * Heading for Health fund comm
                           HEAD3000:             * Heading for EDI data
                           HEAD4000              * Heading for MV billing data
.
.         Option 1
.
HEAD1000  BRANCH    PTCNFNHD,HEAD1500      * Using HDP Equivalent
.
          PRINT     *1,"| Health Fund",*15,"| Name and Address":
                    *47,"| Telephone",*60,"| Theatre Charging":
                    *79,"|   Table",*95,"| Table Name",*123,"| Active",*132,"|";
          PRINT     *N,*1,"|","Grouper Vers.",*15,"|",*47,"| Bus / Fax":
                    *60,"| HFGroup/ HCP Eq. ",*79,"|",*95,"| Table Type      ":
                    *123,"|",*132,"|"
          ADD       TWO,CLNO
          CALL      PAGEND
          GOTO      HEAD5000
.
HEAD1500   PRINT    *1,"| Health Fund",*15,"| Name and Address":
                    *47,"| Telephone",*60,"| Theatre Charging":
                    *79,"|   Table/HDP",*95,"| Table Name",*123,"| Active":
                    *132,"|";
.
          PRINT     *N,*1,"| (HDP Equiv.)",*15,"|",*47,"| Bus / Fax":
                    *60,"| HFGroup/ HCP Eq. ",*79,"|",*95,"| Table Type      ":
                    *123,"| ",*132,"|";
          PRINT     *N,*1,"|Grouper Vers.",*15,"|",*47,"|":
                       *60,"| ",*79,"|":                               *C-90202
                       *95,"|",*123,"|",*132,"|"
          ADD       TWO,CLNO
          CALL      PAGEND
          GOTO      HEAD5000
.
.         Option 2
.
HEAD2000  PRINT     *1,"| Health Fund ",*43,"| Comments",*132,"|"
.
          COMPARE   ZERO,PTCNFNHD
          GOTO      HEAD5000 IF EQUAL
.
          PRINT     *1,"| (HDP Equiv.)",*43,"|",*132,"|"
          ADD       ONE,CLNO
.
.         Option 2 or 3 - Health Fund EDI data
.
HEAD3000  PRINT     *1,"| Health Fund ",*14,"| EDI ",*20,"| Mode ":
                    *38,"| Subject ",*70,"| Address ",*132,"|"
          ADD       ONE,CLNO
          CALL      PAGEND
          GOTO      HEAD5000
.
.         Option 2 or 3 - Health Fund MV Billing data
.
HEAD4000  IF        IBCNMHOS=1
            PRINT     *1,"| Health Fund",*14,"| Name",*47,"| Hours for":
                      *60,"| Daily Rate ",*73,"| Hospital Name":
                      *94,"|Misc. Charge Code for MV":
                      *121,"|Eff.Date",*132,"|"
            PRINT     *1,"|",*14,"|",*47,"| Add.MV Day":
                      *60,"|",*73,"|":
                      *94,"|",*121,"|",*132,"|"
          ELSE
            PRINT     *1,"| Health Fund",*14,"| Name",*47,"| MV Billing ":
                      *60,"| Daily Rate ",*73,"| Hours for":
                      *94,"|Misc. Charge Code for MV",*132,"|"
            PRINT     *1,"|",*14,"|",*47,"|":
                      *60,"|",*73,"| Additional MV Day":
                      *94,"|",*132,"|"
          ENDIF
.
          ADD       ONE,CLNO
          CALL      PAGEND
          GOTO      HEAD5000
.
HEAD5000  ADD       ONE,CLNO
          RETURN
+
.******************************************************************************
.*                                  KHOS0000                                  *
.*                      Keyin hospital ID                                     *
.******************************************************************************
KHOS0000  MOVE       ZERO,EXIT
          COMPARE    ONE,IBCNMHOS
          GOTO       KHOS9999 IF NOT EQUAL
.
          MOVE       SP70,HOSPCODE
          MOVE       SP70,HOSPNAME
          DISPLAY    *P1:13,*EL,"Hospital Id : ";
          MOVE       TEN5,ECOL
          MOVE       TEN3,EVERT
          MOVE       SP3,CKYIDEF3
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOS2000,KHOS9000
          PACK       HOSPCODE,PTHSHOSP,SP70
          PACK       HOSPNAME,PTHSNAME,SP70
          GOTO       KHOS3000
.
KHOS2000  MOVE      "All Hospitals",HOSPNAME
          MOVE      SP10,HOSPCODE
.
KHOS3000  DISPLAY   *P20:13,*V2LON,HOSPNAME;
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS9000  MOVE       ONE,EXIT
KHOS9999  RETURN
+
.******************************************************************************
.
ENDIT     CHAIN     PGM
          STOP
.
.==============================================================================
.
          INC       STD001IO
          INC       PATMCHRD                * Miscellaneous Charges read routine
          INC       PATHSPKY
.
          INCLUDE   PATFCMIO/INC
          INC       PATFN1IO/INC
          INC       PATFN2IO/INC
          INC       PATFX1IO/INC
          INC       PATMCHIO/INC
          INC       PATHSPIO/INC            * Hospital File
+

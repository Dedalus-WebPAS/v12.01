.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   FX206124                                                  *
.* Desc      :   Write operation date to patecoaf                          *
.*                                                                         *
.* V9.12.01  13/10/2009 Sandra Barcham 206124                              *
.*           Remove Debug                                                  *
.***************************************************************************
.
          INC       STD001FD
          INC       OPRDEAFD/INC
          INC       PATECOFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
COUNT     FORM      8
.
PRGID     INIT      "FX206124"
PRGNAM    INIT      "Write operation date to patecoaf"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      OPRDETA2,"oprdetaf"
          OPEN      PATECOA1,"patecoaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Update Operation Date"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,COUNT
          PACK      KEY12,SP70
          CALL      RSPTECO1
PROC1000  CALL      RKPTECO1
          BRANCH    OVRCD,PROC9999
.
          ADD       ONE,COUNT
          IF        (COUNT%1000)=0
            DISPLAY   *P14:15,*V2LON,PTEOADMN:
                      *P14:16,PTEOEPNO:
                      *P14:17,PTEOCNT:
                      *P14:18,COUNT;
          ENDIF
.
          MATCH     SP8,PTEODATE
          GOTO      PROC1000 IF NOT EQUAL
.
          PACK      KEY31,PTEOADMN,SP70
          CALL      RSOPDEA2
.
PROC2000  CALL      RKOPDEA2
          BRANCH    OVRCD,PROC1000
.
          MATCH     OPDAADMN,DPTEOADM
          GOTO      PROC1000 IF NOT EQUAL
.
          COMPARE   FOUR,OPDASTAT
          GOTO      PROC2000 IF NOT EQUAL
.
          PACK      PTEODATE,OPDADATE,SP70
          MOVE      "00:00",PTEOSTTM
          MOVE      "00:00",PTEOEDTM
.
          CALL      UPPTECO1
          GOTO      PROC1000
.
PROC9000  DISPLAY   "Finished";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       OPRDEAIO/INC
          INC       PATECOIO/INC

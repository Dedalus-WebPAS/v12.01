. **********************************************************************
. *            P R O G R A M  S U M M A R Y                            *
. *            - - - - - - -  - - - - - - -                            *
. *                                                                    *
. * PROGRAM NAME:          FKEYUMEN                                    *
. * FUNCTION:              Run Any Other Program Menu                  *
. * AUTHOR:                Steve O'Gorman                              *
. *                                                                    *
. * MODIFICATIONS:                                                     *
. **********************************************************************
. * V10.02.01 01/04/2011  Jill Parkinson CAR 239574                    *
. *           Removed open of ibaprntf                                 *
. * C2.09.B01 06.11.2000  Sandra Barcham                               *
. *           Recompiled for ibaspolf                                  *
. **********************************************************************
. * C1.00.02  05/10/1995  MATT                                         *
. *           Redefined SYSMES as SFILE                                *
. * C1.00.01  13/02/1995  Greg Horvat      SRF 134920 134922 134932    *
. *           Recompiled for IBAPRINT                                  *
. **********************************************************************
. * V6.00.01  06/12/1993  Graeme Williams   SRF 126717                 *
. *           Corrected "Direct to" in IBAM0000                        *
. * V6.01.01  17/02/1994  Graeme Williams   SRF 127813                 *
. *           Fix problem with leaving ports locked.                   *
. **********************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
.
          INC       IBAPRNFD/INC
          INC       IBAPORFD/INC
.
          INC       IBAMDPFD/INC
          INC       IBAMSLFD/INC
          INC       IBASECFD/INC
          INC       IBASMAFD/INC
          INC       IBAUSEFD/INC
.
          INC       BDSYSMES/INC
          INC       CHECKDAT/INC
.
.********************************************************************** 
.*                            Constants                               *
.********************************************************************** 
.
PORTS     FILE      ASCII, FIXED=20
 IFGE     $$VER,7
SYSMES    SFILE    ASCII, FIXED=256
 XIF
 IFEQ     $$VER,6
SYSMES    FILE     ASCII, FIXED=256
 XIF
.
OPTZZ     INIT      "RO"
OPTRR     INIT      "NT"
OPTUU     INIT      "CO"
.
.
.********************************************************************** 
.*                            Variables                               *
.********************************************************************** 
.
CMDLINE   DIM       50
COUNT     FORM      3
.
DIM6      DIM       6
DIM20     DIM       20
DIRPOS    FORM      2
.
FIRSTTIM  FORM      1
FNAMER    DIM       8
FNAMET    DIM       8
FWORK     FORM      6
FWK       DIM       1
.
GAMOPT    FORM      2
.
INSEC     DIM       6
ITEM      FORM      2
.
LINE      FORM      2
.
NOINIMNU  FORM      2
NUM       FORM      3
.
OPCNT     FORM      2
OPTXX     DIM       20
OTHOPT    FORM      2
.
PAGE      FORM      1
PROG      DIM       8
PRTOPT    FORM      2
.
SC        FORM      2[55]
SCRNOPTN  FORM      2
SECHK     DIM       1
SECLEVL   FORM      1[99]
SELECT    DIM       2
SECPOS    FORM      1
SPOOPT    FORM      2
STOWORK   DIM       6
SYSCHK    DIM       1
SYSTEM    DIM       3[99]
.
TAB       FORM      2
TOTAL     FORM      5
.
VERT      FORM      2
.
XX1       FORM      3
XX2       FORM      3
XX3       FORM      3
XX4       FORM      3
XX5       FORM      3
XX6       FORM      3
.
.
.
PRGID     INIT      "FKEYUMEN"
PRGNAM    INIT      "Run Any Other Program"
VERSION   INIT      "V12.01.00"
.
.
.********************************************************************** 
.*                              ML0000                                *
.*                    Mainline Controlling Logic                      *
.********************************************************************** 
.
ML0000    CALL      INIT0000                * Initialize Routine
          CALL      SPOL0000                * Set Up for Spooling
.
ML1000    CALL      MENU0000                * Display User's Menu
          BRANCH    EXIT,ML9000
.
          CHAIN     IBUSMPRO
          GOTO      ML9999
.
ML9000    SPLCLOSE                          * Exit FKEYUMEN
          CALL      ULPORT1
          DISPLAY   *ES
.
ML9999    CLEAR     CMDLINE
          APPEND    "uniq_port ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          APPEND    SP30,CMDLINE
          APPEND    SP30,CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          SHUTDOWN  ""
.
.********************************************************************** 
.*                            INIT0000                                *
.*                       Initialize Routine                           *
.********************************************************************** 
.
INIT0000  CLEAR     FNAMET
          APPEND    "tmport",FNAMET
          APPEND    PORT,FNAMET
          RESET     FNAMET
          REP       " 0",FNAMET
          RESET     FNAMET
.
          CLEAR     CMDLINE
          APPEND    "uniq_port > ",CMDLINE
          APPEND    FNAMET,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          OPEN      PORTS,FNAMET
          READ      PORTS,SEQ;DIM20
          SCAN      " ",DIM20
          BUMP      DIM20
          MOVE      DIM20,PORT
.
.         Now rename temp file based on new port number
.
          CLEAR     FNAMER
          APPEND    "tmport",FNAMER
          APPEND    PORT,FNAMER
          RESET     FNAMER
          REP       " 0",FNAMER
          RESET     FNAMER
.
          CLEAR     CMDLINE
          APPEND    "mv ",CMDLINE
          APPEND    FNAMET,CMDLINE
          APPEND    ".txt ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          OPEN      IBAPORT1,"ibaportf"
          OPEN      CONTROLF,"controlf"
.
          OPEN      IHAPASS1,"ihapassf"
          OPEN      SYSMES,"systmess"
          OPEN      IBAMDPA1,"ibamdpaf"
          OPEN      IBAMSLA1,"ibamslaf"
          OPEN      IBASMAA1,"ibasmaaf"
          OPEN      IBAUSEA1,"ibauseaf"
          OPEN      IBASECU2,"ibasecuf"
.
          READ      CONTROLF,ZERO;*11,CTIMEOUT
.
          READ      CONTROLF,TWO;*4,CONAME:
                                *65,CCOUNTRY:
                               *149,USERFLAG:
                               *182,CSNAME:
                               *249,SPOOL
.
          MOVE      "IBASYS99",PGM          * Use IBASYS99 for Chaining
.
          MOVE      PRGID,IBAPPROG
          CALL      WRPORT1                 * IAM IN FKEYUMEN
.
          CALL      IBACLOCK
.
          MOVE      CONAME,CNAME
          PACK      DIM6 WITH OPTUU,OPTRR,OPTZZ
.
          MOVE      SP1,DIM1
.
          MOVE      ZERO,FIRSTTIM           * Clear First Time Flag
.
INIT9999  RETURN
.
.********************************************************************** 
.*                            SPOL0000                                *
.*                        Set Up Spooling                             *
.********************************************************************** 
.
SPOL0000  COMPARE   TWO TO CPRTFLG
          GOTO      SPOL9999 IF EQUAL
.
          MOVE      SP8,SPLFILE
          MOVE      SPOOL,CPRTFLG
.
SPOL9999  RETURN
.
.********************************************************************** 
.*                            MENU0000                                *
.*                       Display User's Menu                          *
.********************************************************************** 
.
MENU0000  MOVE      PASSCODE,KEY4
          CALL      RDPASS1
          BRANCH    OVRCD,MENU9000
.
          MATCH     SP3,SECMNNUM
          GOTO      MENU5000 IF NOT EQUAL
.
          CALL      IBAM0000                * Standard IBA MENU
          BRANCH    EXIT,MENU9000
.
          MOVE      FALSE,EXIT
          GOTO      MENU9999
.
MENU5000  CALL      NEWM0000                * User Specific Menu
          BRANCH    EXIT,MENU9000
.
          MOVE      FALSE,EXIT
          GOTO      MENU9999
.
MENU9000  MOVE      TRUE,EXIT
.
MENU9999  RETURN
.
.********************************************************************** 
.*                            MESS0000                                *
.*                     Display System Messages                        *
.********************************************************************** 
.
MESS0000  CALL      RSYSMESM
.
          DISPLAY   *P1:19,*ULON,*RPTCHAR SP1:80,*HOFF:
                    *P1:21,*V2LON,SP1,SYSMES1:
                    *P1:22,*V2LON,SP1,SYSMES2:
                    *P1:23,*V2LON,SP1,SYSMES3
.
          RETURN
.
.********************************************************************** 
.*                            IBAM0000                                *
.*                      Display User's Menu                           *
.********************************************************************** 
.
IBAM0000  MOVE      PRGID,IBAPPROG
          CALL      WRPORT1                 * In FKEYUMEN
.
          CALL      DIRT0000                * Check for Direct to System
.
          CALL      CLAR0000                * Clear the Array
.
          DISPLAY   *ES,PRGID,*P25:1,CONAME:
                    *P72:1,CDATE:
                    *P1:2,VERSION,*P25:2,"SYSTEM CONTROL MENU":
                    *P72:2,"PORT: ",PORT:
                    *P1:19,*ULON,*RPTCHAR SP1:80
.
          CALL      MESS0000                * Display System Messages
.
          MOVE      ONE,CCOL
          MOVE      FOUR,CVERT
          MOVE      ONE,SCRNOPTN
.
          PACK      KEY7,SECODE,SP30
          CALL      RSIBUSE1
.
IBAM1000  CALL      RKIBUSE1
          BRANCH    OVRCD,IBAM3000
.
          MATCH     SECODE,IBUSCODE
          GOTO      IBAM3000 IF NOT EQUAL
.
          CMATCH    "0",IBUSLEVL
          GOTO      IBAM1000 IF EQUAL
.
          PACK      KEY3,IBUSSYST
          CALL      RDIBSMA1
          BRANCH    OVRCD,IBAM1000
.
          DISPLAY   *PCCOL:CVERT,*V2LON,SCRNOPTN,*HOFF,DOT,SP1,IBSMNATV;
.
          MOVE      IBUSLEVL,SECLEVL[SCRNOPTN]
          MOVE      IBUSSYST,SYSTEM[SCRNOPTN]
          ADD       ONE,CVERT
          ADD       ONE,SCRNOPTN
.
          COMPARE   TEN7,CVERT
          GOTO      IBAM1000 IF LESS
.
          COMPARE   SIXTY,CCOL
          GOTO      IBAM2000 IF NOT LESS
.
          ADD       TWENTY6,CCOL
          MOVE      FOUR,CVERT
          GOTO      IBAM1000
.
IBAM2000  DISPLAY   *P10:18,*EL,"Display More (",*V2LON,ANSY,*HOFF,SLASH:
                            *V2LON,ANSN,*HOFF,") : ";
.
IBAM2050  KEYIN     *P31:18,*EL,*DV,UNDLN1:
                    *P31:18,*V2LON,ANS;
.
          MATCH     ANSY,ANS
          GOTO      IBAM2100 IF EQUAL
.
          MATCH     ANSN,ANS
          GOTO      IBAM3000 IF EQUAL
.
          BEEP
          GOTO      IBAM2050
.
IBAM2100  MOVE      ONE,CCOL
          MOVE      FOUR,CVERT
.
          CALL      CLAR0000                * Clear the Sec. Level Array
.
          GOTO      IBAM1000
.
IBAM3000  DISPLAY   *P1:18,*EL,"Please Select, (",*V2LON,"E",*HOFF,")xit : ":
                    *P25:18,UNDLN2;
.
          COMPARE   ZERO,CTIMEOUT
          GOTO      IBAM3100 IF EQUAL
.
          KEYIN     *P25:18,*EL,*DV,UNDLN2:
                    *P25:18,*V2LON,*JR,*T1.CTIMEOUT,SELECT
.
          GOTO      IBAM4000 IF NOT LESS
          GOTO      IBAM9000
.
IBAM3100  KEYIN     *P25:18,*EL,*DV,UNDLN2:
                    *P25:18,*V2LON,*JR,SELECT;
.
IBAM4000  RESET     SELECT
          GOTO      IBAM4050 IF EOS
.
          TYPE      SELECT
          GOTO      IBAM4100 IF EQUAL
.
          REP       UPPLOW,SELECT
.
          MATCH     " E",SELECT
          GOTO      IBAM9000 IF EQUAL
.
          MATCH     "*O",SELECT
          GOTO      IBAM4020 IF NOT EQUAL
.
          CALL      OPEN0000                     * Open a Spool File
          GOTO      IBAM0000                     * Redisplay the Menu

IBAM4020  MATCH     "*C",SELECT
          GOTO      IBAM4050 IF NOT EQUAL
.
          CALL      CLOS0000                     * Open a Spool File
          GOTO      IBAM0000                     * Redisplay the Menu

IBAM4050  BEEP
          GOTO      IBAM3000
.
IBAM4100  MOVE      SELECT,FORM2
          COMPARE   ZERO,SECLEVL[FORM2]
          GOTO      IBAM4050 IF EQUAL
.
          PACK      KEY7,SECODE,SYSTEM[FORM2]
          CALL      RDIBUSE1
          BRANCH    OVRCD,IBAM4050
.
          MOVE      IBUSMPRO,CCMENU
          MOVE      IBUSLEVL,SECLEV
          MOVE      FALSE,EXIT
          GOTO      IBAM9999
.
IBAM9000  MOVE      TRUE,EXIT
.
IBAM9999  RETURN
.
.********************************************************************** 
.*                            DIRT0000                                *
.*                      Check for Direct to System                    *
.********************************************************************** 
.
DIRT0000  MOVE      FALSE,EXIT
          MOVE      ZERO,NOINIMNU
.
          PACK      KEY7,SECODE,SP30
          CALL      RSIBUSE1
.
DIRT1000  CALL      RKIBUSE1
          BRANCH    OVRCD,DIRT2000
.
          MATCH     SECODE,IBUSCODE
          GOTO      DIRT2000 IF NOT EQUAL
.
          MATCH     "0",IBUSLEVL
          GOTO      DIRT1000 IF EQUAL
.
          ADD       ONE,NOINIMNU
.
          MOVE      IBUSMPRO,CCMENU
          MOVE      IBUSLEVL,SECLEV
.
          COMPARE   TWO,NOINIMNU
          GOTO      DIRT9999 IF EQUAL
.
          GOTO      DIRT1000
.
DIRT2000  COMPARE   ONE,NOINIMNU
          GOTO      DIRT9999 IF NOT EQUAL
.
          BRANCH    FIRSTTIM,DIRT3000       * Exit From Direct System
.
          CHAIN     CCMENU
DIRT3000  CHAIN     "ANSWER"
.
DIRT9999  RETURN
.
.********************************************************************** 
.*                            CLAR0000                                *
.*                      Clear the Sec. Level Array                    *
.********************************************************************** 
.
CLAR0000  MOVE      ONE,FORM2
.
          WHILE     FORM2<99
            MOVE      ZERO,SECLEVL[FORM2]
            MOVE      SP3,SYSTEM[FORM2]
            ADD       ONE,FORM2
          DO
.
          RETURN
.
.********************************************************************** 
.*                              OPEN0000                              *
.*                          Open a Spoolfile                          *
.********************************************************************** 
.
OPEN0000  BRANCH    CPRTFLG OF OPEN1000,OPEN8000,OPEN1000
.
OPEN1000  DISPLAY   *P1:21,*EF
          MOVE      ZERO,CPRTFLG     * Always allow spooling from FKEYUMEN
          MOVE      "SF",NCURPRT
.
          CALL      SPOOLING
.
          MOVE      TWO,CPRTFLG      * Indicates spooling from FKEYUMEN
          GOTO      OPEN9999
.
OPEN8000  DISPLAY   *P1:24,*B,*V2LON,"** Spooling Currently Active **",*W2;
.
OPEN9999  RETURN
.
.********************************************************************** 
.*                              CLOS0000                              *
.*                         Close a Spoolfile                          *
.********************************************************************** 
.
CLOS0000  BRANCH    CPRTFLG OF CLOS9999,CLOS2000,CLOS1000
.
CLOS1000  DISPLAY   *P1:24,*B,*V2LON,"** Spooling Not Currently Active **",*W2;
          GOTO      CLOS9999
.
CLOS2000  SPLCLOSE
          READ      CONTROLF,TWO;*249,SPOOL
          MOVE      SPOOL,CPRTFLG
          MOVE      SP8,SPLFILE
          DISPLAY   *P1:24,*B,*V2LON,"** Spoolfile Now Closed **",*W2;
          GOTO      CLOS9999
.
CLOS3000  DISPLAY   *P1:24,*B,*V2LON,"** Spooling Not Available **",*W2;
.
CLOS9999  RETURN
.
.********************************************************************** 
.*                              NEWM0000                              *
.*       Display the Users Customized Menu From the Screen Paiter     *
.********************************************************************** 
.
NEWM0000  CALL      NIMN0000                * Determine No. Entries in FKEYUMEN
.
          BRANCH    NOINIMNU,NEWM8000       * If 1 then direct to system
.
          DISPLAY   *ES,PRGID,*P25:1,CONAME:
                    *P72:1,CDATE:
                    *P1:2,VERSION,*P25:2,"SYSTEM CONTROL MENU":
                    *P72:2,"PORT: ",PORT;
          MOVE      THREE,CVERT
          PACK      KEY5,SECMNNUM,SP5
          CALL      RSIBMDP1
.
NEWM1000  CALL      RKIBMDP1
          BRANCH    OVRCD,NEWM2000
.
          MATCH     IBMDMENU,SECMNNUM
          GOTO      NEWM2000 IF NOT EQUAL
.
          PUTVAR    IBMDDATA,IBMDATTR,1,CVERT
.
          ADD       ONE,CVERT
          GOTO      NEWM1000
.
NEWM2000  CALL      MESS0000                     * Display System Messages
.
          DISPLAY   *P1:18,*EL,"Please Select, (",*V2LON,"E",*HOFF,")xit : "
.
NEWM2500  DISPLAY   *P25:18,UNDLN2
.
          COMPARE   ZERO,CTIMEOUT
          GOTO      NEWM3000 IF NOT EQUAL
.
          KEYIN     *P25:18,*V2LON,*JR,SELECT
          GOTO      NEWM4000
.
NEWM3000  MOVE      SP2,SELECT
          KEYIN     *P25:18,*V2LON,*JR,*T60,SELECT;
.
          GOTO      NEWM4000 IF NOT LESS
          SUB       ONE,CTIMEOUT
.
          COMPARE   CTIMEOUT,ZERO
          GOTO      NEWM3000 IF LESS
.
. Exit out of Menu as Time-Out Has Occured.
. The GOTO ML9999 is a way to clean up the unique port number
.
          GOTO      ML9999
.
NEWM4000  RESET     SELECT
          GOTO      NEWM5000 IF EOS
.
          TYPE      SELECT
          GOTO      NEWM6000 IF EQUAL
.
          REP       UPPLOW,SELECT
.
          MATCH     " E",SELECT
          GOTO      NEWM9000 IF EQUAL
.
          MATCH     " e",SELECT
          GOTO      NEWM9000 IF EQUAL
.
          MATCH     "*O",SELECT
          GOTO      NEWM4020 IF NOT EQUAL
.
          CALL      OPEN0000                     * Open a Spool File
          GOTO      NEWM0000                     * Redisplay the Menu

NEWM4020  MATCH     "*C",SELECT
          GOTO      NEWM5000 IF NOT EQUAL
.
          CALL      CLOS0000                     * Open a Spool File
          GOTO      IBAM0000                     * Redisplay the Menu
.
NEWM5000  BEEP
          GOTO      NEWM2500
.
NEWM6000  PACK      KEY6,SECMNNUM,SP1,SELECT
          CALL      RDIBMSL1
          BRANCH    OVRCD,NEWM5000               * Beep & Re-Key
.
          PACK      KEY7,PASSCODE,IBMSNUMB
          CALL      RDIBUSE1
          BRANCH    OVRCD,NEWM8900
.
          MOVE      IBUSMPRO,CCMENU
          MOVE      IBUSLEVL,SECLEV
.
          MOVE      FALSE,EXIT
          GOTO      NEWM9999
.
NEWM8000  BRANCH    FIRSTTIM,NEWM8990       * Not 1st Time so Exit
.
          PACK      KEY6,SECMNNUM,SP6
          CALL      RSIBMSL1
          CALL      RKIBMSL1
.
          PACK      KEY7,PASSCODE,IBMSNUMB
          CALL      RDIBUSE1
          BRANCH    OVRCD,NEWM8900
.
          MOVE      IBUSMPRO,CCMENU
          MOVE      IBUSLEVL,SECLEV
.
          MOVE      FALSE,EXIT
          GOTO      NEWM9999
.
NEWM8900  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON:
                    *P30:24,"*****  System  Failure  *****";
NEWM8990  SHUTDOWN  " "
.
NEWM9000  MOVE      TRUE,EXIT
.
NEWM9999  RETURN
.
.********************************************************************** 
.*                            NIMN0000                                *
.*               Determine the No. of Records in File                 *
.********************************************************************** 
.
NIMN0000  MOVE      ZERO,NOINIMNU
.
          PACK      KEY6,SECMNNUM,SP30
          CALL      RSIBMSL1
.
NIMN1000  CALL      RKIBMSL1
          BRANCH    OVRCD,NIMN9999
.
          MATCH     SECMNNUM,IBMSMENU
          GOTO      NIMN9999 IF NOT EQUAL
.
          ADD       ONE,NOINIMNU
          GOTO      NIMN1000
.
NIMN9999  RETURN
.
.********************************************************************** 
.*                          I/O Includes                              *
.********************************************************************** 
.
.
          INC       STD001IO
          INC       IBAPRINT
.
          INC       BCSYSMES
.
          INC       IBAMDPIO/INC
          INC       IBAMSLIO/INC
          INC       IBASMAIO/INC
          INC       IBAUSEIO/INC
.
          INC       IBAPORIO/INC       **** PORT SCANNING
          INC       IBAPRNIO/INC
          INC       IBASECIO/INC
.
.

.***************************************************************************
.* System    :   Theatre/Billing                                           *
.* Program   :   IBAOPR94                                                  *
.* Desc      :   CMBS/Theatre Confirmation Report                          *
.***************************************************************************
.* Date      :   02/10/2007                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :   This program will print a report of CMBS Items for a      *
.*               selected group of Consultants/dates                       *
.* Modifications :                                                         *
.***************************************************************************
.* V11.03.02 19/04/2023  Thanh T        TSK 0909393                        *
.*           Changed PROC1000 to use RDOPSES1 instead of RDOPSES7.         *
.*           Changed PROC0000 to use RSOPDEA6 instead of RSOPDEA1          *
.* V11.03.01 20/03/2023  PJ Le Febour   TSK 0909393a                       *
.*           Amended KEY19 to KEY22 for theatre index changes              *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       OPRDEAFD/INC
          INC       OPRSESFD/INC
          INC       IBACONFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       PATVISFD/INC
          INC       PMSVX1FD/INC
          INC       PMSMTIFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CMBSSTAT  DIM       1
DOCCODE   DIM       6
DOCNAME   DIM       25
DSPMONTH  DIM       9
ENDDATE   DIM       8
ENDSURG   DIM       6
ESURDESC  DIM       40
HOSCODE   DIM       3
PATNAME   DIM       30
STRTDATE  DIM       8
STRTSURG  DIM       6
SSURDESC  DIM       40
THEATRES  DIM       1
.
JAN       INIT      "January"
FEB       INIT      "February"
MAR       INIT      "March"
APRIL     INIT      "April"
MAY       INIT      "May"
JUNE      INIT      "June"
JULY      INIT      "July"
AUG       INIT      "August"
SEPT      INIT      "September"
OCT       INIT      "October"
NOV       INIT      "November"
DEC       INIT      "December"
.
PRGID     INIT      "IBAOPR94"
PRGNAM    INIT      "CMBS/Theatre Confirmation Report"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmsmtiaf";
          OPEN      PMSMTIA1,"pmsmtiaf"
.
          DISPLAY   *P64:24,"oprdetaf";
          OPEN      OPRDETA6,"oprdetaf"
.
          DISPLAY   *P64:24,"oprsesaf";
          OPEN      OPRSESA1,"oprsesaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          MOVE      ZERO,CLNO
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
OPTN0000  MOVE      ZERO,CEXIT
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS = 1
            OPEN      PATHSPA1,"pathspaf"
.
            DISPLAY   *P5:5,*EF,"Hospital Id              : ";
            MOVE      THIRTY2,ECOL
            MOVE      FIVE,EVERT
            MOVE      SP3,CKYIDEF3
            MOVE      ONE,CKYIMAND               * set for mandatory
            CALL      PATHSPKY                   * get hospital
            COMPARE   ONE,EXIT                   * exit selected
            GOTO      OPTN9999 IF EQUAL
.
            MOVE      KEY3,HOSCODE
            DISPLAY   *P32:5,*EL,PTHSNAME;
          ENDIF
.
          DISPLAY   *P5:6,"Starting Date            : ":
                    *P5:7,"Ending Date              : "
          CALL      STRT0000                  * start date
          BRANCH    EXIT,OPTN9999
.
          CALL      ENDD0000                  * end date
          BRANCH    EXIT,OPTN0000
.
          DISPLAY   *P5:8,"Starting Surgeon Code    : ":
                    *P5:9,"Ending Surgeon Code      : "
          CALL      KSSU0000             * Keyin start surgeon
          CALL      KESU0000             * Keyin ending surgeon
          KEYIN     *P5:10,"Theatre Status           :",THEATRES
          PACK      THEATRES,THEATRES,SP70
          KEYIN     *P5:11,"CMBS Status              :",CMBSSTAT
          PACK      CMBSSTAT,CMBSSTAT,SP70
.
OPTN9999  RETURN
+
.****************************************************************************
.*                               PROC0000                                   *
.*                       Process report                                     *
.****************************************************************************
PROC0000  CALL      HEAD0000
.
          MATCH     "Start",STRTSURG
          IF        @EQUAL
            MOVE      SP70,STRTSURG
          ENDIF
.
          MATCH     "Finish",ENDSURG
          IF        @EQUAL
            MOVE      SP70,ENDSURG
          ENDIF
.
          PACK      KEY26,STRTDATE,SP70
          CALL      RSOPDEA6
PROC1000  CALL      RKOPDEA6
          BRANCH    OVRCD,PROC9999
.
          MATCH     OPDADATE,ENDDATE
          GOTO      PROC9999 IF LESS
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
          CALL      RDOPSES1
          BRANCH    OVRCD,PROC1000
.
          COMPARE   THREE,OPDASTAT
          GOTO      PROC1000 IF EQUAL
.
          MATCH     SP70,STRTSURG
          IF        !@EQUAL
            MATCH     OPDACLIN,STRTSURG
            GOTO      PROC1500 IF EQUAL
            GOTO      PROC1000 IF LESS
          ENDIF
.
          MATCH     SP70,ENDSURG
          IF        !@EQUAL
            MATCH     OPDACLIN,ENDSURG
            GOTO      PROC1500 IF EQUAL
            GOTO      PROC1000 IF NOT LESS
          ENDIF
.
PROC1500  PACK      KEY8,OPDAADMN,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,PROC1000
.
          PACK      KEY8,OPDAURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PROC1000
          CALL      PATN0000
.
          PACK      KEY6,OPDACLIN,SP70
          CALL      RDDOCT1
          CALL      FDOC0000
.
          UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MATCH     SP70,THEATRES
          GOTO      PROC2000 IF EQUAL
.
          MATCH     "1",THEATRES
          IF        @EQUAL
            MATCH     "1",PMVXATCP
            GOTO      PROC1000 IF NOT EQUAL
          ENDIF
.
          MATCH     "2",THEATRES
          IF        @EQUAL
            MATCH     "1",PMVXATCP
            GOTO      PROC1000 IF EQUAL
          ENDIF
.
PROC2000  IF        IBCNMHOS=1
            MATCH     HOSCODE,OPSEHOSP     * Check hospital 
            GOTO      PROC1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,CMBSSTAT
          GOTO      PROC3000 IF EQUAL
.
          PACK      KEY14,OPDAADMN,SP70
          CALL      RSPMMTI1
          GOTO      PROC3500
.
PROC3000  COMPARE   FIFTY5,CLNO
          CALL      HEAD0000 IF NOT LESS
          PRINT     *N,*1,"|",OPDAURNO,*12,"|",OPDAADMN,*21,"|",PATNAME:
                    *52,"|",DOCNAME;
.
          PACK      KEY14,OPDAADMN,SP70
          CALL      RSPMMTI1
PROC3200  CALL      RKPMMTI1
          BRANCH    OVRCD,PROC5000
.
          MATCH     OPDAADMN,PMMIVISN
          GOTO      PROC5000 IF NOT EQUAL
.
          MATCH     OPDAUNIQ,PMMIUNIQ
          GOTO      PROC5000 IF NOT EQUAL
.
          MATCH     "2",PMMIRTYP
          GOTO      PROC3200 IF NOT EQUAL
.
          PRINT     *78,"|",CPCDATE,*90,"|",OPDAEXPS:
                    *100,"|",PMMIITEM,*110,"|";          
.
          MATCH     "1",PMMICCON
          IF        @EQUAL
            PRINT     *111,"Yes",*121,"|";
          ELSE
            PRINT     *111,"No",*121,"|";
          ENDIF
.
          MATCH     "1",PMVXATCP
          IF        @EQUAL
            PRINT     *122,"Yes",*132,"|";
          ELSE
            PRINT     *122,"No",*132,"|";
          ENDIF
          ADD       ONE,CLNO
.
PROC3500  CALL      RKPMMTI1
          BRANCH    OVRCD,PROC1000
.
          MATCH     OPDAADMN,PMMIVISN
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     OPDAUNIQ,PMMIUNIQ
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     "2",PMMIRTYP
          GOTO      PROC3500 IF NOT EQUAL
.
          MATCH     SP70,CMBSSTAT
          GOTO      PROC4000 IF EQUAL
.
          MATCH     "1",CMBSSTAT
          IF        @EQUAL
            MATCH     "1",PMMICCON
            GOTO      PROC3500 IF NOT EQUAL
          ENDIF
.
          MATCH     "2",CMBSSTAT
          IF        @EQUAL
            MATCH     "1",PMMICCON
            GOTO      PROC3500 IF EQUAL
          ENDIF
.
PROC4000  COMPARE   FIFTY5,CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *N,*1,"|",OPDAURNO,*12,"|",OPDAADMN,*21,"|",PATNAME:
                    *52,"|",DOCNAME:
                    *78,"|",CPCDATE,*90,"|",OPDAEXPS:
                    *100,"|",PMMIITEM,*110,"|";
.
          MATCH     "1",PMMICCON
          IF        @EQUAL
            PRINT     *111,"Yes",*121,"|";
          ELSE
            PRINT     *111,"No",*121,"|";
          ENDIF
.
          MATCH     "1",PMVXATCP
          IF        @EQUAL
            PRINT     *122,"Yes",*132,"|";
          ELSE
            PRINT     *122,"No",*132,"|";
          ENDIF
          ADD       ONE,CLNO
          GOTO      PROC3500
.
PROC5000  COMPARE   FIFTY5,CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *78,"|",CPCDATE,*90,"|",OPDAEXPS,*100,"|",*110,"|":
                    *121,"|";
          MATCH     "1",PMVXATCP
          IF        @EQUAL
            PRINT     *122,"Yes",*132,"|";
          ELSE
            PRINT     *122,"No",*132,"|";
          ENDIF
          ADD       ONE,CLNO
          GOTO      PROC1000
.
PROC9999  CALL      DASH0000
          PRINT     *N,"***** End of Report *****"
          RETURN
+
.****************************************************************************
.*                               STRT0000                                   *
.*                       enter start date                                   *
.****************************************************************************
STRT0000  MOVE      THIRTY2,CCOL
          MOVE      SIX,CVERT
          MOVE      CCC,CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          CALL      KEYDATE
.
          MATCH     SP2,CDAY
          GOTO      STRT9000 IF EQUAL
          BRANCH    OVRCD,STRT9000
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          CALL      PACDATE
          MOVE      FALSE,EXIT
          GOTO      STRT9999
.
STRT9000  MOVE      TRUE,EXIT
          GOTO      STRT9999
.
STRT9999  RETURN
+
.****************************************************************************
.*                               ENDD0000                                   *
.*                       enter end date                                     *
.****************************************************************************
ENDD0000  MOVE      THIRTY2,CCOL
          MOVE      SEVEN,CVERT
          MOVE      CCC,CCENT
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          CALL      KEYDATE
.
          MATCH     SP2,CDAY
          GOTO      ENDD5000 IF NOT EQUAL
          MOVE      TRUE,EXIT
          GOTO      ENDD9999
.
ENDD5000  PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE
          MATCH     STRTDATE,ENDDATE
          GOTO      ENDD9200 IF LESS
          GOTO      ENDD9999
.
ENDD9000  DISPLAY   *P1:24,*EL,*B,"Invalid date - ";
          CALL      HITENTER
          GOTO      ENDD0000
.
ENDD9200  DISPLAY   *P1:24,*EL,*B,"TO date cannot be before FROM date. ";
          CALL      HITENTER
          GOTO      ENDD0000
.
ENDD9999  RETURN
+
.**********************************************************************
.*                         KSSU0000                                   *
.*                  Keyin the starting Surgeon                        *
.**********************************************************************
.
KSSU0000  MOVE      SP10,STRTSURG
          MOVE      "32",ECOL
          MOVE      "08",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,KSSU0500,KSSU0000
.
          MOVE      DCODE,STRTSURG
          GOTO      KSSU1000
.
KSSU0500  MOVE      "Start ",STRTSURG
          DISPLAY   *P32:8,*V2LON,STRTSURG
          CLEAR     SSURDESC
          GOTO      KSSU9999
.
KSSU1000  PACK      KEY6,STRTSURG,SP6
          CALL      RDDOCT1
          CALL      FDOC0000             * Format doctors name
.
          MOVE      PACFNAME,SSURDESC
          DISPLAY   *P32:8,*EL,*V2LON,STRTSURG:
                    *P40:8,*HOFF,SSURDESC
          GOTO      KSSU9999
.
KSSU4000
          BEEP
          DISPLAY  *P1:24,*EL,"Code is not on file. ";
          CALL     HITENTER
          GOTO     KSSU0000
.
KSSU9999  RETURN
.
.
.**********************************************************************
.*                         KESU0000                                   *
.*                  Keyin the ending surgeon                          *
.**********************************************************************
KESU0000  MOVE      SP10,ENDSURG
          MOVE      "32",ECOL
          MOVE      "09",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,KESU0500,KESU0000
.
          MOVE      DCODE,ENDSURG
          GOTO      KESU1000
.
KESU0500  MOVE      "Finish",ENDSURG
          DISPLAY   *P32:9,*V2LON,ENDSURG
          CLEAR     ESURDESC
          MOVE      ZERO,EXIT
          GOTO      KESU9999
.
KESU1000  PACK      KEY6,ENDSURG,SP6
          CALL      RDDOCT1
          CALL      FDOC0000
          MOVE      PACFNAME,ESURDESC
          DISPLAY   *P32:9,*EL,*V2LON,ENDSURG:
                    *P40:9,*HOFF,ESURDESC
.
.**** test that end code >= start code ****
.
          MATCH     "Start ",STRTSURG
          GOTO      KESU9999 IF EQUAL
.
          MATCH     ENDSURG,STRTSURG
          GOTO      KESU9999 IF LESS
          GOTO      KESU9999 IF EQUAL
          BEEP
          DISPLAY   *P1:24,*EL,"Starting code must be > or = ending code. ";
          CALL      HITENTER
          DISPLAY   *P40:9,SP30

          GOTO      KESU0000
.
KESU9999  RETURN
.
.**************************************************************************
.*                           PATN0000                                     *
.*                     Format the patients name                           *
.**************************************************************************
PATN0000  MOVE      TWO,PACFRMT
          MOVE      PTITL,PACTITLE
          MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME
PATN9999  RETURN
+
.*********************************************************************
.*                        FDOC0000                                   *
.*                 Format the doctors name                           *
.*********************************************************************
.
FDOC0000  MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DOCNAME
FDOC9999  RETURN
.**************************************************************************
.*                  HEAD0000                                              *
.*            print the heading                                           *
.**************************************************************************
HEAD0000  MOVE      ONE,CNOUNDLN
          IF        CLNO<>0
            CALL      DASH0000
            PRINT     *N
          ENDIF
          MOVE      ZERO,CLNO
          CALL      IBACLOCK
.
          CALL      HEAD132
.
          IF          IBCNMHOS =1
            PRINT     *N,*1,"Hospital : ",PTHSNAME
          ENDIF
.
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,FORM2           * move month to a form field
          LOAD      DSPMONTH,FORM2,JAN,FEB,MAR,APRIL,MAY,JUNE:
                                   JULY,AUG,SEPT,OCT,NOV,DEC
.
          PRINT     *N,*1,"Date From : ",CDAY,SP1,DSPMONTH,SP1,CCENT,CYEAR;
.
          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,FORM2           * move month to a form field
          LOAD      DSPMONTH,FORM2,JAN,FEB,MAR,APRIL,MAY,JUNE:
                                   JULY,AUG,SEPT,OCT,NOV,DEC
.
          PRINT     *60,"Date To : ",CDAY,SP1,DSPMONTH,SP1,CCENT,CYEAR;
.
.
HEAD1000  PRINT     *N,*1,"Consultant From : ",SSURDESC:
                    *60,"Consultant To : ",ESURDESC;
.
          CALL      DASH0000
          PRINT     *N,*1,"|UR Number",*12,"|Visit",*21,"|Patient Name":
                    *52,"|Doctor",*78,"|Date of":
                    *90,"|Start",*100,"|CMBS",*110,"|CMBS",*121,"|Theatre":
                    *132,"|":
                    *N,*1,"|",*12,"|Number",*21,"|":
                    *52,"|",*78,"|Operation",*90,"|Time of":
                    *100,"|Item",*110,"|Confirmed",*121,"|Complete",*132,"|":
                    *N,*1,"|",*12,"|",*21,"|":
                    *52,"|",*78,"|",*90,"|Operation",*100,"|":
                    *110,"|Status",*121,"|Status",*132,"|";
.
          CALL      DASH0000
          ADD       TEN,CLNO
.
HEAD9999  RETURN
+
***************************************************************************
.*                           DASH0000                                     *
.*                  print 132 dashes                                      *
***************************************************************************
DASH0000  PRINT     *N,"----------------------------------------":
                    "----------------------------------------":
                    "----------------------------------------":
                    "------------";
DASH9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATDOCKY
          INC       PATHSPKY
          INC       PATCODIO/INC
          INC       PATHSPIO/INC
          INC       OPRDEAIO/INC
          INC       OPRSESIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC
          INC       PATVISIO/INC
          INC       PMSVX1IO/INC
          INC       PMSMTIIO/INC

.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBABIL38                                             *
.*                                                                            *
.*     FUNCTION:         LENGTH OF STAY REPORT  2                             *
.*                                                                            *
.*     DATE WRITTEN:     11/02/88                                             *
.*                                                                            *
.*     AUTHOR:           J.TAN                                                *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                                                                            *
.*        V12.00.01 30/05/2025  Don Nguyen    TSK 0955096                     *
.*                  Alphanumeric visit number changes                         *
.*        V11.05.01 17.12.2024  DA Sarkies    TSK 0951086                     *
.*                  Increased size of TOTDAY                                  *
.*        V10.05.01 02/02/2015  Ania P        CAR 261630                      *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V5.08.03  05/12/2000  Caleb Sharman                                 *
.*                  Mods to use keyin routines for claim codes and health funds
.*        V5.08.02  16/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.01  06/06/2000  Jill Habasque                                 *
.*                  Changed from key10 to key14 for the health fund file      *
.*                    V5.07.03  01/09/1999  Steve Downing                     *
.*                              Fixed create and open temp index file         *
.*                    V5.07.02  13/08/1999  Steve Downing                     *
.*                              Redundant code removed from date calculations *
.*                    V5.07.01  26/07/1999  Steve Downing                     *
.*                              Century included in date calculations         *
.*                              CALC routine simplified                       *
.*                   V5.07.B02  27/01/1999  Nicole Harrington 507 rebounds    *
.*                              Mods to print century                         *
.*                   V5.07.B01  11/11/1998  Jim Stathopoulos                  *
.*                              507 Changes                                   *
.******************************************************************************
.*                       V5.01  28/02/89 J.Tan                                *
.*                              Fixed total no of patients                    *
.*                       V5.02  09/03/89 J.Tan                                *
.*                              Fixed zero length of stay                     *
.*                       V5.03  05/05/89 Graeme Williams  SRF 4219            *
.*                              Added PINVCADM to inv file                    *
.*                       V5.04  22/05/89 S.O'Gorman                           *
.*                              Modify Display for 8 Digit U/R & Admission Nos*
.*                              Include STD001                                *
.*                       V5.05  27/05/89 J.Tan            SRF 100750          *
.*                              Fixed Mother Adm. to Form 8 (PATMI1FD).       *
.*                       V5.06  06/06/89 J.Tan            SRF 100809          *
.*                              Mods Locking master file (PATIOMAS).          *
.*                                                                            *
.*                    V5.01.01  06/07/89  S.O'Gorman                          *
.*                              Converted for N.Z.                            *
.*                    V5.01.02  22/09/89 Graeme Williams  SRF 101186          *
.*                              Seperated Day Cases                           *
.*                    V5.01.03  23/05/90 J.Tan            SRF 104901          *
.*                              Mods PATFN1FD                                 *
.*                    V5.01.121 27/11/90 Glen Hobby                           *
.*                              Recompiled for changes to PATMX1FD            *
.*                    V5.01.122 07/12/90 i chung          SRF 106846          *
.*                              Changed header - "Number of Admissions" to    *
.*                                               "Number of Discharges"       *
.*                                                                            *
.*        V5.01.123 23.02.92 Neeriem "I Hate Support" Dye (I.B.A.)            *
.*                  Modify to use standard routine KEYDATE                    *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       PATDSCFD/INC
          INC       PATFN1FD/INC
          INC       PATINVFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
.         Temporary index file for the report
.
PSTAY     IFILE      SQL, FIXED=23, KEY="U1-12"
.
.Name     Type       Size       Physical     Start
.----     ----       ----       --------     -----
.ACLAIM   DIM         3            3           1
.AFUNDH   DIM         6            6           4
.DLSTAY   DIM         3            3          10
.TOTPAT   FORM        8            5          13
.BEDDAY   FORM        8            5          18
.                                  EOR        23
.
.               VARIABLES DEFINITIONS
.
PSTSRT   FILE      ASCII, FIXED=256
PSTROL   FILE      ASCII, FIXED=256
.
FNAMEU   DIM     8
FNAMER   DIM     8
FNAMEI   DIM     8
FIRST    FORM    1
.
STATUS   FORM    1
OPCND    FORM    1
CMDLINE  DIM     50
.
OYEAR           FORM    "365"
LYEAR           FORM    "366"
LYR             FORM    1
.
.
.
CFEETYP         FORM    1              * for PATFNDKY
CODE            DIM     2              * for PATCODKY
DLSTAY          DIM     3
DIM9            DIM     9
DIM9A           DIM     9
DEDCLM          DIM     6
DSTCLM          DIM     6
DEDHF           DIM     6
DSTHF           DIM     6
.
WSTAT           FORM    1
.
CHG             FORM    1
.
XDAY1           DIM     2
XMON1           DIM     2
XYEAR1          DIM     2
XCENT1          DIM     2
XDAY2           DIM     2
XMON2           DIM     2
xyear2          DIM     2
XCENT2          DIM     2
.
JDAYS           FORM    3
JYEAR           FORM    2
.
WDATE1          DIM     8        
WDATE2          DIM     8        
CUDATE          DIM     8        
WDATE           DIM     8       
.
FROMDAT         DIM     8
TODAT           DIM     8
FROMDATE        DIM     8        * FROM DATE IN (CCYYMMDD) FORMAT
TODATE          DIM     8        * TO DATE IN (CCYYMMDD) FORMAT
.
BEDDAY          FORM    8
CUMBDAY         FORM    8
LSTAY           FORM    3
LAST            FORM    1
PCBDAY          FORM    8.2
.
STCLM     DIM       3
EDCLM     DIM       3
STHF      DIM       6
EDHF      DIM       6
SAVCL     DIM       3
SAVHF     DIM       6
SAVLST    FORM      3
SUBTOT1   FORM      8
SUBTOT2   FORM      8
SUBTOT3   FORM      8
SUBTOT4   FORM      8.2
GRDTOT1   FORM      8
GRDTOT2   FORM      8
GRDTOT3   FORM      8
GRDTOT4   FORM      8.2
.
TEMPDT    DIM       6
TOTDAY    FORM      4
TOTPAT    FORM      8
TOTBDAY   FORM      8
VERT      FORM      2
.
FNAME     INIT      "pstayf"          * set up for temp file name
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
UKEY      INIT      " 23 U1-12"    * set up for record length & relevant key
.
PRGID     INIT      "IBABIL38"
PRGNAM    INIT      "Length Of Stay Report 2"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR3,"patinvrf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEI
.
          PACK      CUDATE WITH CCC,CYY,CMM,CDD
          REP       " 0",CUDATE
.
.         START OF PROGRAM
.
START     DISPLAY     *P1:4,*EF,"Enter Discharged Date From :":
                          *P1:5,"Enter Discharged Date To   :"
.
.         Keyin From Date
.
KADAT1    MOVE        FOUR,VERT
          MOVE        ZERO,IDAY
          CALL        KEYINDTE
.
          COMPARE     ZERO,IDAY
          GOTO        ENDIT IF EQUAL
.
          PACK        FROMDAT WITH ICENT,IYEAR,IMON,IDAY
          REP         " 0",FROMDAT
.
          MATCH       FROMDAT,CUDATE
          GOTO        INVDTE1 IF LESS
.
          UNPACK      FROMDAT WITH XCENT1,XYEAR1,XMON1,XDAY1
.
.         Keyin To Date
.
KADAT2    MOVE        FIVE,VERT
          CALL        KEYINDTE
.
          PACK        TODAT WITH ICENT,IYEAR,IMON,IDAY
          REP         " 0",TODAT
.
          MATCH       TODAT,CUDATE
          GOTO        INVDTE2 IF LESS
.
          MATCH       FROMDAT,TODAT
          GOTO        INVDATG IF LESS
.
          UNPACK      TODAT WITH XCENT2,XYEAR2,XMON2,XDAY2
.
.         Add one to the TODATE. This is because initially the program did not
.         include the end date, so it looks at all the data upto but including
.         the TODATE.
.
          MOVE        IDAY,DD
          MOVE        IMON,MM
          MOVE        IYEAR,YY
          MOVE        ICENT,CC
          CALL        DMYCON
.
          ADD         ONE,JULDAY
          MOVE        JULDAY,JWKDAY
          MOVE        JULYR,JWKYER
          MOVE        JULCC,JWKCC
          CALL        JULCON
.
          PACK        TODAT WITH CC,YY,MM,DD
          REP         " 0",TODAT
          GOTO        KEYCLM
.
INVDATG   DISPLAY    *P1:24,*B,*EL,"From Date must be less than or":
                                   " equal to To Date.  ";
          CALL       HITENTER
          GOTO       KADAT1
.
INVDTE1   DISPLAY    *P1:24,*B,*EL,"From Date must be less than or":
                                   " equal to today's date.  ";
          CALL       HITENTER
          GOTO       KADAT1
.
INVDTE2   DISPLAY    *P1:24,*B,*EL,"To Date must be less than or":
                                   " equal to today's date.  ";
          CALL       HITENTER
          GOTO       KADAT2
.
.         Keyin Claim Code range
.
KEYCLM    MOVE     SP3,STCLM
          DISPLAY  *P1:7,*EL,"Enter Starting Claim Code  : ";
.
          PACK     TDESC,SP20,SP20,SP20,SP20
          MOVE     SEVEN,EVERT
          MOVE     THIRTY,ECOL
          MOVE     ZERO,CKYIMAND
          MOVE     "CL",CODE
          CALL     PATCODKY
.
          MOVE     ACODE,STCLM
          MOVE     STCLM,DSTCLM
.
          MATCH    SP3,STCLM
          GOTO     KEYCLM1 IF NOT EQUAL
.
          MATCH    SP3,STCLM
          GOTO     KEYCLM1 IF NOT EQUAL
. 
          MOVE     "START",DSTCLM
.
KEYCLM1   DISPLAY  *P30:7,*V2LON,DSTCLM,SP5,TDESC
.
          MOVE     SP3,EDCLM
          PACK     TDESC,SP20,SP20,SP20,SP20
          DISPLAY  *P1:8,*EL,"Enter Ending   Claim Code  : ";
          MOVE     EIGHT,EVERT
          MOVE     THIRTY,ECOL 
          MOVE     ZERO,CKYIMAND
          MOVE     "CL",CODE
          CALL     PATCODKY
.
          MOVE     ACODE,EDCLM
          MOVE     EDCLM,DEDCLM
.
          MATCH    SP3,EDCLM
          GOTO     ECHK IF NOT EQUAL
.
          MOVE     "ZZZ",EDCLM
          MOVE     "FINISH",DEDCLM
.
ECHK      ENDSET   EDCLM
          APPEND   SP3,EDCLM
          RESET    EDCLM
.
          DISPLAY  *P30:8,*V2LON,DEDCLM,SP5,TDESC
.
          MATCH    STCLM,EDCLM
          GOTO     KEYHF IF NOT LESS
.
          DISPLAY  *P1:24,*EL,*B:
                   "Ending Claim must not be less than Starting Claim.  ";
          CALL     HITENTER
          GOTO     KEYCLM
.
.         Keyin Health Fund range
.
KEYHF     MOVE     SP6,STHF
          DISPLAY  *P1:10,*EL,"Enter Starting Health Fund : ";
.
          PACK     HFNAME,SP20,SP20,SP20,SP20
          MOVE     TEN,EVERT
          MOVE     THIRTY,ECOL
          MOVE     ZERO,CKYIMAND
          CALL     PATFNDKY
.
          MOVE     HCODE,STHF 
          MOVE     STHF,DSTHF
.
          MATCH    SP6,STHF 
          GOTO     KEYHF1 IF NOT EQUAL
.
          MOVE     "START",DSTHF
.
KEYHF1    DISPLAY  *P30:10,*V2LON,DSTHF,SP5,HFNAME
.
          MOVE     SP6,EDHF
          DISPLAY  *P1:11,*EL,"Enter Ending   Health Fund : ";
.
          PACK     HFNAME,SP20,SP20,SP20,SP20
          MOVE     TEN1,EVERT
          MOVE     THIRTY,ECOL
          MOVE     ZERO,CKYIMAND
          CALL     PATFNDKY
.
          MOVE     HCODE,EDHF 
          MOVE     EDHF,DEDHF
.
          MATCH    SP6,EDHF 
          GOTO     HCHK IF NOT EQUAL
.
          MOVE     "ZZZZZZ",EDHF
          MOVE     "FINISH",DEDHF
.
HCHK      ENDSET   EDHF
          APPEND   SP6,EDHF
          RESET    EDHF
.
          DISPLAY  *P30:11,*V2LON,DEDHF,HFNAME
.
          MATCH    STHF,EDHF
          GOTO     REKEY IF NOT LESS
.
          DISPLAY  *P1:24,*EL,*B:
                   "Ending H/Fund must not be less than Starting H/Fund.  ";
          CALL     HITENTER
          GOTO     KEYHF
.
.         Check if report parameters are OK or not
.
REKEY     KEYIN       *P1:24,*EF," Ok to Continue (":
                      *V2LON,"Y",*HOFF,"/":
                      *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP         UPPLOW,ANS
          MATCH       "N" TO ANS
          GOTO        START IF EQUAL
.
          MATCH       "Y" TO ANS
          GOTO        CNTCHK IF EQUAL
.
          BEEP
          GOTO       REKEY
.
.              PROCESS START
.
CNTCHK    DISPLAY    *P1:24,*EL,*P22:24,*V2LON:
                   "** Initialising Length of Stay File **",*W2;
          CALL       INDZD
          BRANCH     OPCND OF ENDIT
          CLOCK       TIME,CTIMEIS
.
          DISPLAY     *P1:24,*EL,*P22:24,*V2LON:
                      "** Processing Discharged Admissions **",*W2,*HOFF:
                      *P1:24,*EL,*P8:24,"Discharged Admission No selected :":
                      *P56:24,"Scanning :";
.
          PACK        KEY16 WITH FROMDAT,SP8
          CALL        RDSDSCH2
NEXT3     CALL        RDKDSCH2
          BRANCH      OVRCD OF REPORT
          DISPLAY     *P67:24,DADMNO;
.
          UNPACK      DDATE INTO XCENT,XYEAR,XMON,XDAY
          PACK        TEMPDT WITH XCENT,XYEAR,XMON,XDAY
          REP         " 0",TEMPDT
.
          MATCH       TODAT,TEMPDT
          GOTO        REPORT IF NOT LESS
.
NEXT4     MOVE        DADMNO,KEY8
          CALL        RDMISS1
          BRANCH      OVRCD OF NEXT3
.
          UNPACK      ADATE INTO XCENT,XYEAR,XMON,XDAY
          PACK        WDATE WITH XCENT,XYEAR,XMON,XDAY
          REP         " 0",WDATE
.
          MATCH       TODAT,WDATE
          GOTO        NEXT3 IF NOT LESS
.
.          CHECK FOR THE CLAIM CODE AND HEALTH FUND
.
          MATCH       STCLM,ACLAIM
          GOTO        NEXT3 IF LESS
.
          MATCH       ACLAIM,EDCLM
          GOTO        NEXT3 IF LESS
.
          MATCH       STHF,AFUNDH
          GOTO        NEXT3 IF LESS
.
          MATCH       AFUNDH,EDHF
          GOTO        NEXT3 IF LESS
.
          DISPLAY     *P43:24,*V2LON,DADMNO;
          MOVE        WDATE,FROMDATE
          MOVE        TEMPDT,TODATE
.
          CALL        TRANS  
          GOTO        NEXT3
.
.         Create an indexed file based on port
.         for accumulating Admission Type totals
.
INDZD     CALL      KILL0000
.
          MOVE      ONE,STATUS
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,FNAMEI,UKEY
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      PSTAY,FNAMEI                 * open temp index file
.
          MOVE      ZERO,OPCND

ENDC      RETURN
+
.
.         Deleting an indexed file based on port
.
KILL0000  CLOSE     PSTAY                        * close file
.
          DISPLAY    *P1:24,*EL,*P25:24,*V2LON:
                     "** Deleting Temp Indexed File **",*W;
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,FNAMEI         * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
+
.
.              READ  TRANSFER FILE
.
TRANS     MOVE       SP6,STDATE
          MOVE       " ",STMOVE
          MOVE       ONE,FIRST
.
          MATCH      ADATE,DDATE
          GOTO       DAYCASE IF EQUAL
.
          PACK       KEY30 WITH AADMNO,SP20
          CALL       RDSTRAN2
NEXTT     CALL       RDKTRAN2
          BRANCH     OVRCD OF XXXX
.
          MATCH      TADMN,AADMNO
          GOTO       XXXX IF NOT EQUAL
.
          MOVE       TMOVE,STMOVE
          RESET      TDATE
          REP        " 0",TDATE
          RESET      TDATE
          MATCH      "A",TMOVE
          GOTO       ARITYPE IF EQUAL
          MATCH      "R",TMOVE
          GOTO       ARITYPE IF EQUAL
          MATCH      "C",TMOVE
          GOTO       LOTYPE IF EQUAL
          MATCH      "L",TMOVE
          GOTO       LOTYPE IF EQUAL
.
          MATCH      FROMDATE,TDATE
          RETURN     IF LESS
          MATCH      TODATE,TDATE
          GOTO       XXXX IF LESS
          MOVE       TODATE,TDATE  
          GOTO       XXXX
.
.         Day Case - Get last rate for this patient
.
DAYCASE   MOVE       ZERO,TOTDAY
          CALL       RDMST
	  RETURN
+
.
.         End of a period, calculate the data needed, and then start new period
.
LOTYPE    MATCH      FROMDATE,TDATE
          GOTO       ARITYPE IF LESS
.
          MATCH      TODATE,TDATE
          GOTO       CONT8 IF LESS
.
          MOVE       TODATE,WDATE1
          MOVE       STDATE,WDATE2
.
          CALL       CALC
          RETURN
.
CONT8     MOVE       TDATE,WDATE1
          MOVE       STDATE,WDATE2
          CALL       CALC
.
.         Save new parameters for the start of the new period
.
ARITYPE   MATCH      TODATE,TDATE
          RETURN     IF NOT LESS
          MATCH      FROMDATE,TDATE
          GOTO       CONT2 IF NOT LESS
          MOVE       FROMDATE,STDATE
          GOTO       CONT30
.
CONT2     MOVE       TDATE,STDATE
CONT30    GOTO       NEXTT
.
XXXX      MATCH      "L",STMOVE
          RETURN     IF EQUAL
          MATCH      " ",STMOVE
          RETURN     IF EQUAL
.
          MATCH      "D",STMOVE
          GOTO       CONT9 IF NOT EQUAL
          MOVE       TDATE,WDATE1
          MOVE       STDATE,WDATE2
          GOTO       CONT10
.
CONT9     MOVE       TODATE,WDATE1
          MOVE       STDATE,WDATE2
CONT10    CALL       CALC
          RETURN
+
.
.              CALCULATE DATES DIFFERENCE
.
CALC      DAYSEP     WDATE2,WDATE1,TOTDAY
          COMPARE    ZERO,TOTDAY
          RETURN     IF EQUAL
.
.              READ PATIENT MASTER FILE
.
RDMST     MOVE       AURNO,KEY8
          CALL       RDMAST1
.
          MOVE       TOTDAY,LSTAY
.
          COMPARE    ZERO,TOTDAY               * Day case patient ?
          GOTO       CHKMAX IF NOT EQUAL       * No
.
          MOVE       ONE,TOTDAY                * Day cases treated as one day
          GOTO       OKDAY
.
CHKMAX    COMPARE    "66",LSTAY                * Over 65 days ?
          GOTO       OKDAY IF LESS             * No
.
          MOVE       "999",LSTAY               * Count all 66+ as 999
. 
OKDAY     PACK       KEY12 WITH ACLAIM,AFUNDH,LSTAY
.
          CALL       RDSTAY
          COMPARE    ZERO,OVRCD
          GOTO       UPREC IF EQUAL
.
          MOVE       ZERO,TOTPAT
          MOVE       ZERO,BEDDAY
          CALL       WRSTAY
          CALL       RDSTAY
.
UPREC     ADD        FIRST,TOTPAT
          MOVE       ZERO,FIRST
          ADD        TOTDAY,BEDDAY
          CALL       UPSTAY
          RETURN
+
.
.              PRINT REPORT
.
REPORT    DISPLAY    *P1:24,*EL,*P11:24,*V2LON:
                     "** ",*BLINKON,"Generating Report",*HOFF,*V2LON," **";
.
          MOVE      "60",CLNO
          CALL      PRTCODE
.
          CALL      PRTLN
          PRINT     *N,*N,"  ***  End of Report  ***"
. 
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
          CALL       KILL0000
          GOTO      START
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
          PRINT     *40,"Discharged Date range : ",XDAY1,"/",XMON1,"/",XCENT1:
                    XYEAR1,"  to  ",XDAY2,"/",XMON2,"/",XCENT2,XYEAR2:
                    *N,*40,"Claim Code  range     : ",DSTCLM,"      to  ",DEDCLM:
                    *N,*40,"Health Fund range     : ",DSTHF,"      to  ",DEDHF,*N
.
          BRANCH    LAST OF HEAD10
.
          MOVE      "CL",TCODE
          MOVE      SP20,TDESC
          PACK      KEY5 WITH TCODE,ACLAIM
          CALL      RDCODE1
          PRINT     *N,*5,"Claim Code  : ",TDESC
.
HEAD10    CALL      PRTLN
          PRINT     *1,"|",*33,"|":
                    *38," Length of",*53,"|",*58,"Number of":
                    *72,"|",*88,"|  Cumulative",*103,"|",*118,"|":
                    *N,*1,"| Health Fund",*33,"|",*38,"   Stay":
                    *53,"|",*58,"Discharges":
                    *72,"|   Bed Days":
                    *88,"|  Bed Days",*103,"|",*112,"%",*118,"|"
          CALL      PRTLN
          MOVE      "13",CLNO
          RETURN
+
.
.           PRINT LENGTH OF STAY FILE
.
PRTCODE   DISPLAY   *P55:24,*EL,"Claim Code : ";
.
          CALL      INITVAR
          MOVE      SP3,SAVCL
          MOVE      SP6,SAVHF
          MOVE      SP30,HFNAME
.
          MOVE      SP20,KEY12
          CALL      RDSSTAY
PRT10     CALL      RDKSTAY
          BRANCH    OVRCD OF ENDLN
.
          MATCH     ACLAIM,SAVCL
          GOTO      PRT15 IF NOT EQUAL
.
          MATCH     AFUNDH,SAVHF
          GOTO      PRT20 IF EQUAL
.
PRT15     COMPARE   ZERO,SUBTOT1
          GOTO      PRT18 IF EQUAL
.
          MOVE      ZERO,SUBTOT4
          MOVE      SUBTOT3,SUBTOT4
          MULT      "100",SUBTOT4
          DIV       SUBTOT2,SUBTOT4
.
          CALL      PRTLN
          PRINT     *1,"|",*33,"|",*41,"Total",*53,"|",*57,SUBTOT1:
                    *72,"| ",SUBTOT2:
                    *88,"| ",SUBTOT3,*103,"|",SUBTOT4,*118,"|"
          CALL      PRTLN
          ADD       THREE,CLNO
.
          ADD       SUBTOT1,GRDTOT1
          ADD       SUBTOT2,GRDTOT2
          ADD       SUBTOT3,GRDTOT3
.
          MATCH     ACLAIM,SAVCL
          GOTO      PRT18 IF EQUAL
.
PRT16     CALL      HEAD
          MOVE      ACLAIM,SAVCL
.
PRT18     MOVE      ZERO,SUBTOT1
          MOVE      ZERO,SUBTOT2
          MOVE      ZERO,SUBTOT3
          MOVE      ZERO,SUBTOT4
.
          MOVE      ACLAIM,SAVCL
          MOVE      AFUNDH,SAVHF
          MOVE      LSTAY,SAVLST
.
          MOVE      ZERO,CUMBDAY
          CALL      CALCDAY
          PACK      KEY12 WITH SAVCL,SAVHF,SAVLST
          CALL      RDSTAY
.
          COMPARE   "50",CLNO
          CALL      HEAD IF NOT LESS
.
          MOVE      SP30,HFNAME
          PACK      KEY14 WITH AFUNDH,ZERO,ZERO,ZERO,ZERO,SP10
          CALL      RDFUND1
          PRINT     *1,"| ",HFNAME;
.
PRT20     DISPLAY   *P68:24,*V2LON,ACLAIM;
.
          ADD       BEDDAY,CUMBDAY
          MOVE      ZERO,PCBDAY
          MOVE      CUMBDAY,PCBDAY
          MULT      "100",PCBDAY
          DIV       TOTBDAY,PCBDAY
.
          COMPARE   "56",CLNO
          GOTO      PRT22 IF LESS
          CALL      PRTLN
          CALL      HEAD
.
PRT22     COMPARE   ZERO,LSTAY
          GOTO      PRT25 IF NOT EQUAL
.
          MATCH     SP30,HFNAME
          GOTO      PRT24 IF NOT EQUAL
.
          PRINT     *1,"|";
PRT24     PRINT     *33,"|",*40,"Day Case",*53,"|",*57,TOTPAT:
                    *72,"| ",BEDDAY,*88,"| ",CUMBDAY:
                    *103,"|",PCBDAY,*118,"|"
          GOTO      PRT40
.
PRT25     COMPARE   "999",LSTAY
          GOTO      PRT30 IF NOT EQUAL
.
          MATCH     SP30,HFNAME
          GOTO      PRT28 IF NOT EQUAL
.
          PRINT     *1,"|";
PRT28     PRINT     *33,"|",*38,"Over 65 ",*53,"|",*57,TOTPAT:
                    *72,"| ",BEDDAY,*88,"| ",CUMBDAY:
                    *103,"|",PCBDAY,*118,"|"
          GOTO      PRT40
.
PRT30     MATCH     SP30,HFNAME
          GOTO      PRT35 IF NOT EQUAL
.
          PRINT     *1,"|";
PRT35     PRINT     *33,"|",*42,LSTAY,*53,"|",*57,TOTPAT:
                    *72,"| ",BEDDAY,*88,"| ",CUMBDAY:
                    *103,"|",PCBDAY,*118,"|"
.
PRT40     MOVE      SP30,HFNAME
          ADD       ONE,CLNO
          ADD       TOTPAT,SUBTOT1
          ADD       BEDDAY,SUBTOT2
          MOVE      CUMBDAY,SUBTOT3
          GOTO      PRT10
.
ENDLN     COMPARE   ZERO,SUBTOT1
          GOTO      PRTGRD IF EQUAL
.
          MOVE      ZERO,SUBTOT4
          MOVE      SUBTOT3,SUBTOT4
          MULT      "100",SUBTOT4
          DIV       SUBTOT2,SUBTOT4
.
          CALL      PRTLN
          PRINT     *1,"|",*33,"|",*41,"Total ",*53,"|",*57,SUBTOT1:
                    *72,"| ",SUBTOT2:
                    *88,"| ",SUBTOT3,*103,"|",SUBTOT4,*118,"|"
          CALL      PRTLN
          ADD       SUBTOT1,GRDTOT1
          ADD       SUBTOT2,GRDTOT2
          ADD       SUBTOT3,GRDTOT3
.
PRTGRD    MOVE      ONE,LAST
          CALL      HEAD
          MOVE      GRDTOT3,GRDTOT4
          MULT      "100",GRDTOT4
          DIV       GRDTOT2,GRDTOT4
.
          PRINT     *1,"|",*33,"|",*53,"|":
                    *72,"|",*88,"|",*103,"|",*118,"|":
                    *N,*1,"|",*33,"|",*38,"Grand Total ":
                    *53,"|",*57,GRDTOT1,*72,"| ",GRDTOT2:
                    *88,"| ",GRDTOT3,*103,"|",GRDTOT4,*118,"|"
          RETURN
.
.
PRTLN     PRINT     *1,"*----------------------------":
                    *30,"--------------------------------------------------":
                    *80,"--------------------------------------*"
          RETURN
.
NOPAT     DISPLAY   *P10:24,*EL,*V2LON:
                    "*** No Patient Selected ***",*W3;
          GOTO      KADAT1 
.
.           CALCULATE TOTAL BED DAYS
.
CALCDAY   MOVE      ZERO,TOTBDAY
          PACK      DIM9 WITH SAVCL,SAVHF
.
          PACK      KEY12 WITH DIM9,SP3
          CALL      RDSSTAY
NXTSTAY   CALL      RDKSTAY
          COMPARE   ZERO,OVRCD
          RETURN    IF NOT EQUAL
.
          PACK      DIM9A WITH ACLAIM,AFUNDH
          MATCH     DIM9,DIM9A
          RETURN    IF NOT EQUAL
.
          ADD       BEDDAY,TOTBDAY
          GOTO      NXTSTAY
.
.              INITIALIZE  THE VARIABLES
.
INITVAR   MOVE      ZERO,SUBTOT1
          MOVE      ZERO,SUBTOT2
          MOVE      ZERO,SUBTOT3
          MOVE      ZERO,SUBTOT4
.
          MOVE      ZERO,GRDTOT1
          MOVE      ZERO,GRDTOT2
          MOVE      ZERO,GRDTOT3
          MOVE      ZERO,GRDTOT4
          MOVE      ZERO,LAST
          RETURN
.
.           I/O FOR THE TEMP FILE
.
WRSTAY    RESET     KEY12
          WRITE     PSTAY,KEY12;KEY12,TOTPAT,BEDDAY
          RETURN
.
RDSTAY    MOVE      ZERO,OVRCD
          RESET     KEY12
          READ      PSTAY,KEY12;ACLAIM,AFUNDH,DLSTAY,TOTPAT,BEDDAY
          GOTO      OVERCOND IF OVER
          MOVE      DLSTAY,LSTAY
          RETURN
.
RDKSTAY   MOVE      ZERO,OVRCD
          READKS    PSTAY;ACLAIM,AFUNDH,DLSTAY,TOTPAT,BEDDAY
          GOTO      OVERCOND IF OVER
          MOVE      DLSTAY,LSTAY
          RETURN
.
RDSSTAY   RESET     KEY12
          READ      PSTAY,KEY12;;
          RETURN
.
UPSTAY    MOVE       LSTAY,DLSTAY
          UPDATE     PSTAY;ACLAIM,AFUNDH,DLSTAY,TOTPAT,BEDDAY
          RETURN
+
.
.         Subroutine to key in a valid date
.
KEYINDTE  
          IF        IDAY=0
            MOVE      SP10,CDAY        * no default day/mon
            MOVE      SP10,CMON
            MOVE      SP10,CYEAR
            MOVE      SP10,CCENT
          ELSE
            MOVE      IDAY,CDAY        * set up day/mon
            MOVE      IMON,CMON
            MOVE      IYEAR,CYEAR
            MOVE      ICENT,CCENT
          ENDIF
.
          MOVE      VERT,CVERT         * set up position
          MOVE      "30",CCOL
.
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
            CALL      KEYDATE          * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
.
          RETURN
+
.
ENDIT     CHAIN      PGM
          STOP
.
.==============================================================================
.
          INC       STD001IO
.
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATFN1IO/INC
          INC       PATINVIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       PATCODKY
          INC       PATFNDKY
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

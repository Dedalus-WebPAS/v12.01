.******************************************************************************
.* System    :   Common                                                       *
.* Program   :   HICUPLOD                                                     *
.* Desc      :   Load HIC GP & Practice data into IBA                         *
.******************************************************************************
.* Date      :   28/09/2005                                                   *
.* Author    :   Davin                                                        *
.* Mods      :                                                                *
.*   V10.11.01   09/08/2017  Tracey Nguyen    TSK 0299206                     *
.*               Recompiled for PMSHKIPR                                      *
.*   V10.07.01   26/10/2015  Steve Armstrong  CAR 323085                      *
.*               Removed declaration for LOWUPP (now defined in IBACOMM)      *
.******************************************************************************
.*   V9.10.01    20/06/2008  Davin         CAR 171122                         *
.*               Changed generated hcp & practice codes to begin with "z"     *
.*   V9.05.01    10/04/2006  Davin         CAR                                *
.*               Changed generated hcp & practice codes to begin with "H"     *
.*   V9.04.04    01/12/2005  Davin         CAR 71153                          *
.*               Added routine to activate previously inactive prov.numbers.  *
.*               - any hcp/hcg records previously written as 'left practice'  *
.*                 will be updated with the 'new' address details.            *
.*   V9.04.03    17/11/2005  Davin         CAR 71153                          *
.*               Changed to retrieve last generated codes from hcp/hcg tables *
.*               and use these as a starting point for new codes to be added. *
.*               Added routine to update existing providers if they need to   *
.*               be inactivated on pmshclaf.                                  *
.*               Reduced duplicate practices being written (changed chhcg000) *
.*   V9.04.02    21/10/2005  Davin         CAR 71153                          *
.*               Changed to write provider number to pmhcprv1                 *
.*               Changed practice name to be field005 or field006             *
.*               Changed to write all HCPs as type 5 - referring & gp         *
.*   V9.04.01    18/10/2005  Davin         CAR 71153                          *
.*               Various mods                                                 *
.*   V9.04.00    28/09/2005  Davin         CAR 71153                          *
.*               Created HIC doctor upload program                            *
.******************************************************************************
+
          INC       STD001FD
.
          INC       PATCODFD/INC            * codes file
          INC       PMSHCGFD/INC            * Practice file
          INC       PMSHCLFD/INC            * GP/Practice link file
          INC       PMSHCPFD/INC            * HCP file
          INC       PMSHKIFD/INC            * HCP Keyword index
.
. ----- Input Text File Definition -----
.
INPTEXT1  FILE      HL7, FIXED=256          * HIC input file
.
FIELD001  DIM       80
FIELD002  DIM       80
FIELD003  DIM       80
FIELD004  DIM       80
FIELD005  DIM       80
FIELD006  DIM       80
FIELD007  DIM       80
FIELD008  DIM       80
FIELD009  DIM       80
FIELD010  DIM       80
.
. ----- Program Variables -----
.
CDATEIS   DIM       8
COUNT     FORM      8
DIM3      DIM       3
DIM4      DIM       4
DIM7      DIM       7
DIM8      DIM       8
DIM9      DIM       9
DIM10     DIM       10
DIM20     DIM       20
ERRCODE   DIM       10
ERRTYPE   DIM       1
ERRMESSG  DIM       50
FNAMEI    DIM       8                            * input filename entered
FORM3     FORM      3
GPCODE    FORM      9
DOCCOUNT  FORM      10
INPCOUNT  FORM      8
HCGCOUNT  FORM      8
HCLCOUNT  FORM      8
HCLPROV   DIM       6
HCPCOUNT  FORM      8
HCPADDR1  DIM       60
HCPGIVN1  DIM       35
HCPSURNM  DIM       35
LEFTDATE  DIM       10
LNKCOUNT  FORM      10
TCLPROV   DIM       6
PRACCODE  FORM      9
PRACOUNT  FORM      10
WORKDATE  DATE      8
.
. ----- Program Constants -----
.
LOWz      INIT      "z"
CATCZ     INIT      "CZ"
RET       INIT      015                          * carriage return
ZERO4     INIT      "0000"
.
PRGNAM    INIT      "Upload HIC HCP Data"
PRGID     INIT      "HICUPLOD"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
          BRANCH    EXIT,ML9000
.
          CALL      OPTN0000                * Ask proceed question
          BRANCH    EXIT,ML9000
.
ML1000    CALL      KASC0000                * Keyin ASCII input file
          BRANCH    EXIT,ML0000
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML5000,ML1000,ML0000
.
ML5000    CALL      PINP0000                * Process the input file
.
.>>>>     CALL      GENK0000                * generate HCP keyword records
.
ML9000    MATCH     SP8,PGM
          GOTO      ML9999 IF EQUAL
.
          CHAIN     PGM                     * Chain back to appropriate menu
ML9999    SHUTDOWN  ""
+
.******************************************************************************
.*                                  INIT0000                                  *
.*                      Initialise Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.       
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSHCPA2,"pmshcpaf"
          OPEN      PMSHCPA3,"pmshcpaf"
.
          DISPLAY   *P64:24,"pmshcgaf";
          OPEN      PMSHCGA1,"pmshcgaf"
          OPEN      PMSHCGA2,"pmshcgaf"
.
          DISPLAY   *P64:24,"pmshclaf";
          OPEN      PMSHCLA1,"pmshclaf"
          OPEN      PMSHCLA3,"pmshclaf"
.
          DISPLAY   *P64:24,"pmshkiaf";
          OPEN      PMSHKIA1,"pmshkiaf"
          OPEN      PMSHKIA2,"pmshkiaf"
.
INIT9000  MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9500  MOVE      ONE,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000                                  *
.*                         Select option to run upload                        *
.******************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = Upload GP/Practice details":
                    *P1:7,"Select Option : ";
.
OPTN1000  KEYIN     *P17:7,*V2LON,*DV,UNDLN2,*P17:7,OPTION;
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          COMPARE   ONE,OPTION
          GOTO      OPTN1000 IF NOT EQUAL
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                 KASC0000                                   *
.*                            Keyin ASCI file                                 *
.******************************************************************************
KASC0000  KEYIN     *P1:10,*EF,"Keyin Load File Name : ",*V2LON,FNAMEI
.
          PACK      FNAMEI,FNAMEI,SP70
.
          MATCH     SP70,FNAMEI                  * anything entered ?
          GOTO      KASC9500 IF EQUAL            * no
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      INPTEXT1,FNAMEI              * open file
          TRAPCLR   IO
.
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Input HIC data file not found.  ";
            CALL      HITENTER
            GOTO      KASC0000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC9500  MOVE      ONE,EXIT                     * no filename
KASC9999  RETURN
+
.******************************************************************************
.*                                  PINP0000                                  *
.*                         Process The Input Text File                        *
.******************************************************************************
PINP0000  CALL      IBACLOCK
          PACK      CDATEIS,CCC,CYY,CMM,CDD
          REP       " 0",CDATEIS
          DISPLAY   *P1:17,*EF,"GP/Prc Code: ":
                    *P1:18,"Input Records: ":
                    *P1:20,"Started  : ",*V2LON,CTIMEIS,*HOFF:
                    *P1:24,"Processing ",*V2LON,FNAMEI,*HOFF;
          MOVE      ZERO,COUNT
          MOVE      ZERO,INPCOUNT
          CALL      GTHCP000                *  Get last hcp code
          CALL      GTHCG000                *  Get last hcg code
.>>>>     MOVE      ZERO,GPCODE
.>>>>     MOVE      ZERO,PRACCODE
          MOVE      ZERO,PRACOUNT
          MOVE      ZERO,DOCCOUNT
          MOVE      ZERO,LNKCOUNT
          MOVE      ZERO,HCGCOUNT
          MOVE      ZERO,HCLCOUNT
          MOVE      ZERO,HCPCOUNT
          PACK      TCLPROV,SP70
          CALL      HEAD0000                     * Print the report header
.
. ----- Read the input data file into temporary variables -----
.
PINP1000  READ      INPTEXT1,SEQ;FIELD001,FIELD002,FIELD003,FIELD004,FIELD005:
                                 FIELD006,FIELD007,FIELD008,FIELD009,FIELD010
.
          GOTO      PINP9000 IF OVER             * end of file
.
          MOVE      FIELD004,HCLPROV        * save provider
          MATCH     HCLPROV,TCLPROV
          IF        !@EQUAL
            ADD       ONE,INPCOUNT          * different provider, so count
          ENDIF
.
          CALL      CHHCL000                * check hcl file for provider no.
          BRANCH    EXIT,PINP7000           * provider exists, so do nothing
.
          CALL      CHHCP000                * check if doctor is on hcp or hcl
          BRANCH    EXIT,PINP5000           * doctor exists=don't write new hcp
.
          CALL      WRHCP000                * write new hcp record
.
PINP5000  CALL      CHHCG000                * check if address is on hcg table
          BRANCH    EXIT,PINP6000           * practice exists, don't write new
.
          CALL      WRHCG000                * write new hcg record
PINP6000  CALL      WRHCL000                * write new hcl record
.
PINP7000  ADD       ONE,COUNT
          IF        (COUNT%500)=0
            DISPLAY   *P14:17,*V2LON,KEY10:
                      *P14:18,COUNT;
          ENDIF
          MOVE      HCLPROV,TCLPROV         * temp var for checking duplicates
          GOTO      PINP1000
.
. ----- Finished -----
.
PINP9000  CALL      IBACLOCK
          DISPLAY   *P1:21,"Finished : ",*V2LON,CTIMEIS,*HOFF:
                    *P1:24,*EL,*B,"No more records.  ";
          CALL      HITENTER
          PRINT     *N,*1,COUNT," total input records processed.":
                    *N,*1,INPCOUNT," different HCPs in upload file.",*N:
.>>>>               *N,*1,HCPCOUNT," existing records found on pmshcpaf.":
                    *N,*1,DOCCOUNT," new records written to pmshcpaf.":
.>>>>               *N,*1,HCLCOUNT," existing records found on pmshclaf.":
                    *N,*1,LNKCOUNT," new records written to pmshclaf.":
.>>>>               *N,*1,HCGCOUNT," existing records found on pmshcgaf.":
                    *N,*1,PRACOUNT," new records written to pmshcgaf.":
                    *N,*1,"*** End of Report ***"
PINP9999  RETURN
+
.******************************************************************************
.*                                  GTHCP000                                  *
.*                        Get last hcp code that was generated                *
.******************************************************************************
GTHCP000  PACK      KEY10,LOWz,NINE,Z70
          CALL      RSPMHCP1
          CALL      RPPMHCP1
          BRANCH    OVRCD,GTHCP950
.
          UNPACK    PMHCHCPC,DIM1,DIM9
.
          MOVE      DIM9,GPCODE
          GOTO      GTHCP999
.
GTHCP950  MOVE      ZERO,GPCODE
GTHCP999  RETURN
+
.******************************************************************************
.*                                  GTHCG000                                  *
.*                        Get last hcg code that was generated                *
.******************************************************************************
GTHCG000  PACK      KEY12,LOWz,NINE,Z70
          CALL      RSPMHCG1
          CALL      RPPMHCG1
          BRANCH    OVRCD,GTHCG950
.
          UNPACK    PMHGPRAC,DIM1,DIM9
.
          MOVE      DIM9,PRACCODE
          GOTO      GTHCG999
.
GTHCG950  MOVE      ZERO,PRACCODE
GTHCG999  RETURN
+
.******************************************************************************
.*                                  CHHCP000                                  *
.*                        Check hcp table for provider                        *
.******************************************************************************
.>>>>CHHCP000  PACK      HCPSURNM,FIELD001,SP70
.>>>>          BUMP      HCPSURNM
.>>>>          REP       LOWUPP,HCPSURNM
.>>>>          RESET     HCPSURNM
.>>>>          PACK      HCPGIVN1,FIELD002,SP70
.>>>>          BUMP      HCPGIVN1
.>>>>          REP       LOWUPP,HCPGIVN1
.>>>>          RESET     HCPGIVN1
.>>>>          PACK      HCPADDR1,FIELD005,SP70
.>>>>          PACK      KEY80,HCPSURNM,HCPGIVN1,SP70,SP70
.>>>>          CALL      RSPMHCP2
.>>>>CHHCP100  CALL      RKPMHCP2                * on pmshcp?
.>>>>          BRANCH    OVRCD,CHHCP900
.>>>>.
.>>>>          MATCH     HCPSURNM,PMHCSNAM
.>>>>          GOTO      CHHCP900 IF NOT EQUAL
.>>>>.
.>>>>          MATCH     HCPGIVN1,PMHCGNAM
.>>>>          GOTO      CHHCP900 IF NOT EQUAL
.>>>>.
.>>>>          REP       UPPLOW,PMHCADR1
.>>>>          MATCH     HCPADDR1,PMHCADR1
.>>>>          GOTO      CHHCP100 IF NOT EQUAL
.>>>>.
.>>>>          GOTO      CHHCP950
.>>>>.
CHHCP000  PACK      KEY20,HCLPROV,SP70      * check for provider number on hcp
          CALL      RSPMHCP3
          CALL      RKPMHCP3
          BRANCH    OVRCD,CHHCP910
.
          MATCH     HCLPROV,PMHCPRV1
          GOTO      CHHCP950 IF EQUAL
.
CHHCP910  PACK      KEY30,HCLPROV,SP70      * check for provider number on hcl
          CALL      RSPMHCL3
          CALL      RKPMHCL3
          BRANCH    OVRCD,CHHCP920
.
          MATCH     HCLPROV,PMHLPRV1
          IF        @EQUAL
            MOVE      PMHLHCPC,PMHCHCPC     * save hcp code
            GOTO      CHHCP950
          ENDIF
.
CHHCP920  MOVE      ZERO,EXIT               * hcp not currently on file
          GOTO      CHHCP999
.
CHHCP950  ADD       ONE,HCPCOUNT
          MOVE      ONE,EXIT                * hcp already on file
CHHCP999  RETURN
+
.******************************************************************************
.*                                  CHHCG000                                  *
.*                        Check hcg table for name/addr                       *
.******************************************************************************
CHHCG000  PACK      KEY72,FIELD006,SP70
          CALL      RSPMHCG2
CHHCG100  CALL      RKPMHCG2                * address on pmshcg?
          BRANCH    OVRCD,CHHCG900
.
          REP       UPPLOW,PMHGADD1
          MATCH     FIELD006,PMHGADD1       * address line 1 equal?
          GOTO      CHHCG900 IF NOT EQUAL
.
          REP       UPPLOW,PMHGPNAM
          MATCH     FIELD005,PMHGPNAM       * practice name equal?
          GOTO      CHHCG100 IF NOT EQUAL
.
          REP       UPPLOW,PMHGADD3
          MATCH     FIELD007,PMHGADD3       * suburb equal?
          GOTO      CHHCG950 IF EQUAL
.
          GOTO      CHHCG100                * check next record
.
CHHCG900  MOVE      ZERO,EXIT               * hcg not currently on file
          GOTO      CHHCG999
.
CHHCG950  ADD       ONE,HCGCOUNT
          MOVE      ONE,EXIT                * hcg already on file
CHHCG999  RETURN
+
.******************************************************************************
.*                                  CHHCL000                                  *
.*                        Check hcl table for provider                        *
.******************************************************************************
CHHCL000  PACK      KEY30,FIELD004,SP70
          CALL      RSPMHCL3
          CALL      RKPMHCL3                * provider number on pmshcl?
          BRANCH    OVRCD,CHHCL900
.
          MATCH     FIELD004,PMHLPRV1       * same provider number?
          GOTO      CHHCL900 IF NOT EQUAL   * no
.
          MATCH     "HICUPLOD  ",PMHLWEBC   * created by 'HICUPLOD'?
          GOTO      CHHCL950 IF NOT EQUAL   * no, so don't update
.
          CALL      INHCL000                * provider number to be INACTIVATED?
          BRANCH    EXIT,CHHCL950           * yes, so finished
.
          CALL      ACHCL000                * provider number to be ACTIVATED?
          GOTO      CHHCL950
.
CHHCL900  MOVE      ZERO,EXIT               * not on hcl file
          GOTO      CHHCL999
.
CHHCL950  MOVE      ONE,EXIT                * exists on hcl file
          ADD       ONE,HCLCOUNT
CHHCL999  RETURN
+
.******************************************************************************
.*                                  WRHCP000                                  *
.*                     Write record to pmshcp if required                     *
.******************************************************************************
WRHCP000  ADD       ONE,GPCODE
          CALL      CLPMSHCP                     * clear HCP file vars
          PACK      PMHCHCPC,LOWz,GPCODE,SP70
          REP       " 0",PMHCHCPC
          PACK      KEY10,PMHCHCPC,SP70
          CALL      RAPMHCP1                * check if record already on pmshcp
          IF        OVRCD=1
            CALL      STHCP000              * set up hcp vars
            CALL      WRPMHCP1
            ADD       ONE,DOCCOUNT
          ELSE
            GOTO      WRHCP000
          ENDIF
WRHCP999  RETURN
+
.******************************************************************************
.*                                  STHCP000                                  *
.*                        Set up vars for pmshcp record                       *
.******************************************************************************
STHCP000  MOVE      FIELD003,ANS
          PACK      PMHCSNAM,FIELD001,SP70          * surname
          PACK      PMHCGNAM,FIELD002,SP1,ANS,SP70  * given name & initial
.
          PACK      PMHCADR1,FIELD005,SP70          * address line 1
          PACK      FIELD007,FIELD007,SP70
          MATCH     SP70,FIELD007
          IF        @EQUAL
            PACK      PMHCADR2,SP70                 * no address line 2
            PACK      PMHCADR3,FIELD006,SP70        * address line 3
          ELSE
            MOVE      FIELD007,DIM7
            TYPE      DIM7
            IF        @EQUAL
              CALL      CNDTE000                    * convert date field
              PACK      PMHCADR1,FIELD006,SP70      * address line 1 (pracname)
              PACK      PMHCADR2,FIELD005,SP70      * address line 2 (leftprac)
              PACK      PMHCADR3,LEFTDATE,SP70      * address line 3 (leftdate)
            ELSE
              PACK      PMHCADR1,FIELD005,SP70      * address line 1
              PACK      PMHCADR2,FIELD006,SP70      * address line 2
              PACK      PMHCADR3,FIELD007,SP70      * address line 3
            ENDIF
          ENDIF
          PACK      PMHCADR4,FIELD009,SP70          * address line 4
          PACK      PMHCSTAT,FIELD009,SP70          * state
          PACK      PMHCPOST,FIELD008,SP70          * postcode
          PACK      PMHCTITL,FIELD010,SP70          * title
          PACK      PMHCPRV1,FIELD004,SP70          * provider number
          MOVE      "G ",PMHCHCPT                   * type of hcp=gp
          MOVE      FIVE,PMHCHCST                   * status=referring & GP
          MOVE      "OTH",PMHCSPC1                  * primary speciality 1
          MOVE      "M",PMHCUPDF                    * manual update flag
.>>>>     MATCH     "LEFT PRACTICE",FIELD005
.>>>>     IF        @EQUAL
.>>>>       MOVE      ONE,PMHCSTTS                  * inactive 
.>>>>     ELSE
            MOVE      ZERO,PMHCSTTS                 * active
.>>>>     ENDIF
          MOVE      "HIC Upload Program",PMHCSEML   * save in email address
          MOVE      CDATEIS,PMHCCDTE
          MOVE      CTIMEIS,PMHCCTIM
          MOVE      "HICUPLOD  ",PMHCWEBC           * created by 'HICUPLOD'
STHCP999  RETURN
+
.******************************************************************************
.*                                  CNDTE000                                  *
.*                     Convert date field to ccyy/mm/dd                       *
.******************************************************************************
CNDTE000  UNPACK    DIM7,DIM4,DIM3
          MOVE      DIM3,FORM3
          PACK      WORKDATE,DIM4,ZERO4
          ADD       FORM3,WORKDATE
          UNPACK    WORKDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          MOVE      CPCDATE,LEFTDATE
CNDTE999  RETURN
+
.******************************************************************************
.*                                  WRHCG000                                  *
.*                     Write record to pmshcg if required                     *
.******************************************************************************
WRHCG000  ADD       ONE,PRACCODE
          CALL      CLPMSHCG                     * clear HCG file vars
          PACK      PMHGPRAC,LOWz,PRACCODE,SP70
          REP       " 0",PMHGPRAC
          PACK      KEY12,PMHGPRAC,SP1,ONE,SP70
          CALL      RAPMHCG1                * check if record already on pmshcg
          IF        OVRCD=1
            CALL      STHCG000              * set up hcg vars
            CALL      WRPMHCG1
            ADD       ONE,PRACOUNT
          ELSE
            GOTO      WRHCG000
          ENDIF
WRHCG999  RETURN
+
.******************************************************************************
.*                                  STHCG000                                  *
.*                        Set up vars for pmshcg record                       *
.******************************************************************************
STHCG000  PACK      FIELD007,FIELD007,SP70
          MATCH     SP70,FIELD007
          IF        @EQUAL
            PACK      PMHGPNAM,FIELD006,SP70        * practice name = suburb
            PACK      PMHGADD1,FIELD005,SP70        * address line 1
            PACK      PMHGADD2,SP70                 * no address line 2
            PACK      PMHGADD3,FIELD006,SP70        * address line 3
          ELSE
            MOVE      FIELD007,DIM7
            TYPE      DIM7                          * check for 'left practice'
            IF        @EQUAL
              CALL      CNDTE000                    * convert date field
              PACK      PMHGPNAM,FIELD006,SP70      * practice name = name
              PACK      PMHGADD1,FIELD005,SP70      * addr line 1='left pract'
              PACK      PMHGADD2,SP70               * no address line 2
              PACK      PMHGADD3,LEFTDATE,SP70      * addr line 3='left date'
            ELSE
              PACK      PMHGPNAM,FIELD005,SP70      * practice name = name
              PACK      PMHGADD1,FIELD006,SP70      * address line 1
              PACK      PMHGADD2,SP70               * no address line 2
              PACK      PMHGADD3,FIELD007,SP70      * address line 3
            ENDIF
          ENDIF
          PACK      PMHGADD4,FIELD009,SP70          * address line 4
          PACK      PMHGSTAT,FIELD009,SP70          * state
          PACK      PMHGPOST,FIELD008,SP70          * postcode
          PACK      PMHGCNTR,SP1,ONE                * counter
          MOVE      "0",PMHGGPPI                    * GPFH Extrct GP Prac Inc
          MOVE      "M",PMHGUPFL                    * Update Flag M=manual
          MATCH     "LEFT PRACTICE",FIELD005
          IF        @EQUAL
            MOVE      ONE,PMHGSTTS                  * inactive  
          ELSE
            MOVE      ZERO,PMHGSTTS                 * active
          ENDIF
          MOVE      "HIC Upload Program",PMHGPEML   * save in email address
          MOVE      CDATEIS,PMHGCDTE
          MOVE      CTIMEIS,PMHGCTIM
          MOVE      "HICUPLOD  ",PMHGWEBC           * created by 'HICUPLOD'
STHCG999  RETURN
+
.******************************************************************************
.*                                  WRHCL000                                  *
.*                     Write record to pmshcl if required                     *
.******************************************************************************
WRHCL000  CALL      CLPMSHCL                     * clear HCL file vars
          PACK      PMHCHCPC,PMHCHCPC,SP70
          MATCH     SP70,PMHCHCPC
          GOTO      WRHCL999 IF EQUAL
.
          PACK      PMHGPRAC,PMHGPRAC,SP70
          MATCH     SP70,PMHGPRAC
          GOTO      WRHCL999 IF EQUAL
.
          PACK      KEY20,PMHCHCPC,PMHGPRAC,SP70
          CALL      RAPMHCL1                * check if record already on pmshcl
          IF        OVRCD=1
            CALL      STHCL000              * set up hcl vars
            CALL      WRPMHCL1
            ADD       ONE,LNKCOUNT
          ENDIF
WRHCL999  RETURN
+
.******************************************************************************
.*                                  STHCL000                                  *
.*                        Set up vars for pmshcl record                       *
.******************************************************************************
STHCL000  MOVE      PMHCHCPC,PMHLHCPC          * hcp
          MOVE      PMHGPRAC,PMHLHCPR          * practice
          MOVE      FIELD004,PMHLPRV1          * provider number
          MATCH     "LEFT PRACTICE",FIELD005
          IF        @EQUAL
            PACK      PMHLLPDT,WORKDATE,SP70   * date left practice
            MOVE      ONE,PMHLSTAT             * inactive  
          ELSE
            MOVE      ZERO,PMHLSTAT            * active
          ENDIF
          MOVE      "G",PMHLTYPE               * type of hcp=gp
          MOVE      CDATEIS,PMHLCDTE
          MOVE      CTIMEIS,PMHLCTIM
          MOVE      "HICUPLOD  ",PMHLWEBC      * created by 'HICUPLOD'
STHCL999  RETURN
+
.******************************************************************************
.*                                  INHCL000                                  *
.*              Check if provider number is now to be INACTIVE                *
.******************************************************************************
INHCL000  MATCH     "LEFT PRACTICE",FIELD005   * need to inactivate link?
          GOTO      INHCL950 IF NOT EQUAL      * no, so exit
.
          MATCH     "0",PMHLSTAT               * currently active?
          GOTO      INHCL950 IF NOT EQUAL      * no, so exit
.
.         Inactivate hcp/practice link for this provider number
.
          PACK      KEY20,PMHLHCPC,PMHLHCPR
          CALL      RDPMHCL1
          BRANCH    OVRCD,INHCL950
.
          MOVE      FIELD007,DIM7
          TYPE      DIM7                       * check for 'left practice'
          IF        @EQUAL
            CALL      CNDTE000                 * convert date field
            PACK      PMHLLPDT,WORKDATE,SP70   * date left practice
          ENDIF
          MOVE      ONE,PMHLSTAT               * inactive
          MOVE      CDATEIS,PMHLLUPD           * date updated
          MOVE      CTIMEIS,PMHLLUPT           * time updated
          MOVE      "HICUPLOD  ",PMHLWEBU      * updated by 'HICUPLOD'
          CALL      UPPMHCL1                   * update hcl record
.
INHCL900  MOVE      ONE,EXIT                   * recorded updated
          GOTO      INHCL999
.
INHCL950  MOVE      ZERO,EXIT                  * recorded not updated
INHCL999  RETURN
+
.******************************************************************************
.*                                  ACHCL000                                  *
.*              Check if provider number is now to be ACTIVE                  *
.******************************************************************************
ACHCL000  MATCH     "LEFT PRACTICE",FIELD005   * need to activate link?
          GOTO      ACHCL999 IF EQUAL          * no, keep inactive
.
          MATCH     "0",PMHLSTAT               * currently active?
          GOTO      ACHCL999 IF EQUAL          * yes, so exit
.
.>>>>     Activate & update hcp/practice link for this provider number
.
          PACK      KEY20,PMHLHCPC,PMHLHCPR
          CALL      RDPMHCL1
          BRANCH    OVRCD,ACHCL999
.
          PACK      PMHLLPDT,SP70              * date left practice
          MOVE      ZERO,PMHLSTAT              * active
          MOVE      CDATEIS,PMHLLUPD           * date updated
          MOVE      CTIMEIS,PMHLLUPT           * time updated
          MOVE      "HICUPLOD  ",PMHLWEBU      * updated by 'HICUPLOD'
          CALL      UPPMHCL1                   * update hcl record
.
.>>>>     Activate & update practice
.
          PACK      KEY12,PMHLHCPR,SP1,ONE,SP70
          CALL      RDPMHCG1
          BRANCH    OVRCD,ACHCL500
.
          PACK      FIELD007,FIELD007,SP70
          MATCH     SP70,FIELD007
          IF        @EQUAL
            PACK      PMHGPNAM,FIELD006,SP70        * practice name = suburb
            PACK      PMHGADD1,FIELD005,SP70        * address line 1
            PACK      PMHGADD2,SP70                 * no address line 2
            PACK      PMHGADD3,FIELD006,SP70        * address line 3
          ELSE
            PACK      PMHGPNAM,FIELD005,SP70        * practice name = name
            PACK      PMHGADD1,FIELD006,SP70        * address line 1
            PACK      PMHGADD2,SP70                 * no address line 2
            PACK      PMHGADD3,FIELD007,SP70        * address line 3
          ENDIF
          MOVE      ZERO,PMHGSTTS                   * active
          MOVE      CDATEIS,PMHGLUPD                * Date Record Updated
          MOVE      CTIMEIS,PMHGLUPT                * Time Record Updated
          MOVE      "HICUPLOD  ",PMHGWEBU           * updated by 'HICUPLOD'
          CALL      UPPMHCG1                        * update hcg record
.
.>>>>     May need to update hcp details for this provider number
.
ACHCL500  PACK      KEY10,PMHLHCPC,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,ACHCL999
.
          MATCH     PMHLPRV1,PMHCPRV1               * same provider number?
          GOTO      ACHCL999 IF NOT EQUAL           * no
.
          PACK      PMHCADR1,FIELD005,SP70          * address line 1
          PACK      FIELD007,FIELD007,SP70
          MATCH     SP70,FIELD007
          IF        @EQUAL
            PACK      PMHCADR2,SP70                 * no address line 2
            PACK      PMHCADR3,FIELD006,SP70        * address line 3
          ELSE
            PACK      PMHCADR1,FIELD005,SP70        * address line 1
            PACK      PMHCADR2,FIELD006,SP70        * address line 2
            PACK      PMHCADR3,FIELD007,SP70        * address line 3
          ENDIF
          MOVE      CDATEIS,PMHCLUPD
          MOVE      CTIMEIS,PMHCLUPT
          MOVE      "HICUPLOD  ",PMHCWEBU           * updated by 'HICUPLOD'
          CALL      UPPMHCP1                        * update hcp record
.
ACHCL999  RETURN
+
.******************************************************************************
.*                                  HEAD0000                                  *
.*                           Print The Report Header                          *
.******************************************************************************
HEAD0000  MOVE      ONE,CNOUNDLN
          MOVE      "- Error report",CPHDROPT
          CALL      HEAD80                  * Print the report header
          PRINT     *N;
HEAD9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000                                  *
.*                            Print An Error Report                           *
.******************************************************************************
PRNT0000  COMPARE   "56",CLNO
          CALL      HEAD0000 IF NOT LESS    * Print the report header
.
          PRINT     *1,ERRCODE,SP2,ERRTYPE,SP2,ERRMESSG
          ADD       ONE,CLNO
PRNT9999  RETURN
+
.******************************************************************************
.*                                 GENK0000                                   *
.*                           Generate HCP Keywords                            *
.******************************************************************************
GENK0000  MOVE      ZERO,SECTOR
          PACK      KEY10,SP70
          CALL      RSPMHCP1
GENK1000  CALL      RKPMHCP1
          BRANCH    OVRCD,GENK9999
.
          ADD       ONE,SECTOR
          IF        SECTOR%100=1
            DISPLAY   *P1:24,*EL,*V2LON,"Generating Keyword Index: ",SECTOR:
                                        " - ",PMHCHCPC;
          ENDIF
.
          PROC      PMSHKIUP                     * update pmskhiaf
          GOTO      GENK1000
.
GENK9999  RETURN
+
.==============================================================================
.
          INC       STD001IO
.
          INC       CLPMSHCG                * clear HCG file
          INC       CLPMSHCL                * clear HCL file
          INC       CLPMSHCP                * clear HCP file
          INC       PMSHKIPR                * update HCP keyword
.
          INC       PATCODIO/INC            * Codes file
          INC       PMSHCGIO/INC            * Practice file
          INC       PMSHCLIO/INC            * GP/Practice link file
          INC       PMSHCPIO/INC            * HCP file
          INC       PMSHKIIO/INC            * HCP Keyword index
+

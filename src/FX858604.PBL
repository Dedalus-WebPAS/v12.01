.***************************************************************************
.* System    :   Mental Health                                             *
.* Program   :   Fix Program - FX858604                                    *
.* Desc      :                                                             *
.***************************************************************************
.* Date      :   19/11/2021                                                *
.* Author    :   Sunny                                                     *
.* Function  :   This program will loop all records in mehdiaaf and then   *
.*               read patvisaf to find the patients ur number to fill in   *
.*               mehdiaaf.mhdiurno and do an update                        *
.* Modifications  :                                                        *
.*       V11.02.01  28/01/2022  Sunny         TSK 0858604                  *
.*                  Changed patmi1af to patvisaf to get UR number          *
.*       V11.01.01  28/01/2022  Sunny         TSK 0858604                  *
.*                  Added ADDCOUNT - counter for number of records updated *
.*       V11.01.00  19/11/2021  Sunny         TSK 0858604                  *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       MEHDIAFD/INC
          INC       PATVISFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
PIPE      INIT      "|"
.
ADDCOUNT  FORM      8
CHECKCNT  FORM      4
.
.
PRGID     INIT      "FX858604"
PRGNAM    INIT      "Populate U/R Number field in the MH Diagnosis table"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      MEHDIAA1,"mehdiaaf"
          OPEN      PATVISA1,"patvisaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: PRNT0000 *
.*                           Print The Report Header                          *
.******************************************************************************
.
HEAD0000  CALL      HEAD80                       * print report header
          PRINT     *20,"U/R's not found for following records"
.
          CALL      LINE0000                     * print line
.
          PRINT     *1,PIPE,*3,"Adm Number",*14,"-",*16,"Date",*80,PIPE
.
          CALL      LINE0000
.
          MOVE      SEVEN,CLNO                   * set line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by: HEAD0000  *
.*                      draw line across page                     PMST0000  *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "-------------------------------*"
.
          ADD       ONE,CLNO
.
LINE9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  CALL      HEAD0000
          MOVE      ZERO,ADDCOUNT             * initialize counter  

          DISPLAY   *P1:20,*EL,*V2LON,*HOFF,"Processing records.."

          MOVE      SP70,KEY16
          CALL      RSMHDIA1                  * positional read adm no from
PROC1000  CALL      RKMHDIA1                  * MH diagnosis table 
          BRANCH    OVRCD,PROC9000
.
          MATCH     DMHDIADM,SP70
          GOTO      PROC1000 IF EQUAL
.
          PACK      KEY8,DMHDIADM,SP70
          CALL      RDVISA1
          IF        OVRCD=0
            MOVE      PVIURNO,MHDIURNO 
            CALL      UPMHDIA1
            ADD       ONE,ADDCOUNT            * Add record update count
          ELSE 
            PRINT     *1,"|",*3,DMHDIADM,*14,"-",*16,MHDIDATE,*80,PIPE 
          ENDIF  
.
          GOTO       PROC1000            
.
.         Display Report 
.
PROC9000  PRINT     *N,*1,"Records Updated in MH Diagnosis Table: ",ADDCOUNT
          DISPLAY   *P1:22,*EF,"Records Updated in MH Diag Table: ",ADDCOUNT:
                    *P1:24,*EF;
          CALL      HITENTER          
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
      
          INC       MEHDIAIO/INC
          INC       PATVISIO/INC

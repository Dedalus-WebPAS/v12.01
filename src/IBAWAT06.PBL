.***************************************************************************
.* System    :   Waiting List                                              *
.* Program   :   IBAWAT06                                                  *
.* Desc      :   Waiting list bulk transfer. Transfer all waiting list     *
.*           :   patients for a consultant to another within the same unit *
.***************************************************************************
.* Date      :   12/05/2003                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will look the waiting list treatment file    *
.*           :   and change the consultant. Patients with a status of      *
.*           :   1,2 or 3 will be updated. The new consultant must be from *
.*           :   the same unit as the old consultant. A history record     *
.*           :   will be created to record the consultant change.          *
.* Mods      :                                                             *
.***************************************************************************
.* V11.02.01 10/02/2022  Thanh T       TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                *
.***************************************************************************
.*           :   V9.04.02 06/01/2005 Jill Habasque CAR 52757               *
.*           :            Removed error for doctor from and to equal       *
.*           :   V9.04.01 05/01/2005 Jill Habasque CAR 52757               *
.*           :            Added unit to code                               *
.*           :   V9.03.02 27/05/2004 Jill Habasque CAR 43861               *
.*           :             Added printing of category                      *
.*           :   V9.03.01 14/11/2003 Ebon Clements CAR 43861               *
.*           :             Added filters by status and create extract      *
.*           :             file option                                     *
.*           :   V9.03.00 19/05/2003 Ebon Clements CAR 19879               *
.*           :             Created program                                 *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       WATCONFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       WATTR1FD/INC
          INC       WATVWLFD/INC
          INC       PATVISFD/INC
          INC       PMSVX1FD/INC
          INC       PATMI1FD/INC
          INC       WATHISFD/INC
          INC       BOKRC1FD/INC
          INC       PATDSCFD/INC
          INC       PATMA1FD/INC
          INC       WEBSECFD/INC
          INC       WATPROFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
ADDWLFLG  FORM      1                  * Add waiting list flag for history
CODE      DIM       2                  * Cat Code
CODEWU    INIT      "WU"               * Unit
DATE1     DIM       10                 * Date on list
DATE2     DIM       10                 * Scheduled Adm Date
DIM35     DIM       35                 * Patient Name
DESC29    DIM       29                 * Procedure Desc 
DIM55     DIM       55                 * Dim 50 display variable
DOCCODE   DIM       6                  * Doctor Code
DOCFNAM   DIM       15                 * Print doctors name
DOCTNAM   DIM       15                 * Print doctors name
DOCTFROM  DIM       10                 * Doctor From
DOCTORTO  DIM       10                 * Doctor From
DOCTFNAM  DIM       60                 * Doctor from desc
DOCTTNAM  DIM       60                 * Doctor to desc
EXPORTFL  FORM      1                  * Create Export File
RECCOUNT  FORM      8                  * Record Count
INCLUDEU  FORM      1                  * Include Unscheduled Patients
INCLUDES  FORM      1                  * Include Scheduled Patients
INCLUDEP  FORM      1                  * Include Pre-Admitted Patients
UPDATEFL  FORM      1                  * Update records yes/no
UNITCODE  DIM       3                  * Unit Code
UNITDESC  DIM       20                 * Print unit description
UNTOCODE  DIM       3                  * Unit To Code
UNTODESC  DIM       20                 * Print To unit description
WATHI002  DIM       8                  * Date of change in history
.
PRGID     INIT      "IBAWAT06"
PRGNAM    INIT      "Waiting List Bulk Transfer   "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      DOCF0000               * get doctor from
          BRANCH    EXIT,ML9999
.
ML2000    CALL      UNIT0000               * get doctor from unit 
          BRANCH    EXIT,ML9999
.
ML3000    CALL      DOCT0000               * get doctor to
          BRANCH    EXIT,ML9999
.
ML3500    CALL      UNTO0000               * get doctor to unit
          BRANCH    EXIT,ML9999
.
ML4000    CALL      INCU0000               * Include Unschduled Patients
          BRANCH    EXIT,ML9999
.
ML5000    CALL      INCS0000               * Include Scheduled Patients
          BRANCH    EXIT,ML9999
.
ML6000    CALL      INCP0000               * Include Pre-Admitted Patients
          BRANCH    EXIT,ML9999
.
ML6500    CALL      CRTX0000               * Create Export File for Excel  
          BRANCH    EXIT,ML9999
.
ML7000    CALL      VALD0000               * print report and or update
          BRANCH    EXIT,ML9999
.
ML8000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML9000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML9000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P60:24,"opening controlf"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,THIRTY7;*236,WTCNBRSR
.
          DISPLAY   *P60:24,"opening patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"opening patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P68:24,"wattr1af"
          OPEN      WATTR1A2,"wattr1af"
.
          DISPLAY   *P68:24,"watvwlaf"
          OPEN      WATVWLA1,"watvwlaf"
.
          DISPLAY   *P68:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P68:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P68:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P68:24,"wathisaf"
          OPEN      WATHISA1,"wathisaf"
.
          DISPLAY   *P68:24,"bokrc1a1"
          OPEN      BOKRC1A1,"bokrc1af"
.
          DISPLAY   *P68:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P68:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P68:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P68:24,"watproaf"
          OPEN      WATPROA1,"watproaf"
.
          MOVE      Z70,WATHI002
          MOVE      ZERO,ADDWLFLG
          MOVE      ZERO,UPDATEFL
          CALL      IBACLOCK
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Waiting List Bulk Transfer"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.************************************************************************
.*                         DOCF0000                                     *
.*             Keyin the DOCF that will be printed                      *
.*                   called by : ML1000                                 *
.************************************************************************
DOCF0000  DISPLAY   *P1:9,"Doctor Code : "
.
          MOVE      SP70,DOCTFROM
          MOVE      SP70,DOCTFNAM
          MOVE      "15",ECOL
          MOVE      "9",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,DOCF8000,DOCF0000
.
          MOVE      DCODE,DOCTFROM
.
DOCF4000  PACK      KEY6,DOCTFROM
          MOVE      DSNAME,PACSNAME             * format doctors name
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DOCFNAM
          MOVE      PACFNAME,DOCTFNAM
          DISPLAY   *P35:9,*V2LON,DOCFNAM
          GOTO      DOCF9999
.
DOCF8000  MOVE      ONE,EXIT
.
DOCF9999  RETURN
.************************************************************************
.*                         DOCT0000                                     *
.*             Keyin the DOCT that will be printed                      *
.*                   called by : ML2000                                 *
.************************************************************************
DOCT0000  DISPLAY   *P1:11,"Doctor Code : ",SP30
.
          MOVE      SP70,DOCTORTO
          MOVE      SP70,DOCTTNAM
          MOVE      "15",ECOL
          MOVE      "11",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,DOCT8000,DOCT0000
.
          MOVE      DCODE,DOCTORTO
.
....      MATCH     DOCTFROM,DOCTORTO
....      GOTO      DOCT7100 IF EQUAL
.
....      PACK      KEY9,DOCTORTO,UNITCODE,SP70
....      CALL      RDWTVWL1
....      BRANCH    OVRCD,DOCT7000
.
DOCT4000  PACK      KEY6,DOCTORTO
          MOVE      DSNAME,PACSNAME             * format doctors name
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DOCFNAM
          MOVE      PACFNAME,DOCTTNAM
          DISPLAY   *P35:11,*V2LON,DOCFNAM
          GOTO      DOCT9999
.
DOCT7000  PRINT     "Invalid Consultant to/Unit Combination "
          GOTO      DOCT8000
.
...DOCT7100  PRINT     "Doctor From/To Can Not Be The Same "
...       GOTO      DOCT8000
.
DOCT8000  MOVE      ONE,EXIT
.
DOCT9999  RETURN
.************************************************************************
.*                         UNIT0000                                     *
.*             Keyin the unit that will be printed                      *
.*                   called by : ML4000                                 *
.************************************************************************
UNIT0000  DISPLAY   *P1:10,"Doctor Unit : ",SP30
          MOVE      SP70,UNITCODE
          MOVE      SP70,UNITDESC
          MOVE      "15",ECOL
          MOVE      "10",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF3
          MOVE      CODEWU,CODE
          DISPLAY   *PECOL:EVERT,SP6
.
          CALL      PATCODKY
          BRANCH    EXIT,UNIT9100,UNIT9100
.
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,*P19:EVERT,TDESC
          MOVE      ACODE,UNITCODE
          MOVE      TDESC,UNITDESC
.
          PACK      KEY9,DOCTFROM,UNITCODE,SP70
          CALL      RDWTVWL1
          BRANCH    OVRCD,UNIT8000
.
          GOTO      UNIT9000
.
UNIT8000  PRINT     "Invalid Consultant/Unit Combination "
          GOTO      UNIT9100
.
UNIT9000  MOVE      ZERO,EXIT
          GOTO      UNIT9999
.
UNIT9100  MOVE      ONE,EXIT
.
UNIT9999  RETURN
+
.************************************************************************
.*                         UNTO0000                                     *
.*             Keyin the unit that will be printed                      *
.*                   called by : ML4000                                 *
.************************************************************************
UNTO0000  DISPLAY   *P1:13,"Doctor Unit : ",SP30
          MOVE      SP70,UNTOCODE
          MOVE      SP70,UNTODESC
          MOVE      "15",ECOL
          MOVE      "10",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF3
          MOVE      CODEWU,CODE
          DISPLAY   *PECOL:EVERT,SP6
.
          CALL      PATCODKY
          BRANCH    EXIT,UNTO9100,UNTO9100
.
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,*P19:EVERT,TDESC
          MOVE      ACODE,UNTOCODE
          MOVE      TDESC,UNTODESC
.
          PACK      KEY9,DOCTORTO,UNTOCODE,SP70
          CALL      RDWTVWL1
          BRANCH    OVRCD,UNTO8000
.
          MATCH     DOCTFROM,DOCTORTO
          GOTO      UNTO9000 IF NOT EQUAL
.
          MATCH     UNITCODE,UNTOCODE
          GOTO      UNTO8100 IF EQUAL
.
          GOTO      UNTO9000
.
UNTO8000  PRINT     "Invalid Consultant/Unit Combination "
          GOTO      UNTO9100
.
UNTO8100  PRINT     "No changes required, doctor/unit from and to are equal "
          GOTO      UNTO9100
.
UNTO9000  MOVE      ZERO,EXIT
          GOTO      UNTO9999
.
UNTO9100  MOVE      ONE,EXIT
.
UNTO9999  RETURN
+
.****************************************************************************
.*                                VALD0000             Called by: ML0000    *
.* Print validation report only or print report and update records          *
.****************************************************************************
.
VALD0000  MOVE      ZERO,UPDATEFL                  * No to update records
          MOVE      ZERO,EXIT
.
VALD1000  DISPLAY   *P1:16,"Update Records : " 
          KEYIN     *P18:16,*V2LON,*RV,DIM1:
                    *P18:16,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P18:16,*V2LON,DIM1
          MOVE      ONE,UPDATEFL
          MATCH     "Y",DIM1
          GOTO      VALD9999 IF EQUAL
.
          MOVE      ZERO,UPDATEFL
          MATCH     "N",DIM1
          GOTO      VALD9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     VALD9000 IF EQUAL
.
          BEEP
          GOTO      VALD1000
.
VALD9000  MOVE      ONE,EXIT
          GOTO      VALD9999
.
VALD9999  RETURN
+
.****************************************************************************
.*                                INCU0000             Called by: ML0000    *
.* Include Unscheduled patients                                             *
.****************************************************************************
.
INCU0000  MOVE      ZERO,INCLUDEU                  * No to include unscheduled
          MOVE      ZERO,EXIT
.
INCU1000  DISPLAY   *P1:12,"Include Unscheduled Patients : "
          KEYIN     *P32:12,*V2LON,*RV,DIM1:
                    *P32:12,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:12,*V2LON,DIM1
          MOVE      ONE,INCLUDEU
          MATCH     "Y",DIM1
          GOTO      INCU9999 IF EQUAL
.
          MOVE      ZERO,INCLUDEU
          MATCH     "N",DIM1
          GOTO      INCU9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     INCU9000 IF EQUAL
.
          BEEP
          GOTO      INCU1000
.
INCU9000  MOVE      ONE,EXIT
          GOTO      INCU9999
.
INCU9999  RETURN
+
.****************************************************************************
.*                                INCS0000             Called by: ML0000    *
.* Include Scheduled patients                                               *
.****************************************************************************
.
INCS0000  MOVE      ZERO,INCLUDES                  * No to include scheduled
          MOVE      ZERO,EXIT
.
INCS1000  DISPLAY   *P1:13,"Include Scheduled Patients   : "
          KEYIN     *P32:13,*V2LON,*RV,DIM1:
                    *P32:13,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:13,*V2LON,DIM1
          MOVE      ONE,INCLUDES
          MATCH     "Y",DIM1
          GOTO      INCS9999 IF EQUAL
.
          MOVE      ZERO,INCLUDES
          MATCH     "N",DIM1
          GOTO      INCS9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     INCS9000 IF EQUAL
.
          BEEP
          GOTO      INCS1000
.
INCS9000  MOVE      ONE,EXIT
          GOTO      INCS9999
.
INCS9999  RETURN
+
.****************************************************************************
.*                                INCP0000             Called by: ML0000    *
.* Include Scheduled patients                                               *
.****************************************************************************
.
INCP0000  MOVE      ZERO,INCLUDEP                  * No to include scheduled
          MOVE      ZERO,EXIT
.
INCP1000  DISPLAY   *P1:14,"Include Scheduled Patients   : "
          KEYIN     *P32:14,*V2LON,*RV,DIM1:
                    *P32:14,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:14,*V2LON,DIM1
          MOVE      ONE,INCLUDEP
          MATCH     "Y",DIM1
          GOTO      INCP9999 IF EQUAL
.
          MOVE      ZERO,INCLUDEP
          MATCH     "N",DIM1
          GOTO      INCP9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     INCP9000 IF EQUAL
.
          BEEP
          GOTO      INCP1000
.
INCP9000  MOVE      ONE,EXIT
          GOTO      INCP9999
.
INCP9999  RETURN
+
.****************************************************************************
.*                                CRTX0000             Called by: ML0000    *
.* Create export file for excel                                             *
.****************************************************************************
.
CRTX0000  MOVE      ZERO,EXPORTFL                  * No to create export file
          MOVE      ZERO,EXIT
.
CRTX1000  DISPLAY   *P1:15,"Create Export file for Excel : "
          KEYIN     *P32:15,*V2LON,*RV,DIM1:
                    *P32:15,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:15,*V2LON,DIM1
          MOVE      ONE,EXPORTFL
          MATCH     "Y",DIM1
          GOTO      CRTX9999 IF EQUAL
.
          MOVE      ZERO,EXPORTFL
          MATCH     "N",DIM1
          GOTO      CRTX9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     CRTX9000 IF EQUAL
.
          BEEP
          GOTO      CRTX1000
.
CRTX9000  MOVE      ONE,EXIT
          GOTO      CRTX9999
.
CRTX9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  IF        INCLUDEU=0 & INCLUDES=0 & INCLUDEP=0
            GOTO      PROC9100
          ENDIF
.
          CALL      HEAD0000 
          MOVE      ZERO,RECCOUNT
          MOVE      ZERO,CPAGENO
.
          PACK      KEY20,ONE,SP70
          CALL      RDSTREA2
PROC1000  CALL      RDKTREA2
          BRANCH    OVRCD,PROC9000
.
          BRANCH    WMSTAT,PROC2000,PROC2000,PROC2000
.                          Unsched  Schedul  Pre-Admitted
          GOTO      PROC9000
.
PROC2000  IF        INCLUDEU=1
            COMPARE   ONE,WMSTAT             * Unscheduled
            GOTO      PROC3000 IF EQUAL
          ENDIF
.
          IF        INCLUDES=1
            COMPARE   TWO,WMSTAT             * Scheduled
            GOTO      PROC3000 IF EQUAL
          ENDIF
.
          IF        INCLUDEP=1
            COMPARE   THREE,WMSTAT           * Pre-Admitted
            GOTO      PROC3000 IF EQUAL
          ENDIF
.
          GOTO      PROC1000
.
PROC3000  MATCH     WMDOCTOR,DOCTFROM
          GOTO      PROC1000 IF NOT EQUAL   * Correct Doctor
.
          MATCH     WMUNIT,UNITCODE
          GOTO      PROC1000 IF NOT EQUAL   * Correct Unit
. 
          CALL      PRUR0000                * Print U/R Line details
.
          IF        UPDATEFL=1
            CALL      SAVHSS00              * Save Variables for History Write
            MOVE      DOCTORTO,WMDOCTOR     * Update Doctor
            MOVE      UNTOCODE,WMUNIT       * Update Unit
            CALL      UPTREA2
            CALL      CHGDOC00              * Update Doctor
            MOVE      "31",WTWMBSCD
            CALL      WRITSS00
            CALL      CHGHIS00               * Update all history record
          ENDIF
.
          GOTO      PROC1000
.
PROC9000  CALL      TAIL0000                * Print End of Report
          GOTO      PROC9999
.
PROC9100  DISPLAY   *P1:24,"Please select one type on inclusion ";
          CALL      HITENTER
          GOTO      PROC9999
.
PROC9999  RETURN
.****************************************************************************
.*        Print UR and Name                                                 *
.****************************************************************************
PRUR0000  ADD       ONE,RECCOUNT
          PACK      KEY8,WMURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PRUR9999
.
          MOVE      PTITL,PACTITLE
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Pack Full Name
          MOVE      PACFNAME,DIM35
.
          MOVE      WMDESC,DESC29
.
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATE1
.
          UNPACK    WMDATE3,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATE2
.
          IF        EXPORTFL=1
            PRINT     "<tr><td>",WMURNO,"</td>":
                      "<td>",DIM35,"</td>":
                      "<td>",WMPTY,"</td>":
                      "<td>",WMCODE,"</td>":
                      "<td>",WMCNT,"</td>":
                      "<td>",DESC29,"</td>":
                      "<td>",DATE1,"</td>":
                      "<td>",DATE2,"</td></tr>"
            GOTO      PRUR9999
          ENDIF
.
          COMPARE   "60",CLNO
          GOTO      PRUR1000 IF LESS
          CALL      HEAD0000                 * Print  header
.
PRUR1000  PRINT     *1,"| ",WMURNO," | ",DIM35 ," | ",WMPTY:
                    " | ",WMCODE," | ",WMCNT," | ",DESC29:
                    *106,"| ",DATE1,*119,"| ",DATE2,*132,"|"
          ADD       ONE,CLNO
          GOTO      PRUR9999
.
PRUR9999  RETURN
.****************************************************************************
.*        Trailer Print Routine                                             *
.****************************************************************************
TAIL0000  BRANCH    EXPORTFL,TAIL9999
          
.         COMPARE   "60",CLNO
          GOTO      TAIL1000 IF LESS
          CALL      HEAD0000                 * Print header
.
TAIL1000  CALL      UND132
          PRINT     *1,"Records Selected ",RECCOUNT
          CALL      UND132
          PRINT     *N,"*** End of Report ***"
          DISPLAY   *P1:24,*EL,*P28:24,*V2LON,"** Generation Completed **"
.
TAIL9999  RETURN
.
.---------------------------------------------------------------------
. CHGDOC00 - Check if the doctor need to be updated
.---------------------------------------------------------------------
CHGDOC00  COMPARE   TWO,WMSTAT                   * Scheduled
          GOTO      CHGDOC10 IF EQUAL
.
          COMPARE   THREE,WMSTAT                 * Pre-Admitted
          GOTO      CHGDOC10 IF EQUAL
.
          GOTO      CHGDOC99
.
CHGDOC10  PACK      KEY8,WMBOOK
          CALL      RDBKREC1
          BRANCH    OVRCD,CHGDOC99
.
          COMPARE   ZERO,BKRESTAT                * Booked
          GOTO      CHGDOC20 IF EQUAL
.
          COMPARE   ONE,BKRESTAT                 * Pre-Admitted
          GOTO      CHGDOC20 IF EQUAL
          GOTO      CHGDOC99
.
CHGDOC20  MATCH     SP70,BKRETYPE                * Update doctor if it is a
          GOTO      CHGDOC99 IF EQUAL            * medical booking
.
          PACK      KEY5,ANSB,ANSK,BKRETYPE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHGDOC99
.
          MATCH     "THE ",THCSCOD
          GOTO      CHGDOC99 IF EQUAL
.
CHGDOC30  MOVE      WMDOCTOR,BKREADOC
          CALL      UPBKREC1
.
          COMPARE   ZERO,BKRESTAT                * No visit or admission if
          GOTO      CHGDOC99 IF EQUAL            * status is booked
.
          PACK      KEY8,WMBOOK
          CALL      RDMISS1
          BRANCH    OVRCD,CHGDOC99
.
          MOVE      WMDOCTOR,ADOCTA
          CALL      UPMISS1
.
          PACK      KEY8,WMBOOK
          CALL      RDVISA1
          BRANCH    OVRCD,CHGDOC99
.
          MOVE      WMDOCTOR,PVIDOCT
          CALL      UPVISA1
.
          PACK      KEY8,WMBOOK
          CALL      RDPMVX11
          BRANCH    OVRCD,CHGDOC99
.
          MOVE      WMDOCTOR,PMVXDOCA
          CALL      UPPMVX11
.
CHGDOC99  RETURN
.************************************************************************
.*                        HEAD0000                                      *
.*                 Produce printout heading                             *
.*                   called by : OUT0000,OUT1500,OUT2500                *
.************************************************************************
HEAD0000  IF        EXPORTFL=1
            PRINT     "<table border=1><tr><td width=10%>U/R No</td>":
                      "<td>Patient Name</td>":
                      "<td>Category</td>":
                      "<td>Proc Code</td>":
                      "<td>Cnt</td>":
                      "<td>Procedure Description</td>":
                      "<td>List Date</td>":
                      "<td>Sched Adm</td></tr>"
            GOTO      HEAD9999
          ENDIF
.
          MOVE      ZERO,CLNO
          CALL      HEAD132
.
          PRINT     *40,"Doctor From : ",DOCTFROM,SP1,DOCTFNAM
          PRINT     *40,"Unit        : ",UNITCODE,SP1,UNITDESC
          PRINT     *40,"Doctor To   : ",DOCTORTO,SP1,DOCTTNAM
.
          IF        UPDATEFL=1
            PRINT     *40,"Update Records : ",DYES
          ELSE
            PRINT     *40,"Update Records : ",DNO
          ENDIF
.
          CLEAR     DIM55
          APPEND    "Include : ",DIM55
.
          IF        INCLUDEU=1
            APPEND    "Unscheduled, ",DIM55
          ENDIF
.
          IF        INCLUDES=1
            APPEND    "Scheduled, ",DIM55
          ENDIF
.
          IF        INCLUDEP=1
            APPEND    "Pre-Admitted ",DIM55
          ENDIF
.
          APPEND    "Patients ",DIM55
.
          PRINT     *40,DIM55
.
          CALL      UND132
.
          PRINT     *1,"| U/R No",*12,"| Patient Name",*50," | Cat ":
                    *57,"| Proc Code" :
                    *69,"|Cnt",*74,"| Procedure Description":
                    *105," | List Date":
                    *118," | Sched Adm",*132,"|"
.
          CALL      UND132
          ADD       EIGHT,CLNO
.
HEAD9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATDOCKY
          INC       PATCODKY
          INC       WLHISSUB
.
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       WATTR1IO/INC
          INC       WATVWLIO/INC
          INC       PATVISIO/INC
          INC       PMSVX1IO/INC
          INC       PATMI1IO/INC
          INC       WATHISIO/INC
          INC       BOKRC1IO/INC
          INC       PATDSCIO/INC
          INC       PATMA1IO/INC
          INC       WEBSECIO/INC
          INC       WATPROIO/INC

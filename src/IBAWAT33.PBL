.******************************************************************************
.*   program name : IBAWAT33                                                  *
.*   description  : Current Inpatients Report                                 *
.*   system       : waiting lists                                             *
.******************************************************************************
.*   author       : Paul Duncan                                               *
.*   date         : 30/05/90                                                  *
.*                                                                            *
.*   comments     :                                                           *
.*                                                                            *
.*   mods         :                                                           *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V10.05.01 05/01/2015  Ebon Clements     CAR 261630                  *
.*                  Removed port number from temp file name                   *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.01  27/10/1999 J Habasque                                     *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
.*                  V5.01.10 03/08/90 Justin Coates                           *
.*                           Added the use of WTCNTPDS                        *
.******************************************************************************
.*                        FD INCLUDES                                         *
.******************************************************************************
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       TFILEVAR/INC
          INC       WATCONFD/INC
          INC       WATPROFD/INC
          INC       WATTR1FD/INC
          INC       WEBERRFD/INC

.************************************************************************
.*                        CONSTANTS                                     *
.************************************************************************
CODEWU    INIT      "WU"
.
DESCOPT1  INIT      "Surname Sequence                   "
DESCOPT2  INIT      "Scheduled Admission Date Sequence  "
.
KEYOPT1   INIT      "u92-111,1-8,9-17,18-19"
KEYOPT2   INIT      "u70-77,1-8,9-17,18-19"
.
.
.************************************************************************
.*                        VARIABLES                                     *
.************************************************************************
CMDLINE   DIM       80
.
DESCOPT   DIM       35               * display option
DISPDATE  DIM       10               * print date
DOCNAME   DIM       18               * print doctors name
.
.
PATCNT    FORM      4                * printed patient count
PATNAME   DIM       29               * print patients name
PROCDESC  DIM       31               * print proc. description
.
STRTFLAG  FORM      1
.
TEMPKEY   DIM       40
TEMPFIL   IFILE     SQL, FIXED=144, KEY=TEMPKEY
.
UNITDESC  DIM       11               * print unit description
WATTEMP1  DIM       8
.
.************************************************************************
PRGNAM    INIT      "Current Inpatients Report"
PRGID     INIT      "IBAWAT33"
VERSION   INIT      "V12.01.00"
.************************************************************************
.*                         MAINLINE                                     *
.************************************************************************
ML0000 
          CALL      INIT0000             * Display header and open files
.
ML1000    CALL      OPTN0000             * select option
          BRANCH    MOPTION,ML2000,ML2000
.
          GOTO      ML9999               * exit chosen
ML2000 
          CALL      CONTQST
          BRANCH    CEXIT,ML2222,ML1000,ML9999
ML2222
          CALL      EXTR0000             * extract records and put in tempfile
.
          CALL      OUT0000              * produce printout
          GOTO      ML1000
.
ML9999    CALL      KILL0000             * Delete temp file
          CHAIN     PGM
.************************************************************************
.*                         INIT0000                                     *
.*               Display Header and open files                          *
.*                   called by : ML0000                                 *
.************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"opening patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"opening patdo1af"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P68:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P68:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P68:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P68:24,"watproaf"
          OPEN      WATPROA1,"watproaf"
.
          DISPLAY   *P68:24,"wattr1af"
          OPEN      WATTR1A2,"wattr1af"
.
          DISPLAY   *P68:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,THIRTY7;*122,WTCNTPUC,*185,WTCNTPDS
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,WATTEMP1
.
INIT9999  RETURN
.************************************************************************
.*                        OPTN0000                                      *
.*             select option                                            *
.*                   called by : ML1000                                 *
.************************************************************************
OPTN0000  CALL      SCRN0000           * Display screen
.
OPTN1000  KEYIN     *P17:8,*DV,UNDLN1,*P17:8,*V2LON,MOPTION,*DV,*P17:8,MOPTION
          BRANCH    MOPTION,OPTN2000,OPTN2000
.
          COMPARE   ZERO,MOPTION        * exit chosen
          GOTO      OPTN9999 IF EQUAL
          BEEP                         * invalid entry
          GOTO      OPTN1000
.
OPTN2000  LOAD      DESCOPT,MOPTION,DESCOPT1,DESCOPT2
          MOVE      "- ",CPHDROPT
          APPEND    DESCOPT,CPHDROPT
.
          DISPLAY   *P53:2,*V2LON,"- ",*+,DESCOPT,SP1    * display screen head
          GOTO      OPTN9999
.
OPTN9999  RETURN
.************************************************************************
.*                        SCRN0000                                      *
.*                Display main options screen                           *
.*                   called by : OPTN0000                               *
.************************************************************************
SCRN0000  HLINE     *G37,2,53,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = ",DESCOPT1:
                    *P1:6,*V2LON,TWO,*HOFF," = ",DESCOPT2:
                    *P1:8,"Select Option : "
.
SCRN9999  RETURN
.************************************************************************
.*                        EXTR0000                                      *
.*            Extract values and put in tempfile                        *
.*                   called by : ML1000                                 *
.************************************************************************
EXTR0000  CALL      CREA0000
          DISPLAY   *P1:24,"Extracting record : "
          MOVE      ZERO,STRTFLAG
          PACK      KEY20,FOUR,SP20     * position at start of admitted patients
          CALL      RDSTREA2
.
EXTR1000  CALL      RDKTREA2
          BRANCH    OVRCD,EXTR4000
.
          COMPARE   FOUR,WMSTAT         * only want admitted patients
          GOTO      EXTR4000 IF NOT EQUAL
.
          DISPLAY   *P21:24,*V2LON,WMURNO
          MOVE      SP30,PSNAME
          MOVE      WMURNO,KEY8
          CALL      RDMAST1            * get patients name from master file
.
          MOVE      SP3,AWARD
          MOVE      WMBOOK,KEY8
          CALL      RDMISS1            * get ward
          MOVE      ONE,STRTFLAG
.
          BRANCH    MOPTION,EXTR2000,EXTR3000 
.
EXTR2000  PACK      KEY39,PSNAME,WMURNO,WMCODE,WMCNT
          CALL      WRTMPR1             * write with the first key
          GOTO      EXTR1000
.
EXTR3000  PACK      KEY27,WMDATE3,WMURNO,WMCODE,WMCNT
          CALL      WRTMPR2             * write with the second key
          GOTO      EXTR1000
.
EXTR4000  BRANCH    STRTFLAG,EXTR9999  * any written to tempfile?
          DISPLAY   *P1:24,"** No patients admitted ** ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL;
          GOTO      EXTR9999
.
EXTR9999  RETURN
.************************************************************************
.*                         OUT0000                                      *
.*                       Produce the printout                           *
.*                   called by : ML1000                                 *
.************************************************************************
OUT0000   MOVE      ZERO,CPAGENO      * Set up heading variables
          MOVE      ZERO,PATCNT
          CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
          DISPLAY   *P1:24,*EL,"Printing Record : "
          CALL      HEAD0000          * print heading
.
          BRANCH    MOPTION,OUT1000,OUT2000

OUT1000   PACK      KEY39,SP30,SP10
          CALL      RSTMPR1
.
OUT1500   CALL      RKTMPR1
          BRANCH    OVRCD,OUT4000
.
          DISPLAY   *P19:24,*V2LON,WMURNO
          COMPARE   "55",CLNO        * new page?
          CALL      HEAD0000 IF NOT LESS
.
          CALL      FRMT0000         * format data to print
.
          CALL      PRNT0000         * print a line of output
          GOTO      OUT1500
OUT2000   PACK      KEY27,SP30
          CALL      RSTMPR2
.
OUT2500   CALL      RKTMPR2
          BRANCH    OVRCD,OUT4000
.
          DISPLAY   *P19:24,*V2LON,WMURNO
          COMPARE   "55",CLNO        * new page?
          CALL      HEAD0000 IF NOT LESS
.
          CALL      FRMT0000         * format data to print
.
          CALL      PRNT0000         * print a line of output
          GOTO      OUT2500
.
OUT4000   CALL      END0000          * print total and "end of report"
          DISPLAY   *P1:24,*EL
.
OUT9999   RETURN
.************************************************************************
.*                        HEAD0000                                      *
.*                 Produce printout heading                             *
.*                   called by : OUT0000,OUT1500,OUT2500                *
.************************************************************************
HEAD0000  MOVE      ZERO,CNOUNDLN
          CALL      HEAD132       
.
          PRINT     *N
          CALL      UND132
          PRINT     "|",*3,"U/R No.",*12,"|",*14,"Patient Name",*45,"|":
                    *47,WTCNTPDS," Description",*78,"|",*81,"Admitted":
                    *91,"|",*93,"Ward",*98,"|",*100,"Unit/Clinic",*111,"|":
                    *113,"Doctor Name",*132,"|"
          CALL      UND132
.
          ADD      FOUR,CLNO
.
HEAD9999  RETURN
.************************************************************************
.*                        FRMT0000                                      *
.*          Convert data to the format to print                         *
.*                   called by : OUT1500,OUT2500                        *
.************************************************************************
FRMT0000   MATCH    SP20,PSNAME
           GOTO     FRMT0100 IF NOT EQUAL
           MOVE     "Error - patient master missing",PATNAME
           GOTO     FRMT0200
FRMT0100   MOVE     PGNAME,PACGNAME      * move patients to pacname variables
           MOVE     PSNAME,PACSNAME
           MOVE     PTITL,PACTITLE
           MOVE     TWO,PACFRMT
           CALL     PACNAME              * format name
           MOVE     PACFNAME,PATNAME     * move formatted name to print variable
.
FRMT0200   MOVE     "Error - Unknown procedure",PROCDESC
           MOVE     WMCODE,KEY9         
           CALL     RDPROA1              * get proc. description
           BRANCH   OVRCD,FRMT1000
           MOVE     WPDESC,PROCDESC      * move it to the print variable
.
FRMT1000
           MOVE     SP8,DISPDATE
           MATCH    SP8,WMDATE3
           GOTO     FRMT1111 IF EQUAL
           UNPACK   WMDATE3,CCENT,CYEAR,CMON,CDAY * set up admiss. date
           PACK     DISPDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
FRMT1111                               * This is a Bill Dew labels
           MOVE     "unknown",UNITDESC
           PACK     KEY5,CODEWU,WMUNIT
           CALL     RDCODE1              * get unit description
           BRANCH   OVRCD,FRMT2000
           MOVE     TDESC,UNITDESC       * move to print variable 
.
FRMT2000   MOVE     "Code :",DOCNAME     * set up message in case
           SCAN     SP6,DOCNAME          * code not on file
           APPEND   WMDOCTOR,DOCNAME
           MOVE     WMDOCTOR,KEY6
           CALL     RDDOCT1              * get doctor name
           BRANCH   OVRCD,FRMT9999
           MOVE     DSNAME,PACSNAME      * set up pacname variables
           MOVE     DGNAME,PACGNAME
           MOVE     DTITL,PACTITLE
           MOVE     TWO,PACFRMT 
           CALL     PACNAME              * format name 
           MOVE     PACFNAME,DOCNAME     * move to print variable
.
FRMT9999   RETURN
.************************************************************************
.*                        PRNT0000                                      *
.*              Print a line of output                                  *
.*                   called by : OUT1500,OUT2500                        *
.************************************************************************
PRNT0000  PRINT     "|",*3,WMURNO,*12,"|",*14,PATNAME,*45,"|",*47,PROCDESC:
                    *78,"|",*80,DISPDATE,*91,"|",*94,AWARD,*98,"|",*100:
                    UNITDESC,*111,"|",*113,DOCNAME,*132,"|"
.
          ADD       ONE,PATCNT                  * update patirent count
          ADD       ONE,CLNO                    * update line number
.
PRNT9999  RETURN
.************************************************************************
.*                        END0000                                       *
.*             Print total and end of report                            *
.*                   called by :  OUT4000                               *
.************************************************************************
END0000   CALL      UND132
          PRINT     *N,"Total Patients Printed : ",PATCNT:
                    *N,*N,"*** End of Report ***"
END9999   RETURN
.**********************************************************************
.*                              CREA0000                              *
.*                   Create the Indexed Temp File                     *
.**********************************************************************
CREA0000  CALL      KILL0000                       * kill existing temp file
.
.------ Build new indexed temp file ------
.
          MOVE      SP30,TEMPKEY
          LOAD      TEMPKEY,MOPTION,KEYOPT1,KEYOPT2
          BRANCH    MOPTION,CREA1000,CREA2000
.
CREA1000  CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    WATTEMP1,CMDLINE
          APPEND    " 144 u92-111,1-8,9-17,18-19",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFIL,WATTEMP1
          GOTO      CREA9999
.
CREA2000  CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    WATTEMP1,CMDLINE
          APPEND    " 144 u70-77,1-8,9-17,18-19",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFIL,WATTEMP1
.
CREA9999  RETURN
+
.**********************************************************************
.*                              KILL0000                              *
.*                  Kill the Tempfile if it Already Exists            *
.**********************************************************************
KILL0000  CLEAR     CMDLINE
          CLOSE     TEMPFIL
          APPEND    "iserase ",CMDLINE
          APPEND    WATTEMP1,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.************************************************************************
.*                    I/O Routines for Tempfile                         *
.************************************************************************
RATMPR1   MOVE      ZERO,OVRCD
          RESET     KEY39
          READ      TEMPFIL,KEY39;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY39
          READ      TEMPFIL,KEY39;;
          RETURN
.
RDTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY39
          READ      TEMPFIL,KEY39;WMURNO,WMCODE,DWMCNT,WMDESC,WMDATE3,WMUNIT:
                                  WMDOCTOR,WMBOOK,PSNAME,PGNAME,PTITL,AWARD
          GOTO      OVERCOND IF OVER
          MOVE      DWMCNT,WMCNT
          RETURN
.
RKTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY39
          READKS    TEMPFIL;WMURNO,WMCODE,DWMCNT,WMDESC,WMDATE3,WMUNIT:
                                  WMDOCTOR,WMBOOK,PSNAME,PGNAME,PTITL,AWARD
          GOTO      OVERCOND IF OVER
          MOVE      DWMCNT,WMCNT
          RETURN
.
WRTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY39
          MOVE      WMCNT,DWMCNT
          WRITE     TEMPFIL,KEY39;WMURNO,WMCODE,DWMCNT,WMDESC,WMDATE3,WMUNIT:
                                  WMDOCTOR,WMBOOK,PSNAME,PGNAME,PTITL,AWARD
          GOTO      OVERCOND IF OVER
          RETURN
.
RATMPR2   MOVE      ZERO,OVRCD
          RESET     KEY27
          READ      TEMPFIL,KEY27;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMPR2   MOVE      ZERO,OVRCD
          RESET     KEY27
          READ      TEMPFIL,KEY27;;
          RETURN
.
RDTMPR2   MOVE      ZERO,OVRCD
          RESET     KEY27
          READ      TEMPFIL,KEY27;WMURNO,WMCODE,DWMCNT,WMDESC,WMDATE3,WMUNIT:
                                  WMDOCTOR,WMBOOK,PSNAME,PGNAME,PTITL,AWARD
          GOTO      OVERCOND IF OVER
          MOVE      DWMCNT,WMCNT
          RETURN
.
RKTMPR2   MOVE      ZERO,OVRCD
          RESET     KEY27
          READKS    TEMPFIL;WMURNO,WMCODE,DWMCNT,WMDESC,WMDATE3,WMUNIT:
                                  WMDOCTOR,WMBOOK,PSNAME,PGNAME,PTITL,AWARD
          GOTO      OVERCOND IF OVER
          MOVE      DWMCNT,WMCNT
          RETURN
.
WRTMPR2   MOVE      ZERO,OVRCD
          RESET     KEY27
          MOVE      WMCNT,DWMCNT
          WRITE     TEMPFIL,KEY27;WMURNO,WMCODE,DWMCNT,WMDESC,WMDATE3,WMUNIT:
                                  WMDOCTOR,WMBOOK,PSNAME,PGNAME,PTITL,AWARD
          GOTO      OVERCOND IF OVER
          RETURN
.
.************************************************************************
.*                        I/O INCLUDES                                  *
.************************************************************************
          INC       STD001IO
          INC       IBASEQIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       TFILENAM
          INC       WATPROIO/INC
          INC       WATTR1IO/INC
          INC       WEBERRIO/INC
         

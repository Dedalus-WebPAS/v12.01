.***************************************************************************
.* System    :   Private Practice                                          *
.* Program   :   FXPRITMN                                                  *
.* Desc      :   Update Private practice item file                         *
.***************************************************************************
.* Date      :   01/07/2005                                                *
.* Author    :   J.Tan                                                     *
.* Function  :   This program will copy all records from patitemn to       *
.*           :   priitemf file                                             *
.*           :                                                             *
.* Mods      :                                                             *
.*               V9.04.03 27/10/05  J.Tan   CAR 81811                      *
.*               Mods updating for 01/11/05                                *
.*               V9.04.02  J.Tan                                           *
.*               Mods to update keyword search file                        *
.*               V9.04.01  J.Tan                                           *
.*               Mods to squeeze item code                                 *
.*               V9.04.00  J.Tan      CAR 64299                            *
.*           :   Created                                                   *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PRIIKIFD/INC
          INC       PATITMFD/INC
          INC       PRIITMFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
TOTREC    FORM      8
RECNUM    FORM      8
DIM12     DIM       12
FORM9     FORM      9
ITEMTYP   DIM       2
.ITEMNO    DIM       9
ITEMDESC  DIM       30
.
PRGID     INIT      "FXPRITMN"
PRGNAM    INIT      "Update Private Practice Item file"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PATITEM1,"patitemn"
          OPEN      PRIITEM1,"priitemf"
.
          MOVE      ZERO,RECNUM
          MOVE      ZERO,TOTREC
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Fixit":
                    *P1:6,*V2LON,TWO,*HOFF,". Print Only"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  IF        COPTION=2
            CALL      HEAD0000              * Print heading
          ENDIF
          DISPLAY   *P1:20,"Item:",*P26:20,"Record: ";
          PACK      KEY9,SP70
          CALL      RDSITEM1
PROC1000  CALL      RDKITEM1                * Check all patitem records
          BRANCH    OVRCD,PROC9000
.
          ADD       ONE,RECNUM                    * update record counter
.         IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,ITEMNO,*P34:20,RECNUM;
.         ENDIF
.
.         MOVE      " 0",DPRITFLG           * MBS code
.         MOVE      ITEMNO,PRITITMN
.         MOVE      SP70,PRITSUBN
.         PACK      KEY22,DPRITFLG,PRITITMN,PRITSUBN,SP70
.         CALL      RDPRIT1
.         COMPARE   ZERO,OVRCD
.         GOTO      PROC1000 IF EQUAL       * Already exist
.         CALL      RKPRIT1
.         BRANCH    OVRCD,PROC2000
.
.         MATCH     " 0",DPRITFLG
.         GOTO      PROC2000 IF NOT EQUAL
.         MATCH     ITEMNO,PRITITMN
.         GOTO      PROC2000 IF NOT EQUAL
.         MATCH     SP70,PRITSUBN
.         GOTO      PROC1000 IF EQUAL        * next item
.
PROC2000  MOVE      " 0",DPRITFLG           * MBS code
          SQUEEZE   ITEMNO
          MOVE      ZERO,FORM9
          MOVE      ITEMNO,FORM9
          MOVE      FORM9,ITEMNO
          SQUEEZE   ITEMNO
          PACK      PRITITMN,ITEMNO,SP70
.
          MOVE      SP70,PRITSUBN
          MOVE      "20051101",PRITDAT1
          MOVE      IDESC,PRITDESC
          MOVE      ZERO,PRITKEYI
          MOVE      QIAMT,PRITSFEE
          MOVE      "HIC",PRITSETC
          MOVE      SP70,PRITDAT2            * Date item will cease
          MOVE      SP70,PRITICAT            * Category
          MOVE      SP70,PRITIGRP            * Item Group
          MOVE      SP70,PRITSGRP            * Item Subgroup
          MOVE      "S",PRITFTYP             * Fee Type S - Standard Item
          MOVE      SP70,PRITPTYP            * Provider Type
          MOVE      ONE,PRITPATH             * Pathology item indic 0=Pathology
          MOVE      ZERO,PRITMAXR            * Maximum no. of Requests
          MOVE      ZERO,PRITDAYS            * Period of Days
          MOVE      SP70,PRITSTEP            * Item to stepdown to after max req
          MOVE      SP70,PRITSTSB            * Sub-item to stepdown to
          MOVE      ONE,PRITFIXD             * Fixed charged item ?  1 = Yes
          MOVE      PTITGSTA,PRITGSTA
          MOVE      PTITGSTC,PRITGSTC
.
          ADD       ONE,TOTREC
          IF        COPTION=1
            PACK      KEY22,DPRITFLG,PRITITMN,PRITSUBN,PRITDAT1
            CALL      RAPRIT1
            COMPARE   ZERO,OVRCD
            GOTO      PROC1000 IF EQUAL      * Already exist
            CALL      WRPRIT1
.
            MOVE      PRITFLAG,ITEMTYP  * Input var for PRIIKIUP
            MOVE      PRITITMN,ITEMNO   * Input var for PRIIKIUP
            MOVE      PRITDESC,ITEMDESC * Input var for PRIIKIUP
            PROC      PRIIKIUP
          ELSE
            COMPARE   "55",CLNO
            CALL      HEAD0000 IF NOT LESS    * Line counter >= 55 lines
.
            MOVE      SP70,DIM12
            MOVE      "GST Free",DIM12
            IF        PRITGSTA=1
              MOVE      "GST Payable",DIM12
            ENDIF
.
            PRINT     *1,PRITITMN,*15,PRITDESC,*55,PRITSFEE:
                      *75,DIM12,*95,PRITGSTC
            ADD       ONE,CLNO
          ENDIF
.
          GOTO      PROC1000
.
PROC9000  PRINT      *1,TOTREC," Records Added."
PROC9999  RETURN
+
. =========================================================================
.         Print heading
. =========================================================================
HEAD0000  CALL      HEAD132
          PRINT     *N;
          CALL      UND132
          PRINT     *1,"Item",*15,"Description",*55,"Schedule Fee":
                    *75,"GST Applicable",*95,"GST Code"
          CALL      UND132
          ADD       TWO,CLNO
HEAD9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PRIIKIPR     * AMA/MBS Item Keyword Proc
.
          INC       PRIIKIIO/INC
          INC       PATITMIO/INC
          INC       PRIITMIO/INC

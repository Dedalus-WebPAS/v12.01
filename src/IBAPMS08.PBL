.*****************************************************************************
.* System    :   Patient Management                                          *
.* Program   :   IBAPMS08                                                    *
.* Desc      :   Eclipse Assessment Report Printing                          *
.*****************************************************************************
.* Date      :   15/05/2008                                                  *
.* Author    :   Davin Sloan                                                 *
.* Function  :   This program will allow the user to report on the raw       *
.*               assessment details for a given claim.                       *
.* Mods      :                                                               *
.*        V9.11.00  31/12/2008  Steve Armstrong   CAR 183976                 *
.*                  Ported from V6.16.                                       *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATPARFD/INC
          INC       PMSEADFD/INC
          INC       PMSEAHFD/INC
          INC       PMSESDFD/INC
          INC       PMSESHFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
ASSDESC1  DIM       45
ASSDESC2  DIM       45
.
DIM7      DIM       7
DIM10     DIM       10
DIM10A    DIM       10
DIM10B    DIM       10
DIM10C    DIM       10
DIM10D    DIM       10
DIM53     DIM       53
DISPDTE1  DIM       10
DISPDTE2  DIM       10
DISPDTE3  DIM       10
.
FORM7P2   FORM      7.2
.
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
SP38      INIT      "                                      "
.
.
PRGID     INIT      "IBAPMS08"
PRGNAM    INIT      "Eclipse Assessment Report Printing"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
          CALL      GASS0000               * get assessment header key
          BRANCH    EXIT,MAIN9999          * nothing/exitchar entered
.
          CALL      PRIN0000               * print assessment
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000                                   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmseahaf";
          OPEN      PMSEAHA1,"pmseahaf"
.
          DISPLAY   *P64:24,"pmseadaf";
          OPEN      PMSEADA1,"pmseadaf"
.
          DISPLAY   *P64:24,"pmseshaf";
          OPEN      PMSESHA1,"pmseshaf"
.
          DISPLAY   *P64:24,"pmsesdaf";
          OPEN      PMSESDA1,"pmsesdaf"
.
          DISPLAY   *P64:24,"patparaf";
          OPEN      PATPARA1,"patparaf"
          OPEN      PATPARA2,"patparaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                GASS0000         Called by: MAIN0000       *
.*             Get the assessment key for the report                         *
.* Returns: EXIT    0 = valid pmseahaf record read                           *
.*                  1 = nothing entered or pmseahaf record not found         *
.*****************************************************************************
.
GASS0000  KEYIN     *P1:3,*EF:
                    *P1:4,"Assessment key: ",*V2LON,KEY38
.
          PACK      KEY38,KEY38,SP30,SP8
          MATCH     SP38,KEY38                   * anything entered ?
          GOTO      GASS9100 IF EQUAL            * no - finished        
.
.         Use KEY38 to read the assessment header record (pmseahaf)     
.
          CALL      RDPMEAH1                     * header record found ?
          BRANCH    OVRCD,GASS9100               * no - finished
.
.         Now read the participant record to get the participant name
.                   
          MOVE      "Unknown Participant                     ",PTPRNAME
          MOVE      KEY38,KEY3
          CALL      RDPTPAR1
.
          MOVE      ZERO,EXIT
          GOTO      GASS9999
.
GASS9100  MOVE      ONE,EXIT
.
GASS9999  RETURN
+
.*****************************************************************************
.*                               PRIN0000                                    *
.*                  Process the records for printing                         *
.*****************************************************************************
.
PRIN0000  MOVE      ZERO,CPAGENO
          MOVE      ZERO,CNOUNDLN
          CALL      IBACLOCK
.
          CALL      PMAH0000                * print assessment heading
          CALL      PMAD0000                * print assessment details
          CALL      PMSH0000                * print services heading
          CALL      PMSD0000                * print services details
.
          PRINT     *N,*1,"*** End of Report ***"
          DISPLAY   *P1:24,*EL,"Report printed.  ";
          CALL      HITENTER
.
PRIN9999  RETURN
+
.*****************************************************************************
.*                            PMAH0000                                       *
.*                       Print page heading (pmseahaf details)               *
.*****************************************************************************
.
PMAH0000  CALL      HEAD132                      * display page header
.
          PRINT     *N,*40,"Account Reference ID: ",PMAHARID
.
          COMPARE   ONE,CPAGENO                  * first page ?
          GOTO      PMAH1000 IF NOT EQUAL        * no
.
.         For first page only, print claim header details
.
          PRINT     *N,*1,"Claim Assessment"
          CALL      LINE0000                     * draw line across page
.
          UNPACK    PMAHTCAM,DIM7,DIM2
          PACK      DIM10A,DIM7,DOT,DIM2
          MOVE      DIM10A,FORM7P2
          IF        FORM7P2 = 0
            PACK      DIM10A,ZERO,DOT,ZERO,ZERO,SP10
          ELSE
            MOVE      FORM7P2,DIM10A
            SQUEEZE   DIM10A
          ENDIF
.
          UNPACK    PMAHTBAM,DIM7,DIM2
          PACK      DIM10B,DIM7,DOT,DIM2
          MOVE      DIM10B,FORM7P2
          IF        FORM7P2 = 0
            PACK      DIM10B,ZERO,DOT,ZERO,ZERO,SP10
          ELSE
            MOVE      FORM7P2,DIM10B
            SQUEEZE   DIM10B
          ENDIF
.
          UNPACK    PMAHEXAM,DIM7,DIM2
          PACK      DIM10,DIM7,DOT,DIM2
          MOVE      DIM10,FORM7P2
          IF        FORM7P2 = 0
            PACK      DIM10C,ZERO,DOT,ZERO,ZERO,SP10
          ELSE
            MOVE      FORM7P2,DIM10C
            SQUEEZE   DIM10C
          ENDIF
.
          UNPACK    PMAHCPAM,DIM7,DIM2
          PACK      DIM10,DIM7,DOT,DIM2
          MOVE      DIM10,FORM7P2
          IF        FORM7P2 = 0
            PACK      DIM10D,ZERO,DOT,ZERO,ZERO,SP10
          ELSE
            MOVE      FORM7P2,DIM10D
            SQUEEZE   DIM10D
          ENDIF
.
          PRINT     *1,PIPE,*3,"Participant          : ",PMAHFBID,*30,PTPRNAME:
                    *76,PIPE,*78,"Fund Reference ID  : ",PMAHFRID,*132,PIPE,*N:
                    *1,PIPE,*3,"Fund Status Code     : ",PMAHFSCD:
                    *76,PIPE,*78,"Facility ID        : ",PMAHFCID,*132,PIPE,*N:
                    *1,PIPE,*3,"Claim Fund Assm.Code : ",PMAHCFAC:
                    *76,PIPE,*78,"Total Charge Amount: $",DIM10A,*132,PIPE,*N:
                    *1,PIPE,*3,"Total Benefit Amount : $",DIM10B:
                    *76,PIPE,*78,"Excess Amount      : $",DIM10C,*132,PIPE,*N:
                    *1,PIPE,*3,"CoPayment Amount     : $",DIM10D:
                    *76,PIPE,*78,"Fund Transaction ID: ",PMAHFTID,*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
.         Print the claim assessment details
.
PMAH1000  IF        CPAGENO = 1
            PRINT     *N,*1,"Claim Assessment Details"
          ELSE
            PRINT     *N,*1,"Claim Assessment Details (contd.)"
          ENDIF
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"Claim Fund",*22,PIPE,*24,"Claim Fund":
                    *70,PIPE,*118,PIPE,*120,"Message",*132,PIPE,*N:
.
                    *1,PIPE,*3,"Explanation Code",*22,PIPE:
                    *24,"Explanation Text":
                    *70,PIPE,*72,"Element Name",*118,PIPE,*120,"Part ID":
                    *132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
          IF        CPAGENO = 1
            MOVE      TWENTY,CLNO                * initialise line count
          ELSE
            MOVE      TEN1,CLNO                  * initialise line count
          ENDIF
.
PMAH9999  RETURN
+
.*****************************************************************************
.*                               PMAD0000                                    *
.*                  Process the assessment details for printing              *
.*****************************************************************************
PMAD0000  PACK      KEY45,PMAHFBID,PMAHARID,PMAHFRID,PMAHRPTC,SP70
          CALL      RSPMEAD1                     * position on remittance
PMAD0500  CALL      RKPMEAD1                     * read next record
          BRANCH    OVRCD,PMAD9500               * eof - finished
.
          MATCH     PMAHFBID,PMADFBID            * same participant still ?
          GOTO      PMAD9500 IF NOT EQUAL        * no - finished
.
          MATCH     PMAHARID,PMADARID            * same Account Reference ID ?
          GOTO      PMAD9500 IF NOT EQUAL        * no - finished
.
          MATCH     PMAHFRID,PMADFRID            * same Fund Reference ID ?
          GOTO      PMAD9500 IF NOT EQUAL        * no - finished
.
          MATCH     PMAHRPTC,PMADRPTC            * same report counter ?
          GOTO      PMAD9500 IF NOT EQUAL        * no - finished
.
.         A valid record has been found, so print it
.
          COMPARE   SIXTY,CLNO                   * page full ?
          CALL      PMAH0000 IF NOT LESS         * yes
.
          CALL      DLIN0000                     * print assessment details line
          GOTO      PMAD0500                     * get next record
.
PMAD9500  CALL      LINE0000                     * draw line across page
PMAD9999  RETURN
+
.*****************************************************************************
.*                            DLIN0000                                       *
.*                  Valid assessment details record so print it              *
.*****************************************************************************
.
DLIN0000  MOVE      PMADCFET,ASSDESC1
          MOVE      PMADELNM,ASSDESC2
          PRINT     *1,PIPE,SP1,PMADCFEC,SP15,PIPE,SP1,ASSDESC1,SP1:
                       PIPE,SP1,ASSDESC2,SP1,PIPE,SP1,PMADMPID,SP1;
          PRINT     *132,PIPE
.
          ADD       ONE,CLNO                     * increment line count
.
DLIN9999  RETURN
+
.*****************************************************************************
.*                            LINE0000                                       *
.*                      Draw line across page                                *
.*****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
.*****************************************************************************
.*                            PMSH0000                                       *
.*                       Print page heading (pmseshaf details)               *
.*****************************************************************************
.
PMSH0000  PRINT     *N,*1,"Claimed Services Summary"
.
          CALL      LINE0000                     * draw line across page
.
.| 111222      | Service code 111222                                  |  2 |    100.00 |     60.00 |10/03/2008|15/03/2008|12/03/2008|
.
          PRINT     *1,PIPE,*3,"Service",*15,PIPE,*17,"Service":
                    *70,PIPE,"Num ",*75,PIPE," Charge",*87,PIPE," Benefit":
                    *99,PIPE," From",*110,PIPE," To",*121,PIPE," Service":
                    *132,PIPE,*N:
.
                    *1,PIPE,*3,"Code",*15,PIPE:
                    *17,"Description":
                    *70,PIPE,"Srvs",*75,PIPE," Amount ($)":
                    *87,PIPE," Amount ($)",*99,PIPE," Date":
                    *110,PIPE," Date",*121,PIPE," Date",*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
PMSH1000  PACK      KEY52,PMAHFBID,PMAHARID,PMAHFRID,PMAHRPTC,SP70
          CALL      RSPMESH1                     * position on claim
PMSH1500  CALL      RKPMESH1                     * read next record
          BRANCH    OVRCD,PMSH9500               * eof - finished
.
          MATCH     PMAHFBID,PMSHFBID            * same participant still ?
          GOTO      PMSH9500 IF NOT EQUAL        * no - finished
.
          MATCH     PMAHARID,PMSHARID            * same Account Reference ID ?
          GOTO      PMSH9500 IF NOT EQUAL        * no - finished
.
          MATCH     PMAHFRID,PMSHFRID            * same Fund Reference ID ?
          GOTO      PMSH9500 IF NOT EQUAL        * no - finished
.
          MATCH     PMAHRPTC,PMSHRPTC            * same report counter ?
          GOTO      PMSH9500 IF NOT EQUAL        * no - finished
.
.         A valid record has been found, so print it
.
          CALL      DSLN0000                     * print assessment details line
          GOTO      PMSH1500                     * get next record
.
PMSH9500  CALL      LINE0000                     * draw line across page
PMSH9999  RETURN
+
.*****************************************************************************
.*                               PMSD0000                                    *
.*                  Process the services details for printing                *
.*****************************************************************************
PMSD0000  PRINT     *N,*1,"Claimed Services Details"
.
          CALL      LINE0000                     * draw line across page
.
.| 12345678901 | 1234               | 12345678901234567890123456789012345678901234567890123456789012345678901234567890              |
.
          PRINT     *1,PIPE,SP1,"Service",*15,PIPE,SP1,"Service Fund":
                    *36,PIPE,SP1,"Service Fund",*132,PIPE,*N:
.
                    *1,PIPE,SP1,"Code",*15,PIPE,SP1:
                    *17,"Explanation Code":
                    *36,PIPE,SP1,"Explanation Text",*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
PMSD1000  PACK      KEY59,PMAHFBID,PMAHARID,PMAHFRID,PMAHRPTC,SP70
          CALL      RSPMESD1                     * position on service details
PMSD0500  CALL      RKPMESD1                     * read next record
          BRANCH    OVRCD,PMSD9500               * eof - finished
.
          MATCH     PMAHFBID,PMSDFBID            * same participant still ?
          GOTO      PMSD9500 IF NOT EQUAL        * no - finished
.
          MATCH     PMAHARID,PMSDARID            * same Account Reference ID ?
          GOTO      PMSD9500 IF NOT EQUAL        * no - finished
.
          MATCH     PMAHFRID,PMSDFRID            * same Fund Reference ID ?
          GOTO      PMSD9500 IF NOT EQUAL        * no - finished
.
          MATCH     PMAHRPTC,PMSDRPTC            * same report counter ?
          GOTO      PMSD9500 IF NOT EQUAL        * no - finished
.
.         A valid record has been found, so print it
.
          CALL      DSDL0000                     * print services details line
          GOTO      PMSD0500                     * get next record
.
PMSD9500  CALL      LINE0000                     * draw line across page
PMSD9999  RETURN
+
.*****************************************************************************
.*                            DSLN0000                                       *
.*                  Valid claim services record so print it                  *
.*****************************************************************************
.
.| 111222      | Service code 111222                                  |  2 |    100.00 |     60.00 |10/03/2008|15/03/2008|12/03/2008|
.
DSLN0000  MOVE      PMSHSDSC,DIM53
.
          UNPACK    PMSHFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISPDTE1
.
          UNPACK    PMSHTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISPDTE2
.
          UNPACK    PMSHSDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISPDTE3
.
          UNPACK    PMSHCAMT,DIM7,DIM2
          PACK      DIM10A,DIM7,DOT,DIM2
          MOVE      DIM10A,FORM7P2
          IF        FORM7P2 = 0
            PACK      DIM10A,SP6,ZERO,DOT,ZERO,ZERO
          ELSE
            MOVE    FORM7P2,DIM10A
          ENDIF
.
          UNPACK    PMSHFBAM,DIM7,DIM2
          PACK      DIM10B,DIM7,DOT,DIM2
          MOVE      DIM10B,FORM7P2
          IF        FORM7P2 = 0
            PACK      DIM10B,SP6,ZERO,DOT,ZERO,ZERO
          ELSE
            MOVE    FORM7P2,DIM10B
          ENDIF
.
          PRINT     *1,PIPE,SP1,PMSHSCOD,SP1,PIPE,SP1,DIM53:
                       PIPE,SP1,PMSHNOSV,SP1,PIPE,DIM10A,SP1;
          PRINT     *87,PIPE,DIM10B,SP1,*99,PIPE,DISPDTE1:
                    *110,PIPE,DISPDTE2,*121,PIPE,DISPDTE3,*132,PIPE
.
          ADD       ONE,CLNO                     * increment line count
.
DSLN9999  RETURN
+
.*****************************************************************************
.*                            DSDL0000                                       *
.*            Valid claim services detail record so print it                 *
.*****************************************************************************
.
.| 12345678901 | 1234               | 12345678901234567890123456789012345678901234567890123456789012345678901234567890              |
.
DSDL0000  PRINT     *1,PIPE,SP1,PMSDSCOD,SP1,PIPE,SP1,PMSDSFEC,SP1:
                   *36,PIPE,SP1,PMSDSFET,SP10,SP4,*132,PIPE
.
          ADD       ONE,CLNO                     * increment line count
.
DSDL9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATPARIO/INC
          INC       PMSEADIO/INC
          INC       PMSEAHIO/INC
          INC       PMSESDIO/INC
          INC       PMSESHIO/INC
+

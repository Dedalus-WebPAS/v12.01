. **********************************************************************
. * System      : Common                                               *
. * Program     : PRNTSCRN                                             *
. * Description : Print Screen function for V7 interpreter             *
. *               This program is designed to be executed via a function
. *               key that triggers a FORK to this program             *
. **********************************************************************
. * Author      : Graeme Williams    07/05/1997                        *
. * Mods        :                                                      *
. **********************************************************************
. * V10.02.01 07.04.2011  Jill Parkinson CAR 239574                    *
. *           Removed open of ibaprntf                                 *
. * C2.09.B01 06.11.2000  Sandra Barcham                               *
. *           Recompiled for change to ibaspolf                        *
. **********************************************************************
. * C1.00.02  09/10/1997  Steve Armstrong     SRF 643504               *
. *           Also clear SPLFILE on exiting                            *
. * C1.00.01  09/09/1997  Steve Armstrong     SRF 643504               *
. *           Initialise SPLFILE prior to calling OPNPRINT             *
. **********************************************************************
          INC       STD001FD
          INC       IBAPRNFD/INC             * default printers
.
DATA      DIM       80
ATTR      DIM       80
.
PRGID     INIT      "PRNTSCRN"
PRGNAM    INIT      "Print Screen Function"
VERSION   INIT      "V12.01.00"
.
.******************************************************************************
.* ML0000    : Mainline Code                                                  *
.******************************************************************************
ML0000    CALL      INIT0000                 * program initialisation
          BRANCH    EXIT,ML9999              * initialisation failure
.
          CALL      PSCR0000                 * print screen
          CALL      SPOL0000                 * spool printed screen
ML9999    STOP
.
.******************************************************************************
.* INIT0000  : Program initialisation                                         *
.*             WARNING: Do NOT call DISPHEAD as this will change the screen   *
.*                      we are trying to print                                *
.******************************************************************************
INIT0000  MATCH     SP4,PASSCODE
          IF        @EQUAL
            MOVE      TEN4,HLEF
            MOVE      TEN,HTOP
            MOVE      SIXTY7,HRIG
            MOVE      TEN3,HBOT
            CALL      GETSCR00                 * save current screen
            SUSPEND
            BOXCLR    *HON,*V2LON,14,10,74,13
            BOX       16,*HON,*V2LON,14,10,74,13
            SHADOW    14,10,74,13
            CLIP      15,11,73,12
            DISPLAY   *P3:1,*HON,*V2LON:
                             "Cannot Save Image in Spoolfile from Security":
                             " Screen. ":
                      *P13:2,"Hit <ENTER> to continue: ";
            FLUSH     14,10,74,14
            KEYIN     *HON,*V2LON,*MASK,ANS;
            CLIP
            CALL      PUTSCR00
.
            MOVE      ONE,EXIT
          ELSE
            MOVE      ZERO,EXIT
          ENDIF
.
INIT9999  RETURN
.******************************************************************************
.* PSCR0000  : Print the screen                                               *
.******************************************************************************
PSCR0000  MOVE      ONE,CVERT                * initialise line counter
.
.         SRF 643504 - If a program does not use SPLFILE and the variable
.         is purged by the compiler, then for some unknown reason, the SPLFILE
.         variable will contain a value when it first enters the PRNTSCRN
.         program.  Therefore, when OPNPRINT is called, a new spool file
.         name will not be created.  When printing for a second time, an I*D
.         will therefore occur.
.
          MOVE      SP8,SPLFILE
          CALL      OPNPRINT                 * open spool file
.
          WHILE     CVERT <= 24
            GETVAR    DATA,ATTR,ONE,CVERT    * get details of line
            STRIP     DATA                   * remove trailing blanks
            IFGE      $$VER,7
              PRINTVAR  DATA,ATTR            * print line details
            XIF
            IFLE      $$VERS,6
              PRINT     DATA
            XIF
            PRINT     *N;                    * print end of line
            ADD       ONE,CVERT
          DO
.
          SPLCLOSE                           * close spool file
.
PSCR9999  RETURN
.******************************************************************************
.* SPOL0000  : Spool print file                                               *
.******************************************************************************
SPOL0000  CALL      GDSC0000                 * get description
.
          PACK      CSPDATE,CCC,CYY,CMM,CDD
          REP       " 0",CSPDATE
          CLOCK     TIME,CSPTIME
          MOVE      PASSCODE,CSPOPER
          MOVE      SPLFILE,CSPNAME
          MOVE      SPLFILE,KEY8
          MOVE      CPROGLEV,CSPLEVE
          MOVE      CDEPCDE,CSPDEPT
.
          OPEN      IBASPOL1,"ibaspolf"
          CALL      WRSPOL1
          MOVE      SP8,SPLFILE
.
SPOL9999  RETURN
.******************************************************************************
.* GDSC0000  : Get spool file description                                     *
.******************************************************************************
GDSC0000  MOVE      TEN4,HLEF
          MOVE      TEN,HTOP
          MOVE      SIXTY7,HRIG
          MOVE      TEN3,HBOT
          CALL      GETSCR00                 * save current screen
.
          SUSPEND
          BOXCLR    *HON,*V2LON,14,10,67,13
          BOX       16,*HON,*V2LON,14,10,67,13
          SHADOW    14,10,67,13
          CLIP      15,11,66,12
          DISPLAY   *P3:1,*HON,*V2LON:
                           "Screen image saved in spoolfile ",SPLFILE:
                    *P3:2,"Enter Description: ";
          FLUSH     14,10,69,14
          KEYIN     *HON,*V2LON,CSPDESC;
.
          CLIP
          CALL      PUTSCR00                 * restore screen
.
GDSC9999  RETURN
.
          INC       STD001IO
          INC       IBAPRINT                 * standard printing routines
          INC       IBAPRNIO/INC             * default printers

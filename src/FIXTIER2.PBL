.***************************************************************************
.* System    :   Outpatients                                               *
.* Program   :   FIXTIER2                                                  *
.* Desc      :   Update tier 2 codes for selected clinics from text file   *
.***************************************************************************
.* Date      :   19/01/2018                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will open a text file called fixtier2.txt.   *
.*               The text file contains the site, clinic and new tier 2    *
.*               code.                                                     *
.*               It will update the tier 2 code OTBBTCOD for the           *
.*               selected clinics for bookings from 1st July 2017 to       *
.*               6th Sept 2017.                                            *
.*                                                                         *
.* Modifications  :                                                        *
.* V11.04.02 14/02/2024  Ebon Clements       TSK 0942375                   *
.*           Skip booking if tier 2 codes doesn't need to be updated       *
.* V11.04.01 06/02/2024  Ebon Clements       TSK 0942375                   *
.*           Increased display size of record count - RECCOUNT             *
.* V10.12.00 23/05/2018  Richa Phogat        TSK 0854113                   *
.*           Updated Program to change Outpatient Clinic Profiles with Tier*
.*           2 codes                                                       *
.* V10.10.00 19/01/2018  Ebon Clements       TSK 0846116                   *
.*           Created program                                               *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
.
TEMPFILE  FILE     ASCII, FIXED=16
.
.NAME     TYPE   LENGTH   PHYSICAL  STRT    DESCRIPTION
.----     ----   ------   --------  -----   -----------
XXXXSITE  DIM       6         6      1      Site ID
XXXXCLIN  DIM       6         6      7      Clinic ID
XXXXTCOD  DIM       3         3      13     Tier 2 Code (category NC)
.
. End of Record                      16
.
. LOCAL VARIABLE DEFINITION
. -------------------------
RECCOUNT  FORM      8
FROMDATE  DIM       8
TODATE    DIM       8
.
PRGID     INIT      "FIXTIER2"
PRGNAM    INIT      "Fix tier 2 codes"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      DATE0000               * keyin from and to date range
          CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      OUTBOKA1,"outbokaf"
          OPEN      OUTBB1A1,"outbb1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Count bookings ONLY    - ":
                    " Bookings from __/__/____ to __/__/____":
                    *P1:6,*V2LON,TWO,*HOFF,". Run Tier 2 Code UPDATE - ":
                    " Bookings from __/__/____ to __/__/____":
                    *P1:15,*V2LON,"NOTE : Text file name fixtier2.txt":
                    " - Fixed length":
                    *P1:16,"Site(6),Clinic id(6),New Tier 2 Code(3)."
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               DATE0000              Called by: ML0000  *
.*                          accept date range                             *
.**************************************************************************
.
DATE0000    MOVE       FALSE,EXIT
.
            IF        COPTION=1
              DISPLAY   *P1:23,*EL,*V2LON,"Enter Date range for option 1";
              MOVE      "5",CVERT
              MOVE      "5",EVERT
            ELSE
              DISPLAY   *P1:23,*EL,*V2LON,"Enter Date range for option 2";
              MOVE      "6",CVERT
              MOVE      "6",EVERT
            ENDIF
            MOVE      "44",CCOL
            CALL      IBACLOCK
            MOVE      CCC,CCENT
            MOVE      CYY,CYEAR
            MOVE      CMM,CMON
            MOVE      CDD,CDAY
            CALL      KEYDATE                          * enter the from date
            PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY   * move to form field
            REP       " 0",FROMDATE                    * from date
.
            MOVE      "58",CCOL
            CALL      KEYDATE                          * enter the to date
            PACK      TODATE,CCENT,CYEAR,CMON,CDAY     * move to form field
            REP       " 0",TODATE                      * to date
.
            MATCH      TODATE,FROMDATE
            GOTO       DATE9000 IF NOT LESS
.
            GOTO       DATE9999
.
DATE9000    DISPLAY    *P1:23,*EF,*V2LON,"FROMDATE should be less than TODATE ";
            CALL       HITENTER
            GOTO       DATE0000
.
DATE9999    RETURN
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TEMPFILE,"fixtier2"      * open text file
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"fixtier2.txt File not found.  ";
            CALL      HITENTER
            GOTO      PROC9999
          ENDIF
.
          MOVE      ZERO,RECCOUNT
.
PROC1000  READ      TEMPFILE,SEQ;XXXXSITE,XXXXCLIN,XXXXTCOD
          GOTO      PROC9000 IF OVER
.
          PACK      XXXXTCOD,XXXXTCOD,SP70
.
          PACK      KEY28,XXXXSITE,XXXXCLIN,FROMDATE,SP70
          CALL      RDSBOKA1
PROC2000  CALL      RDKBOKA1
          BRANCH    OVRCD,PROC1000
.
          MATCH     XXXXSITE,OBASITE        * Correct site
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     XXXXCLIN,OBACLIN        * Correct clinic
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     OBADATE,TODATE          * Correct date range
          GOTO      PROC1000 IF LESS
.
          COMPARE   ONE,OBASTAT             * Booked 
          GOTO      PROC2500 IF EQUAL
.
          COMPARE   FOUR,OBASTAT            * Attended 
          GOTO      PROC2500 IF EQUAL
.
          COMPARE   FIVE,OBASTAT            * DNA
          GOTO      PROC2500 IF EQUAL
.
          GOTO      PROC2000
.
PROC2500  PACK      KEY8,OBAOUTNO,SP70
          CALL      RDBOKB1
          BRANCH    OVRCD,PROC2000
.
          MATCH     XXXXTCOD,OTBBTCOD       * Skip record if Tier 2 code
          GOTO      PROC2000 IF EQUAL       * matches
.
          ADD       ONE,RECCOUNT
.
          IF        COPTION=2
            DISPLAY   *P1:22,*EF,OBAOUTNO;
            MOVE      XXXXTCOD,OTBBTCOD     * Tier 2 Code (category NC
            CALL      UPBOKB1
          ENDIF
          GOTO      PROC2000
.
PROC9000  IF        COPTION=1
            DISPLAY   *P1:23,*EF,"Records to be updated : ",RECCOUNT,*N;
          ELSE
            DISPLAY   *P1:23,*EF,"Records Updated : ",RECCOUNT,*N;
          ENDIF
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC

.-------------------------------------------------------------------------
. Program       : IBARSH19  
.
. Function      : Surgical Preference/Favourite Item Global Functions
.
. Modifications :
.
.-------------------------------------------------------------------------
.V11.04.02 15/03/2024  Thanh T         TSK 0923560
.          Added Hospital ID
.V11.04.01 26/02/2024  Thanh T         TSK 0923560
.          Added Hospital ID
. V11.03.01 23/01/2023  Thanh T.           TSK 0919770
.           Changed PROCB000, PROCC000 to cater for adding Hospital field
. V10.15.01 07/02/2020  Peter Vela      TSK 0871644
.           Recompiled for PMSUFIFD
. V10.15.00 13/01/2020  Thanh T         TSK 0871644
.           Initial Version                                                  
.-------------------------------------------------------------------------
.
          INC       STD001FD
.
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       IBACONFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       OPRCONFD/INC
          INC       PATHSPFD/INC            * Hospital file
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       PATMCHFD/INC            * Mscellaneous Charges
          INC       PMSUFIFD/INC            * User Group Favourite Charge Items
          INC       OPRDPFFD/INC            * Doctor Preference Details
          INC       OPRTRBFD/INC            * Tray Details
          INC       OPRITEFD/INC            * Operating Rooms Item
          INC       WEBSECFD/INC
          INC       NZPMCHFD/INC
          INC       WEBERRFD/INC            * Web Server Error Logging
.
.         Temporary file definitions
.
WORK      IFILE     SQL, FIXED=61, KEY="U1-31"
.
.NAME     TYPE   LENGTH   PHYSICAL  START LOC.    DESCRIPTION
.----     ----   ------   --------  ----------    -----------
XMUFHOSP  DIM       3         3         1         Hospital Id
XMUFUOGR  DIM       3         3         4         User Occupational Group 
XMUFUSER  DIM      10        10         7         User Id
XMUFFAVG  DIM       3         3        17         Favourite Group
XMUFGHED  DIM       3         3        20         Group Heading
XMUFCODE  DIM       9         9        23         Item Code
XMUFERRO  DIM      30        30        32         Error Description
.End of Record                         61
.
WORK2     IFILE     SQL, FIXED=67, KEY="U1-37"
.
.NAME     TYPE   LENGTH   PHYSICAL  START LOC.    DESCRIPTION
.----     ----   ------   --------  ----------    -----------
XPDFCODE   DIM       6         6         1        Doctor Code 
XPDFPREF   DIM       9         9         7        Preference Code
XPDFCLSS   DIM       3         3        16        Class Type (Cat OG)
XPDFTYPE   DIM       1         1        19        Item Type
XPDFITEM   DIM      15        15        20        Item Code
XPDFHOSP   DIM       3         3        35        Hospital ID 
XPDFERRO   DIM      30        30        38        Error Description
.End of record                          67 

.
WORK3     IFILE     SQL, FIXED=63, KEY="u1-33"
.
.NAME     TYPE   LENGTH   PHYSICAL  START LOC.    DESCRIPTION
.----     ----   ------   --------  ----------    -----------
XPTDCODE   DIM      15        15         1        Tray Code
XPTDITEM   DIM      15        15        16        Item Code
XPTDHOSP   DIM       3         3        31        Hospital ID 
XPTDERRO   DIM      30        30        34        Error Description
.End of record                          63
.
. ------ <Work Variables and Constants> ------
.
CURRDATE  DIM       8
CODE      DIM       2
COMMAND   DIM       50
CVERTF    FORM      2
CVERTT    FORM      2
.
FNAMEI    DIM       8         * Temporary index file name
FNAMEB    DIM       8         * Temporary index file name
FNAMEC    DIM       8         * Temporary index file name
.
FILTHOSP  DIM       3
FILTOCCG  DIM       3
FILTUSID  DIM       10
FILTFAVG  DIM       3
FILTITFR  DIM       9
FILTITTO  DIM       9
FILTTRFR  DIM       15
FILTTRTO  DIM       15
USERID    DIM       10
HOSPDESC  DIM       50
OCCGDESC  DIM       20
USERNAME  DIM       30
FAVGDESC  DIM       20
ITFRDESC  DIM       90
ITTODESC  DIM       90
TRFRDESC  DIM       30
TRTODESC  DIM       30
.
FAVITMCD  DIM       9
FAVITMDS  DIM       90
PRFITMCD  DIM       15
PRFITMDS  DIM       90
.
NOFITUPD  FORM      6 
NOFITERR  FORM      6 
SAVKEY31  DIM       31
SAVKEY33  DIM       33
SAVKEY37  DIM       37
KEY21A    DIM       21
ERRORDSC  DIM       30
.
.------------------------------------------------------------------------
PRGID     INIT      "IBARSH19"
PRGNAM    INIT      "Surigcal Preference/Favourite Items Global Functions"
VERSION   INIT      "V12.01.00"
.------------------------------------------------------------------------
+
MAIN0000  CALL      INIT0000             * Initialization
.
MAIN0100  MOVE      ZERO,OPTION          * Initialise the sub-option var.
          CALL      OPTN0000             * Select option
          BRANCH    EXIT,MAIN9999        * Exit option selected
          BRANCH    OPTION,MAIN5000:     * Global Upd of Favourite Charge Items
                           MAIN1000      * Global Upd of Preference/Tray Items
          GOTO      MAIN9999
.
MAIN1000  CALL      INPTA000
          CALL      CONTQST              * Okay to continue ?
          MOVE      ANS,EXIT             * Convert answer to a numeric
          BRANCH    EXIT,MAIN2000:       * Yes
                         MAIN1000:       * No.
                         MAIN0100        * Cancel
.
MAIN2000  CALL      CREAA000             * Create a temporary file
          CALL      PROCA000             * Process favourite item
          CALL      GENRA000             * Generate the report
          CALL      KILLA000             * Remove the temporary file
          GOTO      MAIN9999
.
MAIN5000  CALL      INPTB000
          CALL      CONTQST              * Okay to continue ?
          MOVE      ANS,EXIT             * Convert answer to a numeric
          BRANCH    EXIT,MAIN6000:       * Yes
                         MAIN5000:       * No.
                         MAIN0100        * Cancel
.
MAIN6000  CALL      CREAB000             * Create a temporary file
          CALL      PROCB000             * Process preference/tray item
          CALL      GENRB000             * Generate the report
          CALL      KILLB000             * Remove the temporary file
MAIN6100  CALL      CREAC000             * Create a temporary file
          CALL      PROCC000             * Process preference/tray item
          CALL      GENRC000             * Generate the report
          CALL      KILLC000   
          GOTO      MAIN9999
.
MAIN9999  CHAIN     PGM                  * Return from whence thou comest.
+
.------------------------------------------------------------------------
. Initialization Procedure       
.------------------------------------------------------------------------
INIT0000  CALL      DISPHEAD            * Display the standard heading
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          DISPLAY   *P64:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pmsufiaf";
          OPEN      PMSUFIA1,"pmsufiaf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P60:24,"websecaf";
          OPEN      WEBSECA1,"websecaf"
.
          DISPLAY   *P60:24,"oprdpfaf";
          OPEN      OPRDPFA1,"oprdpfaf"
.
          DISPLAY   *P60:24,"oprtrybf";
          OPEN      OPRTRYB1,"oprtrybf"
.
          DISPLAY   *P60:24,"opriteaf";
          OPEN      OPRITEA1,"opriteaf"
.
          DISPLAY   *P60:24,"nzpmchaf";
          OPEN      NZPMCHA3,"nzpmchaf"
.
          DISPLAY   *P60:24,"patmchgf";
          OPEN      PATMCHG1,"patmchgf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEI
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEB
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEC
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN9;*212,CFEETYP
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
          READ      CONTROLF,HUND13;*66,OPCNPTMC
.
INIT9999  RETURN
+
.------------------------------------------------------------------------
. Key in the option wanted                                        
. RETURNS : EXIT    = 0 (Valid Option selected, returned in OPTION) 
.                   = 1 (Exit Option selected)                    
.           OPTION  = 1 (Global Update of Preference/Tray Items) 
.                     2 (Global Update of Favourite Charge Items)  
.------------------------------------------------------------------------
OPTN0000  CALL      DISPHEAD
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Global Update of Preference/Tray Items":
                    *P1:6,*V2LON,TWO,*HOFF," = Global Update of Favourite Charge Items":
                    *P1:8,"Select Option :";
.
.         Key in the option desired
.
OPTN1000  KEYIN     *P17:8,*EF,*DV,UNDLN1,*P17:8,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION         * Have they selected the exit option ?
          GOTO      OPTN9000 IF EQUAL   * Yes. Set indicator.
          DISPLAY   *P17:8,*EL,*V2LON,OPTION;
.
.         Validate option selected
.
          BRANCH    OPTION,OPTN1100:    
                           OPTN1100    
          BEEP                          * Invalid option
          GOTO      OPTN1000            * Rekey option
.
OPTN1100  MOVE      ZERO,EXIT           * Valid Option
          GOTO      OPTN9999
.
.         Exit option
.
OPTN9000  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.------------------------------------------------------------------------
. Get KeyIn parameters for global update of favourite charge items
.------------------------------------------------------------------------
INPTA000  MOVE      SP70,FILTHOSP
          MOVE      SP70,FILTOCCG
          MOVE      SP70,FILTFAVG
          MOVE      SP70,FILTUSID
          MOVE      SP70,FILTITFR
          MOVE      SP70,FILTITTO
          MOVE      SP70,USERID
.
. Keyin Hospital
.
INPTA100  COMPARE   ONE,IBCNMHOS
          GOTO      INPTA200 IF NOT EQUAL
          DISPLAY   *P1:10,*EF,"Hospital            : ";
          MOVE      "23",ECOL
          MOVE      "10",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          CALL      PATHSPKY
          IF        EXIT = ONE
            MOVE      SP70,FILTHOSP
            MOVE      "All",HOSPDESC
            MOVE      SP70,PTHSHOSP
            DISPLAY   *P23:10,*EL,HOSPDESC,*W2;
          ELSE
            IF        EXIT = TWO
              MOVE      SP70,FILTHOSP
              MOVE      "Invalid Hospital ID",ERRORDSC
              CALL      DISERR00
              GOTO      INPTA100
            ELSE
              MOVE      KEY3,FILTHOSP
              MOVE      PTHSNAME,HOSPDESC
              DISPLAY   *P34:10,*EL,HOSPDESC,*W2;
            ENDIF
          ENDIF
.
. Keyin Occupational Group 
.
INPTA200  DISPLAY   *P1:11,*EF,"Occupational Group  : "
          KEYIN     *P23:11,*V2LON,FILTOCCG
          PACK      FILTOCCG,FILTOCCG,SP70
          MATCH     SP70,FILTOCCG
          IF        @EQUAL
            MOVE      SP3,FILTOCCG
            MOVE      "All",OCCGDESC
            DISPLAY   *P23:11,*EL,*V2LON,OCCGDESC
            GOTO      INPTA300
          ENDIF 
.  
          MOVE      "w0",CODE
          PACK      KEY5,CODE,FILTOCCG
          CALL      RDCODE1
          IF        OVRCD = 1
            MOVE      "Invalid Code for Category w0",ERRORDSC
            CALL      DISERR00
            GOTO      INPTA200
          ENDIF
.
          MOVE      TDESC,OCCGDESC
          DISPLAY   *P34:11,*EL,OCCGDESC
.
. Keyin User ID 
.
INPTA300  DISPLAY   *P1:12,*EF,"User Id             : "
          KEYIN     *P23:12,*V2LON,FILTUSID
          PACK      FILTUSID,FILTUSID,SP70
          MATCH     SP70,FILTUSID
          GOTO      INPTA400 IF EQUAL
.
          PACK      KEY10,FILTUSID,SP70
          CALL      RDWBSE1 
          IF        OVRCD = 1
            MOVE      "Invalid User ID",ERRORDSC
            CALL      DISERR00
            GOTO      INPTA300
          ENDIF 
. 
          MOVE      WBSENAM,USERNAME 
          DISPLAY   *P34:12,*EL,USERNAME
.
. Keyin Favourite Group
.
INPTA400  DISPLAY   *P1:13,*EF,"Favourite Group     : "
          KEYIN     *P23:13,*V2LON,FILTFAVG
          PACK      FILTFAVG,FILTFAVG,SP70
          MATCH     SP70,FILTFAVG
          IF        @EQUAL
            MOVE      SP3,FILTFAVG
            MOVE      "All",FAVGDESC
            DISPLAY   *P23:13,*EL,*V2LON,FAVGDESC
            GOTO      INPTA500
          ENDIF
.
          MOVE      "w4",CODE
          PACK      KEY5,CODE,FILTFAVG
          CALL      RDCODE1
          IF        OVRCD = 1
            MOVE      "Invalid Code for Category w4",ERRORDSC
            CALL      DISERR00
            GOTO      INPTA400
          ENDIF
.
          MOVE      TDESC,FAVGDESC
          DISPLAY   *P34:13,*EL,FAVGDESC
.
. Keyin Report User ID
.
INPTA500  DISPLAY   *P1:14,*EF,"Report User Id      : "
          KEYIN     *P23:14,*V2LON,USERID
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          IF        @EQUAL
            MOVE      "Report User ID is Mandatory",ERRORDSC
            CALL      DISERR00
            GOTO      INPTA500
          ENDIF
.
          PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          IF        OVRCD = 1
            MOVE      "Invalid Report User ID",ERRORDSC
            CALL      DISERR00
            GOTO      INPTA500
          ENDIF
.
          DISPLAY   *P34:14,*EL,WBSENAM
.
. Keyin Item To Be Replaced 
.
INPTA600  MOVE      ZERO,EXIT
          DISPLAY   *P1:15,*EF,"Item to be Replaced : "
          KEYIN     *P23:15,*V2LON,FILTITFR
          PACK      FILTITFR,FILTITFR,SP70
          MATCH     SP70,FILTITFR
          IF        @EQUAL
            MOVE      "Item to be Replaced is Mandatory",ERRORDSC
            CALL      DISERR00
            GOTO      INPTA600
          ENDIF
.
          MOVE      FILTITFR,FAVITMCD
          CALL      FVITDS00
          IF        EXIT = ZERO
            MOVE      FAVITMDS,ITFRDESC
            DISPLAY   *P34:15,*EL,*V2LON,ITFRDESC
          ELSE
            MOVE      "Invalid Item to be Replaced",ERRORDSC
            CALL      DISERR00
            GOTO      INPTA600
          ENDIF
.
. Keyin New Item Code
.
INPTA700  MOVE      ZERO,EXIT
          DISPLAY   *P1:16,*EF,"New Item Code       : "
          KEYIN     *P23:16,*V2LON,FILTITTO
          PACK      FILTITTO,FILTITTO,SP70
          MATCH     SP70,USERID
          IF        @EQUAL
            MOVE      "New Item Code is Mandatory",ERRORDSC
            CALL      DISERR00
            GOTO      INPTA700
          ENDIF
.
          MOVE      FILTITTO,FAVITMCD
          CALL      FVITDS00
          IF        EXIT = ZERO
            MOVE      FAVITMDS,ITTODESC
            DISPLAY   *P34:16,*EL,*V2LON,ITTODESC
          ELSE
            MOVE      "Invalid New Item Code",ERRORDSC
            CALL      DISERR00
            GOTO      INPTA700
          ENDIF
.
INPTA999  RETURN
.
.------------------------------------------------------------------------
. Display error description  
.------------------------------------------------------------------------
DISERR00  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** ",ERRORDSC," **";
          CALL      HITENTER
.
DISERR99  RETURN  
.
.------------------------------------------------------------------------
. Get favourite item description
. Input:  FAVITMCD 
. Output: FAVITMDS
.------------------------------------------------------------------------
FVITDS00  MOVE      ZERO,EXIT
          MOVE      SP70,FAVITMDS
          IF        CFEETYP=FIVE
            PACK      KEY37,FAVITMCD,SP70
            CALL      RSNZMCH3
FVITDS20    CALL      RKNZMCH3
            BRANCH    OVRCD,FVITDS99
.
            MATCH     FAVITMCD,NZMCMCHG
            GOTO      FVITDS99 IF NOT EQUAL
.
            MATCH     "1",NZMCACFL
            GOTO      FVITDS20 IF EQUAL
.
            MOVE      NZMCDESC,FAVITMDS
            GOTO      FVITDS99
          ELSE
            PACK      KEY9,FAVITMCD,SP70
            PACK      KEY10,USERID,SP70
            CALL      RDWBSE1
            IF        OVRCD=1
              MOVE      SP70,WBSEHOSP
            ENDIF
            IF        IBCNMHOS=1
              PACK      KEY3,WBSEHOSP,SP70
              CALL      RDPTHSP1
              BRANCH    OVRCD,FVITDS30
              PACK      KEY29,PTHSDCLM,SP6,SP3,KEY9,CURRDATE,SP10
            ELSE
FVITDS30      PACK      KEY29,PTCNDCLM,SP6,SP3,KEY9,CURRDATE,SP10
            ENDIF
.
            CALL      PATMCHRD
            IF        EXIT=ZERO
              MOVE      MDESC,FAVITMDS
              GOTO      FVITDS99
            ENDIF
          ENDIF
.
FVITDS99  RETURN 
.
.------------------------------------------------------------------------
. Get KeyIn parameters for global update of favourite tray items
.------------------------------------------------------------------------
INPTB000  MOVE      SP70,FILTTRFR
          MOVE      SP70,FILTTRTO
          MOVE      SP70,USERID
.
. Keyin Report User ID
.
INPTB100  DISPLAY   *P1:10,*EF,"Report User Id      : "
          KEYIN     *P23:10,*V2LON,USERID
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          IF        @EQUAL
            MOVE      "Report User ID is Mandatory",ERRORDSC
            CALL      DISERR00
            GOTO      INPTB100
          ENDIF
.
          PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          IF        OVRCD = 1
            MOVE      "Invalid Report User ID",ERRORDSC
            CALL      DISERR00
            GOTO      INPTB100
          ENDIF
.
          DISPLAY   *P34:10,*EL,WBSENAM
.
. Keyin Hospital
.
INPTB200  COMPARE   ONE,IBCNMHOS
          GOTO      INPTB500 IF NOT EQUAL
          DISPLAY   *P1:11,*EF,"Hospital            : ";
          MOVE      "23",ECOL
          MOVE      "11",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          CALL      PATHSPKY
          IF        EXIT = ONE
            MOVE      SP70,FILTHOSP
            MOVE      "All",HOSPDESC
            MOVE      SP70,PTHSHOSP
            DISPLAY   *P23:11,*EL,HOSPDESC;
          ELSE
            IF        EXIT = TWO
              MOVE      SP70,FILTHOSP
              MOVE      "Invalid Hospital ID",ERRORDSC
              CALL      DISERR00
              GOTO      INPTA100
            ELSE
              MOVE      KEY3,FILTHOSP
              MOVE      PTHSNAME,HOSPDESC
              DISPLAY   *P34:11,*EL,HOSPDESC;
            ENDIF
          ENDIF
.
. Keyin Item To Be Replaced
.
INPTB500  DISPLAY   *P1:12,*EF,"Item to be Replaced : "
          KEYIN     *P23:12,*V2LON,FILTTRFR
          PACK      FILTTRFR,FILTTRFR,SP70
          MATCH     SP70,FILTTRFR
          IF        @EQUAL
            MOVE      "Item to be Replaced is Mandatory",ERRORDSC
            CALL      DISERR00
            GOTO      INPTB500
          ENDIF
.
          MATCH     "1",OPCNPTMC
          GOTO      INPTB520 IF EQUAL
.      
          PACK      KEY15,FILTTRFR,SP70
          CALL      RDOPITE1 
          IF        OVRCD = 1
            MOVE      "Invalid Item to be Replaced",ERRORDSC
            CALL      DISERR00
            GOTO      INPTB500
          ENDIF 
.
          MOVE      OPITDESC,TRFRDESC
          DISPLAY   *P39:12,*EL,TRFRDESC
.
INPTB520  MOVE      FILTTRFR,FAVITMCD
          CALL      FVITDS00
          IF        EXIT = ZERO
            MOVE      FAVITMDS,TRFRDESC
            DISPLAY   *P39:12,*EL,*V2LON,TRFRDESC
          ELSE
            MOVE      "Invalid Item to be Replaced",ERRORDSC
            CALL      DISERR00
            GOTO      INPTB500
          ENDIF
.
. Keyin New Item Code
.
INPTB600  DISPLAY   *P1:13,*EF,"New Item Code       : "
          KEYIN     *P23:13,*V2LON,FILTTRTO
          PACK      FILTTRTO,FILTTRTO,SP70
          MATCH     SP70,FILTTRTO
          IF        @EQUAL
            MOVE      "New Item Code is Mandatory",ERRORDSC
            CALL      DISERR00
            GOTO      INPTB600
          ENDIF
.
          MATCH     "1",OPCNPTMC
          GOTO      INPTB620 IF EQUAL
.
          PACK      KEY15,FILTTRTO,SP70
          CALL      RDOPITE1
          IF        OVRCD = 1
            MOVE      "Invalid New Item Code",ERRORDSC
            CALL      DISERR00
            GOTO      INPTB600
          ENDIF
.
          MOVE      OPITDESC,TRTODESC
          DISPLAY   *P39:13,*EL,TRTODESC
.
INPTB620  MOVE      FILTTRTO,FAVITMCD
          CALL      FVITDS00
          IF        EXIT = ZERO
            MOVE      FAVITMDS,TRTODESC
            DISPLAY   *P39:13,*EL,*V2LON,TRTODESC
          ELSE
            MOVE      "Invalid New Item Code",ERRORDSC
            CALL      DISERR00
            GOTO      INPTB600
          ENDIF
.
INPTB999  RETURN
+
.------------------------------------------------------------------------
. Create and Open a temporary index file for favourite charge items
.------------------------------------------------------------------------
CREAA000  CALL      KILLA000                * Delete any existing temporary file
.
          CLEAR     COMMAND                 * Clear the command line
          APPEND    "isbuild ",COMMAND      * Isbuild
          APPEND    FNAMEI,COMMAND          * temp_filename
          APPEND    " 61 u1-31",COMMAND    * record_length key
          RESET     COMMAND                 * Reset the command line
.
          EXECUTE   COMMAND,TASKID          * Execute the isbuild command
.
          OPEN      WORK,FNAMEI             * Open the temporary file
.
CREAA999  RETURN
+
.------------------------------------------------------------------------
. Remove a temporary index file for favourite charge items        
.------------------------------------------------------------------------
KILLA000  CLOSE     WORK                * Make sure the temp file is closed
.
          CLEAR     COMMAND             * Clear the command line
          APPEND    "iserase ",COMMAND  * Iserase
          APPEND    FNAMEI,COMMAND      * temp_filename
          RESET     COMMAND             * Reset the command line
.
          EXECUTE   COMMAND,TASKID      * Execute the isbuild command
.
KILLA999  RETURN
+
.------------------------------------------------------------------------
. Create and Open a temporary index file for surgical preference    
.------------------------------------------------------------------------
CREAB000  CALL      KILLB000                * Delete any existing temporary file
.
          CLEAR     COMMAND                 * Clear the command line
          APPEND    "isbuild ",COMMAND      * Isbuild
          APPEND    FNAMEB,COMMAND          * temp_filename
          APPEND    " 67 u1-37",COMMAND     * record_length key
          RESET     COMMAND                 * Reset the command line
.
          EXECUTE   COMMAND,TASKID          * Execute the isbuild command
.
          OPEN      WORK2,FNAMEB            * Open the temporary file
.
CREAB999  RETURN
+
.------------------------------------------------------------------------
. Remove a temporary index file for surgical preference                          
.------------------------------------------------------------------------
KILLB0000  CLOSE     WORK2               * Make sure the temp file is closed
.
          CLEAR     COMMAND             * Clear the command line
          APPEND    "iserase ",COMMAND  * Iserase
          APPEND    FNAMEB,COMMAND      * temp_filename
          RESET     COMMAND             * Reset the command line
.
          EXECUTE   COMMAND,TASKID      * Execute the isbuild command
.
KILLB999  RETURN
.------------------------------------------------------------------------
. Create and Open a temporary index file for theatre tray
.------------------------------------------------------------------------
CREAC000  CALL      KILLC000                * Delete any existing temporary file
.
          CLEAR     COMMAND                 * Clear the command line
          APPEND    "isbuild ",COMMAND      * Isbuild
          APPEND    FNAMEC,COMMAND          * temp_filename
          APPEND    " 63 u1-33",COMMAND     * record_length key
          RESET     COMMAND                 * Reset the command line
.
          EXECUTE   COMMAND,TASKID          * Execute the isbuild command
.
          OPEN      WORK3,FNAMEC            * Open the temporary file
.
CREAC999  RETURN
+
.------------------------------------------------------------------------
. Remove a temporary index file for theatre tray
.------------------------------------------------------------------------
KILLC000  CLOSE     WORK3               * Make sure the temp file is closed
.
          CLEAR     COMMAND             * Clear the command line
          APPEND    "iserase ",COMMAND  * Iserase
          APPEND    FNAMEC,COMMAND      * temp_filename
          RESET     COMMAND             * Reset the command line
.
          EXECUTE   COMMAND,TASKID      * Execute the isbuild command
.
KILLC999  RETURN
+
.------------------------------------------------------------------------
. Process favourite item data and post to the temporary file                 
.------------------------------------------------------------------------
PROCA000  MOVE      ZERO,NOFITUPD 
          MOVE      ZERO,NOFITERR 
.
          MATCH     SP70,FILTHOSP
          IF        @EQUAL
            PACK      KEY31,SP70
          ELSE 
            PACK      KEY31,FILTHOSP,SP70
          ENDIF
.
PROCA100  CALL      RSPMUFI1
PROCA200  CALL      RKPMUFI1
          BRANCH    OVRCD,PROCA999
.
          MATCH     SP70,FILTHOSP
          IF        !@EQUAL
            MATCH     FILTHOSP,PMUFHOSP
            GOTO      PROCA999 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTOCCG
          IF        !@EQUAL
            MATCH     FILTOCCG,PMUFUOGR
            GOTO      PROCA200 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTUSID
          IF        !@EQUAL
            MATCH     FILTUSID,PMUFUSER
            GOTO      PROCA200 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTFAVG
          IF        !@EQUAL
            MATCH     FILTFAVG,PMUFFAVG
            GOTO      PROCA200 IF NOT EQUAL
          ENDIF
.
          MATCH     FILTITFR,PMUFCODE
          GOTO      PROCA200 IF NOT EQUAL
.
PROCA300  MOVE      SP70,XMUFERRO
          PACK      SAVKEY31,PMUFHOSP,PMUFUOGR,PMUFUSER,PMUFFAVG,PMUFGHED:
                             PMUFCODE,SP70
.
. Check if New key already existed
.
          MOVE      FILTITTO,PMUFCODE
          PACK      KEY31,PMUFHOSP,PMUFUOGR,PMUFUSER,PMUFFAVG,PMUFGHED:
                          PMUFCODE,SP70
          CALL      RDPMUFI1
          IF        OVRCD = 0
            MOVE      "Key already Exist in file",XMUFERRO
            ADD       ONE,NOFITERR
            GOTO      PROCA800
          ELSE 
            MOVE      SAVKEY31,KEY31       * Read with current key
            CALL      RDPMUFI1
            IF        OVRCD = 0
              MOVE      FILTITTO,PMUFCODE   
              CALL      UPPMUFI1
              ADD       ONE,NOFITUPD
              GOTO      PROCA800
            ENDIF
          ENDIF 
.
          GOTO      PROCA200
.
PROCA800  MOVE      PMUFHOSP,XMUFHOSP             
          MOVE      PMUFUOGR,XMUFUOGR             
          MOVE      PMUFUSER,XMUFUSER             
          MOVE      PMUFFAVG,XMUFFAVG             
          MOVE      PMUFGHED,XMUFGHED             
          MOVE      FILTITFR,XMUFCODE
.
          CALL      WRWORK              * Write the new record to the temp file          
.
          MOVE      SAVKEY31,KEY31
          GOTO      PROCA100
.
PROCA999  RETURN
+
.------------------------------------------------------------------------
. Process surgical preference Details data and post to the temporary file
.------------------------------------------------------------------------
PROCB000  MOVE      ZERO,NOFITUPD 
          MOVE      ZERO,NOFITERR 
.
          PACK      KEY37,SP70
PROCB100  CALL      RSOPDPF1
PROCB200  CALL      RKOPDPF1
          BRANCH    OVRCD,PROCB999
.
          MATCH     FILTTRFR,OPDFITEM 
          GOTO      PROCB200 IF NOT EQUAL
.
          IF        IBCNMHOS = ONE
            MATCH     FILTHOSP,OPDFHOSP
            GOTO      PROCB200 IF NOT EQUAL
          ELSE
            MATCH     SP70,OPDFHOSP
            GOTO      PROCB200 IF NOT EQUAL
          ENDIF
.
PROCB300  MOVE      SP70,XPDFERRO
          PACK      SAVKEY37,OPDFCODE,OPDFPREF,OPDFCLSS,OPDFTYPE,OPDFITEM:
                             OPDFHOSP,SP70
.
. Check if New key already existed
.
          MOVE      FILTTRTO,OPDFITEM
          PACK      KEY37,OPDFCODE,OPDFPREF,OPDFCLSS,OPDFTYPE,OPDFITEM:
                          OPDFHOSP,SP70
          CALL      RDOPDPF1
          IF        OVRCD = 0
            MOVE      "Key already Exist in file",XPDFERRO
            ADD       ONE,NOFITERR
            GOTO      PROCB800
          ELSE
            MOVE      SAVKEY37,KEY37       * Read with current key
            CALL      RDOPDPF1
            IF        OVRCD = 0
              MOVE      FILTTRTO,OPDFITEM
              CALL      UPOPDPF1
              ADD       ONE,NOFITUPD
              GOTO      PROCB800
            ENDIF
          ENDIF
.
          GOTO      PROCB200
.
PROCB800  MOVE      OPDFCODE,XPDFCODE
          MOVE      OPDFPREF,XPDFPREF
          MOVE      OPDFCLSS,XPDFCLSS
          MOVE      OPDFTYPE,XPDFTYPE
          MOVE      FILTTRFR,XPDFITEM
          MOVE      FILTHOSP,XPDFHOSP
.
          CALL      WRWORK2             * Write the new record to the temp file 
.
          MOVE      SAVKEY37,KEY37
          GOTO      PROCB100
.
PROCB999  RETURN
.
.------------------------------------------------------------------------
. Process Tray Details data and post to the temporary file
.------------------------------------------------------------------------
PROCC000  MOVE      ZERO,NOFITUPD 
          MOVE      ZERO,NOFITERR 
.
          PACK      KEY33,SP70
PROCC100  CALL      RSOPTRB1
PROCC200  CALL      RKOPTRB1
          BRANCH    OVRCD,PROCC999
.
          MATCH     FILTTRFR,OPTDITEM
          GOTO      PROCC200 IF NOT EQUAL
.
          IF        IBCNMHOS = ONE
            MATCH     FILTHOSP,OPTDHOSP
            GOTO      PROCC200 IF NOT EQUAL
          ELSE
            MATCH     SP70,OPTDHOSP
            GOTO      PROCC200 IF NOT EQUAL
          ENDIF 
.
PROCC300  MOVE      SP70,XPTDERRO
          PACK      SAVKEY33,OPTDCODE,OPTDITEM,OPTDHOSP,SP70
.
. Check if New key already existed
.
          MOVE      FILTTRTO,OPTDITEM
          PACK      KEY33,OPTDCODE,OPTDITEM,OPTDHOSP,SP70
          CALL      RDOPTRB1
          IF        OVRCD = 0
            MOVE      "Key already Exist in file",XPTDERRO
            ADD       ONE,NOFITERR
            GOTO      PROCC800
          ELSE
            MOVE      SAVKEY33,KEY33       * Read with current key
            CALL      RDOPTRB1
            IF        OVRCD = 0
              MOVE      FILTTRTO,OPTDITEM
              CALL      UPOPTRB1
              ADD       ONE,NOFITUPD
              GOTO      PROCC800
            ENDIF
          ENDIF
.
          GOTO      PROCC200
.
PROCC800  MOVE      OPTDCODE,XPTDCODE
          MOVE      FILTTRFR,XPTDITEM
          MOVE      FILTHOSP,XPTDHOSP
.
          CALL      WRWORK3             * Write the new record to the temp file
.
          MOVE      SAVKEY33,KEY33
          GOTO      PROCC100
.
PROCC999  RETURN
+
.------------------------------------------------------------------------
. Generate the report from the temporary file for favourite charge item                     
.------------------------------------------------------------------------
GENRA000  DISPLAY   *P1:24,*EL,*P29:24,"** Generating Report **",*W:
                    *P1:24,*EL,*P24:24,"Generating : ";
.
          MOVE      "60",CLNO           * Force a new page with first print
          MOVE      ZERO,CPAGENO        * Start from page zero
.
          CALL      HEADA000
.
. Loop over the temporary file, and print the data
.
          MOVE      SP70,KEY31          * Process the entire temporary file
          CALL      RDSWORK             * Position at start of file
.
GENRA100  CALL      RDKWORK             * Read the next record from the file
          BRANCH    OVRCD,GENRA900      * Finished processing the temp file
.
. Print the data for this record
.
          CALL      PWKDTA00            * Print this data record
          GOTO      GENRA100            * Get the next record
.
. Finished processing the temporary file
.
GENRA900  COMPARE   ZERO,CPAGENO        * Was anything printed ?
          GOTO      GENRA950 IF EQUAL   * No. Tell the user.
.
          CALL      PWKFOA00 
.
          PRINT     *N,*N,"***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
          GOTO      GENRA999            * Finished everything!
.
. No data found to print
.
GENRA950  DISPLAY   *P1:24,*EL,*P23:24,*V2LON:
                    "** No Data for Selected Criteria **",*W2;
.
GENRA999  RETURN
.
.------------------------------------------------------------------------
. Print favourite charge item details
.------------------------------------------------------------------------
PWKDTA00  COMPARE   "52",CLNO             * Is the page full ?
          CALL      HEADA000 IF NOT LESS  * Yes. Print a new page heading
.
          DISPLAY   *P37:24,*V2LON,XMUFHOSP,SP1,XMUFUOGR,SP1,XMUFUSER,SP1:
                            XMUFFAVG,SP1,XMUFGHED,SP1,XMUFCODE;
.
          IF        IBCNMHOS = ONE
            PRINT     *1,"|",XMUFHOSP:
                      *10,"|",XMUFUOGR:
                      *29,"|",XMUFUSER:
                      *40,"|",XMUFFAVG:
                      *56,"|",XMUFGHED:
                      *74,"|",XMUFCODE:
                      *84,"|",XMUFERRO:
                      *115,"|"
          ELSE
            PRINT     *1,"|",XMUFUOGR:
                      *20,"|",XMUFUSER:
                      *31,"|",XMUFFAVG:
                      *47,"|",XMUFGHED:
                      *65,"|",XMUFCODE:
                      *75,"|",XMUFERRO:
                      *106,"|"
          ENDIF
.
          ADD       ONE,CLNO            * Update current line count
.
PWKDTA99  RETURN
+
.------------------------------------------------------------------------
. Print footing for favourite charge item
.------------------------------------------------------------------------
PWKFOA00  PRINT     *1,SP70
          PRINT     *1,"No. of pmsufiaf records updated : ",*35,NOFITUPD
          PRINT     *1,"No. of pmsufiaf records in error: ",*35,NOFITERR 
.
PWKFOA99  RETURN
+
.------------------------------------------------------------------------
. Print a page heading                                    
.------------------------------------------------------------------------
HEADA000  CLOCK     TIME,CTIMEIS        * Get current time
          MOVE      "Global Update of Favourite Charge Items",PRGNAM 
          CALL      HEAD132             
          CALL      POPTA000            * Print the options used
.
HEADA999  RETURN
+
.------------------------------------------------------------------------
. Print sub heading with selected options for favourite charge item            
.------------------------------------------------------------------------
POPTA000  MOVE      FORTY,FORM2 
          IF        IBCNMHOS = ONE
            PRINT     *FORM2,"Hospital            : ",FILTHOSP,SP8,HOSPDESC
            ADD       ONE,CLNO
          ENDIF
.
          PRINT     *FORM2,"Occupation Group    : ",FILTOCCG,SP8,OCCGDESC
          PRINT     *FORM2,"User ID             : ",FILTUSID,SP1,USERNAME               
          PRINT     *FORM2,"Favourite Group     : ",FILTFAVG,SP8,FAVGDESC
          PRINT     *FORM2,"Item to be replaced : ",FILTITFR,SP2,ITFRDESC
          PRINT     *FORM2,"New Item Code       : ",FILTITTO,SP2,ITTODESC                
          PRINT     *1,SP70                
.
          IF        IBCNMHOS = ONE
            PRINT     *1,"|========|==================|==========":
                         "|===============":
                         "|=================|=========": 
                         "|==============================|" 
            PRINT     *1,"|Hospital|Occupational Group|User Id   ":
                         "|Favourite Group":
                         "|Sub-Group Heading|Item Code": 
                         "|Error Description             |" 
            PRINT     *1,"|========|==================|==========":
                         "|===============":
                         "|=================|=========": 
                         "|==============================|" 
          ELSE
            PRINT     *1,"|==================|==========":
                         "|===============":
                         "|=================|=========":
                         "|==============================|"
            PRINT     *1,"|Occupational Group|User Id   ":
                         "|Favourite Group":
                         "|Sub-Group Heading|Item Code":
                         "|Error Description             |"
            PRINT     *1,"|==================|==========":
                         "|===============":
                         "|=================|=========":
                         "|==============================|"
          ENDIF
.
          ADD       NINE,CLNO
.
POPTA999  RETURN
+
.------------------------------------------------------------------------
. Generate the report from the temporary file for surgical preference            
.------------------------------------------------------------------------
GENRB000  DISPLAY   *P1:24,*EL,*P29:24,"** Generating Report **",*W:
                    *P1:24,*EL,*P24:24,"Generating : ";
.
          MOVE      "60",CLNO           * Force a new page with first print
          MOVE      ZERO,CPAGENO        * Start from page zero
.
          CALL      HEADB000
.
. Loop over the temporary file, and print the data
.
          MOVE      SP70,KEY37          * Process the entire temporary file
          CALL      RDSWORK2            * Position at start of file
GENRB100  CALL      RDKWORK2            * Read the next record from the file
          BRANCH    OVRCD,GENRB900      * Finished processing the temp file
.
. Print the data for this record
.
          CALL      PWKDTB00            * Print this data record
          GOTO      GENRB100            * Get the next record
.
. Finished processing the temporary file
.
GENRB900  COMPARE   ZERO,CPAGENO        * Was anything printed ?
          GOTO      GENRB950 IF EQUAL   * No. Tell the user.
.
          CALL      PWKFOB00 
.
          PRINT     *N,*N,"***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
          GOTO      GENRB999            * Finished everything!
.
. No data found to print
.
GENRB950  DISPLAY   *P1:24,*EL,*P23:24,*V2LON:
                    "** No Data for Selected Criteria **",*W2;
.
GENRB999  RETURN
.
.------------------------------------------------------------------------
. Print surgical preference details
.------------------------------------------------------------------------
PWKDTB00  COMPARE   "52",CLNO             * Is the page full ?
          CALL      HEADB000 IF NOT LESS  * Yes. Print a new page heading
.
          DISPLAY   *P37:24,*V2LON,XPDFCODE,SP1,XPDFPREF,SP1,XPDFCLSS:
                            SP1,XPDFTYPE,SP1,XPDFITEM,SP1,XPDFHOSP;
.
          PRINT     *1,"|",XPDFCODE:
                    *13,"|",XPDFPREF:
                    *29,"|",XPDFCLSS:
                    *40,"|",XPDFTYPE:
                    *50,"|",XPDFITEM:
                    *66,"|",XPDFHOSP:
                    *71,"|",XPDFERRO:
                    *102,"|"
.
          ADD       ONE,CLNO            * Update current line count
.
PWKDTB99  RETURN
+
.------------------------------------------------------------------------
. Print footing for surgical preference
.------------------------------------------------------------------------
PWKFOB00  PRINT     *1,SP70
          PRINT     *1,"No. of oprdpfaf records updated : ",*35,NOFITUPD
          PRINT     *1,"No. of oprdpfaf records in error: ",*35,NOFITERR 
.
PWKFOB99  RETURN
+
.------------------------------------------------------------------------
. Print a page heading for surgical preference                                   
.------------------------------------------------------------------------
HEADB000  CLOCK     TIME,CTIMEIS        * Get current time
          MOVE      "Global Update of Preference Items",PRGNAM 
          CALL      HEAD132             
          CALL      POPTB000            * Print the options used
.
HEADB999  RETURN
+
.------------------------------------------------------------------------
. Print sub heading with selected options for surgical preference                
.------------------------------------------------------------------------
POPTB000  MOVE      FORTY,FORM2 
.
          PRINT     *FORM2,"Item to be replaced : ",FILTTRFR,SP2,TRFRDESC
          PRINT     *FORM2,"New Item Code       : ",FILTTRTO,SP2,TRTODESC                
          PRINT     *1,SP70                
.
          PRINT     *1,"|===========|===============|==========":
                       "|=========|===============|====":
                       "|==============================|" 
          PRINT     *1,"|Doctor Code|Preference Code|Class Type":
                       "|Item Type|Item Code      |Hosp":
                       "|Error Description             |" 
          PRINT     *1,"|===========|===============|==========":
                       "|=========|===============|====":
                       "|==============================|" 
.
          ADD       SIX,CLNO
.
POPTB999  RETURN
+
.------------------------------------------------------------------------
. Generate the report from the temporary file for tray details                   
.------------------------------------------------------------------------
GENRC000  DISPLAY   *P1:24,*EL,*P29:24,"** Generating Report **",*W:
                    *P1:24,*EL,*P24:24,"Generating : ";
.
          MOVE      "60",CLNO           * Force a new page with first print
.          MOVE      ZERO,CPAGENO        * Start from page zero
.
          CALL      HEADC000
.
. Loop over the temporary file, and print the data
.
          MOVE      SP70,KEY30          * Process the entire temporary file
          CALL      RDSWORK3            * Position at start of file
GENRC100  CALL      RDKWORK3            * Read the next record from the file
          BRANCH    OVRCD,GENRC900      * Finished processing the temp file
.
. Print the data for this record
.
          CALL      PWKDTC00            * Print this data record
          GOTO      GENRC100            * Get the next record
.
. Finished processing the temporary file
.
GENRC900  COMPARE   ZERO,CPAGENO        * Was anything printed ?
          GOTO      GENRC950 IF EQUAL   * No. Tell the user.
.
          CALL      PWKFOC00 
.
          PRINT     *N,*N,"***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
          GOTO      GENRC999            * Finished everything!
.
. No data found to print
.
GENRC950  DISPLAY   *P1:24,*EL,*P23:24,*V2LON:
                    "** No Data for Selected Criteria **",*W2;
.
GENRC999  RETURN
.
.------------------------------------------------------------------------
. Print Details for tray details
.------------------------------------------------------------------------
PWKDTC00  COMPARE   "52",CLNO             * Is the page full ?
          CALL      HEADC000 IF NOT LESS  * Yes. Print a new page heading
.
          DISPLAY   *P37:24,*V2LON,XPTDCODE,SP1,XPTDITEM;
.
          PRINT     *1,"|",XPTDCODE:
                    *17,"|",XPTDITEM:
                    *33,"|",XPTDHOSP:
                    *38,"|",XPTDERRO:
                    *69,"|"
.
          ADD       ONE,CLNO            * Update current line count
.
PWKDTC99  RETURN
+
.------------------------------------------------------------------------
. Print footing for tray details
.------------------------------------------------------------------------
PWKFOC00  PRINT     *1,SP70
          PRINT     *1,"No. of oprtrybf records updated : ",*35,NOFITUPD
          PRINT     *1,"No. of oprtrybf records in error: ",*35,NOFITERR 
.
PWKFOC99  RETURN
+
.------------------------------------------------------------------------
. Print a page heading for tray details                                    
.------------------------------------------------------------------------
HEADC000  CLOCK     TIME,CTIMEIS        * Get current time
          MOVE      "Global Update of Tray Items",PRGNAM 
          CALL      HEAD132             
          CALL      POPTC000            * Print the options used
.
HEADC999  RETURN
+
.------------------------------------------------------------------------
. Print sub heading with options selected for tray details                
.------------------------------------------------------------------------
POPTC000  MOVE      FORTY,FORM2 
.
          PRINT     *FORM2,"Item to be replaced : ",FILTTRFR,SP2,TRFRDESC
          PRINT     *FORM2,"New Item Code       : ",FILTTRTO,SP2,TRTODESC                
          PRINT     *1,SP70                
.
          PRINT     *1,"|===============|===============|====":
                       "|==============================|" 
          PRINT     *1,"|Tray Code      |Item Code      |Hosp":
                       "|Error Description             |" 
          PRINT     *1,"|===============|===============|====":
                       "|==============================|" 
.
          ADD       SIX,CLNO
.
POPTC999  RETURN
+
.------------------------------------------------------------------------
. I/O Routines for temporary file                                  
.------------------------------------------------------------------------
RDSWORK   RESET     KEY31               * Make sure key is reset
          READ      WORK,KEY31;;        * Position on key
          RETURN
.
RDWORK    RESET     KEY31               * Make sure key is reset
          MOVE      ZERO,OVRCD          * Assume successful read
          READ      WORK,KEY31;XMUFHOSP,XMUFUOGR,XMUFUSER,XMUFFAVG,XMUFGHED:
                               XMUFCODE,XMUFERRO
          GOTO      OVERCOND IF OVER    * Unsuccessful read
          RETURN
.
RDKWORK   MOVE      ZERO,OVRCD          * Assume successful read
          READKS    WORK;XMUFHOSP,XMUFUOGR,XMUFUSER,XMUFFAVG,XMUFGHED:
                         XMUFCODE,XMUFERRO
          GOTO      OVERCOND IF OVER    * Unsuccessful read
          RETURN
.
UPWORK    UPDATE    WORK;XMUFHOSP,XMUFUOGR,XMUFUSER,XMUFFAVG,XMUFGHED:
                         XMUFCODE,XMUFERRO
          RETURN
.
WRWORK    RESET     KEY31               * Make sure key is reset
          WRITE     WORK,KEY31;XMUFHOSP,XMUFUOGR,XMUFUSER,XMUFFAVG,XMUFGHED:
                               XMUFCODE,XMUFERRO
          RETURN
+
.------------------------------------------------------------------------
. I/O Routines for temporary file
.------------------------------------------------------------------------
RDSWORK2  RESET     KEY37               * Make sure key is reset
          READ      WORK2,KEY37;;       * Position on key
          RETURN
.
RDWORK2   RESET     KEY37               * Make sure key is reset
          MOVE      ZERO,OVRCD          * Assume successful read
          READ      WORK2,KEY37;XPDFCODE,XPDFPREF,XPDFCLSS,XPDFTYPE:
                               XPDFITEM,XPDFHOSP,XPDFERRO
          GOTO      OVERCOND IF OVER    * Unsuccessful read
          RETURN
.
RDKWORK2  MOVE      ZERO,OVRCD          * Assume successful read
          READKS    WORK2;XPDFCODE,XPDFPREF,XPDFCLSS,XPDFTYPE:
                          XPDFITEM,XPDFHOSP,XPDFERRO
          GOTO      OVERCOND IF OVER    * Unsuccessful read
          RETURN
.
UPWORK2   UPDATE    WORK2;XPDFCODE,XPDFPREF,XPDFCLSS,XPDFTYPE:
                          XPDFITEM,XPDFHOSP,XPDFERRO
          RETURN
.
WRWORK2   RESET     KEY37               * Make sure key is reset
          WRITE     WORK2,KEY37;XPDFCODE,XPDFPREF,XPDFCLSS,XPDFTYPE:
                          XPDFITEM,XPDFHOSP,XPDFERRO
          RETURN
+
.------------------------------------------------------------------------
. I/O Routines for temporary file
.------------------------------------------------------------------------
RDSWORK3  RESET     KEY30               * Make sure key is reset
          READ      WORK3,KEY30;;        * Position on key
          RETURN
.
RDWORK3   RESET     KEY30               * Make sure key is reset
          MOVE      ZERO,OVRCD          * Assume successful read
          READ      WORK3,KEY30;XPTDCODE,XPTDITEM,XPTDERRO
          GOTO      OVERCOND IF OVER    * Unsuccessful read
          RETURN
.
RDKWORK3  MOVE      ZERO,OVRCD          * Assume successful read
          READKS    WORK3;XPTDCODE,XPTDITEM,XPTDERRO
          GOTO      OVERCOND IF OVER    * Unsuccessful read
          RETURN
.
UPWORK3   UPDATE    WORK3;XPTDCODE,XPTDITEM,XPTDERRO
          RETURN
.
WRWORK3   RESET     KEY30               * Make sure key is reset
          WRITE     WORK3,KEY30;XPTDCODE,XPTDITEM,XPTDERRO
          RETURN
+
.==============================================================================
.
          INC       STD001IO
          INC       PATCODKY
          INC       PATHSPKY
          INC       TFILENAM                     * Generate Temp File Name
          INC       PATMCHRD
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC
          INC       PATHSPIO/INC
          INC       PATMCHIO/INC
          INC       PMSUFIIO/INC
          INC       OPRDPFIO/INC
          INC       OPRTRBIO/INC
          INC       OPRITEIO/INC
          INC       NZPMCHIO/INC
          INC       WEBSECIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging

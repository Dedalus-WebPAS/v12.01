.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAINP82                                             *
.*                                                                            *
.*     FUNCTION:         INPATIENT STATISTICS BY COST CENTRE CODE             *
.*                                                                            *
.*     DATE WRITTEN:     07/03/1994  ( A copy of IBAHOS82)                    *
.*                                                                            *
.*     AUTHOR:           J.TAN                                                *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                                                                            *
.*        ********  Programmers Note:  When making changes in this program,   *
.*                  check the following programs to see if the change is also *
.*                  required there:  IBAHOS82, IBAPMS21, IBAPMS22, IBAHMS21 & *
.*                  IBAHMS65                                                  *
.******************************************************************************
.* V12.00.01 22/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.* V11.04.01 23/04/2024   J.Tan    TSK 0934010                                *
.*           Added Maxyear to stop creating long future day files             *
.* V11.03.02 18/04/2023   Thanh T  TSK 0909393                                *
.*           Changed RDDA0000 to match the hospital of oprsesaf to oprdetaf   *
.* V11.03.01 20/03/2023  PJ Le Febour   TSK 0909393                           *
.*           amended KEY19 to KEY22 for theatre index changes                 *
.* V11.01.01 27/04/2021  Thanh T        TSK 0895165                           *
.*           Recompiled for OPRARDFD changes                                  *
.******************************************************************************
.* V10.03.02  28/12/2012 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
.*        V10.03.01 18/10/2012  Jill Parkinson CAR 273375                     *
.*                  Recompiled for DATECONV - first day of year calculation   *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V10.00.02 10/05/2010  Steve Armstrong  CAR 215292                   *
.*                  Fixed RDDC0000 by replacing RSOPARD1/RKOPARD1 with        *
.*                  RDOPARD1.  Previous code was also matching wrong variable *
.*                  after RK... and so was always looping through opardaf     *
.*                  until the eof.                                            *
.*        V10.00.01 13/04/2010  Saroune Soeur    CAR 215292                   *
.*                  added ovrcd branch to RDDC0000                            *
.******************************************************************************
.*        V9.12.03  29/07/2009  Saroeun Soeur    CAR 194994                   *
.*                  fixed number of operation count  PWAR0000                 *
.*                                                                            *
.*        V9.12.02  13/07/2009  Saroeun Soeur    CAR 194994                   *
.*                  Read OPRARDAF instead of OPRDECAF                         *
.*                  commented out RDDB0000                                    *
.*        V9.12.01  26/05/2009  Saroeun Soeur    CAR 194994                   *
.*                  checked sector read and compiles                          *
.*                                                                            *
.*        V9.12.00  22/04/2009  Steve Armstrong  CAR 194994                   *
.*                  Ported from V6 to determine a ballpark for Regina.        *
.*                  The program compiles except for the PTWRCCTR (Cat CW)     *
.*                  field which will need to be added to PATWR1FD if this     *
.*                  port goes ahead.  I have left the program here in case it *
.*                  does go ahead.  The parameters sectors and position are   *
.*                  yet to be checked.                                        *
.******************************************************************************
.*        V6.09.B01 12/02/2001  Greg Horvat                                   *
.*                  Replaced the call to CALC with DAYSEP                     *
.*        V6.06.01  22/09/1999  Steve Downing                                 *
.*                  Removed unused date range routine                         *
.******************************************************************************
.*        V6.05.02  04/01/1999  Nicole Harrington                             *
.*                  Mods to print from/to date with century                   *
.*        V6.05.01  29/09/1998  Steve Armstrong   SRF 627949                  *
.*                  Mods to display keyin todate correctly                    *
.******************************************************************************
.*        V6.03.04  13/12/1995  Steve Armstrong   SRF 612197                  *
.*                  Mods to make calculation of active days for ward a        *
.*                  Procedure which can be used by other programs which       *
.*                  previously used the CHAC0000 routine.                     *
.*                  27/10/1995  Greg Horvat      SRF 612064 612205 612206     *
.*                  Recompiled for changes to NHOSPDAY                        *
.*        V6.03.03  24/10/1995  Steve Armstrong  SRF 612197                   *
.*                  Mods to calculate figures based on the number of days the *
.*                  ward was active in a period                               *
.*        V6.03.02  11/10/1995  Greg Horvat      SRF 611811                   *
.*                  Changed to use CALCDAYS instead of CALC when calculating  *
.*                  the difference between two dates                          *
.*        V6.03.01  05/07/1995    Justin Coates  SRF 135926                   *
.*                  attempt to fix it selecting in-active wards  - changed    *
.*                  GETB0000 to do the same regardless of winput value        *
.******************************************************************************
.*                       V6.01.02  Ian Rutt 08/06/94  srf# 129887             *
.*                                 Changed heading from                       *
.*                                   INPATIENT STATISTICS BY COST CENTER to   *
.*                                   INPATIENT STATISTICS BY FOCUS CARE CENTRE*
.*                       V6.01.03  Ian Rutt 04/07/94  srf# 130308             *
.*                                 Removed adding one to cpageno, head132 does*
.*                                 it.                                        * 
.*                       V6.02.01  15/09/94 J.Tan   SRF 132105                *
.*                                 Mods to use port instead of portno         *
.******************************************************************************
.
          INC       STD001FD
          INC       PATCOMM/INC
.
          INC       IBACONFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC        * Codes file
          INC       PATCONFD/INC
          INC       PATWBHFD/INC        * Ward/Bed History file
          INC       PATWR1FD/INC        * Ward/Bed file
          INC       PATCT1FD/INC        * Catchment codes
          INC       PATMA1FD/INC        * Master file
          INC       PATDSCFD/INC        * Discharge file
          INC       PATTRNFD/INC        * Transfer file
          INC       PATMI1FD/INC        * Admission file
          INC       PMSVX1FD/INC        * Admission file
          INC       PATDAYFD/INC        * day file
          INC       OPRARDFD/INC
.
          INC       OPRCLIFD/INC        * Clinic file
          INC       OPRDEAFD/INC        * Operation detail file
          INC       OPRDEBFD/INC
          INC       OPRDECFD/INC
          INC       OPRSESFD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WDACTVVR/INC        * variables for WDACTVDY
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
PNPTMPAA  IFILE     SQL, FIXED=9, KEY="U1-8"
.
. ----- Tempfile a consists of following vars -----
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
DXADMN           DIM        8            8          1     Admission No.
.EOR                                                9
.
.  redefine form fields from key
.  -----------------------------
.Name     Type   Length   Start    Description
.----     ----   ------   -----    -----------
XADMN     DIM         8       1    Admission Number     
.
.
PNPTMPBB  IFILE     SQL, FIXED=113, KEY="U1-14,15-17"
.
. ----- Tempfile b consists of following vars -----
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
TMPCCTR         DIM       14           14            1    Cost centre code
TMPWARD         DIM        3            3           15    Ward code
TMPWDES         DIM       20           20           18    Ward description
TMPBEDS         FORM       4            3           38    No of beds
TMPNPRAD        FORM       8            5           41    Adm.(priv not boarder)
TMPNPBAD        FORM       8            5           45    Adm.(pub not boarder)
TMPRIN          FORM       8            5           50    No of transfer in
TMPROUT         FORM       8            5           55    No of transfer out
TMPDSCH         FORM       8            5           60    No of discharges
TMPNBDAY        FORM       8            5           65    Bed days not boarders
TMPBBDAY        FORM       8            5           70    Bed days boarders
TMPDEATH        FORM       8            5           75    No of death patients
TMPBPRAD        FORM       8            5           80    Private Boarders
TMPBPBAD        FORM       8            5           85    Public Boarders
TMPLVBD         FORM       8            5           90    Bed days on leave
TMPAEADM        FORM       8            5           95    No of a&e admission
TMPWCAS         FORM       6            4          100    Number of cases
TMPDAYS         FORM       6            4          104    Days ward is active
TMPBDAYS        FORM       8            5          109    Active Bed Days
TMPASTRX        FORM       1            2          111    Asterisk to be printed
.                                                           0 = no
.                                                           1 = yes
.EOR                                               113
.
PNPTMPCC  IFILE     SQL, FIXED=14, KEY="U1-3"
.
. ----- Tempfile c consists of following vars -----
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
XCCODE          DIM         3           3           1     Catchment code
XCADMN          FORM        8           5           4     No of admissions
XCBOARD         FORM        8           5           9     No of boarders
.EOR                                               14
.
OPCNNOOP  FORM      1
.
.         Work Variables
.
ASTATFLG  FORM      1       * astat flag 
AVDAY     FORM      6.2
AVSTY     FORM      6.2
. 
BRDFLG    FORM      1       * boarder flag 0 = yes, 1 = no
.
CALDAYS   FORM      4
CHGWRD    FORM      1       * change ward flag
CURMTH    DIM       6
CENDDT    DIM       8
CMDLINE   DIM       50
.
DIM4      DIM       4
DIM6      DIM       6
DIM8      DIM       8
DIM36     DIM       36
DAYCASE   FORM      1       * daycase flag
DSHFLG    FORM      1
.
ENDMTH    DIM       6
.
FORM6     FORM      6
FORM8     FORM      8
FADMDTE   DIM       8            * fromdate + 1 ccyymmdd
FROMDATE  DIM       8       * ccyymmdd
.
GTAAE     FORM      8            * Total no of accidents & emergencies
GTADM     FORM      8            * Total no of admissions
GTASTRX   FORM      1            * 0 = don't print asterisk
.                                  1 = print asterisk
GTDSH     FORM      8            * Total no of discharges
GTDEATH   FORM      8            * Total no of deaths
GTLEAVE   FORM      8            * Total no of beddays on leave
GTBDAY    FORM      8            * Total no of beddays
GTBEDAY   FORM      8
GTNPBAD   FORM      8            * Total no of admissions public boarders
GTNPRAD   FORM      8            *           "            private not boarders
GTBPRAD   FORM      8            *           "            private boarders
GTBPBAD   FORM      8            *           "            public boarders
GTRIN     FORM      8            * Total no of transfer in
GTROUT    FORM      8            * Total no of transfer out 
GTTBED    FORM      8            * Total no of beds for the period
GTTHEA    FORM      8
GTWAN     FORM      8            * Total no of operations
.
GTAVDAY   FORM      6.2
GTAVST    FORM      6.2
GTPCENT   FORM      6.2
.
IND       FORM      2
JYEAR     FORM      2
JDAYS     FORM      3
LYR       FORM      1       * leap year
LEVFLG    FORM      1       * on leave flag
.
NDAYPRD   FORM      6
NBRDAYS   FORM      4       * number of days
.
PATDEAD   FORM      1        * patient dead ? (0 = no, 1 = yes)
PCENTG    FORM      6.2
PNPTMP1   DIM       8
PNPTMP2   DIM       8
PNPTMP3   DIM       8
PUBFLG    FORM      1        * public/private flag
.
SYEAR     DIM       4
SDAYMON   DIM       4
SAVKEY11  DIM       11
SAVCCTR   DIM       14
SAVWDES   DIM       20
SAVBED    DIM       3
SAVWARD   DIM       3
SAVWRD    DIM       3
SELMTH    DIM       6
SUBASTRX  FORM      1              * 0 = don't print asterisk for subtotal
.                                    1 = print asterisk for subtotal
SUBBEDS   FORM      4
SUBBEDY   FORM      8
SUBTPAT   FORM      8
SUBTRIN   FORM      8
SUBDSCH   FORM      8
SUBTROUT  FORM      8
SUBTADAY  FORM      6.2
SUBTBDAY  FORM      8
SUBWCAS   FORM      6
.
MAXYEAR   DIM       4
TDAYMON   DIM       4
TYEAR     DIM       4
TIMEIS    DIM       8
TOTCST    FORM      8
TOTPAT    FORM      8
TOTBDAY   FORM      8
TOTPB     FORM      8
TOTPR     FORM      8
TADMDTE   DIM       8            * end date of period
TTODATE   DIM       8            * end date of period + 1
TMPAVSTY  FORM      6.2
TODATE    DIM       8            * ccyymmdd
.
UPORWR    FORM      1
.
VERT      FORM      2
WDATE1    DIM       8
WDATE2    DIM       8
.
.         Constants
.
CODEA     INIT      "A "
CODED     INIT      "D "
CODEP     INIT      "P "
CODES     INIT      "S "
CODECG    INIT      "CG"
CODECH    INIT      "CH"
CODECW    INIT      "CW"
.
LYEAR     FORM      "366"
OYEAR     FORM      "365"
PSTROL    FILE      ASCII, FIXED=256
.
ZEIGHT    INIT      "ZZZZZZZZ"
Z20       INIT      "ZZZZZZZZZZZZZZZZZZZZ"
.
PRGID     INIT      "IBAINP82"
PRGNAM    INIT      "INPATIENT STATISTICS BY FOCUS CARE CENTRE"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                         MAINLINE                                   *
.**********************************************************************
ML0000    CALL      INIT0000                  * initialisation
.
ML1000    CALL      KDAT0000                  * Keyin date range
          BRANCH    EXIT,ML9999
.
          CALL      CREA0000                  * Create temporary file a
          CALL      CREB0000                  * create temporary file b
          CALL      CREC0000                  * create temporary file c
.
          CALL      DAYA0000                  * process patdayaf data
          CALL      PTMP0000                  * process inpatient stats
          CALL      OPWR0000                  * process operating room stats
          CALL      PBED0000                  * process number of beds
          CALL      PRNT0000                  * printing the tempfiles
.
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                         INIT0000                                   *
.*                   Initialisation                                   *
.**********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening oprdetaf";
          OPEN      OPRDETA1,"oprdetaf"
.
          DISPLAY   *P64:24,"oprdetbf";
          OPEN      OPRDETB1,"oprdetbf"
.
          DISPLAY   *P64:24,"oprardaf";
          OPEN      OPRARDA1,"oprardaf"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"oprdetcf";
          OPEN      OPRDETC1,"oprdetcf"
.
          DISPLAY   *P64:24,"oprsesaf";
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRSESA7,"oprsesaf"
.
          DISPLAY   *P64:24,"oprcliaf";
          OPEN      OPRCLIA1,"oprcliaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmissf";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"patwbhaf";
          OPEN      PATWBHA1,"patwbhaf"
.
          DISPLAY   *P64:24,"patct1cf";
          OPEN      PATCT1C1,"patct1cf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SEVENTY9;*115,PTCNDGUP,*116,PTCNDGUA,*239,PTCNDGAD:
                                    PTCNDGDS,PTCNDGTR,PTCNDGRG
          READ      CONTROLF,TWENTY;*250,PTCNH82W
          READ      CONTROLF,EIGHTY;*242,PTCNALOS
          READ      CONTROLF,FORTY;*115,OPCNNOOP
          RETURN
+
.**********************************************************************
.*                         KDAT0000                                   *

.*                   Keyin range of dates                             *
.**********************************************************************
KDAT0000  DISPLAY   *P1:4,*EF,"From Date :":
                        *P1:6,"To Date   :";
.
KDAT1000  MOVE      FOUR,CVERT
          MOVE      TWENTY,CCOL
          MOVE      ONE,CCENTRY
          MOVE      CCC,CCENT
          MOVE      ZERO,CDAY
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                     * Keyin fromdate
          BRANCH    OVRCD,KDAT9000
.
          PACK      FADMDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FADMDTE
.
          MOVE      SIX,CVERT
          CALL      KEYDATE                     * Keyin todate
          BRANCH    OVRCD,KDAT1000
.
          PACK      TADMDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TADMDTE
.
          MATCH     FADMDTE,TADMDTE             * match date range
          GOTO      KDAT2000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid date range entered **    ";
          CALL      HITENTER
          GOTO      KDAT0000
.
KDAT2000  CALL      CONTQST
          MOVE      ZERO,EXIT
          BRANCH    CEXIT,KDAT3000,KDAT1000,KDAT9000
.
.------ yes selected ------
.
KDAT3000  UNPACK    TADMDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON                  * convert to julian format
.
          ADD       ONE,JULDAY              * add one day to todate
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
.
          CALL      JULCON                  * convert back to DDMMYY format
.
          PACK      TTODATE,CC,YY,MM,DD
          REP       " 0",TTODATE
.
          GOTO      KDAT9999
.
KDAT9000  MOVE      ONE,EXIT
KDAT9999  RETURN
+
.**********************************************************************
.*                         DAYA0000                                   *
.*                   Process PATDAYFD Data                            * 
.*       looks in the patdCCYY data file and writes all the admission *
.*       to the temp file B thats within the date range selected      *         
.**********************************************************************
DAYA0000  DISPLAY   *P1:24,*EL,*P20:24,"Admission No : ",*P50:24,"Scanning : ";
.
          UNPACK    TADMDTE,XCENT,XYEAR,TDAYMON       * period range end date
          PACK      TYEAR,XCENT,XYEAR
.
          UNPACK    FADMDTE,XCENT,XYEAR,SDAYMON       * period range start date
          PACK      SYEAR,XCENT,XYEAR
.
          PACK      MAXYEAR,CCC,CYY                   * Current Year
          REP       " 0",MAXYEAR
.
DAYA1000  CLOSE     PATDAYA1
          PACK      CFNAME,FILDAYA1,SYEAR
.
          TRAP      CRET0000 IF IO
          OPEN      PATDAYA1,CFNAME
          TRAPCLR   IO
.
          PACK      KEY12,SDAYMON,SP10
          CALL      RSPTDAY1
DAYA2000  CALL      RKPTDAY1
          BRANCH    OVRCD,DAYA5000
.
.         Display day and month that is being processed
.
          UNPACK    PTDYDATE,XMON,XDAY
          DISPLAY   *P1:24,XDAY,SLASH,XMON
          DISPLAY   *P61:24,*EL,PTDYADMN
.
          MATCH     TYEAR,SYEAR
          GOTO      DAYA3000 IF NOT EQUAL
.
          MATCH     PTDYDATE,TDAYMON
          GOTO      DAYA9999 IF LESS
.
DAYA3000  DISPLAY   *P35:24,*V2LON,PTDYADMN
.
.         Only need to write to the temporary file if the admission 
.         number is not already there
.
          PACK      KEY8,PTDYADMN
          CALL      RDTMP1
          BRANCH    OVRCD,DAYA4000
          GOTO      DAYA2000              
.
DAYA4000  MOVE      PTDYADMN,XADMN
          CALL      WRTMP1
          GOTO      DAYA2000              
.
DAYA5000  MATCH     TYEAR,SYEAR
          GOTO      DAYA9999 IF NOT LESS
.
.         Check Maximum Year (Current year plus 10 years) to stop creating
.         long future day files
.
          MATCH     "9999",SYEAR
          GOTO      DAYA9999 IF EQUAL
          MATCH     SYEAR,MAXYEAR
          GOTO      DAYA9999 IF LESS
.
.         Open the next patdCCYY file if date range spans multiple years
.
          MOVE      SYEAR,FORM4
          ADD       ONE,FORM4
          MOVE      FORM4,SYEAR
          REP       " 0",SYEAR
          MOVE      "0101",SDAYMON
.
          GOTO      DAYA1000
.
DAYA9999  RETURN
+
.**********************************************************************
.*                         CRET0000                                   *
.*        Create the file that is needed to write to                  *
.**********************************************************************
CRET0000  CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    CFNAME,CMDLINE
          APPEND    " 13 U1-4,5-12 ",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      PATDAYA1,CFNAME          
          RETURN
+
.**********************************************************************
.*                         PTMP0000                                   *
.*                   Read through the temp file B                     *
.*                                                                    *
.**********************************************************************
.
PTMP0000  DISPLAY   *P1:24,*EL,*P20:24:
                    "Admission No : ",*P50:24,"Scanning : ";
.
.         start from the top of the temp file B
.
          PACK      KEY8,SP8            
          CALL      RDSTMP1
PTMP1000  CALL      RDKTMP1
          BRANCH    OVRCD,PTMP9999
.
          DISPLAY   *P61:24,XADMN;
.
          MOVE      ONE,PATDEAD               * patient isn't dead as yet
          MOVE      ONE,ASTATFLG              * patient isn't discharged as yet
.
.         Check if patient on the admission file
.
          MOVE      XADMN,AADMNO
          MOVE      AADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,PTMP1000
.
.         Check if the person is cancelled or not
.
          COMPARE   FIVE,ASTAT                * check if patient cancelled
          GOTO      PTMP1000 IF EQUAL
.
.         Check if patient on the master file
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PTMP1000
.
          MATCH     FADMDTE,ADATE             * admission date within period ?
          GOTO      PTMP2000 IF LESS
.
          MATCH     ADATE,TADMDTE
          GOTO      PTMP1000 IF LESS
.
PTMP2000  COMPARE   THREE,ASTAT               * check if patient discharged
          GOTO      PTMP3000 IF NOT EQUAL
.
.         Check if patient on the discharge file
.
          MOVE      XADMN,DADMNO
          MOVE      DADMNO,KEY8
          CALL      RDDSCH1
          BRANCH    OVRCD,PTMP1000
.
          MATCH     FADMDTE,DDATE
          GOTO      PTMP1000 IF LESS
.
.         If the person is discharged their astat value will be a 3
.         So astatflg should remain set to one if the astat value is not a 3
.
          MOVE      ZERO,ASTATFLG             * patient is discharged
.
.         Check whether the patient is dead
.
          PACK      KEY5,CODED,DSTAT
          CALL      RDCODE1
          BRANCH    OVRCD,PTMP3000
.
          MATCH     ANSD,TCINDC1
          GOTO      PTMP3000 IF NOT EQUAL
          MOVE      ZERO,PATDEAD              * patient is dead 
.
PTMP3000  DISPLAY   *P35:24,*V2LON,XADMN;
.
          CALL      UMST0000                  * update the temp file b
          CALL      UCST0000                  * update the temp file c
.
          GOTO      PTMP1000                  * read next temporary file record
.
PTMP9999  RETURN
+
.**********************************************************************
.*                         UMST0000                                   *
.*              Generate data for TEMPFILE file a                     *
.**********************************************************************
UMST0000  MOVE      ZERO,PUBFLG               * public/private flag
          MOVE      SP8,STDATE                * save transfer date
          MOVE      " ",STMOVE                * save transfer movement
          MOVE      SP3,STATYPE               * save transfer admission type
          MOVE      SP6,STADOCT               * save transfer doctor
          MOVE      SP6,STWARD                * save transfer ward
.
          CALL      TRMS0000                  * read through bed transfer file
.
UMST9999  RETURN
+
.**********************************************************************
.*                         TRMS0000                                   *
.*                   Read through the PATTRNFD file                   *
.**********************************************************************
TRMS0000  MOVE      ZERO,DAYCASE              * daycase flag
          MOVE      ZERO,LEVFLG               * on leave flag
          MOVE      ZERO,BRDFLG               * boarder flag 
          MOVE      ZERO,CHGWRD               * change ward flag
.
          BRANCH    ASTATFLG,TRMS0500         * patient discharged ?
.
          MATCH     ADATE,DDATE               * patient a daycase ?
          GOTO      TRMS0500 IF NOT EQUAL     * no 
.
          MATCH     FADMDTE,ADATE             * patient in period ?
          GOTO      TRMS0500 IF LESS          * no patient isn't
          MOVE      ONE,DAYCASE               * patient is a daycase
.
TRMS0500  PACK      SAVCCTR,SP70 
          MOVE      SP1,STMOVE
          MOVE      SP3,STWARD
.
.         Loop through the bed transfer file
.
          PACK      KEY30,XADMN,SP20
          CALL      RDSTRAN2
TRMS1000  CALL      RDKTRAN2
          BRANCH    OVRCD,TRMS8000
.
          MATCH     XADMN,TADMN               * same admission number ?
          GOTO      TRMS8000 IF NOT EQUAL
.
          MOVE      TMOVE,STMOVE
          RESET     TDATE
          REP       " 0",TDATE
.
          MATCH     ANSA,TMOVE                * current admission record
          GOTO      TRMS1500 IF EQUAL
          MATCH     ANSR,TMOVE                * returned from leave record
          GOTO      TRMS2000 IF EQUAL
          MATCH     ANSL,TMOVE                * onleave record
          GOTO      TRMS5000 IF EQUAL
          MATCH     ANSD,TMOVE                * if not discharge record it must 
          GOTO      TRMS6000 IF NOT EQUAL     * be a change record
.
.         Discharge record
.
          MATCH     FADMDTE,TDATE
          GOTO      TRMS9999 IF LESS
          MATCH     TTODATE,TDATE 
          GOTO      TRMS1200 IF LESS
          MOVE      TTODATE,TDATE            * ttodate = end date of period + 1
          GOTO      TRMS8500
.
TRMS1200  CALL      CDIS0000                 * check discharge record
          GOTO      TRMS8500
.
.         Admission record
.
TRMS1500  CALL      CADM0000                 * write/update the admission record
          GOTO      TRMS2500
.
.         Return record
.
TRMS2000  MOVE      ONE,LEVFLG                * patient is on leave
.
TRMS2500  MATCH     TTODATE,TDATE             * ttodate = end date of period + 1
          GOTO      TRMS4500 IF NOT LESS
.
          MATCH     FADMDTE,TDATE
          GOTO      TRMS3000 IF NOT LESS
.
          MOVE      FADMDTE,STDATE
          MOVE      ZERO,LEVFLG               * patient not on leave
          GOTO      TRMS7500
.
TRMS3000  COMPARE   ZERO,LEVFLG               * patient on leave ?
          GOTO      TRMS4000 IF EQUAL
.
          MATCH     TWARD,STWARD              * match this ward to previous 
          GOTO      TRMS3100 IF EQUAL           ward
.
          MOVE      ONE,CHGWRD                * ward changed
.
TRMS3100  MATCH     FADMDTE,STDATE
          GOTO      TRMS3500 IF LESS
.
.         Calculate the number of bed days while patient was on leave
.
          MOVE      STDATE,WDATE2
          MOVE      TDATE,WDATE1
          CALL      UPTM0000                 * update the tempfile file
          MOVE      ZERO,LEVFLG
          GOTO      TRMS4000
.
.         Patient started on leave in the previous period
.
TRMS3500  MOVE      FADMDTE,WDATE2
          MOVE      TDATE,WDATE1
          CALL      UPTM0000                 * update the tempfile file
          MOVE      ZERO,LEVFLG
.
TRMS4000  MOVE      TDATE,STDATE
          GOTO      TRMS7500
.
TRMS4500  COMPARE   ZERO,LEVFLG               * patient on leave ?
          GOTO      TRMS9999 IF EQUAL
.
.         Get bed days of on leave patient until end of the period
.
          MOVE      TTODATE,WDATE1           * ttodate = end date of period + 1
          MOVE      STDATE,WDATE2
          CALL      UPTM0000                 * update the tempfile file
          MOVE      ZERO,LEVFLG              * patient not on leave
          GOTO      TRMS9999
.
.         On leave record
.
TRMS5000  MATCH     FADMDTE,TDATE             * within the period
          GOTO      TRMS7500 IF LESS
.
          MATCH     TTODATE,TDATE
          GOTO      TRMS5500 IF LESS
.
          MOVE      TTODATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      UPTM0000                 * update the tempfile file
          GOTO      TRMS9999
.
TRMS5500  MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      UPTM0000                 * update the tempfile file
          MOVE      TDATE,STDATE
          GOTO      TRMS7500
.
.         Change record
.
TRMS6000  MATCH     TWARD,STWARD
          GOTO      TRMS1000 IF EQUAL
.
          MATCH     FADMDTE,TDATE             * within the period
          GOTO      TRMS6500 IF LESS
.
          MATCH     TTODATE,TDATE
          GOTO      TRMS6500 IF NOT LESS
.
.         Ward is changed during the month
.
          MOVE      ONE,CHGWRD                * ward changed
.
TRMS6500  MATCH     FADMDTE,TDATE             * within the period
          GOTO      TRMS7500 IF LESS
.
          MATCH     TTODATE,TDATE
          GOTO      TRMS7000 IF LESS
.
          MOVE      TTODATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      UPTM0000                 * update the tempfile file
          GOTO      TRMS9999
.
TRMS7000  MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      UPTM0000                 * update the tempfile file
.
          MOVE      TDATE,STDATE
.
.         Save the ward and the ward class
.
TRMS7500  PACK      KEY6,TWARD,SP3
          CALL      RDWARD1
.
          MOVE      TWARD,STWARD            
          MOVE      PTWDGLDG,SAVCCTR           
          MOVE      WBDESC,SAVWDES            * ward description
          GOTO      TRMS1000                  * read the transfer file
.
.         Check the last record
.
TRMS8000  MOVE      STWARD,TWARD              * ward
          MOVE      SAVCCTR,PTWDGLDG          * ward cost centre
          MOVE      SAVWDES,WBDESC
.
TRMS8500  MATCH     ANSL,STMOVE               * on leave record ?
          GOTO      TRMS9000 IF NOT EQUAL
.
          MOVE      ONE,LEVFLG                * patient is on leave
          MOVE      TTODATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      UPTM0000                  * update the tempfile file
          MOVE      ZERO,LEVFLG               * patient not on leave
          GOTO      TRMS9999
.
TRMS9000  MATCH     SP1,STMOVE
          GOTO      TRMS9999 IF EQUAL
.
.         Last record
.
          MATCH     ANSD,STMOVE               * discharge record ?
          GOTO      TRMS9500 IF NOT EQUAL
.
.         Patient is discharged
.
          MOVE      TDATE,WDATE1
          GOTO      TRMS9700
.
TRMS9500  MOVE      TTODATE,WDATE1
.
TRMS9700  MOVE      STDATE,WDATE2
          CALL      UPTM0000                  * update the tempfile file
.
TRMS9999  RETURN
+
.**********************************************************************
.*                         CADM0000                                   *
.*                   Check the first transfer record                  *
.**********************************************************************
CADM0000  MATCH     FADMDTE,ADATE             * admitted within the period
          GOTO      CADM9999 IF LESS
.
          MATCH     ADATE,TTODATE
          GOTO      CADM9999 IF LESS
.
          PACK      KEY6,TWARD,SP3
          CALL      RDWARD1
.
          MOVE      TWARD,STWARD
          MOVE      PTWDGLDG,SAVCCTR 
          MOVE      WBDESC,SAVWDES            * ward description
          MOVE      ZERO,UPORWR
.
          CALL      CFLD0000                  * clear fields for tempfile
          MOVE      STWARD,TMPWARD            * ward code
          MOVE      SAVWDES,TMPWDES           * ward description
          MOVE      SAVCCTR,TMPCCTR           * ward cost centre
.
          PACK      KEY17,SAVCCTR,STWARD
          CALL      RDTMP2
          BRANCH    OVRCD,CADM1000
          MOVE      ONE,UPORWR
          GOTO      CADM1000
.
.         Check if patient type is public or private
.
CADM1000  MOVE      ONE,PUBFLG                * public/private flag
          MOVE      ZERO,FORM1
          MOVE      ZERO,BRDFLG               * boarder flag
.
          MATCH     SP3,TATYPE
          GOTO      CADM3000 IF EQUAL
.
          PACK      KEY5,CODEA,TATYPE
          CALL      RDCODE1
          BRANCH    OVRCD,CADM3000
.
          MOVE      TCINDC1,FORM1
          BRANCH    FORM1,CADM3000,CADM2000
          GOTO      CADM3000
.
CADM2000  MOVE      ZERO,PUBFLG               * patient is private
.
.         Check if boarder or not
.
CADM3000  MOVE      ZERO,BRDFLG
          MATCH     SP3,ACLSS
          GOTO      CADM5000 IF EQUAL
.
          MOVE      ZERO,FORM1
          PACK      KEY5,CODEP,ACLSS
          CALL      RDCODE1
          BRANCH    OVRCD,CADM5000
.
          MOVE      TCINDC1,FORM1
          COMPARE   THREE,FORM1               * patient a boarder ?
          GOTO      CADM5000 IF NOT EQUAL     * no
.
          MOVE      ONE,BRDFLG                * patient is a boarder
.
          BRANCH    PUBFLG,CADM4000           * public/private type
          ADD       ONE,TMPBPRAD              * private boarder
          GOTO      CADM7000
.
CADM4000  ADD       ONE,TMPBPBAD              * public boarder
          GOTO      CADM7000
.
CADM5000  BRANCH    PUBFLG,CADM6000           * public/private type
          ADD       ONE,TMPNPRAD              * private admission
          GOTO      CADM7000
.
CADM6000  ADD       ONE,TMPNPBAD              * public admission
.
.         Check if the patient was admitted from A & E
.
CADM7000  PACK      KEY5,CODES,ASRCE
          CALL      RDCODE1
          BRANCH    OVRCD,CADM8000
.
          CMATCH    ANSA,TCINDC1              * from A & E ?
          GOTO      CADM8000 IF NOT EQUAL     * no
.
          ADD       ONE,TMPAEADM              * a & e patient
.
.         Check if the patient admitted in the selected period
.
CADM8000  BRANCH    UPORWR,CADM8100
          CALL      WRTMP2
          GOTO      CADM9999
.
CADM8100  CALL      UPTMP2
.
CADM9999  RETURN
+
.**********************************************************************
.*                         CDIS0000                                   *
.*                   Check the discharge record                       * 
.**********************************************************************
.
CDIS0000  MOVE      SP3,WCLASS                * ward class
          PACK      KEY6,TWARD,SP3
          CALL      RDWARD1
.
          MOVE      ZERO,UPORWR
          CALL      CFLD0000                  * clear variables
          MOVE      WWARD,TMPWARD             * ward code
          MOVE      WBDESC,TMPWDES            * ward description
          MOVE      PTWDGLDG,TMPCCTR          * ward cost centre
          PACK      KEY17,TMPCCTR,TMPWARD
          CALL      RDTMP2
          BRANCH    OVRCD,CDIS1000
          MOVE      ONE,UPORWR
.
CDIS1000  ADD       ONE,TMPDSCH               * discharge
.
.         Check if patient is dead or not
.
          BRANCH    PATDEAD,CDIS2000          * patient dead ?
.
          ADD       ONE,TMPDEATH               * dead
.
CDIS2000  BRANCH    UPORWR,CDIS8000
          CALL      WRTMP2
          GOTO      CDIS9000
.
CDIS8000  CALL      UPTMP2
.
CDIS9000  MOVE      ZERO,CHGWRD               * ward didn't change
          MOVE      ZERO,DSHFLG               * patient not discharged
          RETURN
+
.**********************************************************************
.*                         UPTM0000                                   *
.*                   Update the TEMPFILE file b                       *
.**********************************************************************
UPTM0000  CALL      CFLD0000                  * clear variables
          MOVE      STWARD,TMPWARD            * ward code
          MOVE      SAVWDES,TMPWDES           * ward description
          MOVE      SAVCCTR,TMPCCTR           * ward cost centre
          MOVE      ZERO,UPORWR
.
          PACK      KEY17,SAVCCTR,STWARD
          CALL      RDTMP2
          BRANCH    OVRCD,UPTM0500
          MOVE      ONE,UPORWR
.
UPTM0500  MOVE      ZERO,NBRDAYS
          MOVE      ZERO,DSHFLG               * patient not discharged
          COMPARE   ZERO,DAYCASE              * patient daycase ?
          GOTO      UPTM1000 IF EQUAL
.
.         A daycase only has one bedday. If this is not the last trans 
.         record, no need to update the beddays only update the 
.         transaction of ward if necessary
.
          CMATCH    ANSD,STMOVE               * patient discharged ?
          GOTO      UPTM3500 IF NOT EQUAL
.
.         The following code can be used to display each admission, the ward
.         and the number of bed days to debug problems with bed days
.
.         DISPLAY   *P1:24,*EL,"Admiss: [",XADMN,"] Ward: [",TMPWARD,"] Bed ":
.                   "Days: 1";
.         CALL      HITENTER
.
          MOVE      ONE,DSHFLG                * patient is discharged 
          MOVE      ONE,NBRDAYS               * one bed day for daycase patients
          GOTO      UPTM2000
.
.         Calculate the number of beddays for non daycase patients
.
UPTM1000  MATCH     TTODATE,WDATE1
          GOTO      UPTM1500 IF LESS
.
.         Use the end of the period date plus one as the WDATE1
.
          MOVE      TTODATE,WDATE1            * ttodate = end date of period + 1
.
UPTM1500  DAYSEP    WDATE2,WDATE1,NBRDAYS
.
.         The following code can be used to display each admission, the ward
.         and the number of bed days to debug problems with bed days
.
.         IF        NBRDAYS <> 0
.           DISPLAY   *P1:24,*EL,"Admiss: [",XADMN,"] Ward: [",TMPWARD,"] Bed ":
.                     "Days: [",NBRDAYS,"] ";
.           CALL      HITENTER
.         ENDIF
.
          BRANCH    LEVFLG,UPTM3000           * patient on leave ?
.
UPTM2000  BRANCH    BRDFLG,UPTM2500           * patient a boarder ? 
          ADD       NBRDAYS,TMPNBDAY          * Beddays for not boarders
          GOTO      UPTM3500
.
UPTM2500  ADD       NBRDAYS,TMPBBDAY          * Beddays for boarders
          GOTO      UPTM3500
.
UPTM3000  ADD       NBRDAYS,TMPLVBD           * Beddays for patients on leave
.
UPTM3500  COMPARE   ZERO,CHGWRD               * ward change during period ?
          GOTO      UPTM4000 IF EQUAL
.
          ADD       ONE,TMPROUT               * transfer out
.
UPTM4000  BRANCH    UPORWR,UPTM4200
          CALL      WRTMP2
          GOTO      UPTM4500
.
UPTM4200  CALL      UPTMP2
.
.         Check if the ward has changed
.
UPTM4500  COMPARE   ZERO,CHGWRD
          GOTO      UPTM9999 IF EQUAL
.
.         Create/update the file using the current ward
.
          PACK      KEY6,TWARD,SP3
          CALL      RDWARD1
.
          CALL      CFLD0000                  * clear variables
          MOVE      WBDESC,TMPWDES            * ward description
          PACK      KEY17,PTWDGLDG,WWARD
          CALL      RDTMP2
          BRANCH    OVRCD,UPTM5500
          ADD       ONE,TMPRIN                * transfer in
          CALL      UPTMP2
          GOTO      UPTM8000
.
UPTM5500  MOVE      ONE,TMPRIN
          CALL      WRTMP2
.
UPTM8000  MOVE      ZERO,CHGWRD               * ward didn't change flag
          MOVE      ZERO,DSHFLG               * patient not discharged
.
UPTM9999  RETURN
+
.**********************************************************************
.*                         UCST0000                                   *
.*               Processing no of patients for tempfile c             *
.**********************************************************************
UCST0000  DISPLAY   *P1:24,"Processing :";
.
.         Find the catchment code using the postcode from the master file.
.         If the postcode is not on file leave cstcode as spaces
.
          MOVE      SP3,CTCCODE               * catchment code
          MOVE      PPOST,KEY4
          CALL      RDCATC1
.
          PACK      KEY3,CTCCODE,SP3
          CALL      RDTMP3
          BRANCH    OVRCD,UCST1200
          MOVE      ONE,UPORWR                * already exists
          GOTO      UCST1500
.
UCST1200  MOVE      ZERO,XCADMN               * number of admissions
          MOVE      ZERO,XCBOARD              * number of boarders
          MOVE      ZERO,UPORWR
.
.         Determine the type of the patient
.
UCST1500  PACK      KEY5,CODEP,ACLSS
          CALL      RDCODE1
          BRANCH    OVRCD,UCST2000
.
          MOVE      ZERO,FORM1
          MOVE      TCINDC1,FORM1
.
          BRANCH    FORM1,UCST2000,UCST3000,UCST4000
.
.         Patient is a normal admission
.
UCST2000  MATCH     FADMDTE,ADATE             * admitted in period ?
          GOTO      UCST5000 IF LESS
.
          MATCH     ADATE,TADMDTE
          GOTO      UCST5000 IF LESS
.
          ADD       ONE,XCADMN                * normal admission
          GOTO      UCST5000
.
.         Patient is a sameday admission
.
UCST3000  MATCH     FADMDTE,ADATE             * admitted in period ?
          GOTO      UCST5000 IF LESS
.
          MATCH     ADATE,TADMDTE
          GOTO      UCST5000 IF LESS
.
          ADD       ONE,XCADMN               * sameday admission
          GOTO      UCST5000
.
.         Patient is a boarder
.
UCST4000  MATCH     FADMDTE,ADATE             * admitted in period ?
          GOTO      UCST5000 IF LESS
.
          MATCH     ADATE,TADMDTE
          GOTO      UCST5000 IF LESS
.
          ADD       ONE,XCBOARD               * boarder admission
.
.         Write or update to the tempfile c
.
UCST5000  BRANCH    UPORWR,UCST6000           * Already exists
          CALL      WRTMP3                    * Write a new record
          GOTO      UCST9999
.
UCST6000  CALL      UPTMP3                    * Update record already exist
UCST9999  RETURN
+
.**********************************************************************
.                  OPWR0000                                           *
.         Processing  operating room wards statistics                 *
.**********************************************************************
OPWR0000  DISPLAY   *P1:24,*EL,*V2LON,"Processing Operation : ";
          PACK      KEY22,FADMDTE,SP70
          CALL      RSOPSES7
.
OPWR1000  CALL      RKOPSES7
          BRANCH    OVRCD,OPWR9000
.
          MATCH     OPSEDATE,TADMDTE
          GOTO      OPWR9000 IF LESS
.
          UNPACK    OPSEDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P26:24,*V2LON,CPCDATE,*HOFF,SP1,OPSECLIN;
.
          CALL      RDDA0000                    * read dec file
          GOTO      OPWR1000
.
OPWR9000  RETURN
+
.**********************************************************************
.                   RDDA0000                                          *
.         Read the operation detail file for session                  *
.**********************************************************************
RDDA0000  PACK      KEY26,OPSEHOSP,OPSEDATE,OPSETIME,OPSECLIN,SP70
          CALL      RSOPDEA1
RDDA1000  CALL      RKOPDEA1                     * read dea
          BRANCH    OVRCD,RDDA9999
.
          MATCH     OPSEHOSP,OPDAHOSP            * right hospital?
          GOTO      RDDA9999 IF NOT EQUAL
.
          MATCH     OPSEDATE,OPDADATE            * right date?
          GOTO      RDDA9999 IF NOT EQUAL
.
          MATCH     OPSETIME,OPDATIME            * right time?
          GOTO      RDDA9999 IF NOT EQUAL
.
          MATCH     OPSECLIN,OPDACLIN            * right clinic?
          GOTO      RDDA9999 IF NOT EQUAL
.
          COMPARE   THREE,OPDASTAT               * cancelled ?
          GOTO      RDDA1000 IF EQUAL            * yes, ignore.
.
          CALL      RDDC0000                     * read usage file
          GOTO      RDDA1000
.
RDDA9999  RETURN
+
.*********************************************************************
.*                  RDDC0000                    called by : RDDA0000 *
.*        Read the Operation Usage File                              *
.*********************************************************************
RDDC0000  PACK      KEY10,OPDAUNIQ,SP70
          CALL      RDOPARD1
          BRANCH    OVRCD,RDDC9999
.
          CALL      PWAR0000                * update war file
.
RDDC9999  RETURN
+
.************************************************************************
.*                           RDDB0000                                   *
.*          Read the Operating Room Team file to update                 *
.************************************************************************
RDDB0000  MOVE      OPDCUNIQ,OPDBUNIQ
          MOVE      OPDCTEAM,OPDBTEAM
.
          PACK      KEY11,OPDBUNIQ,OPDBTEAM
          CALL      RDOPDEB1
          COMPARE   ZERO,OVRCD
          GOTO      RDDB9000 IF EQUAL       * has a DEB record
.
          MATCH     "1",OPDCTEAM
          GOTO      RDDB9100 IF NOT EQUAL   * not first team so invalid
.
RDDB9000  MOVE      FALSE,EXIT              * has a DEB record
          GOTO      RDDB9999
.
RDDB9100  MOVE      TRUE,EXIT               * no DEB record
.
RDDB9999  RETURN
+
.************************************************************************
.*                           PWAR0000                                   *
.*            Update Operating Room Ward Statistics File                *
.************************************************************************
PWAR0000  MOVE     SP3,SAVWARD
          CALL     GTWA0000                    * get ward code
.
          PACK     KEY6,SAVWARD,SP3            * read ward/bed file
          CALL     RDWARD1                     * to get the cost centre
          BRANCH   OVRCD,PWAR9999

          PACK     KEY17,PTWDGLDG,SAVWARD
          CALL     RDTMP2
          BRANCH   OVRCD,PWAR9999              * Skip if we don't have a record
.
          ADD      ONE,TMPWCAS                 * add one to number of
.                                                operations
          CALL     UPTMP2
.
PWAR9999  RETURN
+
.************************************************************************
.*                           GTWA0000                                   *
.*                 Get ward code from transfer file                     *
.************************************************************************
GTWA0000  MOVE      SP3,SAVWARD
.
          MOVE      OPDAADMN,DIM8VISN
          PACK      KEY30,OPDAADMN,OPDADATE,OPDATIME,Z20 
          CALL      RDSTRAN2
          CALL      RDPTRAN2                     * get last ward code
          BRANCH    OVRCD,GTWA8000
.
          MATCH     DIM8VISN,TADMN               * same patient ?
          GOTO      GTWA8000 IF NOT EQUAL        * no
.
          MOVE      TWARD,SAVWARD                * we have a ward
          GOTO      GTWA9999
.
GTWA8000  CALL      RDKTRAN2                     * now try reading forward
          BRANCH    OVRCD,GTWA9999
.
          MATCH     DIM8VISN,TADMN               * same patient ?
          GOTO      GTWA9999 IF NOT EQUAL        * no
.
          MOVE      TWARD,SAVWARD                * we have a ward
.
GTWA9999  RETURN
+
.**********************************************************************
.                   PBED0000
.         Processing no of bed for each wards
.         NOTE: this will only get the ward details from patwardf 
.               i.e. WBED will always be blank ( I think so anyway)
.**********************************************************************
PBED0000  DISPLAY   *P1:24,*EL,"Processing Ward: ",*P50:24,"Scanning: ";
          CALL      INITGT                       * Initialise grand totals var.
          MOVE      SP10,KEY6
          CALL      RDSWARD1
PBED1000  CALL      RDKWARD1
          BRANCH    OVRCD,PBED9999
.
          DISPLAY   *P62:24,WWARD;
          MOVE      ZERO,UPORWR
.
          MATCH     SP3,WBED
          GOTO      PBED9000 IF NOT EQUAL        * ignore the bed records
.
          CALL      CFLD0000                     * clear fields for tempfile
          MOVE      WBDESC,TMPWDES               * ward description
          PACK      KEY17,PTWDGLDG,WWARD
          MOVE      WWARD,TMPWARD
          CALL      RDTMP2
          COMPARE   ZERO,OVRCD
          GOTO      PBED4000 IF EQUAL           * continue processing no of beds
.
          BRANCH    PTCNH82W,PBED5000            * to print no statistics wards
          GOTO      PBED9000                  * not to print wards without stats
.
PBED4000  MOVE      ONE,UPORWR
.
PBED5000  DISPLAY   *P20:24,*V2LON,TMPWARD,*HOFF;
.
.         Get the number of days the ward was active and if it was active,
.         get the number of beds in the ward.
.
          MOVE      ZERO,TMPASTRX
          PACK      ACTVFDTE,FADMDTE
          PACK      ACTVTDTE,TADMDTE
          MOVE      ONE,ACTVWFLG              * don't want weekend days
          PROC      WDACTVDY                  * get days ward was active
          COMPARE   ZERO,ACTVDAYS             * ward active during period ?
          GOTO      PBED9000 IF EQUAL         * no - ignore
          IF        ACTVBCHG = 1
            MOVE      ONE,TMPASTRX
          ENDIF
          ADD       TOTBED,TMPBEDS            * update total
          ADD       ACTVDAYS,TMPDAYS          * update active days
          ADD       ACTVBDAY,TMPBDAYS         * update active bed days
          ADD       ACTVBDAY,GTBEDAY          * update grand total
.
          BRANCH    UPORWR,PBED8000
          PACK      KEY17,PTWDGLDG,WWARD
          CALL      WRTMP2
          GOTO      PBED9000
.
PBED8000  CALL      UPTMP2
.
PBED9000  PACK      KEY6,WWARD,ZEIGHT
          CALL      RDSWARD1
          GOTO      PBED1000
PBED9999  RETURN
+
.**********************************************************************
.                 CFLD0000
.         Clear variables for tempfile b 
.**********************************************************************
CFLD0000  MOVE      SP20,TMPWDES
          MOVE      ZERO,TMPBEDS
          MOVE      ZERO,TMPNPRAD
          MOVE      ZERO,TMPNPBAD
          MOVE      ZERO,TMPRIN
          MOVE      ZERO,TMPROUT
          MOVE      ZERO,TMPDSCH
          MOVE      ZERO,TMPNBDAY
          MOVE      ZERO,TMPBBDAY
          MOVE      ZERO,TMPDEATH
          MOVE      ZERO,TMPBPRAD
          MOVE      ZERO,TMPBPBAD
          MOVE      ZERO,TMPLVBD
          MOVE      ZERO,TMPAEADM
          MOVE      ZERO,TMPWCAS
          MOVE      ZERO,TMPDAYS
          MOVE      ZERO,TMPBDAYS
.
          MOVE      ZERO,AVDAY
          MOVE      ZERO,PCENTG
          MOVE      ZERO,AVSTY
.
          RETURN
+
.**********************************************************************
.                 PRNT0000
.         Print records from the tempfile
.**********************************************************************
PRNT0000  DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Printing Report",*HOFF:
                    *V2LON," **",*HOFF;
.
          CALL      PTOP0000              * print the top of report
.
          MOVE      ZERO,GTPCENT
.
          COMPARE   ZERO,GTBEDAY
          GOTO      PRNT2000 IF EQUAL
.
          COMPARE   ZERO,GTTBED
          GOTO      PRNT2000 IF EQUAL
.
          ASSIGN    ((GTBDAY*100)/GTBEDAY),GTPCENT
.
PRNT2000  MOVE      GTDSH,FORM8
          BRANCH    PTCNALOS,PRNT2100        * divided by Discharges only
          ADD       GTROUT,FORM8
.
PRNT2100  COMPARE   ZERO,FORM8
          GOTO      PRNT3000 IF EQUAL
.
          MOVE      GTBDAY,GTAVST
          DIV       FORM8,GTAVST          * Hospital Average Length of Stay
.
PRNT3000  ADD       GTAAE,GTTHEA
          MOVE      GTTBED,FORM4
.
.         Print the End of Report
.
          PRINT     *1,"| Hospital Total ",*27,"|",FORM4;
          IF        GTASTRX = 1
            PRINT     "*";
          ENDIF
          PRINT    *33,"|",GTADM:
                    *44,"|",GTRIN,*55,"|",GTDSH,*66,"|",GTROUT:
                    *77,"|",GTAVDAY,*88,"|",GTPCENT,"%",*99,"|",GTAVST:
                    *110,"|",GTBDAY,*121,"|",GTWAN,*132,"|"
          ADD       ONE,CLNO
.
          COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          CALL      UND132
.
          PRINT     *N,*114,"A & E = ",GTAAE:
                    *N,*1,"Deaths = ",GTDEATH:
                    *114,"Total = ",GTTHEA
          ADD       THREE,CLNO
.
          CALL      UND132
.
.         Add Private Boarders to Public. As Private boarder is always zero
.
          ADD       GTBPRAD,GTBPBAD
          MOVE      ZERO,GTBPRAD
.
          MOVE      GTNPBAD,TOTPB      * total public patients
          ADD       GTBPBAD,TOTPB
.
          MOVE      GTNPRAD,TOTPR      * total private patients
          ADD       GTBPRAD,TOTPR
.
          COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          PRINT     *68,"Patients",*81,"Boarders",*94,"   Total":
                    *N,*1,"Total Hospital Patients admitted :":
                    *68,GTNPBAD,*81,GTBPBAD,*94,TOTPB:
                    *N,*1,"Total Private  Patients admitted :":
                    *68,GTNPRAD,*81,GTBPRAD,*94,TOTPR
          ADD       THREE,CLNO
.
          COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
.         Print total inpatients based on catchment code
.
          CALL      UND132
          PRINT     *N,*1,"Total Inpatients admitted from   :";
          ADD       ONE,CLNO
.
          CALL      PRCS0000                    * Print total inpatients
.
          COMPARE   ZERO TO CPAGENO
          GOTO      PRNT8000 IF NOT EQUAL
.
          PRINT     *N,"***  End of Report  ***"
          DISPLAY   *P1:24,*EL,*P26:24,*V2LON,*B:
                    "***  No Report Generated  ***",*W2;
          GOTO      PRNT9999
.
PRNT8000  COMPARE   "47",CLNO
          GOTO      PRNT8100 IF LESS
          CALL      HEAD
          GOTO      PRNT8200
.
PRNT8100  CALL      UND132
.
PRNT8200  PRINT     *N,*N,*N:
                 *1,"The Daily Average    =          Occupied Bed Days":
                *64,"The Average Percentage Occupancy = Daily Average",*N:
                 *1,"                       ----------------------------------":
                    "---":
                *64,"                                   -------------- * 100":
              *N,*1,"                       No. of Days Ward was Active ":
                    "in Period":
                *64,"                                   Number of Beds",*N,*N:
                 *1,"The Average Length of Stay = ":
                    "     Occupied Bed Days":
                *64,"Hospital Average Length of Stay  = ";
.
          BRANCH    PTCNALOS,PRNT8500
          PRINT      "     Total Occupied Bed Days",*N:
                  *1,"                             ":
                     "----------------------------":
                 *64,"                               ":
                     "    ----------------------------------",*N:
                  *1,"                             ":
                     "Discharges and Transfers Out":
                 *64,"                               ":
                     "    Total Discharges and Transfers Out",*N
          GOTO      PRNT8600
.
PRNT8500  PRINT      "Total Occupied Bed Days",*N:
                  *1,"                             ":
                     "----------------------------":
                 *64,"                               ":
                     "    -----------------------",*N:
                  *1,"                             ":
                     "Discharges and Transfers Out":
                 *64,"                               ":
                     "       Total Discharges",*N
.
PRNT8600  IF        GTASTRX = 1
            PRINT     *1,"* - Some wards changed number of `Beds' during ":
                      "period.",*N
          ENDIF
.
          PRINT     "***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL,*P22:24,*V2LON:
                    "***  Report Generation Completed  ***",*W2;
.
PRNT9999  CALL      KILA0000                  * remove temporary file a
          CALL      KILB0000                  * remove temporary file b
          CALL      KILC0000                  * remove temporary file c
          RETURN
+
.**********************************************************************
.            PTOP0000
.         Print the top of the report 
.**********************************************************************
PTOP0000  MOVE      "55",CLNO
          MOVE      ZERO,CPAGENO
          PACK      SAVCCTR,SP70
          MOVE      SP3,SAVWRD
          MOVE      ONE,FORM1
          DISPLAY   *P55:24,"Ward :";
.
          PACK      KEY17,SP70
          CALL      RDSTMP2
PTOP1000  CALL      RDKTMP2
          BRANCH    OVRCD,PTOP9000
.
          COMPARE   ZERO,TMPDAYS                 * ward active during period ?
          GOTO      PTOP1000 IF EQUAL            * no - ignore
.
          DISPLAY   *P72:24,*V2LON,TMPWARD;
.
          MOVE      TMPBBDAY,TOTBDAY
          ADD       TMPNBDAY,TOTBDAY            * Total number of bed days
.
          MOVE      TMPNPRAD,TOTPAT
          ADD       TMPNPBAD,TOTPAT
          ADD       TMPBPRAD,TOTPAT
          ADD       TMPBPBAD,TOTPAT            * Total number of patients
.
          MOVE      TOTBDAY,AVDAY
          DIV       TMPDAYS,AVDAY              * Daily Average
.
          MOVE      ZERO,PCENTG
.
          COMPARE   ZERO,TMPBDAYS
          GOTO      PTOP1100 IF EQUAL
.
          COMPARE   ZERO,TMPBEDS
          GOTO      PTOP1100 IF EQUAL
.
          ASSIGN    ((TOTBDAY*100)/TMPBDAYS),PCENTG
.
PTOP1100  MOVE      TMPDSCH,FORM8
          BRANCH    PTCNALOS,PTOP1200           * divided by Discharges only
          ADD       TMPROUT,FORM8
.
PTOP1200  MOVE      ZERO,AVSTY
          COMPARE   ZERO,FORM8
          GOTO      PTOP4000 IF EQUAL
.
          MOVE      TOTBDAY,AVSTY
          DIV       FORM8,AVSTY                 * Average Length of Stay
.
PTOP4000  BRANCH    FORM1,PTOP5000              * First time round
          COMPARE   "55",CLNO
          GOTO      PTOP6000 IF LESS
.
          MATCH     TMPCCTR,SAVCCTR             * same cost centre code ?
          GOTO      PTOP5000 IF EQUAL
          CALL      SUBT0000                    * print subtotal
.
PTOP5000  MOVE      ZERO,FORM1
          CALL      HEAD
          PACK      KEY5,CODECW,TMPCCTR         * A new cost centre code
          CALL      RDCODE1
          IF        OVRCD = 1
            MOVE      SP70,TDESC
          ENDIF
          PRINT     *1,"|",TDESC,*27,"|",*33,"|",*44,"|",*55,"|",*66,"|":
                    *77,"|",*88,"|",*99,"|",*110,"|",*121,"|",*132,"|":
                    *N,*1,"|",TMPWDES," ",TMPWARD,*27,"|",TMPBEDS;
          IF        TMPASTRX = 1
            PRINT     "*";
            MOVE      ONE,SUBASTRX
          ENDIF
          ADD       ONE,CLNO
          GOTO      PTOP8000
.
PTOP6000  MATCH     TMPCCTR,SAVCCTR             * same cost centre code ?
          GOTO      PTOP7000 IF EQUAL
          CALL      SUBT0000                    * print subtotal
.
          PACK      KEY5,CODECW,TMPCCTR         * A new cost centre code
          CALL      RDCODE1
          PRINT     *1,"|",TDESC,*27,"|",*33,"|",*44,"|",*55,"|",*66,"|":
                    *77,"|",*88,"|",*99,"|",*110,"|",*121,"|",*132,"|"
          ADD       ONE,CLNO
.
PTOP7000  MATCH     TMPWARD,SAVWRD              * same ward ?
          GOTO      PTOP8000 IF EQUAL
          PRINT     *1,"|",TMPWDES," ",TMPWARD,*27,"|",TMPBEDS;
          IF        TMPASTRX = 1
            PRINT     "*";
            MOVE      ONE,SUBASTRX
          ENDIF
.
PTOP8000  PRINT     *33,"|",TOTPAT,*44,"|",TMPRIN,*55,"|",TMPDSCH,*66,"|":
                    TMPROUT,*77,"|",AVDAY,*88,"|",PCENTG,"%",*99,"|":
                    AVSTY,*110,"|",TOTBDAY,*121,"|  ",TMPWCAS,*132,"|"
          ADD       ONE,CLNO
.
          ADD       TMPBEDS,SUBBEDS             * subtotal of no of beds
          ADD       TOTPAT,SUBTPAT              * subtotal of no of patients
          ADD       TOTBDAY,SUBTBDAY            * subtotal of bed days
          ADD       AVDAY,SUBTADAY              * sub total of daily average
          ADD       TMPRIN,SUBTRIN              * subtotal of transfer in
          ADD       TMPDSCH,SUBDSCH             * subtotal of discharges
          ADD       TMPROUT,SUBTROUT            * subtotal of transfer out
          ADD       TMPWCAS,SUBWCAS             * subtotal of number of cases
          ADD       TMPBDAYS,SUBBEDY            * subtotal active bed days
.
.         Accumulate the grand total
.
          ADD       AVDAY,GTAVDAY
          ADD       TMPBEDS,GTTBED
          ADD       TMPWCAS,GTWAN
.
          ADD       TOTPAT,GTADM
          ADD       TMPRIN,GTRIN
          ADD       TMPDSCH,GTDSH
          ADD       TMPROUT,GTROUT
          ADD       TOTBDAY,GTBDAY
          ADD       TMPLVBD,GTLEAVE
          ADD       TMPAEADM,GTAAE
          ADD       TMPDEATH,GTDEATH
.
          ADD       TMPNPRAD,GTNPRAD
          ADD       TMPNPBAD,GTNPBAD
          ADD       TMPBPRAD,GTBPRAD
          ADD       TMPBPBAD,GTBPBAD
.
          MOVE      TMPCCTR,SAVCCTR
          MOVE      TMPWARD,SAVWRD
          GOTO      PTOP1000
.
PTOP9000  CALL      SUBT0000
          RETURN
+
.**********************************************************************
.        Print the subtotal
.**********************************************************************
SUBT0000  COMPARE   ZERO,SUBBEDS
          GOTO      SUBT0600 IF EQUAL
.
          COMPARE   ZERO,SUBBEDY
          GOTO      SUBT0600 IF EQUAL
.
          ASSIGN    ((SUBTBDAY*100)/SUBBEDY),GTPCENT
.
SUBT0600  MOVE      SUBDSCH,FORM8
          BRANCH    PTCNALOS,SUBT1000           * Divided by discharges only
          ADD       SUBTROUT,FORM8
.
SUBT1000  COMPARE   ZERO,FORM8
          GOTO      SUBT2000 IF EQUAL
          MOVE      SUBTBDAY,GTAVST
          DIV       FORM8,GTAVST              * Av.length of stay
.
SUBT2000  CALL      UND132
          PRINT     *1,"| Total",*27,"|",SUBBEDS;
          IF        SUBASTRX = 1
            PRINT     "*";
            MOVE      ONE,GTASTRX
          ENDIF
          PRINT     *33,"|",SUBTPAT,*44,"|",SUBTRIN,*55,"|",SUBDSCH,*66,"|":
                    SUBTROUT,*77,"|",SUBTADAY,*88,"|",GTPCENT,"%",*99,"|":
                    GTAVST,*110,"|",SUBTBDAY,*121,"|  ",SUBWCAS,*132,"|"
          ADD       ONE,CLNO
          CALL      UND132
          CALL      INITSUB                     * Initialise subtotals
          RETURN
+
.**********************************************************************
.            PRCS0000
.         Print the total inpatients
.**********************************************************************
PRCS0000  DISPLAY   *P55:24,"Catchment Code :";
          MOVE      ONE,FORM1
          CALL      RDTMP3
          COMPARE   ZERO,OVRCD
          GOTO      PRCS2000 IF EQUAL
.
PRCS1000  CALL      RDKTMP3
          BRANCH    OVRCD,PRCS9000
.
PRCS2000  DISPLAY   *P72:24,*V2LON,XCCODE;
          MOVE      "UNALLOCATED CODES",TDESC
.
          MATCH     SP3,XCCODE
          GOTO      PRCS3000 IF EQUAL
.
          PACK      KEY5 WITH CODECH,XCCODE
          CALL      RDCODE1
          COMPARE   ZERO,OVRCD
          GOTO      PRCS3000 IF EQUAL
          MOVE      "UNKNOWN CODES",TDESC
.
PRCS3000  MOVE      XCADMN,TOTCST
          ADD       XCBOARD,TOTCST
.
          BRANCH    FORM1,PRCS4000        * 1st time round print on same line as
.                                           line description
          PRINT     *N;
.
PRCS4000  PRINT     *36,TDESC,*68,XCADMN,*81,XCBOARD,*94,TOTCST
          ADD       ONE,CLNO
          MOVE      ZERO,FORM1
.
          COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
          GOTO      PRCS1000
.
PRCS9000  PRINT     *N;
          RETURN
+
.**********************************************************************
.        Initialise subtotals
.**********************************************************************
INITSUB   MOVE      ZERO,SUBBEDS
          MOVE      ZERO,SUBTPAT
          MOVE      ZERO,SUBTRIN
          MOVE      ZERO,SUBDSCH
          MOVE      ZERO,SUBTROUT
          MOVE      ZERO,SUBTADAY
          MOVE      ZERO,SUBTBDAY
          MOVE      ZERO,SUBWCAS
          MOVE      ZERO,SUBASTRX
.
          MOVE      ZERO,SUBBEDY
          MOVE      ZERO,GTPCENT
          MOVE      ZERO,GTAVST
          RETURN
+
.**********************************************************************
.         Headings routine
.**********************************************************************
HEAD      CALL      IBACLOCK
.
          CLOCK     TIME,CTIMEIS
.
          CALL      HEAD132
.
          UNPACK    FADMDTE,XCENT,XYEAR,XMON,XDAY
          PRINT     *1,"Inpatients":
                    *40,"From Date : ",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR;
.
          UNPACK    TADMDTE,XCENT,XYEAR,XMON,XDAY
          PRINT     *70,"To Date : ",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
          CALL      UND132
.
          PRINT     *1,"|",*27,"|     |",*41,"Arrivals",*55,"|":
                    *61,"Departures",*77,"|   Daily  | Average  |  Average":
                    *110,"| Occupied |Operation |":
                    *N,*1,"|   Ward",*27,"|Beds |---------------------|":
                    "---------------------|  Average |Percentage|   Length":
                    *110,"|   Bed",*121,"|  Main    |":
                    *N,*1,"|",*27,"|     |   Admit  | Trans In |   Disch  ":
                    "| Trans Out|",*88,"|Occupancy |  of Stay |   Days":
                    *121,"| Theatre  |"
          CALL      UND132
          MOVE      NINE,CLNO
          RETURN
+
.**********************************************************************
.         Initialize total line variables
.**********************************************************************
INITGT    MOVE      ZERO,GTADM
          MOVE      ZERO,GTRIN
          MOVE      ZERO,GTDSH
          MOVE      ZERO,GTROUT
          MOVE      ZERO,GTAVDAY
          MOVE      ZERO,GTPCENT
          MOVE      ZERO,GTAVST
          MOVE      ZERO,GTBDAY
          MOVE      ZERO,GTTHEA
          MOVE      ZERO,GTTBED
          MOVE      ZERO,GTLEAVE
          MOVE      ZERO,GTAAE
          MOVE      ZERO,GTDEATH
          MOVE      ZERO,GTNPRAD
          MOVE      ZERO,GTNPBAD
          MOVE      ZERO,GTBPRAD
          MOVE      ZERO,GTBPBAD
          MOVE      ZERO,GTWAN
          MOVE      ZERO,SUBASTRX
          MOVE      ZERO,SUBBEDY
          MOVE      ZERO,GTASTRX
          MOVE      ZERO,GTBEDAY
          MOVE      ZERO,GTPCENT
          RETURN     
+
.**********************************************************************
.*        CREA0000 - Create Temporary File A                          *
.**********************************************************************
CREA0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,PNPTMP1
.
          CALL      KILA0000
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    PNPTMP1,CMDLINE
          APPEND    " 9 u1-8",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      PNPTMPAA,PNPTMP1
          RETURN
+
.**********************************************************************
.*             KILA0000                                               *
.*         Remove Temporary File A                                    *
.**********************************************************************
KILA0000  CLOSE     PNPTMPAA
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    PNPTMP1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.**********************************************************************
.*                KILB0000                                            *
.*          Remove Temporary File B                                   *
.**********************************************************************
CREB0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,PNPTMP2
.
          CALL       KILB0000                    * Remove tempfile
          PACK       CMDLINE,SP30,SP30
          CLEAR      CMDLINE
          APPEND     "isbuild ",CMDLINE
          APPEND     PNPTMP2,CMDLINE
          APPEND     " 113 u1-14,15-17",CMDLINE
          RESET      CMDLINE
          EXECUTE    CMDLINE,TASKID
          OPEN       PNPTMPBB,PNPTMP2
          RETURN
+
.**********************************************************************
.*                         KILB0000                                   *
.*                  Remove Temporary File B                           *
.**********************************************************************
KILB0000  CLOSE     PNPTMPBB
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE           * remove the tempfile
          APPEND    PNPTMP2,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.**********************************************************************
.*                KILC0000                                            *
.*          Remove Temporary File C                                   *
.**********************************************************************
CREC0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,PNPTMP3
.
          CALL       KILC0000                    * Remove tempfile
          PACK       CMDLINE,SP30,SP30
          CLEAR      CMDLINE
          APPEND     "isbuild ",CMDLINE
          APPEND     PNPTMP3,CMDLINE
          APPEND     " 14 u1-3",CMDLINE
          RESET      CMDLINE
          EXECUTE    CMDLINE,TASKID
          OPEN       PNPTMPCC,PNPTMP3
          RETURN
+
.**********************************************************************
.*                         KILC0000                                   *
.*                  Remove Temporary File C                           *
.**********************************************************************
KILC0000  CLOSE     PNPTMPCC
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE           * remove the tempfile
          APPEND    PNPTMP3,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.**********************************************************************
.*              I/O For Temporary File                                *
.**********************************************************************
.
.         Temporary file 1
.
RDSTMP1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PNPTMPAA,KEY8;;
          RETURN
.
RDKTMP1   MOVE      ZERO,OVRCD
          READKS    PNPTMPAA;DXADMN
          MOVE      DXADMN,XADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTMP1    MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PNPTMPAA,KEY8;DXADMN
          MOVE      DXADMN,XADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMP1    MOVE      XADMN,DXADMN
          UPDATE    PNPTMPAA;DXADMN
          RETURN
.
WRTMP1    MOVE      XADMN,DXADMN
          RESET     KEY8
          WRITE     PNPTMPAA,KEY8;KEY8
          RETURN
.
.         Temporary file 2
.
RDKTMP2   MOVE      ZERO,OVRCD
          READKS    PNPTMPBB;TMPCCTR,TMPWARD,TMPWDES,TMPBEDS,TMPNPRAD,TMPNPBAD:
                             TMPRIN,TMPROUT,TMPDSCH,TMPNBDAY,TMPBBDAY,TMPDEATH:
                             TMPBPRAD,TMPBPBAD,TMPLVBD,TMPAEADM,TMPWCAS,TMPDAYS:
                             TMPBDAYS,TMPASTRX
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSTMP2   MOVE      ZERO,OVRCD
          RESET     KEY17
          READ      PNPTMPBB,KEY17;;
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTMP2    MOVE      ZERO,OVRCD
          RESET     KEY17
          READ      PNPTMPBB,KEY17;TMPCCTR,TMPWARD:
                             TMPWDES,TMPBEDS,TMPNPRAD,TMPNPBAD:
                             TMPRIN,TMPROUT,TMPDSCH,TMPNBDAY,TMPBBDAY,TMPDEATH:
                             TMPBPRAD,TMPBPBAD,TMPLVBD,TMPAEADM,TMPWCAS,TMPDAYS:
                             TMPBDAYS,TMPASTRX
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMP2    UPDATE    PNPTMPBB;TMPCCTR,TMPWARD:
                             TMPWDES,TMPBEDS,TMPNPRAD,TMPNPBAD:
                             TMPRIN,TMPROUT,TMPDSCH,TMPNBDAY,TMPBBDAY,TMPDEATH:
                             TMPBPRAD,TMPBPBAD,TMPLVBD,TMPAEADM,TMPWCAS,TMPDAYS:
                             TMPBDAYS,TMPASTRX
          RETURN
.
WRTMP2    RESET     KEY17
          WRITE     PNPTMPBB,KEY17;KEY17,TMPWDES,TMPBEDS,TMPNPRAD,TMPNPBAD:
                             TMPRIN,TMPROUT,TMPDSCH,TMPNBDAY,TMPBBDAY,TMPDEATH:
                             TMPBPRAD,TMPBPBAD,TMPLVBD,TMPAEADM,TMPWCAS,TMPDAYS:
                             TMPBDAYS,TMPASTRX
          RETURN
.
.         Temporary file 3
.
RDTMP3   MOVE      ZERO,OVRCD
         RESET     KEY3
         READ      PNPTMPCC,KEY3;XCCODE,XCADMN,XCBOARD
         GOTO      OVERCOND IF OVER
         RETURN  
.
RDKTMP3  MOVE      ZERO,OVRCD
         READKS    PNPTMPCC;XCCODE,XCADMN,XCBOARD
         GOTO      OVERCOND IF OVER
         RETURN  
.
WRTMP3   RESET     KEY3
         WRITE     PNPTMPCC,KEY3;KEY3,XCADMN,XCBOARD
         RETURN  
.
UPTMP3   UPDATE    PNPTMPCC;XCCODE,XCADMN,XCBOARD
         RETURN  
.
RDSTMP3  RESET     KEY3
         READ      PNPTMPCC,KEY3;;
         RETURN  
+
.==============================================================================
.
          INC       STD001IO
          INC       CALCDAYS
          INC       DATECONV
          INC       TFILENAM                     * Generate Temp File Name
          INC       WDACTVDY
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       OPRCLIIO/INC        * Clinic file
          INC       OPRDEAIO/INC        * Operation detail file
          INC       OPRDEBIO/INC
          INC       OPRDECIO/INC
          INC       OPRSESIO/INC
          INC       PATDAYIO/INC        * day file
          INC       PATCT1IO/INC       * catchment codes
          INC       PATCODIO/INC
          INC       PATDSCIO/INC        * Discharge file
          INC       PATMA1IO/INC        * Master file
          INC       PATMI1IO/INC        * Admission filei
          INC       PMSVX1IO/INC        * Admission filei
          INC       PATTRNIO/INC        * Transfer file
          INC       PATWR1IO/INC
          INC       PATWBHIO/INC
          INC       OPRARDIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging

.*****************************************************************************
.* System    :   Inpatient                                                   *
.* Program   :   EADSTXML                                                    *
.* Desc      :   Process an eAdmission change status XML File                *
.*****************************************************************************
.* Date      :   28/03/2013                                                  *
.* Author    :   Saroeun Soeur                                               *
.* Function  :   This program will open an eAdmission XML File and           *
.*               load the details into pmsehbaf and pmsedbaf                 *
.* Version   :   v10.03.00                                                   *
.*                                                                           *
.* V12.00.01     25/11/2025 Nikitha Bhaskar  TSK 0969892                     *
.*               Recompiled for PMSEDBXD                                     *
.* V10.11.01     18/12/2017 Peter Vela       TSK 0850270                     *
.*               Recompiled for PMSEDBXD                                     *
.* v10.03.02     01.08.2013 Patrick Adair    Car 289389                      *
.*               recompiled for changes to PMSXMLFD/INC                      *
.* v10.03.01        23.04.2013          SAROEUN SOEUR CAR 280425             *
.*                  changed to work on command line                          *
.*****************************************************************************
          INC       STD002FD
          INC       PATCONFD/INC
          INC       PMSEDBXD/INC
          INC       PMSEDBFD/INC        *eAdmission pending detail table
          INC       PMSEERFD/INC        *eAdmission pending error table
          INC       PMSEHBFD/INC        *eAdmission pending header table
          INC       TFILEVAR/INC 
          INC       WEBERRFD/INC 
          INC       PMSXMLFD/INC
.
. FILE DESCRIPTION INCLUDES
. -------------------------
FLATFILE  FILE      ASCII,FIXED=4000             * XML File
.
. eAdmission Delimited
. -------------------------
EAHDSTXT  FILE      HL7,FIXED=2000
.
.Name     Type      Size      Start
XMHBHOSP  DIM       3         Hospital (pathspaf)
XMHBEAID  DIM       20        eAdmission ID
XMHMRN    DIM       8         MRN urn
XMHACTN   DIM       40        ACTION

. LOCAL VARIABLE DEFINITION
. -------------------------
CMDLINE   DIM       200
DATSTRNG  DIM       300
DIM100    DIM       100
F20       FORM      20
LASTCHAR  DIM       1
LINCOUNT  FORM      8
MESSGKEY  INIT      "1                   "
ADMSURNM  INIT      "PatientSurname"
TAGSTRNG  DIM       300
TYPEFLAG  FORM      1             * information type flag
DIM8      DIM       8
.                                    0 = start tag value
.                                    1 = end tag value
.                                    2 = data value
SECTION   FORM      2
.
HEADERFN  DIM       8     * Header Temp File Name
DETAILFN  DIM       8     * Deatil Temp File Name
.
FILENAM1  DIM       20
FILENAM2  DIM       20
TMPSTRNG  DIM       4000
XMLFLNAM  DIM       80
FORM20    FORM      20
TIME      DIM       8
.
.
. PROGRAM CONSTANTS
. -----------------
ANS001    INIT      "001"
ENDSTRNG  INIT      ">"
PIPE      INIT      "|"
SEDSTRNG  INIT      "sed 's/<\/[^>]*>/&\n/g'"
.
.
PRGID     INIT      "EADSTXML"
PRGNAM    INIT      "eAdmission change status XML load"
VERSION   INIT      "V12.01.01"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      SETX0000
          CALL      INIT0000               * initialisation and open files
.
MAIN0500  CALL      GFIL0000               * get filename 
          BRANCH    EXIT,MAIN9999          * nothing input
.
          CALL      CFIL0000               * copy file
          BRANCH    EXIT,MAIN9999          * unable to copy
.
          CALL      OPEN0000               * open text file
          BRANCH    EXIT,MAIN9999          * unable to open .txt file
.
          CALL      PROC0000               * process
.
MAIN9999  STOP                             * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          OPEN      PMSXMLA1,"pmsxmlaf"
          OPEN      PMSEHBA6,"pmsehbaf"
.
          OPEN      CONTROLF,"controlf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,HEADERFN
          REP       " 0",HEADERFN
.
INIT9999  RETURN
+
.*****************************************************************************
.*                               OPEN0000              Called by: MAIN0000   *
.*            Open the copied ".txt" file prior to processing                *
.*****************************************************************************
.
OPEN0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EAHDSTXT,HEADERFN            * open text file
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Header File not found.  ";
            CALL      HITENTER
            GOTO      OPEN9100
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      OPEN9999
.
OPEN9100  MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9200  DISPLAY   *P1:24,"Invalid Field";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9300  DISPLAY   *P1:24,"Field not found for this message id";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*             Loop through the XML Response file and display the data       *
.*****************************************************************************
.
PROC0000  CALL      RDHD0000
          CLOSE     EAHDSTXT,DELETE        * Delete Temporary File
.
PROC9999  RETURN
+
.*****************************************************************************
.         write eAdmission header to table
.*****************************************************************************
RDHD0000  READ      EAHDSTXT,SEQ;XMHBHOSP,XMHBEAID,XMHMRN,XMHACTN

          PACK      KEY40,XMHBEAID,SP70
          CALL      RAPMEHB6
RDHD1000  CALL      RKPMEHB6
          BRANCH    OVRCD,RDHD9999
.
          REP       " 0",XMHBEAID
          MATCH     PMHBEAID,XMHBEAID
          GOTO      RDHD9999 IF NOT EQUAL
.
          MATCH     PMHBVISN,SP70
          IF        @EQUAL
            PACK      PMHBSTAT,SIX
          ELSE
            PACK      PMHBSTAT,FOUR
          ENDIF
.
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
          CLOCK     TIME,CTIMEIS
.
          PACK      PMHBUDAT,DIM8
          PACK      PMHBUTIM,TIME
.
          CALL      UPPMEHB6
.
          GOTO      RDHD1000
.
RDHD9999  RETURN
.*****************************************************************************
.*                          CFIL0000               Called by: MAIN0000       *
.*          Copy the XML file to a ".txt" file so TBL can open it            *
.* Requires: XMLFLNAM - eAdmission XML Response File name                    *
.* Returns : TEMPFILE - temporary ".txt" filename                            *
.*           EXIT  0 = file successfully copied                              *
.*                 1 = unable to copy file                                   *
.*****************************************************************************
.
.
CFIL0000  STRIP     XMLFLNAM
          CLEAR     CMDLINE
          APPEND    "eadstxml.us1 ",CMDLINE
          APPEND    HEADERFN,CMDLINE
          APPEND    " ",CMDLINE
          APPEND    XMLFLNAM,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          MATCH     "0       ",TASKID
          IF        @EQUAL
            MOVE      ZERO,EXIT
          ELSE
            DISPLAY   *P1:24,*EL,*B,"Unable to convert XML file.  ";
            CALL      HITENTER
            MOVE      ONE,EXIT
          ENDIF
.
CFIL9999  RETURN
+
.*****************************************************************************
.*                          GFIL0000               Called by: MAIN0000       *
.*                       Keyin the response file name                        *
.* Returns:  XMLFLNAM - eAdmission XML Response File name                     *
.*           EXIT  0 = valid filename input                                  *
.*                 1 = nothing input                                         *
.*****************************************************************************
.
GFIL0000  KEYIN     *P1:10,*EL,"Filename (including path): ":
                    *P28:10,XMLFLNAM
.
          PACK      XMLFLNAM,XMLFLNAM,SP30
          MATCH     SP30,XMLFLNAM
          GOTO      GFIL9100 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      GFIL9999
.
GFIL9100  MOVE      ONE,EXIT
.
GFIL9999  RETURN
.=========================================================================
. I/O Includes
.=========================================================================
          INC       STD002IO
          INC       TFILENAM     
          INC       PMSEDBXM/INC
          INC       PMSEERIO/INC        *eAdmission pending error table
          INC       WEBERRIO/INC
          INC       PMSXMLIO/INC
          INC       PMSEHBIO/INC
          INC       PMSEDBIO/INC
.

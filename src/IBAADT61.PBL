.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   IBAADT61                                                    *
.* Desc      :   Multiple Procedures Listing                                 *
.*****************************************************************************
.* Date      :   10/10/2010                                                  *
.* Author    :   J.Tan  (CAR 220206)                                         *
.* Mods.     :                                                               * 
.*       V11.00.01 04/03/2020  J.Tan          TSK 0838262                    *
.*                 Added Effective from and to date to MBS Item file         *
.*       V10.01.01 24/01/2011  Mike Laming   CAR 233054                      *
.*                 Mods to PMSMPRaf (Multiple Proc. Billing) for Contract Id.*
.*****************************************************************************
          INC       STD001FD
          INC       PMSMPRFD/INC  
          INC       PATMTFFD/INC
          INC       PMSCIDFD/INC
          INC       PATCODFD/INC
          INC       PATITMFD/INC
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
ACLAIM    DIM       3
AFUNDH    DIM       6
ACONTID   DIM       6
CODE      DIM       2
CNIDLINE  FORM      1
ECOL1     FORM      2
ENDCLM    DIM       3
ENDCNID   DIM       6
ENDHF     DIM       6
FOPTION   FORM      1
FRCLM     DIM       5
FRCNID    DIM       6
FRHF      DIM       6
FORM7P2   FORM      7.2
HEADFLAG  FORM      1
PREVCNID  DIM       6
PREVDESC  DIM       80
STRCLM    DIM       3
STRCNID   DIM       6
STRHF     DIM       6
TOCLM     DIM       5
TOCNID    DIM       6
TOHF      DIM       6
.
Z20       INIT      "~~~~~~~~~~~~~~~~~~~~"
.
PRGNAM    INIT      "Multiple Procedures List"
PRGID     INIT      "IBAADT61"
VERSION   INIT      "V12.01.00"
.**********************************************************************
.*                           MAINLINE                                 *
.*                       Controlling Logic                            *
.**********************************************************************
ML0000    CALL      INIT0000            * initialization
.                                       * open files
.                                       * display header
.
ML1000    CALL      MOPT0000            * Display main options
          BRANCH    EXIT,ML9999
.
ML2000    CALL      OPTN0000            * select option
          BRANCH    EXIT,ML1000
.
          IF        OPTION = 1
            CALL      KCNID000          * Keyin range of contract id
            BRANCH    EXIT,ML2000
          ENDIF
          CALL      KCLM0000            * Keyin range of claim code
          BRANCH    EXIT,ML2000
          CALL      KFUN0000            * Keyin range of health fund
.
ML2400    CALL      CONTQST             * OK to continue ?
          BRANCH    CEXIT,ML3000,ML2000,ML9999
          BEEP
          GOTO      ML2400
.
ML3000    CALL      PRIN0000            * print Multiple Procedures Billing 
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*               Open files and display header                        *
.**********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening ";
.
          DISPLAY   *P60:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"patitemn";
          OPEN      PATITEM1,"patitemn"
.
          DISPLAY   *P60:24,"pmscidaf";
          OPEN      PMSCIDA1,"pmscidaf"
.
          DISPLAY   *P60:24,"patmtfaf";
          OPEN      PATMTFA3,"patmtfaf"
.
          DISPLAY   *P60:24,"pmsmpraf";
          OPEN      PMSMPRA1,"pmsmpraf"
          OPEN      PMSMPRA4,"pmsmpraf"
.
INIT9999  RETURN
+
.**********************************************************************
.*                           MOPT0000                                 *
.*                   Display the main option screen                   *
.**********************************************************************
MOPT0000  MOVE      ZERO,EXIT
          CALL      DISPHEAD
          DISPLAY   *P45:2:
                    *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Multiple Procedures Report";
.
MOPT1000  KEYIN     *P1:7,*EL,"Please Select : ",*V2LON,FOPTION;
          COMPARE   ZERO,FOPTION
          GOTO      MOPT9000 IF EQUAL
.
          BRANCH    FOPTION,MOPT2000
          BEEP
          GOTO      MOPT1000
.
MOPT2000  DISPLAY   *P51:2,*V2LON,*+," - Multiple Procedures Billing";
          GOTO      MOPT9999
.
MOPT9000  MOVE      TRUE,EXIT
MOPT9999  RETURN
+
.**********************************************************************
.*                           OPTN0000                                 *
.*                   Display the main option screen                   *
.**********************************************************************
OPTN0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:11,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:12,*V2LON,ONE,*HOFF," = Contract Id Sequence":
                    *P1:13,*V2LON,TWO,*HOFF," = Claim Code Sequence";
.
OPTN1000  KEYIN     *P1:15,*EL,"Please Select : ",*V2LON,OPTION;
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
.
          BRANCH    OPTION,OPTN9999,OPTN9999
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      TRUE,EXIT
OPTN9999  RETURN
+
.**********************************************************************
.*                             KCNID000                               *
.*                keyin range of contract id.                         *
.**********************************************************************
KCNID000  MOVE      ZERO,EXIT
          CALL      DISPHEAD
          UNPACK    SP70,FRCNID,TOCNID,STRCNID,ENDCNID
.
          DISPLAY   *P1:4,*EF,"From Contract Id. :":
                    *P1:5,"To   Contract Id. :";
.
          KEYIN     *P21:4,*EL,*V2LON,STRCNID
          PACK      KEY6,STRCNID,SP6
          REP       UPPLOW,KEY6
          MOVE      KEY6,STRCNID
          MOVE      STRCNID,FRCNID
.
          MATCH     SP6,STRCNID
          IF        @EQUAL
            MOVE      "Start",FRCNID
            DISPLAY   *P21:4,*EL,FRCNID
          ELSE
            MOVE      "Unknown Contract Id.",PMCIDESC
            CALL      RDPMCID1
            SQUEEZE   PMCIDESC
            DISPLAY   *P21:4,STRCNID,*P30:4,PMCIDESC
          ENDIF
.
KCNID100  KEYIN     *P21:5,*EL,*V2LON,ENDCNID
          PACK      KEY6,ENDCNID,SP6
          REP       UPPLOW,KEY6
          MOVE      KEY6,ENDCNID
          MOVE      ENDCNID,TOCNID
.
          MATCH     SP6,ENDCNID
          IF        @EQUAL
            MOVE      Z20,ENDCNID
            MOVE      "End",TOCNID
            DISPLAY   *P21:5,*EL,TOCNID
          ELSE
            MOVE      "Unknown Contract Id.",PMCIDESC
            CALL      RDPMCID1
            DISPLAY   *P21:5,TOCNID,*P30:5,PMCIDESC
          ENDIF
.
          MATCH     STRCNID,ENDCNID
          IF        @LESS
            DISPLAY  *P1:24,*EL,*B,*V2LON:
                     "** Start Contract Id. is greater than end Contract Id. **"
            CALL     HITENTER
            GOTO     KCNID000
          ENDIF
.
.
KCNID999  RETURN
+
.**********************************************************************
.*                             KCLM0000                               *
.*                keyin range of claim codes                          *
.**********************************************************************
KCLM0000  MOVE       ZERO,EXIT
          IF         OPTION = 2
            CALL       DISPHEAD
          ENDIF
          UNPACK     SP70,FRCLM,TOCLM,STRCLM,ENDCLM
.
          DISPLAY    *P1:7,*EF,"From Claim Code   : ":
                     *P1:8,"To   Claim Code   : ";
.
          MOVE      "7",EVERT
          MOVE      "21",ECOL               * keyin position
          MOVE      "30",ECOL1              * Display description position
          MOVE      SP3,ACODE
          MOVE      "CL",CODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCLM1000,KCLM9000  * Nothing,exit entered
          MOVE      ACODE,STRCLM  
          MOVE      STRCLM,FRCLM
          GOTO      KCLM5000
.
KCLM1000  DISPLAY   *PECOL:EVERT,*V2LON,"Start",*HOFF;
          MOVE      "Start",FRCLM
          MOVE      ZERO,EXIT
.
KCLM5000  MOVE      "8",EVERT
          MOVE      SP3,ACODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCLM6000,KCLM9000  * Nothing,exit entered
          MOVE      ACODE,ENDCLM  
          MOVE      ENDCLM,TOCLM
          GOTO      KCLM9999
.
KCLM6000  DISPLAY   *PECOL:EVERT,*V2LON,"End",*HOFF;
          MOVE      Z20,ENDCLM
          MOVE      "End",TOCLM
          MOVE      ZERO,EXIT
          GOTO      KCLM9999
.
KCLM9000  MOVE      ONE,EXIT
KCLM9999  RETURN
+
.**********************************************************************
.*                             KFUN0000                               *
.*                Keyin range of health funds                         *
.**********************************************************************
KFUN0000  UNPACK    SP70,STRHF,ENDHF,FRHF,TOHF
.
          KEYIN     *P1:10,*EL,"Starting Health Fund : ",*V2LON,STRHF
          ENDSET    STRHF
          APPEND    SP6,STRHF
          RESET     STRHF
.
          MOVE      STRHF,FRHF
          MATCH     SP6,STRHF
          IF        @EQUAL
            MOVE      "Start",FRHF
          ENDIF
          DISPLAY   *P24:10,*V2LON,FRHF
.
          MOVE      SP6,ENDHF
          KEYIN     *P1:11,*EL,"Ending   Health Fund : ",*V2LON,ENDHF
          ENDSET    ENDHF
          APPEND    SP6,ENDHF
          RESET     ENDHF
.
          MOVE      ENDHF,TOHF
          MATCH     SP6,ENDHF
          IF        @EQUAL
            MOVE      Z20,ENDHF
            MOVE      "End",TOHF
          ENDIF
          DISPLAY   *P24:11,*V2LON,TOHF
.
          MATCH     STRHF,ENDHF
          GOTO      KFUN9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending H.F. < Starting H.F. **";
          CALL      HITENTER
          GOTO      KFUN0000
.
KFUN9999  RETURN
+
.******************************************************************************
.*                                 KCOD0000              Called By : ML0000   *
.*                         Keyin codes                                        *
.******************************************************************************
KCOD0000  CALL      KEYINCOD
          BRANCH    EXIT,KCOD1000:          * Nothing entered
                         KCOD2000           * Exitchar entered
.
KCOD1000  DISPLAY   *PECOL1:EVERT,*V2LON,TDESC;
KCOD2000  RETURN
+
.**********************************************************************
.*                             PRIN0000                               *
.*                Read file and print all Multiple Procedures billing *
.**********************************************************************
PRIN0000  MOVE       ZERO,CPAGENO
          MOVE       SP10,PREVCNID
          CLOCK      TIME,CTIMEIS
          MOVE       "60",CLNO
          DISPLAY    *P1:24,*EL;
.
          IF        OPTION = 1
            DISPLAY   *P10:24,"Printing Contract Id : ":
                      *P36:24,*EL,*V2LON,PMMPCNID;
          ELSE
            DISPLAY   *P10:24,"Printing Claim Code : ":
                      *P35:24,*V2LON,PMMPCCOD,*P42:24,"- ",PMMPHFUN;
          ENDIF
.
          MOVE      ZERO,HEADFLAG
          CALL      RSTEMP1                       * position to start of file
PRIN1000  CALL      RKTEMP1 
          BRANCH    OVRCD,PRIN8000
.
          IF        OPTION = 1
            DISPLAY   *P36:24,*EL,*V2LON,PMMPCNID;
            MATCH     PMMPCNID,ENDCNID
            GOTO      PRIN8000 IF LESS
            MATCH     PREVCNID,PMMPCNID
            IF        !@EQUAL
              UNPACK    SP70,PMCIDESC
              PACK      KEY6,PMMPCNID,SP10
              CALL      RDPMCID1
              MOVE      PMMPCNID,PREVCNID
              MOVE      PMCIDESC,PREVDESC
              MOVE      ONE,CNIDLINE
            ENDIF
          ENDIF
.                                                 * check Claim Code range
          MATCH     STRCLM,PMMPCCOD
          GOTO      PRIN1000 IF LESS
          MATCH     PMMPCCOD,ENDCLM
          IF        OPTION = 1
            GOTO      PRIN1000 IF LESS
          ELSE 
            GOTO      PRIN8000 IF LESS
          ENDIF
.
....      MATCH     SP6,STRHF                     * check RHealth Fund range
....      IF        !@EQUAL
            MATCH     STRHF,PMMPHFUN
            GOTO      PRIN1000 IF LESS
....      ENDIF
....      MATCH     SP6,ENDHF
....      IF        !@EQUAL
            MATCH     PMMPHFUN,ENDHF
            GOTO      PRIN1000 IF LESS
....      ENDIF
.
PRIN2000  IF        PMMPCOUN=0
            CALL       PHED0000                       * print Record Header
          ELSE
            CALL       PREC0000                       * print Record  
          ENDIF
          GOTO       PRIN1000
.
PRIN8000  COMPARE    ZERO,CPAGENO
          GOTO       PRIN9000 IF EQUAL
.
          CALL       UND132
          PRINT      *N,"*** END OF REPORT  ***";
          GOTO       PRIN9999
.
PRIN9000  DISPLAY   *P1:24,*EL,*B,"No Data Available. ";
          CALL      HITENTER
.
PRIN9999  RETURN
+
.**********************************************************************
.*                                HEAD0000                            *
.*                          Print page header                         *
.**********************************************************************
HEAD0000  CALL      HEAD132
.
          MOVE      ZERO,CLNO
          IF        OPTION = 1
            PRINT     *40,"Contract Id. from : ",FRCNID:
                      *66," to : ",TOCNID
            ADD       ONE,CLNO
          ENDIF
.
          PRINT     *40,"Claim Code from   : ",FRCLM,*66," to : ",TOCLM
          IF        OPTION = 1
            PRINT     *1,"Contract Id. sequence";
          ELSE
            PRINT     *1,"Claim Code sequence";
          ENDIF
          PRINT     *40,"Health Fund from  : ",FRHF,*66," to : ",TOHF
          ADD       TWO,CLNO
.
HEAD0400  CALL      UND132
          PRINT     *1,"|",*26,"| Health",*35,"|Table",*41,"|Contract":
                    *50,"|Procedure",*61,"|  Rebate  ",*73,"|  Patient":
                    *85,"|",*132,"|"
          PRINT     *1,"| Claim Code",*26,"| Fund",*35,"|Type",*41,"|Id.":
                    *50,"|Code   ",*61,"|  Amount  ",*73,"|  Amount ":
                    *85,"| Procedures Combination",*132,"|"
.
          ADD       THREE,CLNO
          CALL      UND132
.
          IF        OPTION = 1
            PRINT     *1,"| Contract Id. : ",PREVCNID,SP2,PREVDESC,*132,"|"
            ADD       TWO,CLNO
            CALL      UND132
            MOVE      ZERO,CNIDLINE
          ENDIF
.
HEAD9999  RETURN
+
.**********************************************************************
.*                                PHED0000                             *
.*                   Routine to print record header                    *
.**********************************************************************
PHED0000  IF        HEADFLAG=1
            PRINT     *85,"|":
                      *132,"|"
            ADD       ONE,CLNO
          ENDIF
.
          COMPARE   "55",CLNO
          GOTO      PHED1000 IF NOT LESS
.
          PRINT     *1,"|",*26,"|",*35,"|",*41,"|",*50,"|",*61,"|",*73,"|":
                    *85,"| ",*132,"|"
          ADD       ONE,CLNO
          GOTO      PHED2000
.
PHED1000  CALL      HEAD0000
.
PHED2000  IF        OPTION = 1 & CNIDLINE = 1
            CALL      UND132
            PRINT     *1,"| Contract Id. : ",PREVCNID,SP2,PREVDESC,*132,"|"
            ADD       THREE,CLNO
            CALL      UND132
            MOVE      ZERO,CNIDLINE
          ENDIF
.
          UNPACK    SP70,KEY15,KEY18,KEY22
          MOVE      "CL",TCODE
          PACK      KEY5,TCODE,PMMPCCOD
          CALL      RDCODE1
          PACK      KEY18,TDESC
.
          PRINT     *1,"| ",PMMPCCOD," -",KEY18:
                    *26,"| ",PMMPHFUN:
                    *35,"| ",PMMPHFTY:
                    *41,"| ",PMMPCNID:
                    *50,"| ",PMMPMPRC;
.
          MOVE      PMMPRAMT,FORM7P2
          PRINT     *61,"|",FORM7P2;
          MOVE      PMMPPAMT,FORM7P2
          PRINT     *73,"|",FORM7P2;
          MOVE       ONE,HEADFLAG
.
PHED9999  RETURN
+
.**********************************************************************
.*                                PREC0000                             *
.*                   Routine to print record                           *
.**********************************************************************
PREC0000  MOVE      PMMPITMN,KEY9
          PACK      KEY17,KEY9,SP70
          CALL      PATITMRD
          IF        OVRCD=0
            UNPACK    IDESC,KEY36
          ENDIF
.
          BRANCH    HEADFLAG,PREC2000
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,"|",*26,"|",*35,"|",*41,"|",*50,"|",*61,"|",*73,"|";
.
PREC2000  PRINT     *85,"| ",PMMPITMN,"-",KEY36:
                    *132,"|"
          ADD       ONE,CLNO
          MOVE      ZERO,HEADFLAG
. 
PREC9999  RETURN
+
.**********************************************************************
.*                                I/O                                  *
.**********************************************************************
RSTEMP1   MOVE      ZERO,EXIT
          IF        OPTION=1
            PACK      KEY28,STRCNID,SP70
            CALL      RSPMMPR1              * position read on Contract Id.
          ELSE
            MATCH     SP3,STRCLM
            IF        @EQUAL
              PACK      KEY28,STRCLM,SP20
            ELSE
              PACK      KEY28,STRCLM,STRHF,SP20
            ENDIF
            CALL      RSPMMPR4              * position read on Claim Code
          ENDIF
          RETURN
.
RKTEMP1   IF         OPTION=1
            CALL       RKPMMPR1             * read next record of Contract Id
          ELSE
            CALL       RKPMMPR4             * read next record of Claim Code
          ENDIF
.
          RETURN
+
.******************************************************************************
.
          INC       STD001IO
          INC       PATDSCOD
          INC       KEYINCOD
          INC       PATITMRD
          INC       PMSMPRIO/INC
          INC       PATMTFIO/INC
          INC       PMSCIDIO/INC
          INC       PATCODIO/INC
          INC       PATITMIO/INC

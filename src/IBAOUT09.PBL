.*****************************************************************************
.* System    :   Private Practice / Outpatient                               *
.* Program   :   IBAOUT09                                                    *
.* Desc      :   Eclipse DBS Assessment Report Printing                      *
.*****************************************************************************
.* Date      :   29/01/2019                                                  *
.* Author    :   Peter Vela                                                  *
.* Function  :   This program will allow the user to report on the raw       *
.*               assessment details for a given DBS claim.                   *
.* Mods      :                                                               *
.*       V10.13.00  29/01/2019  Peter Vela        TSK 0859743                *
.*                  Program created.                                         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCONFD/INC
          INC       OUTEADFD/INC
          INC       OUTEAHFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
ASSDESC2  DIM       45
.
DIM6      DIM       6
DIM7      DIM       7
DIM10A    DIM       10
DIM9B     DIM       9 
DIM9C     DIM       9 
DISPDTE3  DIM       10
.
FORM6P2   FORM      6.2 
FORM7P2   FORM      7.2
.
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
SP32      INIT      "                                "
.
.
PRGID     INIT      "IBAOUT09"
PRGNAM    INIT      "Eclipse DBS Processing Report Printing"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
          MATCH     "1",PTCNUEBB
          GOTO      MAIN9999 IF NOT EQUAL
.
          CALL      GASS0000               * get assessment header key
          BRANCH    EXIT,MAIN9999          * nothing/exitchar entered
.
          CALL      PRIN0000               * print assessment
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000                                   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND27;*122,PTCNUEBB
.
          DISPLAY   *P56:24,*EL,"Opening ";
.
          MATCH     "1",PTCNUEBB
          IF        @EQUAL
            DISPLAY   *P64:24,"outeahaf";
            OPEN      OUTEAHA1,"outeahaf"
.
            DISPLAY   *P64:24,"outeadaf";
            OPEN      OUTEADA1,"outeadaf"
          ENDIF
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                GASS0000         Called by: MAIN0000       *
.*             Get the assessment key for the report                         *
.* Returns: EXIT    0 = valid outeahaf record read                           *
.*                  1 = nothing entered or outeahaf record not found         *
.*****************************************************************************
.
GASS0000  KEYIN     *P1:3,*EF:
                    *P1:4,"Assessment key: ",*V2LON,KEY33
.
          PACK      KEY33,KEY33,SP70
          MATCH     SP70,KEY33                   * anything entered ?
          GOTO      GASS9100 IF EQUAL            * no - finished        
.
.         Use KEY33 to read the assessment header record (outeahaf)     
.
          CALL      RDOTEAH1                     * header record found ?
          BRANCH    OVRCD,GASS9100               * no - finished
.
          MOVE      ZERO,EXIT
          GOTO      GASS9999
.
GASS9100  MOVE      ONE,EXIT
.
GASS9999  RETURN
+
.*****************************************************************************
.*                               PRIN0000                                    *
.*                  Process the records for printing                         *
.*****************************************************************************
.
PRIN0000  MOVE      ZERO,CPAGENO
          MOVE      ZERO,CNOUNDLN
          CALL      IBACLOCK
.
          CALL      PRAH0000                * print assessment heading
          CALL      PRAD0000                * print assessment details
.
          PRINT     *N,*1,"*** End of Report ***"
          DISPLAY   *P1:24,*EL,"Report printed.  ";
          CALL      HITENTER
.
PRIN9999  RETURN
+
.*****************************************************************************
.*                            PRAH0000                                       *
.*                       Print page heading (outeahaf details)               *
.*****************************************************************************
.
PRAH0000  CALL      HEAD132                      * display page header
.
          PRINT     *N,*40,"Claim Id: ",OTEHCLID
.
          COMPARE   ONE,CPAGENO                  * first page ?
          GOTO      PRAH1000 IF NOT EQUAL        * no
.
.         For first page only, print claim header details
.
          PRINT     *N,*1,"Claim Assessment"
          CALL      LINE0000                     * draw line across page
.
          UNPACK    OTEHCLBP,DIM6,DIM2
          PACK      DIM9B,DIM6,DOT,DIM2
          MOVE      DIM9B,FORM6P2
          IF        FORM6P2 = 0
            PACK      DIM9B,SP6,ZERO,DOT,ZERO,ZERO
          ELSE
            MOVE    FORM6P2,DIM9B
          ENDIF
.
          UNPACK    OTEHCLCA,DIM6,DIM2
          PACK      DIM9C,DIM6,DOT,DIM2
          MOVE      DIM9C,FORM6P2
          IF        FORM6P2 = 0
            PACK      DIM9C,SP6,ZERO,DOT,ZERO,ZERO
          ELSE
            MOVE    FORM6P2,DIM9C
          ENDIF
.
          PRINT     *1,PIPE,*3,"Claim Id             : ",OTEHCLID:
                    *76,PIPE,*78,"Claim Benefit Paid : ",DIM9B,*132,PIPE,*N:
                    *1,PIPE,*3,"Claim Charge Amount  : ",DIM9C:
                    *76,PIPE,*78,"Patient Family Name: ",OTEHSNAM,*132,PIPE,*N:
                    *1,PIPE,*3,"Patient First Name   : ",OTEHFNAM:
                    *76,PIPE,*78,"Medicare Card Flag : ",OTEHMECF,*132,PIPE,*N:
                    *1,PIPE,*3,"Patient Medicare Num : ",OTEHMNUM:
                    *76,PIPE,*78,"Patient Ref Num    : ",OTEHRNUM,*132,PIPE,*N:
                    *1,PIPE,*3,"Servicing Prov Num   : ",OTEHSRVP:
                    *76,PIPE,*78,"Claim Tran Id      : ",OTEHCTID,*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
.         Print the claim assessment details
.
PRAH1000  IF        CPAGENO = 1
            PRINT     *N,*1,"Claim Assessment Details"
          ELSE
            PRINT     *N,*1,"Claim Assessment Details (contd.)"
          ENDIF
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"Service Id",*22,PIPE,*24,"Item Number":
                    *44,PIPE,*46,"Benefit Amount",*66,PIPE,*68,"No of Patients":
                    *88,PIPE,*90,"Explanation Code":
.                   *110,PIPE,*112,"Transaction Id":
                    *132,PIPE,*N 
.
          CALL      LINE0000                     * draw line across page
.
          IF        CPAGENO = 1
            MOVE      TWENTY,CLNO                * initialise line count
          ELSE
            MOVE      TEN1,CLNO                  * initialise line count
          ENDIF
.
PRAH9999  RETURN
+
.*****************************************************************************
.*                               PRAD0000                                    *
.*                  Process the assessment details for printing              *
.*****************************************************************************
.
PRAD0000  PACK      KEY40,OTEHCLID,OTEHRPTC,SP70
          CALL      RSOTEAD1                     * position on remittance
PRAD0500  CALL      RKOTEAD1                     * read next record
          BRANCH    OVRCD,PRAD9500               * eof - finished
.
          MATCH     OTEHCLID,OTEDCLID            * same participant still ?
          GOTO      PRAD9500 IF NOT EQUAL        * no - finished
.
          MATCH     OTEHRPTC,OTEDRPTC            * same Account Reference ID ?
          GOTO      PRAD9500 IF NOT EQUAL        * no - finished
.
.         A valid record has been found, so print it
.
          COMPARE   SIXTY,CLNO                   * page full ?
          CALL      PRAH0000 IF NOT LESS         * yes
.
          CALL      DLIN0000                     * print assessment details line
          GOTO      PRAD0500                     * get next record
.
PRAD9500  CALL      LINE0000                     * draw line across page
.
PRAD9999  RETURN
+
.*****************************************************************************
.*                            DLIN0000                                       *
.*                  Valid assessment details record so print it              *
.*****************************************************************************
. 
DLIN0000  UNPACK    OTEDSERB,DIM7,DIM2
          PACK      DIM10A,DIM7,DOT,DIM2
          MOVE      DIM10A,FORM7P2
          IF        FORM7P2 = 0
            PACK      DIM10A,SP6,ZERO,DOT,ZERO,ZERO
          ELSE
            MOVE    FORM7P2,DIM10A
          ENDIF
.
          PRINT     *1,PIPE,*3,OTEDSRVI,*22,PIPE,*24,OTEDITMN:
                    *44,PIPE,*46,DIM10A,*66,PIPE,*68,OTEDNOPA:
                    *88,PIPE,*90,OTEDEXPC:
.                   *110,PIPE,*112,OTEDCTID:
                    *132,PIPE
.
          ADD       ONE,CLNO                     * increment line count
.
DLIN9999  RETURN
+
.*****************************************************************************
.*                            LINE0000                                       *
.*                      Draw line across page                                *
.*****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       OUTEADIO/INC
          INC       OUTEAHIO/INC
+

.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   EXTRBOOK                                                    *
.* Desc      :   O/P Future Bookings Data Extraction Program                 *
.*****************************************************************************
.* Date      :   23/12/2014                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will extract the future O/P bookings           *
.*               into a pipe delimited file in a format that will feed into  *
.*               CONVBOOK.                                                   *
.* Mods:                                                                     *
.*****************************************************************************
.*        V12.00.01 21/05/2025  Bella Turco   TSK 0955096                    *
.*                  Alphanumeric changes                                     *
.*        V11.02.01 09/02/2022  Thanh T       TSK 0905641                    *
.*                  Recompiled as OUTHEDFD/OUTMA1FD/WATTR1FD changes         *
.*        V11.00.01 12/01/2021  Ebon Clements    TSK 0901440                 *
.*                  Fixed deposit file read comdepaf index 2 key size - KEY26*
.*****************************************************************************
.*        V10.12.03 05/03/2018  Ebon Clements    TSK 0846913                 *
.*                  Populate original booking date/time in OUTCANFD          *
.*****************************************************************************
.*        V10.11.03 27/11/2017  Thanh Tieu       Tsk 0821710                 *
.*                  Recompiled as PATMISTD changed                           *
.*        V10.11.02 09/11/2017  Peter Vela       TSK 0847214                 *
.*                  Recompiled for EXACCDAT                                  *
.*        V10.11.01 21/09/2017  Thanh T.         TSK 0821710                 *
.*                  Audit Amission/outpatient visit comments                 *
.*****************************************************************************
.*        V10.08.02 27/10/2016  Steve Armstrong  TSK 0819758 (CTAS)          *
.*                  Mods to extract ACC Comment data                         *
.*        V10.08.01 05/10/2015  Steve Armstrong  TSK 0819982 (CTAS)          *
.*                  Added new fields BOOKCONF, BOOKCNID & BOOKCNDT           *
.*****************************************************************************
.*        V10.06.06 02/09/2015  Steve Armstrong  CAR 318304                  *
.*                  Mods to allow a range of Clinic Type codes to be         *
.*                  extracted for a specific site.                           *
.*        V10.06.05 10/08/2015  Steve Armstrong  CAR 320430                  *
.*                  Mods to populate BOOKAVIS with the old webPAS O/P number *
.*                  (OBAOUTNO) and BOOKRVIS with the old linked Referral     *
.*                  number (ALLNVISN).                                       *
.*        V10.06.04 31/07/2015  Steve Armstrong   CAR 320197                 *
.*                  Mods to check if DGCLICOC needs to be called if no ACC   *
.*                  data is found or if not NZ.                              *
.*        V10.06.03 08/07/2015  Don Nguyen        CAR 313893                 *
.*                  Modified INIT0000 to also open 'alllnkaf' with index 1   *
.*        V10.06.02 10/06/2015  Steve Armstrong   CAR 313856                 *
.*                  Mods to use CLVISIAU instead of an internal clear routine*
.*                  07/05/2015  Steve Armstrong  CAR 313857                  *
.*                  Mods to handle ACC data extraction (CRISP).              *
.*                  Also added BOOKPURC, BOOKOUTC and BOOKCONT fields to the *
.*                  data record (CRISP).                                     *
.*        V10.06.01 12/03/2015  Steve Armstrong  CAR 314108                  *
.*                  Removed reference to PATCGPFD (No Longer in use)         *
.*****************************************************************************
.*        V10.05.00 23/12/2014  Steve Armstrong  CAR 305263                  *
.*                  Created program.                                         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ACCCMTFD/INC
          INC       ALLCONFD/INC
          INC       ALLENCFD/INC
          INC       ALLLNKFD/INC
          INC       ALLQUEFD/INC
          INC       ALLREFFD/INC
          INC       COMDEPFD/INC
          INC       DISMASFD/INC
          INC       DISPATFD/INC
          INC       DISPTLFD/INC
          INC       HICCLMFD/INC
          INC       HICCITFD/INC
          INC       HICBCNFD/INC
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       IBACONFD/INC
          INC       NHIMASFD/INC
          INC       OUTBB1FD/INC
          INC       OUTBITFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBOCFD/INC
          INC       OUTCANFD/INC
          INC       OUTCONFD/INC
          INC       OUTCTYFD/INC
          INC       OUTDIAFD/INC
          INC       OUTHEDFD/INC
          INC       OUTLKBFD/INC
          INC       OUTLPCFD/INC
          INC       OUTMA1FD/INC
          INC       OUTMSPFD/INC
          INC       OUTPREFD/INC
          INC       OUTRF1FD/INC
          INC       OUTRSHFD/INC
          INC       OUTSESFD/INC
          INC       OUTSITFD/INC
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC
          INC       PATMISTD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATHSPFD/INC
          INC       PATIPEFD/INC
          INC       PATMA1FD/INC
          INC       PATNIDFD/INC
          INC       PMSAIDFD/INC
          INC       PMSMTIFD/INC
          INC       PATWC1FD/INC
          INC       PMSWX1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSQPTFD/INC
          INC       PMSTSPFD/INC
          INC       PMSVX1FD/INC
          INC       VISCMTFD/INC
          INC       VISIAUFD/INC
          INC       VISINTFD/INC
          INC       WATMESFD/INC
          INC       WATOPAFD/INC
          INC       WATOPSFD/INC
          INC       WATTR1FD/INC
          INC       WATTX1FD/INC
          INC       WEBSECFD/INC
.
.
.         O/P Future Bookings upload file layout - convbook.txt
.         (as per CONVBOOK)
.
BOOKTEXT  FILE      HL7, FIXED=4096         * Pipe delimited upload file
.                                             4096 is maximum
.
.         Fields must be pipe delimited and in the following sequence.
.         Field lengths are irrelevant other than the fact that where
.         a field is longer than allowed, the extra data will be ignored.
.
BOOKTYPE  DIM       1     1       * O/P record type
.                                     0 = Booking
.PIPE     DIM       1     2       * Pipe Delimiter
BOOKURNO  DIM       8     3       * U/R Number                         (obaurno)
.PIPE     DIM       1     11      * Pipe Delimiter
BOOKSITE  DIM       6     12      * Site ID (outsitaf)                 (obasite)
.PIPE     DIM       1     18      * Pipe Delimiter
BOOKCLIN  DIM       6     19      * Clinic ID (outcliaf)               (obaclin)
.PIPE     DIM       1     25      * Pipe Delimiter
BOOKDATE  DIM       8     26      * Date (ccyymmdd)                    (obadate)
.PIPE     DIM       1     34      * Pipe Delimiter
BOOKTIME  DIM       5     35      * Slot Time (hh:mm)                  (obatime)
.PIPE     DIM       1     40      * Pipe Delimiter
BOOKVIST  DIM       3     41      * Visit Type (Cat CV)               (obavisit)
.PIPE     DIM       1     44      * Pipe Delimiter
BOOKCDAT  DIM       8     45      * Booking Created Date (ccyymmdd)   (obabkdte)
.PIPE     DIM       1     53      * Pipe Delimiter
BOOKCTIM  DIM       8     54      * Booking Created Time (hh:mm:ss)   (obabkdte)
.PIPE     DIM       1     62      * Pipe Delimiter
BOOKSRCR  DIM       3     63      * Source of Referral (Cat S)         (obasrcr)
.PIPE     DIM       1     66      * Pipe Delimiter
BOOKCOMP  DIM       3     67      * Compensation Eligibility (Cat CL)  (obacomp)
.PIPE     DIM       1     70      * Pipe Delimiter
BOOKPRTY  DIM       3     71      * Priority Code (Cat PR)             (obaprty)
.PIPE     DIM       1     74      * Pipe Delimiter
BOOKUSR1  DIM       3     75      * User Defined Field 1 (Cat O1)      (obousr1)
.PIPE     DIM       1     78      * Pipe Delimiter
BOOKDEPT  DIM       3     79      * Department Code (Cat AC)          (otbbdept)
.PIPE     DIM       1     82      * Pipe Delimiter
BOOKTRAN  DIM       3     83      * Transport Allocation (Cat OB)     (otbbtran)
.PIPE     DIM       1     86      * Pipe Delimiter
BOOKRFGP  DIM       10    87      * Referring GP (pmshcpaf)           (otbbrfgp)
.PIPE     DIM       1     97      * Pipe Delimiter
BOOKSNDL  DIM       1     98      * Send Letter (Y/N)                 (otbbsndl)
.PIPE     DIM       1     99      * Pipe Delimiter
BOOKSPCA  DIM       3     100     * Special Arrangements (Cat SA)     (otbbspca)
.PIPE     DIM       1     103     * Pipe Delimiter
BOOKPCOM  DIM       50    104     * Presenting Complaint              (pmvxudf1)
.PIPE     DIM       1     154     * Pipe Delimiter
BOOKDIAG  DIM       9     155     * Diagnosis Code                    (opdicode)
.PIPE     DIM       1     164     * Pipe Delimiter
BOOKDDES  DIM       50    165     * Diagnosis Description    (outdiaaf.opdicode)
.PIPE     DIM       1     215     * Pipe Delimiter
BOOKAVIS  DIM       20    216     * Alternate Visit Number   (ibaalvaf.ibavavis)
.PIPE     DIM       1     236     * Pipe Delimiter
BOOKRVIS  DIM       20    237     * Referral Visit Number    (ibaalvaf.ibavavis)
.PIPE     DIM       1     257     * Pipe Delimiter
BOOKINST  DIM       200   3829    * Pt. Instructions         (viscmtaf.vsctdata)
.                                                                    type = 013
.                                                             1 line = 60 bytes
.                                                             max. of 4 lines
.PIPE     DIM       1     4029    * Pipe Delimiter
BOOKSNGL  DIM       1     4030    * Send Generate Letter     (outbb1af.otbbsngl)
.                                    (Y/N)
.PIPE     DIM       1     4031    * Pipe Delimiter
BOOKINTR  DIM       1     4032    * Interpreter Req'd (Y/N)           (pmvxintr)
.PIPE     DIM       1     4033    * Pipe Delimiter
BOOKPURC  DIM       3     4034    * Health Purchaser (Cat HP)         (otbbpurc)
.PIPE     DIM       1     4037    * Pipe Delimiter
BOOKOUTC  DIM       3     4038    * Triage Outcome (Cat OZ)           (otbboutc)
.PIPE     DIM       1     4041    * Pipe Delimiter
BOOKCONT  DIM       3     4042    * Contract (Cat gb)                 (otbbcont)
.PIPE     DIM       1     4045    * Pipe Delimiter
BOOKCONF  DIM       1     4046    * Appointment confirmed (Y/N)       (pmvxconf)
.PIPE     DIM       1     4047    * Pipe Delimiter
BOOKCNID  DIM       10    4048    * Visit Confirmed Web User Id       (pmvxcnid)
.                                   (websecaf)
.PIPE     DIM       1     4058    * Pipe Delimiter
BOOKCNDT  DIM       13    4059    * Visit Confirmed Date/Time         (pmvxcndt)
.                                   (ccyymmddhh:mm)
.
. End of Record           4072
.
.
.         ACC Data Records
.
.         Fields must be pipe delimited and in the following sequence.
.         Field lengths are irrelevant other than the fact that where
.         a field is longer than allowed, the extra data will be ignored.
.
.BOOKTYPE DIM       1      1      * O/P record type
.                                     1 = ACC Data
.PIPE     DIM       1      2      * Pipe Delimiter
.BOOKURNO DIM       8      3      * U/R Number                        (ptwcurno)
.PIPE     DIM       1      11     * Pipe Delimiter
ACCDNAME  DIM       30     12     * Employer Name                     (wcename)
.PIPE     DIM       1      42     * Pipe Delimiter
ACCDADD1  DIM       35     43     * Employer Address Line 1           (wceadd1)
.PIPE     DIM       1      78     * Pipe Delimiter
ACCDADD2  DIM       35     79     * Employer Address Line 2           (wceadd2)
.PIPE     DIM       1      114    * Pipe Delimiter
ACCDADD3  DIM       35     115    * Employer Address Line 3           (wceadd3)
.PIPE     DIM       1      150    * Pipe Delimiter
ACCDADD4  DIM       35     151    * Employer Address Line 4           (wceadd4)
.PIPE     DIM       1      186    * Pipe Delimiter
ACCDPOST  DIM       8      187    * Employer Postcode                 (wcepost)
.PIPE     DIM       1      195    * Pipe Delimiter
ACCDTELP  DIM       20     196    * Employer Telephone                (wcetele)
.PIPE     DIM       1      216    * Pipe Delimiter
ACCDACDT  DIM       8      217    * Date of Accident (ccyymmdd)       (wcacdat)
.PIPE     DIM       1      225    * Pipe Delimiter
ACCDACPT  DIM       1      226    * Accepted by Ins. Co. (Y/N)        (wcaccpt)
.PIPE     DIM       1      227    * Pipe Delimiter
ACCDINSR  DIM       6      228    * Insurance Company Code            (wcicode)
.PIPE     DIM       1      234    * Pipe Delimiter
ACCDCLNO  DIM       20     235    * Insurance Claim Number            (wcclmno)
.PIPE     DIM       1      255    * Pipe Delimiter
ACCDCOM1  DIM       40     256    * Comments 1                        (wccomn1)
.PIPE     DIM       1      296    * Pipe Delimiter
ACCDCOM2  DIM       40     297    * Comments 2                        (wccomn2)
.PIPE     DIM       1      337    * Pipe Delimiter
ACCDALOC  DIM       50     338    * Accident Location                 (pmwxaloc)
.PIPE     DIM       1      388    * Pipe Delimiter
ACCDCINJ  DIM       3      389    * Cause of Injury (Cat IK)          (pmwxcinj)
.PIPE     DIM       1      392    * Pipe Delimiter
ACCDICOD  DIM       3      393    * Injury Code (Cat IJ)              (pmwxicod)
.PIPE     DIM       1      396    * Pipe Delimiter
ACCDCNAM  DIM       40     397    * Contact Name                      (pmwxcnam)
.PIPE     DIM       1      437    * Pipe Delimiter
ACCDCDTE  DIM       8      438    * Date Record Created (ccyymmdd)    (pmwxcdte)
.PIPE     DIM       1      446    * Pipe Delimiter
ACCDCTIM  DIM       8      447    * Time Record Created (hh:mm:ss)    (pmwxctim)
.PIPE     DIM       1      455    * Pipe Delimiter
ACCDWEBC  DIM       10     456    * WEB User ID Record Creator        (pmwxwebc)
.PIPE     DIM       1      466    * Pipe Delimiter
ACCDLUPD  DIM       8      467    * Last Update Date (ccyymmdd)       (pmwxlupd)
.PIPE     DIM       1      475    * Pipe Delimiter
ACCDLUPT  DIM       8      476    * Last Update Time (hh:mm:ss)       (pmwxlupt)
.PIPE     DIM       1      484    * Pipe Delimiter
ACCDWEBU  DIM       10     485    * WEB User ID Record Updator        (pmwxwebu)
.PIPE     DIM       1      495    * Pipe Delimiter
ACCDACCF  DIM       3      496    * Work Related (Cat IQ)             (pmwxaccf)
.PIPE     DIM       1      499    * Pipe Delimiter
ACCDPLIN  DIM       3      500    * Place Injury Occurred (Cat IP)    (pmwxplin)
.PIPE     DIM       1      503    * Pipe Delimiter
ACCDACTI  DIM       3      504    * Activity When Injured (Cat IO)    (pmwxacti)
.PIPE     DIM       1      507    * Pipe Delimiter
ACCDADTE  DIM       8      508    * ACC Decline Date (ccyymmdd)       (pmwxadte)
.PIPE     DIM       1      516    * Pipe Delimiter
ACCDATME  DIM       8      517    * Accident Time (hh:mm:ss)          (pmwxatme)
.PIPE     DIM       1      525    * Pipe Delimiter
ACCDACLC  DIM       3      526    * Accident Location (Cat IM)        (pmwxaclc)
.PIPE     DIM       1      529    * Pipe Delimiter
ACCDAINZ  DIM       1      530    * Accident in NZ (Y/N)              (pmwxainz)
.PIPE     DIM       1      531    * Pipe Delimiter
ACCDMOVV  DIM       1      532    * Involves Moving Vehicle           (pmwxmovv)
.                                    on Public Road (Y/N)
.PIPE     DIM       1      533    * Pipe Delimiter
ACCDSPTI  DIM       1      534    * Sporting Injury (Y/N)             (pmwxspti)
.PIPE     DIM       1      535    * Pipe Delimiter
ACCDSPRT  DIM       3      536    * Sport Name (Cat IB)               (pmwxsprt)
.PIPE     DIM       1      539    * Pipe Delimiter
ACCDRECI  DIM       1      540    * Recurring Injury Indicator (Y/N)  (pmwxreci)
.PIPE     DIM       1      541    * Pipe Delimiter
ACCDTRIC  DIM       1      542    * Treatment Injury Claim (Y/N)      (pmwxtric)
.PIPE     DIM       1      543    * Pipe Delimiter
ACCDESTA  DIM       3      544    * Employment Status (Cat IF)        (pmwxesta)
.PIPE     DIM       1      547    * Pipe Delimiter
ACCDPDDT  DIM       8      548    * Patient Declaration Date          (pmwxpddt)
.                                     (ccyymmdd)
.PIPE     DIM       1      556    * Pipe Delimiter
ACCDARG1  DIM       20     557    * Authorised Representative         (pmwxarg1)
.                                     Given Name 1
.PIPE     DIM       1      577    * Pipe Delimiter
ACCDARG2  DIM       20     578    * Authorised Representative         (pmwxarg2)
.                                     Given Name 2
.PIPE     DIM       1      598    * Pipe Delimiter
ACCDARSN  DIM       25     599    * Authorised Representative         (pmwxasrn)
.                                     Surname
.PIPE     DIM       1      624    * Pipe Delimiter
ACCDARTL  DIM       4      625    * Authorised Representative         (pmwxartl)
.                                     Title
.PIPE     DIM       1      629    * Pipe Delimiter
ACCDARRP  DIM       10     630    * Authorised Representative         (pmwxarrp)
.                                     Relation
.PIPE     DIM       1      640    * Pipe Delimiter
ACCDASST  DIM       1      641    * Assistance is Required (Y/N)      (pmwxasst)
.PIPE     DIM       1      642    * Pipe Delimiter
ACCDNEED  DIM       1      643    * Need to Call HP (Y/N)             (pmwxneed)
.PIPE     DIM       1      644    * Pipe Delimiter
ACCDCONT  DIM       1      645    * Continue Normal Hours of          (pmwxcont)
.                                     Work (Y/N)
.PIPE     DIM       1      646    * Pipe Delimiter
ACCDALTW  DIM       1      647    * Alternative Work Indicator (Y/N)  (pmwxaltw)
.PIPE     DIM       1      648    * Pipe Delimiter
ACCDTYPA  DIM       3      649    * Type of Alternative Work (Cat IH) (pmwxtypa)
.PIPE     DIM       1      652    * Pipe Delimiter
ACCDSALT  DIM       8      653    * Start Date of Alternative         (pmwxsalt)
.                                     Work (ccyymmdd)
.PIPE     DIM       1      661    * Pipe Delimiter
ACCDHPDA  DIM       2      662    * Hours per Day of Alternative      (pmwxhpda)
.                                     Work
.PIPE     DIM       1      664    * Pipe Delimiter
ACCDUNFD  DIM       3      665    * Unfit for Work for Duration       (pmwxunfd)
.PIPE     DIM       1      668    * Pipe Delimiter
ACCDUNFT  DIM       3      669    * Unfit for Work for Type (Cat UI)  (pmwxunft)
.PIPE     DIM       1      672    * Pipe Delimiter
ACCDFISD  DIM       8      673    * Full Incapacity Start Date        (pmwxfisd)
.                                     (ccyymmdd)
.PIPE     DIM       1      681    * Pipe Delimiter
ACCDRNWD  DIM       8      682    * Return to Normal Work Date        (pmwxrnwd)
.                                     (ccyymmdd)
.PIPE     DIM       1      690    * Pipe Delimiter
ACCDCSTA  DIM       1      691    * Claim Status                      (pmwxcsta)
.                                     0 = Existing
.                                     1 = Parked
.                                     2 = Completed
.PIPE     DIM       1      692    * Pipe Delimiter
ACCDTWRK  DIM       3      693    * Type of Work (Cat IX)             (pmwxtwrk)
.PIPE     DIM       1      696    * Pipe Delimiter
ACCDOEST  DIM       50     697    * Other Employment Status           (pmwxoest)
.PIPE     DIM       1      747    * Pipe Delimiter
ACCDTPRO  DIM       10     748    * HCP Treatment Provider            (pmwxtpro)
.PIPE     DIM       1      758    * Pipe Delimiter
ACCDECOD  DIM       6      759    * Employer Code (pateoraf)          (pmwxecod)
.PIPE     DIM       1      765    * Pipe Delimiter
ACCDTYP1  DIM       3      766    * Hard-coded Comment Type           (accmtype)
.                                     001 = Cause of Injury
.PIPE     DIM       1      769    * Pipe Delimiter
ACCDDAT1  DIM       500    770    * Cause of Injury Comments          (accmdata)
.PIPE     DIM       1     1270    * Pipe Delimiter
ACCDTYP2  DIM       3     1271    * Hard-coded Comment Type           (accmtype)
.                                     002 = Injury Comments
.PIPE     DIM       1     1274    * Pipe Delimiter
ACCDDAT2  DIM       500   1275    * Injury Comments                   (accmdata)
.PIPE     DIM       1     1775    * Pipe Delimiter
ACCDTYP3  DIM       3     1776    * Hard-coded Comment Type           (accmtype)
.                                     003 = Alternative Work Restrictions
.PIPE     DIM       1     1779    * Pipe Delimiter
ACCDDAT3  DIM       500   1780    * Alternative Work Restrictions Comments
.                                                                     (accmdata)
.
. End of Record           2280
.
.
. Visit comment header upload file layout
.
.Name     Type    Length Start   Description
.----     ----    ------ -----   -----------
.BOOKTYPE DIM       1      1     Comment Header (2)
.PIPE     DIM       1      2     Pipe Delimiter
.BOOKURNO DIM       8      3     U/R Number         
.PIPE     DIM       1      11    Pipe Delimiter
CSMDVISN  DIM       8      12    U/R Number
.PIPE     DIM       1      20    Pipe Delimiter
CSMDTYPE  DIM       3      21    Comment Type
.PIPE     DIM       1      24    Pipe Delimiter
CSMDNOTE  DIM       6      25    Counter/Note Number
.PIPE     DIM       1      31    Pipe Delimiter
CSMDDATE  DIM       8      32    Date Record Created (ccyymmdd)
.PIPE     DIM       1      40    Pipe Delimiter
CSMDTIME  DIM       8      41    Time Record Created (hh:mm:ss)
.PIPE     DIM       1      49    Pipe Delimiter
CSMDUSER  DIM       10     50    User Who Created Record
.PIPE     DIM       1      60    Pipe Delimiter
CSMDOCCG  DIM       3      61    Occupational Group WEB User ID
.PIPE     DIM       1      64    Pipe Delimiter
CSMDDELT  DIM       1      65    Delete Indicator (0=No, 1=Yes)
.PIPE     DIM       1      66    Pipe Delimiter
CSMDDDAT  DIM       8      67    Date Record Deleted (ccyymmdd)
.PIPE     DIM       1      75    Pipe Delimiter
CSMDDTIM  DIM       8      76    Time Record Deleted (hh:mm:ss)
.PIPE     DIM       1      84    Pipe Delimiter
CSMDDUSE  DIM       10     85    User Who Deleted Record (websecaf)
.PIPE     DIM       1      95    Pipe Delimiter
CSMDDOCC  DIM       3      96    Occupational Grp WEB UserID Deleted
.
.End of Record             99
.
.
. Visit comment detail upload file layout
.
.Name     Type    Length Start   Description
.----     ----    ------ -----   -----------
.BOOKTYPE DIM       1      1     Comment Details (3)
.PIPE     DIM       1      2     Pipe Delimiter
.BOOKURNO DIM       8      3     U/R Number         
.PIPE     DIM       1      11    Pipe Delimiter
CSMTVISN  DIM       8      12    U/R Number
.PIPE     DIM       1      20    Pipe Delimiter
CSMTTYPE  DIM       3      21    Comment Type
.PIPE     DIM       1      24    Pipe Delimiter
CSMTNOTE  DIM       6      25    Counter/Note Number
.PIPE     DIM       1      31    Pipe Delimiter
CSMTUNIQ  DIM       3      32    Line Number
.PIPE     DIM       1      35    Pipe Delimiter
CSMTCMNT  DIM       70     36    Comment
.
.End of Record            106
.
. LOCAL VARIABLE DEFINITION
. -------------------------
ACCCMTYP  DIM       3
ACCCOUNT  FORM      10
AUDIFLAG  FORM      1
CASEKEYS  DIM       28
CHKGRPIN  FORM      1
CISMFLAG  FORM      1
CODE      DIM       2
CURRDATE  DIM       8
CURRTIME  DIM       8
DIM8      DIM       8
DIM9      DIM       9
DIM14     DIM       14
DIM500    DIM       500
ENDCTYP   DIM       6
EXPRDATE  DATE      8
EXTCOUNT  FORM      10
PRCCNTHR  FORM      10
PRCCNTDT  FORM      10
EXTCNTHR  FORM      10
EXTCNTDT  FORM      10
FOUNDFLG  FORM      1             * Record(s) found flag
.                                     0 = records found
.                                     1 = no records found
HBWAIT    FORM      1        * S27/P46 - Waiting Lists (1=ON,  2=OFF)BOKCONFD
HOUR      FORM      2
IBAMFLAG  FORM      1
INTAUDTY  DIM       1
LETNUMBR  DIM       3
LETPRNTR  DIM       6
LETTNCOD  FORM      3
LINCOUNT  FORM      1
MESSAGID  DIM       20
MIN       DIM       2
MINUTES   FORM      6
NMPNUMB   DIM       20
OTRFL017  DIM       8                       * Request for App. Canc Date
OTRFL018  DIM       3                       * Reason for request (Cat RQ)
OVERBOOK  FORM      1
RECCOUNT  FORM      10
REVWFLAG  FORM      1
SAVECTYP  DIM       6
SAVECODE  DIM       3
SAVSKEYS  DIM       25
SCHDCOPY  FORM      3
SCHDPRNT  DIM       6
SESSKEYS  DIM       25
SETDFPRG  DIM       8
SETDFRPT  FORM      2
STRTCTYP  DIM       6
SAVDISKY  DIM       17
SAVEREFL  DIM       8
SAVKEY28  DIM       28
SAVREPTN  FORM      2
SAVPRGID  DIM       8
SITECODE  DIM       6
SLOTOPT   DIM       3
SLOTSTAT  FORM      1
STARTDAT  DIM       8
TYPCOUNT  FORM      1
VISNUMBR  DIM       8
VSLOT     DIM       3
.
.
. PROGRAM CONSTANTS
. -----------------
ACCTYPE1  INIT      "001"
ACCTYPE2  INIT      "002"
ACCTYPE3  INIT      "003"
LETTRREF  INIT      "LETTRREF"
PIPE      INIT      "|"
.
.-----------------------------------------------------------------------
PRGID     INIT      "EXTRBOOK"
PRGNAM    INIT      "O/P Future Bookings Extraction"
VERSION   INIT      "V12.01.00"
.-----------------------------------------------------------------------
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * O/P Future bookings extract
.
MAIN1000  CALL      GSIT0000               * get site code
          BRANCH    EXIT,MAIN0100          * nothing or exitchar entered
.
          CALL      GSCT0000               * get starting clinic type
          BRANCH    EXIT,MAIN0100          * exitchar entered
.
          CALL      GECT0000               * get ending clinic type
          BRANCH    EXIT,MAIN0100          * exitchar entered
.
          CALL      GCAN0000               * get cancellation reason code
          BRANCH    EXIT,MAIN0100          * nothing or exitchar entered
.
          CALL      SDAT0000               * get extract start date
          BRANCH    EXIT,MAIN0100          * nothing or exitchar entered
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN5000  CALL      CFIL0000               * create file
          BRANCH    EXIT,MAIN0100          * file exists
.
          CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"outctyaf";
          OPEN      OUTCTYA1,"outctyaf"
.
          DISPLAY   *P64:24,"acccmtaf";
          OPEN      ACCCMTA1,"acccmtaf"
.
          DISPLAY   *P64:24,"allencaf";
          OPEN      ALLENCA1,"allencaf"
          OPEN      ALLENCA7,"allencaf"
.
          DISPLAY   *P64:24,"alllnkaf";
          OPEN      ALLLNKA1,"alllnkaf"
          OPEN      ALLLNKA2,"alllnkaf"
.
          DISPLAY   *P64:24,"allrefaf";
          OPEN      ALLREFA1,"allrefaf"
.
          DISPLAY   *P64:24,"comdepaf";
          OPEN      COMDEPA2,"comdepaf"
.
          DISPLAY   *P64:24,"dismasaf";
          OPEN      DISMASA1,"dismasaf"
.
          DISPLAY   *P64:24,"dispataf";
          OPEN      DISPATA1,"dispataf"
          OPEN      DISPATA2,"dispataf"
.
          DISPLAY   *P64:24,"disptlaf";
          OPEN      DISPTLA1,"disptlaf"
.
          DISPLAY   *P64:24,"hicclmaf";
          OPEN      HICCLMA2,"hicclmaf"
.
          DISPLAY   *P64:24,"vismdtaf";
          OPEN      VISMDTA1,"vismdtaf"
          DISPLAY   *P64:24,"vismtxaf";
          OPEN      VISMTXA1,"vismtxaf"
.
          DISPLAY   *P64:24,"outlpctf";
          OPEN      OUTLPCA1,"outlpcaf"
.
          DISPLAY   *P64:24,"outrf1af";
          OPEN      OUTRF1A1,"outrf1af"
.
          DISPLAY   *P64:24,"outpreaf";
          OPEN      OUTPREA1,"outpreaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patipenf";
          OPEN      PATIPEN1,"patipenf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmsmtiaf";
          OPEN      PMSMTIA1,"pmsmtiaf"
.
          DISPLAY   *P64:24,"pmsaidaf";
          OPEN      PMSAIDA1,"pmsaidaf"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pmstspaf";
          OPEN      PMSTSPA1,"pmstspaf"
.
          DISPLAY   *P64:24,"viscmtaf";
          OPEN      VISCMTA1,"viscmtaf"
.
          DISPLAY   *P64:24,"visiauaf";
          OPEN      VISIAUA1,"visiauaf"
.
          DISPLAY   *P64:24,"visintaf";
          OPEN      VISINTA1,"visintaf"
.
          MOVE      ONE,CNEWDS
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS:
                                  *85,IBCNUGST:
                                  *132,IBCNUMCI
          READ      CONTROLF,TWENTY7;*46,HBWAIT
          READ      CONTROLF,THIRTY1;*51,HOAUDA,*52,HOAUDB
          READ      CONTROLF,THIRTY2;*108,OTCNCREV,OTCNCSUM,*111,OTCNAUPD:
                                     *115,OTCNRSST,*122,OTCNMTVS,*124,OTCNMTVE:
                                     *128,OTCNREVC,*131,OTCNINTR,*132,OTCNDACI:
                                     *138,OTCNRDWB,*141,OTCNHSEC
          READ      CONTROLF,SEVENTY9;*248,PTCNIPEN
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
.
          IF        HBWAIT=1
            OPEN      WATOPAA1,"watopaaf"
            OPEN      WATOPAA2,"watopaaf"
            OPEN      WATOPSA1,"watopsaf"
            OPEN      WATOPSA2,"watopsaf"
            OPEN      WATTR1A1,"wattr1af"
            OPEN      WATTX1A1,"wattx1af"
            OPEN      WATMESA1,"watmesaf"
          ENDIF
.
.         If NZ, open files for ACC data
.
          IF        PTCNHDPS = 1
            DISPLAY   *P64:24,"patwc1af";
            OPEN      PATWC1A1,"patwc1af"
.
            DISPLAY   *P64:24,"pmswx1af";
            OPEN      PMSWX1A1,"pmswx1af"
          ENDIF
.
          MOVE      SP70,SAVSKEYS
          MOVE      SP70,SESSKEYS
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run data extraction                     *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". O/P Future Bookings ":
                                           "Data Extract & Cancel":
                    *P1:6,*V2LON,TWO,*HOFF,". O/P Future Bookings Report Only"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run extraction
                            OPTN9000             * run report
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                                  CFIL0000       Called by: MAIN0000       *
.*                         Create the extract file                           *
.*****************************************************************************
.
CFIL0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          IF        COPTION = 1
            OPEN      BOOKTEXT,"convbook"
          ELSE
            OPEN      BOOKTEXT,"convbook.tst"
          ENDIF
          TRAPCLR   IO
          BRANCH    OVRCD,CFIL5000
.
          DISPLAY   *P1:23,*B,*EF,"The extract file convbook.";
          IF        COPTION = 1
            DISPLAY   "txt";
          ELSE
            DISPLAY   "tst";
          ENDIF
          DISPLAY   " already exists."
CFIL1000  KEYIN     *P1:24,"Overwrite it (",*V2LON,*DV,ANSY,*HOFF,"/":
                    *V2LON,*DV,ANSN,*HOFF,")?":
                    *P21:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS                * "Y" entered ?
          GOTO      CFIL2000 IF EQUAL       * yes - continue
.
          MATCH     ANSN,ANS                * "N" entered ?
          GOTO      CFIL9100 IF EQUAL       * no
.
          BEEP
          GOTO      CFIL1000
.
.         Delete the existing file
.
CFIL2000  CLOSE     BOOKTEXT,DELETE
.
.         Create a new extract file
.
CFIL5000  IF        COPTION = 1
            PREP      BOOKTEXT,"convbook"
          ELSE
            PREP      BOOKTEXT,"convbook.tst"
          ENDIF
.
.         Open the O/P site related files
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,OSTFILE,FILBOKA1
          OPEN      OUTBOKA1,CFNAME         
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open O/P Booking A file.  ";
            CALL      HITENTER
            GOTO      CFIL9100
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,OSTFILE,FILBOKA5
          OPEN      OUTBOKA5,CFNAME         
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open O/P Booking A file.  ";
            CALL      HITENTER
            GOTO      CFIL9100
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,OSTFILE,FILBB1A1
          OPEN      OUTBB1A1,CFNAME         
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open O/P Booking B file.  ";
            CALL      HITENTER
            GOTO      CFIL9100
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,OSTFILE,FILDIAG1
          OPEN      OUTDIAG1,CFNAME         
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open O/P Diagnosis file.  ";
            CALL      HITENTER
            GOTO      CFIL9100
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,OSTFILE,FILCANA1
          OPEN      OUTCANA1,CFNAME         
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open O/P Cancellation file.  ";
            CALL      HITENTER
            GOTO      CFIL9100
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,OSTFILE,FILCTYA1
          OPEN      OUTCTYA1,CFNAME
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open O/P Clinic Type file.  ";
            CALL      HITENTER
            GOTO      CFIL9100
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,OSTFILE,FILLKBA1
          OPEN      OUTLKBA1,CFNAME
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open O/P LKB file.  ";
            CALL      HITENTER
            GOTO      CFIL9100
          ENDIF
          OPEN      OUTLKBA2,CFNAME
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,OSTFILE,FILSESA1
          OPEN      OUTSESA1,CFNAME
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open O/P Sessions file.  ";
            CALL      HITENTER
            GOTO      CFIL9100
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,OSTFILE,FILMSPA1
          OPEN      OUTMSPA1,CFNAME
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open O/P Suspensions file.  ";
            CALL      HITENTER
            GOTO      CFIL9100
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,OSTFILE,FILBITA1
          OPEN      OUTBITA1,CFNAME
          TRAPCLR   IO
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,OSTFILE,FILBOKC1
          OPEN      OUTBOKC1,CFNAME
          TRAPCLR   IO
.
          IF        HOAUDA=0
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            PACK      CFNAME,OSTFILE,FILAUDBA
            CLOSE     OUTAUDBA
            OPEN      OUTAUDBA,CFNAME
            TRAPCLR   IO
          ENDIF
.
          IF        HOAUDB=0
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            PACK      CFNAME,OSTFILE,FILAUBB1
            CLOSE     OUTAUBB1
            OPEN      OUTAUBB1,CFNAME      * 0 = use audit, 1 = not required
            TRAPCLR   IO
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,OSTFILE,FILHEDA1  * Get the name of the HEDA1 file
          CLOSE     OUTHEDA1
          OPEN      OUTHEDA1,CFNAME           * Open the file
          TRAPCLR   IO
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,OSTFILE,FILMA1A1
          CLOSE     OUTMA1A1
          OPEN      OUTMA1A1,CFNAME           * Open the file
          TRAPCLR   IO
.
          MOVE      ZERO,EXIT
          GOTO      CFIL9999
.
CFIL9100  MOVE      ONE,EXIT
.
CFIL9999  RETURN
.
.-----------------------------------------------------------------------
. Extract all the active visit comment details
.-----------------------------------------------------------------------
EXCMTA00  MOVE      OBAOUTNO,KEY8
          PACK      KEY17,KEY8,SP70
          CALL      RSVSMDT1
EXCMTA10  CALL      RKVSMDT1
          BRANCH    OVRCD,EXCMTA99
.
          MATCH     KEY8,VSMDVISN
          GOTO      EXCMTA99 IF NOT EQUAL
.
          ADD       ONE,PRCCNTHR
.
          MATCH     "1",VSMDDELT            * is comment deleted
          GOTO      EXCMTA10 IF EQUAL
.
. Extract comment header
.
          MOVE      "2",BOOKTYPE
          MOVE      VSMDVISN,CSMDVISN
          MOVE      VSMDTYPE,CSMDTYPE
          MOVE      VSMDNOTE,CSMDNOTE
          MOVE      VSMDDATE,CSMDDATE
          MOVE      VSMDTIME,CSMDTIME
          MOVE      VSMDUSER,CSMDUSER
          MOVE      VSMDOCCG,CSMDOCCG
          MOVE      VSMDDELT,CSMDDELT
          MOVE      VSMDDDAT,CSMDDDAT
          MOVE      VSMDDTIM,CSMDDTIM
          MOVE      VSMDDUSE,CSMDDUSE
          MOVE      VSMDDOCC,CSMDDOCC
.
          SQUEEZE   CSMDVISN
          SQUEEZE   CSMDTYPE
          SQUEEZE   CSMDNOTE
          SQUEEZE   CSMDDATE
          SQUEEZE   CSMDTIME
          SQUEEZE   CSMDUSER
          SQUEEZE   CSMDOCCG
          SQUEEZE   CSMDDELT
          SQUEEZE   CSMDDDAT
          SQUEEZE   CSMDDTIM
          SQUEEZE   CSMDDUSE
          SQUEEZE   CSMDDOCC
.
          WRITE     BOOKTEXT,SEQ;*+,BOOKTYPE,PIPE,BOOKURNO,PIPE,CSMDVISN,PIPE:
                                    CSMDTYPE,PIPE,CSMDNOTE,PIPE,CSMDDATE,PIPE:
                                    CSMDTIME,PIPE,CSMDUSER,PIPE,CSMDOCCG,PIPE:
                                    CSMDDELT,PIPE,CSMDDDAT,PIPE,CSMDDTIM,PIPE:
                                    CSMDDUSE,PIPE,CSMDDOCC,*-
.     
          ADD       ONE,EXTCNTHR
.
          CALL      EXCMXA00
.
          GOTO      EXCMTA10
.
EXCMTA99  RETURN
.
.---------------------------------------------------------------------
. Extract visit comment text details
.---------------------------------------------------------------------
EXCMXA00  PACK      KEY20,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
          CALL      RSVSMTX1
EXCMXA20  CALL      RKVSMTX1
          BRANCH    OVRCD,EXCMXA99
.
          MATCH     VSMDVISN,VSMTVISN
          GOTO      EXCMXA99 IF NOT EQUAL
.
          MATCH     VSMDTYPE,VSMTTYPE
          GOTO      EXCMXA99 IF NOT EQUAL
.
          MATCH     VSMDNOTE,VSMTNOTE
          GOTO      EXCMXA99 IF NOT EQUAL
.
          ADD       ONE,PRCCNTDT
.
. Extract comment details
.
          MOVE      "3",BOOKTYPE
          MOVE      VSMTVISN,CSMTVISN
          MOVE      VSMTTYPE,CSMTTYPE
          MOVE      VSMTNOTE,CSMTNOTE
          MOVE      VSMTUNIQ,CSMTUNIQ
          MOVE      VSMTCMNT,CSMTCMNT
.
          SQUEEZE   CSMTVISN
          SQUEEZE   CSMTTYPE
          SQUEEZE   CSMTNOTE
          SQUEEZE   CSMTUNIQ 
          STRIP     CSMTCMNT
.
          WRITE     BOOKTEXT,SEQ;*+,BOOKTYPE,PIPE,BOOKURNO,PIPE,CSMTVISN,PIPE:
                                    CSMTTYPE,PIPE,CSMTNOTE,PIPE,CSMTUNIQ,PIPE:
                                    CSMTCMNT,*-
          ADD       ONE,EXTCNTDT
.
          GOTO      EXCMXA20
.
EXCMXA99  RETURN
+
.*****************************************************************************
.*                               GSIT0000              Called by: MAIN0000   *
.*                    Get the user to input the required O/P site code       *
.* Returns: EXIT  0 - Ok to continue                                         *
.*                1 - Nothing or exitchar entered                            *
.*          OSTSITE - Site required code                                     *
.*          OSTFILE - Site file prefix                                       *
.*****************************************************************************
.
GSIT0000  MOVE      TWENTY3,ECOL
          MOVE      TEN,EVERT
          DISPLAY   *P1:10,*EL,"O/P Site            :"
          MOVE      SP3,CKYIDEF3                 * no default value
          MOVE      ZERO,CKYIMAND                * not mandatory
.
          CALL      OUTSITKY
          BRANCH    EXIT,GSIT9100:               * nothing or spaces entered
                         GSIT9100                * exitchar entered
.
          MOVE      OSTSITE,IDDD
          DISPLAY   *P30:10,*EL,OSTDESC
.
          MOVE      ZERO,EXIT
          GOTO      GSIT9999
.
GSIT9100  MOVE      ONE,EXIT
.
GSIT9999  RETURN
+
.*****************************************************************************
.*                               GSCT0000              Called by: MAIN0000   *
.*                    Get the user to input the starting clinic type         *
.* Returns: EXIT  0 - Ok to continue                                         *
.*                1 - Exitchar entered                                       *
.*          STRTCTYP - Starting Clinic Type                                  *
.*****************************************************************************
.
GSCT0000  MOVE      TWENTY3,ECOL
          MOVE      TEN1,EVERT
          DISPLAY   *P1:11,*EL,"Start Clinic Type   :"
          MOVE      SP6,CKYIDEF6                 * no default value
          MOVE      ZERO,CKYIMAND                * not mandatory
.
          CALL      KYOUTCTY
          BRANCH    EXIT,GSCT8000:               * nothing or spaces entered
                         GSCT9100                * exitchar entered
.
          MOVE      OCTCTYP,STRTCTYP             * save code
          DISPLAY   *P30:11,*EL,OCTDESC
          GOTO      GSCT9000
.
GSCT8000  MOVE      SP6,STRTCTYP
          DISPLAY   *P23:11,*V2LON,"Start"
.
GSCT9000  MOVE      ZERO,EXIT
          GOTO      GSCT9999
.
GSCT9100  MOVE      ONE,EXIT
.
GSCT9999  RETURN
+
.*****************************************************************************
.*                               GECT0000              Called by: MAIN0000   *
.*                    Get the user to input the ending clinic type           *
.* Returns: EXIT  0 - Ok to continue                                         *
.*                1 - Exitchar entered                                       *
.*          ENDCTYP - Ending Clinic Type                                     *
.*****************************************************************************
.
GECT0000  MOVE      TWENTY3,ECOL
          MOVE      TEN2,EVERT
          DISPLAY   *P1:12,*EL,"End   Clinic Type   :"
          MOVE      SP6,CKYIDEF6                 * no default value
          MOVE      ZERO,CKYIMAND                * not mandatory
.
          CALL      KYOUTCTY
          BRANCH    EXIT,GECT8000:               * nothing or spaces entered
                         GECT9100                * exitchar entered
.
          MOVE      OCTCTYP,ENDCTYP              * save code
          DISPLAY   *P30:12,*EL,OCTDESC
.
          MATCH     STRTCTYP,ENDCTYP
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"End code less than start code.  ";
            CALL      HITENTER
            GOTO      GECT0000
          ENDIF
.
          GOTO      GECT9000
.
GECT8000  MOVE      "~~~~~~",ENDCTYP
          DISPLAY   *P23:12,*V2LON,"End"
.
GECT9000  MOVE      ZERO,EXIT
          GOTO      GECT9999
.
GECT9100  MOVE      ONE,EXIT
.
GECT9999  RETURN
+
.*****************************************************************************
.*                               GCAN0000              Called by: MAIN0000   *
.*                    Get the user to input the cancellation code            *
.* Returns: EXIT  0 - Ok to continue                                         *
.*                1 - Nothing or exitchar entered                            *
.*          SAVECODE - Cancellation code (Cat CB)                            *
.*****************************************************************************
.
GCAN0000  BRANCH    COPTION,GCAN1000:            * Extract and Cancel
                            GCAN9000             * Report Only
.
GCAN1000  MOVE      TWENTY3,ECOL
          MOVE      TEN4,EVERT
          PACK      CODE,ANSC,ANSB
          DISPLAY   *P1:14,"Cancellation Reason :"
          MOVE      SP3,CKYIDEF3                 * no default value
          MOVE      ZERO,CKYIMAND                * not mandatory
.
          CALL      PATCODKY
          BRANCH    EXIT,GCAN9100:               * nothing or spaces entered
                         GCAN9100                * exitchar entered
.
          MOVE      ACODE,SAVECODE               * save code
          DISPLAY   *P30:14,*EL,TDESC
.
GCAN9000  MOVE      ZERO,EXIT
          GOTO      GCAN9999
.
GCAN9100  MOVE      ONE,EXIT
.
GCAN9999  RETURN
+
.****************************************************************************
.*                             SDAT0000                Called by: MAIN0000  *
.*                       Get extract booking starting date                  *
.* Returns: CURRDATE - current date (ccyymmdd)                              *
.*          CURRTIME - current time (hh:mm:ss)                              *
.*          STARTDAT - Start extract booking date (ccyymmdd)                *
.****************************************************************************
.
SDAT0000  DISPLAY   *P1:15,*EF,"From Booking Date   :"
          MOVE      ONE,CCENTRY
          MOVE      TEN5,CVERT
          MOVE      TWENTY3,CCOL
.
          CALL      IBACLOCK                     * get current date/time
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          MOVE      CTIMEIS,CURRTIME
          REP       " 0",CURRTIME
          MOVE      SP2,CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,SDAT0000
          PACK      STARTDAT,CCENT,CYEAR,CMON,CDAY
.
          MATCH     STARTDAT,CURRDATE            * date in future ?
          GOTO      SDAT9999 IF LESS             * yes
.
          DISPLAY   *P1:24,*EL,*B,"Date must be in the future.  ";
          CALL      HITENTER
          GOTO      SDAT0000
.
SDAT9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*   Loop through the O/P Future Bookings file and extract each record       *
.*   for each site.                                                          *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,RECCOUNT                * initialise record count
          MOVE      ZERO,EXTCOUNT                * initialise extract count
          MOVE      ZERO,ACCCOUNT                * initialise acc count
          MOVE      ZERO,PRCCNTHR
          MOVE      ZERO,PRCCNTDT
          MOVE      ZERO,EXTCNTHR
          MOVE      ZERO,EXTCNTDT
          MOVE      STRTCTYP,SAVECTYP            * initialise saved clinic type
          DISPLAY   *P1:24,*EL,"Processing:":
                    *P13:24,*V2LON,SAVECTYP;
.
PROC0100  PACK      KEY35,SITECODE,SAVECTYP,STARTDAT,SP70
          CALL      RDSBOKA5                     * pos'n at start of site/clinic
PROC0500  CALL      RDKBOKA5                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          MATCH     SITECODE,OBASITE             * same site still ?
          GOTO      PROC9000 IF NOT EQUAL        * no - finished
.
          MATCH     SAVECTYP,OBACTYP             * same clinic type still ?
          IF        !@EQUAL
            MATCH     OBACTYP,ENDCTYP            * no - end clinic type reached?
            GOTO      PROC9000 IF LESS           * yes - finished
.
            MOVE      OBACTYP,SAVECTYP           * no - save new clinic type
            DISPLAY   *P13:24,*V2LON,SAVECTYP;
            GOTO      PROC0100
          ENDIF
.
          BRANCH    OBASTAT,PROC1000             * booked
          GOTO      PROC0500
.
.         Get the corresponding outbb1af record
.
PROC1000  MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1                      * booking B record found ?
          BRANCH    OVRCD,PROC0500               * no - ignore booking
.
.         Get the visit extension record for the visit
.
          MOVE      SP3,PMVXINTR
          MOVE      OBAOUTNO,KEY8
          CALL      RDPMVX11                     * visit extension found ?
.
.         See if the O/P visit was linked to an A/H referral
.
          MOVE      SP8,SAVEREFL
          PACK      KEY16,OBAOUTNO,SP20
          CALL      RSALLNK2                     * pos'n before O/P visit number
          CALL      RKALLNK2                     * read next record
          BRANCH    OVRCD,PROC1100
.
          MOVE      OBAOUTNO,DIM8
          MATCH     DIM8,ALLNLNKV                * same O/P visit number ?
          GOTO      PROC1100 IF NOT EQUAL        * no
.
          MOVE      ALLNVISN,SAVEREFL            * save referral visit number
.
PROC1100  MOVE      SP9,OPDICODE
          MOVE      SP70,OPDIDESC
          MOVE      OBAOUTNO,KEY8
          CALL      RDDIAG1
.
          IF        COPTION = 1
            PACK      SAVKEY28,OBASITE,OBACLIN,OBADATE,OBASTRT,DOBASLOT
          ENDIF
.
          ADD       ONE,RECCOUNT                 * increment record counter
.
          DISPLAY   *P13:24,*V2LON,RECCOUNT
.
          MOVE      "0",BOOKTYPE
          MOVE      OBAURNO,BOOKURNO
          SQUEEZE   BOOKURNO
          MOVE      OBASITE,BOOKSITE
          SQUEEZE   BOOKSITE
          MOVE      OBACLIN,BOOKCLIN
          SQUEEZE   BOOKCLIN
          MOVE      OBADATE,BOOKDATE
          SQUEEZE   BOOKDATE
          MOVE      OBATIME,BOOKTIME
          SQUEEZE   BOOKTIME
          MOVE      OBAVISIT,BOOKVIST
          SQUEEZE   BOOKVIST
          UNPACK    OBABKDTE,BOOKCDAT,BOOKCTIM
          MOVE      OBASRCR,BOOKSRCR
          SQUEEZE   BOOKSRCR
          MOVE      OBACOMP,BOOKCOMP
          SQUEEZE   BOOKCOMP
          MOVE      OBAPRTY,BOOKPRTY
          SQUEEZE   BOOKPRTY
          MOVE      OBOUSR1,BOOKUSR1
          SQUEEZE   BOOKUSR1
          MOVE      OTBBDEPT,BOOKDEPT
          SQUEEZE   BOOKDEPT
          MOVE      OTBBTRAN,BOOKTRAN
          SQUEEZE   BOOKTRAN
          MOVE      OTBBRFGP,BOOKRFGP
          SQUEEZE   BOOKRFGP
          MOVE      OTBBSPCA,BOOKSPCA
          SQUEEZE   BOOKSPCA
          MOVE      PMVXUDF1,BOOKPCOM
          STRIP     BOOKPCOM
          MOVE      OPDICODE,BOOKDIAG
          SQUEEZE   BOOKDIAG
          MOVE      OPDIDESC,BOOKDDES
          STRIP     BOOKDDES
          MOVE      OBAOUTNO,BOOKAVIS
          SQUEEZE   BOOKAVIS
          MOVE      SAVEREFL,BOOKRVIS
          SQUEEZE   BOOKRVIS
.
          MATCH     "1",OTBBSNDL
          IF        @EQUAL
            MOVE      ANSY,BOOKSNDL
          ELSE
            MOVE      ANSN,BOOKSNDL
          ENDIF
.
          MATCH     "1",OTBBSNGL
          IF        @EQUAL
            MOVE      ANSY,BOOKSNGL
          ELSE
            MOVE      ANSN,BOOKSNGL
          ENDIF
.
          MATCH     "1",PMVXINTR
          IF        @EQUAL
            MOVE      ANSY,BOOKINTR
          ELSE
            MOVE      ANSN,BOOKINTR
          ENDIF
.
          MOVE      OTBBPURC,BOOKPURC
          SQUEEZE   BOOKPURC
          MOVE      OTBBOUTC,BOOKOUTC
          SQUEEZE   BOOKOUTC
          MOVE      OTBBCONT,BOOKCONT
          SQUEEZE   BOOKCONT
.
          MATCH     "1",PMVXCONF
          IF        @EQUAL
            MOVE      ANSY,BOOKCONF
          ELSE
            MOVE      ANSN,BOOKCONF
          ENDIF
.
          MOVE      PMVXCNID,BOOKCNID
          SQUEEZE   BOOKCNID
          MOVE      PMVXCNDT,BOOKCNDT
          SQUEEZE   BOOKCNDT
.
          CALL      GINS0000                     * get instructions
.
          WRITE     BOOKTEXT,SEQ;*+,BOOKTYPE,PIPE,BOOKURNO,PIPE,BOOKSITE,PIPE:
                                    BOOKCLIN,PIPE,BOOKDATE,PIPE,BOOKTIME,PIPE:
                                    BOOKVIST,PIPE,BOOKCDAT,PIPE,BOOKCTIM,PIPE:
                                    BOOKSRCR,PIPE,BOOKCOMP,PIPE,BOOKPRTY,PIPE:
                                    BOOKUSR1,PIPE,BOOKDEPT,PIPE,BOOKTRAN,PIPE:
                                    BOOKRFGP,PIPE,BOOKSNDL,PIPE,BOOKSPCA,PIPE:
                                    BOOKPCOM,PIPE,BOOKDIAG,PIPE,BOOKDDES,PIPE:
                                    BOOKAVIS,PIPE,BOOKRVIS,PIPE:
                                    BOOKINST,PIPE,BOOKSNGL,PIPE,BOOKINTR,PIPE:
                                    BOOKPURC,PIPE,BOOKOUTC,PIPE,BOOKCONT,PIPE:
                                    BOOKCONF,PIPE,BOOKCNID,PIPE,BOOKCNDT,*-
.
          ADD       ONE,EXTCOUNT                 * increment extract count
.
          CALL      EXCMTA00                     * get visit comments
.
.         Now get matching ACC data record for NZ sites only
.
          MOVE      OBAOUTNO,VISNUMBR
          CALL      EXACCDAT                     * extract ACC data for NZ only
          BRANCH    EXIT,PROC8000:               * no ACC data
                         PROC8000                * not NZ
.
          MOVE      "1",BOOKTYPE
.
          WRITE     BOOKTEXT,SEQ;*+,BOOKTYPE,PIPE,BOOKURNO,PIPE:
                                    ACCDNAME,PIPE,ACCDADD1,PIPE,ACCDADD2,PIPE:
                                    ACCDADD3,PIPE,ACCDADD4,PIPE,ACCDPOST,PIPE:
                                    ACCDTELP,PIPE,ACCDACDT,PIPE,ACCDACPT,PIPE:
                                    ACCDINSR,PIPE,ACCDCLNO,PIPE,ACCDCOM1,PIPE:
                                    ACCDCOM2,PIPE,ACCDALOC,PIPE,ACCDCINJ,PIPE:
                                    ACCDICOD,PIPE,ACCDCNAM,PIPE,ACCDCDTE,PIPE:
                                    ACCDCTIM,PIPE,ACCDWEBC,PIPE,ACCDLUPD,PIPE:
                                    ACCDLUPT,PIPE,ACCDWEBU,PIPE,ACCDACCF,PIPE:
                                    ACCDPLIN,PIPE,ACCDACTI,PIPE,ACCDADTE,PIPE:
                                    ACCDATME,PIPE,ACCDACLC,PIPE,ACCDAINZ,PIPE:
                                    ACCDMOVV,PIPE,ACCDSPTI,PIPE,ACCDSPRT,PIPE:
                                    ACCDRECI,PIPE,ACCDTRIC,PIPE,ACCDESTA,PIPE:
                                    ACCDPDDT,PIPE,ACCDARG1,PIPE,ACCDARG2,PIPE:
                                    ACCDARSN,PIPE,ACCDARTL,PIPE,ACCDARRP,PIPE:
                                    ACCDASST,PIPE,ACCDNEED,PIPE,ACCDCONT,PIPE:
                                    ACCDALTW,PIPE,ACCDTYPA,PIPE,ACCDSALT,PIPE:
                                    ACCDHPDA,PIPE,ACCDUNFD,PIPE,ACCDUNFT,PIPE:
                                    ACCDFISD,PIPE,ACCDRNWD,PIPE,ACCDCSTA,PIPE:
                                    ACCDTWRK,PIPE,ACCDOEST,PIPE,ACCDTPRO,PIPE:
                                    ACCDECOD,PIPE,ACCDTYP1,PIPE,ACCDDAT1,PIPE:
                                    ACCDTYP2,PIPE,ACCDDAT2,PIPE,ACCDTYP3,PIPE:
                                    ACCDDAT3,*-
.
          ADD       ONE,ACCCOUNT                 * increment acc count
.
PROC8000  IF        COPTION = 1
            MOVE      "EXTRBOOK  ",WBSEUID
            CALL      CANS0000              * Cancel booking (from OUTWEB02)
          ENDIF
.
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:19,*EF,"Extraction complete.":
                    *P1:20,*V2LON,EXTCOUNT,*HOFF," O/P Future Booking records";
          IF        COPTION = 1
            DISPLAY   " extracted.";
          ELSE
            DISPLAY   " listed.";
          ENDIF
.
          DISPLAY   *P1:21,*V2LON,ACCCOUNT,*HOFF," O/P Booking ACC records ";
          IF        COPTION = 1
            DISPLAY   "extracted.";
          ELSE
            DISPLAY   "listed.";
          ENDIF
.
          DISPLAY   *P1:22,*V2LON,EXTCNTHR,*HOFF," Visit Comment Header records ";
          IF        COPTION = 1
            DISPLAY   "extracted.";
          ELSE
            DISPLAY   "listed.";
          ENDIF
.
          DISPLAY   *P1:23,*V2LON,EXTCNTDT,*HOFF," Visit Comment Detail records ";
          IF        COPTION = 1
            DISPLAY   "extracted.";
          ELSE
            DISPLAY   "listed.";
          ENDIF
.
          DISPLAY   *P1:24;
          CALL      HITENTER
.
PROC9999  RETURN
+
.*******************************************************************************
.  Cancel a single booking - from OUTWEB02
.*******************************************************************************
CANS0000  MOVE      ZERO,OVERBOOK
          PACK      CASEKEYS,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT,SP70
.
CANS1000  CALL      CHKD0000                 * Check for deposits
          BRANCH    EXIT,CANS9940
.
          CALL      LOGC0000                 * Log the cancellation of booking
.
.>>>>     CALL      COPD0000                 * Write OPD Cancel action
.
          CALL      INTDEL00                 * Delete Interpretor Details
.
          CALL      CLET0000                 * Clear referral letter booking
.
.>>nz     IF        RETREFTW = 1
.>>nz       CALL      RETREW00               * Return linked Referral to Waiting
.>>nz     ENDIF
.
          CALL      DLNK0000                 * Delete A/H referral link
.
          CALL      DHIC0000                 * Delete HIC CMBS items (if any)
          CALL      DCHR0000                 * Delete any existing charges
.
          CALL      DTRN0000                 * Delete Transport Details(if any)
.
          MOVE      OBAOUTNO,KEY8            * Key for the bokb file
          CALL      DEBOKB1                  * Delete the bokb record
          MOVE      FOUR,AUDIFLAG            * set to a write audit
          CALL      AUDOTBOB                 * write the audit
.
          CALL      CWAT0000                 * Cancel waiting list link PAC
          CALL      CWAI0000                 * Cancel waiting list link PAS
.
          MOVE      ONE,REVWFLAG
          CALL      REVD0000                 * Update waiting list review date
.
          CALL      SCAN0000                 * Cancel series booking
.
          MOVE      OBAOUTNO,KEY8            * Key for the bokc file
          CALL      RAOTBOC1
          IF        OVRCD = 0
            CALL      DEOTBOC1               * Delete the bokc record
          ENDIF
.
          MOVE      OBAOUTNO,KEY8            * Key for the diagnosis file
          CALL      DEDIAG1                  * Delete the diagnosis record
.
          MOVE      OBAOUTNO,KEY8            * Key for the preaf file
          CALL      DEPREA1                  * Delete the preaf record
.
          MOVE      OBAOUTNO,KEY8            * Key for the visit extn file
          CALL      DEPMVX11                 * Delete the pmsvx1af record
.
          MOVE      CASEKEYS,KEY25
          CALL      RDSESA1
          BRANCH    OVRCD,CANS5000
.
CANS5000  MOVE      TWO,AUDIFLAG             * set to a before audit
          CALL      AUDOTBOA                 * write the audit
.
          CALL      RMDIS000                 * Remove Disaster Details
.
          MOVE      CASEKEYS,KEY28
          CALL      RDBOKA1                  * Position on the record
          BRANCH    OVRCD,CANS6000           * Check for extended slots
.
          PACK      KEY5,ANSC,ANSV,OBAVISIT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CANS5500
.
          MATCH     ANSZ,TCINDC2
          IF        @EQUAL
            MOVE      ONE,OVERBOOK           * Yes it is an over bookings
          ENDIF                              * so delete slots
.
CANS5500  CALL      CHKSUS00                 * Check Clinic Master Suspension
          IF        EXIT = 1
            MOVE      SIX,SLOTSTAT           * Suspended - set status to Susp.
          ELSE
            MOVE      ZERO,SLOTSTAT          * Not Suspended - reset status
          ENDIF
.
          MOVE      SLOTSTAT,OBASTAT         * Set slot status
          MOVE      ZEROUR,OBAURNO           * Set the U/R Number back to zero
          MOVE      ZEROVISN,OBAOUTNO        * Set the outpatient number to zero
          MOVE      SP20,OBABKDTE            * Reset the booking date
          MOVE      ZERO,OBAPULL             * Reset pulling list indicator
.
          IF        OTCNCREV=1
            MATCH     SP70,OTCNREVC
            IF        !@EQUAL & !@EOS
.
              MATCH     "000",OTCNREVC
              GOTO      CANS5550 IF EQUAL
.
              MOVE      OTCNREVC,OBAVISIT
              PACK      OBAVISIT,OBAVISIT,SP70
            ELSE
CANS5550      MOVE      "R  ",OBAVISIT         * Convert visit type to REVIEW
            ENDIF
          ENDIF
.
          IF        OVERBOOK=1
            PACK      KEY28,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT,SP70
            CALL      DEBOKA1                * Delete this over booking slot
          ELSE
            CALL      UPBOKA1                * Clear this slot
          ENDIF
.
          MOVE      THREE,AUDIFLAG           * set to an after audit
          CALL      AUDOTBOA                 * write the audit
.
.         Check for any extended slots that also need to be cleared
.
          PACK      SAVSKEYS,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
          CALL      RCAL0000               * Recalculate clinic counts as a
.
          MOVE      CASEKEYS,KEY25
          CALL      RDSESA1
          MOVE      CASEKEYS,KEY28
          CALL      RDBOKA1                  * Position on the record
          MOVE      OBASLOT,VSLOT            * Correct parent ?
.
CANS6000  CALL      RDKBOKA1                 * Get the next booking record
          BRANCH    OVRCD,CANS9000           * Booking cancelled.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     KEY25,CASEKEYS            * Correct session ?
          GOTO      CANS9000 IF NOT EQUAL    * No. Finished
          COMPARE   THREE,OBASTAT            * Is this an extended slot ?
          GOTO      CANS9000 IF NOT EQUAL    * No. Finished
.
          MOVE      OBAEXSL,SLOTOPT          * Move parent slot to a dim field
          MATCH     SLOTOPT,VSLOT            * Correct parent ?
          GOTO      CANS9000 IF NOT EQUAL    * No. Finished
.
          MOVE      TWO,AUDIFLAG             * set to a before audit
          CALL      AUDOTBOA                 * write the audit
.
          MOVE      ZEROUR,OBAURNO           * Set the U/R Number back to zero
          MOVE      ZEROVISN,OBAOUTNO        * Set the outpatient number to zero
          MOVE      SP20,OBABKDTE            * Reset the booking date
          MOVE      ZERO,OBAPULL             * Reset pulling list indicator
.
          IF        OTCNCREV=1
.
            MATCH     SP70,OTCNREVC
            IF        !@EQUAL & !@EOS
.
              MATCH     "000",OTCNREVC
              GOTO      CANS6010 IF EQUAL
.
              MOVE      OTCNREVC,OBAVISIT
              PACK      OBAVISIT,OBAVISIT,SP70
            ELSE
CANS6010      MOVE      "R  ",OBAVISIT         * Convert visit type to REVIEW
            ENDIF
.
            MOVE      ZERO,OBAEXSL           * Clear parent slot number
            MOVE      SLOTSTAT,OBASTAT       * Set slot status
          ENDIF
.
          IF        OVERBOOK=1
            PACK      KEY28,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT,SP70
            CALL      DEBOKA1                  * Clear this slot
            CALL      RDSBOKA1
          ELSE
            CALL      UPBOKA1                  * Clear this slot
          ENDIF
.
          MOVE      THREE,AUDIFLAG           * set to an after audit
          CALL      AUDOTBOA                 * write the audit
          GOTO      CANS6000                 * Check for another extended slot
.
CANS9000  MOVE      CASEKEYS,KEY28
          CALL      RDBOKA1                  * Position on the record
          IF        OVRCD=1
            CALL      CLOUTBOA               * Clear outbokaf variables
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      CANS9999
.
CANS9940  
.>>>>     MOVE      "Deposits Exist - Booking not cancelled",WEBTITLE
.>>>>     CALL      WEBERR00
.>>>>     MOVE      ONE,EXIT
          GOTO      CANS9999
.
CANS9999  RETURN
+
.*****************************************************************************
.*                                GINS0000         Called by: PROC0000       *
.*         Get any patient instructions associated with the booking          *
.* Returns:  BOOKINST - free format patient instructions                     *
.*****************************************************************************
.
GINS0000  CLEAR     BOOKINST
          MOVE      ONE,FOUNDFLG                 * initialise found flag
.
          PACK      KEY14,OBAOUTNO,ZERO,ONE,THREE,SP20
          CALL      RSVSCMT1                     * position on O/P visit
GINS0500  CALL      RKVSCMT1                     * read next record
          BRANCH    OVRCD,GINS9000               * eof - finished
.
          MOVE      OBAOUTNO,DIM8
          MATCH     DIM8,VSCTVIST                * same visit still ?
          GOTO      GINS9000 IF NOT EQUAL        * no - finished comments
.
          MATCH     "013",VSCTTYPE               * same visit type still ?
          GOTO      GINS9000 IF NOT EQUAL        * no - finished comments
.
          MOVE      ZERO,FOUNDFLG                * set found flag
.
          APPEND    VSCTDATA,BOOKINST            * load comment line
          GOTO      GINS0500
.
GINS9000  BRANCH    FOUNDFLG,GINS9999            * nothing found ?
.
          RESET     BOOKINST
          STRIP     BOOKINST
.
GINS9999  RETURN
+
.******************************************************************************
.*             LOGC0000: Write a cancellation record to outcanaf              *
.******************************************************************************
LOGC0000  CALL      CLOUTCAN
          MOVE      OBASITE,OPCNSITE
          MOVE      OBACLIN,OPCNCLIN
          MOVE      OBADATE,OPCNDATE
          MOVE      OBASTRT,OPCNSTRT
          MOVE      OBASLOT,OPCNSLOT
          MOVE      OBATIME,OPCNTIME
          MOVE      OBAOUTNO,OPCNOUTN
          MOVE      OBAURNO,OPCNURNO
          MOVE      SAVECODE,OPCNREAS
          MOVE      CURRDATE,OPCNRDTE
          MOVE      CURRTIME,OPCNRTIM
          MOVE      OBASRCR,OPCNSRCR
          MOVE      OBACOMP,OPCNCOMP
          MOVE      OBACLASS,OPCNCLAS
          MOVE      OBAINS,OPCNINS
          MOVE      OBACAT,OPCNCAT
          MOVE      OBAPRTY,OPCNPRTY
          MOVE      PASSCODE,OPCNOPER
          MOVE      OTBBTRAN,OPCNTRAN
          MOVE      OBAVISIT,OPCNVTYP
          MOVE      OBACTYP,OPCNCTYP
          MOVE      OTBBNMDS,OPCNNMDS
          MOVE      OTBBSRVD,OPCNSRVC
          MOVE      OTBBCART,OPCNCART
          MOVE      SAVEREFL,OPCNREFN
          MOVE      OBABKDTE,OPCNBKDT
          MOVE      SP70,OPCNSPAR
.
          MOVE      OPCNOUTN,KEY8
          CALL      RAOTCAN1
          IF        OVRCD = 1
            CALL      WROTCAN1                 * write cancellation record
          ELSE
            CALL      UPOTCAN1                 * update cancellation record
          ENDIF
.
          PACK      KEY8,OBAURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,LOGC9999          * invalid UR number
.
          PACK      KEY8,OBAURNO,SP70
          CALL      RDPMPX21                * read 2nd master file extension
          BRANCH    OVRCD,LOGC9999          * invalid UR number
.
.         Broadcast a message for the cancellation
.
          CALL      PMIGTNID                 * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      ONE,HL7TRGID
          MOVE      SP8,HL7INCLD
          PROC      DGCLICOC                 * broadcast cancellation(ADT^A38)
.
LOGC9999  RETURN
+
.*******************************************************************************
. Delete Interpreter Table Record
. Input  - OBAURNO
.          OBAOUTNO
.*******************************************************************************
INTDEL00  PACK      KEY8,OBAURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,INTDEL99
.
          MATCH     "1",OTCNINTR            * Using visit based interpreter req
          GOTO      INTDEL10 IF EQUAL
.
          MATCH     "1",PMPXINTR
          GOTO      INTDEL99 IF NOT EQUAL
.
INTDEL10  PACK      KEY8,OBAOUTNO
          CALL      RDVSINT1
          IF        OVRCD=0
            CALL      DEVSINT1           * Delete record from Int.Table(Cancel)
            MOVE      "D",INTAUDTY
            CALL      INTAUD00           * Add record to Int.Audit after Delete
          ENDIF
          MOVE      ZERO,EXIT
INTDEL99  RETURN
+
.*******************************************************************************
.  CLET0000 - Clear the referral letter booked site and clinic if an appointment
.             is cancelled
.*******************************************************************************
CLET0000  READ      CONTROLF,THIRTY1;*174,OTCNULET
          COMPARE   ONE,OTCNULET
          GOTO      CLET9999 IF NOT EQUAL   * not using referral letters
.
          MOVE      OBAOUTNO,KEY8
          CALL      RDOTRFL1
          BRANCH    OVRCD,CLET9999          * not a referral
.
          MOVE      SP70,OTRLBSIT
          MOVE      SP70,OTRLBCLI
          MOVE      SP70,OTRLBDTE
          MOVE      SP70,OTRLBSTR
          MOVE      SP70,OTRLBSLT
.
          MOVE      Z70,OTRFL017
          MATCH     Z70,OTRFL017
          IF        !@EQUAL
            MOVE      OTRFL017,OTRLCDTE     * Cancellation Date
          ENDIF
.
          MOVE      Z70,OTRFL018
          MATCH     Z70,OTRFL018
          IF        !@EQUAL
            MOVE      OTRFL018,OTRLREAS     * Cancellation Reason
          ENDIF
.
          PACK      OTRLUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OTRLUDAT
          CLOCK     TIME,OTRLUTIM
          MOVE      WBSEUID,OTRLUUID
.
          CALL      UPOTRFL1
.
.>>>>     CALL      PRTLET00               * Print referral letter
.
CLET9999  RETURN
+
.*******************************************************************************
. DLNK0000 - Delete allied health referral letter link
.*******************************************************************************
DLNK0000  PACK      KEY16,OBAOUTNO,SP70
          CALL      RSALLNK2
          CALL      RKALLNK2
          BRANCH    OVRCD,DLNK9999          * not a referral
.
          MATCH     DOBAOUTN,ALLNLNKV
          GOTO      DLNK9999 IF NOT EQUAL
.
          PACK      KEY16,ALLNVISN,ALLNLNKV,SP70
          CALL      DEALLNK1
.
          MATCH     "1",ALLNMOHR
            IF        @EQUAL
            MOVE      "2",ALLNMOHR           * set to 'Send Delete'
          ENDIF
          MOVE      FOUR,AUDTTYPE           * delete history record
          CALL      WAALLN00
.
.>>nz     COMPARE   ONE,RETREFTW            * Return Referral To Waiting?
.>>nz     GOTO      DLNK9999 IF EQUAL
.
          CALL      URSR0000                * Update Referral Status
.
DLNK9999  RETURN
+
.------------------------------------------------------------
.     Delete billing charges in pmsmtiaf file
.------------------------------------------------------------
DCHR0000  MOVE      ZERO,F2
          PACK      KEY14,OBAOUTNO,SP20
          CALL      RSPMMTI1
DCHR1000  CALL      RKPMMTI1
          BRANCH    OVRCD,DCHR3000
.
          MATCH     DOBAOUTN,PMMIVISN
          GOTO      DCHR3000 IF NOT EQUAL  * different outpatient number
          ADD       ONE,F2
.
          MATCH     SP9,PMMIITEM
          GOTO      DCHR1000 IF NOT EQUAL  * it's not an outpatient charge rec.
.
          SUB       ONE,F2
          PACK      KEY14,PMMIVISN,PMMITRAN
          CALL      DEPMMTI1
          GOTO      DCHR1000
.
DCHR3000  COMPARE   ZERO,F2
          GOTO      DCHR9999 IF NOT EQUAL     * still got charges
          IF        PTCNIPEN=0
            MOVE      OBAOUTNO,IPADMNO                             
            PACK      KEY8,IPADMNO             * Set key up
            CALL      DEIPEN1
          ENDIF
DCHR9999  RETURN
+
.------------------------------------------------------------
. Delete transport details when cancelling a booking
.------------------------------------------------------------
DTRN0000  PACK      KEY19,OBAURNO,OBAOUTNO,SP70
          CALL      RSPMTSP1
          CALL      RKPMTSP1
          BRANCH    OVRCD,DTRN9999
.
          MATCH     OBAURNO,PMTSURNO             * Correct U/R
          GOTO      DTRN9999 IF NOT EQUAL
.
          MATCH     DOBAOUTN,PMTSVISN            * Correct Visit
          GOTO      DTRN9999 IF NOT EQUAL
.
          PACK      KEY19,PMTSURNO,PMTSVISN,PMTSUNIQ,SP70
          CALL      DEPMTSP1                     * Delete Transport Details
.
          GOTO      DTRN0000
.
DTRN9999  RETURN
+
.------------------------------------------------------------------------------
. Cancel/DNA - Cancel waiting list link record PAC
.------------------------------------------------------------------------------
CWAT0000  COMPARE   ONE,HBWAIT            * Using w/list
          GOTO      CWAT9999 IF NOT EQUAL
.
          PACK      KEY8,OBAOUTNO,SP70
          CALL      RDWTOPA2
          BRANCH    OVRCD,CWAT9999
.
          PACK      KEY19,WTOPURNO,WTOPCODE,WTOPCOUN,SP70
          CALL      RDTREA1
          BRANCH    OVRCD,CWAT9999
.
          MOVE      SP70,WTWMREAD      * Clear Pre Adm date
          CALL      UPTREA1
.
          PACK      KEY19,WTOPURNO,WTOPCODE,WTOPCOUN,SP70
          CALL      RDWTTX11
          BRANCH    OVRCD,CWAT9999
.
          MOVE      WTOPOSTA,WTTXPAST  * Set back to original pre adm stat
          MOVE      SP70,WTTXPTIM      * Clear Pre Adm time
          CALL      UPWTTX11
.
          MOVE      ONE,WTOPSTAT       * Cancelled
          PACK      WTOPUDAT,CCC,CYY,CMM,CDD
          REP       " 0",WTOPUDAT
          CLOCK     TIME,WTOPUTIM
          MOVE      WBSEUID,WTOPUUID
          CALL      UPWTOPA2
.
CWAT9999  RETURN
+
.------------------------------------------------------------------------------
. Cancel/DNA - Cancel waiting list link record PAS
.------------------------------------------------------------------------------
CWAI0000  COMPARE   ONE,HBWAIT            * Using w/list
          GOTO      CWAI9999 IF NOT EQUAL
.
          PACK      KEY8,OBAOUTNO,SP70
          CALL      RDWTOPS2
          BRANCH    OVRCD,CWAI9999
.
          PACK      KEY19,WTOSURNO,WTOSCODE,WTOSCOUN,SP70
          CALL      RDTREA1
          BRANCH    OVRCD,CWAI9999
.
          MOVE      SP70,WTWMTRPA      * Clear Pre Anaesthetic date
          CALL      UPTREA1
.
          PACK      KEY19,WTOSURNO,WTOSCODE,WTOSCOUN,SP70
          CALL      RDWTTX11
          BRANCH    OVRCD,CWAI9999
.
          MOVE      WTOSOSTA,WTTXPANS  * Set back to original pre adm stat
          MOVE      SP70,WTTXPANT      * Clear Pre Adm time
          CALL      UPWTTX11
.
          MOVE      ONE,WTOSSTAT       * Cancelled
          PACK      WTOSUDAT,CCC,CYY,CMM,CDD
          REP       " 0",WTOSUDAT
          CLOCK     TIME,WTOSUTIM
          MOVE      WBSEUID,WTOSUUID
          CALL      UPWTOPS2
.
CWAI9999  RETURN
+
.------------------------------------------------------------------------------
. Update waiting list review date
. OTCNAUPD - Update waiting list outpatient review date set to yes
. REVWFLAG - 0 = Update review date
.            1 = Delete review date
.------------------------------------------------------------------------------
REVD0000  COMPARE   ONE,HBWAIT                   * Using w/list
          GOTO      REVD9999 IF NOT EQUAL
.
          MATCH     "1",OTCNAUPD                 * Not using auto update review
          GOTO      REVD9999 IF NOT EQUAL        * date
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          MATCH     CURRDATE,OBADATE             * Exit if booking date is
          GOTO      REVD9999 IF LESS             * less than today
.
          PACK      KEY12,OBASITE,OBACTYP,SP70
          CALL      RDCTYA1                      * Exit if invalid clinic type
          BRANCH    OVRCD,REVD9999
.
          MATCH     SP70,OCTWUNIT                * Exit if no W/List unit
          GOTO      REVD9999 IF EQUAL
.
          PACK      KEY19,OBAURNO,SP70
          CALL      RDSTREA1
REVD1000  CALL      RDKTREA1
          BRANCH    OVRCD,REVD9999
.
          MATCH     WMURNO,OBAURNO               * Select records that are
          GOTO      REVD9999 IF NOT EQUAL        * Unscheduled, Scheduled or
.                                                * Pre-Admitted
          BRANCH    WMSTAT,REVD2000,REVD2000,REVD2000
.
          GOTO      REVD1000
.
REVD2000  BRANCH    REVWFLAG,REVD6000            * Cancel booking
.
          MATCH     SP70,WMDATE2                 * Update if no review date
          GOTO      REVD5000 IF EQUAL
.
          MATCH     WMDATE2,OBADATE              * Skip if booking date is
          GOTO      REVD1000 IF EQUAL            * equal to review date
.
          MATCH     WMDATE2,OBADATE              * Exit if booking date is
          GOTO      REVD1000 IF LESS             * less than review date
.
REVD5000  MOVE      OBADATE,WMDATE2              * Set review date to book date
          GOTO      REVD8000
.
REVD6000  MATCH     SP70,WMDATE2                 * Skip if no review date to
          GOTO      REVD1000 IF EQUAL            * clear on cancel
.
          MOVE      SP70,WMDATE2                 * Clear review date
.
REVD8000  CALL      UPTREA1                      * Update waiting list
.
          GOTO      REVD1000                     * Check next record
.
REVD9999  RETURN
+
.**********************************************************************
.*  SCAN0000  - Update the status of a series booking to cancelled    *
.*              OTLKBSTA  1=Cancelled  0=Not Cancelled                *
.**********************************************************************
SCAN0000  PACK      KEY24,OBAURNO,OBAOUTNO,SP70 * Is this a master record
          CALL      RDOTLKB1
          BRANCH    OVRCD,SCAN1000
.
          MOVE      ONE,OTLKBSTA
          PACK      OTLKUDAT,CCC,CYY,CMM,CDD * current date
          REP       " 0",OTLKUDAT
          MOVE      CTIMEIS,OTLKUTIM
          MOVE      WBSEUID,OTLKUUID
          CALL      UPOTLKB1
          GOTO      SCAN9999
.
SCAN1000  PACK      KEY24,OBAURNO,OBAOUTNO,SP70 * Check non master records
          CALL      RSOTLKB2
          CALL      RKOTLKB2
          BRANCH    OVRCD,SCAN9999
.
          MATCH     OBAURNO,OTLKURNO
          GOTO      SCAN9999 IF NOT EQUAL
.
          MATCH     DOBAOUTN,OTLKLBOK
          GOTO      SCAN9999 IF NOT EQUAL
.
          MOVE      ONE,OTLKBSTA
          PACK      OTLKUDAT,CCC,CYY,CMM,CDD * current date
          REP       " 0",OTLKUDAT
          MOVE      CTIMEIS,OTLKUTIM
          MOVE      WBSEUID,OTLKUUID
          CALL      UPOTLKB2
.
SCAN9999  RETURN
+
.*******************************************************************************
. RMDIS000 - Remove disaster details when cancelling preadmission
.*******************************************************************************
..  First, delete the disaster link record if there is one
RMDIS000  PACK     SAVDISKY,SP70
          PACK     KEY25,OBAURNO,SP70
          CALL     RSDSPTL1
RMDIS100  CALL     RKDSPTL1
          BRANCH   OVRCD,RMDIS999
.
          MATCH    DSPLURNO,OBAURNO
          GOTO     RMDIS999 IF NOT EQUAL
.
          MATCH    DSPLVISN,DOBAOUTN
          GOTO     RMDIS100 IF NOT EQUAL
.
          PACK     KEY25,DSPLURNO,DSPLCODE,DSPLVISN,SP70
          CALL     DEDSPTL1
          PACK     SAVDISKY,DSPLURNO,DSPLCODE,SP70
.
..  If this was the only disaster link record, delete the patient level record
.
          PACK     KEY25,OBAURNO,SP70
          CALL     RSDSPTL1
          CALL     RKDSPTL1
          BRANCH   OVRCD,RMDIS200
.
          MATCH    DSPLURNO,OBAURNO
          GOTO     RMDIS200 IF NOT EQUAL
          GOTO     RMDIS999
.
RMDIS200  PACK     KEY17,SAVDISKY,SP70
          CALL     DEDSPAT1
          CALL     DELALT00
.
RMDIS999  RETURN
+
.------------------------------------------------------------
. Check for Master Suspension
.------------------------------------------------------------
CHKSUS00  MOVE      ZERO,EXIT
          PACK      KEY24,OSESITE,OSECLIN,OSEDAY,Z70
          CALL      RSOTMSP1
CHKSUS10  CALL      RPOTMSP1
          BRANCH    OVRCD,CHKSUS99
          MATCH     OSESITE,OTMSSITE
          GOTO      CHKSUS99 IF NOT EQUAL
          MATCH     OSECLIN,OTMSCLIN
          GOTO      CHKSUS99 IF NOT EQUAL
          MOVE      OSEDAY,KEY1
          MATCH     KEY1,OTMSDOWK
          GOTO      CHKSUS99 IF NOT EQUAL
.
          MATCH     OSESTRT,OTMSSTIM             * Same session start time
          GOTO      CHKSUS10 IF NOT EQUAL
.
.         MATCH     SP70,OTMSTDAT
.         IF        !@EQUAL
.           MATCH     OTMSTDAT,OSEDATE           * ????????????
.           GOTO      CHKSUS10 IF LESS
.         ENDIF
.
          MATCH     OTMSFDAT,OSEDATE
          GOTO      CHKSUS10 IF LESS
          GOTO      CHKSUS20 IF EQUAL
.
          MATCH     OSEDATE,OTMSTDAT
          GOTO      CHKSUS10 IF LESS
.
CHKSUS20  MOVE      ONE,EXIT
.
CHKSUS99  RETURN
+
.**********************************************************************
.*  CHKD0000  - Check if there are any deposits for this visit if so  *
.*              display an error                                      *
.**********************************************************************
CHKD0000  MOVE      ZERO,EXIT
          MOVE      "0",CMDESTAT
          PACK      KEY26,OBAOUTNO,CMDESTAT,SP70
          CALL      RSCMDEP2
          CALL      RKCMDEP2
          BRANCH    OVRCD,CHKD9999
.
          MATCH     DOBAOUTN,CMDEADMN            * Same outpatient booking
          GOTO      CHKD9999 IF NOT EQUAL
          MATCH     "0",CMDESTAT                 * Deposit held?
          GOTO      CHKD9999 IF NOT EQUAL        * so check for other deposits
.
CHKD9000  MOVE      ONE,EXIT
          GOTO      CHKD9999

CHKD9999  RETURN
+
.*******************************************************************************
. Update Interpreter Audit Table
.  Input - CASEKEYS  Key To Booking
.          INTAUDTY  Audit Type
.*******************************************************************************
INTAUD00  CALL      CLVISIAU
          PACK      VSIACDAT,CCC,CYY,CMM,CDD
          REP       " 0",VSIACDAT
          MOVE      CTIMEIS,VSIACTIM
          MOVE      INTAUDTY,VSIARTYP
.
          MOVE      VSINVISN,VSIAVISN
          MOVE      VSINDATE,VSIADATE
          MOVE      VSINTIME,VSIATIME
.....     MOVE      "2",VSIATYPE                                        D-40489
          MOVE      VSINTYPE,VSIATYPE                                  *I-40489
          MOVE      VSINSUBK,VSIASUBK
          MOVE      VSINWUID,VSIAWUID
          MOVE      PMPXLNG1,VSIALNG1
          MOVE      PMPXLNG2,VSIALNG2
          MOVE      PMPXLNG3,VSIALNG3
          MOVE      VSINNOTF,VSIANOTF
          MOVE      VSINCONF,VSIACONF
.
          PACK      KEY25,VSIACDAT,VSIACTIM,VSIARTYP,VSIAVISN,SP70
          CALL      RAVSIAU1
          IF        OVRCD=0
            CALL      UPVSIAU1
          ELSE
            CALL      WRVSIAU1
          ENDIF
          MOVE      ZERO,EXIT
INTAUD99  RETURN
+
.---------------------------------------------------------------------
. DELALT00 - Delete alternate ID record for disaster patients
.---------------------------------------------------------------------
DELALT00  UNPACK    SAVDISKY,DIM8,DIM9
          PACK      KEY9,DIM9,SP70
          CALL      RDDSMAS1
          BRANCH    OVRCD,DELALT99
.
          MATCH     SP70,DSMAAICD
          GOTO      DELALT99 IF EQUAL
          GOTO      DELALT99 IF EOS
.
          PACK      KEY31,DIM8,DSMAAICD,SP70
          CALL      RSPMAID1
          CALL      RKPMAID1
          BRANCH    OVRCD,DELALT99
.
          MATCH     PMAIURNO,DIM8
          GOTO      DELALT99 IF NOT EQUAL
.
          MATCH     PMAITYPE,DSMAAICD
          GOTO      DELALT99 IF NOT EQUAL
.
          PACK      KEY31,PMAIURNO,PMAITYPE,PMAIALID,SP70
          CALL      DEPMAID1
.
DELALT99  RETURN
+
.------------------------------------------------------------
. Delete CMBS items for booking for HIC
.------------------------------------------------------------
DHIC0000  PACK      KEY10,OBAOUTNO,SP70
          CALL      RSOTBIT1
          CALL      RKOTBIT1
          BRANCH    OVRCD,DHIC2000          * no items on file
.
          MATCH     DOBAOUTN,OTBIVISN
          GOTO      DHIC2000 IF NOT EQUAL
.
          PACK      KEY10,OTBIVISN,OTBIITMC
          CALL      DEOTBIT1
          GOTO      DHIC0000
.
.         Check if claim form had been printed
.
DHIC2000  MATCH     "1",IBCNUMCI            * Using medclaims
          GOTO      DHIC9999 IF NOT EQUAL
.
          OPEN      HICBCNA1,"hicbcnaf"
          OPEN      HICCITA1,"hiccitaf"
          PACK      KEY16,OBAOUTNO,SP70
          CALL      RSHCCLM2
          CALL      RKHCCLM2
          BRANCH    OVRCD,DHIC9999
          MATCH     DOBAOUTN,HCCLVISN       * Same visit
          GOTO      DHIC9999 IF NOT EQUAL
.
          MATCH     " 0",HCCLSTAT           * Status of Printed?
          GOTO      DHIC9999 IF NOT EQUAL   * No
.
.         Delete records from Item claims file
.
DHIC3000  PACK      KEY18,HCCLCLMN,HCCLVISN,SP70
          CALL      RSHCCIT1
          CALL      RKHCCIT1
          BRANCH    OVRCD,DHIC4000
          MATCH     HCCLCLMN,HCCICLMN       * same claim number?
          GOTO      DHIC4000 IF NOT EQUAL
          MATCH     HCCLVISN,HCCIVISN       * same visit number?
          GOTO      DHIC4000 IF NOT EQUAL
.
          PACK      KEY18,HCCICLMN,HCCIVISN,HCCITRAN
          CALL      DEHCCIT1
          GOTO      DHIC3000
.
DHIC4000  PACK      KEY16,HCCLVISN,HCCLCLMN
          CALL      DEHCCLM2
.
          PACK      KEY33,HCCLPMCI,HCCLPYEE,HCCLPSRV,HCCLBTCH
          CALL      RDHCBCN1
          BRANCH    OVRCD,DHIC9999
          SUB       ONE,HCBCBCNT            * subtract batch counter
          MOVE      "0",HCBCSTAT            * batch not completed
          CALL      UPHCBCN1
.
DHIC9999  RETURN
+
.------------------------------------------------------------
. Update referral status to waiting/active
.------------------------------------------------------------
URSR0000  MOVE      SP70,BOOKDATE
.
          PACK      KEY8,ALLNVISN,SP70      * Patient Referral Details
          CALL      RDALREF1
          BRANCH    OVRCD,URSR9999
.
          MATCH     "0",ALRESTAT           * Waiting
          GOTO      URSR1000 IF EQUAL
.
          MATCH     "1",ALRESTAT            * Active
          GOTO      URSR1000 IF EQUAL
.
          GOTO      URSR9999                * Not waiting/Active so exit
.
URSR1000  CALL      ARMHI000              * Set MHINC indicator
          PACK      KEY16,ALREVISN,SP70
          CALL      RSALLNK1
          CALL      RKALLNK1
          BRANCH    OVRCD,URSR2000
.
          MATCH     ALREVISN,ALLNVISN       * Linked bookings
          IF        @EQUAL
            CALL      DATR0000              * Get first booking date
            GOTO      URSR9100
          ENDIF
.
URSR2000  PACK      KEY16,ALREVISN,SP70
          CALL      RSALENC1
          CALL      RKALENC1
          BRANCH    OVRCD,URSR9000
.
          MATCH     ALREVISN,ALENVISN       * Encounters
          GOTO      URSR9200 IF EQUAL
.
URSR9000  MATCH     "0",ALRESTAT
          IF        !@EQUAL
            MOVE      TWO,AUDTTYPE            * before history record
            CALL      WAALRE00

.           MOVE      ZERO,ALRESTAT         * Update status to waiting
            MOVE      SP70,ALREREXP         * Clear expiry date if not linked
            CALL      UPALREF1
.
            CALL      PMIGTNID              * get national id for dgate write
            MOVE      NMPNUMB,PTNINMPI
            MOVE      TEN8,HL7TRGID
            MOVE      SP8,HL7INCLD
            CALL      DGCLII13              * broadcast Update Ref. (REF^I13)
.
            MOVE      THREE,AUDTTYPE        * after history record
            CALL      WAALRE00
          ENDIF
          GOTO        URSR9999
.
URSR9100  MOVE      TWO,AUDTTYPE            * before history record
          CALL      WAALRE00
.
          MOVE      ONE,ALRESTAT          * Update status to active
          CALL      CALX0000              * Calculate referral expiry date
          CALL      UPALREF1
.
          CALL      PMIGTNID                * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      TEN9,HL7TRGID
          MOVE      SP8,HL7INCLD
          CALL      DGCLII13                * broadcast Update Ref. (REF^I13)
.
          MOVE      THREE,AUDTTYPE          * after history record
          CALL      WAALRE00
          GOTO      URSR9999
.
URSR9200  MATCH     "1",ALRESTAT            * EOC only no linked referrals
          IF        !@EQUAL
            MOVE      TWO,AUDTTYPE            * before history record
            CALL      WAALRE00
.
            MOVE      ONE,ALRESTAT          * Update status to active
            MOVE      SP70,ALREREXP         * Clear expiry date if not linked
            CALL      UPALREF1
.
            CALL      PMIGTNID              * get national id for dgate write
            MOVE      NMPNUMB,PTNINMPI
            MOVE      TWENTY,HL7TRGID
            MOVE      SP8,HL7INCLD
            CALL      DGCLII13              * broadcast Update Ref. (REF^I13)
.
            MOVE      THREE,AUDTTYPE        * after history record
            CALL      WAALRE00
          ENDIF
          GOTO        URSR9999
.
URSR9999  RETURN
+
.------------------------------------------------------------
. Calculate referral expiry date if required
.------------------------------------------------------------
CALX0000  MATCH     SP70,ALRERTYP           * No referral type
          GOTO      CALX9999 IF EQUAL
.
          MATCH     SP70,BOOKDATE           * No booking date
          GOTO      CALX9999 IF EQUAL
.
          PACK      KEY5,ANSR,ANSI,ALRERTYP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CALX9999
.
          COMPARE   ZERO,TCFORM6            * No days for expiry warning
          GOTO      CALX9999 IF EQUAL
.
          MOVE      BOOKDATE,EXPRDATE
          ADD       TCFORM6,EXPRDATE
          MOVE      EXPRDATE,ALREREXP
.
CALX9999  RETURN
+
CLOUTBOA  MOVE      SP70,OBASITE
          MOVE      SP70,OBACLIN
          MOVE      SP70,OBADATE
          MOVE      SP70,OBASTRT
          MOVE      SP70,DOBASLOT
          MOVE      SP70,OBATIME
          MOVE      SP70,OBACTYP
          MOVE      ZEROUR,OBAURNO
          MOVE      SP70,OBAVISIT
          MOVE      SP70,DOBAOUTN
          MOVE      SP70,DOBASTAT
          MOVE      ZERO,OBADAY
          MOVE      ZERO,OBAEXSL
          MOVE      SP70,OBAFINST
          MOVE      SP70,OBALOCK
          MOVE      ZERO,OBAPXRAY
          MOVE      SP70,OBABKDTE
          MOVE      ZERO,OBAPULL
          MOVE      SP70,OBASESST
          MOVE      SP70,OBADISCH
          MOVE      SP70,DOTBARES
          MOVE      SP70,OBASPARA
          MOVE      ZERO,OBASLOT
          MOVE      ZEROVISN,OBAOUTNO
          MOVE      ZERO,OBASTAT
          MOVE      ZERO,OTBARESV
          RETURN
+
.------------------------------------------------------------
. Work out the first booking date for this referral
.------------------------------------------------------------
DATR0000  MOVE      SP70,BOOKDATE
.
          PACK      KEY16,ALREVISN,SP70
          CALL      RSALLNK1
DATR1000  CALL      RKALLNK1
          BRANCH    OVRCD,DATR9999
.
          MATCH     ALREVISN,ALLNVISN       * Linked bookings
          GOTO      DATR9999 IF NOT EQUAL
.
          MATCH     SP70,BOOKDATE
          IF        @EQUAL
            MOVE      ALLNDATE,BOOKDATE         * Calc ref expiry
          ENDIF
.
          MATCH     BOOKDATE,ALLNDATE
          IF        @LESS
            MOVE      ALLNDATE,BOOKDATE
          ENDIF
          GOTO      DATR1000
.
DATR9999  RETURN
+
.******************************************************************************
.         MHINC - MH Department and Primary Ref.
.******************************************************************************
ARMHI000  PACK      KEY5,ANSC,ANSG,ALREDEPT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,ARMHI999
.
          MATCH     "M",TCINDC4
          GOTO      ARMHI999 IF NOT EQUAL      * Not a Mental Health department
.
          MATCH     "1",ALREUYN4
          GOTO      ARMHI999 IF NOT EQUAL      * Not Primary Referral
.
          MOVE      "0",ALREMOHR               * Unsent
.
ARMHI999  RETURN
+
.------------------------------------------------------------------------------
. Re calculate the session statistics
.------------------------------------------------------------------------------
RCAL0000  MATCH     SP70,SAVSKEYS
          GOTO      RCAL9999 IF EQUAL
.
          MOVE      SAVSKEYS,SESSKEYS
.
          PACK      KEY25,SESSKEYS,SP70
          CALL      RDSESA1
          BRANCH    OVRCD,RCAL9999
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,RCAL9999
.
          PACK      KEY18,OSESITE,OSECLIN,OSEDAY,OSESTRT,SP70
          CALL      RDMASA1
          BRANCH    OVRCD,RCAL9999
.
... SRF 33923 - Commented out the next 3 lines of code
.          PACK      KEY25,OSESITE,OSECLIN,OSEDATE,OSESTRT
.          PACK      OSESCSTT,SP5
.          CALL      GCST0000
.
          MOVE      "00:00",OSETIMEU
          MOVE      ZERO,OSESLOTB              * slots booked
          MOVE      ZERO,OSESLOTS              * slots scheduled
          MOVE      ZERO,OSESSLTN              * New slots scheduled
          MOVE      ZERO,OSESSLTR              * Review slots scheduled
          MOVE      ZERO,OSESSLTS              * Special slots scheduled
          MOVE      ZERO,OSEBNEW               * New slots scheduled
          MOVE      ZERO,OSEBRV                * Review slots scheduled
          MOVE      ZERO,OSEBSPC               * Special slots scheduled
          MOVE      ZERO,OSEANEW               * New slots scheduled
          MOVE      ZERO,OSEARV                * Review slots scheduled
          MOVE      ZERO,OSEASPC               * Special slots scheduled
.
          CALL      CSLT0000
.
          CALL      UPSESA1
.
RCAL9999  RETURN
+
.**********************************************************************
.*                               CSLT0000                             *
.*        Count Slots                                                 *
.**********************************************************************
CSLT0000  PACK      KEY28,OSESITE,OSECLIN,OSEDATE,OSESTRT
          CALL      RDSBOKA1
CSLT1000  CALL      RDKBOKA1
          BRANCH    OVRCD,CSLT9999
          MATCH     OSESITE,OBASITE
          GOTO      CSLT9999 IF NOT EQUAL
          MATCH     OSECLIN,OBACLIN
          GOTO      CSLT9999 IF NOT EQUAL
          MATCH     OSEDATE,OBADATE
          GOTO      CSLT9999 IF NOT EQUAL
          MATCH     OSESTRT,OBASTRT
          GOTO      CSLT9999 IF NOT EQUAL
.
          COMPARE   THREE,OBASTAT
          GOTO      CSLT1000 IF EQUAL
.
          COMPARE   SEVEN,OBASTAT
          GOTO      CSLT1000 IF EQUAL
.
          MOVE      "CV",KEY2
          PACK      KEY5,KEY2,OBAVISIT
          CALL      RDCODE1
          BRANCH    OVRCD,CSLT1000
.
          IF        TCFORM6=0
            MOVE      ONE,TCFORM6
          ENDIF
          ADD       TCFORM6,OSESLOTS
.
          IF        (OBASTAT=1|OBASTAT=4|OBASTAT=5)
            ADD       TCFORM6,OSESLOTB
            CALL      ADDTIM00
          ENDIF
.
          MATCH     ANSN,TCINDC1
          GOTO      CSLT2000 IF NOT EQUAL
.
          ADD       ONE,OSESSLTN
          IF        (OBASTAT=1|OBASTAT=5)
            ADD       ONE,OSEBNEW
          ENDIF
          IF        OBASTAT=4
            ADD       ONE,OSEBNEW
            ADD       ONE,OSEANEW
          ENDIF
          GOTO      CSLT1000
.
CSLT2000  MATCH     ANSR,TCINDC1
          GOTO      CSLT3000 IF NOT EQUAL
.
          ADD       ONE,OSESSLTR
          IF        (OBASTAT=1|OBASTAT=5)
            ADD       ONE,OSEBRV
          ENDIF
          IF        OBASTAT=4
            ADD       ONE,OSEBRV
            ADD       ONE,OSEARV
          ENDIF

          GOTO      CSLT1000
.
CSLT3000  MATCH     ANSS,TCINDC1
          GOTO      CSLT1000 IF NOT EQUAL
.
          ADD       ONE,OSESSLTS
          IF        (OBASTAT=1|OBASTAT=5)
            ADD       ONE,OSEBSPC
          ENDIF
          IF        OBASTAT=4
            ADD       ONE,OSEBSPC
            ADD       ONE,OSEASPC
          ENDIF
          GOTO      CSLT1000
.
CSLT9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       AUDOTBOA
          INC       AUDOTBOB
          INC       BRHLCOMN
          INC       CLOUTCAN
          INC       CLPMSWX1
          INC       CLVISIAU
          INC       COUNTSLT
          INC       DGCLII13
          INC       DGCLICOC
          INC       EXACCDAT
          INC       KYOUTCTY
          INC       OUTDSCTY
          INC       OUTSITDS
          INC       OUTSITKY
          INC       PATCODKY
          INC       PATHSPKY
          INC       PMIGTNID
.
          INC       ACCCMTIO/INC
          INC       ALLENCIO/INC
          INC       ALLLNKIO/INC
          INC       ALLREFIO/INC
          INC       ALLQUEIO/INC
          INC       COMDEPIO/INC
          INC       DISMASIO/INC
          INC       DISPATIO/INC
          INC       DISPTLIO/INC
          INC       HICCLMIO/INC
          INC       HICCITIO/INC
          INC       HICBCNIO/INC
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       NHIMASIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTBITIO/INC
          INC       OUTBOCIO/INC
          INC       OUTCANIO/INC
          INC       OUTCTYIO/INC
          INC       OUTDIAIO/INC
          INC       OUTHEDIO/INC
          INC       OUTLKBIO/INC
          INC       OUTLPCIO/INC
          INC       OUTMA1IO/INC
          INC       OUTMSPIO/INC
          INC       OUTPREIO/INC
          INC       OUTRF1IO/INC
          INC       OUTSESIO/INC
          INC       OUTSITIO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       PATCODIO/INC
          INC       PATHSPIO/INC
          INC       PATIPEIO/INC
          INC       PATMA1IO/INC
          INC       PATNIDIO/INC
          INC       PMSAIDIO/INC
          INC       PMSMTIIO/INC
          INC       PATWC1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSWX1IO/INC
          INC       PMSQPTIO/INC
          INC       PMSTSPIO/INC
          INC       PMSVX1IO/INC
          INC       WEBSECIO/INC
          INC       VISCMTIO/INC
          INC       VISIAUIO/INC
          INC       VISINTIO/INC
          INC       WATMESIO/INC
          INC       WATOPAIO/INC
          INC       WATOPSIO/INC
          INC       WATTR1IO/INC
          INC       WATTX1IO/INC

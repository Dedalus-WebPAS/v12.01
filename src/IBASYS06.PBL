. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBASYS06                                            *
. *                                                                           *
. *     FUNCTION:         PRINTER FILE MAINTENANCE/LISTING                    *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *       V9.08.01  24/10/2006  Steve Armstrong  CAR 107236                   *
. *                 Added printer type 9 - Generic barcoding                  *
. *                 Recompiled for changes to IBAPRTKY.                       *
. *****************************************************************************
. *       V9.04.02  16/09/2005  Mike Laming      CAR 68672                    *
. *                 Add Datamax E-class printer - printer type 8              *
. *       V9.04.01  07/04/2005  Steve Armstrong  CAR 60343                    *
. *                 Mods to remove IBCNSATO and make the Sato printer the     *
. *                 permanent type 6 printer.                                 *
. *****************************************************************************
. *       V9.02.01  13/08/2001  Tonii                                         *
. *                 Mods to add new printer type (7) for Zebra printer        *
. *****************************************************************************
. *       C2.00.02  17/07/2000  Steve Armstrong                               *
. *                 Mods to add new printer type (6) for Sato CX200           *
. *       C2.00.01  03/02/1999  Steve Armstrong                               *
. *                 Mods to add new printer type (5) for Epson DLQ-3000+      *
. *                 For Anne Caudle Centre                                    *
. *****************************************************************************
. *       C1.00.02  21/05/1996  Greg Horvat      SRF 614884                   *
. *                 Added print type 4 - Intermec 3400                        *
. *       C1.00.01  10/08/1995  Steve Armstrong                               *
. *                 Mods to include extra printer types (2 & 3) that were in  *
. *                 5.01 and were lost when rel 6 SYS06 was used for merged   *
. *                 common                                                    *
. *                 Also fixed bug in change mode when changing printer type. *
. *                 Also added record locking and std keyin routine for       *
. *                 printer code.                                             *
. *     V5.01.02  Steve O'Gorman  13/12/89                                    *
. *               Recompile for change to IBAPRTFD.INC                        *
. *     V6.00     Steve O'Gorman  29/07/91                                    *
. *               Added Paper Type                                            *
. *               Made Standard                                               *
. *****************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       IBAPRTFD/INC
.
.********************************************************************** 
.*                             Constants                              *
.********************************************************************** 
.
CODEPT    INIT      "PT"
.
DIM21A    INIT      "HP2934A              "
DIM21B    INIT      "Epson with Dongle    "
DIM21C    INIT      "Tharo Freedom        "
DIM21D    INIT      "Intermec 3400        "
DIM21E    INIT      "Epson DLQ-3000+      "
DIM21F    INIT      "Sato CX200           "
DIM21G    INIT      "Zebra                "
DIM21H    INIT      "Datamax E-class      "                            *I-68672
DIM21I    INIT      "Generic Barcoding    "
DIM21Z    INIT      "No Barcoding Facility"
.
EITHER    INIT      "Either Paper      "
.
NOBARCOD  INIT      "No Barcoding  "
.
PIPE      INIT      "|"
PLAIN     INIT      "Plain Paper       "
.
SPECIAL   INIT      "Special Stationery"
.
.
SECURTXT  FILE      ASCII, FIXED=50
.
.********************************************************************** 
.*                             Variables                              *
.********************************************************************** 
.
CHG       FORM      1
CODE      DIM       2
.
DIM3      DIM       3 
DIM21     DIM       21
.
FIELD     FORM      2
FILENAME  DIM       8
.
MODE      DIM       1
MODE1     DIM       1
.
PAPRDESC  DIM       18
PRTTYPED  DIM       2
PRTAVALD  DIM       1
.
SEC       FORM      2
.
.
PRGID     INIT      "IBASYS06"
PRGNAM    INIT      "Printer File Maintenance/listing"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P64:24,"Opening"
.
          DISPLAY   *P72:24,"ibaprtaf";
          OPEN      IBAPRTA1,"ibaprtaf"
.
          DISPLAY   *P72:24,"ibacodef";
          OPEN      IBACODE1,"ibacodef"
.
          DISPLAY   *P72:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
START     HLINE     *G37,2,65,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Insert":
                    *P1:6,*V2LON,TWO,*HOFF," = Change":
                    *P1:7,*V2LON,THREE,*HOFF," = Delete":
                    *P1:8,*V2LON,FOUR,*HOFF," = List  "
.
START0    KEYIN     *P1:11,*EF,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
REPEAT    BRANCH    OPTION OF INSERT,CHANGE,DELETE,LIST
          BEEP
          GOTO      START0
+
.
.       ADD A PROGRAM SECURITY
.
INSERT    CALL      DISPI
          CALL      DISP1
          MOVE      ANSI,MODE
          MOVE      ANSI,MODE1
          GOTO      KPROG
.
.       CHANGE A PROGRAM SECURITY
.
CHANGE    CALL      DISPC
          CALL      DISP1
          MOVE      ANSC,MODE
          MOVE      ANSC,MODE1
          GOTO      KPROG
RDOPGM    CALL      DISPDA
          GOTO      SFIELD
+
.
.               DELETE A PROGRAM SECURITY
.
DELETE    CALL      DISPD
          CALL      DISP1
          MOVE      ANSD,MODE
          MOVE      ANSD,MODE1
          GOTO      KPROG
.
DELCS     CALL      DISPDA
.
REKEY1    CALL      DELQST
          BRANCH    CEXIT,PROGSDEL,DELETE,START
.
PROGSDEL  CALL      DEPRTA1
          DISPLAY   *P1:24,*EL,*P30:24,*V2LON,"*** DELETED ***"
          GOTO      REPEAT
+
.
.             INPUT PRINTER CODE
.
KPROG     MOVE      SP3,PRTCODE
          CALL      DISPCL
          CALL      ULPRTA1
          MOVE      THIRTY,ECOL
          MOVE      FIVE,EVERT
          MOVE      ZERO,CKYIMODE
          IF        OPTION = 1
            MOVE      ONE,CKYIMODE
          ENDIF
.
          CALL      IBAPRTKY
          BRANCH    EXIT,START,START
.
          MOVE      KEY3,PRTCODE
          MOVE      PRTCODE,KEY3
          CALL      RLPRTA1
          COMPARE   TWO,OVRCD
          GOTO      KPROG10 IF NOT EQUAL
.
          CALL      RECLOCK
          GOTO      KPROG
.
KPROG10   MATCH     ANSC,MODE
          GOTO      RDOPGM IF EQUAL
.
          MATCH     ANSD,MODE
          GOTO      DELCS IF EQUAL
+
.
.             INPUT  PRINTER DESCRIPTION
.
KDESC     PACK      PRTDESC WITH UNDLN,UNDLN
          DISPLAY   *P30:7,*EL,PRTDESC
          MOVE      SP30,PRTDESC
          KEYIN     *P30:7,*V2LON,PRTDESC,*P30:7,*DV,PRTDESC
          MOVE      ONE,CHG
.
          ENDSET    PRTDESC
          APPEND    SP30,PRTDESC
          RESET     PRTDESC
          MATCH     ANSC,MODE
          GOTO      SFIELD IF EQUAL
+
.
.             INPUT AVAILABILITY
.
KAVAL     MOVE      ZERO,PRTAVAL
          DISPLAY   *P30:8,*EL,UNDLN1
          KEYIN     *P30:8,*V2LON,*JR,PRTAVALD
.
          REP       "nNyY",PRTAVALD
.
          MATCH     "Y" TO PRTAVALD
          GOTO      KAVAL1 IF EQUAL
.
          MATCH     "N" TO PRTAVALD
          GOTO      KAVAL2 IF EQUAL
.
          BEEP
          DISPLAY   *P1:24,*EL,*B,"Answer '",*V2LON,"Y'",*HOFF:
                                     " or '",*V2LON,"N'",*HOFF:
                                     " - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      KAVAL
.
KAVAL1
          DISPLAY   *P30:8,*V2LON,"Yes"
          MOVE      ZERO TO PRTAVAL
          GOTO      KAVAL3
.
KAVAL2
          DISPLAY   *P30:8,*V2LON,"No"
          MOVE      ONE TO PRTAVAL
.
KAVAL3
          MOVE      ONE,CHG
          MATCH     ANSC,MODE
          GOTO      SFIELD IF EQUAL
.
+
.
.             INPUT PRINTER TYPE
.
KTYPE     MOVE      " 0",PRTTYPED
          KEYIN     *P30:9,*EL,*V2LON,*JR,PRTTYPED:
                    *P30:9,*DV,PRTTYPED
.
          RESET     PRTTYPED
          GOTO      KTYPE1 IF EOS
.
          MATCH     " ?",PRTTYPED
          GOTO      KTYPE2 IF EQUAL

          TYPE      PRTTYPED
          GOTO      KTYPE05 IF NOT EQUAL
.
KTYPE0    MOVE      PRTTYPED,PRTTYPE
          MATCH     " 0",PRTTYPED
          GOTO      KTYPE3 IF EQUAL
          BRANCH    PRTTYPE,KTYPE3,KTYPE3,KTYPE3,KTYPE3,KTYPE3,KTYPE3,KTYPE3:
                            KTYPE3,KTYPE3
.
KTYPE05   BEEP
          GOTO      KTYPE
.
KTYPE1    MOVE      " 0",PRTTYPED
          GOTO      KTYPE0
.
KTYPE2    CALL      GETSCR00
          DISPLAY   *P20:4,*EF,*ULON,*V2LON,"PRINTER TYPES",*HOFF:
                    *P1:6,*V2LON,ZERO,*HOFF,*P6:6,DIM21Z:
                    *P1:7,*V2LON,ONE,*HOFF,*P6:7,DIM21A:
                    *P1:8,*V2LON,TWO,*HOFF,*P6:8,DIM21B:
                    *P1:9,*V2LON,THREE,*HOFF,*P6:9,DIM21C:
                    *P1:10,*V2LON,FOUR,*HOFF,*P6:10,DIM21D:
                    *P1:11,*V2LON,FIVE,*HOFF,*P6:11,DIM21E:
                    *P1:12,*V2LON,SIX,*HOFF,*P6:12,DIM21F:
                    *P1:13,*V2LON,SEVEN,*HOFF,*P6:13,DIM21G:
                    *P1:14,*V2LON,EIGHT,*HOFF,*P6:14,DIM21H:           *I-68672
                    *P1:15,*V2LON,NINE,*HOFF,*P6:15,DIM21I;
.
KTYPE21   KEYIN     *P1:24,*EL,"Printer Type : ",*V2LON,*DE,*JR,PRTTYPED
.
          RESET     PRTTYPED
          GOTO      KTYPE24 IF EOS
.
KTYPE22   MOVE      PRTTYPED,PRTTYPE
          MATCH     " 0",PRTTYPED
          GOTO      KTYPE25 IF EQUAL
.
          BRANCH    PRTTYPE,KTYPE25,KTYPE25,KTYPE25,KTYPE25,KTYPE25,KTYPE25:
                            KTYPE25,KTYPE25,KTYPE25                    *C-68672
.
          BEEP
          GOTO      KTYPE21
.
KTYPE24   MOVE      " 0",PRTTYPED
          GOTO      KTYPE22
.
KTYPE25   CALL      PUTSCR00
.
KTYPE3    MOVE      DIM21Z,DIM21

          LOAD      DIM21,PRTTYPE,DIM21A,DIM21B,DIM21C,DIM21D,DIM21E,DIM21F:
                                  DIM21G,DIM21H,DIM21I                 *C-68672
.
          DISPLAY   *P30:9,*V2LON,PRTTYPE,*HOFF,*P40:9,DIM21
.
KTYPE99   MOVE      ONE,CHG
          MATCH     ANSC,MODE
          GOTO      SFIELD IF EQUAL
.
. -----   Keyin paper Type   -----
.
KPAPRTYP  DISPLAY   *P1:24,*EL,*V2LON,"0",*HOFF," = ",EITHER,SP2:
                               *V2LON,"1",*HOFF," = ",PLAIN,SP2:
                               *V2LON,"2",*HOFF," = ",SPECIAL;
.
KPAPER1   DISPLAY   *P30:10,*EL,UNDLN2
          KEYIN     *P30:10,*V2LON,PRTPAPER
.
          ADD       ONE,PRTPAPER
          BRANCH    PRTPAPER,KPAPER2,KPAPER2,KPAPER2
.
          BEEP
          GOTO      KPAPER1
.
KPAPER2   LOAD      PAPRDESC,PRTPAPER,EITHER,PLAIN,SPECIAL
          SUB       ONE,PRTPAPER
          DISPLAY   *P30:10,*V2LON,PRTPAPER,*HOFF:
                    *P40:10,PAPRDESC:
                    *P1:24,*EL;
.
          MOVE      ONE,CHG                 * Flag the Change
.
          GOTO      SFIELD
+
.
.                       SELECT THE FIELD TO UPDATE
.
SFIELD    CALL      MAINQST
.
          COMPARE   "-1",CCITEM             * Cancel
          GOTO      REPEAT IF EQUAL
.
          COMPARE   ZERO,CCITEM             * Post
          GOTO      POSTCNT IF EQUAL
.
          BRANCH    CCITEM,KDESC,KAVAL,KTYPE,KPAPRTYP
.
          BEEP
          GOTO      SFIELD
.
POSTCNT   MATCH     ANSC,MODE1
          GOTO      SECUSUP IF EQUAL
.
          MATCH     ANSI,MODE1
          GOTO      SECUSWR IF EQUAL
.
          GOTO      SFIELD
.
.
.       CHECK CHG INDICATOR TO SEE IF ANYTHING WAS CHANGED
.
SECUSUP   COMPARE   ZERO,CHG
          GOTO      REPEAT IF EQUAL
          CALL      UPPRTA1
          GOTO      ENDPST
.
SECUSWR   MOVE      PRTCODE,KEY3
          CALL      WRPRTA1
.
ENDPST    DISPLAY   *P1:24,*EL,*P30:24,*EL,*V2LON:
                    "*** Data successfully posted *** "
          GOTO      REPEAT
+
.
.       PROCEDURE TO CLEAN MENU
.
DISPCL    DISPLAY   *P30:5,*EL,*P30:7,*EL,*P30:8,*EL,*P30:9,*EL,*P30:10,*EF
          RETURN
.
.       MENUS FOR EACH MODE
.
DISPI     DISPLAY   *P65:2,*V2LON," Insert "
          RETURN
.
DISPC     DISPLAY   *P65:2,*V2LON," Change "
          RETURN
.
DISPD     DISPLAY   *P65:2,*V2LON," Delete "
          RETURN
.
.      MAIN MENU
.
DISP1     DISPLAY   *P1:4,*EF:
                    *P1:5,"    Printer Code          : ":
                    *P1:7,*V2LON," 1",*HOFF,". Printer Description   : ":
                    *P1:8,*V2LON," 2",*HOFF,". Availability (Y/N)    : ":
                    *P1:9,*V2LON," 3",*HOFF,". Printer Type          : ":
                   *P1:10,*V2LON," 4",*HOFF,". Paper Type            : "
.
          RETURN
.
DISPDA    MOVE      "No ",DIM3
          BRANCH    PRTAVAL OF DISPDA1
          MOVE      "Yes",DIM3
.
DISPDA1   MOVE      DIM21Z,DIM21
          LOAD      DIM21,PRTTYPE,DIM21A,DIM21B,DIM21C,DIM21D,DIM21E,DIM21F:
                                  DIM21G,DIM21H,DIM21I                 *C-68672
.
DISPDA2   ADD       ONE,PRTPAPER
          LOAD      PAPRDESC,PRTPAPER,EITHER,PLAIN,SPECIAL
          SUB       ONE,PRTPAPER
.
          DISPLAY   *P30:5,*V2LON,PRTCODE,*P30:7,PRTDESC,*P30:8,DIM3:
                    *P30:9,*V2LON,PRTTYPE,*P40:9,*HOFF,DIM21:
                   *P30:10,*V2LON,PRTPAPER,*P40:10,*HOFF,PAPRDESC;
.
          RETURN
.
+
+
.
.          PRINT PROGRAM SECURITY FILE
.
LIST      DISPLAY   *P1:24,*EL,*P30:24,*V2LON:
                    "*** Generating List ***":
                    *P20:24,*HOFF,*EL,"Printer Name :"
          MOVE      ZERO,CPAGENO
          MOVE      SP4,KEY3
          CALL      RDSPRTA1
CONT      CALL      RDKPRTA1
          BRANCH    OVRCD OF FINISH
          DISPLAY   *P36:24,*V2LON,PRTCODE
          COMPARE   ZERO,CPAGENO
          CALL      HEAD IF EQUAL
          COMPARE   "55",CLNO
          GOTO      NEXTL IF LESS
          CALL      HLINE
          CALL      HEAD
NEXTL     CALL      DETAIL
          GOTO      CONT
.
.          FINALISE
.
FINISH    COMPARE   ZERO,CPAGENO
          GOTO      NOPROG IF EQUAL
          CALL      HLINE
          PRINT     *N,*N,*35,"*** END OF REPORT ***"
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** Report Generation Completed **"
          GOTO      START0
.
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,CPAGENO
          CLOCK     TIME,CTIMEIS
          PRINT     *F,PRGID,*35,CNAME,*91,"DATE : ",CDATE:
                    *N,VERSION,*35,PRGNAM:
                    *91,"TIME : ",*98,CTIMEIS:
                    *N,*91,"PAGE : ",*103,CPAGENO,*N,*N
          CALL      HLINE
          PRINT     *1,PIPE," Code      ":
                   *13,PIPE," Description":
                   *48,PIPE," Available ?  ":
                   *63,PIPE," Printer Type":
                   *80,PIPE," Paper Type":
                  *101,PIPE
.
          CALL      HLINE
          MOVE      "7",CLNO
          RETURN
.
.
HLINE     PRINT     *1,"*-------------------------------------------------":
                       "--------------------------------------------------*"
          RETURN
.
.
DETAIL    MOVE      DIM21Z,DIM21
          LOAD      DIM21,PRTTYPE,DIM21A,DIM21B,DIM21C,DIM21D,DIM21E,DIM21F:
                                  DIM21G,DIM21H,DIM21I                 *C-68672
.
          ADD       ONE,PRTPAPER
          LOAD      PAPRDESC,PRTPAPER,EITHER,PLAIN,SPECIAL
          SUB       ONE,PRTPAPER
.
          MOVE      "No ",DIM3
          BRANCH    PRTAVAL OF DETAIL1
          MOVE      "Yes",DIM3
.
DETAIL1   PRINT     *1,PIPE,*3,PRTCODE:
                   *13,PIPE,*15,PRTDESC:
                   *48,PIPE,*53,DIM3:
                   *63,PIPE,*65,DIM21:
                   *80,PIPE,*82,PAPRDESC:
                  *101,PIPE
.
          ADD       ONE,CLNO
          RETURN
.
.
NOPROG    DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "*** FILE IS EMPTY ***",*W2,*P1:24,*EL;
          GOTO      START0
.
          INC       IBAPRTKY
.
          INC       IBAPRTIO/INC
.
          INC       STD001IO
.
ENDIT     CHAIN     PGM
          STOP

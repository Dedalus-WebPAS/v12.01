.***************************************************************************
.* System    :   Waiting List                                              *
.* Program   :   CHKWLSUS                                                  *
.* Desc      :   Check if all suspension records have been written to      *
.*           :   wateseaf                                                  *
.***************************************************************************
.* Date      :   24/10/2005                                                *
.* Author    :   Jill Habasque                                             *
.***************************************************************************
.* V12.00.02 08/08/2025  Ebon Clements  TSK 0964891                        *
.*           Fixed wattx1af read for ASA score - WESE0000                  *
.* V12.00.01 14/05/2025 Thanh T         TSK 0955096                        *
.*           Changed COMPARE to MATCH for Alphanumeric checking            *
.***************************************************************************
.* V11.02.01 10/02/2022  Thanh T       TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                *
.***************************************************************************
.* V11.05.01     18.03.2025  DA Sarkies    Task 0955909                    *
.*               Updated the ESIS extract to add the ASAS code             *
.* V11.01.01     22/02/2021  J.Tan          TSK 0888639                    *
.*               Changed patmmbs counter record to DIM3                    *
.* V10.01.01     10/03/2011 Jill Parkinson CAR 233088                      *
.*               Added pmsvx1af                                            *
.* V9.05.B01     29/11/2005 Jill Habasque                                  *
.*               Added check for patmmbsf                                  *
.* 25/10/2005    V9.04.01 Jill Habasque                                    *
.*               Added check for admission date                            *
.* V9.04.02      31/10/2005 Jill Habasque                                  *
.*               Added update of admission date                            *
.* V9.04.03      24/11/2005 Sandra Barcham                                 *
.*               Added U/R Number to errors for admission date             *
.***************************************************************************
.
          INC       STD001FD
          INC       WATSUSFD/INC
          INC       WATESNFD/INC
          INC       WATESEFD/INC
          INC       WATPROFD/INC
          INC       WATTR1FD/INC
          INC       WATTX1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATDSCFD/INC
          INC       PATCODFD/INC
          INC       PATMMBFD/INC
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CURDATE   DIM       8
RECNUM    FORM      5
SAVKEY36  DIM       36
SAVEVDT   DIM       8 
SAVEVAL   DIM       70
RFCVALUE  DIM       3
.
PRGID     INIT      "CHKWLSUS"
PRGNAM    INIT      "Check ESIS Suspension records"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML1000  * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      GRFC0000
          BRANCH    EXIT,ML0100
          CALL      LESN0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTR1A2,"wattr1af"
          OPEN      WATPROA1,"watproaf"
          OPEN      WATSUSA1,"watsusaf"
          OPEN      WATESNA1,"watesnaf"
          OPEN      WATESEA1,"wateseaf"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATMMBS1,"patmmbsf"
.
          CALL      IBACLOCK
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Print Only":
                    *P1:6,*V2LON,TWO,*HOFF,". Print and Update"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
GRFC0000  MOVE      ZERO,EXIT
          PACK      KEY5,ANSV,ANSL,SP70
          CALL      RDSCODE1
GRFC0500  CALL      RDKCODE1
          BRANCH    OVRCD,GRFC9500
.
          MATCH     "VL",TCODE
          GOTO      GRFC9500 IF NOT EQUAL
.
          MATCH     "A",PTCOACTV           * Only want active codes
          GOTO      GRFC0500 IF NOT EQUAL
.
          MATCH     "R",TCINDC1            * Must have indicator 1 = "R"
          GOTO      GRFC0500 IF NOT EQUAL
.
          MOVE      ACODE,RFCVALUE
          GOTO      GRFC9999
. 
GRFC9500  PRINT     *1,"Missing RFC value"
          MOVE      ONE,EXIT
.
GRFC9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
LESN0000  MOVE      ZERO,RECNUM
          DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY20,SP70
          CALL      RDSTREA2
LESN1000  CALL      RDKTREA2
          BRANCH    OVRCD,LESN9000
          PACK      KEY19,WMURNO,WMCODE,WMCNT
          ADD       ONE,RECNUM
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          COMPARE   "5",WMSTAT
          GOTO      LESN2000 IF LESS
.
          PACK      KEY9,WMCODE
          CALL      RDPROA1               * Read a procedure file record
          IF        OVRCD<>1
            SQUEEZE   WTWPHDPE
            PACK      WTWPHDPE,WTWPHDPE,SP5
            MATCH     "200",WTWPHDPE
            GOTO      LESN2000 IF EQUAL   * Ignore this procedure
          ENDIF
.
          IF        WMSTAT=6
            MATCH     "20050430",WMDATE4
            GOTO      LESN1000 IF LESS
            GOTO      LESN2000
          ENDIF
.
          PACK      KEY8,WMBOOK
          CALL      RDDSCH1
          BRANCH    OVRCD,LESN8100
.
          MATCH     "20050430",DDATE
          GOTO      LESN1000 IF LESS
.
          PACK      KEY8,WMBOOK
          CALL      RDMISS1
          BRANCH    OVRCD,LESN8000
.
          CALL      LESE0000
.
LESN2000  
.....CALL      READ0000
          GOTO      LESN1000
.
LESN8000  PRINT     *1,"UR Number ",WMURNO,"Procedure ",WMCODE,"Count ":
                    WMCNT," missing from patmi1af Visit no. ",WMBOOK
          GOTO      LESN1000
.
LESN8100  PRINT     *1,"UR Number ",WMURNO," Visit Number ",WMBOOK:
                    "  missing from patdschf"
          GOTO      LESN1000
.
LESN8200  PRINT     *1,"UR Number ",WMURNO,"Procedure ",WMCODE,"Count ":
                    WMCNT," missing extension record"
          GOTO      LESN1000
.
LESN9000  CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
          CALL      HITENTER
.
LESN9999  RETURN
+
.**************************************************************************
.*                               LESE0000              Called by:LESN0000 *
.**************************************************************************
LESE0000   PACK     KEY17,WTWMECNT,SP70
           CALL     RDWTESE1
           BRANCH   OVRCD,LESE0500
           GOTO     LESE1500
.
LESE0500   CALL     RSWTESE1
LESE1000   CALL     RKWTESE1
           BRANCH   OVRCD,LESE9000
.
           MATCH    WTWMECNT,WTEEUNIQ
           GOTO     LESE9000 IF NOT EQUAL
.
LESE1500   MATCH    SP70,WTEEADAT
           GOTO     LESE1000 IF EQUAL
. 
           GOTO     LESE9999
.
LESE9000   PACK      KEY11,WMBOOK,SP70
           CALL      RDSMMBS1
LESE9100   CALL      RDKMMBS1
           BRANCH    OVRCD,LESE9150
.
           MATCH     WMBOOK,MMADMN
           GOTO      LESE9150 IF NOT EQUAL
.
           PRINT     *1,"Unique ID ",WTWMECNT,"U/R Number ",WMURNO:
                     " Missing admission date - coded" 
           IF        COPTION=2
             CALL      WESE0000
           ENDIF
           GOTO      LESE9999
           
.
LESE9150   PRINT     *1,"Unique ID ",WTWMECNT,"U/R Number ",WMURNO:
                     " Missing admission date" 
           IF        COPTION=2
             CALL      WESE0000
           ENDIF
.
LESE9999   RETURN
+
.**************************************************************************
.*                               READ0000              Called by:LESN0000 *
.*        Load urgency details                                            *
.**************************************************************************
READ0000  PACK      KEY21,WMURNO,WMCODE,DWMCNT,SP70
          CALL      RSWTSUS1
READ1500  CALL      RKWTSUS1
          BRANCH    OVRCD,READ9999
.
          MATCH     WMURNO,WTSUURNO
          GOTO      READ9999 IF NOT EQUAL
.
          MATCH     WMCODE,WTSUCODE
          GOTO      READ9999 IF NOT EQUAL
.
          MATCH     DWMCNT,DWTSUCNT
          GOTO      READ9999 IF NOT EQUAL
.
          MATCH     CURDATE,WTSUFDAT
          GOTO      READ1500 IF NOT LESS
.
          MOVE      WTSUFDAT,WTENEVDT
          MOVE      WTSULSTS,WTENEVAL
          CALL      WRRED000
.
          MATCH     SP70,WTSUTDAT
          GOTO      READ1500 IF EQUAL
.
          MATCH     CURDATE,WTSUTDAT
          GOTO      READ1500 IF NOT LESS
.
          MOVE      WTSUTDAT,WTENEVDT
          MOVE      RFCVALUE,WTENEVAL
          CALL      WRRED000
.
          GOTO      READ1500
.
READ9999  RETURN
.
WRRED000  PACK      WTENUNIQ,WTWMECNT,SP70
          PACK      WTENETYP,ANSR
          PACK      SAVEVDT,WTENEVDT,SP70
          PACK      SAVEVAL,WTENEVAL,SP70
.
          PACK      SAVKEY36,WTENUNIQ,WTENETYP,WTENEVDT,SP70
          PACK      KEY36,WTENUNIQ,WTENETYP,WTENEVDT,SP70
          CALL      RSWTESN1
          CALL      RKWTESN1
          BRANCH    OVRCD,WRRED100 
.
          PACK      KEY36,WTENUNIQ,WTENETYP,WTENEVDT,SP70
          MATCH     KEY36,SAVKEY36
          GOTO      WRRED999 IF EQUAL
.
          PACK      WTENUNIQ,WTWMECNT,SP70
          PACK      WTENEDAT,SP70
          PACK      WTENETYP,ANSR
          PACK      WTENEVDT,SAVEVDT,SP70
          PACK      WTENEVAL,SAVEVAL,SP70
          PACK      WTENWEBC,SP70
          CALL      IBACLOCK
          PACK      WTENCDAT,CCC,CYY,CMM,CDD
          REP       " 0",WTENCDAT
          CLOCK     TIME,WTENCTIM
          REP       " 0",WTENCTIM
          PACK      WTENWEBU,SP70
          PACK      WTENUDAT,SP70
          PACK      WTENUTIM,SP70
          PACK      WTENSADI,SP70
          PACK      KEY36,WTENUNIQ,WTENETYP,WTENEVDT,SP70
.
WRRED100  CALL      RAWTESN1
          IF        OVRCD=1
        PRINT     *1,"Unique ID ",WTENUNIQ," U/R ",WMURNO," Date ",WTENEVDT:
                  " Value ",WTENEVAL
            IF        COPTION=2
              CALL      WRWTESN1
            ENDIF
          ENDIF
.
WRRED999  RETURN
.
.******************************************************************************
.*                                 WESE0000                                   *
.*                Write/update wateseaf (episode details)                     *
.******************************************************************************
WESE0000  PACK      KEY17,WTWMECNT,Z70
          CALL      RSWTESE1
          CALL      RPWTESE1
          BRANCH    OVRCD,WESE9999
.
          MATCH     WTEEUNIQ,WTWMECNT
          GOTO      WESE1000 IF NOT EQUAL
.
          MATCH     WTEEADAT,ADATE     * admission date already on file
          GOTO      WESE1000 IF NOT EQUAL
.
          MATCH     WTEEINSD,ACLAIM    * claim type already on file
          GOTO      WESE9999 IF EQUAL
.
          MATCH     WTEEREMD,ADATE    * operation date = removal date
          GOTO      WESE1000 IF NOT EQUAL
.
WESE1000  PACK      WTEEADAT,ADATE
          PACK      WTEEINSD,ACLAIM
          PACK      WTEEREMD,ADATE
          REP       " 0",WTEEREMD
          PACK      WTEEREMD,ADATE
          REP       " 0",WTEEREMD
          MOVE      SP70,WTEERREM
          PACK      KEY5,ANSW,ANSR,SP70
          CALL      RDSCODE1
WESE1500  CALL      RDKCODE1
          BRANCH    OVRCD,WESE2000
          MATCH     "WR",TCODE
          GOTO      WESE2000 IF NOT EQUAL
.
          MATCH     ANSW,TCINDC3
          IF        @EQUAL
            MOVE      ACODE,WTEERREM
            GOTO      WESE2000
          ENDIF
          GOTO      WESE1500
.
WESE2000  PACK      WTEEEDAT,SP70
          PACK      WTEEWEBU,SP70
          PACK      WTEEUDAT,SP70
          PACK      WTEEUTIM,SP70
          PACK      WTEEASAS,SP70
.
          CALL      CLWATTX1
          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
WESE2100  CALL      RDWTTX11
          BRANCH    OVRCD,WESE2500
.
          MOVE      WTTXASAS,WTEEASAS
.
WESE2500  PACK      KEY17,WTEEUNIQ,WTEEEDAT,SP70
          CALL      RAWTESE1
          IF        OVRCD=0
            PACK      WTEEWEBU,PASSCODE,SP70
            CALL      IBACLOCK
            PACK      WTEEUDAT,CCC,CYY,CMM,CDD
            REP       " 0",WTEEUDAT
            CLOCK     TIME,WTEEUTIM
            REP       " 0",WTEEUTIM
            CALL      UPWTESE1
          ELSE
            PACK      WTEEWEBC,PASSCODE,SP70
            CALL      IBACLOCK
            PACK      WTEECDAT,CCC,CYY,CMM,CDD
            REP       " 0",WTEECDAT
            CLOCK     TIME,WTEECTIM
            REP       " 0",WTEECTIM
            CALL      WRWTESE1
          ENDIF
.
WESE9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       CLWATTX1
          INC       WATSUSIO/INC
          INC       WATTR1IO/INC
          INC       WATPROIO/INC
          INC       WATESNIO/INC
          INC       WATESEIO/INC
          INC       WATTX1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATDSCIO/INC
          INC       PATCODIO/INC
          INC       PATMMBIO/INC

.******************************************************************************
.* Program   :   WIES13                                                       *
.* Desc      :   Convert text fields into form fields                         *
.******************************************************************************
.* Date      :   10/07/2006                                                   *
.* Author    :                                                                *
.* Mods      :                                                                *
.*        V9.07.01  10/07/2006  Davin S  CAR 111764                           *
.*                  Create WIES 13 File.                                      *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
          INC       PATW13FD/INC            * WIES13 weights file
.
. ----- file layout - weights.txt -----
.
WEIGHTMP  FILE      ASCII, FIXED=152
.
.Name     Type    Length    Start  Description
.----     ----    ------    -----  -----------
DRGC      DIM       4          1   DRG
DRGD      DIM       60         5   DRG Description
SDMT      DIM       1         65   Same Day Med. Target
MVELIG    DIM       1         66   Mech. Vent. Elig.
OTHCOP    DIM       3         67   Other Copayments (T)
LIBP      DIM       4         70   LIBP
HIBP      DIM       4         74   HIBP
AILOS     DIM       7         78   Av. ILOS
SDOD      DIM       1         85   Same Day / One Day
TSDW      DIM       11        86   Total SDW
TODW      DIM       11        97   Total ODW
TLOMDW    DIM       11       108   Total LOMDW
TIMDW     DIM       11       119   Total IMDW
THOMDW    DIM       11       130   Total HOMDW
THITHW    DIM       11       141   Total HITHW
.
.End of Record               152 
.
. ----- Program Variables -----
.
. ----- Program Constants -----
.
PRGNAM    INIT      "Convert Weights"
PRGID     INIT      "WIES13"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
.
ML0000    CALL      INIT0000                * Initialise variables & open files
          BRANCH    EXIT,ML9000
.
          CALL      OPTN0000                * Ask proceed question
          BRANCH    EXIT,ML9000
.
          CALL      PMST0000                * Process the master file
.
ML9000    MATCH     SP6,PGM
          GOTO      ML9999 IF EQUAL
.
          CHAIN     PGM                     * Chain back to appropriate menu
ML9999    SHUTDOWN  ""
+
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                      Initialise Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          OPEN      PATW13A1,"patw13af"
.
. ------- open weights.txt ---------------------------------------------------
.
          OPEN      WEIGHTMP,"weights"
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                            Ask Proceed Question                            *
.******************************************************************************
.
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,"Note : This program converts all ":
                    "records in the weights.txt file"
.
          DISPLAY   *P1:24,*EL,*HOFF,"Ok to Proceed (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ";
.
OPTN1000  MOVE      SP1,ANS
          KEYIN     *P23:24,*V2LON,*RV,ANS;
          REP       UPPLOW,ANS
.
          CMATCH    ANSY,ANS
          GOTO      OPTN9000 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      OPTN9500 IF EQUAL
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                  PMST0000              Called by: ML0000   *
.*                           Process The Weight File                          *
.******************************************************************************
.
PMST0000  CLOCK     TIME,CTIMEIS
.
. ----- Read the weights.txt file -----
.
PMST1000  READ      WEIGHTMP,SEQ;DRGC,DRGD,SDMT,MVELIG,OTHCOP,LIBP,HIBP:
                                 AILOS,SDOD:
                                 TSDW,TODW,TLOMDW,TIMDW,THOMDW,THITHW
          GOTO      PMST9000 IF OVER
.
. ----- New record to be posted -----
.
PMST5000  CALL      SETM0000                * Set up the master file variables
          GOTO      PMST1000
.
. ----- Finished -----
.
PMST9000  CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:21,"Finished : ",*V2LON,CTIMEIS,*HOFF:
                    *P1:24,*EL,*B,"No more records.  ";
          CALL      HITENTER
PMST9999  RETURN
+
.******************************************************************************
.*                                  SETM0000              Called by: PMST0000 *
.*                      Set up The File Variables                             *
.******************************************************************************
.
SETM0000  MOVE      DRGC,PT13DRGC
          MOVE      DRGD,PT13DRGD
          MOVE      SDMT,PT13SDMT
          MOVE      MVELIG,PT13MVEL
          MOVE      OTHCOP,PT13CPAY
          MOVE      LIBP,PT13LIBP
          MOVE      HIBP,PT13HIBP
          MOVE      AILOS,PT13ILOS
          MOVE      SDOD,PT13SDOD
          MOVE      TSDW,PT13TSDW
          MOVE      TODW,PT13TODW
          MOVE      TLOMDW,PT13TLOM
          MOVE      TIMDW,PT13TIMW
          MOVE      THOMDW,PT13THOM
          MOVE      THITHW,PT13THIH
          PACK      PT13SPAR,SP70
.
          PACK      KEY4,DRGC
          CALL      WRPT131
.
SETM9999  RETURN
+
.==============================================================================
.
          INC       STD001IO
.
          INC       PATW13IO/INC            * WIES13 weights file
+
.


. **********************************************************************
. * System    :   Supply & Inventory System                            *
. * Program   :   IBASIN96                                             *
. * Desc      :   Audit File Reporting                                 *
. **********************************************************************
. * Date      :   11.10.1992                                           *
. * Author    :   Neeriem Dye                                          *
. * Mods      :                                                        *
. **********************************************************************
. * V9.04.01  08/04/2005 Peter Vela CAR 59696                          *
. *           Changed commodity group from DIM 5 to DIM 6              *
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.09.B01 24.11.2000 Charles Handaya                               *
. *           Recompiled (Modification of Warehouse Security)          *
. **********************************************************************
. * VF.01.01  18.07.2000 Charles Handaya                               *
. *           Omnicell import file - Add Restock ID (SIBARESI)         *
. *           Quote 9817                                               *
. **********************************************************************
. * VF.00.01  10.11.1999 Charles Handaya                               *
. *           Fix Y2K to print Audit Files Date out                    *
. **********************************************************************
.$$$$$$
.        PROGRAM IBASIN96
.        END
.$$$$$$
          INC       STDGENDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
.
          INC       SINBACFD/INC
          INC       SINCCAFD/INC
          INC       SINCGAFD/INC
          INC       SINCIAFD/INC
          INC       SINCICFD/INC
          INC       SINCIEFD/INC
          INC       SINFACFD/INC
          INC       SINIMPFD/INC
          INC       SINORIFD/INC
          INC       SINPOAFD/INC
          INC       SINPOCFD/INC
          INC       SINPOFFD/INC
          INC       SINNATFD/INC
          INC       SINSUAFD/INC
          INC       SINWARFD/INC
          INC       SINCONFD/INC
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
.
BLAB      INIT      "                                   " * labels for screen B
BLAB1     INIT      "Backorders               (sinbacaf)"
BLAB2     INIT      "Cost Centre              (sinccaaf)"
BLAB3     INIT      "Commodity Group          (sincgaaf)"
BLAB4     INIT      "Catalog                  (sinciaaf)"
BLAB5     INIT      "Catalog/Supplier         (sincicaf)"
BLAB6     INIT      "Catalog/Warehouse        (sincieaf)"
BLAB7     INIT      "Supply/Issue Factors     (sinfacaf)"
BLAB8     INIT      "Imprest                  (sinimpaf)"
BLAB9     INIT      "Originator               (sinoriaf)"
BLAB10    INIT      "Purchase Order Header    (sinpoaaf)"
BLAB11    INIT      "Purchase Order Detail    (sinpocaf)"
BLAB12    INIT      "Purchase Order Receivals (sinpofaf)"
BLAB13    INIT      "Product Group            (sinsuaaf)"
BLAB14    INIT      "Warehouse                (sinwaraf)"
BLAB15    INIT      " "
BLAB16    INIT      " "
BLAB17    INIT      " "
BLAB18    INIT      " "
BLAB19    INIT      " "
BLAB20    INIT      " "
BLABMAX   FORM      2
.
ADDED     FORM      3        * no. of added records
CHANGED   FORM      3        * no. of changed records
DELETED   FORM      3        * no. of deleted records
TEMPNUM   FORM      3        * temporary form 3 variable
OPTIONB   FORM      2        * option on screen B
OPTIONC   DIM       2        * option on screen B
PRINTALL  FORM      1        * 0=No
.
PRGID     INIT      "IBASIN96"
PRGNAM    INIT      "Audit File Reporting"
VERSION   INIT      "V12.01.00"
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000
          CALL      ONAT0000
.
ML1000    CALL      SCRB0000      * display screen B and get option
          BRANCH    EXIT,ML9999   * user wants to exit screen
          IF        PRINTALL=TRUE
            MOVE      ZERO,OPTIONB
            REPEAT  
              ADD       ONE,OPTIONB
              CALL      PRNT0000
            UNTIL     OPTIONB=BLABMAX
          ELSE
            CALL      PRNT0000      * print audits
          ENDIF
.
ML2000    CALL      SCRC0000      * delete audits ?
          GOTO      ML1000        * get another option
.
ML9999    CHAIN     PGM
.******************************************************************************
.* INIT - Open Files                                                          *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening sinaudba";
          OPEN      SINAUDBA,"sinaudba"
.
          DISPLAY   *P56:24,"Opening sinaudca";
          OPEN      SINAUDCA,"sinaudca"
.
          DISPLAY   *P56:24,"Opening sinaudga";
          OPEN      SINAUDGA,"sinaudga"
.
          DISPLAY   *P56:24,"Opening sinaudia";
          OPEN      SINAUDIA,"sinaudia"
.
          DISPLAY   *P56:24,"Opening sinaudic";
          OPEN      SINAUDIC,"sinaudic"
.
          DISPLAY   *P56:24,"Opening sinaudie";
          OPEN      SINAUDIE,"sinaudie"
.
          DISPLAY   *P56:24,"Opening sinaudfa";
          OPEN      SINAUDFA,"sinaudfa"
.
          DISPLAY   *P56:24,"Opening sinaudim";
          OPEN      SINAUDIM,"sinaudim"
.
          DISPLAY   *P56:24,"Opening sinaudor";
          OPEN      SINAUDOR,"sinaudor"
.
          DISPLAY   *P56:24,"Opening sinaudpa";
          OPEN      SINAUDPA,"sinaudpa"
.
          DISPLAY   *P56:24,"Opening sinaudpc";
          OPEN      SINAUDPC,"sinaudpc"
.
          DISPLAY   *P56:24,"Opening sinaudpf";
          OPEN      SINAUDPF,"sinaudpf"
.
          DISPLAY   *P56:24,"Opening sinaudsa";
          OPEN      SINAUDSA,"sinaudsa"
.
          DISPLAY   *P56:24,"Opening sinaudwa";
          OPEN      SINAUDWA,"sinaudwa"
.
          DISPLAY   *P1:24,*EL;
.
INIT9999  RETURN
.------------------------------------------------------------------------
. Open Nato X-Ref File if required
.------------------------------------------------------------------------
ONAT0000  OPEN      CONTROLF,"controlf"
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF FORMAT
          READ      CONTROLF,SIXTY3;*141,INCNNATO
          TRAPCLR   FORMAT
          IF        OVRCD=1
            MOVE      ZERO,INCNNATO
            WRITAB    CONTROLF,SIXTY3;*141,INCNNATO
          ENDIF
.
          IF        INCNNATO=1
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      SINNATA1,"sinnataf"
            TRAPCLR   IO
            IF        OVRCD=1
              DISPLAY   *P1:24,*EL,*B,"Nato file does not exist. Run fixit.  ";
              CALL      HITENTER
              CHAIN     PGM
            ENDIF
.
            OPEN      SINNATA2,"sinnataf"
          ENDIF
.
ONAT9999  RETURN
.**********************************************************************
. SCRB -  display screen B and get print option      Called By   ML0000
.         Returns : EXIT       (0=continue, 1=quit)
.**********************************************************************
SCRB0000  DISPLAY   *P1:3,*EF,*+:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = ",BLAB1:
                    *P1:6,*V2LON," 2",*HOFF," = ",BLAB2:
                    *P1:7,*V2LON," 3",*HOFF," = ",BLAB3:
                    *P1:8,*V2LON," 4",*HOFF," = ",BLAB4:
                    *P1:9,*V2LON," 5",*HOFF," = ",BLAB5:
                   *P1:10,*V2LON," 6",*HOFF," = ",BLAB6:
                   *P1:11,*V2LON," 7",*HOFF," = ",BLAB7:
                   *P1:12,*V2LON," 8",*HOFF," = ",BLAB8:
                   *P1:13,*V2LON," 9",*HOFF," = ",BLAB9:
                   *P1:14,*V2LON,"10",*HOFF," = ",BLAB10:
                   *P1:15,*V2LON,"11",*HOFF," = ",BLAB11:
                   *P1:16,*V2LON,"12",*HOFF," = ",BLAB12:
                   *P1:17,*V2LON,"13",*HOFF," = ",BLAB13:
                   *P1:18,*V2LON,"14",*HOFF," = ",BLAB14:
                   *P1:24,"Select, Print ",*V2LON,"A",*HOFF,"ll :";
          MOVE     "14",BLABMAX          **** Update when increased ****
.
SCRB1000  MOVE      SP70,OPTIONC
          MOVE      FALSE,PRINTALL
          KEYIN     *P21:24,"__",*P21:24,*V2LON,*JR,OPTIONC,*EL; * read option
          PACK      OPTIONC,OPTIONC,SP70 
          MATCH     SP70,OPTIONC
          GOTO      SCRB9500 IF EQUAL
          SQUEEZE   OPTIONC
          REP       "aA",OPTIONC
          MATCH     "A",OPTIONC
          IF        @EQUAL
            MOVE      TRUE,PRINTALL
            GOTO      SCRB9000
          ENDIF
          TYPE      OPTIONC
          GOTO      SCRB2000 IF NOT EQUAL
.
          MOVE      OPTIONC,OPTIONB
          COMPARE   ZERO,OPTIONB
          GOTO      SCRB9500 IF EQUAL
          PACK      BLAB,SP70                    * clear screen label
          LOAD      BLAB,OPTIONB,BLAB1,BLAB2,BLAB3,BLAB4,BLAB5:
                                       BLAB6,BLAB7,BLAB8,BLAB9,BLAB10:
                                       BLAB11,BLAB12,BLAB13,BLAB14,BLAB15:
                                       BLAB16,BLAB17,BLAB18,BLAB19,BLAB20
.
          MATCH     SP70,BLAB
          GOTO      SCRB9000 IF NOT EQUAL        * legal option ?
.
SCRB2000  BEEP                                   * illegal option
          GOTO      SCRB1000                     * get another option
.
SCRB9000  MOVE      ZERO,EXIT                    * continue
          GOTO      SCRB9999
.
SCRB9500  MOVE      ONE,EXIT                     * quit
.
SCRB9999  RETURN
.**********************************************************************
. SCRB -  delete audits if required                  Called By   ML0000
.**********************************************************************
SCRC0000  IF        PRINTALL=TRUE
            DISPLAY   *P1:24,"Delete All Audits (Y/N) : ";
          ELSE
            DISPLAY   *P1:24,"Delete Audit File (Y/N) : ";
          ENDIF
.
SCRC1000  KEYIN     *P27:24,"_",*EL,*V2LON,*P27:24,ANS;    * get answer
          PACK      ANS,ANS,SP1
          REP       "1x2xY1y1N2n2",ANS          * convert answer
          MOVE      "9",FORM1
          MOVE      ANS,FORM1
          BRANCH    FORM1,SCRC2000,SCRC9999     * "yes" or "no" entered ?
          BEEP                                  * illegal option
          GOTO      SCRC1000                    * get another option
.
.---- delete audit file ----
.
SCRC2000  IF        PRINTALL=TRUE
            CALL      DELALL00
            GOTO      SCRC9999
          ENDIF
.
          CALL      FILE0000           * go to start of file
.
SCRC3000  CALL      NREC0000           * get next record
          BRANCH    OVRCD,SCRC9999     * no more records ?
          CALL      DREC0000           * delete record
          GOTO      SCRC3000           * get next record
.
SCRC9999  RETURN
.********************************************************************
. DELALL - Delete All Audits                        Called by ML0000 
.********************************************************************
DELALL00  MOVE      ZERO,OPTIONB
          REPEAT
            ADD       ONE,OPTIONB
            CALL      FILE0000           * go to start of file
            REPEAT
              CALL      NREC0000           * get next record
              IF        OVRCD=0
                CALL      DREC0000           * delete record
              ENDIF
            UNTIL     OVRCD=1
          UNTIL     OPTIONB=20
.
DELALL99  RETURN
.********************************************************************
. PRNT - print Report                               Called by ML0000 
.         Requires : OPTIONB (option to print)
.********************************************************************
PRNT0000  DISPLAY   *P50:24,"Printing :   ";
          MOVE      ZERO,CPAGENO                 * reset page no.
          CLOCK     TIME,CTIMEIS                 * get current time
          MOVE      ZERO,ADDED     * no. of added records
          MOVE      ZERO,CHANGED   * no. of changed records
          MOVE      ZERO,DELETED   * no. of deleted records
          CALL      HEAD0000                     * print header
.
          CALL      FILE0000                    * go to correct Position in file
.
PRNT1000  CALL      NREC0000                     * get next code record
          BRANCH    OVRCD,PRNT9000               * end of file ?
.
          COMPARE   "50",CLNO                    * end of page ?
          CALL      HEAD0000 IF NOT LESS         * start new page
.
          CALL      WRIT0000                     * print details
          GOTO      PRNT1000                     * get next record
.
PRNT9000  CALL      LINE0000                     * print line
          PRINT     *N,"Total Additions Printed : ",ADDED,*N:
                    "Total Changes Printed   : ",CHANGED,*N:
                    "Total Deletions Printed : ",DELETED,*N:
                    "****   End of Report   ****"
.
PRNT9999  RETURN
.********************************************************************
. FILE - got to start of desired file               Called by PRNT,SCRC 
.********************************************************************
FILE0000  MOVE      SP20,KEY19
          BRANCH    OPTIONB,FILE0100,FILE0200,FILE0300,FILE0400,FILE0500:
                            FILE0600,FILE0700,FILE0800,FILE0900,FILE1000:
                            FILE1100,FILE1200,FILE1300,FILE1400,FILE9999:
                            FILE9999,FILE9999,FILE9999,FILE9999,FILE9999
.
.---- audit file 1 ----
.
FILE0100  CALL      ASSIBA         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 2 ----
.
FILE0200  CALL      ASSICA         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 3 ----
.
FILE0300  CALL      ASSIGA         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 4 ----
.
FILE0400  CALL      ASSIIA         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 5 ----
.
FILE0500  CALL      ASSIIC         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 6 ----
.
FILE0600  CALL      ASSIIE         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 7 ----
.
FILE0700  CALL      ASSIFA         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 8 ----
.
FILE0800  CALL      ASSIIM         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 9 ----
.
FILE0900  CALL      ASSIOR         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 10 ----
.
FILE1000  CALL      ASSIPA         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 11 ----
.
FILE1100  CALL      ASSIPC         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 12 ----
.
FILE1200  CALL      ASSIPF         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 13 ----
.
FILE1300  CALL      ASSISA         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 14 ----
.
FILE1400  CALL      ASSIWA         * go to correct Position in file
          GOTO      FILE9999
.
FILE9999  RETURN
.********************************************************************
. NREC - get next record of current file            Called by PRNT,SCRC 
.********************************************************************
NREC0000  
          BRANCH    OPTIONB,NREC0100,NREC0200,NREC0300,NREC0400,NREC0500:
                            NREC0600,NREC0700,NREC0800,NREC0900,NREC1000:
                            NREC1100,NREC1200,NREC1300,NREC1400,NREC9999:
                            NREC9999,NREC9999,NREC9999,NREC9999,NREC9999
.
.---- audit file 1 ----
.
NREC0100  CALL      AKSIBA             * get next record
          GOTO      NREC9999
.
.---- audit file 2 ----
.
NREC0200  CALL      AKSICA             * get next record
          GOTO      NREC9999
.
.---- audit file 3 ----
.
NREC0300  CALL      AKSIGA             * get next record
          GOTO      NREC9999
.
.---- audit file 4 ----
.
NREC0400  CALL      AKSIIA             * get next record
          GOTO      NREC9999
.
.---- audit file 5 ----
.
NREC0500  CALL      AKSIIC             * get next record
          GOTO      NREC9999
.
.---- audit file 6 ----
.
NREC0600  CALL      AKSIIE             * get next record
          GOTO      NREC9999
.
.---- audit file 7 ----
.
NREC0700  CALL      AKSIFA             * get next record
          GOTO      NREC9999
.
.---- audit file 8 ----
.
NREC0800  CALL      AKSIIM             * get next record
          GOTO      NREC9999
.
.---- audit file 9 ----
.
NREC0900  CALL      AKSIOR             * get next record
          GOTO      NREC9999
.
.---- audit file 10 ----
.
NREC1000  CALL      AKSIPA             * get next record
          GOTO      NREC9999
.
.---- audit file 11 ----
.
NREC1100  CALL      AKSIPC             * get next record
          GOTO      NREC9999
.
.---- audit file 12 ----
.
NREC1200  CALL      AKSIPF             * get next record
          GOTO      NREC9999
.
.---- audit file 13 ----
.
NREC1300  CALL      AKSISA             * get next record
          GOTO      NREC9999
.
.---- audit file 14 ----
.
NREC1400  CALL      AKSIWA             * get next record
          GOTO      NREC9999
.
NREC9999  RETURN
.********************************************************************
. DREC - delete current record of desired file     Called by SCRC0000 
.********************************************************************
DREC0000  
          BRANCH    OPTIONB,DREC0100,DREC0200,DREC0300,DREC0400,DREC0500:
                            DREC0600,DREC0700,DREC0800,DREC0900,DREC1000:
                            DREC1100,DREC1200,DREC1300,DREC1400,DREC9999:
                            DREC9999,DREC9999,DREC9999,DREC9999,DREC9999
.
.---- audit file 1 ----
.
DREC0100  BRANCH    SIBAAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,SIBAAUDD,SIBAAUDT,SIBAAUDP,SIBAAUDR,SIBAAUDS,SIBAAUDO
          CALL      ADSIBA            * delete record
          PACK      KEY19,SP70
          CALL      ASSIBA            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 2 ----
.
DREC0200  BRANCH    SICAAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,SICAAUDD,SICAAUDT,SICAAUDP,SICAAUDR,SICAAUDS,SICAAUDO
          CALL      ADSICA            * delete record
          PACK      KEY19,SP70
          CALL      ASSICA            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 3 ----
.
DREC0300  BRANCH    SIGAAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,SIGAAUDD,SIGAAUDT,SIGAAUDP,SIGAAUDR,SIGAAUDS,SIGAAUDO
          CALL      ADSIGA            * delete record
          PACK      KEY19,SP70
          CALL      ASSIGA            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 4 ----
.
DREC0400  BRANCH    SIIAAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,SIIAAUDD,SIIAAUDT,SIIAAUDP,SIIAAUDR,SIIAAUDS,SIIAAUDO
          CALL      ADSIIA            * delete record
          PACK      KEY19,SP70
          CALL      ASSIIA            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 5 ----
.
DREC0500  BRANCH    SIICAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,SIICAUDD,SIICAUDT,SIICAUDP,SIICAUDR,SIICAUDS,SIICAUDO
          CALL      ADSIIC            * delete record
          PACK      KEY19,SP70
          CALL      ASSIIC            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 6 ----
.
DREC0600  BRANCH    SIIEAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,SIIEAUDD,SIIEAUDT,SIIEAUDP,SIIEAUDR,SIIEAUDS,SIIEAUDO
          CALL      ADSIIE            * delete record
          PACK      KEY19,SP70
          CALL      ASSIIE            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 7 ----
.
DREC0700  BRANCH    SIFAAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,SIFAAUDD,SIFAAUDT,SIFAAUDP,SIFAAUDR,SIFAAUDS,SIFAAUDO
          CALL      ADSIFA            * delete record
          PACK      KEY19,SP70
          CALL      ASSIFA            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 8 ----
.
DREC0800  BRANCH    SIIMAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,SIIMAUDD,SIIMAUDT,SIIMAUDP,SIIMAUDR,SIIMAUDS,SIIMAUDO
          CALL      ADSIIM            * delete record
          PACK      KEY19,SP70
          CALL      ASSIIM            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 9 ----
.
DREC0900  BRANCH    SIORAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,SIORAUDD,SIORAUDT,SIORAUDP,SIORAUDR,SIORAUDS,SIORAUDO
          CALL      ADSIOR            * delete record
          PACK      KEY19,SP70
          CALL      ASSIOR            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 10 ----
.
DREC1000  BRANCH    SIPAAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,SIPAAUDD,SIPAAUDT,SIPAAUDP,SIPAAUDR,SIPAAUDS,SIPAAUDO
          CALL      ADSIPA            * delete record
          PACK      KEY19,SP70
          CALL      ASSIPA            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 11 ----
.
DREC1100  BRANCH    SIPCAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,SIPCAUDD,SIPCAUDT,SIPCAUDP,SIPCAUDR,SIPCAUDS,SIPCAUDO
          CALL      ADSIPC            * delete record
          PACK      KEY19,SP70
          CALL      ASSIPC            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 12 ----
.
DREC1200  BRANCH    SIPFAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,SIPFAUDD,SIPFAUDT,SIPFAUDP,SIPFAUDR,SIPFAUDS,SIPFAUDO
          CALL      ADSIPF            * delete record
          PACK      KEY19,SP70
          CALL      ASSIPF            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 13 ----
.
DREC1300  BRANCH    SISAAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,SISAAUDD,SISAAUDT,SISAAUDP,SISAAUDR,SISAAUDS,SISAAUDO
          CALL      ADSISA            * delete record
          PACK      KEY19,SP70
          CALL      ASSISA            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 14 ----
.
DREC1400  BRANCH    SIWAAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,SIWAAUDD,SIWAAUDT,SIWAAUDP,SIWAAUDR,SIWAAUDS,SIWAAUDO
          CALL      ADSIWA            * delete record
          PACK      KEY19,SP70
          CALL      ASSIWA            * reposition file pointer
          GOTO      DREC9999
.
DREC9999  RETURN
.********************************************************************
. HEAD - print report                             Called by PRNT0000 
.********************************************************************
HEAD0000  CALL      HEAD132                      * print heading
          MOVE      SP70,BLAB                    * set up default file type
          LOAD      BLAB,OPTIONB,BLAB1,BLAB2,BLAB3,BLAB4,BLAB5:
                                       BLAB6,BLAB7,BLAB8,BLAB9,BLAB10:
                                       BLAB11,BLAB12,BLAB13,BLAB14,BLAB15:
                                       BLAB16,BLAB17,BLAB18,BLAB19,BLAB20
.
          PRINT     "Audit Report For ",BLAB     * print description
          ADD       ONE,CLNO
          CALL      LINE0000                     * print line
          PRINT     "|   Date   |   Time   |Port|Type| User |":
                    " Audit Data",*132,"|"
          ADD       ONE,CLNO
          CALL      LINE0000                     * print line
.
HEAD9999  RETURN
.********************************************************************
. LINE - print line                               Called by PRNT0000 
.********************************************************************
LINE0000  PRINT     "*-------------------------------------------":
                    "--------------------------------------------":
                    "-------------------------------------------*"
          ADD       ONE,CLNO           * add one to line number
.
LINE9999  RETURN
.********************************************************************
. WRIT - print data line                          Called by PRNT0000 
.********************************************************************
WRIT0000  
.
.----- total additions, changes, and deletions ----
.
          MOVE      SP1,DIM1
          LOAD      DIM1,OPTIONB,SIBAAUDR,SICAAUDR,SIGAAUDR,SIIAAUDR,SIICAUDR:
                                 SIIEAUDR,SIFAAUDR,SIIMAUDR,SIORAUDR,SIPAAUDR:
                                 SIPCAUDR,SIPFAUDR,SISAAUDR,SIWAAUDR
          REP       "A1B2D3",DIM1                          * convert type 
          MOVE      ZERO,FORM1
          MOVE      DIM1,FORM1
.
          LOAD      TEMPNUM,FORM1,ADDED,CHANGED,DELETED    * get type
          ADD       ONE,TEMPNUM                            * increment type
          STORE     TEMPNUM,FORM1,ADDED,CHANGED,DELETED    * store type
.
.---- print data ----
.
          BRANCH    OPTIONB,WRIT0100,WRIT0200,WRIT0300,WRIT0400,WRIT0500:
                            WRIT0600,WRIT0700,WRIT0800,WRIT0900,WRIT1000:
                            WRIT1100,WRIT1200,WRIT1300,WRIT1400,WRIT9999:
                            WRIT9999,WRIT9999,WRIT9999,WRIT9999,WRIT9999
.
.---- print data for file 1 ----
.
WRIT0100  UNPACK    SIBAAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",SIBAAUDT," | ",SIBAAUDP," |  ":
                    SIBAAUDR," | ",SIBAAUDO," | ":
                    SIBACST,"~",SIBACAT,"~",SIBAREQ,"~",SIBAWAR,"~":
                    SIBADAT,"~",SIBAQTY,"~",SIBARESI,"~",SIBASPA,"~":
                    *132,"|"
          ADD       "1",CLNO                     * add one to line number
          DISPLAY   *P61:24,*V2LON,SIBAAUDT,*EL; * display data on screen
.
          MOVE      "2",SIBAAUDS
          CALL      AUSIBA                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 2 ----
.
WRIT0200  UNPACK    SICAAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",SICAAUDT," | ",SICAAUDP," |  ":
                    SICAAUDR," | ",SICAAUDO," | ":
                    SICACODE,"~",SICADESC,"~",SICATELE,"~",SICACONT,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SICAGL,"~",SICAIMPD,"~",SICADAD1,"~",SICADAD2,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SICADAD3,"~",SICALOC,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SICAMES,"~",SICAUR1,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SICAUR2,"~",SICAUD1,"~",SICAUD2,"~",SICAUC1,"~":
                    SICAUC2,"~",SICAUY1,"~",SICAUY2,"~",SICARID,"~",SICASPA,"~":
                    *132,"|"
.
          ADD       "5",CLNO                     * add one to line number
          DISPLAY   *P61:24,*V2LON,SICAAUDT,*EL; * display data on screen
.
          MOVE      "2",SICAAUDS
          CALL      AUSICA                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 3 ----
.
WRIT0300  UNPACK    SIGAAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",SIGAAUDT," | ",SIGAAUDP," |  ":
                    SIGAAUDR," | ",SIGAAUDO," | ":
                    SIGACOD,"~",SIGADES,"~",SIGAGLI,"~",SIGAUDD1,"~":
                    SIGAUDD2,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIGAUDY1,"~",SIGAUDY2,"~",SIGAUDR1,"~",SIGAUDR2,"~":
                    SIGASPA,"~":
                    *132,"|"
.
          ADD       "2",CLNO                     * add one to line number
          DISPLAY   *P61:24,*V2LON,SIGAAUDT,*EL; * display data on screen
.
          MOVE      "2",SIGAAUDS
          CALL      AUSIGA                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 4 ----
.
WRIT0400  UNPACK    SIIAAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",SIIAAUDT," | ",SIIAAUDP," |  ":
                    SIIAAUDR," | ",SIIAAUDO," | ":
                    SIIACAT,"~",SIIADES,"~",SIIASUB,"~",SIIAISS,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIIASTI,"~",SIIAAVC,"~",SIIANAT,"~",SIIAABC,"~":
                    SIIAPSC,"~",SIIAREX,"~",SIIANON,"~",SIIASAB,"~":
                    SIIASUS,"~",SIIADTC,"~",SIIATMC,"~",SIIAUSC,"~",SIIAUR1,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIIAUR2,"~",SIIAUD1,"~",SIIAUD2,"~",SIIAUC1,"~":
                    SIIAUC2,"~",SIIAUY1,"~",SIIAUY2,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIIAQIN,"~",SIIADUN,"~",SIIAGST,"~",SIIASPA,"~":
                    *132,"|"
.
          ADD       "4",CLNO                     * add one to line number
          DISPLAY   *P61:24,*V2LON,SIIAAUDT,*EL; * display data on screen
.
          MOVE      "2",SIIAAUDS
          CALL      AUSIIA                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 5 ----
.
WRIT0500  UNPACK    SIICAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",SIICAUDT," | ",SIICAUDP," |  ":
                    SIICAUDR," | ",SIICAUDO," | ":
                    SIICCAT,"~",SIICSUP,"~",SIICSUT,"~",SIICCCT,"~":
                    SIICLCT,"~",SIICLTM,"~",SIICPNO,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIICPD1,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIICPD2,"~",SIICMAN,"~",SIICAPO,"~",SIICUR1,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIICUR2,"~",SIICUD1,"~",SIICUD2,"~",SIICUC1,"~":
                    SIICUC2,"~",SIICUY1,"~",SIICUY2,"~",SIICSPA,"~":
                    *132,"|"
.
          ADD       "4",CLNO                     * add one to line number
          DISPLAY   *P61:24,*V2LON,SIICAUDT,*EL; * display data on screen
.
          MOVE      "2",SIICAUDS
          CALL      AUSIIC                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 6 ----
.
WRIT0600  UNPACK    SIIEAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",SIIEAUDT," | ",SIIEAUDP," |  ":
                    SIIEAUDR," | ",SIIEAUDO," | ":
                    SIIECAT,"~",SIIEWAR,"~",SIIESID,"~",SIIERAS,"~":
                    SIIESOH,"~",SIIESOO,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIIEROQ,"~",SIIEROL,"~",SIIESTI,"~",SIIESTD,"~",SIIESIC,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIIESAB,"~",SIIEABC,"~",SIIESUS,"~",SIIEUR1,"~":
                    SIIEUR2,"~",SIIEUC1,"~",SIIEUC2,"~",SIIEUD1,"~":
                    SIIEUD2,"~",SIIEUY1,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIIEUY2,"~",SIIESPA,"~":
                    *132,"|"
.
          ADD       "4",CLNO                     * add one to line number
          DISPLAY   *P61:24,*V2LON,SIIEAUDT,*EL; * display data on screen
.
          MOVE      "2",SIIEAUDS
          CALL      AUSIIE                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 7 ----
.
WRIT0700  UNPACK    SIFAAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",SIFAAUDT," | ",SIFAAUDP," |  ":
                    SIFAAUDR," | ",SIFAAUDO," | ":
                    SIFAISSU,"~",SIFASUPP,"~",SIFACTOR,"~",SIFAFILL,"~":
                    *132,"|"
.
          ADD       "1",CLNO                     * add one to line number
          DISPLAY   *P61:24,*V2LON,SIFAAUDT,*EL; * display data on screen
.
          MOVE      "2",SIFAAUDS
          CALL      AUSIFA                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 8 ----
.
WRIT0800  UNPACK    SIIMAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",SIIMAUDT," | ",SIIMAUDP," |  ":
                    SIIMAUDR," | ",SIIMAUDO," | ":
                    SIIMCST,"~",SIIMCAT,"~",SIIMSEQ,"~",SIIMMAX,"~":
                    SIIMREQ,"~",SIIMQOH,"~",SIIMUR1:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    "~",SIIMUR2,"~",SIIMUD1,"~",SIIMUD2,"~",SIIMUC1,"~",SIIMUC2:
                    "~",SIIMUY1,"~",SIIMUY2,"~",SIIMWAR,"~",SIIMSPA,"~":
                    *132,"|"
.
          ADD       "2",CLNO                     * add one to line number
          DISPLAY   *P61:24,*V2LON,SIIMAUDT,*EL; * display data on screen
.
          MOVE      "2",SIIMAUDS
          CALL      AUSIIM                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 9 ----
.
WRIT0900  UNPACK    SIORAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",SIORAUDT," | ",SIORAUDP," |  ":
                    SIORAUDR," | ",SIORAUDO," | ":
                    SIORORIG,"~",SIORNAME,"~",SIORHOSP,"~",SIORDEL,"~":
                    SIORMESS,"~",SIOREXT,"~",SIORCON,"~",SIORUR1,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIORUR2,"~",SIORUD1,"~",SIORUD2,"~",SIORUC1,"~":
                    SIORUC2,"~",SIORUY1,"~",SIORUY2,"~",SIORSPA,"~":
                    *132,"|"
.
          ADD       "2",CLNO                     * add one to line number
          DISPLAY   *P61:24,*V2LON,SIORAUDT,*EL; * display data on screen
.
          MOVE      "2",SIORAUDS
          CALL      AUSIOR                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 10 ----
.
WRIT1000  UNPACK    SIPAAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",SIPAAUDT," | ",SIPAAUDP," |  ":
                    SIPAAUDR," | ",SIPAAUDO," | ":
                    SIPAPON,"~",SIPASUP,"~",SIPAORI,"~",SIPAICN,"~",SIPASCN,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIPADEL,"~",SIPAMES,"~",SIPADIN,"~",SIPADPR,"~",SIPADCN,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIPADCM,"~",SIPACON,"~",SIPAIDP,"~",SIPAUR1,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIPAUR2,"~",SIPAUD1,"~",SIPAUD2,"~",SIPAUC1,"~":
                    SIPAUC2,"~",SIPAUY1,"~",SIPAUY2,"~",SIPASPA,"~":
                    *132,"|"
.
          ADD       "4",CLNO                     * add one to line number
          DISPLAY   *P61:24,*V2LON,SIPAAUDT,*EL; * display data on screen
.
          MOVE      "2",SIPAAUDS
          CALL      AUSIPA                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 11 ----
.
WRIT1100  UNPACK    SIPCAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",SIPCAUDT," | ",SIPCAUDP," |  ":
                    SIPCAUDR," | ",SIPCAUDO," | ":
                    SIPCPON,"~",SIPCITM,"~",SIPCCST,"~",SIPCPRD,"~":
                    SIPCDAT,"~",SIPCWAR,"~",SIPCCAT,"~",SIPCPN,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIPCPD,"~",SIPCCON,"~",SIPCCDT,"~",SIPCSUT,"~":
                    SIPCEDD,"~",SIPCFDD,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                   SIPCOQT,"~",SIPCIQT,"~",SIPCRQT,"~",SIPCECT,"~",SIPCGSTA,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIPCTCR,"~",SIPCTGS,"~",SIPCTIN,"~",SIPCIGS,"~":
                    SIPCOQO,"~",SIPCOOP,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIPCOOP,"~",SIPCOGS,"~",SIPCGST,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIPCUR1,"~",SIPCUR2,"~",SIPCUD1,"~",SIPCUD2,"~":
                    SIPCUY1,"~",SIPCUY2,"~",SIPCSPA,"~":
                    *132,"|"
.
          ADD       "5",CLNO                     * add one to line number
          DISPLAY   *P61:24,*V2LON,SIPCAUDT,*EL; * display data on screen
.
          MOVE      "2",SIPCAUDS
          CALL      AUSIPC                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 12 ----
.
WRIT1200  UNPACK    SIPFAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",SIPFAUDT," | ",SIPFAUDP," |  ":
                    SIPFAUDR," | ",SIPFAUDO," | ":
                    SIPFPON,"~",SIPFITM,"~",SIPFREC,"~",SIPFDAT,"~":
                    SIPFDEL,"~",SIPFQTY,"~",SIPFCST,"~",SIPFGST,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIPFSUT,"~",SIPFNDD,"~",SIPFDUP,"~",SIPFUDT,"~":
                    SIPFEDD,"~",SIPFSPA,"~":
                    *132,"|"
.
          ADD       "2",CLNO                     * add one to line number
          DISPLAY   *P61:24,*V2LON,SIPFAUDT,*EL; * display data on screen
.
          MOVE      "2",SIPFAUDS
          CALL      AUSIPF                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 13 ----
.
WRIT1300  UNPACK    SISAAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",SISAAUDT," | ",SISAAUDP," |  ":
                    SISAAUDR," | ",SISAAUDO," | ":
                    SISACODE,"~",SISADESC,"~",SISACOMM,"~",SISAGLI:
                    "~",SISAUR1,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SISAUR2,"~",SISAUD1,"~",SISAUD2,"~",SISAUC1,"~":
                    SISAUC2,"~",SISAUY1,"~",SISAUY2,"~",SISASPA,"~":
                    *132,"|"
.
          ADD       "2",CLNO                     * add one to line number
          DISPLAY   *P61:24,*V2LON,SISAAUDT,*EL; * display data on screen
.
          MOVE      "2",SISAAUDS
          CALL      AUSISA                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 14 ----
.
WRIT1400  UNPACK    SIWAAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",SIWAAUDT," | ",SIWAAUDP," |  ":
                    SIWAAUDR," | ",SIWAAUDO," | ":
                    SIWAWAR,"~",SIWADES,"~",SIWATYP,"~",SIWACTY,"~":
                    SIWAPTY,"~",SIWALED,"~",SIWAIACC,"~",SIWAPACC,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIWADISS,"~",SIWARESP,"~",SIWAUR1,"~",SIWAUR2,"~":
                    SIWAUD1,"~",SIWAUD2,"~",SIWAUC1,"~",SIWAUC2,"~":
                    SIWAUY1,"~",SIWAUY2,"~",SIWAPIC,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    SIWAPA1,"~",SIWAPA2,"~",SIWAPA3,"~",SIWAPA4,"~":
                    SIWANPO,"~",SIWASTA,"~":
                    *132,"|"
.
          ADD       "3",CLNO                     * add one to line number
          DISPLAY   *P61:24,*V2LON,SIWAAUDT,*EL; * display data on screen
.
          MOVE      "2",SIWAAUDS
          CALL      AUSIWA                       * set status flag to "printed"
          GOTO      WRIT9999
.
.
WRIT9999  PRINT     "|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|",*132,"|"
          ADD       ONE,CLNO                     * add one to line number
          RETURN
.******************************************************************************
.* INCLUDES FOR I/O'S                                                         *
.******************************************************************************
.
          INC       STDGENCD
.
          INC       SINBACIO/INC
          INC       SINCCAIO/INC
          INC       SINCGAIO/INC
          INC       SINCIAIO/INC
          INC       SINCICIO/INC
          INC       SINCIEIO/INC
          INC       SINFACIO/INC
          INC       SINIMPIO/INC
          INC       SINORIIO/INC
          INC       SINPOAIO/INC
          INC       SINNATIO/INC
          INC       SINPOCIO/INC
          INC       SINPOFIO/INC
          INC       SINSUAIO/INC
          INC       SINWARIO/INC

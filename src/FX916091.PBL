.***************************************************************************
.* System    :   Eclipse/WebServices                                       *
.* Program   :   Fix Program - FX916091                                    *
.* Desc      :                                                             *
.***************************************************************************
.* Date      :   02/02/2022                                                *
.* Author    :   Sunny                                                     *
.* Function  :   This program will copy outstanding Health Fund Claims     *
.*               from the existing Eclipse table to the new WebServices    *
.*               table to enable customers a seamless transition           *
.* Modifications  :                                                        *
.*       V11.02.04  12/05/2022  Peter Vela    TSK 0918312                  *
.*                  Added start date/time end date/time display            *
.*       V11.02.03  12/04/2022  Peter Vela    TSK 0916091                  *
.*                  Added Status 15 Closed to record transfer              *
.*                  Closed records that have have an                       *
.*                  assessment report pmseahaf and no payment report       *
.*                  pmserdaf                                               *
.*       V11.02.02  25/02/2022  Peter Vela    TSK 0916091                  *
.*                  Fixed infinite loop                                    *
.*                  Added batch records                                    *
.*       V11.02.01  02/02/2022  Sunny         TSK 0916091                  *
.*                  Created program - Eclipse to WebServices Mapping       *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PMSEAHFD/INC
          INC       PMSECTFD/INC
          INC       PMSEBHFD/INC
          INC       PMSERDFD/INC
          INC       WEBIHCFD/INC
          INC       WEBIHBFD/INC
          
.
. LOCAL VARIABLE DEFINITION
. -------------------------
PIPE      INIT      "|"
DISP0405  INIT      "No of records mapped from 04(old) to 05(new): "
DISP0708  INIT      "No of records mapped from 07(old) to 08(new): "
DISP0910  INIT      "No of records mapped from 09(old) to 10(new): "
DISP1314  INIT      "No of records mapped from 13(old) to 14(new): "
DISP1516  INIT      "No of records mapped from 15(old) to 16(new): "
.
ADDCNT04  FORM      8
ADDCNT07  FORM      8
ADDCNT09  FORM      8
ADDCNT13  FORM      8
ADDCNT15  FORM      8
DIM8      DIM       8
TOTALCNT  FORM      8
FLAGTEMP  DIM       2
HOSPTYPE  DIM       2
KEYPMECT  DIM       40
.
.
PRGID     INIT      "FX916091"
PRGNAM    INIT      "Eclipse to WebServices Mapping"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PMSEAHA2,"pmseahaf"
          OPEN      PMSECTA6,"pmsectaf"
          OPEN      PMSEBHA1,"pmsebhaf"
          OPEN      PMSERDA2,"pmserdaf"
          OPEN      WEBIHCA6,"webihcaf"
          OPEN      WEBIHBA1,"webihbaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = Exit    (0)  Exit program                         *
.*                        Public  (1)                                       *
.*                        Private (2)                                       *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:4,*V2LON,ONE,*HOFF,". Public(PU)":
                    *P1:5,*V2LON,TWO,*HOFF,". Private(PR)"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * public hosp type
                            OPTN9100             * private hosp type 
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          MOVE      "PU",HOSPTYPE
          GOTO      OPTN9999
.
OPTN9100  MOVE      ZERO,EXIT
          MOVE      "PR",HOSPTYPE
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: PRNT0000 *
.*                           Print The Report Header                          *
.******************************************************************************
.
HEAD0000  CALL      HEAD80                       * print report header
          PRINT     *20,"Copy data from Eclipse(pmsectaf) ":
                        "to WebServices(webihcaf)"
.
          CALL      LINE0000                     * print line
.
          PRINT     *1,PIPE,*3,"Invoice Number":
                    *20,PIPE,*23,"Duplicate Transaction ID found":
                    *80,PIPE 
.
          CALL      LINE0000
.
          MOVE      SEVEN,CLNO                   * set line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by: HEAD0000  *
.*                      draw line across page                     PMST0000  *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "-------------------------------*"
.
          ADD       ONE,CLNO
.
LINE9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  CALL      HEAD0000
          CALL      CLWEBIHC                   * clear webihcfd var's 
          MOVE      ZERO,ADDCNT04              * initialize counters  
          MOVE      ZERO,ADDCNT07             
          MOVE      ZERO,ADDCNT09            
          MOVE      ZERO,ADDCNT13
          MOVE      ZERO,ADDCNT15
          MOVE      SP70,KEYPMECT           
.
          DISPLAY   *P1:11,*EL,*V2LON,*HOFF,"Processing records.."
.
          CALL      IBACLOCK
          MOVE      CTIMEIS,DIM8
          REP       " 0",DIM8
          DISPLAY   *P1:12,"Start Time: ",DIM8
.
          PACK      KEY40,SP70
          CALL      RSPMECT6                  * positional read on Eclipse
PROC1000  CALL      RKPMECT6                  * Claim Table (pmsectaf)
          BRANCH    OVRCD,PROC9000
.
          MATCH     SP70,DPMECFLG
          GOTO      PROC1000 IF EQUAL          
.
          MATCH     " 4",PMECSTAT             * check if already transferred
          GOTO      PROC1000 IF EQUAL      
.
          DISPLAY   *P1:13,"Invoice Number: ",PMECINVN
.
PROC1050  MATCH     " 4",DPMECFLG
          IF        @EQUAL
            MOVE      " 5",FLAGTEMP
            ADD       ONE,ADDCNT04
            GOTO      PROC2000
          ENDIF
.
          MATCH     " 7",DPMECFLG
          IF        @EQUAL
            MOVE      " 8",FLAGTEMP
            ADD       ONE,ADDCNT07
            GOTO      PROC2000
          ENDIF
.
          MATCH     " 9",DPMECFLG
          IF        @EQUAL
            MOVE      "10",FLAGTEMP
            ADD       ONE,ADDCNT09
            GOTO      PROC2000
          ENDIF
.
          MATCH     "13",DPMECFLG
          IF        @EQUAL
            MOVE      "14",FLAGTEMP
            ADD       ONE,ADDCNT13 
            GOTO      PROC2000
          ENDIF
.
          MATCH     "15",DPMECFLG
          IF        @EQUAL
.
            MATCH     SP70,PMECTRID
            IF        !@EQUAL
.
              PACK      KEY62,PMECTRID,SP70
              CALL      RSPMEAH2
              CALL      RKPMEAH2
              BRANCH    OVRCD,PROC1000
.
              MATCH     PMECTRID,PMAHFTID
              GOTO      PROC1000 IF NOT EQUAL
.
              PACK      KEY82,PMECTRID,SP70,SP70
              CALL      RSPMERD2
              CALL      RKPMERD2
              BRANCH    OVRCD,PROC1075
.
              MATCH     PMECTRID,PMEDTRID
              GOTO      PROC1075 IF NOT EQUAL
.
              GOTO       PROC1000       
.
PROC1075      MOVE      "16",FLAGTEMP
              ADD       ONE,ADDCNT15
              GOTO      PROC2000
.
            ENDIF
.
          ENDIF
.
          GOTO      PROC1000
.
PROC2000  CALL      CLWEBIHC                   * clear webihcfd var's       
          PACK      KEYPMECT,PMECTRID,PMECINVN,PMECBATN,SP70
          PACK      KEY40,PMECTRID,SP70
          CALL      RSWBIHC6
          CALL      RKWBIHC6
          BRANCH    OVRCD,PROC2100
.
          MATCH     SP70,PMECTRID
          GOTO      PROC2100 IF EQUAL  
.
          MATCH     PMECTRID,WBICTRID
          IF        @EQUAL
                      PRINT *1,PIPE,*3,PMECINVN,*20,PIPE,*23,PMECTRID:
                            *80,PIPE
          ENDIF
.
PROC2100  MOVE      HOSPTYPE,WBICCTYP
          MOVE      FLAGTEMP,WBICFLAG
          CALL      MVPMWB00
.
          PACK     KEY40,WBICTRID,WBICINVN,WBICBATN,SP70
          CALL     RAWBIHC6
          IF       OVRCD = 1
            CALL      WRWBIHC6
.
            PACK      KEY40,KEYPMECT,SP70
            CALL      RDPMECT6
            IF        OVRCD=0
              MOVE      " 4",PMECSTAT
              CALL      UPPMECT6                     * update claim
            ENDIF
.
.
.
            PACK      KEY8,PMECBATN,SP70            
            CALL      RDPMEBH1
            IF        OVRCD=0
.
              PACK      KEY8,PMEBBTHN,SP70
              CALL      RAWBIHB1
              IF        OVRCD=1
.
                MOVE      PMEBBTHN,WBIJBTHN
                MOVE      PMEBHFND,WBIJHFND
                MOVE      PMEBBHTL,WBIJBHTL
                MOVE      PMEBTRIB,WBIJTRIB
                MOVE      PMEBDTBC,WBIJDTBC
                MOVE      PMEBTMBC,WBIJTMBC
                MOVE      PMEBOPER,WBIJOPER
                MOVE      PMEBEETP,WBIJEETP
                MOVE      PMEBEFNM,WBIJEFNM
                MOVE      PMEBLOCN,WBIJLOCN
                MOVE      PMEBSNID,WBIJSNID
                PACK      WBIJSPAR,SP70,SP70              
.
                CALL      WRWBIHB1
.
              ENDIF
.
            ENDIF
.
.
.
            ADD       ONE,TOTALCNT
.
            PACK      KEY40,KEYPMECT,SP70
            CALL      RSPMECT6 
          ENDIF
.
          GOTO       PROC1000            
.
PROC9000  PRINT     *N,DISP0405,*47,ADDCNT04:
                    *N,DISP0708,*47,ADDCNT07:
                    *N,DISP0910,*47,ADDCNT09:
                    *N,DISP1314,*47,ADDCNT13:
                    *N,DISP1516,*47,ADDCNT15:
                    *N,"----------------------------------":
                    *N,"Total Records Copied: ",TOTALCNT:
                    *N,"----------------------------------"; 
          PRINT     *N,*1,"*** End of Report ***";
.
          CALL      IBACLOCK
          MOVE      CTIMEIS,DIM8
          REP       " 0",DIM8
          DISPLAY   *P1:14,"End Time: ",DIM8
.                        
          DISPLAY   *P1:16,*EF,DISP0405,ADDCNT04:
                    *P1:17,*EF,DISP0708,ADDCNT07:
                    *P1:18,*EF,DISP0910,ADDCNT09:
                    *P1:19,*EF,DISP1314,ADDCNT13:  
                    *P1:20,*EF,DISP1516,ADDCNT15:
                    *P1:22,*EF,"Total Records Copied: ",TOTALCNT:  
                    *P1:24,*EF;
          CALL      HITENTER          
.
PROC9999  RETURN
+
. -------------------------------------------------------------------------
.         MVPMWB00      
. -------------------------------------------------------------------------
MVPMWB00  MOVE      PMECHFND,WBICHFND       * Health Fund Code
          MOVE      PMECADMN,WBICADMN       * Admission No
          MOVE      PMECINVN,WBICINVN       * Invoice No
          MOVE      PMECBATN,WBICBATN       * Batch No
          MOVE      PMECURNO,WBICURNO       * U/R No
          MOVE      PMECPBAT,WBICPBAT       * Prev Batch No
          MOVE      PMECNBAT,WBICNBAT       * Next Batch No
          MOVE      PMECCCFL,WBICCCFL       * Contiguous Claim Flag
          MOVE      PMECTRID,WBICTRID       * Claim Transaction Id
          MOVE      DPMECEET,WBICEETP       * Eclipse extract type
          MOVE      PMECAMTC,WBICAMTC       * Amount Claimed
          MOVE      PMECAMTP,WBICAMTP       * Amount Paid by Health Fund
          MOVE      PMECPDAT,WBICPDAT       * Payment Date (ccyymmdd)
          MOVE      PMECSTAT,WBICSTAT       * Closed Status
          MOVE      PMECFTID,WBICFTID       * Fund Report Transaction Id
          MOVE      PMECHOSP,WBICHOSP       * Hospital ID
          MOVE      PMECPCTI,WBICPCTI       * Previous Claim Transaction ID
          MOVE      PMECSPAR,WBICSPAR       * Spare Variable  
.
MVPMWB99  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       CLWEBIHC
.      
          INC       PMSEAHIO/INC
          INC       PMSECTIO/INC
          INC       PMSEBHIO/INC
          INC       PMSERDIO/INC
          INC       WEBIHCIO/INC
          INC       WEBIHBIO/INC

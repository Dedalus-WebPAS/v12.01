.******************************************************************************
.* System         : All                                                       *
.* Program        : CKTXTFIL                                                  *
.* Description    : Check Textfile                                            *
.******************************************************************************
.* Date           : 30/10/1997                                                *
.* Author         : Junior                                                    *
.* Function       : Reads a textfile & reports if any lines aren't the keyed  *
.*                  in length or if there any control characters in the line. *
.* Modifications  :                                                           *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
.
. ----- Tempfile Definition -----
.
TEMP1     FILE      ASCII, FIXED=1270
.
.Name     Type    Length    Start    Description
.----     ----    ------    -----    -----------
KEY127A   DIM       127       1      Line 1
KEY127B   DIM       127      128     Line 2
KEY127C   DIM       127      255     Line 3
KEY127D   DIM       127      382     Line 4
KEY127E   DIM       127      509     Line 5
KEY127F   DIM       127      636     Line 6
KEY127G   DIM       127      763     Line 7
KEY127H   DIM       127      890     Line 8
KEY127I   DIM       127      1017    Line 9
KEY127J   DIM       127      1144    Line 10
.
.End of Record               1270
.
. ----- Program Variables -----
.
CHRNO     FORM      3                       * Character no
.
ERRCNT    FORM      7                       * Error counter
ERRLNE    DIM       27                      * Error line
ERRNO     FORM      2                       * Error no
ENDOFLNE  FORM      4                       * End of line
.
FILENAME  DIM       12                      * Filename
FORM3     FORM      3
FORM3A    FORM      3
.
KEY127    DIM       127
LNECNT    FORM      7                       * Line counter
VARNO     FORM      2                       * Variable no
.
. ----- Program Constants -----
.
ERRLNE01  INIT      "Line too short             "
ERRLNE02  INIT      "Line too long              "
ERRLNE03  INIT      "Line has control characters"
.
PRGNAM    INIT      "Check Textfile"
PRGID     INIT      "CKTXTFIL"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise varibales & open files
ML1000    CALL      FILE0000                * Keyin the filename
          BRANCH    EXIT,ML9999
.
ML2000    CALL      EOFL0000                * Keyin end of file
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML3000,ML2000,ML1000
.
ML3000    CALL      PROC0000                * Process the textfile
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                  INIT0000                                  *
.*                      Initialise Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
          CLOCK     TIME,CTIMEIS
.
INIT9000  MOVE      ZERO,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  FILE0000              Called by: ML0000   *
.*                             Keyin The Filename                             *
.******************************************************************************
FILE0000  MOVE      "19",CCOL
          MOVE      "4",CVERT
          DISPLAY   *P1:CVERT,*EF,"Textfile Name   : ";
.
FILE1000  MOVE      SP20,FILENAME
          KEYIN     *PCCOL:CVERT,*EL,*V2LON,FILENAME;
.
          ENDSET    FILENAME
          APPEND    SP20,FILENAME
          RESET     FILENAME
.
          CMATCH    EXITCHAR,FILENAME
          GOTO      FILE9500 IF EQUAL       * Exit
.
          MATCH     SP20,FILENAME
          GOTO      FILE9500 IF EQUAL       * Exit
.
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,FILENAME;
          MOVE      FILENAME,KEY12
          REP       "./",KEY12
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TEMP1,KEY12
          TRAPCLR   IO
          COMPARE   ONE,OVRCD
          GOTO      FILE9000 IF NOT EQUAL   * File exists, exit
.
          DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      FILE1000
.
FILE9000  MOVE      ZERO,EXIT
          GOTO      FILE9999
.
FILE9500  MOVE      ONE,EXIT
FILE9999  RETURN
+
.******************************************************************************
.*                                  EOFL0000              Called by: ML0000   *
.*                            Keyin The End Of File                           *
.******************************************************************************
EOFL0000  MOVE      "19",CCOL
          MOVE      "6",CVERT
          DISPLAY   *P1:CVERT,*EF,"End Of Textfile : ";
.
EOFL1000  MOVE      ZERO,ENDOFLNE
          KEYIN     *PCCOL:CVERT,*V2LON,ENDOFLNE;
.
          COMPARE   ZERO,ENDOFLNE
          GOTO      EOFL9500 IF EQUAL
.
          IF        ENDOFLNE<0 | ENDOFLNE>1270
            BEEP
            GOTO      EOFL1000
          ENDIF
.
EOFL9000  MOVE      ZERO,EXIT
          GOTO      EOFL9999
.
EOFL9500  MOVE      ONE,EXIT
EOFL9999  RETURN
+
.******************************************************************************
.*                                  PROC0000              Called by: ML0000   *
.*                            Process The Textfile                            *
.******************************************************************************
PROC0000  MOVE      ZERO,CPAGENO
          CALL      HEAD0000                * Print report header
          MOVE      ZERO,ERRCNT
          MOVE      ZERO,LNECNT
          DISPLAY   *P1:24,*EL,"Scanning Line : ",*P35:24,"Errors : ";
.
          MOVE      ZERO,VARNO
          ASSIGN    ENDOFLNE/127,VARNO
          ADD       ONE,VARNO
          MOVE      ZERO,CHRNO
          ASSIGN    ENDOFLNE%127,CHRNO
.
PROC1000  READ      TEMP1,SEQ;KEY127A,KEY127B,KEY127C,KEY127D,KEY127E:
                              KEY127F,KEY127G,KEY127H,KEY127I,KEY127J
          GOTO      PROC6000 IF OVER
.
          ADD       ONE,LNECNT
          IF        LNECNT%10=1
            DISPLAY   *P17:24,*V2LON,LNECNT;
          ENDIF
.
          MOVE      KEY127A,KEY127
          LOAD      KEY127,VARNO,KEY127A,KEY127B,KEY127C,KEY127D,KEY127E:
                                 KEY127F,KEY127G,KEY127H,KEY127I,KEY127J
.
          RESET     KEY127,CHRNO
          GOTO      PROC2000 IF EOS         * No character before EOL, error
.
          BUMP      KEY127
          GOTO      PROC3000 IF NOT EOS     * Character after EOL, error
          GOTO      PROC4000
.
PROC2000  MOVE      "1",ERRNO
          CALL      PRNT0000                * Print error line
          GOTO      PROC4000
.
PROC3000  MOVE      "2",ERRNO
          CALL      PRNT0000                * Print error line
.
PROC4000  MOVE      ONE,FORM2
          REPEAT
            RESET     KEY127
            LOAD      KEY127,VARNO,KEY127A,KEY127B,KEY127C,KEY127D,KEY127E:
                                   KEY127F,KEY127G,KEY127H,KEY127I,KEY127J
            MOVE      ONE,FORM3
            MOVE      "127",FORM3A
            IF        FORM2>=VARNO
              MOVE      CHRNO,FORM3A
            ENDIF
            REPEAT
              MOVE     KEY127,KEY1
              MATCH    SP1,KEY1
              GOTO     PROC5000 IF LESS     * Character > space, error
.
              BUMP     KEY127
              ADD      ONE,FORM3
            UNTIL    FORM3>FORM3A
            ADD      ONE,FORM2
          UNTIL     FORM2>VARNO
          GOTO      PROC1000
.
PROC5000  MOVE      "3",ERRNO
          CALL      PRNT0000                * Print error line
          GOTO      PROC1000
.
PROC6000  CALL      UND80
          PRINT     *N,*1,"Records Read : ",LNECNT:
                    *N,*1,"Errors Found : ",ERRCNT:
                    *N,*N,*1,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*B,"Check Finished.  ";
          IF        ERRCNT>0
            DISPLAY   "Errors Found.  ";
          ENDIF
          CALL      HITENTER
.
PROC9000  MOVE      ZERO,EXIT
PROC9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: PROC0000 *
.*                             Print Report Header                 & PRNT0000 *
.******************************************************************************
.Line No|Error No & Type               |Text                                   |
HEAD0000  CALL      HEAD80
          PRINT     *20,"Textfile Name   : ",FILENAME,*N:
                    *20,"End Of Textfile : ",ENDOFLNE,*N
          CALL      UND80
          PRINT     *1,"|Line No|Error No & Type",*40,"|Text",*80,"|"
          CALL      UND80
          ADD       "4",CLNO
.
HEAD9000  MOVE      ZERO,EXIT
HEAD9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: PROC0000 *
.*                             Print Report Header                            *
.******************************************************************************
.1234567|12 123456789012345678901234567|123456789012345678901234567890123456789|
PRNT0000  MOVE      SP30,ERRLNE
          LOAD      ERRLNE,ERRNO,ERRLNE01,ERRLNE02,ERRLNE03
          MOVE      KEY127A,KEY39
          PRINT     *1,"|",LNECNT,"|",ERRNO,SP1,ERRLNE,"|",KEY39,*80,"|"
          ADD       "1",CLNO
          COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS    * Line count >= 60, print header
.
          ADD       ONE,ERRCNT
          DISPLAY   *P44:24,*V2LON,ERRCNT;
.
PRNT9000  MOVE      ZERO,EXIT
PRNT9999  RETURN
+
.==============================================================================
.
          INC       STD001IO
+
.


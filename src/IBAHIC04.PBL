.***************************************************************************
.* System    :   HIC Claims                                                *
.* Program   :   IBAHIC04                                                  *
.* Desc      :   Denied Claims Report                                      *
.***************************************************************************
.* Date      :   06/05/2005                                                *
.* Author    :   J.Tan                                                     *
.* Function  :   This program will list all batches that have been         *
.*           :   rejected  according to the filters entered                *
.*                                                                         *
.*           V10.11.01 25/07/2017  Ebon Clements     TSK 0268970           *
.*                       Change to use OUTCLIFD index 1 instead of index 2 *
.*           V10.11.01   21/07/2017  Ebon Clements  TSK 0268970            *
.*                       Change to use OUTCLIFD index 1 instead of index 2 *
.*           V10.04.01   15/04/2014  J.Tan          CAR 261630             *
.*                       Removed port number from temp file name           *
.*           V9.05.01    10/03/2006 J.Tan         CAR 96067                *
.*                       Mods maximum number of lines to print             *
.*           V9.05.B01   01/02/2005  J.Tan          CAR 93766              *
.*                       Fixed HIC claim comments                          *
.*                       Sort report by med.prac/clinic/batch date/batch no*
.*           V9.04.04    18/11/2005  J.Tan          CAR 77215              *
.*                       Mods to include all batches that have been receivd*
.*           V9.04.03    21/10/2005  J.Tan          CAR 77215              *
.*                       Mods to print batches after had been actioned     *
.*           V9.04.02    01/08/2005  J.Tan          CAR 69062              *
.*                       Changed claim index 7 to include voucher number   *
.*           V9.04.01    07/06/2005  J.Tan          CAR 56701              *
.*                       Mods to print service date instead of claim date  *
.*           V9.04.00    06/05/2005  J.Tan          CAR 56701              *
.*                       Created program                                   *
.***************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       HICBTCFD/INC
          INC       HICCLMFD/INC
          INC       HICCITFD/INC
          INC       HICEMSFD/INC
          INC       PMSHCPFD/INC
          INC       PRIPRAFD/INC
          INC       PRIDOCFD/INC
          INC       OUTSITFD/INC
          INC       OUTCLIFD/INC
          INC       PATCODFD/INC
          INC       WEBSECFD/INC
          INC       PRIALSFD/INC
          INC       PRISECFD/INC
          INC       PATMA1FD/INC
          INC       VISCMTFD/INC
.
.         Temporary File Definition
.         -------------------------
.
.         Filename : hic04axx.dat          (xx = port id)
.
HICTEMP1  IFILE SQL, FIXED=62, KEY="UC23-27,46-53"
HICTEMP2  IFILE SQL, FIXED=62, KEY="UC7-12,1-6,54-61,23-27,46-53"
.
.NAME     TYPE    LENGTH     PHYSICAL     START     DESCRIPTION
.----     ----    ------     --------     -----     -----------
TEMPCLID  DIM       6      6        1     Clinic Id
TEMPMPRA  DIM       6      6        7     Medical Practice
TEMPPSRV  DIM       10     10       13    Service Provider Number
TEMPBTCH  DIM       5      5        23    Batch Number (1 Alpha 4 Numeric)
TEMPPMCI  DIM       8      8        28    MCI (3 Alpha 5 Numeric)
TEMPPYEE  DIM       10     10       36    Payee Provider Number
TEMPCLMN  DIM       8      8        46    Claim Number
TEMPBDAT  DIM       8      8        54    Batch Date
.End of Record                      62
.
.         Temporary File Variables
.
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
TEMPFILE  DIM       8
TOTCLMN   FORM      9.2
TOTAMNT   FORM      9.2
UKEY      INIT      " 62 UC23-27,46-53 UC7-12,1-6,54-61,23-27,46-53"
. LOCAL VARIABLE DEFINITION
. -------------------------
.
ALACCESS  FORM      1
CLINFROM  DIM       6
CLINICTO  DIM       6
CMDLINE   DIM       127
DISPNAME  DIM       60
DOCFNAME  DIM       50
FORM9P2   FORM      9.2
FORM12P2  FORM      12.2
FILTPROV  DIM       10
FILBTCTO  DIM       5
FILBTCFR  DIM       5
FILCLMTO  DIM       8
FILCLMFR  DIM       8
FMTSNAME  DIM       35
FMTGNAME  DIM       35
FMTTITLE  DIM       10
HEADFLAG  FORM      1
HEADING1  DIM       30
HEADING2  DIM       30
HEADING3  DIM       40
HEADING4  DIM       10
HEADING5  DIM       30
HEADING6  DIM       30    
HEADING7  DIM       30    
HEADING8  DIM       30    
HEADING9  DIM       30    
HEADING0  DIM       30    
NAME35    DIM       35
PRACTIFR  DIM       6
PRACTITO  DIM       6
SITE      DIM       6
SAVEVAR   DIM       12
SAVEBTCH  DIM       5
WEBUSRID  DIM       10
.
PRGID     INIT      "IBAHIC04"
PRGNAM    INIT      "Denied Claims Report    "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
          BRANCH    EXIT,ML9999
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML2000,ML3000
.
          CALL      PRCF0000               * keyin practice from
          BRANCH    EXIT,ML0100
.
          CALL      PRCT0000               * keyin practice to
          BRANCH    EXIT,ML0100
.
          CALL      FCLN0000               * keyin clinic from
          BRANCH    EXIT,ML0100
.
          CALL      TCLN0000               * keyin clinic to
          BRANCH    EXIT,ML0100
.
          CALL      KPRV0000               * keyin provider
          GOTO      ML4000
.
.         Keyin range of batch number
.
ML2000    CALL      FBTCH000
          CALL      TBTCH000
          GOTO      ML4000
.
.         Keyin range of claim number
.
ML3000    CALL      FCLMN000
          CALL      TCLMN000
          GOTO      ML4000
.
ML4000    CALL      WEBU0000               * keyin user
          BRANCH    EXIT,ML0100
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML6000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML6000    CALL      CREA0000               * Create tempfile
.
          IF        COPTION=1
            CALL      PROB0000             * process by batch no
          ENDIF
          IF        COPTION=2
            CALL      PROC0000             * process by claim number
          ENDIF
          IF        COPTION=3
            CALL      PROP0000             * process clinic id/medical prac
          ENDIF
.
          CALL      PRIN0000               * Print data
          CALL      ENDP0000               * End of report
.
          CALL      KILL0000               * Delete tempfile
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          CALL      IBACLOCK
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TEMPFILE
.
          OPEN      HICBTCA1,"hicbtcaf"       * batch file
          OPEN      HICBTCA5,"hicbtcaf"       * batch file
          OPEN      HICCITA1,"hiccitaf"       * item claim file
          OPEN      HICEMSA1,"hicemsaf"       * medicare explanation file
          OPEN      HICCLMA1,"hicclmaf"       * claim file
          OPEN      HICCLMA7,"hicclmaf"       * claim file
          OPEN      PMSHCPA1,"pmshcpaf"       * HCP file
          OPEN      PRIPRAC1,"pripracf"       * private practice
          OPEN      PRIDOCT1,"pridoctf"       * practice doctors
          OPEN      OUTSITA1,"outsitaf"       * outpatient site file
          OPEN      PATCODE1,"patcodes"       * codes file
          OPEN      WEBSECA1,"websecaf"       * web user id
          OPEN      PRIALSA1,"prialsaf"       * all practice security
          OPEN      PRISECA1,"prisecaf"       * practice security
          OPEN      PATMA1A1,"patma1af"       * master file
          OPEN      PATMX1A1,"patmx1af"
          OPEN      VISCMTA1,"viscmtaf"
.
          OPEN      OUTSITA1,"outsitaf"
          MOVE      IDDD,SITE
          MOVE      IDDD,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,INIT9000
.
          PACK      CFNAME WITH UID,FILCLIA1        * OUTCLIFD
          OPEN      OUTCLIA1,CFNAME
.
          MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9000  DISPLAY   *P1:24,*EL,*B,"Site Code not on file. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      INIT9999
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". By Batch Number":
                    *P1:6,*V2LON,TWO,*HOFF,". By Claim Number":
                    *P1:7,*V2LON,THREE,*HOFF,". By Clinic ID/Med Prac."
.
OPTN0500  KEYIN     *P1:9,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:9,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          MOVE      SP70,CLINFROM
          MOVE      SP70,CLINICTO
          MOVE      SP70,PRACTIFR
          MOVE      SP70,PRACTITO
          MOVE      SP70,FILTPROV
.
          BRANCH    COPTION,OPTN9000,OPTN9000,OPTN9000    * run clean-up
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.**************************************************************************
.*                              CREA0000               Called by:         *
.*             create a new temporary file                                *
.**************************************************************************
.
CREA0000  CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      HICTEMP1,TEMPFILE            * open temp index file
          OPEN      HICTEMP2,TEMPFILE            * open temp index file
.
          MOVE      ZERO,ALACCESS           * Set all access to no
          PACK      KEY10,WEBUSRID,SP70
          CALL      RDPRALS1
          IF        OVRCD=0
            MOVE      ONE,ALACCESS
          ENDIF
          UNPACK    SP70,TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPBTCH
          UNPACK    SP70,TEMPPMCI,TEMPPYEE,TEMPCLMN,TEMPBDAT
.
CREA9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by:           *
.*               close and erase the temporary file                         *
.****************************************************************************
.
KILL0000  CLOSE     HICTEMP1                     * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
+
.**************************************************************************
.*                               PROB0000              Called by: ML0000  *
.*                        By batch number                                 *
.**************************************************************************
PROB0000  PACK      KEY33,FILBTCFR,SP70
          CALL      RSHCBTC1
PROB1000  CALL      RKHCBTC1
          BRANCH    OVRCD,PROB9999
.
          MATCH     IDDD,HCBTSITE                * My site
          GOTO      PROB1000 IF NOT EQUAL
.
.         Check range of Batch number
.
          MATCH     SP70,FILBTCTO
          IF        !@EQUAL
            MATCH     HCBTBTCH,FILBTCTO
            GOTO      PROB9999 IF LESS
          ENDIF
.
.         Batch status must have at least been actioned 
.
          MOVE      ZERO,FORM2
          MOVE      HCBTSTAT,FORM2
          COMPARE   FIVE,FORM2             * batch status must be aftr Received
          GOTO      PROB1000 IF LESS
.
          BRANCH    ALACCESS,PROB2000       * Access to all practices 
.
          PACK      KEY16,HCBTMPRA,WEBUSRID,SP70
          CALL      RDPRSEC1
          BRANCH    OVRCD,PROB1000          * No access to this practice
.
PROB2000  CALL      CITM0000                * Write to tempfile
          GOTO      PROB1000
.
PROB9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                           By Claim number                              *
.**************************************************************************
PROC0000  PACK      KEY8,FILCLMFR
          CALL      RDHCCLM1
          COMPARE   ZERO,OVRCD
          GOTO      PROC2000 IF EQUAL
.
PROC1000  CALL      RKHCCLM1
          BRANCH    OVRCD,PROC9999
.
          MATCH     IDDD,HCCLSITE                * My site
          GOTO      PROC1000 IF NOT EQUAL
.
.         Check range of claim number
.
          MATCH     SP70,FILCLMTO
          IF        !@EQUAL
            MATCH     HCCLCLMN,FILCLMTO
            GOTO      PROC9999 IF LESS
          ENDIF
.
          COMPARE   ZERO,HCCLREJA           * Check total amount rejected
          GOTO      PROC1000 IF EQUAL
.
          BRANCH    ALACCESS,PROC2000       * Access to all practices 
.
          PACK      KEY16,HCCLMPRA,WEBUSRID,SP70
          CALL      RDPRSEC1
          BRANCH    OVRCD,PROC1000          * No access to this practice
.
.         Batch status must have at least been actioned once
.
PROC2000  PACK      KEY33,HCCLBTCH,HCCLPSRV,HCCLPMCI,HCCLPYEE,SP70
          CALL      RDHCBTC1
          BRANCH    OVRCD,PROC1000
.
          MOVE      ZERO,FORM2
          MOVE      HCBTSTAT,FORM2
          COMPARE   FIVE,FORM2             * Check batch status
          GOTO      PROC1000 IF LESS
.
          CALL      WTMP0000                * Write to tempfile
          GOTO      PROC1000
.
PROC9999  RETURN
+
.**********************************************************************
.*                         PROP0000                                   *
.*                    By clinic id and medical practice               *
.**********************************************************************
PROP0000  MOVE      " 5",HCBTSTAT
          PACK      KEY35,HCBTSTAT,FILTPROV,SP70
          CALL      RSHCBTC5
PROP1000  CALL      RKHCBTC5
          BRANCH    OVRCD,PROP9999
.
          MATCH     IDDD,HCBTSITE            * My site
          GOTO      PROP1000 IF NOT EQUAL
.
          MATCH     SP70,FILTPROV
          IF        !@EQUAL
            MATCH     FILTPROV,HCBTPSRV      * Same provider
            GOTO      PROP1000 IF NOT EQUAL
          ENDIF
.
          BRANCH    ALACCESS,PROP6000       * Access to all practices 
.
          PACK      KEY16,HCBTMPRA,WEBUSRID,SP70
          CALL      RDPRSEC1
          BRANCH    OVRCD,PROP1000          * No access to this practice
.
.         Check range of clinic id
.
PROP6000  MATCH     SP70,CLINFROM
          IF        !@EQUAL
            MATCH     CLINFROM,HCBTCLID    * clinic id within range?
            GOTO      PROP1000 IF LESS
          ENDIF
.
          MATCH     SP70,CLINICTO
          IF        !@EQUAL
            MATCH     HCBTCLID,CLINICTO    * clinic id within range?
            GOTO      PROP1000 IF LESS
          ENDIF
.
.         Check for range of medical practice
.
          MATCH     SP70,PRACTIFR
          IF        !@EQUAL
            MATCH     PRACTIFR,HCBTMPRA
            GOTO      PROP1000 IF LESS
          ENDIF
.
          MATCH     SP70,PRACTITO
          IF        !@EQUAL
            MATCH     HCBTMPRA,PRACTITO
            GOTO      PROP1000 IF LESS
          ENDIF
.
          CALL      CITM0000                  * Write to tempfile
          GOTO      PROP1000
.
PROP9999  RETURN
+
.**********************************************************************
.*                         CITM0000                                   *
.*        Loop through claim file for the batch number                *
.**********************************************************************
CITM0000  PACK      KEY15,HCBTBTCH,SP70
          CALL      RSHCCLM7
CITM1000  CALL      RKHCCLM7
          BRANCH    OVRCD,CITM9999
.
          MATCH     HCBTBTCH,HCCLBTCH       * Same batch number
          GOTO      CITM9999 IF NOT EQUAL
.
          COMPARE   ZERO,HCCLREJA           * Check rejected amount
          GOTO      CITM1000 IF EQUAL
.
          CALL      WTMP0000                * write to tempfile
          GOTO      CITM1000
.
CITM9999  RETURN
+
.**********************************************************************
.*                         WTMP0000                                   *
.*                        Write to tempfile                           *
.**********************************************************************
WTMP0000  MOVE      HCCLCLID,TEMPCLID
          MOVE      HCCLMPRA,TEMPMPRA
          MOVE      HCCLPSRV,TEMPPSRV
          MOVE      HCCLBTCH,TEMPBTCH
          MOVE      HCCLPMCI,TEMPPMCI
          MOVE      HCCLPYEE,TEMPPYEE
          MOVE      HCCLCLMN,TEMPCLMN
          MOVE      HCBTDATE,TEMPBDAT
.
          PACK      KEY13,TEMPBTCH,TEMPCLMN,SP70
          CALL      RATEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ENDIF
.
WTMP9999  RETURN
.
.**********************************************************************
.*                         PRIN0000                                   *
.*                        Print Data                                  *
.**********************************************************************
PRIN0000  MOVE      ZERO,CPAGENO
          MOVE      ZERO,TOTCLMN
          MOVE      ZERO,TOTAMNT
          MOVE      SP70,SAVEBTCH
          MOVE      SP70,SAVEVAR
          MOVE      SP70,KEY32
          CALL      RSTEMP2
PRIN1000  CALL      RKTEMP2
          BRANCH    OVRCD,PRIN9000
.
          PACK      KEY33,TEMPBTCH,TEMPPSRV,TEMPPMCI,TEMPPYEE,SP70
          CALL      RDHCBTC1
          BRANCH    OVRCD,PRIN1000               * Invalid batch number
.
          MATCH     SP70,SAVEVAR
          GOTO      PRIN1200 IF EQUAL            * first record
          CALL      CVAR0000                     * check if sort field changed
          BRANCH    EXIT,PRIN1100                * changed
.
          MATCH     SP70,SAVEBTCH
          GOTO      PRIN1300 IF EQUAL
.
          MATCH     HCBTBTCH,SAVEBTCH
          GOTO      PRIN1400 IF EQUAL
          GOTO      PRIN1300
.
PRIN1100  CALL      UND132
          PRINT     *52,"| TOTAL",*62,"|",TOTCLMN,*75,"|",TOTAMNT,*88,"|":
                    *N,*52,"*---------",*62,"*------------":
                    *75,"*------------*"
          ADD       TWO,CLNO
          MOVE      ZERO,TOTCLMN
          MOVE      ZERO,TOTAMNT
          MOVE      ONE,CNOUNDLN                 * no underline
.
PRIN1200  CALL      HEAD0000
          MOVE      ZERO,CNOUNDLN
          CALL      SVAR0000                     * save sort variable
.
PRIN1300  PACK      TDESC,SP70
          MATCH     SP70,HCBTCMNT
          IF        !@EQUAL
            PACK      KEY5,ANSI,ANSE,HCBTCMNT
            CALL      RDCODE1
          ENDIF
          IF        HEADFLAG=0
            CALL      UND132
          ENDIF
.
          COMPARE   "48",CLNO
          CALL      HEAD0000 IF NOT LESS
          PRINT     *1,"| Batch Number : ",HCBTBTCH:
                    *32,"      Comment:",TDESC,*132,"|"
          CALL      UND132                   * finish off report
          ADD       ONE,CLNO
          MOVE      HCBTBTCH,SAVEBTCH            * Save batch number
.
PRIN1400  MOVE      TEMPCLMN,KEY8
          CALL      RDHCCLM1
          BRANCH    OVRCD,PRIN1000               * Invalid claim number
.
.         Check outstanding amount
.
          MOVE      ZERO,FORM12P2
          MOVE      HCCLCAMN,FORM12P2
          SUB       HCCLRCVA,FORM12P2            * received
          SUB       HCCLWOFF,FORM12P2            * write off
          ADD       HCCLADJM,FORM12P2            * adjustment
          COMPARE   ZERO,FORM12P2
          GOTO      PRIN1000 IF EQUAL            * no outstanding amount
.
          PACK      KEY18,HCCLCLMN,SP70
          CALL      RSHCCIT1
PRIN2000  CALL      RKHCCIT1
          BRANCH    OVRCD,PRIN1000
.
          MATCH     HCCLCLMN,HCCICLMN
          GOTO      PRIN1000 IF NOT EQUAL        * Different claim number
.
          COMPARE   ZERO,HCCIREJA
          GOTO      PRIN2000 IF EQUAL            * no rejection amount
.
          MATCH     SP70,HCCIIDAT
          IF        !@EQUAL
            UNPACK    HCCIIDAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
          ENDIF
.
          MOVE      SP70,KEY24
          MOVE      HCCLURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD=0
            MOVE      PSNAME,PACSNAME
            MOVE      PGNAME,PACGNAME
            MOVE      PTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,KEY20
          ENDIF
.
          COMPARE   "48",CLNO
          CALL      HEAD0000 IF NOT LESS
          MOVE      HCCICAMT,FORM9P2
          PRINT     *1,"|",HCCLCLMN,*10,"|",HCCLURNO:
                    *19,"|",KEY20,*40,"|",CPCDATE:
                    *52,"|",HCCIITMN,*62,"|",FORM9P2;
          ADD       HCCICAMT,TOTCLMN
.
          MOVE      HCCIREJA,FORM9P2
          PRINT     *75,"|",FORM9P2;
          ADD       HCCIREJA,TOTAMNT              * total rejection amount
.
          MOVE      SP70,KEY18
          MATCH     SP70,HCCIREJR
          IF        !@EQUAL
            MOVE      HCCIREJR,KEY3
            CALL      RDHCEMS1
            IF        OVRCD=0
              PACK      KEY20,HCEMDESC
            ENDIF
          ENDIF
          CALL      GETCMN00              * Get claim comment
          PRINT     *88,"|",KEY20:
                    *109,"|",KEY22,*132,"|"
          ADD       ONE,CLNO
          MOVE      ZERO,HEADFLAG
          GOTO      PRIN2000              * read next record
.
PRIN9000  COMPARE   ZERO,CPAGENO
          GOTO      PRIN9999 IF EQUAL
.
          CALL      UND132                   * finish off report
          PRINT     *52,"| TOTAL",*62,"|",TOTCLMN,*75,"|",TOTAMNT,*88,"|":
                    *N,*52,"*---------",*62,"*------------":
                    *75,"*------------*"
.
PRIN9999  RETURN
+
.*********************************************************************
.         Get claim comments
.*********************************************************************
GETCMN00  MOVE      SP70,KEY22
          MOVE      "006",KEY3
          PACK      KEY14,HCCIVISN,KEY3,SP70
          CALL      RSVSCMT1
GETCMN10  CALL      RKVSCMT1
          BRANCH    OVRCD,GETCMN99
.
          MATCH     HCCIVISN,VSCTVIST     * Same visit number
          GOTO      GETCMN99 IF NOT EQUAL
.
          MATCH     KEY3,VSCTTYPE         * HIC Claim comment
          GOTO      GETCMN99 IF NOT EQUAL
.
          PACK      KEY22,VSCTDATA
GETCMN99  RETURN
+
.*********************************************************************
.         Save sort variable
.*********************************************************************
SVAR0000  MOVE      SP70,SAVEVAR
.
.         Sort by medical practice/clinic id
.
          PACK      SAVEVAR,TEMPMPRA,TEMPCLID,SP70
.
SVAR9999  RETURN
+
.*********************************************************************
.         check if sort variable change
.*********************************************************************
CVAR0000  MOVE      ZERO,EXIT
.
          PACK      KEY12,TEMPMPRA,TEMPCLID,SP70
          MATCH     SAVEVAR,KEY12        * check medical practice/clinic id
          GOTO      CVAR9999 IF EQUAL
.
CVAR9000  MOVE      ONE,EXIT
CVAR9999  RETURN

.*********************************************************************
.         print heading
.*********************************************************************
PRHD0000  COMPARE   "48",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MATCH     SP70,TEMPMPRA
          GOTO      PRHD2000 IF EQUAL
.
          PACK      PRPRDESC,SP70
          PACK      KEY6,TEMPMPRA
          CALL      RDPRPR1
          PRINT     *1,"Medical Practice : ",PRPRDESC;
.
PRHD2000  MATCH     SP70,TEMPCLID
          GOTO      PRHD9000 IF EQUAL
.
          MOVE      SP70,OCADESC
          PACK      KEY20,IDDD,TEMPCLID,Z70
          CALL      RDSCLIA1
          CALL      RDPCLIA1                *To get Clinic ID Description
          IF        OVRCD=0
            MATCH     IDDD,OCASITE
            GOTO      PRHD4000 IF EQUAL
.
            MATCH     TEMPCLID,OCACLIN
            GOTO      PRHD4000 IF EQUAL
          ENDIF
.
          MOVE      SP70,OCADESC
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
          ENDIF
.
PRHD4000  PRINT     *60,"Clinic Id : ",OCADESC,"(",TEMPPSRV,")";
.
PRHD9000  PRINT     *N;
          ADD       ONE,CLNO
PRHD9999  RETURN
+
.*********************************************************************
.         print a new page
.*********************************************************************
HEAD0000  MOVE      SP70,CPHDROPT
          IF        COPTION=1
            MOVE      "- By Batch Number",CPHDROPT
          ENDIF
          IF        COPTION=2
            MOVE      "- By Claim Number",CPHDROPT
          ENDIF
          IF        COPTION=3
            MOVE      "- By Med.Practice",CPHDROPT
          ENDIF
          CALL      HEAD132
.
          COMPARE   ONE,CPAGENO
          GOTO      HEAD2000 IF NOT EQUAL
.
          IF        COPTION=1
            PRINT     *1,"Batch Number From     : ":
                      HEADING1," To : ",HEADING2,*N
          ENDIF
.
          IF        COPTION=2
            PRINT     *1,"Claim Number From     : ":
                      HEADING3," To : ",HEADING4,*N
          ENDIF
.
          IF        COPTION=3
            PRINT     *1,"Medical Practice From : ",HEADING7," To : ",HEADING8
            PRINT     *1,"Clinic From           : ":
                      HEADING5," To : ",HEADING6
            PRINT     *1,"Provider Number : ",HEADING0,*N
            ADD       ONE,CLNO
          ENDIF
.
HEAD2000  CALL      PRHD0000                     * print sort heading
.
          CALL      UND132
          PRINT     *1,"|Claim No",*10,"| U/R No":
                    *19,"|Patient Name",*40,"|Serv.Date":
                    *52,"|Item No",*62,"| Item Amount":
                    *75,"|Amnt.denied",*88,"|Reason by HIC":
                    *109,"|Claim Comment",*132,"|"
          CALL      UND132
          ADD       THREE,CLNO
          MOVE      ONE,HEADFLAG
.
HEAD9999  RETURN
+
.**********************************************************************
.*                           ENDP0000                                 *
.*                      Print the end of the report                   *
.**********************************************************************
ENDP0000  COMPARE   ZERO,CPAGENO
          GOTO      ENDP9000 IF NOT EQUAL
.
          CALL      HEAD0000
ENDP9000  PRINT     *N,*50,"*** End of Report ***"
ENDP9999  RETURN
+
.******************************************************************************
.*                                   FBTCH000             Called by: ML0000   *
.*                             Keyin the Batch number                         *
.******************************************************************************
FBTCH000  MOVE      SP70,FILBTCFR
          KEYIN     *P1:9,*EL,"Batch No From: ",*V2LON,FILBTCFR
.
          PACK      FILBTCFR,FILBTCFR,SP70
          MATCH     SP70,FILBTCFR
          GOTO      FBTCH200 IF EQUAL
.
          MOVE      FILBTCFR,HEADING1
          DISPLAY   *P29:9,*EL,*V2LON,FILBTCFR;
          MOVE      ZERO,EXIT
          GOTO      FBTCH999
.
FBTCH200  MOVE      "All",HEADING1
          DISPLAY   *P29:9,*EL,*V2LON,"All";
          MOVE      ZERO,EXIT
          GOTO      FBTCH999
.
FBTCH999  RETURN
+
.******************************************************************************
.*                                   TBTCH000             Called by: ML0000   *
.*                             Keyin the Batch number                         *
.******************************************************************************
TBTCH000  MOVE      SP70,FILBTCTO
          KEYIN     *P1:10,*EL,"Batch No To  : ",*V2LON,FILBTCTO
.
          PACK      FILBTCTO,FILBTCTO,SP70
          MATCH     SP70,FILBTCTO
          GOTO      TBTCH200 IF EQUAL
.
          MOVE      FILBTCTO,HEADING2
          DISPLAY   *P29:10,*EL,*V2LON,FILBTCTO;
          MOVE      ZERO,EXIT
          GOTO      TBTCH999
.
TBTCH200  MOVE      "All",HEADING2
          DISPLAY   *P29:10,*EL,*V2LON,"All";
          MOVE      ZERO,EXIT
          GOTO      TBTCH999
.
TBTCH999  RETURN
.
.******************************************************************************
.*                                   FCLMN000             Called by: ML0000   *
.*                             Keyin the Claim number                         *
.******************************************************************************
FCLMN000  MOVE      SP70,FILCLMFR
          KEYIN     *P1:9,*EL,"Claim No From: ",*JR,*V2LON,FILCLMFR
.
          PACK      FILCLMFR,FILCLMFR,SP70
          MATCH     SP70,FILCLMFR
          GOTO      FCLMN200 IF EQUAL
.
          MOVE      FILCLMFR,HEADING3
          DISPLAY   *P29:9,*EL,*V2LON,FILCLMFR;
          MOVE      ZERO,EXIT
          GOTO      FCLMN999
.
FCLMN200  MOVE      "All",HEADING3
          DISPLAY   *P29:9,*EL,*V2LON,"All";
          MOVE      ZERO,EXIT
          GOTO      FCLMN999
.
FCLMN999  RETURN
+
.******************************************************************************
.*                                   TCLMN000             Called by: ML0000   *
.*                             Keyin the Claim number                         *
.******************************************************************************
TCLMN000  MOVE      SP70,FILCLMTO
          KEYIN     *P1:10,*EL,"Claim No To  : ",*JR,*V2LON,FILCLMTO
.
          PACK      FILCLMTO,FILCLMTO,SP70
          MATCH     SP70,FILCLMTO
          GOTO      TCLMN200 IF EQUAL
.
          MOVE      FILCLMTO,HEADING4
          DISPLAY   *P29:10,*EL,*V2LON,FILCLMTO;
          MOVE      ZERO,EXIT
          GOTO      TCLMN999
.
TCLMN200  MOVE      "All",HEADING4
          DISPLAY   *P29:10,*EL,*V2LON,"All";
          MOVE      ZERO,EXIT
          GOTO      TCLMN999
.
TCLMN999  RETURN
.
.******************************************************************************
.*                                   FCLN0000             Called by: ML0000   *
.*                             Keyin The Clinic Code From                     *
.******************************************************************************
FCLN0000  KEYIN     *P1:11,*EL,"Clinic Id From : ",*V2LON,CLINFROM
.
          PACK      CLINFROM,CLINFROM,SP70
          MATCH     SP70,CLINFROM
          GOTO      FCLN6000 IF EQUAL
.
          PACK      KEY20,IDDD,CLINFROM,Z70
          CALL      RDSCLIA1
FCLN1000  CALL      RDPCLIA1
          BRANCH    OVRCD,FCLN9500
.
          MATCH     OCASITE,IDDD         * Must be in the Same Site
          GOTO      FCLN9500 IF NOT EQUAL
.
          MATCH     OCACLIN,CLINFROM     * Must be in the Same clinic
          GOTO      FCLN9500 IF NOT EQUAL
.
          MATCH     "1",OTCLIACT
          GOTO      FCLN1000 IF EQUAL
.
          MATCH     SP70,OCADESC            * Read HCP if no doctor
          GOTO      FCLN5000 IF NOT EQUAL
.
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
          ELSE
            MOVE      "Invalid clinic HCP Code",OCADESC
          ENDIF
.
FCLN5000  MOVE      OCADESC,HEADING5
          DISPLAY   *P25:11,*EL,*V2LON,OCADESC;
          MOVE      ZERO,EXIT
          GOTO      FCLN9999
.
FCLN6000  MOVE      "Start",HEADING5
          DISPLAY   *P25:11,*EL,*V2LON,"Start";
          MOVE      ZERO,EXIT
          GOTO      FCLN9999
.
FCLN9500  DISPLAY   *P1:24,*EL,*B,"Invalid Clinic id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
FCLN9999  RETURN
+
.******************************************************************************
.*                                   TCLN0000             Called by: ML0000   *
.*                             Keyin The Clinic Code To                       *
.******************************************************************************
TCLN0000  KEYIN     *P1:12,*EL,"Clinic Id To   : ",*V2LON,CLINICTO
.
          PACK      CLINICTO,CLINICTO,SP70
          MATCH     SP70,CLINICTO
          GOTO      TCLN6000 IF EQUAL
.
          PACK      KEY20,IDDD,CLINICTO,Z70
          CALL      RDSCLIA1
TCLN1000  CALL      RDPCLIA1
          BRANCH    OVRCD,TCLN9500
.
          MATCH     OCASITE,IDDD         * Must be in the Same Site
          GOTO      TCLN9500 IF NOT EQUAL
.
          MATCH     OCACLIN,CLINICTO     * Must be in the Same clinic
          GOTO      TCLN9500 IF NOT EQUAL
.
          MATCH     "1",OTCLIACT
          GOTO      TCLN1000 IF EQUAL
.
          MATCH     SP70,OCADESC            * Read HCP if no doctor
          GOTO      TCLN5000 IF NOT EQUAL
.
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
          ELSE
            MOVE      "Invalid clinic HCP Code",OCADESC
          ENDIF
.
TCLN5000  MOVE      OCADESC,HEADING6
          DISPLAY   *P25:12,*EL,*V2LON,OCADESC;
          MOVE      ZERO,EXIT
          GOTO      TCLN9999
.
TCLN6000  MOVE      "Finish",HEADING6
          DISPLAY   *P25:12,*EL,*V2LON,"Finish";
          MOVE      ZERO,EXIT
          GOTO      TCLN9999
.
TCLN9500  DISPLAY   *P1:24,*EL,*B,"Invalid Clinic id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
TCLN9999  RETURN
.
+
.******************************************************************************
.*                                   PRCF0000             Called by: ML0000   *
.*                             Keyin The medical practice from                *
.******************************************************************************
PRCF0000  KEYIN     *P1:13,*EL,"Medical Practice : ",*V2LON,PRACTIFR
.
          PACK      PRACTIFR,PRACTIFR,SP70
          MATCH     SP70,PRACTIFR
          GOTO      PRCF6000 IF EQUAL
.
          PACK      KEY6,PRACTIFR,SP70
          CALL      RDPRPR1
          BRANCH    OVRCD,PRCF9500
.
PRCF5000  MOVE      PRPRDESC,HEADING7
          DISPLAY   *P27:13,*EL,*V2LON,PRPRDESC;
          MOVE      ZERO,EXIT
          GOTO      PRCF9999
.
PRCF6000  MOVE      "Start",HEADING7
          DISPLAY   *P27:13,*EL,*V2LON,"Start";
          MOVE      ZERO,EXIT
          GOTO      PRCF9999
.
PRCF9500  DISPLAY   *P1:24,*EL,*B,"Invalid Medical Practice. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
PRCF9999  RETURN
+
.******************************************************************************
.*                                   PRCT0000             Called by: ML0000   *
.*                             Keyin The medical practice from                *
.******************************************************************************
PRCT0000  KEYIN     *P1:14,*EL,"Medical Practice : ",*V2LON,PRACTITO
.
          PACK      PRACTITO,PRACTITO,SP70
          MATCH     SP70,PRACTITO
          GOTO      PRCT6000 IF EQUAL
.
          PACK      KEY6,PRACTITO,SP70
          CALL      RDPRPR1
          BRANCH    OVRCD,PRCT9500
.
PRCT5000  MOVE      PRPRDESC,HEADING8
          DISPLAY   *P27:14,*EL,*V2LON,PRPRDESC;
          MOVE      ZERO,EXIT
          GOTO      PRCT9999
.
PRCT6000  MOVE      "Finish",HEADING8
          DISPLAY   *P27:14,*EL,*V2LON,"Finish";
          MOVE      ZERO,EXIT
          GOTO      PRCT9999
.
PRCT9500  DISPLAY   *P1:24,*EL,*B,"Invalid Medical Practice. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
PRCT9999  RETURN
.
+
.******************************************************************************
.*                                   KPRV0000             Called by: ML0000   *
.*                             Keyin the provider number                      *
.******************************************************************************
KPRV0000  KEYIN     *P1:16,*EL,"Provider Number : ",*V2LON,FILTPROV
.
          PACK      FILTPROV,FILTPROV,SP70
          MATCH     SP70,FILTPROV
          GOTO      KPRV6000 IF EQUAL
.
KPRV5000  MOVE      FILTPROV,HEADING0
          DISPLAY   *P29:16,*EL,*V2LON,FILTPROV;
          MOVE      ZERO,EXIT
          GOTO      KPRV9999
.
KPRV6000  MOVE      "All",HEADING0
          DISPLAY   *P29:16,*EL,*V2LON,"All";
          MOVE      ZERO,EXIT
          GOTO      KPRV9999
.
KPRV9999  RETURN
+
.******************************************************************************
.*                                   WEBU0000             Called by: ML0000   *
.*                             Keyin The web user id                          *
.******************************************************************************
WEBU0000  MOVE      SP70,WEBUSRID
          KEYIN     *P1:17,*EL,"User Id : ",*V2LON,WEBUSRID
.
          PACK      WEBUSRID,WEBUSRID,SP70
.
          PACK      KEY10,WEBUSRID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,WEBU9500
.
          MOVE      ZERO,EXIT
          GOTO      WEBU9999
.
WEBU9500  DISPLAY   *P1:24,*EL,*B,"Invalid User Id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
WEBU9999  RETURN
+
.****************************************************************************
.*        IO ROUTINES FOR TEMPORARY FILE                                    *
.****************************************************************************
RATEMP1   RESET     KEY13
          MOVE      ZERO,OVRCD
          READ      HICTEMP1,KEY13;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     HICTEMP1,KEY13;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPBTCH:
                                   TEMPPMCI,TEMPPYEE,TEMPCLMN,TEMPBDAT
          RETURN
.
RSTEMP2   MOVE      SP70,KEY32
          READ      HICTEMP2,KEY32;;
          RETURN
.
RKTEMP2   MOVE      ZERO,OVRCD
          READKS    HICTEMP2;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPBTCH:
                             TEMPPMCI,TEMPPYEE,TEMPCLMN,TEMPBDAT
          GOTO      OVERCOND IF OVER
          RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
.
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       HICBTCIO/INC
          INC       HICCLMIO/INC
          INC       HICCITIO/INC
          INC       HICEMSIO/INC
          INC       PMSHCPIO/INC
          INC       PRIPRAIO/INC
          INC       PRIDOCIO/INC
          INC       OUTSITIO/INC
          INC       OUTCLIIO/INC
          INC       PATCODIO/INC
          INC       WEBSECIO/INC
          INC       PRIALSIO/INC
          INC       PRISECIO/INC
          INC       PATMA1IO/INC
          INC       VISCMTIO/INC

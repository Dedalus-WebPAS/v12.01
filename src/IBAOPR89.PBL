.******************************************************************************
.* System   : Operating Room System                                           *
.* Program  : IBAOPR89                                                        *
.* Desc     : Expensive Item File Master List                                 *
.******************************************************************************
.* Date     :  13/12/89                                                       *
.* Author   :  K.Peak                                                         *
.*                                                                            *
.* Modifications :                                                            *
.*        V10.01.01 17/12/2010  Mike Laming   CAR 233046                      *
.*                  Mods to Misc.Charges PATMCHFD - Key change, logic changes *
.*        V10.00.01 30/04/2010  J.Tan        CAR 220887                       *
.*                  Mods checking for Active/Inactive of Misc.charge          *
.******************************************************************************
.*        V9.03.01  12/09/2003 Lina Vo                      CAR 404975        *
.*                  Changed index and read of patmchgf.                       *
.******************************************************************************
.*          :  02/04/90  Bill Dew                                             *
.*             PRNT routine is stuffed.  Endless loop.                        *
.*             This program will work after I change it..                     *
.*             15/10/90 Peter Eddey (whirlpool)                               *
.*             changed the branch on "ok to continue " routine.               *
.*             Updated the ? routine to current standards & fixed             *
.*             the bugs. Centered the report layout.                          *
.*             23/10/90 Glen Hobby                                            *
.*                      Added print of miscellaneous charge code              *
.*             V5.01.02 16/01/92 Graeme Williams                              *
.*                      Use PTCNDCLM for the default claim code               *
.*        V5.01.03  12/06/92  Graeme Williams               SRF 114865        *
.*                  Changes for 9 character misc items                        *
.******************************************************************************
.
.$$$$$
.    Operating Room System Expensive Item File Master List (IBAOPR89 )
.    -----------------------------------------------------------------
.
.    - Initialisation
.
.    - Display Header
.            - Open Files
.               OPREXPA1
.               PATCODE1
.               CONTROLF
.
.    - Clear Input Variables
.
.    - Display Screen Format
.
.    - Input Range 
.      If "?" entered, call EXPSCOD routine
.
.    - Read through appropriate file
.
.    - Print Report
.
.    - END
.$$$$$
.
          INC       STD001FD
          INC       OPREXPFD/INC           * Expensive Item File
          INC       OPRCONFD/INC           * Operating Room Control file
          INC       PATCODFD/INC           * patient codes file
          INC       PATMCHFD/INC           * Miscellaneous charges file
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
CODE      DIM       5
CURRDATE  DIM       8
.
DIM15     DIM       15
.
ECODE     DIM       6
ECDESC    DIM       30
.
FINISH    INIT      "FINISH                        "
.
ITEMCODE  DIM       6
.
KEY21A    DIM       21
KEY26A    DIM       26
.
PAGENO    FORM      3
PTCNDCLM  DIM       3
.
START     INIT      "START                         "
STARTFIN  FORM      1
SCODE     DIM       6
SCDESC    DIM       30
.
UVERT1    FORM      2
.
VAR       FORM      2
.
PRGID     INIT      "IBAOPR89"
PRGNAM    INIT      "Expensive Item Master Listing"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
.
ML0000    CALL      INIT0000               * initialization and open files
.
ML0100    CALL      CLR0000                * clear input variables
.
          CALL      OPTN0000               * select option         
          BRANCH    EXIT,ML9999
.
ML0200    CALL      RNGE0000               * select range
          CALL      CONT0000               * ok to continue
          BRANCH    EXIT,ML1000,ML0200,ML0100  
.
ML1000    CALL      CODE0000               * sort data by code sequence
          CALL      END0000                * end of report
          GOTO      ML0100
.
ML9999    CHAIN     PGM                    * chain back to program
+
.**********************************************************************
.*                             INIT0000                               *
.*                      Initialization  Routine                       *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"oprexpaf";
          OPEN      OPREXPA1,"oprexpaf"
.
          DISPLAY   *P64:24,"oprexpaf";
          OPEN      OPREXPA2,"oprexpaf"
.
          DISPLAY   *P64:24,"patmchgf";
          OPEN      PATMCHG1,"patmchgf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
          READ      CONTROLF,FORTY;*113,OPCNRUSI
          READ      CONTROLF,FORTY2;*43,OPCNXCHG
          MOVE      ONE,CNEWDS
.
          MATCH     SP3,PTCNDCLM
          GOTO      INIT9999 IF NOT EQUAL
.
          MOVE      "0  ",PTCNDCLM
.
INIT9999  RETURN
+
.**********************************************************************
.*                             CLR0000                                *
.*                 Routine to Clear Local Variables                   *
.**********************************************************************
.
CLR0000
.
          MOVE      ZERO,PAGENO
.
. ----- Clear the Expensive Item File Variables -----
.
          MOVE      SP6,OPEXCODE
          PACK      OPEXDESC,SP20,SP20
          MOVE      SP3,OPEXRUS
.
CLR9999   RETURN
+
.**********************************************************************
.*                             OPTN0000                               *
.*                          Select Option                             *
.**********************************************************************
.
OPTN0000  MOVE      FALSE,EXIT
.
          DISPLAY   *P1:4,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P2:5,*V2LON,ONE,*HOFF,". Item Sequence"
.
OPTN1000  DISPLAY   *P1:7,*EL,"Option : "
          KEYIN     *P10:7,*V2LON,OPTION
.
          BRANCH    OPTION,OPTN9999
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      TRUE,EXIT
OPTN9999  RETURN
+
.**********************************************************************
.*                             RNGE0000                               *
.*                          Select Range                              *
.**********************************************************************
.
RNGE0000  MOVE      SP6,SCODE
          MOVE      SP6,ECODE
          MOVE      SP30,SCDESC
          MOVE      SP30,ECDESC
.
.         Find Expensive Item Range
.
RNGE1000  DISPLAY   *P1:9,*EF:
                    *P1:10,"Item From : ":
                    *P1:11,"Item To   : "
.
RNGE1100  KEYIN     *P13:10,*EL,*V2LON,SCODE
.
          ENDSET    SCODE
          APPEND    SP6,SCODE
          RESET     SCODE
.
          MATCH     QUEST,SCODE
          GOTO      RNGE1200 IF NOT EQUAL
          MOVE      SP6,SCODE
          CALL      EXPSCOD
          MOVE      ZERO,STARTFIN
          CALL      EXIQ0000
          GOTO      RNGE2300
.
RNGE1200  MATCH     SP6,SCODE
          GOTO      RNGE1500 IF NOT EQUAL
          MOVE      START,SCDESC
          DISPLAY   *P13:10,*EL,*V2LON,START
          GOTO      RNGE2300
.
RNGE1500  MOVE      SP20,SCDESC
          MOVE      SCODE,KEY6
          CALL      RDOPEXP1
          BRANCH    OVRCD,RNGE2000
          MOVE      OPEXDESC,SCDESC
.
RNGE1700  DISPLAY   *P24:10,SCDESC
          GOTO      RNGE2300
.
RNGE2000  DISPLAY   *P1:24,*EL,"Code Does Not Exist - Hit <",*V2LON,"ENTER":
                    *HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL
          GOTO      RNGE0000
.
RNGE2300  KEYIN     *P13:11,*EL,*V2LON,ECODE
.
          ENDSET    ECODE
          APPEND    SP6,ECODE
          RESET     ECODE
.
          MATCH     QUEST,ECODE
          GOTO      RNGE2400 IF NOT EQUAL
          MOVE      SP6,ECODE
          CALL      EXPSCOD
          MOVE      ONE,STARTFIN
          CALL      EXIQ0000
          GOTO      RNGE9999
.
RNGE2400  MATCH     SP6,ECODE
          GOTO      RNGE2500 IF NOT EQUAL
          MOVE      FINISH,ECDESC
          MOVE      "ZZZZZZ",ECODE     * set end code with highest ascii value
          DISPLAY   *P13:11,*EL,*V2LON,FINISH
          GOTO      RNGE2700
.
RNGE2500  MOVE      SP20,ECDESC
          MOVE      ECODE,KEY6
          CALL      RDOPEXP1
          BRANCH    OVRCD,RNGE3000
          MOVE      OPEXDESC,ECDESC
          DISPLAY   *P24:11,ECDESC
.
RNGE2700 
.
          MATCH     SCODE,ECODE
          GOTO      RNGE3500 IF LESS
          GOTO      RNGE9999
.
RNGE3000  DISPLAY   *P1:24,*EL,"Code Does Not Exist - Hit <",*V2LON,"ENTER":
                    *HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL
          GOTO      RNGE2300
.
RNGE3500  DISPLAY   *P1:24,*EL,"To Code musr be >= to From Code - Hit <":
                    *V2LON,"ENTER",*HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL
          GOTO      RNGE0000
.
RNGE9999  RETURN
+
.*********************************************************************
.*                            EXIQ0000                               *
.*              handles a question mark keyin                        *
.*********************************************************************
.
EXIQ0000
          MOVE      SP6,ITEMCODE
.
          DISPLAY   *P1:24,*EL,"Item code : "
          KEYIN     *P13:24,*DV,UNDLN6:
                    *P13:24,*V2LON,ITEMCODE:
                    *P13:24,*DV,ITEMCODE
.
          RESET     ITEMCODE
          GOTO      EXIQ2000 IF EOS
.
          PACK      ITEMCODE,ITEMCODE,SP6
          MATCH     SP6,ITEMCODE
          GOTO      EXIQ3000 IF NOT EQUAL
EXIQ2000
          BRANCH    STARTFIN,EXIQ2200
          MOVE      START,SCDESC                * begin at start of nurse codes
          MOVE      SP6,SCODE
          GOTO      EXIQ6000
EXIQ2200
          MOVE      FINISH,ECDESC 
          MOVE      "zzzzzz",ECODE 
          GOTO      EXIQ6000
.
EXIQ3000  
          BRANCH    STARTFIN,EXIQ4000
          MOVE      SP20,SCDESC
          GOTO      EXIQ5000
EXIQ4000
          MOVE      SP20,ECDESC
EXIQ5000
          MOVE      ITEMCODE,KEY6
          CALL      RDOPEXP1
          BRANCH    OVRCD,EXIQ7000
          BRANCH    STARTFIN,EXIQ5500
          MOVE      ITEMCODE,SCODE
          MOVE      OPEXDESC,SCDESC
          CALL      RDSP0000
          GOTO      EXIQ9999 
EXIQ5500
          MOVE      ITEMCODE,ECODE
          MATCH     SCODE,ECODE
          GOTO      EXIQ8000 IF LESS
.
          MOVE      OPEXDESC,ECDESC
          MOVE      ITEMCODE,ECODE
          CALL      RDSP0000
          GOTO      EXIQ9999 
.
EXIQ6000  
          CALL      RDSP0000
          GOTO      EXIQ9999
.
EXIQ7000  
          BEEP
          DISPLAY   *P1:24,*EL,"Code Does Not Exist. ";
          CALL      HITENTER
          GOTO      EXIQ0000
.
EXIQ8000  
          BEEP
          DISPLAY   *P1:24,*EL,"To code must be >= from code. ";
          CALL      HITENTER
          GOTO      EXIQ0000
.
EXIQ9999  RETURN
.**********************************************************************
.*                             CONT0000                               *
.*                          Ok to Continue                            *
.**********************************************************************
.
CONT0000  MOVE      FALSE,EXIT
.
          CALL      CONTQST
          MOVE      ANS,FORM1
          BRANCH    FORM1,CONT9999,CONT9000,CONT9500
.
CONT9000  MOVE      TWO,EXIT
          GOTO      CONT9999
.
CONT9500  MOVE      THREE,EXIT
CONT9999  RETURN
+
.**********************************************************************
.*                             CODE0000                               *
.*                     Sort Data by Code Sequence                     *
.**********************************************************************
.
CODE0000
          DISPLAY   *P1:24,*EL,*P20:24,"Item Code : "
          MOVE      "60",CLNO
.
          PACK      KEY6,SCODE
.
          MATCH     SP6,KEY6
          GOTO      CODE0555 IF EQUAL
.
          CALL      RDOPEXP1           * do a read with full key 
          BRANCH    OVRCD,CODE9999
.
          GOTO      CODE2222
.
CODE0555  CALL      RSOPEXP1
.
CODE1000  CALL      RKOPEXP1
          BRANCH    OVRCD,CODE9999
CODE2222
          MATCH     OPEXCODE,ECODE
          GOTO      CODE9999 IF LESS
CODE3000
          DISPLAY   *P32:24,*V2LON,OPEXCODE
.
          CALL      PRNT0000
.
          GOTO      CODE1000
.
CODE9999  RETURN
.**************************************************************************
.*                         PRNT0000                                       *
.*                        Print Data                                      *
.**************************************************************************
.
.         To make things easier, we'll split this print routine into 4 parts.
.         These are the four posible combinations of the two system paramters.
.
PRNT0000  COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          BRANCH    OPCNRUSI,PRNT0100
.
          BRANCH    OPCNXCHG,PRNT0300
.
.         OPCNRUSI = 0. OPCNXCHG = 0.
.
          PRINT     *18,"| ",OPEXCODE,*27,"| ",OPEXDESC,*60,"|"
          GOTO      PRNT3000
.
PRNT0100  BRANCH    OPCNXCHG,PRNT0200.
.
.         OPCNRUSI = 1. OPCNXCHG = 0.
.
          PRINT     *18,"| ",OPEXCODE,*27,"| ",OPEXDESC,*60,"|":
                    *62,OPEXRUS,*66,"|"
          GOTO      PRNT3000
.
.         OPCNRUSI = 1. OPCNXCHG = 1.
.
PRNT0200  PRINT     *18,"| ",OPEXCODE,*27,"| ",OPEXDESC,*60,"|":
                    *62,OPEXRUS,*66,"|",*68,OPEXMCHG,*82,"| ";
.
.         Get the description
.
          MOVE      PTCNDCLM,MCLMCOD
          MOVE      OPEXMCHG,MCHARGE
.
          PACK      KEY29,MCLMCOD,SP6,SP3,OPEXMCHG,CURRDATE,SP10
          CALL      PATMCHRD                * read Misc.Charges file 
          COMPARE   ZERO,EXIT
          GOTO      PRNT0270 IF EQUAL
.
PRNT0260  MOVE      SP30,MDESC
.
PRNT0270  PRINT     MDESC,*110,"|"
          GOTO      PRNT3000
.
.         OPCNRUSI = 0. OPCNXCHG = 1.
.
PRNT0300  PRINT     *18,"| ",OPEXCODE,*27,"| ",OPEXDESC:
                    *60,"|",*68,OPEXMCHG,*82,"| ";
.
.         Get the description
.
          MOVE      PTCNDCLM,MCLMCOD
          MOVE      OPEXMCHG,MCHARGE
.
          PACK      KEY29,MCLMCOD,SP6,SP3,OPEXMCHG,CURRDATE,SP10
          CALL      PATMCHRD                * read Misc.Charges file
          COMPARE   ZERO,EXIT
          GOTO      PRNT0350 IF EQUAL
.
PRNT0320  MOVE      SP30,MDESC
.
PRNT0350  PRINT     MDESC,*110,"|"
          GOTO      PRNT3000
.
PRNT3000  ADD       ONE,CLNO
.
PRNT9999  RETURN
+
.**************************************************************************
.*                         END0000                                        *
.*                      End of Report                                     *
.**************************************************************************
.
END0000   COMPARE   ZERO,PAGENO
          GOTO      END1000 IF NOT EQUAL
.
.         No Data Found
.
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** No Record Selected **"
.
          CALL      HEAD0000
.
          PRINT     *N,*N,"** END OF REPORT **"
          GOTO      END9999
.
END1000   CALL      PGES0000
          PRINT     *N,*N,"** END OF REPORT **"
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** Report Generation Completed **"
.
END9999   DISPLAY   *P1:14,*EF
          RETURN
+
.**************************************************************************
.*                         HEAD0000                                       *
.*                     Print Heading                                      *
.**************************************************************************
HEAD0000  COMPARE   ZERO,PAGENO
          CALL      PGE0000 IF NOT EQUAL
          CLOCK     TIME,CTIMEIS
          ADD       ONE,PAGENO
.
          CALL      HEAD132
.
          MOVE      ECODE,KEY6
          MATCH     "ZZZZZZ",ECODE
          GOTO      HEAD0004 IF NOT EQUAL
          MOVE      SP6,KEY6
HEAD0004
          PRINT     *40,"Item From : ",SCODE,SP3,SCDESC,*N:
                    *40,"Item To   : ",KEY6,SP3,ECDESC,*N
.
          CALL      PGE0000
.
.         Check if the RUS system parameter is set.
.
          COMPARE   ZERO,OPCNRUSI
          GOTO      HEAD1000 IF EQUAL
.
          PRINT     *18,"| Item",*27,"|",*60,"|":
                    *62,"RUS",*66,"|";
.
          COMPARE   ZERO,OPCNXCHG
          GOTO      HEAD0100 IF EQUAL
.
          PRINT     *68,"Miscellaneous | Miscellaneous             |";
.
HEAD0100  PRINT     *N,*18,"| Code",*27,"| Description",*60,"|":
                    *62,"Ind",*66,"|";
          GOTO      HEAD2000
.
HEAD1000  PRINT     *18,"| Item",*27,"|",*60,"|";
.
          COMPARE   ZERO,OPCNXCHG
          GOTO      HEAD1100 IF EQUAL
.
          PRINT     *68,"Miscellaneous | Miscellaneous             |";
.
HEAD1100  PRINT     *N,*18,"| Code",*27,"| Description",*60,"|";
.
HEAD2000  COMPARE   ZERO,OPCNXCHG
          GOTO      HEAD3000 IF EQUAL
.
          PRINT     *68,"Charge Code   | Charge Code Description   |";
. 
HEAD3000  CALL      PGE0000
          MOVE      TEN1,CLNO
.
HEAD9999  RETURN
+
.**********************************************************************
.*                             PGE0000                                *
.*                     Print Page Separator                           *
.**********************************************************************
.
PGE0000   COMPARE   ZERO,OPCNXCHG
          GOTO      PGE1000 IF EQUAL
.
          PRINT     *N,*18,"*---------------------------------------":
                    "----------------------------------------------------*"
          GOTO      PGE2000
.
PGE1000   COMPARE   ZERO,OPCNRUSI
          GOTO      PGE1500 IF EQUAL
.
          PRINT     *N,*18,"*-----------------------------------------------*"
          GOTO      PGE2000
.
PGE1500   PRINT     *N,*18,"*-----------------------------------------*"
.
PGE2000   ADD       ONE,CLNO
.
PGE9999   RETURN
.
.**********************************************************************
.*                             PGES0000                               *
.*                     Print Page Separator - routine 2               *
.**********************************************************************
.
PGES0000  COMPARE   ZERO,OPCNXCHG
          GOTO      PGES1000 IF EQUAL
.
          PRINT     *18,"*---------------------------------------":
                    "----------------------------------------------------*"
          GOTO      PGES2000
.
PGES1000  COMPARE   ZERO,OPCNRUSI
          GOTO      PGES1500 IF EQUAL
.
          PRINT     *18,"*-----------------------------------------------*"
          GOTO      PGES2000
.
PGES1500  PRINT     *18,"*-----------------------------------------*"
.
PGES2000  ADD       ONE,CLNO
.
PGES9999  RETURN
+
.**********************************************************************
.*                             RDSP0000                               *
.*                        Redisplay Screen                            *
.**********************************************************************
.
RDSP0000  DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P2:5,*V2LON,ONE,*HOFF,". Item Sequence":
                    *P1:7,*EL,"Option : ",*P10:7,*V2LON,OPTION,*HOFF
          MATCH     SP6,SCODE
          GOTO      RDSP2000 IF EQUAL
          DISPLAY   *P1:10,"Item From : ":
                    *P1:11,"Item To   : ":
                    *P13:10,*EL,*V2LON,SCODE,*HOFF,*P24:10,SCDESC
          GOTO      RDSP4000
RDSP2000
          DISPLAY   *P1:10,"Item From : ",*V2LON,START,*HOFF:
                    *P1:11,"Item To   : "
RDSP4000
          MATCH     "zzzzzz",ECODE
          GOTO      RDSP5000 IF EQUAL
          DISPLAY   *P13:11,*EL,*V2LON,ECODE,*HOFF,*P24:11,ECDESC
          GOTO      RDSP9999
RDSP5000
          COMPARE   ZERO,STARTFIN
          GOTO      RDSP9999 IF EQUAL
.
          DISPLAY   *P13:11,*EL,*V2LON,FINISH
.
RDSP9999  RETURN
.
.********************************************************************** 
.
.     EXPSCOD : Display Expensive Item File Entries
.
.     This include displays entries in the Expensive Item file .
.
.     Modifications: 
.
.         Display Heading, and then loop over entries
.
EXPSCOD   DISPLAY   *P1:4,*EF,*P30:4,*V2LON,*ULON,"Expensive Item File Enteries"
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
          PACK      KEY6,SP6
          CALL      RSOPEXP1
EXPSCOD1  CALL      RKOPEXP1
          BRANCH    OVRCD OF EXPSCOD5
.
          DISPLAY   *PCCOL:CVERT,*V2LON,OPEXCODE,SP2,*HOFF,OPEXDESC
          ADD       ONE,CVERT
          COMPARE   TWENTY4,CVERT
          GOTO      EXPSCOD1 IF LESS
.
          MOVE      FIVE,CVERT
          COMPARE   ONE,CCOL
          GOTO      EXPSCOD2 IF NOT EQUAL
          MOVE      FORTY2,CCOL   
          GOTO      EXPSCOD1
.
EXPSCOD2 
          BRANCH    CNEWDS,EXPSCOD3
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSE,ANS
          GOTO      EXPSCOD6 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      EXPSCOD IF EQUAL
          GOTO      EXPSCOD4
.
EXPSCOD3  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSC,ANS
          GOTO      EXPSCOD6 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      EXPSCOD IF EQUAL
.
.         Clear the screen
.
EXPSCOD4  MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
          GOTO      EXPSCOD1
.
.      End of codes
.
EXPSCOD5  BRANCH    CNEWDS,EXPSCOD6
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
.
EXPSCOD6  MOVE      ZERO,OVRCD
          RETURN
.********************************************************************** 
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
          INC       STD001IO
          INC       PATCODKY                * patient codes file routine
          INC       PATMCHRD                * Miscellaneous Charges read routine
          INC       PATCODIO/INC            * patient i/o for codes
          INC       PATMCHIO/INC            * miscellaneous charges file
          INC       OPREXPIO/INC            * expensive item i/o
.

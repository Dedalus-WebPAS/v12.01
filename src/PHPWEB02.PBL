.-------------------------------------------------------------------------------
. Program       : PHPWEB02 
.
. Function      : CISAM Version of PHP Calls
.
. Modifications  :
.-------------------------------------------------------------------------------
.         V12.00.01  28/05/2025  Bella Turco    TSK 0955096
.                    Alphanumeric changes                    
.         V11.03.01  03/03/2023  Peter Vela     TSK 0909393
.                    Changed KEY for oprsesaf oprdetaf reads
.-------------------------------------------------------------------------------
.         V11.00.01  09/12/2020  Thanh T        TSK 0872840
.                    Added PMSCCDIO as PMSPX2TM changes
.-------------------------------------------------------------------------------
.         V10.13.01  05/12/2018  Don Nguyen      TSK 0838558
.                    Moved temp file creation (CRHTMC00) to comments processing
.                    section rather than on server startup.
.                    Removed PORT from temp filename (TEMPNAME).
.-------------------------------------------------------------------------------
.         V10.11.01  28/09/2017  Thanh T.         TSK 0821710
.                    Removed PATCOMFD since it is not used
.-------------------------------------------------------------------------------
.         V10.08.01  23/08/2016  Peter McMullen  0823848
.                    Add lower case login search to XMLRED00
.-------------------------------------------------------------------------------
.         V10.07.01  05/11/2015  Steve Armstrong  CAR 303363
.                    Recompiled for changes to RESHEAFD
.-------------------------------------------------------------------------------
.         V10.05.01  06/11/2014  Don Nguyen      CAR 304059
.                    Recompiled for changes to PMSDSDFD
.-------------------------------------------------------------------------------
.         V10.04.01  20/02/2014 Steve Armstrong  CAR 290918
.                    Recompiled for changes to PMSDSDFD
.-------------------------------------------------------------------------------
.         V10.03.08  B.G.Ackland    CAR 288225
.                    Align Case List with PHP version
.         V10.03.07  B.G.Ackland    CAR 288225
.                    Fix Ward/Bed Condition Lookup for 10.03 Changes
.         V10.03.06  Saroeun Soeur  CAR 272404
.                    EXPL0000 - fixed expected patient list
.         V10.03.05  B.G.Ackland    CAR 269027 
.                    Theatre Case List by Associated Theatre Doctor CASL0000
.-------------------------------------------------------------------------------
.         V10.01.04  B.G.Ackland    CAR 264486 
.                    Patient List by Associated Theatre Doctor THEADM00
.         V10.01.03  Saroeun Soeur  CAR 267808
.                    fixed CASL0000 rows
.         V10.01.02  Saroeun Soeur  CAR 265085
.                    fixed CASL0000 - missing URNO field for addrow
.         V10.01.01  Saroeun Soeur  CAR 265085
.                    fixed CASL0000 
.         V10.01.00  B.G. Ackland   CAR 265085
.                    Program created.
.-------------------------------------------------------------------------------
          INC       IBAWEBDF    
.
          INC       WEBDATDF
          INC       PMSWORFD/INC
          INC       PATLOCFD/INC
          INC       PATDO1FD/INC
          INC       PATONLFD/INC
          INC       PATNOBFD/INC
          INC       PATMI1FD/INC
          INC       PATWR1FD/INC
          INC       PATMA1FD/INC
          INC       PATPR1FD/INC
          INC       PATGSRFD/INC 
          INC       BOKRC1FD/INC 
          INC       BOKRX1FD/INC 
          INC       OPRCONFD/INC 
          INC       OPRDEAFD/INC 
          INC       OPRSESFD/INC 
          INC       OPRCLIFD/INC 
          INC       OPRTSMFD/INC 
.
          INC       PMSCCDFD/INC 
          INC       PATVISFD/INC 
          INC       OBSPCOFD/INC 
          INC       OBSPCTFD/INC       * Correspondence Storage Location Table
          INC       OBSMDTFD/INC 
          INC       OBSMTXFD/INC 
          INC       EMRVCDFD/INC 
          INC       VISMDTFD/INC 
          INC       VISMTXFD/INC 
.
          INC       OBSCNAFD/INC
          INC       OBSCNBFD/INC
          INC       OBSCNCFD/INC
          INC       OBSTYPFD/INC
          INC       PATHSPFD/INC
          INC       MLTSECFD/INC
.
          INC       PATCONFD/INC
          INC       PATDGWFD/INC
          INC       PATFN1FD/INC
          INC       PATIN1FD/INC
          INC       PATITMFD/INC
          INC       PMSDSDFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPTD/INC
          INC       PMSHCGTD/INC
          INC       PMSPX2FD/INC
          INC       PMSPX2TD/INC
          INC       PATRE1FD/INC
          INC       PATCALAG/INC
          INC       PATDHEAD/INC
          INC       PMSVX1FD/INC
.
          INC       MRTMASFD/INC
          INC       OUTSITFD/INC
          INC       RESCLNFD/INC
          INC       RESLURFD/INC
          INC       RESLNKFD/INC
          INC       RESHEAFD/INC
          INC       PATALRFD/INC
          INC       HL7CODFD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
.
          INC       TFILEVAR/INC
.
. Variable Declarations  
.
SHOWFLAG  FORM      1
ALERTDES  DIM       20
ALLVISIT  FORM      1
DESTINAT  DIM       50
RESLNK01  DIM       8   * Result Date
RESLNK02  DIM       5   * Time
RESLNK03  DIM       10  * DSS
RESLNK04  DIM       2   * Status
RESLNK05  DIM       1   * Normal
RESLNK06  DIM       25  * Provider
RESLNK07  DIM       20  * Reference
.
RESULTKY  DIM       17
REFERLKY  DIM       8
RESULTYR  DIM       4
YEAROPEN  DIM       4
UNIQUEID  FORM      4
TABCFLAG  FORM      1
AT        INIT      "@"
PREHEA    INIT      "reha"
PREHEB    INIT      "rehb"
PREHEC    INIT      "rehc"
PREHED    INIT      "rehd"
PREDEA    INIT      "reda"
PREDEB    INIT      "redb"
PREDEC    INIT      "redc"
PRELEN    INIT      "reln"    * I CAR 38290
.
CMNDLINE  DIM       127
DATELUPD  DIM       11        * last update date
DTYPDESC  DIM       25
LINKTYPE  DIM       2
LINKKEYS  DIM       10
.
.
HL7TABID  DIM       4
SAVISIT   DIM       8         * Input date          
FORM8     FORM      8
CURRDATE  DIM       8         * Current date
CMDLINE   DIM       127       * Command line instruction
CORRESID  FORM      4
COPYFKEY  DIM       20        * Document Copied From Key
CORSFILE  DIM       20     
CORSP001  DIM       20        * Correspondence File Name
CORSP002  DIM       8         * Input date          
CORSP003  DIM       3         * Type of correspondance
CORSP004  DIM       8         * Input Time        
CORSP005  DIM       1         * Mark as deleted 
CORSP006  DIM       2         * Security Level  
CORSP007  DIM       50        * Comment 1
CORSP008  DIM       50        * Comment 2
CORSP009  DIM       30        * From 
CORSP010  DIM       30        * To
CORSP011  DIM       3         * User Def Code 1
CORSP012  DIM       3         * User Def Code 2 1
CORSP013  DIM       3         * User Def Y/N 1  
CORSP014  DIM       3         * User Def Y/N 1   
CORSP015  DIM       8         * User Def Date 1  
CORSP016  DIM       8         * User Def Date 2  
CORSP017  DIM       2         * Alert Category
CORSP018  DIM       3         * Alert Code
DRAFTDOC  DIM       1         * Current Draft Document   1=true
HTMLLINE  DIM       200        * HTML Page Submitted
HTMLLNK2  DIM       200
HTMLLNK3  DIM       200
HTMLLNK4  DIM       200
HTMLCNT   FORM      6         * HTML Page Submitted
TEMPNAME  DIM       8         * HTML Page Submitted
TEMPFILE  FILE      ASCII,FIXED=127
.
AUDITKEY  DIM       20        * Key to Patient Correspondence Table
AMBERSND  INIT      "\&"
DASH      INIT      "-"
DATE      DIM       11        * Correspondence Date
DETAILKY  DIM       20        * Key to Patient Correspondence Table
DIM4      DIM       4
DIM6      DIM       6
DIM8      DIM       8
DIM100    DIM       100
DOCTCODE  DIM       6
.
EMRGFLAG  DIM       1         * Flag for emergency
.
FORMACTN  DIM       3         * Form Action      
.
INPTDATE  DIM       8         * parameter used in INPDTE00 to display a date
INPTWUID  DIM       10        * parameter used in DISUSR00 to display user 
.
LINKTYP   DIM       3         * Type of Correspondance 
LNK2PRT   FORM      1
.
MBSDESC   DIM       40
CMDMOVE   INIT      "mv "
.
NEXTPAGE  FORM      1         * Nextpage parameter
.
OBPCT001  DIM       4         * Storage Location ID
OBPCT002  DIM       80        * Storage Directory
OBPCT003  DIM       80        * URL Prefix
OBPCT004  DIM       1         * Read ONly 0=NO, 1=YES
OBPCT005  DIM       8         * Date Closed
OBPCT006  DIM       1         * Set as Cuurent Storage Location 0=No, 1=Yes
OUTVAR    DIM       127
.
CATWI     INIT      "wi"
CLIDF001  DIM       5         * Data Field Number
CLIDF002  DIM       50        * Description
CLIDF003  DIM       20        * Group Description
CLIDF004  DIM       8         * Table
CLIDF005  DIM       8         * Field
.
CLICT001  DIM       5         * Download Type
CLICT002  DIM       35        * Description
CLICT003  DIM       2         * Security Level
CLICT004  DIM       5         * Copy from
CLICT005  DIM       5         * Copy to
.
CLICD001  DIM       5         * Download Type
CLICD002  DIM       5         * Data Field Number
CLICD003  DIM       3         * Download Sequence 
CLICD004  DIM       25        * Heading Name 
CLICD005  DIM       1         * Convert to Title Text
.
FAXNUMBR  DIM       20
RESLNKFL  DIM       8         * File name var for reslnk CAR 38290
RECIPNAM  DIM       20
SENDFAXB  DIM       1
TESTKEY   DIM       16        * Last Update date and time
TIME      DIM       5         * Time displayed in 00:00 format 
UPDATEKY  DIM       16        * Update key(date & time of last update) 
UPDTTYPE  DIM       1         * Update Type (A)dd,(U)pdate,(D)elete,Blank=disp
STARTKEY  DIM       10        * Starting Key
CORRTYPE  DIM       5         * Current Field Number for Next & Prev pages
CORRDESC  DIM       35        * Download Type Description 
VISTDATE  DIM       12
.
LOKKEY20  DIM       20                                               * I-90207
SAVE2PDF  FORM      1
HTML2PDF  INIT      "ConvertHTML2PDF"
SENDSUMM  INIT      "SendDischargeSummary"
PRINTEXE  INIT      "PrintDocument"
SENDTEXE  INIT      "SendDocument"
.
SENDTYPE  FORM      1         * Passed to SendDocument. 1=Fax, 2=Email, 3=Email Copy to Self, 4=Email & SMS Notification?
SENDTOPH  DIM       25        * Passed to SendDocument. Fax to Phone Number
SENDFREM  DIM       70        * Passed to SendDocument. Email From Address
SENDTOEM  DIM       70        * Passed to SendDocument. Email To Address
COPYTOCT  FORM      1         * Email Copy to Count
COPYTOEM  DIM       70[9]     * Passed to SendDocument. CC Email Addresses (Not Implemented)
HCPFROMC  DIM       10        * Send From HCP Code (Not Implemented)
HCPSENDC  DIM       10        * Send To HCP Code   (Not Implemented)
HCPCOPCT  FORM      1         * HCP Copy to Count
HCPCOPYC  DIM       10[9]     * Copy To HCP Code   (Not Implemented)
PRINTFMT  DIM       10        * Printing Format for HTML to PDF conversion
.
PHPFILEN  DIM       80        * PHP File
WARDCODE  DIM       3
OBSFLAG   FORM      1
MEDFLAG   FORM      1
DOCFLAG   FORM      1
DIAFLAG   FORM      1
BILFLAG   FORM      1
HISFLAG   FORM      1
THEFLAG   FORM      1
VISFLAG   FORM      1
NOTFLAG   FORM      1
AMTFLAG   FORM      1
DISFLAG   FORM      1
FIRSTROW  FORM      1
ROWCOUNT  FORM      3
DISPDATE  DIM       13
DOCTTYPE  DIM       3
CATCO     INIT      "CO"
CATct     INIT      "ct"
CATT      INIT      "T "
CATAC     INIT      "AC"
CATFS     INIT      "FS"
CATPV     INIT      "PV"
OADESC    DIM       25  
ACDESC    DIM       25  
CONDIND1  DIM       1   
UNITDESC  DIM       25  
DIETDESC  DIM       25  
CONDESC   DIM       25
CONDESC1  DIM       20
ANAFNAME  DIM       50
SURFNAME  DIM       50
SDOCFNAM  DIM       50
SESSKEYS  DIM       22
STBYFLAG  FORM      1
FOLDERSF  DIM       3
LBRACKT   INIT      "("
RBRACKT   INIT      ")"
EXPCTDSC  DIM       8
LENPTR    FORM      2
COUNT     FORM      2
SAVPSNAM  DIM       20                      * Save PSNAME
SESSFLAG  FORM      1
.
SESSDATE  DIM       8
SESSTIME  DIM       5
SESSCLIN  DIM       6
IMGSRC    DIM       20
WORKDESC  DIM       125
CODECO    INIT      "CO"
.
.
PRGID     INIT      "PHPWEB02"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "PHP Services for CISAM"
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      XMLRED00       * Read Fifo WEBPID and USERID    * C-90207
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,MAIN1000
.
          MATCH     "oprweb01",PHPFILEN
          IF        @EQUAL
            CALL      XMLHED00
            CALL      OPRW0000
            CALL      XMLEND00
            GOTO      MAIN1000
          ENDIF
.
          MATCH     "patweb93",PHPFILEN
          IF        @EQUAL
            CALL      XMLHED00
            CALL      PATA0000
            CALL      XMLEND00
            GOTO      MAIN1000
          ENDIF
.
          GOTO      MAIN1000
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.
PROFLD00
          RETURN
.-------------------------------------------------------------------------------
. Open Files and Initialize Variables
.-------------------------------------------------------------------------------
INIT0000  
.
          OPEN      PMSWORA1,"pmsworaf"
          OPEN      PATNOBE1,"patnobef"
          OPEN      PATONLV1,"patonlvf"
          OPEN      PATLOCA1,"patlocaf"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PATMI1A7,"patmi1af"
          OPEN      PATDO1A1,"patdo1af"
.
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PMSCCDA1,"pmsccdaf"
          OPEN      PATGSRN1,"patgsrnf"
          OPEN      OPRCLIA1,"oprcliaf"
          OPEN      OPRDETA1,"oprdetaf"
          OPEN      OPRDETA2,"oprdetaf"
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRTSMA1,"oprtsmaf"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A3,"bokrc1af"
          OPEN      BOKRX1A1,"bokrx1af"
.
          OPEN      WEBSECA1,"websecaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATHSPA1,"pathspaf"
.
.
          READ      CONTROLF,FORTY;*43,OPCNAUDA,OPCNAUDB,*46,OPCNAUDD:
                                   *61,OPCNAUDS,OPCNAUDT
          READ      CONTROLF,FORTY2;*28,OPCNFULL,*90,OPCNASTD,*119,OPCNASLN:
                                    *133,OPCNMIND,*235,OPCNFTIM,*237,OPCNTTIM
          READ      CONTROLF,TEN9;*208,CPMIAD
          READ      CONTROLF,FORTY;*55,OPCNAUDM,*61,OPCNAUDS,*111,OPCNCLSU
          READ      CONTROLF,FORTY2;*93,OPCNCLSH,*118,OPCNCMBD,*241,OPCNSCOM:
                                    *249,OPCNTPAT
          READ      CONTROLF,SIXTY8;*244,OPCNCPOD,*245,OPCNDSAS,*247,OPCNDSAE
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN9;*212,CFEETYP
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
          READ      CONTROLF,HUND05;*216,PTCNDWAW
          READ      CONTROLF,HUND10;*246,PTCNMHCP
          READ      CONTROLF,HUND12;*101,PTCNUFFR
.
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
            OPEN      MLTSECA1,"mltsecaf"
          ENDIF
.
INIT9999  RETURN
.-------------------------------------------------------------------------------
. Clear Parameters
.-------------------------------------------------------------------------------
CLRPAR00  MOVE      SP70,PHPFILEN 
          MOVE      SP70,WARDCODE 
          MOVE      SP70,DOCTTYPE 
          MOVE      SP70,FRSTDATE 
          MOVE      SP70,LASTDATE 
          MOVE      SP70,URNUMBER 
          MOVE      SP70,ADMISSNO
          MOVE      SP70,FORMACTN
          MOVE      SP70,COPYFKEY
          MOVE      SP70,DRAFTDOC
          MOVE      ZERO,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,UPDATEKY
          MOVE      SP70,VIEWTYPE
          MOVE      ZERO,STARTNUM
          MOVE      ZERO,NORECORD
          MOVE      SP70,STARTKEY
          MOVE      SP70,CORRTYPE
          MOVE      SP70,DETAILKY
          MOVE      SP70,AUDITKEY
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,FRSTDATE
          MOVE      SP70,LASTDATE
          MOVE      SP70,VYEARMTH
          MOVE      SP70,LINKTYP  
          MOVE      Z70,SENDFAXB 
          MOVE      Z70,RECIPNAM 
          MOVE      Z70,FAXNUMBR 
.
          MOVE      ZERO,SENDTYPE
          MOVE      SP70,SENDTOPH
          MOVE      SP70,SENDFREM
          MOVE      SP70,SENDTOEM
          MOVE      ZERO,COPYTOCT
          MOVE      SP70,COPYTOEM[1]
          MOVE      SP70,COPYTOEM[2]
          MOVE      SP70,COPYTOEM[3]
          MOVE      SP70,COPYTOEM[4]
          MOVE      SP70,COPYTOEM[5]
          MOVE      SP70,COPYTOEM[6]
          MOVE      SP70,COPYTOEM[7]
          MOVE      SP70,COPYTOEM[8]
          MOVE      SP70,COPYTOEM[9]
          MOVE      SP70,HCPFROMC
          MOVE      SP70,HCPSENDC
          MOVE      ZERO,HCPCOPCT
          MOVE      SP70,HCPCOPYC[1]
          MOVE      SP70,HCPCOPYC[2]
          MOVE      SP70,HCPCOPYC[3]
          MOVE      SP70,HCPCOPYC[4]
          MOVE      SP70,HCPCOPYC[5]
          MOVE      SP70,HCPCOPYC[6]
          MOVE      SP70,HCPCOPYC[7]
          MOVE      SP70,HCPCOPYC[8]
          MOVE      SP70,HCPCOPYC[9]
.
          MOVE      SP70,SELPRINT
          MOVE      ZERO,NOCOPIES
          MOVE      ZERO,SAVE2PDF
          MOVE      SP70,OBPCT001 
          MOVE      SP70,OBPCT002 
          MOVE      SP70,OBPCT003 
          MOVE      SP70,OBPCT004 
          MOVE      SP70,OBPCT005
          MOVE      ZERO,OBPCT006 
.
          MOVE      SP70,CLIDF001
          MOVE      SP70,CLIDF002
          MOVE      SP70,CLIDF003
          MOVE      SP70,CLIDF004
          MOVE      SP70,CLIDF005
.
          MOVE      SP70,CLICT001 
          MOVE      SP70,CLICT002 
          MOVE      SP70,CLICT003 
          MOVE      SP70,CLICT004 
          MOVE      SP70,CLICT005 
.
          MOVE      SP70,CLICD001  
          MOVE      SP70,CLICD002  
          MOVE      SP70,CLICD003 
          MOVE      SP70,CLICD004 
          MOVE      SP70,CLICD005 
          MOVE      SP70,CORRTYPE
          MOVE      SP70,CORRDESC
.
          MOVE      SP70,RESLNK01
          MOVE      SP70,RESLNK02
          MOVE      SP70,RESLNK03
          MOVE      SP70,RESLNK04
          MOVE      SP70,RESLNK05
          MOVE      SP70,RESLNK06
          MOVE      SP70,RESLNK07
.
          MOVE      SP70,CORSFILE
          MOVE      SP70,CORSP001
          MOVE      SP70,CORSP002
          MOVE      SP70,CORSP003
          MOVE      SP70,CORSP004
          MOVE      SP70,CORSP005
          MOVE      SP70,CORSP006
          MOVE      SP70,CORSP007
          MOVE      SP70,CORSP008
          MOVE      SP70,CORSP009
          MOVE      SP70,CORSP010
          MOVE      SP70,CORSP011
          MOVE      SP70,CORSP012
          MOVE      SP70,CORSP013
          MOVE      SP70,CORSP014
          MOVE      SP70,CORSP015
          MOVE      SP70,CORSP016
          MOVE      SP70,CORSP017
          MOVE      SP70,CORSP018
          MOVE      SP70,RESULTKY
          MOVE      SP70,PRINTFMT
          MOVE      SP70,REFERLKY
          MOVE      SP70,EMRGFLAG
.
CLRPAR99  RETURN
+
.------------------------------------------------------------
.         Create HTML comments temp file
.------------------------------------------------------------
CRHTMC00  
.         CLEAR     TEMPNAME
.         APPEND    "CLIW08",TEMPNAME
.         APPEND    PORT,TEMPNAME
.         RESET     TEMPNAME
.         REP       " 0",TEMPNAME
.
          CALL      TFILENAM
          MOVE      TFILNAME,TEMPNAME
.
          CLEAR     KEY13
          APPEND    TEMPNAME,KEY13
          APPEND    ".html",KEY13
          RESET     KEY13
.
          PREP      TEMPFILE,KEY13
          MOVE      ZERO,HTMLCNT
.
CRHTMC99  RETURN
+
.-------------------------------------------------------------------------------
. Set Parameters
.-------------------------------------------------------------------------------
SETPAR00  MATCH     "phpfilen=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PHPFILEN
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "docttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DOCTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "wardcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,WARDCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "frstdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FRSTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "lastdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LASTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "htmlline=",QRYNAME
          IF        @EQUAL
            CALL      CRHTMC00         * Create HTML comments temp file
            CALL      GETHTM00     
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "detailky=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DETAILKY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "sessdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SESSDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "sesstime=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SESSTIME
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "sessclin=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SESSCLIN
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
+
.------------------------------------------------------------
. Get Text Comments
.------------------------------------------------------------
GETHTM00  ADD       ONE,HTMLCNT
          PACK      HTMLLINE,QRYDATA,SP70,SP70
          WRITE     TEMPFILE,SEQ;HTMLLINE
.
GETHTM10  READ      CONFFILE,SEQ;QRYNAME,QRYDATA * Read CONFFILE sequentially
          GOTO      GETHTM99 IF OVER             * If end of file
          MATCH     SP70,QRYNAME                 * If next CGI parameter
          GOTO      GETHTM80 IF NOT EQUAL
          ADD       ONE,HTMLCNT
          PACK      HTMLLINE,QRYDATA,SP70,SP70
          WRITE     TEMPFILE,SEQ;HTMLLINE
          GOTO      GETHTM10
.
GETHTM80  MOVE      ONE,TEXTAREA                 * Flag for Next CGI Parameter
GETHTM99  RETURN
.----------------------------------------------------------------------------------------------------
. oprweb01.php functions
.----------------------------------------------------------------------------------------------------
OPRW0000  BRANCH    REPORTNO,OPRW1100,OPRW1200,OPRW1300,OPRW1400,OPRW1500:
                             OPRW1600
          GOTO      OPRW9999
.
OPRW1100  CALL     CASL0000
          GOTO     OPRW9999

OPRW1200  CALL     THEADM00
          GOTO     OPRW9999
.
OPRW1300  
          GOTO     OPRW9999
.
OPRW1400  CALL     DOCL0000
          GOTO     OPRW9999
.
OPRW1500  CALL     EXPL0000
          GOTO     OPRW9999
.
OPRW1600  CALL     CASS0000
          GOTO     OPRW9999
.
OPRW9999  RETURN
.--------------------------------------------------------------------------------
. Patient Admission List by Theatre Association
.
. $qry = "select distinct p.purno urnumber
.        ,opdaadmn  admissno
.        ,nvl((select replace(tcindc1,' ','0') from patcodes where tcode='FS' and acode=px2.pmpxfldr),'0')||
.         nvl((select replace(tcindc1,' ','0') from patcodes where tcode='T' and acode=p.ptype),'0')||
.         nvl((select replace(tcindc1,' ','0') from patcodes where tcode='PV' and acode=px2.pmpxprvi),'0')  folder
.        ,'<b>'||initcap(p.psname) || '</b>, ' || initcap(p.ptitl) || ' ' || initcap(p.pgname)  ||
.         ' (' || p.psage || 'y , ' || p.psex || ', ' || trim(p.purno) ||')' patfname
.        ,trim(mi1.adiag1) || ' ' || trim(mi1.adiag2)
.        ,trim(mi1.adate) || trim(mi1.atime)
.        ,pmwoedat
.        ,trim(mi1.award)||'/'|| trim(mi1.abed)
.        ,(select tdesc from patcodes where tcode='AC' and acode=mi1.aclssft) unitdesc
.      from patmi1af mi1
.      join pmsworaf on pmwoadmn=mi1.daadmno
.      join oprdetaf on opdaadmn=mi1.daadmno
.      join oprsesaf on opsedate=opdadate and opsetime=opdatime and opseclin=opdaclin
.      join patma1af p on p.purno=opdaurno
.      join opropraf on oprmroom=opsethet and
.           oprmhosp=(select nvl(replace(wbsehosp,' '),oprmhosp) from websecaf where wbseuid='".$uid."')
.      left join pmspx2af px2 on px2.pmpxurno=opdaurno
.      left join oprcliaf on opclclin=opdaclin
.      where mi1.dastat in ('2','4')
.      and   opsestat > 0
.      and   dopdasta <> '3'
.      and ( opcldoct=(select wbsedoc from websecaf where wbseuid='".$uid."')
.            or opdaclin=(select wbsedoc from websecaf where wbseuid='".$uid."')
.            or opseanae=(select wbsedoc from websecaf where wbseuid='".$uid."')
.            or   opdauniq in (select optsunid
.                              from   oprtsmaf
.                              where  optsunid=opdauniq
.                              and    doptssin in (' 0',' 1',' 3')
.                              and    optsscde=(select wbsedoc
.                                               from websecaf
.                                               where wbseuid='".$uid."')))
.      ";
.  $reply .= "function AddRows() {";
.--------------------------------------------------------------------------------
THEADM00  WRITE     HTMLFILE;"function AddRows() {";
          PACK      KEY9,TWO,SP70            * Scan Current Admission
          CALL      RDSMISS2
THEADM10  CALL      RDKMISS2
          BRANCH    OVRCD,THEADM30
          MATCH     "2",DASTAT
          GOTO      THEADM30 IF NOT EQUAL
.
          CALL      CHKTH000
          GOTO      THEADM10
.
THEADM30  PACK      KEY9,FOUR,SP70            * Scan Onleave Admission
          CALL      RDSMISS2
THEADM40  CALL      RDKMISS2
          BRANCH    OVRCD,THEADM99
          MATCH     "4",DASTAT
          GOTO      THEADM99 IF NOT EQUAL
.
          CALL      CHKTH000
          GOTO      THEADM40
.
THEADM99  WRITE     HTMLFILE;" };";
          RETURN
.
CHKTH000  MOVE      ZERO,SHOWFLAG
          PACK      KEY31,DAADMNO,SP70
          CALL      RSOPDEA2
CHKTH100  CALL      RKOPDEA2
          BRANCH    OVRCD,CHKTH999
          MATCH     DAADMNO,OPDAADMN
          GOTO      CHKTH999 IF NOT EQUAL
          COMPARE   THREE,OPDASTAT    * Cancelled
          GOTO      CHKTH100 IF EQUAL
.
          MATCH     WBSEDOC,OPDACLIN
          IF        @EQUAL
            MOVE      ONE,SHOWFLAG               * Doctors Clinic
          ENDIF
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
          CALL      RDOPSES1
          BRANCH    OVRCD,CHKTH999
          COMPARE   ZERO,OPSESTAT
          GOTO      CHKTH999 IF EQUAL
          MATCH     WBSEDOC,OPSEANAE
          IF        @EQUAL
            MOVE      ONE,SHOWFLAG               * Anaethestist
          ENDIF
.
          PACK      KEY6,OPDACLIN,SP70
          CALL      RDOPCLI1
          IF        OVRCD=0
            MATCH     WBSEDOC,OPCLDOCT
            IF        @EQUAL
              MOVE      ONE,SHOWFLAG               * Surgeon
            ENDIF
          ENDIF
.
          BRANCH    SHOWFLAG,CHKTH290
.
          PACK      KEY35,OPDAUNIQ,SP70
          CALL      RSOPTSM1
CHKTH200  CALL      RKOPTSM1
          BRANCH    OVRCD,CHKTH290
          MATCH     OPDAUNIQ,OPTSUNID
          GOTO      CHKTH290 IF NOT EQUAL
          MATCH     OPTSSCDE,WBSEDOC
          GOTO      CHKTH200 IF NOT EQUAL
          MOVE      ONE,SHOWFLAG               * Associated
.
CHKTH290  IF        SHOWFLAG=1
            CALL      THEROW00                   * Doctors is associated to the Admission in some way
          ENDIF
.
CHKTH999  RETURN
.
THEROW00  MOVE      OPDAADMN,ADMISSNO
          CALL      CASDET00
.
          MOVE      "OA",CATEGORY
          MOVE      OPDAANAE,CODE
          CALL      GETCOD00
          MOVE      TDESC,OADESC
.
          CALL      GETDET00
.
          CALL      GETCON00 * Get Patient Condition Details
          MOVE      "DC",CATEGORY
          MOVE      PDIET,CODE
          CALL      GETCOD00
          MOVE      TDESC,DIETDESC
.
          MOVE      "AC",CATEGORY
          MOVE      ACLSSFT,CODE
          CALL      GETCOD00
          MOVE      TDESC,UNITDESC
.
          MOVE      OPSEANAE,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
.           CALL      DOCNAM00
            MOVE      DTITL,FMTTITLE           
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000                  
            MOVE      DOCFNAME,ANAFNAME
          ELSE
            MOVE      "&nbsp;",ANAFNAME
          ENDIF
.
          MOVE      OPSECLIN,KEY6
          COMPARE   ZERO,OPCNCLSU
          IF        @EQUAL
            MOVE      OPCLDOCT,KEY6
          ENDIF
          CALL      RDDOCT1
          IF        OVRCD=0
.           CALL      DOCNAM00
            MOVE      DTITL,FMTTITLE         
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000              
            MOVE      DOCFNAME,SURFNAME
          ELSE
            MOVE      "&nbsp;",SURFNAME
            MOVE      OPCLDESC,SURFNAME
          ENDIF
.
          WRITE     HTMLFILE;"t.addRow(":
                    QUOTE,AURNO,QUOTE,COMMA:                * 0  - patient UR
                    QUOTE,OPDAADMN,QUOTE,COMMA:             * 1  - patient admission
                    QUOTE,KEY3,QUOTE,COMMA:                 * 2  - patient folder indicator
                    QUOTE,FORMATNM,QUOTE,COMMA:             * 3  - patient formated name
                    QUOTE,ADIAG1,ADIAG2,QUOTE,COMMA:        * 4  - admission diagnosis
                    QUOTE,ADATE,ATIME,QUOTE,COMMA:          * 5  - admission diagnosis
                    QUOTE,PMWOEDAT,QUOTE,COMMA:             * 6  - expected discharge
                    QUOTE,AWARD,"/",ABED,QUOTE,COMMA:       * 7  - Ward/Bed
                    QUOTE,UNITDESC,QUOTE,COMMA:             * 8  - Unit
                    QUOTE,DIETDESC,QUOTE,COMMA:             * 9  - Diet
                    QUOTE,OPDADATE,QUOTE,COMMA:             * 10 - theatre date
                    QUOTE,OPDATIME,QUOTE,COMMA:             * 11 - theatre time
                    QUOTE,OPDACLIN,QUOTE,COMMA:             * 12 - clinic
                    QUOTE,DOPDASTA,QUOTE,COMMA:             * 13 - theatre status
                    QUOTE,OPDACASE,QUOTE,COMMA:             * 14 - theate case id
                    QUOTE,OPDADES1,QUOTE,COMMA:             * 15 - Operation desc. line  1
                    QUOTE,OPDADES2,QUOTE,COMMA:             * 16 - Operation desc. line 2
                    QUOTE,OPDADES3,QUOTE,COMMA:             * 17 - Operation desc. line 3
                    QUOTE,OPDACOMM,QUOTE,COMMA:             * 18 - Comments
                    QUOTE,SURFNAME,QUOTE,COMMA:             * 19 - doctor name
                    QUOTE,ANAFNAME,QUOTE,COMMA:             * 20 - anaesthetist
                    QUOTE,AWARD,QUOTE,COMMA:                * 21 - ward
                    QUOTE,ABED,QUOTE,COMMA:                 * 22 - bed
                    QUOTE,OADESC,QUOTE,COMMA:               * 23 - CAT OA desc.
                    QUOTE,OPDADATE,OPDATIME,OPDACASE,QUOTE,");"       * 24 - casekey
          RETURN
.--------------------------------------------------------------------------------
. Doctor List
.  $qry = "select distinct mi1.adocta, do1.dsname||','||do1.dtitl||' '||do1.dgname docfname
.          from patmi1af mi1
.          join patdo1af do1 on do1.dcode=mi1.adocta
.          where mi1.dastat in ('2','4')
.          order by 2
.      ";
.--------------------------------------------------------------------------------
DOCL0000  WRITE     HTMLFILE;"function consultantList() {";

          PACK      KEY6,SP70
          CALL      RDSDOCT1
DOCL1000  CALL      RDKDOCT1
          BRANCH    OVRCD,DOCL9000
.
          PACK      KEY23,DCODE,TWO,SP70
          CALL      RDSMISS7
          CALL      RDKMISS7
          BRANCH    OVRCD,DOCL2000
          MATCH     DCODE,ADOCTA
          GOTO      DOCL2000 IF NOT EQUAL
          MATCH     "2",DASTAT
          GOTO      DOCL2000 IF NOT EQUAL
          GOTO      DOCL3000
.
DOCL2000  PACK      KEY23,DCODE,FOUR,SP70
          CALL      RDSMISS7
          CALL      RDKMISS7
          BRANCH    OVRCD,DOCL1000
          MATCH     DCODE,ADOCTA
          GOTO      DOCL1000 IF NOT EQUAL
          MATCH     "4",DASTAT
          GOTO      DOCL1000 IF NOT EQUAL
.
DOCL3000  WRITE     HTMLFILE;"t.addRow(":
                    QUOTE,ADOCTA,QUOTE,COMMA:
                    QUOTE,DSNAME,",",DTITL," ",DGNAME,QUOTE,");"
          GOTO      DOCL1000
.
DOCL9000  WRITE     HTMLFILE;" };";
.
          RETURN
.
. Theatre Cases by Associated Doctor Code
.
CASL0000  WRITE     HTMLFILE;"function AddRows() {";
          PACK      KEY22,FRSTDATE,SP70
          CALL      RSOPSES7
CASL1000  CALL      RKOPSES7
          BRANCH    OVRCD,CASL9000
          MATCH     OPSEDATE,LASTDATE
          GOTO      CASL9000 IF LESS
          COMPARE   ZERO,OPSESTAT      * Cancelled
          GOTO      CASL1000 IF EQUAL
.
          MOVE      OPSECLIN,KEY6
          COMPARE   ZERO,OPCNCLSU
          IF        @EQUAL
            CALL      RDOPCLI1
            IF        OVRCD=0
              MOVE      OPCLDOCT,KEY6
            ENDIF
          ENDIF
          CALL      RDDOCT1
          IF        OVRCD=0
.           CALL      DOCNAM00
            MOVE      DTITL,FMTTITLE    
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000           
            MOVE      DOCFNAME,SURFNAME
          ELSE
            MOVE      "&nbsp;",SURFNAME
          ENDIF
          MATCH     SP70,OPCLDESC,
          IF        @EQUAL
            MOVE      SURFNAME,OPCLDESC
          ENDIF

          MOVE      ZERO,SESSFLAG         
          MATCH     WBSEDOC,KEY6
          IF        @EQUAL
            MOVE      ONE,SESSFLAG     
          ENDIF
.
          MATCH     WBSEDOC,OPSEANAE
          IF        @EQUAL
            MOVE      ONE,SESSFLAG    
          ENDIF
.
          MOVE      OPSEANAE,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
.           CALL      DOCNAM00
            MOVE      DTITL,FMTTITLE           
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000                
            MOVE      DOCFNAME,ANAFNAME
          ELSE
            MOVE      "&nbsp;",ANAFNAME
          ENDIF
.
          PACK      SESSKEYS,OPSEHOSP,OPSEDATE,OPSETIME,OPSECLIN,SP70
          PACK      KEY26,OPSEHOSP,OPSEDATE,OPSETIME,OPSECLIN,SP70
          CALL      RSOPDEA1
CASL2000  CALL      RKOPDEA1
          BRANCH    OVRCD,CASL9000
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
          MATCH     KEY22,SESSKEYS
          GOTO      CASL1000 IF NOT EQUAL
          COMPARE   THREE,OPDASTAT
          GOTO      CASL2000 IF EQUAL       * Cancelled
.
          BRANCH    SESSFLAG,CASL2200       * Match on Session Doctors
          PACK      KEY35,OPDAUNIQ,SP70     * Check for other doctors associated with case
          CALL      RSOPTSM1
CASL2100  CALL      RKOPTSM1
          BRANCH    OVRCD,CASL2000
          MATCH     OPDAUNIQ,OPTSUNID
          GOTO      CASL2000 IF NOT EQUAL
          MATCH     OPTSSCDE,WBSEDOC        
          GOTO      CASL2100 IF NOT EQUAL   * Match on associated doctor drop through and process
.
CASL2200  MOVE      OPDAADMN,ADMISSNO
          CALL      CASDET00
.
          MOVE      "OA",CATEGORY
          MOVE      OPDAANAE,CODE
          CALL      GETCOD00
          MOVE      TDESC,OADESC
.
          CALL      GETDET00
.
          WRITE     HTMLFILE;"t.addRow(":
                    QUOTE,AURNO,QUOTE,COMMA:                * 0 - patient UR
                    QUOTE,OPDAADMN,QUOTE,COMMA:             * 1 - patient admission
                    QUOTE,KEY3,QUOTE,COMMA:                 * 2 - patient folder indicator
                    QUOTE,FORMATNM,QUOTE,COMMA:             * 3 - patient formated name
                    QUOTE,OPDADATE,QUOTE,COMMA:             * 4 - theatre date
                    QUOTE,OPDATIME,QUOTE,COMMA:             * 5 - theatre time
                    QUOTE,OPDACLIN,QUOTE,COMMA:             * 6 - clinic
                    QUOTE,DOPDASTA,QUOTE,COMMA:             * 7 - theatre status
                    QUOTE,OPDACASE,QUOTE,COMMA:             * 8 - theate case id
                    QUOTE,OPDADES1,QUOTE,COMMA:             * 9 - Operation desc. line  1
                    QUOTE,OPDADES2,QUOTE,COMMA:             * 10 - Operation desc. line 2
                    QUOTE,OPDADES3,QUOTE,COMMA:             * 11 - Operation desc. line 3
                    QUOTE,OPDACOMM,QUOTE,COMMA:             * 12 - Comments
                    QUOTE,OPCLDESC,QUOTE,COMMA:             * 13 - Session Name
                    QUOTE,ANAFNAME,QUOTE,COMMA:             * 14 - anaesthetist
                    QUOTE,AWARD,QUOTE,COMMA:                * 15 - ward
                    QUOTE,ABED,QUOTE,COMMA:                 * 16 - bed
                    QUOTE,OADESC,QUOTE,COMMA:                         * 17 - CAT OA desc.
                    QUOTE,OPDADATE,OPDATIME,OPDACASE,QUOTE,COMMA:     * 18 - casekey - not used
                    QUOTE,OPDADATE,OPDATIME,OPDACASE,QUOTE,COMMA:     * 19 - casekey
                    QUOTE,SURFNAME,QUOTE,");"       * 19 - casekey
          GOTO      CASL2000
.
CASL9000  WRITE     HTMLFILE;" };";
          RETURN
.------------------------------------------------------------
. Case Details by Theatre Session
.------------------------------------------------------------
CASS0000  PACK      SESSKEYS,WBSEHOSP,SESSDATE,SESSTIME,SESSCLIN,SP70
          PACK      KEY26,WBSEHOSP,SESSDATE,SESSTIME,SESSCLIN,SP70
          CALL      RSOPDEA1
CASS2000  CALL      RKOPDEA1
          BRANCH    OVRCD,CASS9000
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
          MATCH     KEY22,SESSKEYS
          GOTO      CASS9000 IF NOT EQUAL
.
          MOVE      OPDAADMN,ADMISSNO
          CALL      CASDET00
.
          MOVE      "OA",CATEGORY
          MOVE      OPDAANAE,CODE
          CALL      GETCOD00
          MOVE      TDESC,OADESC
.
          CALL      GETDET00
.
          WRITE     HTMLFILE;"t.addRow(":
                    QUOTE,AURNO,QUOTE,COMMA:                * 0 - patient UR
                    QUOTE,OPDAADMN,QUOTE,COMMA:             * 1 - patient admission
                    QUOTE,KEY3,QUOTE,COMMA:                 * 2 - patient folder indicator
                    QUOTE,FORMATNM,QUOTE,COMMA:             * 3 - patient formated name
                    QUOTE,OPDADATE,QUOTE,COMMA:             * 4 - theatre date
                    QUOTE,OPDATIME,QUOTE,COMMA:             * 5 - theatre time
                    QUOTE,OPDACLIN,QUOTE,COMMA:             * 6 - clinic
                    QUOTE,DOPDASTA,QUOTE,COMMA:             * 7 - theatre status
                    QUOTE,OPDACASE,QUOTE,COMMA:             * 8 - theate case id
                    QUOTE,OPDADES1,QUOTE,COMMA:             * 9 - Operation desc. line  1
                    QUOTE,OPDADES2,QUOTE,COMMA:             * 10 - Operation desc. line 2
                    QUOTE,OPDADES3,QUOTE,COMMA:             * 11 - Operation desc. line 3
                    QUOTE,OPDACOMM,QUOTE,COMMA:             * 12 - Comments
                    QUOTE,SURFNAME,QUOTE,COMMA:             * 13 - doctor name
                    QUOTE,ANAFNAME,QUOTE,COMMA:             * 14 - anaesthetist
                    QUOTE,AWARD,QUOTE,COMMA:                * 15 - ward
                    QUOTE,ABED,QUOTE,COMMA:                 * 16 - bed
                    QUOTE,OADESC,QUOTE,COMMA:                         * 17 - CAT OA desc.
                    QUOTE,OPDADATE,OPDATIME,OPDACASE,QUOTE,COMMA:     * 18 - casekey - not used
                    QUOTE,OPDADATE,OPDATIME,OPDACASE,QUOTE,");"       * 19 - casekey
          GOTO      CASS2000
.
CASS9000  WRITE     HTMLFILE;" };";
          RETURN
.
.------------------------------------------------------------
. Get patient Infomation
.------------------------------------------------------------
GETDET00  UNPACK    PBDATE,CDAY,CMON,CYEAR,CCENT
          CALL      IBACLOCK
          IF        PCEASE=1
            MOVE      PDECDTE,AGEDATE            * Date of Death
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
          CALL      CALCAGE
.
          CALL      PATLN000
.
          CLEAR     KEY3
          PACK      KEY5,CATFS,PMPXFLDR,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            REP       " 0",TCINDC1
            APPEND    TCINDC1,KEY3
          ELSE
            APPEND    ZERO,KEY3
          ENDIF
.
          PACK      KEY5,CATT,PTYPE,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            REP       " 0",TCINDC1
            APPEND    TCINDC1,KEY3
          ELSE
            APPEND    ZERO,KEY3
          ENDIF
.
          PACK      KEY5,CATPV,PMPXPRVI,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            REP       " 0",TCINDC1
            APPEND    TCINDC1,KEY3
          ELSE
            APPEND    ZERO,KEY3
          ENDIF
          RESET     KEY3
          RETURN
.
.----------------------------------------------------------------------------------------------------
. Expected Patients by Doctor
.----------------------------------------------------------------------------------------------------
EXPL0000  WRITE     HTMLFILE;"function AddRows() {";
          PACK      KEY22,WBSEDOC,FRSTDATE,SP70
          CALL      RSBKREC3
EXPL1000  CALL      RKBKREC3
          BRANCH    OVRCD,EXPL2000
          MATCH     BKREADOC,WBSEDOC 
          GOTO      EXPL2000 IF NOT EQUAL
          MATCH     BKREEDAT,LASTDATE
          GOTO      EXPL2000 IF LESS
          COMPARE   ZERO,BKRESTAT      * Booked
          GOTO      EXPL1000 IF NOT EQUAL
.
          MOVE      DBKREBOO,ADMISSNO
          CALL      CASDET00
          CALL      GETDET00
.
          MOVE      SP70,ACDESC
          MATCH     SP70,BKREDEPT
          IF        !@EQUAL
            PACK      KEY5,CATAC,BKREDEPT,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,ACDESC
            ENDIF
          ENDIF
.
          MOVE      SP70,WBDESC
          MATCH     SP70,BKREWARD
          IF        !@EQUAL
            PACK      KEY6,BKREWARD,SP70
            CALL      RDWARD1
          ENDIF
.
          WRITE     HTMLFILE;"t.addRow(":
                    QUOTE,BKREURNO,QUOTE,COMMA:
                    QUOTE,DBKREBOO,QUOTE,COMMA:
                    QUOTE,KEY3,QUOTE,COMMA:
                    QUOTE,FORMATNM,QUOTE,COMMA:
                    QUOTE,BKREEDAT,QUOTE,COMMA:
                    QUOTE,"Booked",QUOTE,COMMA:
                    QUOTE,BKRXUFF1," ",BKRXUFF2," ",BKRXUFF3,QUOTE,COMMA:
                    QUOTE,ACDESC,QUOTE,COMMA:
                    QUOTE,WBDESC,QUOTE,");"
          GOTO      EXPL1000
.
EXPL2000  
          PACK      KEY23,WBSEDOC,ONE,SP70
          CALL      RDSMISS7
EXPL3000  CALL      RDKMISS7
          BRANCH    OVRCD,EXPL9000
          MATCH     ADOCTA,WBSEDOC 
          GOTO      EXPL9000 IF NOT EQUAL
          MATCH     "1",DASTAT 
          GOTO      EXPL9000 IF NOT EQUAL
.
          MATCH     FRSTDATE,ADATE
          GOTO      EXPL3000 IF LESS
          MATCH     ADATE,LASTDATE
          GOTO      EXPL3000 IF LESS
.
          MOVE      DAADMNO,ADMISSNO
          CALL      CASDET00
          CALL      GETDET00
.
          MOVE      SP70,ACDESC
          MATCH     SP70,ACLSSFT
          IF        !@EQUAL
            PACK      KEY5,CATAC,ACLSSFT,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,ACDESC
            ENDIF
          ENDIF
.
          MOVE      SP70,WBDESC
          MATCH     SP70,AWARD
          IF        !@EQUAL
            PACK      KEY6,AWARD,SP70
            CALL      RDWARD1
          ENDIF
.
          WRITE     HTMLFILE;"t.addRow(":
                    QUOTE,AURNO,QUOTE,COMMA:
                    QUOTE,AADMNO,QUOTE,COMMA:
                    QUOTE,KEY3,QUOTE,COMMA:
                    QUOTE,FORMATNM,QUOTE,COMMA:
                    QUOTE,ADATE,QUOTE,COMMA:
                    QUOTE,"Pre-Admitted",QUOTE,COMMA:
                    QUOTE,ADIAG1," ",ADIAG2," ",SP1,QUOTE,COMMA:
                    QUOTE,ACDESC,QUOTE,COMMA:
                    QUOTE,WBDESC,QUOTE,");"
          GOTO      EXPL3000
.
EXPL9000  WRITE     HTMLFILE;" };";
          RETURN
.------------------------------------------------------------
. Get Case Details
.------------------------------------------------------------
CASDET00  MOVE      ADMISSNO,KEY8
          CALL      GURN0000
          BRANCH    EXIT,CASDET99
          MATCH     "0       ",AURNO
          GOTO      CASDET05 IF EQUAL   * have a UR number
          MATCH     "       0",AURNO
          GOTO      CASDET05 IF EQUAL   * have a UR number
          MATCH     "        ",AURNO
          GOTO      CASDET10 IF NOT EQUAL   * have a UR number
.
.         on pre-admission file
.
CASDET05  MOVE      ADMISSNO,KEY8           * visit number
          CALL      RDPRAM1
          BRANCH    OVRCD,CASDET10          * invalid visit number
.
          MOVE      ZERO,PURNO
          MOVE      PURNO,URNUMBER
          GOTO      CASDET99                * valid patient
.
.         on master file
.
CASDET10  PACK      KEY8,AURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,CASDET99          * invalid UR number
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          MOVE      PURNO,URNUMBER
.
CASDET99  RETURN
.******************************************************************************
.                   GURN0000
.    Given the admission number in KEY8/KEY6 this gets the UR number from
.    either the admission or booking master file.
.    Returns : AURNO
.    Routine by Bill Dew 14/03/90
.*******************************************************************************
GURN0000  CALL      RDMISS1                 * read admission file
          IF        OVRCD=1
            CALL      CLPATMIS
          ENDIF
.
GURN5555  CALL      RDBKREC1                * read booking records file
          BRANCH    OVRCD,GURN9990
          MOVE      BKREURNO,AURNO
.
          CALL      RDBKRX11
          BRANCH    OVRCD,GURN9990
          MOVE      ZERO,EXIT               * a valid admission number
          GOTO      GURN9999
.
GURN9990  MOVE      ZERO,EXIT                * an invalid admission number
.
GURN9999  RETURN
.
.----------------------------------------------------------------------------------------------------
. oprweb01.php functions
.----------------------------------------------------------------------------------------------------
PATA0000  BRANCH    REPORTNO,PATA1100,PATA1200,PATA1300,PATA1400,PATA1500
          GOTO      PATA9999
.
PATA1100  CALL     WRDL0000
          GOTO     PATA9999

PATA1200  
          GOTO     PATA9999
.
PATA1300  
          GOTO     PATA9999
.
PATA1400  
          GOTO     PATA9999
.
PATA1500  
          GOTO     PATA9999
.
PATA9999  RETURN
.--------------------------------------------------------------------------------
. Ward Patient List patweb93.php
.--------------------------------------------------------------------------------
WRDL0000  WRITE     HTMLFILE;"function AddRows() {";
          CALL      IBACLOCK            * Calculate expected discharge date
          PACK      KEY6,WARDCODE,SP70
          CALL      RDWARD1
          BRANCH    WINPUT,CTABC500
.
. Bed Ward
.------------------------------------------------------------
CTABC100  CALL      RDKWARD1
          BRANCH    OVRCD,CTABC500
          MATCH     WARDCODE,WWARD
          GOTO      CTABC500 IF NOT EQUAL
          BRANCH    WACTIVE,CTABC100     *Do not display inactive bed
          CALL      CROWC000
          GOTO      CTABC100
.
. No Bed Ward
.------------------------------------------------------------
CTABC500  PACK      KEY13,WARDCODE,SP20
          CALL      RDSNOBE1
CTABC600  CALL      RDKNOBE1                * read next record
          BRANCH    OVRCD,CTABC700
          MATCH     WARDCODE,NBWARD         * same ward?
          GOTO      CTABC700 IF NOT EQUAL
          PACK      ADMISSNO,NBADMNO
          MOVE      NBROOM,WBED
          MOVE      ZERO,STBYFLAG
          CALL      CBEDC000         * Output Row Details
          GOTO      CTABC600
.
. On Leave Pateints
.------------------------------------------------------------
CTABC700  PACK      KEY14,WARDCODE,SP70
          CALL      RDSONLV1
CTABC800  CALL      RDKONLV1
          BRANCH    OVRCD,CTABC950
          MATCH     OWARD,WARDCODE
          GOTO      CTABC950 IF NOT EQUAL
          MATCH     ZEROVISN,OADMNO
          GOTO      CTABC800 IF EQUAL
          PACK      ADMISSNO,OADMNO
          MOVE      OBED,WBED
          MOVE      ZERO,STBYFLAG
          CALL      CBEDC000         * Output Row Details
          GOTO      CTABC800
.
CTABC950  
.
WRDL9000  WRITE     HTMLFILE;" };";
.
          RETURN
.------------------------------------------------------------
. Output Ward/Bed Row
.------------------------------------------------------------
CROWC000  MATCH     ZEROVISN,WADMNO
          GOTO      CROWC900 IF EQUAL
.
          PACK      ADMISSNO,WADMNO
          MOVE      ZERO,STBYFLAG
          CALL      CBEDC000         * Output Row Details
.
          MATCH     ZEROVISN,WSTBY
          GOTO      CROWC999 IF EQUAL
.
          PACK      ADMISSNO,WSTBY
          MOVE      ONE,STBYFLAG
          CALL      CBEDC000         * Output Row Details
          GOTO      CROWC999
.
CROWC900  
CROWC999  RETURN

.------------------------------------------------------------
. Current Admissions Patient Details
.------------------------------------------------------------
CBEDC000  CALL      GETPAT00
          CALL      PATLN000
          CALL      PATFLD00                * Use standard Patient Folder sub
          MOVE      KEY3,FOLDERSF           * and store suffix
          MOVE      ADOCTA,DOCTCODE
.
          CALL      GETCON00 * Get Patient Condition Details
          MOVE      "DC",CATEGORY
          MOVE      PDIET,CODE
          CALL      GETCOD00
          MOVE      TDESC,DIETDESC
.
          MOVE      "AC",CATEGORY
          MOVE      ACLSSFT,CODE
          CALL      GETCOD00
          MOVE      TDESC,UNITDESC
.
          WRITE     HTMLFILE;"t.addRow(":
                    QUOTE,URNUMBER,QUOTE,COMMA:
                    QUOTE,ADMISSNO,QUOTE,COMMA:
                    QUOTE,FOLDERSF,QUOTE,COMMA:
                    QUOTE,FORMATNM,QUOTE,COMMA:
                    QUOTE,ADIAG1,"  ",ADIAG2,QUOTE,COMMA:
                    QUOTE,ADATE,ATIME,QUOTE,COMMA:
                    QUOTE,PMWOEDAT,QUOTE,COMMA:
                    QUOTE,PMWOWDI1,QUOTE,COMMA:
                    QUOTE,PMWOWDI2,QUOTE,COMMA:
                    QUOTE,PMWOWDI3,QUOTE,COMMA:
                    QUOTE,PCEASE,QUOTE,COMMA:
                    QUOTE,LPCONAM,QUOTE,COMMA:
                    QUOTE,CONDESC,QUOTE,COMMA:
                    QUOTE,CONDIND1,QUOTE,COMMA:
                    QUOTE,DIETDESC,QUOTE,COMMA:
                    QUOTE,ABED,QUOTE,COMMA:
                    QUOTE,UNITDESC,QUOTE,COMMA:
                    QUOTE,SDOCFNAM,QUOTE,COMMA:
                    QUOTE,PSNAME,PGNAME,QUOTE,COMMA:
                    QUOTE,DOCTCODE,QUOTE,COMMA:
                    QUOTE,LPCDATE,QUOTE,COMMA:
                    QUOTE,LPCTIME,QUOTE,COMMA:
                    QUOTE,PSUBRB,QUOTE,COMMA:
                    QUOTE,PPOST,QUOTE,COMMA:
                    QUOTE,PADD1," ",PADD2," ",PSUBRB," ",PADD4," ",PPOST,QUOTE,");"
.

          RETURN
.------------------------------------------------------------
. Get Patient Condition Details
.------------------------------------------------------------
GETCON00  MOVE      SP70,CONDESC1
.
          PACK      KEY88,PTMASNAM,PMPXFNAM,AADMNO
          CALL      RDLOCA1
          BRANCH    OVRCD,GETCON10
.
          MATCH     SP70,LPCONDC
          GOTO      GETCON15 IF EQUAL
.
          PACK      KEY5,CODECO,LPCONDC
          CALL      RDCODE1
          BRANCH    OVRCD,GETCON15
          MOVE      TDESC,CONDESC
          MOVE      TDESC,CONDESC1
          MATCH     SP70,TCINDC1
          IF        @EQUAL
            MOVE      ZERO,TCINDC1
          ENDIF
          GOTO      GETCON20
.
GETCON10  MOVE      SP70,LPCONDD
          MOVE      SP70,LPCONDC
          MOVE      SP70,LPCDATE
          MOVE      SP70,LPCTIME
          MOVE      SP70,LPCONAM
GETCON15  MOVE      SP70,CONDESC
          MOVE      ZERO,TCINDC1
.
GETCON20  CLEAR     IMGSRC
          APPEND    "ConditionIcon_",IMGSRC
          IF        PCEASE=1
            APPEND    "D",IMGSRC
            MOVE      "Deceased",CONDESC
          ELSE
            APPEND    TCINDC1,IMGSRC
          ENDIF
          APPEND    ".gif",IMGSRC
          RESET     IMGSRC
.
GETCON50  PACK      KEY8,AADMNO,SP70
          CALL      RDPMWOR1
          IF        OVRCD=1
            CALL      CLPMWO00
            PACK      WORKDESC,SP70,SP70
            GOTO      GETCON99
          ENDIF
.
          PACK      WORKDESC,SP70,SP70
          CLEAR     WORKDESC
.
          MATCH     SP70,PMWOWDI1
          IF        !@EQUAL
            APPEND    "<br>",WORKDESC
            APPEND    PMWOWDI1,WORKDESC
          ENDIF
.
          MATCH     SP70,PMWOWDI2
          IF        !@EQUAL
            APPEND    "<br>",WORKDESC
            APPEND    PMWOWDI2,WORKDESC
          ENDIF
.
          MATCH     SP70,PMWOWDI3
          IF        !@EQUAL
            APPEND    "<br>",WORKDESC
            APPEND    PMWOWDI3,WORKDESC
          ENDIF
.
          APPEND    "&nbsp;",WORKDESC
          RESET     WORKDESC
.
GETCON99  RETURN
.
.---------------------------------------------------------------------------
. Patient Details from master file to display in table c
.   Requires ADMISSNO & URNUMBER to be set.
.   URNUMBER can be blank if a Valid Admission Record is Expected.
.---------------------------------------------------------------------------
GETPAT00  MOVE      ADMISSNO,KEY8
          CALL      RDMISS1
          IF        OVRCD=0
            MOVE      AURNO,URNUMBER
          ENDIF
          MOVE      OVRCD,MISSFLAG
.
          MATCH     "       0",URNUMBER
          IF        @EQUAL
            MOVE      ADMISSNO,KEY8
            CALL      RDPRAM1
            BRANCH    OVRCD,GETPAT95
          ELSE
            PACK      KEY8,URNUMBER
            CALL      RDMAST1
            BRANCH    OVRCD,GETPAT95
          ENDIF
.
          CALL      PATINT00                * Format Patient Name with Initial
.
          IF        PCEASE=1
            MOVE      PDECDTE,AGEDATE            * Date of Death
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
          CALL      CALCAGE
.
          MOVE      SP70,DOCFNAME
          PACK      KEY6,ADOCTA
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE            * I CAR 13038
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000                  * end I CAR 13038
            PACK      SDOCFNAM,DOCFNAME
          ENDIF
.
.
GETPAT10  PACK      KEY8,ADMISSNO,SP70
          CALL      RDPMWOR1
          IF        OVRCD=1
            CALL      CLPMWO00
          ENDIF
.
          MATCH     SP70,PMWOEDAT
          IF        @EQUAL
            UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
            MOVE      ASTAY,ADJDAYS
            CALL      ADJDAT00
            UNPACK    PMWOEDAT,CCENT,CYEAR,CMON,CDAY
          ENDIF
.
          PACK      KEY3,SP70
          PACK      KEY8,URNUMBER
          CALL      RDPMPX21
          BRANCH    OVRCD,GETPAT95
          GOTO      GETPAT99
.
GETPAT95  MOVE      "Invalid Record",PSNAME
          MOVE      SP70,PGNAME
          MOVE      SP70,PTITL
          CALL      PATNAM00
.
GETPAT99  RETURN
.------------------------------------------------------------------------
. Format Patient Name with Second Name Initial
.------------------------------------------------------------------------
PATINT00  CLEAR     DISPNAME
          MOVE      PSNAME,SAVPSNAM
.
          REP       "#" ",PSNAME
          REP       "#" ",PGNAME
          MOVE      SP70,PATFNAME
          REP       UPPLOW,PSNAME
          APPEND    "<b>",DISPNAME
          APPEND    PSNAME,DISPNAME
          APPEND    "</b>",DISPNAME
          PACK      NAME35,PTITL,SP70
          CALL      NAMSTR00
.
          MOVELPTR  PGNAME,LENPTR
          MOVE      ONE,COUNT
PATINT10  BUMP      PGNAME
          GOTO      PATINT99 IF EOS
          ADD       ONE,COUNT
          MATCH     SP1,PGNAME
          GOTO      PATINT10 IF NOT EQUAL
          BUMP      PGNAME
          ADD       ONE,COUNT
          RESET     PGNAME
          SETLPTR   PGNAME,COUNT
          PACK      NAME35,PGNAME,SP70
          CALL      NAMSTR00
          RESET     PGNAME,LENPTR
          RESET     PGNAME
.
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,PATFNAME
          STRIP     PATFNAME
.
          MOVE      SAVPSNAM,PSNAME         * Original value with quotes etc
.
PATINT99  RETURN
.------------------------------------------------------------
. Format Patient Name <b>SURNAME</b>Title Given Names
.------------------------------------------------------------
PATNAM00  CLEAR     DISPNAME
          REP       "#" ",PSNAME
          REP       "#" ",PGNAME
          MOVE      SP70,PATFNAME
          REP       UPPLOW,PSNAME
          APPEND    "<b>",DISPNAME
          APPEND    PSNAME,DISPNAME
          APPEND    "</b>",DISPNAME
          PACK      NAME35,PTITL,SP70
          CALL      NAMSTR00
          PACK      NAME35,PGNAME,SP70
          CALL      NAMSTR00
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,PATFNAME
          STRIP     PATFNAME
          RETURN
.------------------------------------------------------------
. Clear file variables for PMSWORFD
.------------------------------------------------------------
CLPMWO00  MOVE      SP70,PMWOADMN
          MOVE      SP70,PMWOWDI1
          MOVE      SP70,PMWOWDI2
          MOVE      SP70,PMWOWDI3
          MOVE      SP70,PMWOEDAT
          MOVE      SP70,PMWOCDAT
          MOVE      SP70,PMWOCTIM
          MOVE      SP70,PMWOCUID
          MOVE      SP70,PMWOUDAT
          MOVE      SP70,PMWOUTIM
          MOVE      SP70,PMWOUUID
          MOVE      SP70,PMWOEDTM
          MOVE      SP70,PMWODPST
          MOVE      SP70,PMWOCORD
          MOVE      SP70,PMWODDES
          MOVE      SP70,PMWODTRN
          MOVE      SP70,PMWOUDF1
          MOVE      SP70,PMWOUDF2
          MOVE      SP70,PMWOUDF3
          MOVE      SP70,PMWOUDF4
          MOVE      SP70,PMWOUDF5
          MOVE      SP70,PMWOUDF6
          MOVE      SP70,PMWOUDF7
          MOVE      SP70,PMWOUDF8
          MOVE      SP70,PMWOCEXT
          MOVE      SP70,PMWOSPAR
.
CLPMWO99  RETURN

          INC       PMSWORIO/INC
          INC       PATLOCIO/INC
          INC       PATONLIO/INC
          INC       PATNOBIO/INC
          INC       PATDO1IO/INC
          INC       PATMI1IO/INC
          INC       PATWR1IO/INC
          INC       PATMA1IO/INC
          INC       PATPR1IO/INC
          INC       PMSPX2IO/INC
          INC       OPRDEAIO/INC 
          INC       OPRCLIIO/INC 
          INC       OPRSESIO/INC 
          INC       OPRTSMIO/INC 
          INC       BOKRC1IO/INC 
          INC       BOKRX1IO/INC 
          INC       PMSCCDIO/INC   * Concession Card Details
          INC       PMSVX1IO/INC
.
          INC       PATHSPIO/INC
          INC       MLTSECIO/INC
.
          INC       PATDOCCD    * Formate Doctors Name
          INC       PMSPX2TM    
          INC       PATDOCTM    * Template Fill-in for Doctors File
          INC       PATNAMCD    * Formate Patient Name
          INC       CLPATMAS
          INC       CLPATMIS
          INC       CLPMSPX2

.
          INC       DAYOFWEK
          INC       CALCAGE
          INC       NAMSTRCD
          INC       WEBDATCD
          INC       IBAWEBCD
.
          INC       TFILENAM

.******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAADT06                                                 *
.* Description    :  ICD10 Suggested Classification Upload                    *
.******************************************************************************
.* Date           :  13/01/2010                                               *
.* Author         :  J.Tan                                                    *
.* Function       :  Upload ICD10 Suggested Class.from ASCII file             *
.* Modifications  :                                                           *
.* V10.12.01 20/02/2018  Ken Bell  TSK 0825346                                *
.*           Allow for 3 character ICD Code Group to be added if valid        *
.*----------------------------------------------------------------------------*
.* V10.02.01 19/08/2011  J.Tan     CAR 234109                                 *
.*           Mods checking for blank lines                                    *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       PMSSGAFD/INC
          INC       PATICDFD/INC
          INC       PATFN1FD/INC
          INC       PATCONFD/INC
.
.         ICD10 Suggested Classfication Upload file
.
ICDCLTXT  FILE       HL7, FIXED=1000        * Upload file for ICD10 Sugg.Class.
.
.Name     Type     Size     Start
.----     ----     ----     -----
XCLAIM    DIM         3     Claim code (Catg CL)
XHFUND    DIM         6     Health Fund 
XHFTAB    DIM         8     Health Fund Table
XICDCD    DIM         9     ICD10 Disease Code
XSCLAS    DIM         3     Suggested Classification
.
. ----- Program Variables -----
.
CRETURN   INIT      015                * carriage return character
CMDLINE   DIM       100
ERRNUMB   FORM      6
FNAMEI    DIM       150
FORM12P2  FORM      12.2
RECNUMB   FORM       6
RECWRDT   FORM       6
RECUPDT   FORM       6
USERID    DIM       10
PATHNAME  DIM       100
TIMEIS    DIM       8
.
XFLD0001  DIM       30
XFLD0002  DIM       30
XFLD0003  DIM       30
XFLD0004  DIM       30
XFLD0005  DIM       30
.
PRGID     INIT      "IBAADT06"
PRGNAM    INIT      "Upload ICD10 Suggested Class."
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000              * Initailize variables & open files
          CALL      KOPT0000              * Keyin Upload or Update option
          BRANCH    EXIT,ML9999
.
          CALL      KASC0000              * Keyin ascii file
          BRANCH    EXIT,ML9900
.
          CALL      KUSR0000              * Keyin user id
          BRANCH    EXIT,ML0000
.
          CALL      KPTH0000              * Keyin pathname
          BRANCH    EXIT,ML0000
.
          CALL      PRHD0000              * Print header
          CALL      POST0000              * Updating file
          GOTO      ML9999
.
ML9900    CALL      HEAD132
          PRINT     *N,"Failed to open upload file. "
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000                                   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,"Opening pmssgaaf";
          OPEN      PMSSGAA1,"pmssgaaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATFN1A1,"patfn1af"
          CALL      OPICD1              * Open ICD10 V1-4 Dis file
INIT9999  RETURN
+
.******************************************************************************
.*                                 KOPT0000                                   *
.*                            Upload ICD10 Suggested Classification           *
.******************************************************************************
KOPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COPTION
          DISPLAY   *P1:6,*EF:
                    *P1:6,*V2LON,"0",*HOFF," = Exit":
                    *P1:7,*V2LON,"1",*HOFF," = Upload ICD10 Suggested Class.":
                    *P1:10,"Select Option : ";
.
KOPT1000  KEYIN     *P17:10,*V2LON,COPTION;
          BRANCH    COPTION,KOPT9999
.
          COMPARE   ZERO,COPTION
          GOTO      KOPT9000 IF EQUAL
          BEEP
          GOTO      KOPT1000
.
KOPT9000  MOVE      ONE,EXIT
KOPT9999  RETURN
+
.******************************************************************************
.*                                 KASC0000                                   *
.*                            Keyin ASCI file                                 *
.******************************************************************************
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEI WITH SP70,SP70,SP70
.
          DISPLAY   *P19:13,*EF,"ICD10 Suggested Classification Load File";
.
          KEYIN     *P1:14,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEI
.
          PACK      FNAMEI,FNAMEI,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEI
          GOTO      KASC9000 IF EQUAL
.
          SCAN      ".txt",FNAMEI
          IF        !@EQUAL
            SQUEEZE   FNAMEI
            ENDSET    FNAMEI
            APPEND    ".txt",FNAMEI      * add ".txt" if there is a path
            APPEND    SP70,FNAMEI
            RESET     FNAMEI
          ENDIF
          RESET     FNAMEI
.
          DISPLAY   *P26:14,*V2LON,*EL,FNAMEI;
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ICDCLTXT,FNAMEI
          TRAPCLR   IO
          BRANCH    OVRCD,KASC8000
.
          CALL      EXCUS000
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
+
.******************************************************************************
.*                                  KUSR0000                                  *
.*                     Keyin user id                                          *
.******************************************************************************
KUSR0000  KEYIN     *P1:16,*EL,"Keyin Username: ",*P26:16,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.******************************************************************************
.*                                  KPTH0000                                  *
.*                     Keyin path name                                        *
.******************************************************************************
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P1:17,*EL,"Keyin Pathname: ",*P26:17,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
.         PACK      PATHNAME,PATHNAME,SP70,SP70,SP70
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.******************************************************************************
.*                                  POST0000                                  *
.*                        Post record                                         *
.******************************************************************************
POST0000  MOVE      ZERO,RECNUMB
          MOVE      ZERO,RECWRDT
          MOVE      ZERO,RECUPDT
          MOVE      ZERO,ERRNUMB
.
          OPEN      ICDCLTXT,FNAMEI
POST1000  READ      ICDCLTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005
          GOTO      POST9000 IF OVER
.
          UNPACK    SP70,XCLAIM,XHFUND,XHFTAB,XICDCD,XSCLAS
          PACK      XCLAIM,XFLD0001,SP70
          PACK      XHFUND,XFLD0002,SP70
          PACK      XHFTAB,XFLD0003,SP70
          PACK      XICDCD,XFLD0004,SP70
          PACK      XSCLAS,XFLD0005,SP70
          CALL      CRET0000              * Check for carriage return
.
          PACK      KEY29,XCLAIM,XHFUND,XHFTAB,XICDCD,XSCLAS,SP70
          MATCH     SP70,KEY29
          GOTO      POST1000 IF EQUAL
.
          ADD       ONE,RECNUMB
          CALL      VALC0000              * Validating
          BRANCH    EXIT,POST1000
.
          CALL      UICD0000              * Update ICD10 Sugg.Class Record
          GOTO      POST1000
.
POST9000  CLOSE     ICDCLTXT
.
          PRINT     *N,*1,"Number of Records in txt file : ",RECNUMB:
                    *N,*1,"Number of Records Added       : ",RECWRDT:
                    *N,*1,"Number of Records Updated     : ",RECUPDT:
                    *N,*1,"Number of Errors occured      : ",ERRNUMB
.
POST9999  RETURN
+
.******************************************************************************
.         Check for Carriage return
.******************************************************************************
CRET0000  MOVE      SP70,KEY3
          CLEAR     KEY3
          SQUEEZE   XSCLAS
          RESET     XSCLAS
          GOTO      CRET1200
.
CRET1000  BUMP      XSCLAS
          GOTO      CRET2000 IF EOS
.
CRET1200  MATCH     CRETURN,XSCLAS
          GOTO      CRET2000 IF EQUAL
.
          MOVE      XSCLAS,ANS
          APPEND    ANS,KEY3
          GOTO      CRET1000
.
CRET2000  APPEND    SP70,KEY3
          RESET     KEY3
          MOVE      KEY3,XSCLAS
CRET9999  RETURN
+
.******************************************************************************
.                             VALC0000
.                         Validation
.******************************************************************************
VALC0000  MATCH     SP70,XCLAIM
          GOTO      VALC1000 IF NOT EQUAL
.
          MATCH     SP70,XHFUND
          GOTO      VALC8400 IF EQUAL
          GOTO      VALC2000
.
VALC1000  MOVE      "CL",KEY2
          PACK      KEY5,KEY2,XCLAIM,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,VALC8000            * Invalid claim code
.
          MATCH     SP70,XHFUND
          GOTO      VALC4000 IF EQUAL
.
VALC2000  MATCH     SP70,XHFTAB
          IF        @EQUAL
            MOVE      "0000    ",KEY8
          ELSE
            MOVE      XHFTAB,KEY8
          ENDIF
          PACK      KEY14,XHFUND,KEY8,SP70
          CALL      RDFUND1
          BRANCH    OVRCD,VALC8100             * Invalid Health Fund
.
VALC4000  MATCH     SP70,XICDCD
          GOTO      VALC8200 IF EQUAL
.
          MOVE      XICDCD,KEY9
          CALL      RDPTICD1
          BRANCH    OVRCD,VALC4200             * ICD10 code exactly match ?
          GOTO      VALC4500                   * Yes, ICD Code is verified
.
VALC4200  UNPACK    KEY9,KEY3,KEY6
          PACK      KEY9,KEY3,SP6
          MATCH     XICDCD,KEY9                * ICD code group supplied ?
          GOTO      VALC8200 IF NOT EQUAL      * No, Invalid ICD10 code
.
          CALL      RSPTICD1                   * ICD group, Positional read
          CALL      RKPTICD1                   * First ICD code for group
          BRANCH    OVRCD,VALC8500             * No, Invalid ICD10 group
.
          UNPACK    DPCODE,KEY3,KEY6
          PACK      KEY9,KEY3,SP6              * Group code from first ICD
          MATCH     XICDCD,KEY9                * Does group code match ?
          GOTO      VALC8500 IF NOT EQUAL      * No, Invalid ICD10 group
.
VALC4500  MATCH     SP70,XSCLAS
          GOTO      VALC8300 IF EQUAL
.
          MOVE      "A ",KEY2
          PACK      KEY5,KEY2,XSCLAS,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,VALC8300
.
          MOVE      ZERO,EXIT
          GOTO      VALC99999
.
VALC8000  CALL      PREC0000                  * Print record details
          PRINT     *77,"Invalid Claim Code"
          GOTO      VALC9000
.
VALC8100  CALL      PREC0000                  * Print record details
          PRINT     *77,"Invalid Health Fund/Table"
          GOTO      VALC9000
.
VALC8200  CALL      PREC0000                  * Print record details
          PRINT     *77,"Invalid ICD10 Disease Code"
          GOTO      VALC9000
.
VALC8300  CALL      PREC0000                  * Print record details
          PRINT     *77,"Invalid Suggested Classification Code"
          GOTO      VALC9000
.
VALC8400  CALL      PREC0000                  * Print record details
          PRINT     *77,"Claim Code or Health Fund can not be blank"
          GOTO      VALC9000
.
VALC8500  CALL      PREC0000                  * Print record details
          PRINT     *77,"Invalid ICD Disease Code Group"
          GOTO      VALC9000
.
VALC9000  MOVE      ONE,EXIT
          ADD       ONE,ERRNUMB
VALC9999  RETURN
+
.----------------------------------------------------------------------
. Subroutine to post ICD10 Suggested Class.
.----------------------------------------------------------------------
UICD0000  PACK      KEY26,XCLAIM,XHFUND,XHFTAB,XICDCD,SP70
          CALL      RDPMSGA1
          IF        OVRCD=1
            UNPACK    KEY26,PMSGCLMN,PMSGHFND,PMSGHFTB,PMSGICDC
            MOVE      XSCLAS,PMSGCLSS
            MOVE      SP70,PMSGSPAR
            CALL      WRPMSGA1
            ADD       ONE,RECWRDT
          ELSE
            MOVE      XSCLAS,PMSGCLSS
            CALL      UPPMSGA1
            ADD       ONE,RECUPDT
          ENDIF
UICD9999  RETURN
+
.******************************************************************************
.         Print record
.******************************************************************************
PREC0000  PRINT     *1,XCLAIM,*8,XHFUND,"/",XHFTAB,*28,XICDCD,*40,XSCLAS;
PREC9999  RETURN
+
.******************************************************************************
.*                                  PRHD0000                                  *
.*                        Print the selection details                         *
.******************************************************************************
PRHD0000  CALL      HEAD132
          PRINT     *N,"Date               : ",CDATE:
                    *N,"Time               : ",CTIMEIS:
                    *N,"User ID            : ",USERID:
                    *N,"ICD10 Load file    : ",PATHNAME:
                    *N,*N,*1,"Claim Code",*8,"Health Fund/Table":
                    *28,"ICD10 Code",*40,"Suggested Class.":
                    *58,"Error Description":
                    *N,*1,"----------",*8,"-----------------":
                    *28,"----------",*40,"---------------":
                    *58,"---------------------------------"
PRHD9999  RETURN
+
.******************************************************************************
.         Execute us1 command before processing
.******************************************************************************
EXCUS000  CLEAR     CMDLINE
          APPEND    "ibaadt06.us1 ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
EXCUS999  RETURN
+
.******************************************************************************
          INC       STD001IO
.
          INC       PATCODIO/INC            * Codes file
          INC       PMSSGAIO/INC
          INC       PATFN1IO/INC
          INC       PATICDIO/INC
.
.
.
.

.******************************************************************************
.* System         : Patient Management                                        *
.* Program        : IBABIL16.PBL                                              *
.* Name           : Theatre Fee Master Listing                                *
.******************************************************************************
.* Date           : 04/02/1995                                                *
.* Author         : Jeni Tan                                                  *
.* System         : Print the theatre fee file for a claim code & health fund *
.* Modifications  :                                                           *
.*        V10.02.01 28/03/2011 Peter Vela CAR 233048                          *
.*                  Added contract id displays                                *
.*        V10.01.02 02/02/2011 Peter Vela CAR 233048                          *
.*                  Added Johns Hopkins functionality                         *
.*        V10.01.01 13/01/2011 J.Tan      CAR 233048                          *
.*                  Mods PATTFEFD to use HF Table type                        *
.*        V9.05.01  14/03/2006 Peter Vela CAR 97898                           *
.*                  Removed $ sign from fee columns, added to heading         *
.*        V9.04.01  03/08/05 J.Tan    CAR 62750                               *
.*                  Removed redefined amount variables                        *
.*        V5.08.03  27/10/2000  Dean Cramer   SRF 6710                        *
.*                  Recomplied for PATFNDDS via PATFNDKY                      *
.*                  Changed for health fund table code size increase to       *
.*                  8 characters                                              *
.*        V5.08.02  16/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.01  06/06/2000  Jill Habasque                                 *
.*                  Changed from key10 to key14 for the health fund file      *
.*                       V3.02 05/09/88 G.Williams                            *
.*                       Add Day Case rates & theatre bnds                    *
.*        V5.01.01  05/07/89 S. O'Gorman                                      *
.*                  Converted for N.Z.                                        *
.*        V5.01.02  26/09/89 Graeme Williams     SRF 102442                   *
.*                  Fix H.F range option                                      *
.*        V5.01.03  23/05/90 J.Tan               SRF 104901                   *
.*                  Mods PATFN1FD                                             *
.*        V5.01.04  22/02/94 Rob Leonard         SRF 127990                   *
.*                  Changed open routine for patfn1af                         *
.*        V5.03.01  23/11/1995  Greg Horvat      SRF 612530                   *
.*                  Rewritten to standards                                    *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
.
          INC       PATCODFD/INC            * Codes file
          INC       PATCONFD/INC            * Control file
          INC       PATFN1FD/INC            * Health fund file
          INC       PATTFEFD/INC            * Theatre fee file
.         INC       JHSTFEFD/INC            * Johns Hopkins Theatre Fee File
          INC       PMSCIDFD/INC
.
. ----- Program Variables -----
.
CODE      DIM       2
.
DSPCLCOD  DIM       19                      * Display claim code
DSPDYCSE  DIM       16                      * Display day case
DSPEDCLM  DIM       20                      * Display ending claim code
DSPEDFND  DIM       20                      * Display ending health fund
DSPHFTAB  DIM       19                      * Display health fund table
DSPHLFND  DIM       19                      * Display health fund
DSPSTCLM  DIM       20                      * Display starting claim code
DSPSTFND  DIM       20                      * Display starting haelth fund
.
ENDCLM    DIM       3                       * Ending claim code
ENDFND    DIM       6                       * Ending health fund
ENDCONT   DIM       6
FRCONT    DIM       6
FORM62    FORM      6.2
.
STRTCLM   DIM       3                       * Starting claim code
STRTFND   DIM       6                       * Starting health fund
STRCONT   DIM       6
TOCONT    DIM       6
.
. ----- Program Constants -----
.
CODECL    INIT      "CL"
FINISH    INIT      "Finish"
.
OPTION1   INIT      " - Current File "
OPTION2   INIT      " - New File "
.
START     INIT      "Start"
ZED20     INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBABIL16"
PRGNAM    INIT      "Theatre Fee Master Listing"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  MAINLINE                                  *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialize variables & open files
ML1000    CALL      OPTN0000                * Select main option
          BRANCH    OPTION,ML2000,ML2000
          GOTO      ML9999
.
ML2000    CALL      KCLM0000                * Keyin the claim code
          BRANCH    EXIT,ML1000
.
ML3000    CALL      KFND0000                * Keyin the health fund
          BRANCH    EXIT,ML2000
.
ML3500    IF        CFEETYP<>9
            CALL      KEYN0000
            BRANCH    EXIT,ML3000
          ENDIF
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML4000,ML3000,ML1000
.                         Yes    No     Cancel
.
ML4000    CALL      PROC0000                * Process the theatre fee file
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                      Initialize Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN		    PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"pmscidaf";
          OPEN      PMSCIDA1,"pmscidaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TEN9;*212,CFEETYP
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                             Select Main Option                             *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          MOVE      ZERO,CPAGENO
          CLOSE     PATTFEE1
          HLINE     *G37,2,53,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Code Sequence":
                    *P1:6,*V2LON,"2",*HOFF," = Code Sequence (New File)":
                    *P1:8,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*EL,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN2000,OPTN3000
.
          BEEP
          GOTO      OPTN1000
.
OPTN2000  DISPLAY   *P52:2,*V2LON,OPTION1;
          GOTO      OPTN9000
.
OPTN3000  DISPLAY   *P52:2,*V2LON,OPTION2;
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  KCLM0000              Called by: ML0000   *
.*                            Enter The Claim Code                            *
.******************************************************************************
KCLM0000  DISPLAY   *P1:10,"Starting Claim Code  :",*EF:
                    *P1:11,"Ending   Claim Code  :";
          MOVE      "24",ECOL
          MOVE      CODECL,CODE
.
. ----- Starting Claim Code -----
.
KCLM1000  MOVE      "10",EVERT
          DISPLAY   *PECOL:EVERT,*EL;
          MOVE      SP3,CKYIDEF3            * No default
          MOVE      ZERO,CKYIMAND           * Keyin not mandatory
          MOVE      ONE,CNEWDS              * Don't return for keyin
          CALL      PATCODKY                * Keyin the claim code
          BRANCH    EXIT,KCLM2000,KCLM9500
.                        Space    Exit
.
          MOVE      ACODE,STRTCLM
          MOVE      TDESC,DSPSTCLM
          DISPLAY   *P24:EVERT,*EL,*V2LON,STRTCLM,*P31:EVERT,*HOFF,DSPSTCLM;
          GOTO      KCLM3000
.
KCLM2000  MOVE      ACODE,STRTCLM
          PACK      DSPSTCLM,START,SP20
          DISPLAY   *P24:EVERT,*EL,*V2LON,DSPSTCLM;
.
. ----- Ending Claim Code -----
.
KCLM3000  MOVE      "11",EVERT
          DISPLAY   *PECOL:EVERT,*EL;
          MOVE      STRTCLM,CKYIDEF3        * Default
          MOVE      ZERO,CKYIMAND           * Keyin not mandatory
          MOVE      ONE,CNEWDS              * Don't return for keyin
          CALL      PATCODKY                * Keyin the claim code
          BRANCH    EXIT,KCLM4000,KCLM1000
.                        Space    Exit
.
          MOVE      ACODE,ENDCLM
          MOVE      TDESC,DSPEDCLM
          DISPLAY   *P24:EVERT,*EL,*V2LON,ENDCLM,*P31:EVERT,*HOFF,DSPEDCLM;
          GOTO      KCLM5000
.
KCLM4000  MOVE      "zzz",ENDCLM
          PACK      DSPEDCLM,FINISH,SP20
          DISPLAY   *P24:EVERT,*EL,*V2LON,DSPEDCLM;
.
KCLM5000  MATCH     STRTCLM,ENDCLM
          GOTO      KCLM9000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Ending claim code must be after starting ":
                                  "claim code. ";
          CALL       HITENTER
          GOTO       KCLM3000
.
KCLM9000  MOVE      ZERO,EXIT
          GOTO      KCLM9999
.
KCLM9500  MOVE      ONE,EXIT
KCLM9999  RETURN
+
.******************************************************************************
.*                                  KFND0000              Called by: ML0000   *
.*                            Enter The Health Fund                           *
.******************************************************************************
KFND0000  DISPLAY   *P1:13,"Starting Health Fund :",*EF:
                    *P1:14,"Ending   Health Fund :";
          MOVE      "24",ECOL
.
. ----- Starting Health Fund -----
.
KFND1000  MOVE      "13",EVERT
          DISPLAY   *PECOL:EVERT,*EL;
          MOVE      SP6,CKYIDEF6            * No default
          MOVE      ZERO,CKYIMAND           * Keyin not mandatory
          MOVE      ONE,CNEWDS              * Don't return for keyin
          CALL      PATFNDKY                * Keyin the health fund
          BRANCH    EXIT,KFND2000,KFND9500
.                        Space    Exit
.
          MOVE      HCODE,STRTFND
          MOVE      HFNAME,DSPSTFND
          DISPLAY   *P24:EVERT,*EL,*V2LON,STRTFND,*P31:EVERT,*HOFF,DSPSTFND;
          GOTO      KFND3000
.
KFND2000  MOVE      HCODE,STRTFND
          PACK      DSPSTFND,START,SP20
          DISPLAY   *P24:EVERT,*EL,*V2LON,DSPSTFND;
.
. ----- Ending Health Fund -----
.
KFND3000  MOVE      "14",EVERT
          DISPLAY   *PECOL:EVERT,*EL;
          MOVE      STRTFND,CKYIDEF6        * Default
          MOVE      ZERO,CKYIMAND           * Keyin not mandatory
          MOVE      ONE,CNEWDS              * Don't return for keyin
          CALL      PATFNDKY                * Keyin the health fund
          BRANCH    EXIT,KFND4000,KFND1000
.                        Space    Exit
.
          MOVE      HCODE,ENDFND
          MOVE      HFNAME,DSPEDFND
          DISPLAY   *P24:EVERT,*EL,*V2LON,ENDFND,*P31:EVERT,*HOFF,DSPEDFND;
          GOTO      KFND5000
.
KFND4000  MOVE      "zzzzzz",ENDFND
          PACK      DSPEDFND,FINISH,SP20
          DISPLAY   *P24:EVERT,*EL,*V2LON,DSPEDFND;
.
KFND5000  MATCH     STRTFND,ENDFND
          GOTO      KFND9000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Ending health fund must be after starting ":
                                  "health fund. ";
          CALL       HITENTER
          GOTO       KFND3000
.
KFND9000  MOVE      ZERO,EXIT
          GOTO      KFND9999
.
KFND9500  MOVE      ONE,EXIT
KFND9999  RETURN
+
.******************************************************************************
.*                                  PROC0000              Called by: ML0000   *
.*                        Process The Theatre Fee File                        *
.******************************************************************************
PROC0000  BRANCH    OPTION,PROC1000,PROC2000
.                          Cur file New file
.
PROC1000  IF        CFEETYP=9
            OPEN      JHSTFEA1,"jhstfeaf"
          ELSE
            OPEN      PATTFEE1,"pattfeef"    * Open the current theatre fee file
          ENDIF 
          GOTO      PROC3000
.
PROC2000  IF        CFEETYP=9
.
            TRAP      OVERCOND IF IO
            OPEN      JHSTFEA1,"newtfeaf"
            TRAPCLR   IO
            IF        OVRCD=1
              DISPLAY   *P1:24,*EL,*B,"New JHS theatre fee file doesn't exist.";
              CALL      HITENTER
              GOTO      PROC9500
            ENDIF
.
          ELSE
.
            TRAP      OVERCOND IF IO
            OPEN      PATTFEE1,"newtfeef"     * Open the new theatre fee file
            TRAPCLR   IO
            IF        OVRCD=1
              DISPLAY   *P1:24,*EL,*B,"New theatre fee file doesn't exist.";
              CALL      HITENTER
              GOTO      PROC9500
            ENDIF
.
          ENDIF
.
PROC3000  DISPLAY   *P1:24,*EL,"Claim Code : ",*P20:24,"Health Fund : ";
          CLOCK     TIME,CTIMEIS
          MOVE      "60",CLNO               * Force a print header
.
          IF        CFEETYP=9
            PACK      KEY23,STRTCLM,STRTFND,SP20
            CALL      RSJHTFE1                * Position on & read the theatre
          ELSE
            PACK      KEY24,STRTCLM,STRTFND,SP20
            CALL      RDSTFEE1                * Position on & read the theatre
          ENDIF
.
PROC4000  IF        CFEETYP=9
            CALL      RKJHTFE1
          ELSE
            CALL      RDKTFEE1                  fee file
          ENDIF
          BRANCH    OVRCD,PROC7000
.
          MATCH     TFCLM,ENDCLM
          GOTO      PROC7000 IF LESS        * Claim code out of range
.
          MATCH     STRTFND,TFHFUND
          GOTO      PROC5000 IF LESS        * Repos at the start fund for CL
.
          MATCH     TFHFUND,ENDFND
          GOTO      PROC6000 IF LESS        * Repos at the end fund for CL
.
          IF        CFEETYP<>9
            MATCH     SP70,STRCONT
            IF        !@EQUAL & !@EOS
              MATCH     STRCONT,PTTFCNID
              GOTO      PROC4000 IF LESS
            ENDIF
.
            MATCH     SP70,ENDCONT
            IF        !@EQUAL & !@EOS
              MATCH     PTTFCNID,ENDCONT
              GOTO      PROC4000 IF LESS
            ENDIF
          ENDIF
.
          CALL      PRNT0000                * Print the report
          GOTO      PROC4000
.
PROC5000  IF        CFEETYP=9
            PACK      KEY23,TFCLM,STRTFND,SP30 
            CALL      RDJHTFE1
          ELSE 
            PACK      KEY24,TFCLM,STRTFND,SP30
            CALL      RDSTFEE1                * Position on the theatre fee file
          ENDIF
          GOTO      PROC4000
.
PROC6000  IF        CFEETYP=9
            PACK      KEY23,TFCLM,ZED20
            CALL      RDJHTFE1
          ELSE
            PACK      KEY24,TFCLM,ZED20
            CALL      RDSTFEE1                * Position on the theatre fee file
          ENDIF
          GOTO      PROC4000
.
. ----- Finished -----
.
PROC7000  IF        CPAGENO=0
            DISPLAY   *P1:24,*EL,*B,"No data on the theatre fee file for ":
                                    "claim code & health fund. ";
            CALL      HITENTER
            GOTO      PROC9500
          ENDIF
.
          CALL      UND132                  * Print an underline
          PRINT     *N,"*** End of Report ***"
          DISPLAY   *P1:24,*EL,"Report Generation Complete. ";
          CALL      HITENTER
          MOVE      ZERO,EXIT
          GOTO      PROC9999
.
PROC9500  MOVE      ONE,EXIT
PROC9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: PROC0000 *
.*                              Print The Report                              *
.******************************************************************************
PRNT0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * Start a new page
.
          MOVE      SP30,DSPCLCOD
          MOVE      SP30,DSPHLFND
          MOVE      SP30,DSPHFTAB
          MOVE      SP30,DSPDYCSE
.
          MATCH     "D",TFDAYC
          GOTO      PRNT1000 IF NOT EQUAL
.
          MOVE      " - DAY CASE RATE",DSPDYCSE
PRNT1000  PACK      KEY5,CODECL,TFCLM
          CALL      RDCODE1                 * Get the claim code description
.
          MOVE      TDESC,DSPCLCOD
          PACK      KEY14,TFHFUND,ZERO,ZERO,ZERO,ZERO,SP10
          CALL      RDFUND1                 * Get the health fund
          BRANCH    OVRCD,PRNT2000
          MOVE      HFNAME,DSPHLFND
.
          IF        CFEETYP=9
            PACK      KEY14,TFHFUND,TFHFTAB,SP70
            CALL      RDFUND1
            MOVE      HFNAME,DSPHFTAB
          ELSE
            PACK      KEY5,ANSH,ANST,PTTFHTYP * Health fund table type
            CALL      RDCODE1
            MOVE      TDESC,DSPHFTAB
          ENDIF
.
PRNT2000  MOVE      TFPAT1,FORM62
          PRINT     *1,"!",DSPCLCOD,*21,"!",DSPHLFND,*41,"!",DSPHFTAB:
                    *61,"!",TFENDMBS,*68,"!",FORM62;
          MOVE      TFPAT2,FORM62
          PRINT     *78,"!",FORM62;
.
          MOVE      TFPAT3,FORM62
          PRINT     *88,"!",FORM62;
.
          MOVE      TFPAT4,FORM62
          PRINT     *98,"!",FORM62;
.
          MOVE      TFPAT5,FORM62
          PRINT     *108,"!",FORM62;
.
          MOVE      TFPAT6,FORM62
          PRINT     *118,"!",FORM62,*128,"! ",TFNINV,*132,"!",*N:
                    *1,"!",PTTFCNID,*21,"!",*41,"!",DSPDYCSE:
                    *61,"!";
.
          MOVE      TFHF1,FORM62
          PRINT     *68,"!",FORM62;
.
          MOVE      TFHF2,FORM62
          PRINT     *78,"!",FORM62;
.
          MOVE      TFHF3,FORM62
          PRINT     *88,"!",FORM62;
.
          MOVE      TFHF4,FORM62
          PRINT     *98,"!",FORM62;
.
          MOVE      TFHF5,FORM62
          PRINT     *108,"!",FORM62;
.
          MOVE      TFHF6,FORM62
          PRINT     *118,"!",FORM62,*128,"!",*132,"!"
          CALL      UND132                  * Print an underline
          ADD       "3",CLNO
          DISPLAY   *P14:24,*V2LON,TFCLM,*P34:24,TFHFUND;
PRNT9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: PRNT0000 *
.*                            Print Report Heading                            *
.******************************************************************************
HEAD0000  MOVE      SP30,CPHDROPT
          LOAD      CPHDROPT,OPTION,OPTION1,OPTION2
          CALL      HEAD132
          PRINT     *N:
                    *40,"Claim Code Range  : ",DSPSTCLM," to ",DSPEDCLM,*N:
                    *40,"Health Fund Range : ",DSPSTFND," to ",DSPEDFND,*N:
                    *40,"Contract ID Range : ",FRCONT," to ",TOCONT,*N
          ADD       "5",CLNO
          CALL      UND132                  * Print an underline
          PRINT     *1,"!Claim Code",*21,"!Health Fund":
                    *41,"!Health Fund Table",*61,"!MBS or":
                    *68,"!  First",*78,"!  Second",*88,"!  Third":
                    *98,"!  Fourth",*108,"!  Fifth",*118,"!  Sixth":
                    *128,"!No.!",*N:
                    *1,"!Contract ID",*21,"!",*41,"!",*61,"!T.Cls":
                    *68,"! Pat/Reb",*78,"! Pat/Reb",*88,"! Pat/Reb":
                    *98,"! Pat/Reb",*108,"! Pat/Reb",*118,"! Pat/Reb":
                    *128,"!Inv",*132,"!"
          PRINT     *1,"!",*21,"!",*41,"!",*61,"!":
                    *68,"!    $   ",*78,"!    $   ",*88,"!    $   ":
                    *98,"!    $   ",*108,"!    $   ",*118,"!    $   ":
                    *128,"!",*132,"!"
          ADD       "3",CLNO
          CALL      UND132                  * Print an underline
HEAD9999  RETURN
+
.**************************************************************************
.                               KEYN0000                                  *
.  Keyin selection details for contract Id                                *
.**************************************************************************
KEYN0000  UNPACK    SP70,FRCONT,TOCONT,STRCONT,ENDCONT

          DISPLAY   *P1:22,*EF:
                    *P1:22,"Contract Id From : ":
                    *P1:23,"Contract Id To   : ";
.
. ------  keyin the contract Id
.
          KEYIN     *P24:22,FRCONT
KEYN1500  PACK      FRCONT,FRCONT,SP70
          MOVE      FRCONT,STRCONT
.
          MATCH     SP70,FRCONT
          GOTO      KEYN1000 IF EQUAL
.
          PACK      KEY6,FRCONT,SP70
          CALL      RDPMCID1
          BRANCH    OVRCD,KEYN9000
.
          DISPLAY   *P31:22,PMCIDESC
          GOTO      KEYN5000
.
KEYN1000  DISPLAY   *P31:22,*V2LON,"Start",*HOFF;
          MOVE      "Start",FRCONT
          MOVE      ZERO,EXIT
.
KEYN5000  KEYIN     *P24:23,TOCONT
          PACK      TOCONT,TOCONT,SP70
          MOVE      TOCONT,ENDCONT
.
          MATCH     SP70,TOCONT
          GOTO      KEYN6000 IF EQUAL
.
          PACK      KEY6,TOCONT,SP70
          CALL      RDPMCID1
          BRANCH    OVRCD,KEYN9000
.
          DISPLAY   *P31:23,PMCIDESC
          GOTO      KEYN7000
.
KEYN6000  DISPLAY   *P31:23,*V2LON,"End",*HOFF;
          MOVE      "End  ",TOCONT
          MOVE      ZERO,EXIT
          GOTO      KEYN9999
.
KEYN7000  PACK      STRCONT,STRCONT,SP10
          PACK      ENDCONT,ENDCONT,SP10
          MATCH     STRCONT,ENDCONT
          GOTO      KEYN9999 IF NOT LESS
.
          DISPLAY   *P1:24,"To contract must be greater than from contract. ";
          CALL      HITENTER
          GOTO      KEYN5000
.
KEYN9000  MOVE      ONE,EXIT
.
KEYN9999  RETURN
+
.==============================================================================
.
          INC       STD001IO
.
          INC       PATCODKY                * Keyin the claim code
          INC       PATFNDKY                * Keyin the health fund
.
          INC       PATCODIO/INC            * Codes file
          INC       PATFN1IO/INC            * Health fund file
          INC       PATTFEIO/INC            * Theatre fee file
.         INC       JHSTFEIO/INC
          INC       PMSCIDIO/INC
+
.


.*******************************************************************************
.*                                                                             *
.*    Title        : FXRESAUD.PBL                                              *
.*                                                                             *
.*    Description  : Create New Yearly tables from resaudaf.dat                *
.*                                                                             *
.*    Modification : V9.03.00 30/01/2004 Peter Vela     CAR 38290              *
.*                            Created Program                                  *
.*                                                                             *
.*******************************************************************************.
          INC       STD001FD
          INC       RESAUDFD/INC
.
CMDLINE   DIM       100
DIM4      DIM       4
DIM4A     DIM       4
RECNUM    FORM      8
NEWFILE   DIM       60
.
PRGNAM    INIT      "Create Yearly tables from resaudaf"
PRGID     INIT      "FXRESAUD"
VERSION   INIT      "V12.01.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999
.
MAIN1000  CALL      READ0000
.
MAIN9999  CHAIN     PGM
+
.*******************************************************************************
.*                                 INIT0000                                    *
.*                 Display Heading and Initialise Record Count                 *
.*******************************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
.
INIT9999  RETURN
+
.*******************************************************************************
.*                                 READ0000                                    *
.*******************************************************************************
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          OPEN      RESAUDA1,"resaudaf"
          PACK      KEY40,SP70
          CALL      RSREAU1
          GOTO      READ1050
.
READ1000  OPEN      RESAUDA1,"resaudaf"
          CALL      RDREAU1
READ1050  CALL      RKREAU1
          BRANCH    OVRCD,READ9000
.
          PACK      KEY40,REAURDT,REAURTM,REAURUN,REAUSDT,REAUSTM,REAUUID,SP70
.
          ADD       ONE,RECNUM
          IF        RECNUM%1000=0
            CLOCK     TIME,CTIMEIS
            DISPLAY   *P1:23,"Records ",RECNUM," at ",CTIMEIS
          ENDIF
.
          UNPACK    REAURDT,DIM4,DIM4A
          CLEAR     NEWFILE
          APPEND    "reau",NEWFILE
          APPEND    DIM4,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESAUDA1,NEWFILE
          TRAPCLR   IO
.
          COMPARE   ZERO,OVRCD
          GOTO      READ2000 IF EQUAL
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " 100 UC1-8,9-13,14-17,18-25,26-30,31-40",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      RESAUDA1,NEWFILE
.
READ2000  CALL      RAREAU1
          COMPARE   ONE,OVRCD
          IF        @EQUAL
            CALL      WRREAU1
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ9000  DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
          CALL      HITENTER
.
READ9999  RETURN
+
          INC       RESAUDIO/INC
          INC       STD001IO

. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAHOS05                                            *
. *                                                                           *
.*      FUNCTION:         CURRENT PATIENT MISC. ALLOCATION                    *
.*                        This program writes a Misc.Charge (a pmsmtiaf       *
.*                        Transaction Detail record) to all Patients in the   *
.*                        selected ward for the Misc.Charge selected.         *
. *                                                                           *
. *     AUTHOR:           JENI TAN   (I.B.A)                                  *
. *                                                                           *
. *     DATE WRITTEN:     25/05/88                                            *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
.******************************************************************************
. * V12.00.01 21/05/2025  Bella Turco    TSK 0955096                          *
. *           Alphanumeric changes                                            *
. * V11.05.01 29/11/2024  Thanh T        TSK 0939466                          *
. *           Added Distance in kms field                                     *
.******************************************************************************
. *       V11.02.01 24/01/2022  J.Tan           TSK 0911322                   *
. *                 Fixed checking hospital id for All Ward Option            *
. *       V11.01.01 18/02/2021  J.Tan           TSK 0888639                   *
. *                 Added PMMITMNO - Theatre Team no                          *
. *       V10.13.03 23/10/2018  Tracey Nguyen    TSK 0859743                  *
. *                 Added PMMIHOSI - Hopsital Indicator on PMSMTIFD           *
. *       V10.13.02 19/09/2018  J.Tan            TSK 0863727                  *
. *                 Added Restrictive Override code                           *
. *       V10.13.01 21/08/2017  J.Tan        TSK 0859742                      *
. *                 Added ECLIPSE new fields from pmsmtiaf                    *
. *       V10.11.01 31/08/2017  J.Tan         TSK 0837479                     *
. *                 Added PMMICTYP - Consumption Type                         *
. *       V10.05.01 05/08/2014  Peter Vela    CAR 302164                      *
. *                 Added PMMIRBRS - Rebate Reason CAT Rr                     *
. *       V10.04.02 16/04/2014  J.Tan         CAR 261630                      *
. *                 Removed pataum audit files                                *
. *       V10.04.01 17/01/2014  J.Tan        CAR 295008                       *
. *                 Mods to set PMMIDTCR,PMMITMCR,PMMIIDCR                    *
. *                 20/01/2014  J.Tan        CAR 249828                       *
. *                 Added PMMIPMBS - Patient MBS Team and Counter             *
. *       V10.03.01 24/05/2012  J.Tan         CAR 264599                      *
. *                 Mods to set pmmiserv (quantity) to one instead of zero    *
. *       V10.01.01 20/12/2010  Mike Laming   CAR 233046                      *
. *                 Mods to Misc.Charges PATMCHFD - Key change, logic changes *
. *       V10.00.02 18/08/2010 J.Tan         CAR 222031                       *
. *                 Mods to set PMMIDUPD and PMMITUPD                         *
. *       V10.00.01 30/04/2010  J.Tan     CAR 220887                          *
. *                 Mods checking for Active/Inactive of Misc.charge          *
. *       V9.08.01  07/05/2007 Peter Vela CAR SJOG ED Billing                 *
. *                 Initialised new pmsmtiaf variables                        *
. *       V9.04.01  05/10/2004  Lina Vo        CAR 53798                      *
. *                 Moved Hospital Field keyin before ward and misc charge.   *
. *       V9.04.00  03/09/2004  Ebon Clements  CAR 53110                      *
. *                 Ported from 5.12                                          *
. *       V5.09.01  02/07/2001  Caleb Sharman  SRF 10970                      *
. *                 Recompiled for PATWRDDS                                   *
. *       V5.08.02  06/12/2000  Caleb Sharman                                 *
. *                 Fixed display bug                                         *
. *       V5.08.01  16/08/2000  Caleb Sharman                                 *
. *                 Changed Health Fund variables to be 8 chars               *
. *                   V5.08.B03 24/03/2000  Jill Habasque                     *
. *                             Added ? search for ward and misc item         *
. *                   V5.08.B01 18/02/2000  Steve Downing                     *
. *                             Mods to initialise GST variables              *
. *****************************************************************************
. *                   V5.07.B01 17/11/1998  Jim Stathopoulos                  *
. *                             507 Changes                                   *
. *****************************************************************************
. *                       V3.01 05/08/88 G.Williams (I.B.A)                   *
. *                             Eliminate 1st index of DTRA                   *
. *                       V5.01 13/04/89 Graeme Williams                      *
. *                             New index added to Dtrans                     *
. *                             SRF 5645                                      *
. *                       V5.02 23/05/89 D.Di.Paola                           *
. *                             Alter screen display to                       *
. *                             allow "8" digit Adm. No.                      *
. *                             Put in STD001FD..                             *
. *                       V5.03 27/05/89 J.Tan                                *
. *                             Fixed Mother Adm. to form8                    *
. *                             (PATMI1FD).     SRF 100750                    *
. *                       V5.04 06/06/89 J.Tan                                *
. *                             Mods Locking master file                      *
. *                             (PATIOMAS)     SRF 100809                     *
. *                       V5.05 17.06.89 S.Barcham                            *
. *                             Compiled for new standbys                     *
. *                             SRF 101101                                    *
. *                    V5.01.01 04/07/89 M.Ohleiter                           *
. *                             Changes for 8 char ur and                     *
. *                             admiss - release 5.01                         *
. *                   V5.01.121 27/11/90 Glen Hobby                           *
. *                            Recompiled for changes to                      *
. *                            PATMX1FD                                       *
. *       V5.01.122 12/06/92  Graeme Williams               SRF 114865        *
. *                 Changes for 9 character misc items                        *
. *       V5.02.01  20/02/1995  Greg Horvat      SRF 134879                   *
. *                 Recompiled for PATAUMFD & PATIOAUM - added new variable & *
. *                 changed to write to this new variable                     *
. *                                                                           *
. ******************************************************************************
.
          INC       STD001FD 
.
          INC       IBAPASFD/INC
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC

          INC       PATMCHFD/INC
          INC       PATDSCFD/INC
. 
.         INC       PATDTRFD/INC
          INC       PMSMTIFD/INC
.
          INC       PATTRNFD/INC
          INC       PATVISFD/INC
          INC       PATWR1FD/INC
          INC       IBACONFD/INC
          INC       PATHSPFD/INC
          INC       PMSVX1FD/INC
.
+
CLAIM     DIM       3            * vars for PATMCHKY
CURRDATE    DIM       8
DIM8      DIM       8
HFUND     DIM       6
HFTAB     DIM       8
SAVCHG    DIM       9
CLMFLG    FORM      1
FNDFLG    FORM      1
CMCNOHF   FORM      1  
BATAMTM   FORM      8.2
KEY21A    DIM       21
KEY26A    DIM       26
.
FNAMEB    DIM        8
HEADSEC   FORM       5
.
OPTION2   FORM      2
ONAME     DIM       30
OPERCODE  DIM       4
.
PTCNDCLM  DIM       3
.
SEC1      FORM    "00001"
SAVTRNO   FORM    3
SP12      INIT    "            "
SP14      INIT    "              "
.
TIMEIS     DIM     8
WARDFLG    FORM    "0"
.
PRGID     INIT      "IBAHOS05"
PRGNAM    INIT      "CHARGE PATIENTS WITH MISCELLANEOUS ITEM"
VERSION   INIT      "V12.01.00"
+
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.         OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmchgf";
          OPEN	    PATMCHG1,"patmchgf"
.
...       DISPLAY   *P64:24,"patdtraf";
...       OPEN	    PATDTRA1,"patdtraf"
          DISPLAY   *P64:24,"pmsmtiaf";
          OPEN	    PMSMTIA1,"pmsmtiaf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN 	    PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
.
          DISPLAY   *P64:24,"ihapassf";
          CALL       OPPASS1
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
          READ      CONTROLF,TEN9;*215,CMCNOHF
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          PACK      CURRDATE WITH CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          MATCH     SP3,PTCNDCLM
          IF        @EQUAL
            MOVE      "0  ",PTCNDCLM
          ENDIF
.
.        START OF PROGRAM
.             SELECT OPTION
.
START     HLINE         *G37,2,59,80 
          DISPLAY       *P1:3,*EF:
                        *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                        *P1:5,*V2LON,ONE,*HOFF," = By Misc.Charge Code"
.
          KEYIN         *P1:7,*EL,"Option : ",*V2LON,OPTION;
.
          COMPARE       ZERO,OPTION
          GOTO          ABORT IF EQUAL
+
.
.       SELECT WHICH WARD/BED OPTION
.
KOPTION   DISPLAY   *P1:4,*EF,*ULON,"Ward Option",*HOFF:
                    *P1:6,*V2LON,ONE,*HOFF," = Particular Ward":
                    *P1:7,*V2LON,TWO,*HOFF," = All Wards"
.
OPT2      KEYIN     *P1:9,"Option : ",*EL,*V2LON,OPTION2
.
          COMPARE   ZERO,OPTION2
          GOTO      START IF EQUAL
.
          MOVE      ZERO,WARDFLG
          BRANCH    OPTION2 OF HOSP0000,HOSP0000
          BEEP
          GOTO      KOPTION
+
HOSP0000  CALL      KHOS0000                   * Keyin hospital
          BRANCH    EXIT,OPT2 
          MOVE      ZERO,WARDFLG
          BRANCH    OPTION2 OF KWARD,KITEM
.
.
.               PRINT VIA WARD NUMBER
.
KWARD   DISPLAY   *P1:11,*EF,"Enter Ward Number  : ";
.
KWARD1  MOVE      TWENTY3,ECOL
        MOVE      TEN1,EVERT
        CALL      PATWRDKY
        IF        EXIT=1
          GOTO      KOPTION
        ENDIF
.       MOVE      SP3,SWWARD
.       KEYIN     *P23:11,*EL,*V2LON,SWWARD
        MOVE      WWARD,SWWARD
        RESET     SWWARD
        GOTO      KOPTION IF EOS
.
        PACK      KEY6 WITH SWWARD,SP6
        CALL      RDWARD1
        BRANCH    OVRCD OF INVWARD
.
        DISPLAY   *P40:11,*V2LON,WBDESC;
        MOVE      ONE,WARDFLG
.
.
.       Keyin  miscellaneous item code
.
KITEM   DISPLAY   *P1:13,*EL,"Miscellaneous Item : ";
.
KITEM1  MOVE      PTCNDCLM,CLAIM
        IF        IBCNMHOS=1
          MATCH     SP10,PTHSDCLM
          IF        !@EQUAL
            MOVE      PTHSDCLM,CLAIM
          ENDIF
        ENDIF
        MOVE      SP10,HFUND
        MOVE      SP10,HFTAB
        KEYIN     *P1:13,"Miscellaneous Item :",*EL,*V2LON,MCHARGE
.
        RESET     MCHARGE
        GOTO      KITEM2A IF EOS
        GOTO      KITEM3B
.
KITEM2A BRANCH    OPTION2,KWARD1,KOPTION
        GOTO      KOPTION
.
KITEM3B ENDSET    MCHARGE
        APPEND    SP9,MCHARGE
        RESET     MCHARGE
.
        MOVE      PTCNDCLM,MCLMCOD
        IF        IBCNMHOS=1
          MATCH     SP10,PTHSDCLM
          IF        !@EQUAL
            MOVE      PTHSDCLM,MCLMCOD
          ENDIF
        ENDIF
.
        PACK      KEY29,MCLMCOD,SP6,SP3,MCHARGE,CURRDATE,SP10
        CALL      PATMCHRD                * read Misc.Charges file 
        COMPARE   ZERO,EXIT
        GOTO      INVMISC IF NOT EQUAL
.
KITEM3D DISPLAY   *P31:13,*EL,*P40:13,*V2LON,MDESC;
.
OKCONT   KEYIN     *P1:24,*EL,"Ok to continue (":
                        *V2LON,"Y",*HOFF,*DV,SLASH,*V2LON,"N",*HOFF:
                        ") ? ",*V2LON,ANS;
.
         CMATCH    "N",ANS
         GOTO      KOPTION IF EQUAL
.
         CMATCH    "Y",ANS
         GOTO      PROC00 IF EQUAL
.
         BEEP
         GOTO      OKCONT
.
.       Get the current patient
.
PROC00  DISPLAY     *P1:24,*EL,*P10:24,*V2LON:
                    "** Processing Current Admission **",*W:
                    *P1:24,*EL,*P20:24,"Current Admission No :":
                    *P55:24,"Scanning :";
.
        PACK      KEY9 WITH TWO,SP8
        CALL      RDSMISS2
PROC10  CALL      RDKMISS2
        BRANCH    OVRCD OF PROC20
.
        DISPLAY   *P66:24,AADMNO;
.
        COMPARE   TWO,ASTAT
        GOTO      PROC20 IF NOT EQUAL
        CALL      CHKREC
        GOTO      PROC10
.
.       Get on leave patient
.
PROC20 DISPLAY     *P1:24,*EL,*P10:24,*V2LON:
                    "** Processing On Leave Admission **",*W:
                    *P1:24,*EL,*P20:24,"On Leave Admission No :":
                    *P55:24,"Scanning :";
.
        PACK      KEY9 WITH FOUR,SP8
        CALL      RDSMISS2
PROC25  CALL      RDKMISS2
        BRANCH    OVRCD OF START
.
        DISPLAY   *P66:24,AADMNO;
.
        COMPARE   FOUR,ASTAT
        GOTO      START IF NOT EQUAL
        CALL      CHKREC
        GOTO      PROC25
+
.******************************************************************************
.       Get the master detail
.******************************************************************************
CHKREC    MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF NOMAST
.
.       Check if for all ward or a particular ward only
.
          COMPARE   ZERO,WARDFLG
          GOTO      CHKR40 IF EQUAL
.
.       If only a particular ward, you need to check the ward from tran file
.
          MOVE      SP3,STWARD
          PACK      KEY30 WITH AADMNO,SP30
          CALL      RDSTRAN2
CHKR10    CALL      RDKTRAN2
          BRANCH    OVRCD OF CHKR20
.
          MATCH     TADMN,AADMNO
          GOTO      CHKR20 IF NOT EQUAL
.
          MOVE      TWARD,STWARD
          GOTO      CHKR10
.
CHKR20    MATCH     SP3,STWARD
          GOTO      CHKR99 IF EQUAL
.
          MATCH     STWARD,SWWARD
          GOTO      CHKR99 IF NOT EQUAL
.
CHKR40    COMPARE   ONE,IBCNMHOS         * Using multi hospital
          GOTO      CHKR50 IF NOT EQUAL
.
          MATCH     SP70,PTHSHOSP        * All Hospitals
          GOTO      CHKR50 IF EQUAL
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,CHKR99
.
          MATCH     PMVXMHOS,PTHSHOSP    * Correct hospital
          GOTO      CHKR99 IF NOT EQUAL
.
.         Lock admission record
.
CHKR50    MOVE      AADMNO,KEY8
          CALL      RLPTMIS1
.
          COMPARE   ZERO,OVRCD
          GOTO      CHKR60 IF EQUAL
.
          DISPLAY   *P1:23,*EL:
                    "*** Patient Admission ",*V2LON,AADMNO,*HOFF:
                    " Busy on Terminal ":
                    *V2LON,APORT,*HOFF," ***":
                    *BLINKON," Please Wait",*W5,*P1:23,*EL;
.
          GOTO      CHKR50
.
.         Get the last transaction number
.
CHKR60    MOVE      AADMNO,PMMIVISN
.         MOVE      "P" TO TTYPEDEB
.
          MOVE      PMMIVISN,PVIBILL
          MOVE      PVIBILL,KEY8
          CALL      TRVISA1
          MOVE      PVITRAN,PMMITRAN
          MOVE      PMMITRAN,ATRANNO
          MOVE      PVIURNO,PMMIURNO
          MOVE      PVITYPE,PMMISYST
.
          DISPLAY   *P44:24,*V2LON,AADMNO;
.
          CALL      SETUPTRN
.
          PACK      KEY14 WITH PMMIVISN,PMMITRAN,SP70
          CALL      WRPMMTI1
.
.       Update next transaction number in admission record
.
          ADD       ONE,ATRANNO
          MOVE      ATRANNO,SAVTRNO
          MOVE      AADMNO,KEY8
          CALL      RDMISS1
.
.        Update & Release Admission record
.
          MOVE      SAVTRNO,ATRANNO
          CALL      UPMISS1
CHKR99    RETURN
.
.******************************************************************************
.         Subroutine to setup the DTRAN record ready for posting
.******************************************************************************
SETUPTRN  MOVE      SP2,PMMISESS
          MOVE      MMSCGRP,PMMIMGRP
          MOVE      ZERO,PMMIAMTG
          MOVE      ZERO,PMMIAMTH
          MOVE      ZERO,PMMIAMTP
          MOVE      SP70,PMMIREFN
          MOVE      MITMTYP,PMMIRTYP
          MOVE      ZERO,PMMIMVBR
          MOVE      ONE,PMMIINVN
.
          MOVE      MPATPOR,PMMIAMTP
          MOVE      MPATPOR,PMMIAMTT
          ADD       MHFPOR,PMMIAMTT
          MOVE      MHFPOR,PMMIRBAT
.
          PACK      PMMITDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMMITDAT
.
          MOVE      MCHARGE,PMMIITEM
          MOVE      MDESC,PMMIDESC
          MOVE      SP70,PMMIDES2
          MOVE      SP6,PMMITDOC
          MOVE      SP6,PMMIODOC
          MOVE      ONE,PMMISERV
          MOVE      ZERO,PMMIGSTA
          MOVE      SP6,PMMIGSTC
          MOVE      ZERO,PMMIGSTM
          MOVE      SP70,PMMICNTR
          MOVE      SP70,PMMISUNQ
          MOVE      SP70,PMMIINCT
          MOVE      SP70,PMMISUBN
          MOVE      SP70,PMMIEDAD
          MOVE      SP70,PMMISVTM
          MOVE      SP70,PMMICCON
          MOVE      ZERO,PMMISCHF
          MOVE      SP70,PMMIITYP
          MOVE      SP70,PMMIDUPD
          MOVE      SP70,PMMITUPD
          MOVE      SP70,PMMIWUSR
          PACK      PMMIDTCR,CCC,CYY,CMM,CDD
          REP       " 0",PMMIDTCR
          CALL      IBACLOCK
          MOVE      CTIMEIS,PMMITMCR
          MOVE      PASSCODE,PMMIIDCR
          MOVE      SP70,PMMITMNO
          MOVE      SP70,PMMIPMBS
          MOVE      SP70,PMMIRBRS
          MOVE      SP70,PMMICTYP
          PACK      PMMIACOI,SP70       * After Care Override Indicator
          PACK      PMMIDSOV,SP70       * Duplicate Service Override
          PACK      PMMISTXT,SP70       * Service Text
          PACK      PMMILSPN,SP70       * Location Specific Practice No(LSPN)
          PACK      PMMIMPOV,SP70       * Multi Procedure Override
          PACK      PMMINMPT,SP70       * Number of Patients Seen
          PACK      PMMISDCD,SP70       * Self Deem Code (Cat Sd)
          PACK      PMMITDUR,SP70       * Time Duration (Mins)
          PACK      PMMIROVR,SP70       * Restricted Override Code (Cat Ro)
          PACK      PMMIHOSI,SP70       * Hospital Indicator         *T0859743
.
          MOVE      SP70,PMMIDKSM       * Distance in kms
          MOVE      SP70,PMMISPAR
          RETURN
.
INVWARD  DISPLAY      *P30:11,*EL,*B,*V2LON:
                      "*** Ward does not exist ***",*W2;
         GOTO         KWARD
.
INVMISC  DISPLAY      *P30:13,*EL,*B,*V2LON:
                      "*** Miscellaneous Code does not exist ***",*W2;
         GOTO         KITEM1
.
NOMAST  DISPLAY         *P1:24,*EL,*B,*V2LON,"Adm.No. ",AADMNO:
                        " Master Index record is missing ":
                        "   PLEASE contact I.B.A IMMEDIATELY **":
                        *W6,*P40:24,*EL;
        RETURN
.
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:10,*EL,"Hospital Id        : ";
          MOVE      TWENTY3,ECOL
          MOVE      TEN,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      SP10,PTHSDCLM
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P40:10,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
.
.
.        INC       PATMCHKY
         INC       PATWRDKY
         INC       PATHSPKY
         INC       PATMCHRD                * Miscellaneous Charges read routine
         INC       IBAPASIO/INC
         INC       PATMI1IO/INC
         INC       PATMA1IO/INC
         INC       PATMCHIO/INC
         INC       PATDSCIO/INC
.
.        INC       PATDTRIO/INC
         INC       PMSMTIIO/INC
.
         INC       PATTRNIO/INC
         INC       PATVISIO/INC
         INC       PATWR1IO/INC
         INC       PATHSPIO/INC
         INC       PMSVX1IO/INC
.
         INC        STD001IO
.
ABORT    CHAIN      PGM
         STOP
.

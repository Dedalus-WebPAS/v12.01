.*****************************************************************************
.* System    :   Patient Management                                          *
.* Program   :   EXOLDCON                                                    *
.* Desc      :   Extract Old format contacts                                 *
.*****************************************************************************
.* Date      :   20/04/2015                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will loop through the PMI master file and      *
.*               extract out the contacts and format them into pipe          *
.*               delimited records (in convcont.txt) ready for upload via    *
.*               CONVCONT into pmscexaf.                                     *
.* Mods      :                                                               *
.*        V10.07.01 24/11/2015  Steve Armstrong  CAR 295025                  *
.*                  Mods to include email address, mobile number and         *
.*                  correspondence fields from pmspx2af for Next of Kin,     *
.*                  Emergency Contact and Nearest Relative.                  *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
.
.
.         PMI Related Extra Contacts upload file layout - convcont.txt
.
CNTCTUPL  FILE      HL7, FIXED=4000         * Pipe delimited upload file
.                                             4096 is maximum
.
.         Fields must be pipe delimited and in the following sequence.
.         Field lengths are irrelevant other than the fact that where
.         a field is longer than allowed, the extra data will be ignored.
.
CONTURNO  DIM       8      1      * U/R Number                        (pmceurno)
.PIPE     DIM       1      9      * Pipe Delimiter
CONTTYPE  DIM       3      10     * Type of Contact (Cat tc)          (pmcetype)
.PIPE     DIM       1      13     * Pipe Delimiter
CONTTITL  DIM       4      14     * Title (pmstleaf)                  (pmcetitl)
.PIPE     DIM       1      18     * Pipe Delimiter
CONTSNAM  DIM       40     19     * Surname                           (pmcesnam)
.PIPE     DIM       1      59     * Pipe Delimiter
CONTGNM1  DIM       40     60     * First Given Name                  (pmcegnam)
.PIPE     DIM       1      100    * Pipe Delimiter
CONTGNM2  DIM       40     101    * Second Given Name                 (pmcegnm2)
.PIPE     DIM       1      141    * Pipe Delimiter
CONTADD1  DIM       35     142    * Address Line 1                    (pmceadd1)
.PIPE     DIM       1      177    * Pipe Delimiter
CONTADD2  DIM       35     178    * Address Line 2                    (pmceadd2)
.PIPE     DIM       1      213    * Pipe Delimiter
CONTADD3  DIM       35     214    * Address Line 3                    (pmceadd3)
.PIPE     DIM       1      249    * Pipe Delimiter
CONTADD4  DIM       35     250    * Address Line 4                    (pmceadd4)
.PIPE     DIM       1      285    * Pipe Delimiter
CONTPOST  DIM       8      286    * Postcode                          (pmcepost)
.PIPE     DIM       1      294    * Pipe Delimiter
CONTTELP  DIM       20     295    * Private Telephone Number          (pmcetelp)
.PIPE     DIM       1      315    * Pipe Delimiter
CONTTELB  DIM       20     316    * Business Telephone Number         (pmcetelb)
.PIPE     DIM       1      336    * Pipe Delimiter
CONTTELM  DIM       20     337    * Mobile Telephone Number           (pmcetelm)
.PIPE     DIM       1      357    * Pipe Delimiter
CONTRELP  DIM       10     358    * Relationship (pmsrelaf)           (pmcerelp)
.PIPE     DIM       1      368    * Pipe Delimiter
CONTEMAI  DIM       80     369    * Email address                     (pmceemai)
.PIPE     DIM       1      449    * Pipe Delimiter
CONTCDAT  DIM       8      450    * Date Record Created (ccyymmdd)    (pmcecdat)
.PIPE     DIM       1      458    * Pipe Delimiter
CONTCTIM  DIM       8      459    * Time Record Created (hh:mm:ss)    (pmcectim)
.PIPE     DIM       1      467    * Pipe Delimiter
CONTCUID  DIM       10     468    * Web User ID Created               (pmcecuid)
.PIPE     DIM       1      478    * Pipe Delimiter
CONTUDAT  DIM       8      479    * Date Record Updated (ccyymmdd)    (pmceudat)
.PIPE     DIM       1      487    * Pipe Delimiter
CONTUTIM  DIM       8      488    * Time Record Updated (hh:mm:ss)    (pmceutim)
.PIPE     DIM       1      496    * Pipe Delimiter
CONTUUID  DIM       10     497    * Web User ID Updated               (pmceuuid)
.PIPE     DIM       1      507    * Pipe Delimiter
CONTTPID  DIM       20     508    * Third Party Unique ID             (pmcetpid)
.PIPE     DIM       1      528    * Pipe Delimiter
CONTSLET  DIM       1      529    * Send Letter (Y/N)                 (pmceslet)
.PIPE     DIM       1      530    * Pipe Delimiter
CONTCONT  DIM       3      531    * Country of Birth (Cat C)          (pmcecont)
.
. End of Record            534
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
DIM20     DIM       20
EMERFLAG  FORM      1             * Emergency contact details flag
.                                      0 = Extracting
.                                      1 = Not extracting
EXTCOUNT  FORM      10
FATHFLAG  FORM      1             * Father's contact details flag
.                                      0 = Extracting
.                                      1 = Not extracting
FULLGNAM  DIM       80
FRSTGNAM  DIM       40
MOTHFLAG  FORM      1             * Mother's contact details flag
.                                      0 = Extracting
.                                      1 = Not extracting
NOKNFLAG  FORM      1             * NoK contact details flag
.                                      0 = Extracting
.                                      1 = Not extracting
NRELFLAG  FORM      1             * Nearest relative contact details flag
.                                      0 = Extracting
.                                      1 = Not extracting
PADDFLAG  FORM      1             * Postal address details flag
.                                      0 = Extracting
.                                      1 = Not extracting
RECCOUNT  FORM      10
SCNDGNAM  DIM       40
TMPSURNM  DIM       20
.
.
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
.
.
.
PRGID     INIT      "EXOLDCON"
PRGNAM    INIT      "Old Patient Contacts Extraction"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with data extraction
.
MAIN1000  CALL      ASKQ0000               * select contact types for extract
          BRANCH    EXIT,MAIN0100          * nothing selected
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN3000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN3000  CALL      CFIL0000               * create file
          BRANCH    EXIT,MAIN0100          * file exists - don't proceed
.
          CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af";
          OPEN      PATMX1A1,"patmx1af";
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af";
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run extraction                          *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Contact Data Extraction"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run data extraction
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               ASKQ0000              Called by: MAIN0000   *
.*                  Ask run related questions                                *
.* Returns: EMERFLAG  - Emergency contact details flag                       *
.*                        0 = Extracting                                     *
.*                        1 = Not extracting                                 *
.*          FATHFLAG  - Father's contact details flag                        *
.*                        0 = Extracting                                     *
.*                        1 = Not extracting                                 *
.*          MOTHFLAG  - Mother's contact details flag                        *
.*                        0 = Extracting                                     *
.*                        1 = Not extracting                                 *
.*          NOKNFLAG  - NoK contact details flag                             *
.*                        0 = Extracting                                     *
.*                        1 = Not extracting                                 *
.*          NRELFLAG  - Nearest relative contact details flag                *
.*                        0 = Extracting                                     *
.*                        1 = Not extracting                                 *
.*          PADDFLAG  - Postal address details flag                          *
.*                        0 = Extracting                                     *
.*                        1 = Not extracting                                 *
.* EXIT  0 = Ok to continue                                                  *
.*       1 = Don't continue (no contact type selected)                       *
.*****************************************************************************
.
.         Check if we are extracting Mother's contact details
.
ASKQ0000  MOVE      ANSY,ANS
          KEYIN     *P1:9,*EL,"Extracting Mother's contact details         (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ?":
                    *P53:9,*V2LON,*RV,ANS
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS                     * Y input ?
          IF        @EQUAL
            DISPLAY   *P53:9,*V2LON,DYES
            MOVE      ZERO,MOTHFLAG              * yes
            GOTO      ASKQ1000
          ENDIF
.
          MATCH     ANSN,ANS                     * N input ?
          IF        @EQUAL
            DISPLAY   *P53:9,*V2LON,DNO
            MOVE      ONE,MOTHFLAG               * yes
            GOTO      ASKQ1000
          ENDIF
.
          GOTO      ASKQ0000                     * invalid input
.
.         Check if we are extracting Father's contact details
.
ASKQ1000  MOVE      ANSY,ANS
          KEYIN     *P1:10,*EL,"Extracting Father's contact details         (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ?":
                    *P53:10,*V2LON,*RV,ANS
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS                     * Y input ?
          IF        @EQUAL
            DISPLAY   *P53:10,*V2LON,DYES
            MOVE      ZERO,FATHFLAG              * yes
            GOTO      ASKQ2000
          ENDIF
.
          MATCH     ANSN,ANS                     * N input ?
          IF        @EQUAL
            DISPLAY   *P53:10,*V2LON,DNO
            MOVE      ONE,FATHFLAG               * yes
            GOTO      ASKQ2000
          ENDIF
.
          GOTO      ASKQ1000                     * invalid input
.
.         Check if we are extracting NoK contact details
.
ASKQ2000  MOVE      ANSY,ANS
          KEYIN     *P1:11,*EL,"Extracting NoK contact details              (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ?":
                    *P53:11,*V2LON,*RV,ANS
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS                     * Y input ?
          IF        @EQUAL
            DISPLAY   *P53:11,*V2LON,DYES
            MOVE      ZERO,NOKNFLAG              * yes
            GOTO      ASKQ3000
          ENDIF
.
          MATCH     ANSN,ANS                     * N input ?
          IF        @EQUAL
            DISPLAY   *P53:11,*V2LON,DNO
            MOVE      ONE,NOKNFLAG               * yes
            GOTO      ASKQ3000
          ENDIF
.
          GOTO      ASKQ2000                     * invalid input
.
.         Check if we are extracting Emergency contact details
.
ASKQ3000  MOVE      ANSY,ANS
          KEYIN     *P1:12,*EL,"Extracting Emergency contact details        (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ?":
                    *P53:12,*V2LON,*RV,ANS
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS                     * Y input ?
          IF        @EQUAL
            DISPLAY   *P53:12,*V2LON,DYES
            MOVE      ZERO,EMERFLAG              * yes
            GOTO      ASKQ4000
          ENDIF
.
          MATCH     ANSN,ANS                     * N input ?
          IF        @EQUAL
            DISPLAY   *P53:12,*V2LON,DNO
            MOVE      ONE,EMERFLAG               * yes
            GOTO      ASKQ4000
          ENDIF
.
          GOTO      ASKQ3000                     * invalid input
.
.         Check if we are extracting Nearest Relative contact details
.
ASKQ4000  MOVE      ANSY,ANS
          KEYIN     *P1:13,*EL,"Extracting Nearest Relative contact details (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ?":
                    *P53:13,*V2LON,*RV,ANS
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS                     * Y input ?
          IF        @EQUAL
            DISPLAY   *P53:13,*V2LON,DYES
            MOVE      ZERO,NRELFLAG              * yes
            GOTO      ASKQ5000
          ENDIF
.
          MATCH     ANSN,ANS                     * N input ?
          IF        @EQUAL
            DISPLAY   *P53:13,*V2LON,DNO
            MOVE      ONE,NRELFLAG               * yes
            GOTO      ASKQ5000
          ENDIF
.
          GOTO      ASKQ4000                     * invalid input
.
.         Check if we are extracting Postal Address contact details
.
ASKQ5000  MOVE      ANSY,ANS
          KEYIN     *P1:14,*EL,"Extracting Postal Address contact details   (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ?":
                    *P53:14,*V2LON,*RV,ANS
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS                     * Y input ?
          IF        @EQUAL
            DISPLAY   *P53:14,*V2LON,DYES
            MOVE      ZERO,PADDFLAG              * yes
            GOTO      ASKQ9000
          ENDIF
.
          MATCH     ANSN,ANS                     * N input ?
          IF        @EQUAL
            DISPLAY   *P53:14,*V2LON,DNO
            MOVE      ONE,PADDFLAG               * yes
            GOTO      ASKQ9000
          ENDIF
.
          GOTO      ASKQ5000                     * invalid input
.
ASKQ9000  IF        MOTHFLAG = 1 & FATHFLAG = 1 & NOKNFLAG = 1 & EMERFLAG = 1 & NRELFLAG = 1 & PADDFLAG = 1
            DISPLAY   *P1:24,*B,"No contact types selected.  ";
            CALL      HITENTER
            MOVE      ONE,EXIT
          ELSE
            MOVE      ZERO,EXIT
          ENDIF
.
ASKQ9999  RETURN
+
.*****************************************************************************
.*                                  CFIL0000       Called by: MAIN0000       *
.*                         Create the extract file                           *
.* Returns:  EXIT  0 = Ok to continue                                        *
.*                 1 = Don't continue                                        *
.*****************************************************************************
.
CFIL0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CNTCTUPL,"convcont"
          TRAPCLR   IO
          BRANCH    OVRCD,CFIL5000
.
          DISPLAY   *P1:23,*B,*EF,"The extract file convcont.txt already ":
                    "exists."
CFIL1000  KEYIN     *P1:24,"Overwrite it (",*V2LON,*DV,ANSY,*HOFF,"/":
                    *V2LON,*DV,ANSN,*HOFF,")?":
                    *P21:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS                * "Y" entered ?
          GOTO      CFIL2000 IF EQUAL       * yes - continue
.
          MATCH     ANSN,ANS                * "N" entered ?
          GOTO      CFIL9100 IF EQUAL       * no
.
          BEEP
          GOTO      CFIL1000
.
.         Delete the existing file
.
CFIL2000  CLOSE     CNTCTUPL,DELETE
.
.         Create a new extract file
.
CFIL5000  PREP      CNTCTUPL,"convcont"
.
          MOVE      ZERO,EXIT
          GOTO      CFIL9999
.
CFIL9100  MOVE      ONE,EXIT
.
CFIL9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*         Loop through the patient master file and extract the relevant     *
.*         contact details                                                   *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,RECCOUNT                * initialise record count
          MOVE      ZERO,EXTCOUNT                * initialise extract count
          DISPLAY   *P1:24,*EL,"Processing:";
.
          MOVE      SP8,KEY8
PROC0400  CALL      RDSMAST1                     * position at start of file
PROC0500  CALL      RDKMAST1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          COMPARE   ZERO,PSTAT                   * valid U/R ?
          GOTO      PROC0500 IF NOT EQUAL        * no
.
.         Read the corresponding pmspx2af record
.
          CALL      CLPMSPX2
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
.
          ADD       ONE,RECCOUNT                 * increment record counter
.
          DISPLAY   *P13:24,*V2LON,RECCOUNT
.
          BRANCH    MOTHFLAG,PROC1000            * not loading mother's contact
.
.         Mother's Contact Details
.
          MATCH     PMPXMSNA,SP70                * blank surname ?
          GOTO      PROC0700 IF NOT EQUAL        * no - extract record
.
          MATCH     PMPXMAD1,SP70                * blank address line 1 ?
          GOTO      PROC1000 IF EQUAL            * yes - don't extract record
.
.         Data available so extract
.
PROC0700  MOVE      PURNO,CONTURNO
          SQUEEZE   CONTURNO
          MOVE      "001",CONTTYPE
          MOVE      PMPXMTLE,CONTTITL
          SQUEEZE   CONTTITL
          PACK      CONTSNAM,PMPXMSNA,SP70
          STRIP     CONTSNAM
.
          PACK      FULLGNAM,PMPXMGNA,SP70
          CALL      SEPRNAME
          MOVE      FRSTGNAM,CONTGNM1
          SQUEEZE   CONTGNM1
          MOVE      SCNDGNAM,CONTGNM2
          STRIP     CONTGNM2
.
          MOVE      PMPXMAD1,CONTADD1
          STRIP     CONTADD1
          MOVE      PMPXMAD2,CONTADD2
          STRIP     CONTADD2
          MOVE      PMPXMAD3,CONTADD3
          STRIP     CONTADD3
          MOVE      PMPXMAD4,CONTADD4
          STRIP     CONTADD4
          MOVE      PMPXMPOS,CONTPOST
          SQUEEZE   CONTPOST
          MOVE      PMPXMPNO,CONTTELP
          STRIP     CONTTELP
          MOVE      PMPXMBNO,CONTTELB
          STRIP     CONTTELB
          MOVE      PMPXMMNO,CONTTELM
          STRIP     CONTTELM
          MOVE      PMPXMREL,CONTRELP
          SQUEEZE   CONTRELP
          MOVE      PMPXMEML,CONTEMAI
          SQUEEZE   CONTEMAI
          MOVE      SP8,CONTCDAT
          SQUEEZE   CONTCDAT
          MOVE      SP8,CONTCTIM
          SQUEEZE   CONTCTIM
          MOVE      SP8,CONTCUID
          SQUEEZE   CONTCUID
          MOVE      SP8,CONTUDAT
          SQUEEZE   CONTUDAT
          MOVE      SP8,CONTUTIM
          SQUEEZE   CONTUTIM
          MOVE      SP8,CONTUUID
          SQUEEZE   CONTUUID
          MOVE      SP8,CONTTPID
          SQUEEZE   CONTTPID
          MOVE      PMPXMCRP,CONTSLET
          SQUEEZE   CONTSLET
          MOVE      PMPXMCNT,CONTCONT
          SQUEEZE   CONTSLET
.
          WRITE     CNTCTUPL,SEQ;*+,CONTURNO,PIPE,CONTTYPE,PIPE,CONTTITL,PIPE:
                                    CONTSNAM,PIPE,CONTGNM1,PIPE,CONTGNM2,PIPE:
                                    CONTADD1,PIPE,CONTADD2,PIPE,CONTADD3,PIPE:
                                    CONTADD4,PIPE,CONTPOST,PIPE,CONTTELP,PIPE:
                                    CONTTELB,PIPE,CONTTELM,PIPE,CONTRELP,PIPE:
                                    CONTEMAI,PIPE,CONTCDAT,PIPE,CONTCTIM,PIPE:
                                    CONTCUID,PIPE,CONTUDAT,PIPE,CONTUTIM,PIPE:
                                    CONTUUID,PIPE,CONTTPID,PIPE,CONTSLET,PIPE:
                                    CONTCONT,*-
.
          ADD       ONE,EXTCOUNT                 * increment extract count
.
PROC1000  BRANCH    FATHFLAG,PROC2000            * not loading father's contact
.
.         Father's Contact Details
.
          MATCH     PMPXFSNA,SP70                * blank surname ?
          GOTO      PROC1700 IF NOT EQUAL        * no - extract record
.
          MATCH     PMPXFAD1,SP70                * blank address line 1 ?
          GOTO      PROC2000 IF EQUAL            * yes - don't extract record
.
.         Data available so extract
.
PROC1700  MOVE      PURNO,CONTURNO
          SQUEEZE   CONTURNO
          MOVE      "002",CONTTYPE
          MOVE      PMPXFTLE,CONTTITL
          SQUEEZE   CONTTITL
          PACK      CONTSNAM,PMPXFSNA,SP70
          STRIP     CONTSNAM
.
          PACK      FULLGNAM,PMPXFGNA,SP70
          CALL      SEPRNAME
          MOVE      FRSTGNAM,CONTGNM1
          SQUEEZE   CONTGNM1
          MOVE      SCNDGNAM,CONTGNM2
          STRIP     CONTGNM2
.
          MOVE      PMPXFAD1,CONTADD1
          STRIP     CONTADD1
          MOVE      PMPXFAD2,CONTADD2
          STRIP     CONTADD2
          MOVE      PMPXFAD3,CONTADD3
          STRIP     CONTADD3
          MOVE      PMPXFAD4,CONTADD4
          STRIP     CONTADD4
          MOVE      PMPXFPOS,CONTPOST
          SQUEEZE   CONTPOST
          MOVE      PMPXFPNO,CONTTELP
          STRIP     CONTTELP
          MOVE      PMPXFBNO,CONTTELB
          STRIP     CONTTELB
          MOVE      PMPXFMNO,CONTTELM
          STRIP     CONTTELM
          MOVE      PMPXFREL,CONTRELP
          SQUEEZE   CONTRELP
          MOVE      PMPXFEML,CONTEMAI
          SQUEEZE   CONTEMAI
          MOVE      SP8,CONTCDAT
          SQUEEZE   CONTCDAT
          MOVE      SP8,CONTCTIM
          SQUEEZE   CONTCTIM
          MOVE      SP8,CONTCUID
          SQUEEZE   CONTCUID
          MOVE      SP8,CONTUDAT
          SQUEEZE   CONTUDAT
          MOVE      SP8,CONTUTIM
          SQUEEZE   CONTUTIM
          MOVE      SP8,CONTUUID
          SQUEEZE   CONTUUID
          MOVE      SP8,CONTTPID
          SQUEEZE   CONTTPID
          MOVE      PMPXFCRP,CONTSLET
          SQUEEZE   CONTSLET
          MOVE      PMPXFCNT,CONTCONT
          SQUEEZE   CONTSLET
.
          WRITE     CNTCTUPL,SEQ;*+,CONTURNO,PIPE,CONTTYPE,PIPE,CONTTITL,PIPE:
                                    CONTSNAM,PIPE,CONTGNM1,PIPE,CONTGNM2,PIPE:
                                    CONTADD1,PIPE,CONTADD2,PIPE,CONTADD3,PIPE:
                                    CONTADD4,PIPE,CONTPOST,PIPE,CONTTELP,PIPE:
                                    CONTTELB,PIPE,CONTTELM,PIPE,CONTRELP,PIPE:
                                    CONTEMAI,PIPE,CONTCDAT,PIPE,CONTCTIM,PIPE:
                                    CONTCUID,PIPE,CONTUDAT,PIPE,CONTUTIM,PIPE:
                                    CONTUUID,PIPE,CONTTPID,PIPE,CONTSLET,PIPE:
                                    CONTCONT,*-
.
          ADD       ONE,EXTCOUNT                 * increment extract count
.
PROC2000  BRANCH    NOKNFLAG,PROC3000            * not loading NoK contact
.
.         Next of Kin's Contact Details
.
          MATCH     PNKNAME,SP70                 * blank name ?
          GOTO      PROC2700 IF NOT EQUAL        * no - extract record
.
          MATCH     PNKADD1,SP70                 * blank address line 1 ?
          GOTO      PROC3000 IF EQUAL            * yes - don't extract record
.
.         Data available so extract
.
PROC2700  MOVE      PURNO,CONTURNO
          SQUEEZE   CONTURNO
          MOVE      "003",CONTTYPE
          MOVE      SP4,CONTTITL
          SQUEEZE   CONTTITL
.
          MOVE      PNKNAME,DIM20
          CALL      GNAM0000
          MOVE      TMPSURNM,CONTSNAM
          STRIP     CONTSNAM
.
          CALL      SEPRNAME
          MOVE      FRSTGNAM,CONTGNM1
          SQUEEZE   CONTGNM1
          MOVE      SCNDGNAM,CONTGNM2
          STRIP     CONTGNM2
.
          MOVE      PNKADD1,CONTADD1
          STRIP     CONTADD1
          MOVE      PNKADD2,CONTADD2
          STRIP     CONTADD2
          MOVE      PNKSUBR,CONTADD3
          STRIP     CONTADD3
          MOVE      PNKADD4,CONTADD4
          STRIP     CONTADD4
          MOVE      PNKPOST,CONTPOST
          SQUEEZE   CONTPOST
          MOVE      PNKTELP,CONTTELP
          STRIP     CONTTELP
          MOVE      PNKTELB,CONTTELB
          STRIP     CONTTELB
          MOVE      PMPXKMOB,CONTTELM
          STRIP     CONTTELM
          MOVE      PNKRELP,CONTRELP
          SQUEEZE   CONTRELP
          MOVE      PMPXKEML,CONTEMAI
          SQUEEZE   CONTEMAI
          MOVE      SP8,CONTCDAT
          SQUEEZE   CONTCDAT
          MOVE      SP8,CONTCTIM
          SQUEEZE   CONTCTIM
          MOVE      SP8,CONTCUID
          SQUEEZE   CONTCUID
          MOVE      SP8,CONTUDAT
          SQUEEZE   CONTUDAT
          MOVE      SP8,CONTUTIM
          SQUEEZE   CONTUTIM
          MOVE      SP8,CONTUUID
          SQUEEZE   CONTUUID
          MOVE      SP8,CONTTPID
          SQUEEZE   CONTTPID
          MOVE      PMPXKCRP,CONTSLET
          SQUEEZE   CONTSLET
          MOVE      SP3,CONTCONT
          SQUEEZE   CONTSLET
.
          WRITE     CNTCTUPL,SEQ;*+,CONTURNO,PIPE,CONTTYPE,PIPE,CONTTITL,PIPE:
                                    CONTSNAM,PIPE,CONTGNM1,PIPE,CONTGNM2,PIPE:
                                    CONTADD1,PIPE,CONTADD2,PIPE,CONTADD3,PIPE:
                                    CONTADD4,PIPE,CONTPOST,PIPE,CONTTELP,PIPE:
                                    CONTTELB,PIPE,CONTTELM,PIPE,CONTRELP,PIPE:
                                    CONTEMAI,PIPE,CONTCDAT,PIPE,CONTCTIM,PIPE:
                                    CONTCUID,PIPE,CONTUDAT,PIPE,CONTUTIM,PIPE:
                                    CONTUUID,PIPE,CONTTPID,PIPE,CONTSLET,PIPE:
                                    CONTCONT,*-
.
          ADD       ONE,EXTCOUNT                 * increment extract count
.
PROC3000  BRANCH    EMERFLAG,PROC4000            * not loading Emergency contact
.
.         Emergency Contact Details
.
          MATCH     PTMXECNM,SP70                * blank name ?
          GOTO      PROC3700 IF NOT EQUAL        * no - extract record
.
          MATCH     PTMXECA1,SP70                * blank address line 1 ?
          GOTO      PROC4000 IF EQUAL            * yes - don't extract record
.
.         Data available so extract
.
PROC3700  MOVE      PURNO,CONTURNO
          SQUEEZE   CONTURNO
          MOVE      "004",CONTTYPE
          MOVE      SP4,CONTTITL
          SQUEEZE   CONTTITL
.
          MOVE      PTMXECNM,DIM20
          CALL      GNAM0000
          MOVE      TMPSURNM,CONTSNAM
          STRIP     CONTSNAM
.
          CALL      SEPRNAME
          MOVE      FRSTGNAM,CONTGNM1
          SQUEEZE   CONTGNM1
          MOVE      SCNDGNAM,CONTGNM2
          STRIP     CONTGNM2
.
          MOVE      PTMXECA1,CONTADD1
          STRIP     CONTADD1
          MOVE      PTMXECA2,CONTADD2
          STRIP     CONTADD2
          MOVE      PTMXECA3,CONTADD3
          STRIP     CONTADD3
          MOVE      PTMXECA4,CONTADD4
          STRIP     CONTADD4
          MOVE      PTMXECPC,CONTPOST
          SQUEEZE   CONTPOST
          MOVE      PTMXECPP,CONTTELP
          STRIP     CONTTELP
          MOVE      PTMXECBP,CONTTELB
          STRIP     CONTTELB
          MOVE      PMPXCMOB,CONTTELM
          STRIP     CONTTELM
          MOVE      PTMXECRE,CONTRELP
          SQUEEZE   CONTRELP
          MOVE      PMPXCEML,CONTEMAI
          SQUEEZE   CONTEMAI
          MOVE      SP8,CONTCDAT
          SQUEEZE   CONTCDAT
          MOVE      SP8,CONTCTIM
          SQUEEZE   CONTCTIM
          MOVE      SP8,CONTCUID
          SQUEEZE   CONTCUID
          MOVE      SP8,CONTUDAT
          SQUEEZE   CONTUDAT
          MOVE      SP8,CONTUTIM
          SQUEEZE   CONTUTIM
          MOVE      SP8,CONTUUID
          SQUEEZE   CONTUUID
          MOVE      SP8,CONTTPID
          SQUEEZE   CONTTPID
          MOVE      PMPXCCRP,CONTSLET
          SQUEEZE   CONTSLET
          MOVE      SP3,CONTCONT
          SQUEEZE   CONTSLET
.
          WRITE     CNTCTUPL,SEQ;*+,CONTURNO,PIPE,CONTTYPE,PIPE,CONTTITL,PIPE:
                                    CONTSNAM,PIPE,CONTGNM1,PIPE,CONTGNM2,PIPE:
                                    CONTADD1,PIPE,CONTADD2,PIPE,CONTADD3,PIPE:
                                    CONTADD4,PIPE,CONTPOST,PIPE,CONTTELP,PIPE:
                                    CONTTELB,PIPE,CONTTELM,PIPE,CONTRELP,PIPE:
                                    CONTEMAI,PIPE,CONTCDAT,PIPE,CONTCTIM,PIPE:
                                    CONTCUID,PIPE,CONTUDAT,PIPE,CONTUTIM,PIPE:
                                    CONTUUID,PIPE,CONTTPID,PIPE,CONTSLET,PIPE:
                                    CONTCONT,*-
.
          ADD       ONE,EXTCOUNT                 * increment extract count
.
PROC4000  BRANCH    NRELFLAG,PROC5000            * not loading NR contact
.
.         Nearest Relative Details
.
          MATCH     PTMXNRNM,SP70                * blank name ?
          GOTO      PROC4700 IF NOT EQUAL        * no - extract record
.
          MATCH     PTMXNRA1,SP70                * blank address line 1 ?
          GOTO      PROC5000 IF EQUAL            * yes - don't extract record
.
.         Data available so extract
.
PROC4700  MOVE      PURNO,CONTURNO
          SQUEEZE   CONTURNO
          MOVE      "005",CONTTYPE
          MOVE      SP4,CONTTITL
          SQUEEZE   CONTTITL
.
          MOVE      PTMXNRNM,DIM20
          CALL      GNAM0000
          MOVE      TMPSURNM,CONTSNAM
          STRIP     CONTSNAM
.
          CALL      SEPRNAME
          MOVE      FRSTGNAM,CONTGNM1
          SQUEEZE   CONTGNM1
          MOVE      SCNDGNAM,CONTGNM2
          STRIP     CONTGNM2
.
          MOVE      PTMXNRA1,CONTADD1
          STRIP     CONTADD1
          MOVE      PTMXNRA2,CONTADD2
          STRIP     CONTADD2
          MOVE      PTMXNRA3,CONTADD3
          STRIP     CONTADD3
          MOVE      PTMXNRA4,CONTADD4
          STRIP     CONTADD4
          MOVE      PTMXNRPC,CONTPOST
          SQUEEZE   CONTPOST
          MOVE      PTMXNRPP,CONTTELP
          STRIP     CONTTELP
          MOVE      PTMXNRBP,CONTTELB
          STRIP     CONTTELB
          MOVE      PMPXRMOB,CONTTELM
          STRIP     CONTTELM
          MOVE      PTMXNRRE,CONTRELP
          SQUEEZE   CONTRELP
          MOVE      PMPXREML,CONTEMAI
          SQUEEZE   CONTEMAI
          MOVE      SP8,CONTCDAT
          SQUEEZE   CONTCDAT
          MOVE      SP8,CONTCTIM
          SQUEEZE   CONTCTIM
          MOVE      SP8,CONTCUID
          SQUEEZE   CONTCUID
          MOVE      SP8,CONTUDAT
          SQUEEZE   CONTUDAT
          MOVE      SP8,CONTUTIM
          SQUEEZE   CONTUTIM
          MOVE      SP8,CONTUUID
          SQUEEZE   CONTUUID
          MOVE      SP8,CONTTPID
          SQUEEZE   CONTTPID
          MOVE      PMPXRCRP,CONTSLET
          SQUEEZE   CONTSLET
          MOVE      SP3,CONTCONT
          SQUEEZE   CONTSLET
.
          WRITE     CNTCTUPL,SEQ;*+,CONTURNO,PIPE,CONTTYPE,PIPE,CONTTITL,PIPE:
                                    CONTSNAM,PIPE,CONTGNM1,PIPE,CONTGNM2,PIPE:
                                    CONTADD1,PIPE,CONTADD2,PIPE,CONTADD3,PIPE:
                                    CONTADD4,PIPE,CONTPOST,PIPE,CONTTELP,PIPE:
                                    CONTTELB,PIPE,CONTTELM,PIPE,CONTRELP,PIPE:
                                    CONTEMAI,PIPE,CONTCDAT,PIPE,CONTCTIM,PIPE:
                                    CONTCUID,PIPE,CONTUDAT,PIPE,CONTUTIM,PIPE:
                                    CONTUUID,PIPE,CONTTPID,PIPE,CONTSLET,PIPE:
                                    CONTCONT,*-
.
          ADD       ONE,EXTCOUNT                 * increment extract count
.
PROC5000  BRANCH    PADDFLAG,PROC0500            * not loading postal contact
.
.         Postal Address Details
.
          MATCH     PTMXADD1,SP70                * blank address line 1 ?
          GOTO      PROC0500 IF EQUAL            * yes - don't extract record
.
.         Data available so extract
.
PROC5700  MOVE      PURNO,CONTURNO
          SQUEEZE   CONTURNO
          MOVE      "006",CONTTYPE
          MOVE      SP4,CONTTITL
          SQUEEZE   CONTTITL
          MOVE      SP70,CONTSNAM
          SQUEEZE   CONTSNAM
          MOVE      SP70,CONTGNM1
          SQUEEZE   CONTGNM1
          MOVE      SP70,CONTGNM2
          SQUEEZE   CONTGNM2
          MOVE      PTMXADD1,CONTADD1
          STRIP     CONTADD1
          MOVE      PTMXADD2,CONTADD2
          STRIP     CONTADD2
          MOVE      PTMXADD3,CONTADD3
          STRIP     CONTADD3
          MOVE      PTMXADD4,CONTADD4
          STRIP     CONTADD4
          MOVE      PTMXPOST,CONTPOST
          SQUEEZE   CONTPOST
          MOVE      SP20,CONTTELP
          SQUEEZE   CONTTELP
          MOVE      SP20,CONTTELB
          SQUEEZE   CONTTELB
          MOVE      SP20,CONTTELM
          SQUEEZE   CONTTELM
          MOVE      SP10,CONTRELP
          SQUEEZE   CONTRELP
          PACK      CONTEMAI,SP70,SP70
          SQUEEZE   CONTEMAI
          MOVE      SP8,CONTCDAT
          SQUEEZE   CONTCDAT
          MOVE      SP8,CONTCTIM
          SQUEEZE   CONTCTIM
          MOVE      SP8,CONTCUID
          SQUEEZE   CONTCUID
          MOVE      SP8,CONTUDAT
          SQUEEZE   CONTUDAT
          MOVE      SP8,CONTUTIM
          SQUEEZE   CONTUTIM
          MOVE      SP8,CONTUUID
          SQUEEZE   CONTUUID
          MOVE      SP8,CONTTPID
          SQUEEZE   CONTTPID
          MOVE      SP1,CONTSLET
          SQUEEZE   CONTSLET
          MOVE      SP3,CONTCONT
          SQUEEZE   CONTSLET
.
          WRITE     CNTCTUPL,SEQ;*+,CONTURNO,PIPE,CONTTYPE,PIPE,CONTTITL,PIPE:
                                    CONTSNAM,PIPE,CONTGNM1,PIPE,CONTGNM2,PIPE:
                                    CONTADD1,PIPE,CONTADD2,PIPE,CONTADD3,PIPE:
                                    CONTADD4,PIPE,CONTPOST,PIPE,CONTTELP,PIPE:
                                    CONTTELB,PIPE,CONTTELM,PIPE,CONTRELP,PIPE:
                                    CONTEMAI,PIPE,CONTCDAT,PIPE,CONTCTIM,PIPE:
                                    CONTCUID,PIPE,CONTUDAT,PIPE,CONTUTIM,PIPE:
                                    CONTUUID,PIPE,CONTTPID,*-
.
          ADD       ONE,EXTCOUNT                 * increment extract count
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:22,*EF,"Extraction complete.":
                    *P1:23,*V2LON,EXTCOUNT,*HOFF," Contact records ":
                    "extracted.";
          DISPLAY   *P1:24;
          CALL      HITENTER
.
PROC9999  RETURN
+
.*****************************************************************************
.*                                GNAM0000         Called by: PROC0000       *
.*                                                                           *
.* Requires:  DIM20    - full patient name (free text)                       *
.* Returns :  TMPSURNM - surname                                             *
.*            FULLGNAM - given name(s)                                       *
.* Assumptions - this routine assumes that the last word in the name field   *
.*               is the surname and therefore, the rest of the name          *
.*               represents the given name(s). No allowance has been made to *
.*               try and cater for title.  Users are best to review surname, *
.*               given name(s) and title by viewing/modifying the extract    *
.*               file, or uploading into a s/s for editing before uploading  *
.*               into CONVCONT.                                              *
.*****************************************************************************
.
GNAM0000  MOVE      SP20,TMPSURNM                * init. surname variable
          PACK      FULLGNAM,SP70,SP70           * init. given name variable
          MOVE      ZERO,FORM2
.
          MATCH     SP20,DIM20                   * blank name ?
          GOTO      GNAM9999 IF EQUAL            * yes - finished
.
          STRIP     DIM20                        * remove trailing spaces
.
.         Find the last space in the field
.
GNAM1000  SCAN      SP1,DIM20
          GOTO      GNAM5000 IF NOT EQUAL
.
          MOVEFPTR  DIM20,FORM2                  * save last blank space
          BUMP      DIM20
          GOTO      GNAM1000
.
.         The position of the last space has been found, so now
.         reposition on the next character in the string, which will be
.         the start of the surname
.
GNAM5000  RESET     DIM20
          IF        FORM2 > 0
            BUMP      DIM20,FORM2
          ENDIF
.
          PACK      TMPSURNM,DIM20,SP20          * load surname
.
          IF        FORM2 > 0
            RESET     DIM20
            SUB       ONE,FORM2
            SETLPTR   DIM20,FORM2
            PACK      FULLGNAM,DIM20,SP70,SP70
          ENDIF
.
GNAM9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       CLPMSPX2
          INC       SEPRNAME
.
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC

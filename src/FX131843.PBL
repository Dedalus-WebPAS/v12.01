.***************************************************************************
.* System    :   Outpatients                                               *
.* Program   :   FX131843                                                  *
.* Desc      :   Copy referral letter referring HCP to referring GP        *
.***************************************************************************
.* Date      :   15/05/2007                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will loop outrf1af and copy the referring    *
.*           :   HCP(OTRLRDOC) to the referring GP(OTRLRFGP).              *
.*           :                                                             *
.*           :   THIS SHOULD BE RUN AT STV ONLY                            *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       OUTRF1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
PRGID     INIT      "FX131843"
PRGNAM    INIT      "Fix referral letter GP"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      OUTRF1A1,"outrf1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Copy the referring HCP to the ":
                          "referring GP in outrf1af"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  PACK      KEY8,SP70
          CALL      RSOTRFL1
PROC1000  CALL      RKOTRFL1
          BRANCH    OVRCD,PROC9999
.
          MATCH     SP70,OTRLRDOC           * Referring HCP
          GOTO      PROC1000 IF EQUAL
.
          MOVE      OTRLRDOC,OTRLRFGP       * Update referring GP
.
          CALL      UPOTRFL1
          GOTO      PROC1000
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       OUTRF1IO/INC

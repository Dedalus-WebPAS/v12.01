.******************************************************************************
.* System         : Operating Rooms                                           *
.* Program        : IBAOPR72.PBL                                              *
.* Name           : Exception Report                                          *
.******************************************************************************
.* Date           : 04/10/1991                                                *
.* Author         : Justin Coates                                             *
.* Function       : To report various discrepencies between bookings/usage    *
.* Modifications  :                                                           *
.******************************************************************************
.* V11.03.02 19/04/2023  Thanh T  TSK 0909393                                 *
.*           Changed SWUD0000 to use RDOPSES7 instead of RDOPSES1.            *
.*           Changed BWUD0000, BNOP0000, BWSF0000 to use RSOPDEA6 instead of  *
.*           RSOPDEA1                                                         * 
.* V11.03.01 20/03/2023  PJ Le Febour   TSK 0909393a                          *
.*           Amended KEY19 to KEY22 for theatre index changes                 *
.* V11.01.02 27/04/2021  Thanh T        TSK 0895165                           *
.*           Recompiled for OPRARDFD changes                                  *
.* V11.01.01 16/02/2021  Tracey Nguyen  TSK 0888639                           *
.*           FD changed - oprpmbaf.OPRPMBA1 from KEY13 to KEY14               *
.******************************************************************************
.*        V10.11.01 23/11/2017  Davin          TSK 0846231                    *
.*                  Recompiled for OPRPMBFD - added oppmrefn                  *
.*        V10.04.02 14/02/2014 Sandra Barcham CAR 295648                      *
.*                  Print Hospital code in report header                      *
.*        V10.04.01 20/01/2014 Peter Vela     CAR 295648                      *
.*                  Added multi-hospital functionality                        *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.03.01  07/08/2003  Jill Habasque CAR 41815                       *
.*                  Changed matches of opdastat                               *
.*        V9.02.02  14/02/2003  Tonii   SRS No: 36667                         *
.*                  Mods to use new tables if a record exists in oprsrgaf     *
.*        V9.02.01  18/12/2002  J.Tan                                         *
.*                  Mods to use oprard and oprsrg for checking usage          *
.******************************************************************************
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.******************************************************************************
.
.$$$$$
.         IBAOPR72.PBL        Exception Report
.         ------------        ----------------
.         OPTN0000 - Enter which report to do
.         RDAT0000 - Enter the date range
.         BWUD0000 - Report all Bookings without Usage Details
.         BNOP0000 - Report all Bookings with Usage details but no Operations
.         BWSF0000 - Report all Bookings without Statistical Fields
.         SWUD0000 - Report all Sessions without Usage Details
.$$$$$
.
.         FD's required
.
          INC       STD001FD/PBL            * standard vars
          INC       BOKRC1FD/INC            * bookings file
          INC       OPRCLIFD/INC            * clinic file
          INC       OPRCONFD/INC            * control file
          INC       OPRDEAFD/INC            * operation details
          INC       OPRDECFD/INC            * operation usage details
          INC       OPRSESFD/INC            * session file
          INC       OPRARDFD/INC            * operation arrival and recovery
          INC       OPRPMBFD/INC            
          INC       OPRSRGFD/INC            * operation surgical
          INC       PATDO1FD/INC            * doctor file
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC            * master file
          INC       PATMI1FD/INC            * admission file
          INC       PMSVX1FD/INC            * admission file
          INC       PATPR1FD/INC            * pre-admission file
.
.         program variables
.
CNTBOOK   FORM      3         * count of bookings
CURRSESS  DIM       22        * current session
DATESTRT  DIM       8         * start date
DATESTOP  DIM       8         * end   date
GRNDTOTL  FORM      5         * grand total
IBCNMHOS  FORM      1
NAME33    DIM       33        * patients name
NAME35    DIM       35        * doctors name
SESNDATE  DIM       8         * current date of session
STATUS    DIM       9         * session status
SUBOPTN   DIM       37        * option chosen
.
.         program contsants
.
OPTA      INIT      "Bookings without Usage Details       "
OPTB      INIT      "Bookings with Usage but No Operations"
OPTC      INIT      "Bookings with No Statistical Fields  "
OPTD      INIT      "Sessions with no Usage Details       "
STAT0     INIT      "Cancelled"
STAT1     INIT      "Booked   "
STAT2     INIT      "Extra    "
.
PRGID     INIT      "IBAOPR72"
PRGNAM    INIT      "Exception Report"
VERSION   INIT      "V12.01.00"
+
.*********************************************************************
.*                  ML0000                                           *
.*        Mainline Code / Controlling Logic                          *
.*********************************************************************
ML0000    CALL      INIT0000                * initilization
.
ML0500    CALL      OPTN0000                * enter option
          COMPARE   ZERO,MOPTION
          GOTO      ML9999 IF EQUAL         * exit selected
.
          CALL      RDAT0000                * enter date range
          BRANCH    EXIT,ML0500             * Cancel selected
.
          BRANCH    MOPTION,ML1000,ML2000,ML3000,ML4000
.
ML1000    CALL      BWUD0000                * Bookings Without Usage Details
          GOTO      ML0500
.
ML2000    CALL      BNOP0000                * Bookings with usage but No OPers
          GOTO      ML0500
.
ML3000    CALL      BWSF0000                * Bookings Without Stats. Fields
          GOTO      ML0500
.
ML4000    CALL      SWUD0000                * Sessions Without Usage Details
          GOTO      ML0500
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialization and open files                              *
.*********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CNEWDS
          MOVE      "0",CNOUNDLN            * print underlines
.
          DISPLAY   *P50:24,"Opening "
          DISPLAY   *P58:24,"bokrc1af"
          OPEN      BOKRC1A1,"bokrc1af"
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P58:24,"oprcliaf"
          OPEN      OPRCLIA1,"oprcliaf"
          DISPLAY   *P58:24,"oprdetaf"
          OPEN      OPRDETA1,"oprdetaf"
          OPEN      OPRDETA6,"oprdetaf"
          DISPLAY   *P58:24,"oprdetcf"
          OPEN      OPRDETC1,"oprdetcf"
          DISPLAY   *P58:24,"oprsesaf"
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRSESA7,"oprsesaf"
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          DISPLAY   *P58:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P58:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P58:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          DISPLAY   *P58:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P58:24,"oprardaf"
          OPEN      OPRARDA1,"oprardaf"
.
          DISPLAY   *P58:24,"oprpmbaf"
          OPEN      OPRPMBA1,"oprpmbaf"
.
          DISPLAY   *P58:24,"oprsrgaf"
          OPEN      OPRSRGA1,"oprsrgaf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,FORTY;*96,OPCNCDSC,*111,OPCNCLSU
.
          ENDSET    OPCNCDSC                * set FP to end
.
          IF        IBCNMHOS = 1
            DISPLAY   *P64:24,"pathspaf";
            OPEN      PATHSPA1,"pathspaf"
          ENDIF
.
INIT9000  MATCH     SP1,OPCNCDSC
          GOTO      INIT9100 IF NOT EQUAL   * stripped all blanks
.
          BUMP      OPCNCDSC,-1
          GOTO      INIT9000 IF NOT EOS     * more chars to strip
.
INIT9100  LENSET    OPCNCDSC                * set LL
          RESET     OPCNCDSC                * reset FP
.
INIT9999  RETURN
+
.*********************************************************************
.*                  OPTN0000                    Called by : ML0500   *
.*        Enter the option                                           *
.*        Returns : MOPTION       the choice entered 0-4             *
.*********************************************************************
OPTN0000  HLINE     *G37,2,48,80
          DISPLAY   *P1:3,*EF,*V2LON:
                    *P1:4,"0",*P1:5,"1",*P1:6,"2",*P1:7,"3",*P1:8,"4",*HOFF:
                    *P3:4,"= Exit":
                    *P3:5,"= All Bookings without Usage Details":
                    *P3:6,"= All Bookings with Usage Details, ":
                    "but no Operation codes":
                    *P3:7,"= All Bookings without Statistical Fields":
                    *P3:8,"= All Sessions without Session Usage Details":
                    *P1:10,"Select option : "
.
OPTN1000  KEYIN     *P17:10,*DV,UNDLN1:
                    *P17:10,*V2LON,MOPTION
.
          BRANCH    MOPTION,OPTN9000,OPTN9000,OPTN9000,OPTN9000
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9999 IF EQUAL       * valid choice
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  LOAD      SUBOPTN,MOPTION,OPTA,OPTB,OPTC,OPTD
          DISPLAY   *P48:2,*V2LON,"- ",*+,SUBOPTN,SP1
.
OPTN9999  RETURN
+
.*********************************************************************
.*                  RDAT0000                    Called by : ML0500   *
.*        Enter the date ranges                                      *
.*        Returns : EXIT = 1      exit chosen                        * 
.*                  DATESTRT      starting date                      *
.*                  DATESTOP      ending date                        *
.*********************************************************************
RDAT0000  UNPACK    SP6,CYEAR,CMON,CDAY     * no defaults
          MOVE      CCC,CCENT
          MOVE      ONE,CCANLDTE            * cancel defaults
          MOVE      ONE,CDEFDTE             * once to accept default
          MOVE      "4",CVERT               * row
          MOVE      "23",CCOL               * col
          DISPLAY   *P1:3,*EF:
                    *P1:4,"Enter Starting date : ":
                    *P1:5,"Enter Ending   date : "
.
          CALL      KEYDATE
          BRANCH    OVRCD,RDAT9000          * nothing entered
          BRANCH    CQUEST,RDAT0000
.
          PACK      DATESTRT,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATESTRT           * set up start date
.
.         enter ending date (default to starting date)
.
RDAT2000  MOVE      "5",CVERT               * row
          UNPACK    DATESTRT,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,RDAT9000          * nothing entered
          BRANCH    CQUEST,RDAT2000
.
          PACK      DATESTOP,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATESTOP           * set up end date
.
          MATCH     DATESTRT,DATESTOP
          GOTO      RDAT3000 IF NOT LESS    * date is <= start date
.
          DISPLAY   *P1:24,*B,"Ending date must be after Starting date.  ",*EL;
          CALL      HITENTER 
          GOTO      RDAT2000
.
RDAT3000  CALL      KHOS0000                * keyin hospital (multi-campus only)
          BRANCH    EXIT,RDAT9000
.
.         OK to Continue
.
          CALL      CONTQST
          BRANCH    CEXIT,RDAT8000,RDAT0000,RDAT9000
.                         Yes      No       Cancel
.
RDAT8000  MOVE      ZERO,EXIT               * valid dates
          GOTO      RDAT9999
.
RDAT9000  MOVE      ONE,EXIT                * exit selected
.
RDAT9999  RETURN
+
.*********************************************************************
.*                  BWUD0000                    Called by : ML1000   *
.*        Report on all Bookings with no usage details               *
.*********************************************************************
BWUD0000  MOVE      ZERO,CPAGENO            * clear pageno
          MOVE      SP20,CURRSESS           * clear current session
          MOVE      "70",CLNO               * current line number
          MOVE      ZERO,CNTBOOK            * clear counter
          MOVE      ZERO,GRNDTOTL           * clear grand total
          CLOCK     TIME,CTIMEIS            * get time
          DISPLAY   *P1:24,"Booking Number : ",*EL;
          PACK      KEY26,DATESTRT,SP30
          CALL      RSOPDEA6
.
.         loop over operation details file
.
BWUD1000  CALL      RKOPDEA6
          BRANCH    OVRCD,BWUD7000          * end of details
.
          MATCH     OPDADATE,DATESTOP
          GOTO      BWUD7000 IF LESS        * after end date
.
          DISPLAY   *P18:24,OPDAADMN
.
          COMPARE   THREE,OPDASTAT
          GOTO      BWUD1000 IF EQUAL       * not a booking
.
.         have a booking, see if any usage details
.
          PACK      KEY10,OPDAUNIQ,SP70
          CALL      RDOPARD1
          COMPARE   ZERO,OVRCD
          GOTO      BWUD1000 IF EQUAL       * has usage, so not valid
.
          PACK      KEY11,OPDAUNIQ,SP1
          CALL      RSOPDEC1
          CALL      RKOPDEC1
          BRANCH    OVRCD,BWUD1500          * no usage so print person
.
          MATCH     OPDAUNIQ,OPDCUNIQ
          GOTO      BWUD1000 IF EQUAL       * has usage so not valid
.
BWUD1500  IF        IBCNMHOS=1
            MATCH     SP70,PTHSHOSP                * all hospitals ?
            GOTO      BWUD2000 IF EQUAL            * yes - ignore check
.
            PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
            CALL      RDOPSES1                     * session record found ?
            BRANCH    OVRCD,BWUD1000               * no - ignore record
.
            MATCH     OPSEHOSP,PTHSHOSP            * same hospital ?
            GOTO      BWUD1000 IF NOT EQUAL        * no - ignore record
          ENDIF
.
.         have a booking with no usage details
.
BWUD2000  DISPLAY   *P18:24,*V2LON,OPDAADMN
          CALL      LINA0000                * print details
          GOTO      BWUD1000
.
.         no more patients to be found
.
BWUD7000  ADD       CNTBOOK,GRNDTOTL        * add to grand total
          COMPARE   ZERO,GRNDTOTL
          GOTO      BWUD7500 IF NOT EQUAL   * people found
.
.         no people found
.
          CALL      HEAD0000                * print header
          PRINT     *N,"No patients found.":
                    *N,*N,"*** End of Report ***",*N
          GOTO      BWUD9999
.
BWUD7500  PRINT     "*---------------------------------------":
                    "---------------------------------------*":
                    *N,"Number of Bookings : ",CNTBOOK:
                    *N,*N,"Total Number of Bookings : ",GRNDTOTL:
                    *N,*N,"*** End of Report ***",*N
.
BWUD9999  RETURN
+
.*********************************************************************
.*                  BNOP0000                    Called by : ML2000   *
.*        Report on all Bookings with usage details but no operation *
.*        codes on file                                              *
.*********************************************************************
BNOP0000  MOVE      ZERO,CPAGENO            * clear pageno
          MOVE      SP20,CURRSESS           * clear current session
          MOVE      "70",CLNO               * current line number
          MOVE      ZERO,CNTBOOK            * clear counter
          MOVE      ZERO,GRNDTOTL           * clear grand total
          CLOCK     TIME,CTIMEIS            * get time
          DISPLAY   *P1:24,"Booking Number : ",*EL;
          PACK      KEY26,DATESTRT,SP30
          CALL      RSOPDEA6
.
.         loop over operation details file
.
BNOP1000  CALL      RKOPDEA6
          BRANCH    OVRCD,BNOP7000          * end of details
.
          MATCH     OPDADATE,DATESTOP
          GOTO      BNOP7000 IF LESS        * after end date
.
          DISPLAY   *P18:24,OPDAADMN
.
          COMPARE   THREE,OPDASTAT
          GOTO      BNOP1000 IF EQUAL       * not a booking
.
.         have a booking, see if any usage details
.
          PACK      KEY10,OPDAUNIQ,SP70
          CALL      RDOPARD1
          BRANCH    OVRCD,BNOP1200          * no usage so not valid
.
          PACK      KEY11,OPDAUNIQ,SP1
          CALL      RSOPSRG1
          CALL      RKOPSRG1
          BRANCH    OVRCD,BNOP1000          * no usage so not valid
.
          MATCH     OPDAUNIQ,OPSGUNID
          GOTO      BNOP1000 IF NOT EQUAL   * no usage so not valid
.
          UNPACK    SP70,OPDCICD1,OPDCICD2,OPDCICD3,OPDCICD4,OPDCICD5,OPDCICD6
.
          PACK      KEY14,OPSGUNID,OPSGTMNO,SP20
          CALL      RSOPPMB1
BNOP1100  CALL      RKOPPMB1
          BRANCH    OVRCD,BNOP1500
.
          MATCH     OPSGUNID,OPPMUNID
          GOTO      BNOP1500 IF NOT EQUAL
.
          MATCH     OPSGTMNO,OPPMTMNO
          GOTO      BNOP1500 IF NOT EQUAL
.
          STORE     OPPMCMBS,OPPMCNTR,OPDCICD1,OPDCICD2,OPDCICD3:
                                      OPDCICD4,OPDCICD5,OPDCICD6
.
          GOTO      BNOP1100
.
BNOP1200  PACK      KEY11,OPDAUNIQ,SP1
          CALL      RSOPDEC1
          CALL      RKOPDEC1
          BRANCH    OVRCD,BNOP1000          * no usage so print person
.
          MATCH     OPDAUNIQ,OPDCUNIQ
          GOTO      BNOP1000 IF NOT EQUAL   * no usage so not valid
.
.         have a booking with usage details, see if any operations
.
BNOP1500  ENDSET    OPDCICD1
          APPEND    SP10,OPDCICD1
          RESET     OPDCICD1
          MATCH     SP9,OPDCICD1
          GOTO      BNOP1000 IF NOT EQUAL   * have operation code
.
          ENDSET    OPDCICD2
          APPEND    SP10,OPDCICD2
          RESET     OPDCICD2
          MATCH     SP9,OPDCICD2
          GOTO      BNOP1000 IF NOT EQUAL   * have operation code
.
          ENDSET    OPDCICD3
          APPEND    SP10,OPDCICD3
          RESET     OPDCICD3
          MATCH     SP9,OPDCICD3
          GOTO      BNOP1000 IF NOT EQUAL   * have operation code
.
          ENDSET    OPDCICD4
          APPEND    SP10,OPDCICD4
          RESET     OPDCICD4
          MATCH     SP9,OPDCICD4
          GOTO      BNOP1000 IF NOT EQUAL   * have operation code
.
          ENDSET    OPDCICD5
          APPEND    SP10,OPDCICD5
          RESET     OPDCICD5
          MATCH     SP9,OPDCICD5
          GOTO      BNOP1000 IF NOT EQUAL   * have operation code
.
          ENDSET    OPDCICD6
          APPEND    SP10,OPDCICD6
          RESET     OPDCICD6
          MATCH     SP9,OPDCICD6
          GOTO      BNOP1000 IF NOT EQUAL   * have operation code
.
          IF        IBCNMHOS=1
            MATCH     SP70,PTHSHOSP                * all hospitals ?
            GOTO      BNOP2000 IF EQUAL            * yes - ignore check
.
            PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
            CALL      RDOPSES1                     * session record found ?
            BRANCH    OVRCD,BNOP1000               * no - ignore record
.
            MATCH     OPSEHOSP,PTHSHOSP            * same hospital ?
            GOTO      BNOP1000 IF NOT EQUAL        * no - ignore record
          ENDIF
.
BNOP2000  DISPLAY   *P18:24,*V2LON,OPDAADMN
          CALL      LINA0000                * print details
          GOTO      BNOP1000
.
.         no more patients to be found
.
BNOP7000  ADD       CNTBOOK,GRNDTOTL        * add to grand total
          COMPARE   ZERO,GRNDTOTL
          GOTO      BNOP7500 IF NOT EQUAL   * people found
.
.         no people found
.
          CALL      HEAD0000                * print header
          PRINT     *N,"No patients found.":
                    *N,*N,"*** End of Report ***",*N
          GOTO      BNOP9999
.
BNOP7500  PRINT     "*---------------------------------------":
                    "---------------------------------------*":
                    *N,"Number of Bookings : ",CNTBOOK:
                    *N,*N,"Total Number of Bookings : ",GRNDTOTL:
                    *N,*N,"*** End of Report ***",*N
.
BNOP9999  RETURN
+
.*********************************************************************
.*                  BWSF0000                    Called by : ML3000   *
.*        Report on all Bookings with no Statisical fields           *
.*********************************************************************
BWSF0000  MOVE      ZERO,CPAGENO            * clear pageno
          MOVE      SP20,CURRSESS           * clear current session
          MOVE      "70",CLNO               * current line number
          MOVE      ZERO,CNTBOOK            * clear counter
          MOVE      ZERO,GRNDTOTL           * clear grand total
          CLOCK     TIME,CTIMEIS            * get time
          DISPLAY   *P1:24,"Booking Number : ",*EL;
          PACK      KEY26,DATESTRT,SP30
          CALL      RSOPDEA6
.
.         loop over operation details file
.
BWSF1000  CALL      RKOPDEA6
          BRANCH    OVRCD,BWSF7000          * end of details
.
          MATCH     OPDADATE,DATESTOP
          GOTO      BWSF7000 IF LESS        * after end date
.
          DISPLAY   *P18:24,OPDAADMN
.
          COMPARE   THREE,OPDASTAT
          GOTO      BWSF1000 IF EQUAL       * not a booking
.
.         have a booking, see if any stats fields
.
          MATCH     SP3,OPDAANAE
          GOTO      BWSF1500 IF EQUAL       * no anaesthetic
.
          MATCH     SP3,OPDATYPE
          GOTO      BWSF1000 IF NOT EQUAL   * have an operation type
.
BWSF1500  IF        IBCNMHOS=1
            MATCH     SP70,PTHSHOSP                * all hospitals ?
            GOTO      BWSF2000 IF EQUAL            * yes - ignore check
.
            PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
            CALL      RDOPSES1                     * session record found ?
            BRANCH    OVRCD,BWSF1000               * no - ignore record
.
            MATCH     OPSEHOSP,PTHSHOSP            * same hospital ?
            GOTO      BWSF1000 IF NOT EQUAL        * no - ignore record
          ENDIF
.
.         patient has either no anaesthetic or operation code
.
BWSF2000  DISPLAY   *P18:24,*V2LON,OPDAADMN
          CALL      LINC0000                * print details
          GOTO      BWSF1000
.
.         no more patients to be found
.
BWSF7000  ADD       CNTBOOK,GRNDTOTL        * add to grand total
          COMPARE   ZERO,GRNDTOTL
          GOTO      BWSF7500 IF NOT EQUAL   * people found
.
.         no people found
.
          CALL      HEAD0000                * print header
          PRINT     *N,"No patients found.":
                    *N,*N,"*** End of Report ***",*N
          GOTO      BWSF9999
.
BWSF7500  PRINT     "*---------------------------------------":
                    "---------------------------------------*":
                    *N,"Number of Bookings : ",CNTBOOK:
                    *N,*N,"Total Number of Bookings : ",GRNDTOTL:
                    *N,*N,"*** End of Report ***",*N
.
BWSF9999  RETURN
+
.*********************************************************************
.*                  SWUD0000                    Called by : ML4000   *
.*        Report on all Sessions without Usage Details               *
.*********************************************************************
SWUD0000  MOVE      ZERO,CPAGENO            * clear pageno
          MOVE      SP10,SESNDATE           * clear current session date
          MOVE      "70",CLNO               * current line number
          MOVE      ZERO,CNTBOOK            * clear counter
          MOVE      ZERO,GRNDTOTL           * clear grand total
          CLOCK     TIME,CTIMEIS            * get time
          DISPLAY   *P1:24,"Session Date : ",*EL;
          PACK      KEY22,DATESTRT,SP30
          CALL      RSOPSES7
.
.         loop over session file
.
SWUD1000  CALL      RKOPSES7
          BRANCH    OVRCD,SWUD7000          * end of details
.
          MATCH     OPSEDATE,DATESTOP
          GOTO      SWUD7000 IF LESS        * after end date
.
          UNPACK    OPSEDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P16:24,CPCDATE
.
          COMPARE   ZERO,OPSESTAT
          GOTO      SWUD1000 IF EQUAL       * dont display cancelled sessions
.
.         see if any usage details
.
          MATCH     SP5,OPSEACTS
          GOTO      SWUD1000 IF NOT EQUAL   * has act. start time
.
          MATCH     SP5,OPSEACTE
          GOTO      SWUD1000 IF NOT EQUAL   * has act. end time
.
          COMPARE   ZERO,OPSEACTD
          GOTO      SWUD1000 IF NOT EQUAL   * has act. duration
.
          IF        IBCNMHOS=1
            MATCH     SP70,PTHSHOSP                * all hospitals ?
            GOTO      SWUD1500 IF EQUAL            * yes - ignore check
.
            MATCH     OPSEHOSP,PTHSHOSP            * same hospital ?
            GOTO      SWUD1000 IF NOT EQUAL        * no - ignore record
          ENDIF
.
.         have a session without usage
.
SWUD1500  DISPLAY   *P16:24,*V2LON,CPCDATE
          CALL      LIND0000                * print details
          GOTO      SWUD1000
.
.         no more sessions to be found
.
SWUD7000  ADD       CNTBOOK,GRNDTOTL        * add to grand total
          COMPARE   ZERO,GRNDTOTL
          GOTO      SWUD7500 IF NOT EQUAL   * people found
.
.         no sessions found
.
          CALL      HEAD0000                * print header
          PRINT     *N,"No Sessions found.":
                    *N,*N,"*** End of Report ***",*N
          GOTO      SWUD9999
.
SWUD7500  PRINT     "*---------------------------------------":
                    "---------------------------------------*":
                    *N,"Number of Sessions : ",CNTBOOK:
                    *N,*N,"Total Number of Sessions : ",GRNDTOTL:
                    *N,*N,"*** End of Report ***",*N
.
SWUD9999  RETURN
+
.*********************************************************************
.*                  LINA0000                    Called by : BWUD BNOP*
.*        Print a line of the report for Option 1,2,3         BWSF   *
.*********************************************************************
LINA0000  PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN
          MATCH     KEY22,CURRSESS
          GOTO      LINA2000 IF EQUAL       * same session
.
          MATCH     SP20,CURRSESS
          GOTO      LINA2000 IF EQUAL       * no last session
.
.         have a different session so finish off last session
.
          PRINT     "*---------------------------------------":
                    "---------------------------------------*",*N:
                    "Number of Bookings : ",CNTBOOK
          ADD       CNTBOOK,GRNDTOTL        * add to grand total
          MOVE      ZERO,CNTBOOK            * clear counter
          ADD       "2",CLNO                * add to line counter
          MOVE      KEY22,CURRSESS          * set current session
          MOVE      "1",CNOUNDLN            * don't print underlines
.
          COMPARE   "50",CLNO
          GOTO      LINA1000 IF LESS        * table will fit on page
.
          CALL      HEAD0000                * print new heading
          GOTO      LINA2000
.
LINA1000  CALL      TBLA0000                * print table heading
.
.         obtain and print patient details
.
LINA2000  MOVE      KEY22,CURRSESS          * set current session
          MOVE      "0",CNOUNDLN            * print underlines
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * need a new page
.
.         get the UR number for the visit from either bokrc1af or patmi1af
.
          MOVE      "No PMI Details",NAME33 * set for invalid name
          MOVE      SP8,AURNO               * clear UR number
          MOVE      OPDAADMN,KEY8
          CALL      RDMISS1                 * read admission file
          COMPARE   ZERO,OVRCD
          GOTO      LINA2500 IF EQUAL       * on the admission file
.
          CALL      RDBKREC1                * read bookings file
          BRANCH    OVRCD,LINA7000          * no UR number for patient
          MOVE      BKREURNO,AURNO          * set UR number
.
.         get PMI details
.
LINA2500  MATCH     ZEROUR,AURNO
          GOTO      LINA3000 IF EQUAL       * zero UR - read pram file
.
.         read master file
.
          MOVE      AURNO,KEY8              * UR number is key
          CALL      RDMAST1
          BRANCH    OVRCD,LINA7000          * invalid UR number
          GOTO      LINA5000
.
.         read pre-admission file
.
LINA3000  MOVE      OPDAADMN,KEY8           * booking number is key
          CALL      RDPRAM1
          BRANCH    OVRCD,LINA7000          * invalid booking number
.
LINA5000  MOVE      PSNAME,PACSNAME         * surname
          MOVE      PGNAME,PACGNAME         * surname
          MOVE      PTITL,PACTITLE          * title
          MOVE      TWO,PACFRMT             * format
          CALL      PACNAME
          MOVE      PACFNAME,NAME33         * set name
.
LINA7000  PRINT     "|",OPDAADMN," | ",OPDACASE,"  | ",AURNO," | ",NAME33:
                    " | ",OPDAEXPS," | ",OPDAEXPD," |"
          ADD       ONE,CLNO
          ADD       ONE,CNTBOOK             * add to booking counter
.
LINA9999  RETURN
+
.*********************************************************************
.*                  LINC0000                    Called by : BWUD BNOP*
.*        Print a line of the report for Option 1,2,3         BWSF   *
.*********************************************************************
LINC0000  PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN
          MATCH     KEY22,CURRSESS
          GOTO      LINC2000 IF EQUAL       * same session
.
          MATCH     SP20,CURRSESS
          GOTO      LINC2000 IF EQUAL       * no last session
.
.         have a different session so finish off last session
.
          PRINT     "*---------------------------------------":
                    "---------------------------------------*",*N:
                    "Number of Bookings : ",CNTBOOK
          ADD       CNTBOOK,GRNDTOTL        * add to grand total
          MOVE      ZERO,CNTBOOK            * clear counter
          ADD       "2",CLNO                * add to line counter
          MOVE      KEY22,CURRSESS          * set current session
          MOVE      "1",CNOUNDLN            * don't print underlines
.
          COMPARE   "50",CLNO
          GOTO      LINC1000 IF LESS        * table will fit on page
.
          CALL      HEAD0000                * print new heading
          GOTO      LINC2000
.
LINC1000  CALL      TBLC0000                * print table heading
.
.         obtain and print patient details
.
LINC2000  MOVE      KEY22,CURRSESS          * set current session
          MOVE      "0",CNOUNDLN            * print underlines
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * need a new page
.
.         get the UR number for the visit from either bokrc1af or patmi1af
.
          MOVE      "No PMI Details",NAME33 * set for invalid name
          MOVE      SP8,AURNO               * clear UR number
          MOVE      OPDAADMN,KEY8
          CALL      RDMISS1                 * read admission file
          COMPARE   ZERO,OVRCD
          GOTO      LINC2500 IF EQUAL       * on the admission file
.
          CALL      RDBKREC1                * read bookings file
          BRANCH    OVRCD,LINC7000          * no UR number for patient
          MOVE      BKREURNO,AURNO          * set UR number
.
.         get PMI details
.
LINC2500  MATCH     ZEROUR,AURNO
          GOTO      LINC3000 IF EQUAL       * zero UR - read pram file
.
.         read master file
.
          MOVE      AURNO,KEY8              * UR number is key
          CALL      RDMAST1
          BRANCH    OVRCD,LINC7000          * invalid UR number
          GOTO      LINC5000
.
.         read pre-admission file
.
LINC3000  MOVE      OPDAADMN,KEY8           * booking number is key
          CALL      RDPRAM1
          BRANCH    OVRCD,LINC7000          * invalid booking number
.
LINC5000  MOVE      PSNAME,PACSNAME         * surname
          MOVE      PGNAME,PACGNAME         * surname
          MOVE      PTITL,PACTITLE          * title
          MOVE      TWO,PACFRMT             * format
          CALL      PACNAME
          MOVE      PACFNAME,NAME33         * set name
.
LINC7000  PRINT     "|",OPDAADMN," | ",OPDACASE,"  | ",AURNO," | ",NAME33:
                    " | ",OPDATYPE,"  | ",OPDAANAE,"   |"
          ADD       ONE,CLNO
          ADD       ONE,CNTBOOK             * add to booking counter
.
LINC9999  RETURN
+
.*********************************************************************
.*                  LIND0000                    Called by : SWUD1500 *
.*        Print a line of the report for Option 4                    *
.*********************************************************************
LIND0000  MATCH     OPSEDATE,SESNDATE
          GOTO      LIND2000 IF EQUAL       * same date
.
          MATCH     SP8,SESNDATE
          GOTO      LIND2000 IF EQUAL       * no last date
.
.         have a different session so finish off last date
.
          PRINT     "*---------------------------------------":
                    "---------------------------------------*",*N:
                    "Number of Sessions : ",CNTBOOK
          ADD       CNTBOOK,GRNDTOTL        * add to grand total
          MOVE      ZERO,CNTBOOK            * clear counter
          ADD       "2",CLNO                * add to line counter
          MOVE      OPSEDATE,SESNDATE       * set current date
          MOVE      "1",CNOUNDLN            * don't print underlines
.
          COMPARE   "50",CLNO
          GOTO      LIND1000 IF LESS        * table will fit on page
.
          CALL      HEAD0000                * print new heading
          GOTO      LIND2000
.
LIND1000  CALL      TBLD0000                * print table heading
.
.         obtain and print patient details
.
LIND2000  MOVE      OPSEDATE,SESNDATE       * set current date
          MOVE      "0",CNOUNDLN            * print underlines
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * need a new page
.
          MOVE      OPSECLIN,KEY6           * set code
          CALL      CLIN0000                * get clinic/doct name
          MOVE      STAT0,STATUS
          LOAD      STATUS,OPSESTAT,STAT1,STAT2
          PRINT     "| ",OPSETIME," | ",OPSECLIN,SP1,NAME35," |    ",OPSEDURA:
                    "     | ",STATUS," |"
          ADD       ONE,CLNO
          ADD       ONE,CNTBOOK             * add to booking counter
.
LIND9999  RETURN
+
.*********************************************************************
.*                  HEAD0000                    Called by : LINA0000 *
.*        Print a page heading for the report               LIND0000 *
.*********************************************************************
HEAD0000  CALL      HEAD80 
          PRINT     *20,SUBOPTN
          UNPACK    DATESTRT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *20,"Starting Date : ",CPCDATE
          UNPACK    DATESTOP,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *20,"Ending   Date : ",CPCDATE
.
          IF        IBCNMHOS=1
            PRINT     *20,"Hospital      : ",PTHSNAME
          ENDIF
.  
          MOVE      "7",CLNO
          BRANCH    MOPTION,HEAD6000,HEAD6000,HEAD7000,HEAD8000
.
HEAD6000  CALL      TBLA0000                * print table heading
          GOTO      HEAD9999
.
HEAD7000  CALL      TBLC0000                * print table heading
          GOTO      HEAD9999
.
HEAD8000  CALL      TBLD0000                * print table heading
          GOTO      HEAD9999
.
HEAD9999  RETURN
+
.*********************************************************************
.*                  TBLA0000                    Called by : LINA0000 *
.*        Print the table heading for Option 1,2                     *
.*********************************************************************
TBLA0000  MATCH     SP20,CURRSESS
          GOTO      TBLA9999 IF EQUAL       * no current session 
.
          UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *N,"Session Date : ",CPCDATE," Start Time : ",OPDATIME:
                    SP1,*+,OPCNCDSC," : ",OPDACLIN:
                    *N,"*---------------------------------------":
                    "---------------------------------------*",*N:
                    "| Booking | Case | U/R No.  | Name",*65,"| Start | Durn |":
                    *N,"*---------------------------------------":
                    "---------------------------------------*"
          ADD       "5",CLNO                * add to line number
.
TBLA9999  RETURN
+
.*********************************************************************
.*                  TBLC0000                    Called by : LINC0000 *
.*        Print the table heading for Option 3                       *
.*********************************************************************
TBLC0000  MATCH     SP20,CURRSESS
          GOTO      TBLC9999 IF EQUAL       * no current session
.
          UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *N,"Session Date : ",CPCDATE," Start Time : ",OPDATIME:
                    SP1,*+,OPCNCDSC," : ",OPDACLIN:
                    *N,"*---------------------------------------":
                    "---------------------------------------*",*N:
                    "| Booking | Case | U/R No.  | Name",*65,"| Type | Anaes |":
                    *N,"*---------------------------------------":
                    "---------------------------------------*"
          ADD       "5",CLNO                * add to line number
.
TBLC9999  RETURN
+
.*********************************************************************
.*                  TBLD0000                    Called by : LIND0000 *
.*        Print the table heading for Option 4                       *
.*********************************************************************
TBLD0000  MATCH     SP20,CURRSESS
          GOTO      TBLD9999 IF EQUAL       * no current session
.
          MOVE      SP10,CPCDATE
          MATCH     SP8,SESNDATE
          GOTO      TBLD1000 IF EQUAL       * no date to display
.
          UNPACK    SESNDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
TBLD1000  PRINT     *N,"Session Date : ",CPCDATE:
                    *N,"*---------------------------------------":
                    "---------------------------------------*":
                    *N,"| Start | ",OPCNCDSC,*54,"| Sess. Dur'n | Status    |":
                    *N,"*---------------------------------------":
                    "---------------------------------------*"
          ADD       "5",CLNO                * add to line number
.
TBLD9999  RETURN
+
.*********************************************************************
.*                  CLIN0000                                         *
.*        Get clinic/surgeon name                                    *
.*        Para's  : KEY6          code to get name for               *
.*        Returns : NAME35        Clinic or Doctor Description       *
.*********************************************************************
CLIN0000  MATCH     SP6,KEY6
          GOTO      CLIN9000 IF EQUAL       * no code
.
          MOVE      "Invalid Doctor Code",NAME35
          BRANCH    OPCNCLSU,CLIN5000       * using doctor code
.
.         read clinic file
.
          CALL      RDOPCLI1                * check for valid clinic
          BRANCH    OVRCD,CLIN9999          * invalid code
.
          MOVE      OPCLDESC,NAME35         * set up description
.
          MATCH     SP6,OPCLDOCT 
          GOTO      CLIN9999 IF EQUAL       * not using doctors code
.
          MOVE      OPCLDOCT,KEY6           * set up key
.
.         read doctors file
.
CLIN5000  CALL      RDDOCT1                 * read doctors information
          BRANCH    OVRCD,CLIN9999          * invalid code
.
          MOVE      DSNAME,PACSNAME         * surname
          MOVE      DGNAME,PACGNAME         * surname
          MOVE      DTITL,PACTITLE          * title
          MOVE      TWO,PACFRMT             * format
          CALL      PACNAME
          MOVE      PACFNAME,NAME35         * set name
          GOTO      CLIN9999
.
CLIN9000  PACK      NAME35,SP30,SP10        * clear the description
.
CLIN9999  RETURN
+
.******************************************************************************
.*                                  KHOS0000       Called by:                 *
.*                For multi-hospital, get the hospital code                   *
.******************************************************************************
.
KHOS0000  COMPARE   ONE,IBCNMHOS                 * using multi-hospital ?
          GOTO      KHOS9999 IF NOT EQUAL        * no - finished
.
          DISPLAY   *P1:15,*EL,"Hospital Id               : ";
          MOVE      TWENTY9,ECOL
          MOVE      "15",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND                * Not Mandatory
.
          CALL      PATHSPKY                     * get code
          BRANCH    EXIT,KHOS2000:               * nothing input
                         KHOS9500                * exitchar input
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          DISPLAY   *P29:15,*V2LON,PTHSNAME;
          MOVE      ZERO,EXIT
          GOTO      KHOS9999
.
KHOS3000  DISPLAY   *P37:15,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
.
KHOS9999  RETURN
+
.
.         I/O's required
.
          INC       STD001IO/PBL            * standard IO
          INC       PATHSPKY/PBL
          INC       BOKRC1IO/INC            * bookings file
          INC       OPRARDIO/INC            * operation arrival and recovery
          INC       OPRSRGIO/INC            * operation surgical
          INC       OPRPMBIO/INC            
          INC       OPRCLIIO/INC            * clinic file
          INC       OPRDEAIO/INC            * operation details
          INC       OPRDECIO/INC            * operation usage details
          INC       OPRSESIO/INC            * session file
          INC       PATDO1IO/INC            * doctor file
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC            * master file
          INC       PATMI1IO/INC            * admission file
          INC       PMSVX1IO/INC            * admission file
          INC       PATPR1IO/INC            * pre-admission file
...............................................................................

.***************************************************************************
.* System   :   CASH RECEIPTING                                            *
.* Program  :   IBARCP21                                                   *
.* Desc     :   Cashier Enquiry                                            *
.***************************************************************************
.* Comments :                                                              *
.* Mods     :                                                              *
.*             V10.07.02 28/01/2016  J.Tan        CAR 0303942              *
.*                       Mods for additional payment types                 *
.*             V10.07.01 23/12/2015  J.Tan        CAR 0303942              *
.*                       Mods for additional payment types                 *
.*             V9.03.04  06/02/2004  Lina Vo                               *
.*                       Fixed Calculation of totals.  Was only reading    *
.*                       first row in rcpbdtaf.                            *
.*             V9.03.03  15/01/2004  J.TaN        CAR 44080                *
.*                       Fixed key for rcpbnk file                         *
.*             V9.03.02  01/12/2003  Lina Vo      CAR 44080                *
.*                       Changed program to use new receipting tables.     *
.*             V9.03.01  03/11/2003  Lina Vo      CAR 44080                *
.*                       Added Cashier & Start and End Enquiry date to     *
.*                       filter amounts processed.                         *
.*                       Changed display to also breakup amounts           *
.*                       to Released and Un-released.                      *
.*             V9.02.01  13/02/2003  Dean Cramer  CAR 22238                *
.*                        Recompiled for RCPTRNFD                          *
.*             V5.07.01   31.03.1999  Sandra Barcham                       *
.*                        Fix upplow                                       *
.*             V5.07.B01  29/10/1998  Davin                                *
.*                        Mods for 507                                     *
.*              V5.01.00  20/01/92  DIG                                    *
.*                        Ported from release 5                            *
.*              V5.01.01  05/07/94  ROD                                    *
.*                        recompile for rcptrnaf                           *
.*              V5.02.01  18/04/1995  Steve Armstrong                      *
.*                        Recompiled for changes to RCPTRNFD               *
.***************************************************************************
+
         INC          STD001FD
.
         INC          RCPCIDFD/INC
....         INC          RCPCSHFD/INC
....         INC          RCPTRNFD/INC
         INC          RCPBNKFD/INC
         INC          RCPBDTFD/INC
         INC          WEBSECFD/INC
         INC          PATCODFD/INC
.
FRSTDATE DIM          8
LASTDATE DIM          8
ENQDATE  DIM          8
CODEPY   INIT         "Py"
.
TOTCASH  FORM         10.2         * Total Released Cash
TOTCHQ   FORM         10.2         * Total Released Cheque
TOTCRED  FORM         10.2         * Total Released Credit
TOTDDBT  FORM         10.2         * Total Released Direct Debit
TOTMONOR FORM         10.2         * Total Released Money Order
TOTEFTPO FORM         10.2         * Total Released EFTPOS Cash
TOTEFTCR FORM         10.2         * Total Released EFTPOS Credit card
TOTBPAY  FORM         10.2         * Total BPAY
TOTHICAP FORM         10.2         * Total HICAPS
TOTMCARE FORM         10.2         * Total Medicare
TOTOTHR  FORM         10.2[99]     * Total other payment
.
TOTTOTAL FORM         10.2
TURCASH  FORM         10.2         * Total Un-released Cash
TURCHQ   FORM         10.2         * Total Un-released Cheque
TURCRED  FORM         10.2         * Total Un-released Credit Card
TURDDBT  FORM         10.2         * Total Un-released Direct Debit
TURMONOR FORM         10.2         * Total Un-released Money Order
TUREFTPO FORM         10.2         * Total Un-released EFTPOS Cash
TUREFTCR FORM         10.2         * Total Un-released EFTPOS Credit card
TURBPAY  FORM         10.2         * Total BPAY
TURHICAP FORM         10.2         * Total HICAPS
TURMCARE FORM         10.2         * Total Medicare
TUROTHR  FORM      10.2[99]        * Total other payment
.
PRGID     INIT      "IBARCP21"
PRGNAM    INIT      "Cashier Enquiry"
VERSION   INIT      "V12.01.00"
+
. Open Files
.
         CALL         DISPHEAD
         DISPLAY      *P56:24,"Opening rcpbnkaf";
         OPEN         RCPBNKA2,"rcpbnkaf"
         DISPLAY      *P56:24,"Opening rcpbdtaf";
         OPEN         RCPBDTA1,"rcpbdtaf"
         OPEN         PATCODE1,"patcodes"
         OPEN         PATCODE5,"patcodes"
.
..         DISPLAY      *P56:24,"Opening rcptrnaf";
..         OPEN         RCPTRNA1,"rcptrnaf"
.
         DISPLAY      *P64:24,"rcpcidaf";
         OPEN         RCPCIDA1,"rcpcidaf"
.
...         DISPLAY      *P64:24,"rcpcshaf";
...         OPEN         RCPCSHA1,"rcpcshaf"
.
         DISPLAY      *P64:24,"websecaf";
         OPEN         WEBSECA4,"websecaf"
.
         DISPLAY      *P64:24,"controlf";
         OPEN         CONTROLF,"controlf"
.
BEGIN    CALL         PREPVAR
         CALL         DISPSC
.
.        Cash Drawer
.
         KEYIN        *P23:4,*V2LON,RCPCASH,*P23:4,*DV,RCPCASH
         DISPLAY      *P23:4,*V2LON,RCPCASH;
.
         ENDSET       RCPCASH
         APPEND       SP6,RCPCASH
         RESET        RCPCASH
.
         MATCH        SP6,RCPCASH
         GOTO         ENDIT IF EQUAL
.
         MOVE         RCPCASH,KEY6
         CALL         RDCIDA1 
.
         BRANCH       OVRCD OF ENDNO
.
         PACK      PACTITLE,SP70
         MOVE      RCPGIVEN,PACGNAME
         MOVE      RCPSUR,PACSNAME
         MOVE      ONE,PACFRMT
         CALL      PACNAME
         DISPLAY      *P35:4,*V2LON,PACFNAME
.
.        Cashier
.
BEGIN100 KEYIN        *P23:5,*V2LON,WBSEUID,*P23:5,*DV,WBSEUID
         DISPLAY      *P23:5,*V2LON,WBSEUID;
.
         ENDSET       WBSEUID
         APPEND       SP6,WBSEUID
         RESET        WBSEUID
.
         MATCH        SP10,WBSEUID
         GOTO         BEGIN110 IF EQUAL
.
         PACK         KEY11,ONE,WBSEUID,SP70
         CALL         RDWBSE4
         BRANCH       OVRCD,BEGIN120
         GOTO         BEGIN130
.
BEGIN110 MOVE         "ALL CASHIERS",WBSENAM
         GOTO         BEGIN130
.
BEGIN120 DISPLAY      *P40:5,*V2LON:
                      "** Cashier Does Not Exist ** ";
         GOTO         BEGIN100
.
BEGIN130 DISPLAY      *P23:5,*EL,*V2LON,WBSEUID:
                      *P35:5,WBSENAM
.
.        Transaction Dates
.
         MOVE         TWENTY3,CCOL
         MOVE         SIX,CVERT
         MOVE         ONE,ECOL
         MOVE         TWENTY4,EVERT
         MOVE         ZERO,CHIGHLT
         MOVE         ZERO,CDEFDTE
.
         CALL         KEYDATE
         PACK         FRSTDATE,CCENT,CYEAR,CMON,CDAY
         REPLACE      " 0",FRSTDATE
.
         MOVE         SEVEN,CVERT
         CALL         KEYDATE
         PACK         LASTDATE,CCENT,CYEAR,CMON,CDAY
         REPLACE      " 0",LASTDATE
.
         PACK         KEY26 WITH FRSTDATE,RCPCASH,SP10
         CALL         RSRCBNK2
.
BEGIN200 CALL         RKRCBNK2
         BRANCH       OVRCD,SFIELD
.
         MATCH        RCBNTDAT,LASTDATE
         GOTO         SFIELD IF LESS
.
         MATCH        RCPCASH,RCBNCDRW
         GOTO         BEGIN200 IF NOT EQUAL
.
         MATCH        SP10,WBSEUID
         IF           !@EQUAL
           MATCH        WBSEUID,RCBNCUID
           GOTO         BEGIN200 IF NOT EQUAL
         ENDIF
.
         MATCH        "1",RCBNSTAT                * Cancelled
         GOTO         BEGIN200 IF EQUAL
.
         PACK         KEY15,RCBNRECN,SP70
         CALL         RSRCBDT1
BEGIN210 CALL         RKRCBDT1
         BRANCH       OVRCD,BEGIN200
.
         MATCH        RCBDRECN,RCBNRECN
         GOTO         BEGIN200 IF NOT EQUAL
.
         MOVE         ZERO,F1
         MOVE         RCBDPTYP,F1
         MOVE         F1,F2
         PACK         KEY5,CODEPY,RCBDPCOD
         CALL         RDCODE1
         IF           OVRCD=0
           SQUEEZE      PTCDNHCP
           MOVE         PTCDNHCP,F2
         ENDIF
         BRANCH       F2,TYPE1,TYPE2,TYPE3,TYPE4,TYPE5,TYPE6:
                         TYPE7,TYPE8,TYPE9
.
         COMPARE      ZERO,F2
         GOTO         BEGIN210 IF EQUAL
.
         SUB          NINE,F2
         ADD          RCBDPAMT,TOTTOTAL
         MATCH        "0",RCBNSTAT
         IF           @EQUAL
           ADD          RCBDPAMT,TUROTHR[F2]    * Total Un-released other paym.
         ELSE
           ADD          RCBDPAMT,TOTOTHR[F2]    * Total Released
         ENDIF
         GOTO         BEGIN210
.
TYPE1    ADD          RCBDPAMT,TOTTOTAL
         MATCH        "0",RCBNSTAT
         IF           @EQUAL
           ADD          RCBDPAMT,TURCASH          * Un-released
         ELSE
           ADD          RCBDPAMT,TOTCASH          * Released
         ENDIF
         GOTO         BEGIN210
.
TYPE2    ADD          RCBDPAMT,TOTTOTAL
         MATCH        "0",RCBNSTAT
         IF           @EQUAL
           ADD          RCBDPAMT,TURCHQ
         ELSE
           ADD          RCBDPAMT,TOTCHQ
         ENDIF
         GOTO         BEGIN210
.
TYPE3    ADD          RCBDPAMT,TOTTOTAL
         MATCH        "0",RCBNSTAT
         IF           @EQUAL
           ADD          RCBDPAMT,TURCRED
         ELSE
           ADD          RCBDPAMT,TOTCRED
         ENDIF
         GOTO         BEGIN210
.
TYPE4    ADD          RCBDPAMT,TOTTOTAL
         MATCH        "0",RCBNSTAT
         IF           @EQUAL
           ADD          RCBDPAMT,TURDDBT 
         ELSE
           ADD          RCBDPAMT,TOTDDBT 
         ENDIF
         GOTO         BEGIN210
.
TYPE5    ADD          RCBDPAMT,TOTTOTAL
         MATCH        "0",RCBNSTAT
         IF           @EQUAL
           ADD          RCBDPAMT,TURMONOR
         ELSE
           ADD          RCBDPAMT,TOTMONOR
         ENDIF
         GOTO         BEGIN210
.
TYPE6    ADD          RCBDPAMT,TOTTOTAL
         MATCH        "0",RCBNSTAT
         IF           @EQUAL
           MATCH        "1",RCBDEFTT
           IF           @EQUAL
             ADD          RCBDPAMT,TUREFTPO
           ELSE
             ADD          RCBDPAMT,TUREFTCR
           ENDIF
         ELSE
           MATCH        "1",RCBDEFTT
           IF           @EQUAL
             ADD          RCBDPAMT,TOTEFTPO
           ELSE
             ADD          RCBDPAMT,TOTEFTCR
           ENDIF
         ENDIF
         GOTO         BEGIN210
.
TYPE7    ADD          RCBDPAMT,TOTTOTAL
         MATCH        "0",RCBNSTAT
         IF           @EQUAL
           ADD          RCBDPAMT,TURBPAY
         ELSE
           ADD          RCBDPAMT,TOTBPAY
         ENDIF
         GOTO         BEGIN210
.
TYPE8    ADD          RCBDPAMT,TOTTOTAL
         MATCH        "0",RCBNSTAT
         IF           @EQUAL
           ADD          RCBDPAMT,TURHICAP
         ELSE
           ADD          RCBDPAMT,TOTHICAP
         ENDIF
         GOTO         BEGIN210
.
TYPE9    ADD          RCBDPAMT,TOTTOTAL
         MATCH        "0",RCBNSTAT
         IF           @EQUAL
           ADD          RCBDPAMT,TURMCARE
         ELSE
           ADD          RCBDPAMT,TOTMCARE
         ENDIF
         GOTO         BEGIN210
.
ENDNO    DISPLAY      *P40:4,*V2LON:
                      "** Cash Drawer Does Not Exist ** ";
         KEYIN        ANS;
         GOTO         CHKNXT1
.
SFIELD   CALL         DISPDA
.
         KEYIN        *P1:24,*EL,"(":
                      *V2LON,"P",*HOFF,")rint ,(":
                      *V2LON,"N",*HOFF,")ext ,e(":
                      *V2LON,"X",*HOFF,")it ? ",*V2LON,ANS;
         REP          UPPLOW,ANS
.
         CMATCH       "P",ANS
         GOTO         CHKNXT1 IF NOT EQUAL
.
         CALL         PRINTS
         GOTO         SFIELD
.
CHKNXT1  CMATCH       "E",ANS
         GOTO         ENDIT IF EQUAL
         CMATCH       "X",ANS
         GOTO         ENDIT IF EQUAL
.
         DISPLAY      *P1:4,*EF
         GOTO         BEGIN
.
. Display Main Menu
.
DISPSC   DISPLAY      *P1:4,*EF:
                      *P1:4,"Cash Drawer         :":
                      *P1:5,"Cashier             :":
                      *P1:6,"Enquiry Start Date  :":
                      *P1:7,"Enquiry End Date    :":
                      *P1:9,"Cash Drawer Totals  :":
                      *P25:11,*ULON,"   Not Banked":
                      *P39:11,"       Banked",*HOFF:
                      *P1:12,"Cash                :":
                      *P1:13,"Cheque              :":
                      *P1:14,"Credit Cards        :":
                      *P1:15,"Direct Debit        :":
                      *P1:16,"Money Orders        :":
                      *P1:17,"EFTPOS              :"
.
         RETURN
.
. Display Data Onto The Screen
.
DISPDA   DISPLAY      *P23:9,*V2LON,TOTTOTAL:
                      *P25:12,TURCASH:
                      *P39:12,TOTCASH:
                      *P25:13,TURCHQ:
                      *P39:13,TOTCHQ:
                      *P25:14,TURCRED:
                      *P39:14,TOTCRED:
                      *P25:15,TURDDBT: 
                      *P39:15,TOTDDBT: 
                      *P25:16,TURMONOR:
                      *P39:16,TOTMONOR:
                      *P25:17,TUREFTPO: 
                      *P39:17,TOTEFTPO
         RETURN
+
PRINTS   DISPLAY      *P1:24,*EL,*P40:24,*V2LON,"** Generating **",*W2;
         CLOCK        TIME,CTIMEIS
.
         UNPACK       FRSTDATE,CCENT,CYEAR,CMON,CDAY
         CALL         PACDATE
         MOVE         CPCDATE,KEY10
.
         UNPACK       LASTDATE,CCENT,CYEAR,CMON,CDAY
         CALL         PACDATE
.
         CALL         HEAD80
         PRINT        *N,*4,"Cash Drawer         : ":
                      RCPCASH,*37,PACFNAME:
                      *N,*N,*4,"Cashier             : ":
                      WBSEUID,*37,WBSENAM:
                      *N,*N,*4,"Enquiry Date        : ",KEY10:
                               "  To  ",CPCDATE:
                      *N,*N,*N,*4,"Cash Drawer Totals  :",TOTTOTAL:
                      *N,*N,*28,"   Not Banked                 Banked":
                      *N,*28,"--------------         --------------";
.
         PACK         KEY9,CODEPY,SP70
         CALL         RDSCODE5
PRIN100  CALL         RDKCODE5
         BRANCH       OVRCD,LOWLEV
.
         MATCH        CODEPY,TCODE
         GOTO         LOWLEV IF NOT EQUAL
.
         MATCH        "A",PTCOACTV
         GOTO         PRIN100 IF NOT EQUAL        * Not active
.
         MOVE         ZERO,F2
         SQUEEZE      PTCDNHCP
         MOVE         PTCDNHCP,F2
         BRANCH       F2,PRIN101,PRIN102,PRIN103,PRIN104,PRIN105:
                         PRIN106,PRIN107,PRIN108,PRIN109
.
         COMPARE      ZERO,F2
         GOTO         PRIN100 IF EQUAL
         SUB          NINE,F2
         PRINT        *N,*N,*4,TDESC," : ",*28,TUROTHR[F2],*51,TOTOTHR[F2];
         GOTO         PRIN100
.
PRIN101  PRINT        *N,*N,*4,TDESC," : ",*28,TURCASH,*51,TOTCASH;
         GOTO         PRIN100
.
PRIN102  PRINT        *N,*N,*4,TDESC," : ",*28,TURCHQ,*51,TOTCHQ;
         GOTO         PRIN100
.
PRIN103  PRINT        *N,*N,*4,TDESC," : ",*28,TURCRED,*51,TOTCRED;
         GOTO         PRIN100
.
PRIN104  PRINT        *N,*N,*4,TDESC," : ",*28,TURDDBT,*51,TOTDDBT;
         GOTO         PRIN100
.
PRIN105  PRINT        *N,*N,*4,TDESC," : ",*28,TURMONOR,*51,TOTMONOR;
         GOTO         PRIN100
.
PRIN106  UNPACK       TDESC,KEY15
         PRINT        *N,*N,*4,KEY15,"-Cash : ",*28,TUREFTPO,*51,TOTEFTPO;
.
         PRINT        *N,*N,*4,KEY15,"-Cred : ",*28,TUREFTCR,*51,TOTEFTCR;
         GOTO         PRIN100
.
PRIN107  PRINT        *N,*N,*4,TDESC," : ",*28,TURBPAY,*51,TOTBPAY;
         GOTO         PRIN100
.
PRIN108  PRINT        *N,*N,*4,TDESC," : ",*28,TURHICAP,*51,TOTHICAP;
         GOTO         PRIN100
.
PRIN109  PRINT        *N,*N,*4,TDESC," : ",*28,TURMCARE,*51,TOTMCARE;
         GOTO         PRIN100
.
LOWLEV   PRINT        *N,*N,*N,*1,"*** End Of Enquiry ***"
.
         RETURN
.
PREPVAR  MOVE         CDD,CDAY
         MOVE         CMM,CMON
         MOVE         CYY,CYEAR
         MOVE         CCC,CCENT
.
         MOVE         TWENTY3,CCOL
         MOVE         SIX,CVERT
         MOVE         ONE,ECOL
         MOVE         TWENTY4,EVERT
         MOVE         ZERO,CHIGHLT
         MOVE         ZERO,CDEFDTE
.
         MOVE         ZERO,TOTCASH
         MOVE         ZERO,TOTCHQ
         MOVE         ZERO,TOTCRED
         MOVE         ZERO,TOTDDBT
         MOVE         ZERO,TOTMONOR
         MOVE         ZERO,TOTEFTPO
         MOVE         ZERO,TOTEFTCR
         MOVE         ZERO,TOTBPAY
         MOVE         ZERO,TOTHICAP
         MOVE         ZERO,TOTMCARE
         MOVE         ONE,F2
         WHILE        F2<99
           MOVE         ZERO,TOTOTHR[F2] * Total Not Banked for other payment
           MOVE         ZERO,TUROTHR[F2] * Total Banked for other payment
           ADD          ONE,F2
         DO
         MOVE         ZERO,TURCASH
         MOVE         ZERO,TURCHQ
         MOVE         ZERO,TURCRED
         MOVE         ZERO,TURDDBT
         MOVE         ZERO,TURMONOR
         MOVE         ZERO,TUREFTPO
         MOVE         ZERO,TUREFTCR
         MOVE         ZERO,TURBPAY
         MOVE         ZERO,TURHICAP
         MOVE         ZERO,TURMCARE
         MOVE         ZERO,TURBPAY
         MOVE         ZERO,TURHICAP
         MOVE         ZERO,TURMCARE
         MOVE         ZERO,TOTTOTAL
.
         RETURN
.
ENDIT    CHAIN      PGM
         STOP
+
         INC        RCPCIDIO/INC
...         INC        RCPCSHIO/INC
...         INC        RCPTRNIO/INC
         INC        RCPBNKIO/INC
         INC        RCPBDTIO/INC
         INC        WEBSECIO/INC
         INC        PATCODIO/INC
.
         INC        STD001IO

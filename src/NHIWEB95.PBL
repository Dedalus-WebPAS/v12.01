.----------------------------------------------------------------------
. Program       : NHIWEB95
.
. Function      : NHI Web Server (NMDS / NBRS Management)
.                 
. Modifications : 
.----------------------------------------------------------------------
. V11.02.01 10/02/2022  Thanh T          TSK 0889899
.           Recompiled as WATTR1FD changes
. V11.00.01 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
.----------------------------------------------------------------------
.     V10.14.01  18/09/2019  Peter Vela     TSK 0870848
.                Added Hospital validation to TABWTD21
.     V10.13.01  01/08/2018  Thanh T        TSK 0860301
.                Removed the check for PTHSHDPC from TABWTD10 and TABNMD10
.     V10.12.02  17/07/2018  Thanh T        TSK 0860301
.                Corrected TABNMD00 GOTO label from TABWTD10 to TABNMD10
.     V10.12.01  23/05/2018  Don Nguyen     TSK 0857481
.                Modified TABWTD00 (NBRS Transmissions List) & 
.                TABNMD00 (NMDS Transmissions List) to only output records for
.                a matching Health Department code.
.     V10.11.02  27/11/2017  Thanh Tieu     Task 0821710
.                Recompiled as PATMASTM changed
.     V10.11.01  30/07/2017  Thanh T.       TSK 0821710
.                Audit admission/outpatient/UR comments. changed 
.                PATMASTM/PMSPX2TM.
.----------------------------------------------------------------------
.     V10.08.01  23/03/2016  Don Nguyen     TSK 0303887
.                Use different key sizes for read/write 'pmsnmdaf' & 'watnbdaf'.
.                Modified TABWTD00 to include NBRS details records with a status
.                of '6' (Accepted with Warning) when filtering on 'Accepted'.
.                Changed size of NBRRECKY to KEY31.
.     V10.07.02  02/03/2016  Don Nguyen     TSK 0303887
.                Modified TABWTD00 to show LinkStatus icon always.
.                Modified ERRFIL00 to use NMDS/NBRS extracts directories.
.                Changed error file extension to be uppercase for NBRS.
.     V10.07.01  19/02/2016  Don Nguyen     TSK 0303887
.                Moved update code for NMDS extract status to UPNMDS00.
.                Modified OUTFIL00 to use parameter values for Extract dir.
.                Removed check on processing status; always output link to
.                extract details, not just for 'Ack File Loaded' status.
.                Output icon image filenames (LookupIcon.gif & Delete.gif).
.
.     V10.06.00  11/06/2015  Don Nguyen     CAR 303887
.                Created Web Server
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
.
          INC       APSMASFD/INC       * Creditor/Supplier Master File
          INC       MRTMASFD/INC       * MRT Master Details
          INC       NHIETHFD/INC       * NHI Ethnicity Codes
          INC       NHIMASFD/INC       * NHI/MWS HCU Details
          INC       OUTSITFD/INC       * Outpatients Site Code
          INC       PATALRFD/INC       * Patient Alerts
          INC       PATCALAG/INC       * Data variable definitions for CALCAGE
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATCONFD/INC       * Control File
          INC       PATDO1FD/INC       * Doctor File
          INC       PATFN1FD/INC       * Patient Health Fund
          INC       PATHSPFD/INC       * Hospital Details
          INC       PATIN1FD/INC       * Insurance Details
          INC       PATMA1FD/INC       * Patient Master File
          INC       PATMI1FD/INC       * Patient Admissions File
          INC       PATVISFD/INC       * Patient Visit File
          INC       PMSVX1FD/INC       * Patient Visit Extension File
          INC       PATPR1FD/INC       * Pre-Admission Details
          INC       PATWR1FD/INC       * Patient Ward/Bed Details
.
          INC       PMSCCDFD/INC   * Concession Card Details
          INC       PMSHCGFD/INC       * HCP Practice
          INC       PMSHCGTD/INC       * CGI vars for PMSHCGFD
          INC       PMSHCPFD/INC       * Healthcare Profession
          INC       PMSHCPTD/INC       * CGI variables for PMSHCPFD
          INC       PMSNMDFD/INC       * NMDS Extract Records
          INC       PMSNMHFD/INC       * NMDS Transmissions File Batches
          INC       PMSPX2FD/INC       * PMI extension table
          INC       PMSPX2TD/INC       * CGI variables for PMSPX2FD
          INC       WATNBDFD/INC       * NBRS Extract Records
          INC       WATNBHFD/INC       * NBRS Transmission File Batches
          INC       WATTR1FD/INC       * Waiting List Patient Procedures
          INC       WATHISFD/INC       * Waiting List History File
.
.----------------------------------------------------------------------
NEXTPAGE  DIM       1        * Nextpage parameter CGI parameter
UPDTTYPE  DIM       1        * Update Type
UPDALLFL  DIM       1        * Update All Flag
STARTKEY  DIM       10       * Starting Key
CASEKEYS  DIM       19       * Casekeys for WL entry
CURRDATE  DIM       8        * Current Date
EXECCMD   DIM       127
FORM8     FORM      8 
DOCTCODE  DIM       8
PSAGETYP  DIM       3
PSAGECON  DIM       3
.
BATCHNUM  DIM       5        * Batch number
DISPDATE  DIM       20       * Date & Time string (DD MMM YYYY hh:mm:ss)
DIM3      DIM       3
DEFTHDHB  DIM       1        * Flag to default hospital DHB code
DEFTSTAT  DIM       1        * Flag to default status
ERRORFIL  DIM       12       * Error filename
EXTFILNM  DIM       12       * Extract filename (with extension)
FILELINE  DIM       500      * Extract file line length
FILTSTAT  DIM       1        * Processing Status filter
FILTRDHB  DIM       3        * DHB filter
FULLFPTH  DIM       100      * Input file path (full)
ICONFILN  DIM       50       * Icon (full filename)
OUTFILNM  DIM       12       * Output filename (extract contents)
.
RECCNTR1  FORM      5
RECCNTR2  FORM      5
RECCNTR3  FORM      5
RECCNTR4  FORM      5
RECCNTR5  FORM      5
RECCNTR6  FORM      5
RECRDKEY  DIM       17       * pmsnmhaf / watnbhaf record key (index 1)
NMDRECKY  DIM       19       * pmsnmdaf record key (index 1)
NBRRECKY  DIM       31       * watnbdaf record key (index 1)
.
STATCODE  DIM       1        * Status Code 1-5
STATSTRG  DIM       40       * Status string
STATSENT  INIT      "Sent"
STATACCP  INIT      "Accepted"
STATACKL  INIT      "Ack File Loaded"
STATACWN  INIT      "Accepted with Warning"
STATCANC  INIT      "Cancelled"
STATCORR  INIT      "Corrected"
STATERRO  INIT      "Error"
STATERRP  INIT      "Error Processing"
STATCOMP  INIT      "Completed"
STATFILF  INIT      "File Failure"
.
FILELNK1  DIM       127      * <a> HTML file link
FILELNK2  DIM       127      * <a> HTML file link
HTMLLNK2  DIM       127
HTMLLNK3  DIM       127
.
PMNH0001  DIM       1        * Processing Status (pmsnmhaf)
PMND0001  DIM       1        * Processing Status (pmsnmdaf)
WTNH0001  DIM       1        * Processing Status (watnbhaf)
WTND0001  DIM       1        * Processing Status (watnbdaf)
.
CAT_dh    INIT      "dh"
.
SP100     INIT      "                                                  ":
                    "                                                  "
.
INPTFILE  FILE      ASCII, FIXED=4065
.
.
.----------------------------------------------------------------------
PRGID     INIT      "NHIWEB95"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "NHI Web Server (NMDS / NBRS)"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      NMDSTR00        * NMDS Transmissions List
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      NMDSDT00        * NMDS Details
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1300  CALL      NBRSTR00        * NBRS Transmissions List
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1400  CALL      NBRSDT00        * NBRS Details
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
+
.------------------------------------------------------------
.         Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
+
.------------------------------------------------------------------------------
.         Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
+
.-------------------------------------------------------------------------------
.         Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
+
.------------------------------------------------------------
.         Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      CONTROLF,"controlf"     * Inpatient CONTROL File
.
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"     
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PMSCCDA1,"pmsccdaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      MRTMASA1,"mrtmasaf"
          OPEN      MRTMASA2,"mrtmasaf"
          OPEN      PMSNMHA1,"pmsnmhaf"     * NMDS Transmissions File Batches
          OPEN      PMSNMDA1,"pmsnmdaf"     * NMDS Extract Records
          OPEN      PMSNMDA4,"pmsnmdaf"     * NMDS Extract Records
          OPEN      WATNBHA1,"watnbhaf"     * NBRS Transmissions File Batches
          OPEN      WATNBDA1,"watnbdaf"     * NBRS Extract Records
          OPEN      WATNBDA4,"watnbdaf"     * NMDS Extract Records
          OPEN      WATTR1A1,"wattr1af"     * Waiting List Patient Procedures
          OPEN      WATHISA1,"wathisaf"     * Waiting List History File
.
INIT9999  RETURN
+
.------------------------------------------------------------
.         Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,NEXTPAGE
          MOVE      SP70,REDIRECT
          MOVE      SP70,STARTKEY
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,URNUMBER
          MOVE      SP70,ADMISSNO
.
          MOVE      SP70,FILTSTAT
          MOVE      SP70,FILTRDHB
          MOVE      SP70,DEFTHDHB
          MOVE      SP70,DEFTSTAT
          MOVE      SP70,EXTFILNM
          MOVE      SP70,OUTFILNM
          MOVE      SP70,RECRDKEY
          MOVE      SP70,NMDRECKY
          MOVE      SP70,NBRRECKY
          MOVE      ZERO,PMNH0001
          MOVE      ZERO,PMND0001
          MOVE      ZERO,WTNH0001
          MOVE      ZERO,WTND0001
          MOVE      SP70,UPDALLFL
.
CLRPAR99  RETURN
+
.------------------------------------------------------------
.         Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.  
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF 
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtstat=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FILTSTAT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtrdhb=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FILTRDHB
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "defthdhb=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DEFTHDHB
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "deftstat=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DEFTSTAT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "outfilnm=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,OUTFILNM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "recrdkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,RECRDKEY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nmdrecky=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NMDRECKY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nbrrecky=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NBRRECKY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmnh0001=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PMNH0001
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmnd0001=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PMND0001
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "wtnh0001=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,WTNH0001
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "wtnd0001=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,WTND0001
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updallfl=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDALLFL
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
+
.------------------------------------------------------------
.         NMDS Transmissions
.------------------------------------------------------------
NMDSTR00  CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          CLOCK     TIME,CTIMEIS
.
          MATCH     SP70,UPDTTYPE
          GOTO      NMDSTR70 IF EQUAL
.
          MATCH     "U",UPDTTYPE
          GOTO      NMDSTR30 IF EQUAL
.
          GOTO      NMDSTR90
.
NMDSTR30  PACK      KEY17,RECRDKEY,SP70
          CALL      RDPMNMH1
          IF        OVRCD=0
            MOVE      PMNH0001,PMNHSTAT
            MOVE      WBSEUID,PMNHUSRS
            MOVE      CURRDATE,PMNHUDAT
            MOVE      CTIMEIS,PMNHUTIM
            CALL      UPPMNMH1
          ENDIF
          GOTO      NMDSTR99
.
NMDSTR70  PACK      KEY3,WBSEHOSP,SP70
          CALL      RDPTHSP1
          IF        OVRCD=1
            CALL      CLPATHSP
          ENDIF
.
          PACK      KEY17,RECRDKEY,SP70
          CALL      RDPMNMH1
          IF        OVRCD=1
            CALL      CLPMSNMH
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "NMDS Transmissions",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,NMDSTR99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      NMDSTR99
.
NMDSTR90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NMDSTR99
.
NMDSTR99  RETURN
+
.------------------------------------------------------------
.         NMDS Transmissions Details
.------------------------------------------------------------
NMDSDT00  CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          CLOCK     TIME,CTIMEIS
.
          MATCH     SP70,UPDTTYPE
          GOTO      NMDSDT70 IF EQUAL
.
          MATCH     "U",UPDTTYPE
          GOTO      NMDSDT30 IF EQUAL
.
          GOTO      NMDSDT90
.
NMDSDT30  CALL      UPNMDS00           * Update NMDS extract status
          GOTO      NMDSDT99
.
NMDSDT70  PACK      KEY3,WBSEHOSP,SP70
          CALL      RDPTHSP1
          IF        OVRCD=1
            CALL      CLPATHSP
          ENDIF
.
          PACK      KEY17,RECRDKEY,SP70
          CALL      RDPMNMH1
          IF        OVRCD=1
            CALL      CLPMSNMH
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "NMDS Transmissions Details",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,NMDSDT99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      NMDSDT99
.
NMDSDT90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NMDSDT99
.
NMDSDT99  RETURN
+
.------------------------------------------------------------
.         Update NMDS Extract Record Status
.------------------------------------------------------------
UPNMDS00  PACK      KEY19,NMDRECKY,SP70
          CALL      RDPMNMD1
          BRANCH    OVRCD,UPNMDS99
.
          MOVE      PMNDADMN,ADMISSNO
          MOVE      PMNDBTCH,BATCHNUM
.
          MOVE      ZERO,F1
          MOVE      PMND0001,F1        * Store the NMDS status value (cgi)
.
          MATCH     "1",UPDALLFL       * Update all records?
          GOTO      UPNMDS20 IF EQUAL
.
UPNMDS10  IF        F1=4
            MOVE      SIX,PMNDSTAT     * Cancelled
          ELSE
            MOVE      FOUR,PMNDSTAT    * Corrected
          ENDIF
.
          MOVE      WBSEUID,PMNDUSRS
          MOVE      CURRDATE,PMNDUDAT
          MOVE      CTIMEIS,PMNDUTIM
          CALL      UPPMNMD1
          GOTO      UPNMDS30
.
UPNMDS20  PACK      KEY19,BATCHNUM,SP70
          CALL      RSPMNMD4
UPNMDS21  CALL      RKPMNMD4
          BRANCH    OVRCD,UPNMDS30
.
          MATCH     BATCHNUM,PMNDBTCH
          GOTO      UPNMDS30 IF NOT EQUAL
.
          MATCH     ADMISSNO,PMNDADMN
          GOTO      UPNMDS21 IF NOT EQUAL
.
          IF        F1=4
            MOVE      SIX,PMNDSTAT     * Cancelled
          ELSE
            MOVE      FOUR,PMNDSTAT    * Corrected
          ENDIF
.
          MOVE      WBSEUID,PMNDUSRS
          MOVE      CURRDATE,PMNDUDAT
          MOVE      CTIMEIS,PMNDUTIM
          CALL      UPPMNMD4
          GOTO      UPNMDS21
.
UPNMDS30  PACK      KEY8,ADMISSNO,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,UPNMDS99
.
          IF        F1 <> 4
            MOVE      PMND0001,AELIG   * Update AELIG flag if NOT cancelled
            CALL      UPMISS1
          ENDIF
.
UPNMDS99  RETURN
+
.------------------------------------------------------------
.         Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD10,PROFLD10,PROFLD10
.
          GOTO      PROFLD99 
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12
          GOTO      PROFLD99
.
PROFLD11  CALL      MASF0000           * Master File Info
          GOTO      PROFLD99
.
PROFLD12  CALL      MSCF0000           * NMDS / NBRS fields
          GOTO      PROFLD99
.
PROFLD99  RETURN
+
.------------------------------------------------------------
.         Process NMDS / NBRS fields
.------------------------------------------------------------
MSCF0000  BRANCH    FLDITMNO,MSCF0010,MSCF0020,MSCF0030,MSCF0040,MSCF0050:
                             MSCF0060,MSCF0070,MSCF0080,MSCF0090,MSCF0100:
                             MSCF0110,MSCF0120,MSCF0130,MSCF0140,MSCF0150:
                             MSCF0160,MSCF0170,MSCF0180,MSCF0190
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      MSCF9999
.
MSCF0010  CALL      TABNMH00                * NMDS Transmissions List
          GOTO      MSCF9999
.
MSCF0020  MOVE      CAT_dh,CATEGORY
          MATCH     "1",DEFTHDHB
          IF        @EQUAL
            PACK      KEY5,CAT_dh,PTHSHDHB,SP70  * Use Hospital DHB Code
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      THCSCOD,FILTRDHB         * HDP Default for DHB
            ENDIF
          ENDIF
          MOVE      FILTRDHB,CODE
          CALL      CODITM00
          GOTO      MSCF9999
.
MSCF0030  WRITE     HTMLFILE;FILTSTAT;
          GOTO      MSCF9999
.
MSCF0040  WRITE     HTMLFILE;FILTRDHB;
          GOTO      MSCF9999
.
MSCF0050  CALL      OUTFIL00                * Output Extract file content
          GOTO      MSCF9999
.
MSCF0060  WRITE     HTMLFILE;PMNHDHBC,PMNHBTCH;  * Extract name
          GOTO      MSCF9999
.
MSCF0070  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,MSCF0071
.
          WRITE     HTMLFILE;PMNHSTAT;      * Processing Status
          GOTO      MSCF9999
.
MSCF0071  MOVE      PMNHSTAT,STATCODE
          CALL      STATMH00                * Get Status string
          WRITE     HTMLFILE;STATSTRG;
          GOTO      MSCF9999
.
MSCF0080  WRITE     HTMLFILE;RECRDKEY;      * Record Key
          GOTO      MSCF9999
.
MSCF0090  UNPACK    RECRDKEY,D8,D3,BATCHNUM
          MATCH     "1",DEFTSTAT
          IF        @EQUAL
            MOVE      "7",FILTSTAT
          ENDIF
          CALL      TABNMD00                * NMDS Transmissions Details List
          GOTO      MSCF9999
.
MSCF0100  CALL      NMDDET00                * Extract record HE detail text
          GOTO      MSCF9999
.
MSCF0110  WRITE     HTMLFILE;NMDRECKY;      * pmsnmdaf Record Key
          GOTO      MSCF9999
.
MSCF0120  UNPACK    NMDRECKY,D8
          WRITE     HTMLFILE;D8;            * Admis no. for pmsnmdaf Record Key
          GOTO      MSCF9999
.
MSCF0130  CALL      TABWTH00                * NBRS Transmissions List
          GOTO      MSCF9999
.
MSCF0140  WRITE     HTMLFILE;WTNHDHBC,WTNHBTCH;  * Extract name
          GOTO      MSCF9999
.
MSCF0150  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,MSCF0151
.
          WRITE     HTMLFILE;WTNHSTAT;      * Processing Status
          GOTO      MSCF9999
.
MSCF0151  MOVE      WTNHSTAT,STATCODE
          CALL      STATMH00                * Get Status string
          WRITE     HTMLFILE;STATSTRG;
          GOTO      MSCF9999
.
MSCF0160  UNPACK    RECRDKEY,D8,D3,BATCHNUM
          MATCH     "1",DEFTSTAT
          IF        @EQUAL
            MOVE      "7",FILTSTAT
          ENDIF
          CALL      TABWTD00                * NBRS Transmissions Details
          GOTO      MSCF9999
.
MSCF0170  CALL      NBRDET00                * Extract record BE details text
          GOTO      MSCF9999
.
MSCF0180  WRITE     HTMLFILE;WTNHBTCH;      * Batch number
          GOTO      MSCF9999
.
MSCF0190  CALL      ERRFIL00                * Output Error file content
          GOTO      MSCF9999
.
MSCF9999  RETURN
+
.------------------------------------------------------------
.         Output Extract File contents (NMDS / NBRS)
.------------------------------------------------------------
OUTFIL00  READ      CONTROLF,HUND24;*1,PTCNNMED,*51,PTCNNBED
          STRIP     PTCNNMED           * NMDS Extracts Directory
          STRIP     PTCNNBED           * NBRS Extracts Directory
.
          UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,OUTFIL10
.
          MOVE      PTCNNMED,FULLFPTH  * NMDS Extract File
          GOTO      OUTFIL20
.
OUTFIL10  MOVE      PTCNNBED,FULLFPTH  * NBRS Extract File
.
.         Copy matching extract file to a temp file for reading later
.         E.g:
.           cp `ls -t /extracts/CAP02230.ndm* | head -1` tmp.txt
.
OUTFIL20  CLEAR     EXECCMD
          APPEND    "cp `ls -t ",EXECCMD
          APPEND    FULLFPTH,EXECCMD
          APPEND    "/",EXECCMD
          APPEND    OUTFILNM,EXECCMD
          APPEND    "* | head -1` tmp.txt",EXECCMD
          RESET     EXECCMD
          EXECUTE   EXECCMD,TASKID
.
          MATCH     "0",TASKID
          GOTO      OUTFIL91 IF NOT EQUAL
.
OUTFIL50  CLEAR     FULLFPTH
          APPEND    "tmp.txt",FULLFPTH
          RESET     FULLFPTH
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      INPTFILE,FULLFPTH
          TRAPCLR   IO
          BRANCH    OVRCD,OUTFIL99
.
OUTFIL60  READ      INPTFILE,SEQ;FILELINE
          GOTO      OUTFIL98 IF OVER
.
          STRIP     FILELINE
          WRITE     HTMLFILE;FILELINE,"<br>";
          GOTO      OUTFIL60
.
OUTFIL91  WRITE     HTMLFILE;"Extract file (",OUTFILNM,") not found !!"
          GOTO      OUTFIL99
.
OUTFIL98  CLOSE     INPTFILE,DELETE
OUTFIL99  RETURN
+
.------------------------------------------------------------
.         Output Error File contents
.------------------------------------------------------------
ERRFIL00  READ      CONTROLF,HUND24;*1,PTCNNMED,*51,PTCNNBED
          STRIP     PTCNNMED           * NMDS Extracts Directory
          STRIP     PTCNNBED           * NBRS Extracts Directory
          CLEAR     FULLFPTH
.
          UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,ERRFIL10
.
          APPEND    PTCNNMED,FULLFPTH  * NMDS Extract File
          GOTO      ERRFIL20
.
ERRFIL10  APPEND    PTCNNBED,FULLFPTH  * NBRS Extract File
.
ERRFIL20  APPEND    "/",FULLFPTH
          APPEND    OUTFILNM,FULLFPTH
          RESET     FULLFPTH
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      INPTFILE,FULLFPTH
          TRAPCLR   IO
          BRANCH    OVRCD,ERRFIL91
.
ERRFIL30  READ      INPTFILE,SEQ;FILELINE
          GOTO      ERRFIL99 IF OVER
.
          STRIP     FILELINE
          WRITE     HTMLFILE;FILELINE,"<br>";
          GOTO      ERRFIL30
.
ERRFIL91  WRITE     HTMLFILE;"Error file (",OUTFILNM,") not found !!"
          GOTO      ERRFIL99
.
ERRFIL99  RETURN
+
.------------------------------------------------------------
.         Output Extract Record HE Details Text
.------------------------------------------------------------
NMDDET00  MOVE      NMDRECKY,KEY19
          CALL      RDPMNMD1
          BRANCH    OVRCD,NMDDET99
.
          WRITE     HTMLFILE;PMNDREDT;
.
NMDDET99  RETURN
+
.------------------------------------------------------------
.         Get the Processing Status string for 'pmsnmhaf'
.         
.         Parameter:
.           STATCODE - Status Code
.
.         Return:
.           STATSTRG - Status String (one of the defined values)
.------------------------------------------------------------
STATMH00  MOVE      ZERO,F1
          MOVE      SP70,STATSTRG
.
          MOVE      STATCODE,F1
          LOAD      STATSTRG,F1,STATSENT,STATACKL,STATERRP,STATCOMP,STATFILF
.
STATMH99  RETURN
+
.------------------------------------------------------------
.         Get the Processing Status string for 'pmsnmdaf'
.         
.         Parameter:
.           STATCODE - Status Code
.
.         Return:
.           STATSTRG - Status String (one of the defined values)
.------------------------------------------------------------
STATMD00  MOVE      ZERO,F1
          MOVE      SP70,STATSTRG
.
          MOVE      STATCODE,F1
          LOAD      STATSTRG,F1,STATSENT,STATERRO,STATACCP,STATCORR,STATFILF:
                                STATCANC
.
STATMD99  RETURN
+
.------------------------------------------------------------
.         Get the Processing Status string for 'watnbdaf'
.         
.         Parameter:
.           STATCODE - Status Code
.
.         Return:
.           STATSTRG - Status String (one of the defined values)
.------------------------------------------------------------
STATWD00  MOVE      ZERO,F1
          MOVE      SP70,STATSTRG
.
          MOVE      STATCODE,F1
          LOAD      STATSTRG,F1,STATSENT,STATERRO,STATACCP,STATCORR,STATFILF:
                                STATACWN
.
STATWD99  RETURN
+
.------------------------------------------------------------
.         Table output for NMDS Transmissions (pmsnmhaf)
.------------------------------------------------------------
TABNMH00  PACK      KEY17,CURRDATE,Z70
          CALL      RSPMNMH1
TABNMH10  CALL      RPPMNMH1
          BRANCH    OVRCD,TABNMH99
.
          MATCH     SP1,FILTSTAT            * No Status filter?
          GOTO      TABNMH20 IF EQUAL
.
          MATCH     FILTSTAT,PMNHSTAT
          GOTO      TABNMH10 IF NOT EQUAL
.
TABNMH20  MATCH     SP70,FILTRDHB           * Filter DHB code?
          GOTO      TABNMH50 IF EQUAL
.
          MATCH     FILTRDHB,PMNHDHBC
          GOTO      TABNMH10 IF NOT EQUAL
.
TABNMH50  MOVE      ZERO,F1
          MOVELPTR  PMNHFNAM,F1
          IF        F1=0
            MOVE      SP70,EXTFILNM
          ELSE
            MOVE      PMNHFNAM,EXTFILNM
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:LinkDetails('",HTMLLINK
          APPEND    PMNHXDAT,HTMLLINK
          APPEND    PMNHDHBC,HTMLLINK
          APPEND    PMNHBTCH,HTMLLINK
          APPEND    PMNHSTAT,HTMLLINK
          APPEND    "');",HTMLLINK
          RESET     HTMLLINK
.
          PACK      FILELNK1,SP70
          PACK      FILELNK2,SP70
.
          MATCH     SP70,EXTFILNM
          GOTO      TABNMH60 IF EQUAL
.
          CLEAR     FILELNK1
          APPEND    "<a href=javascript:LinkExtract('",FILELNK1
          APPEND    EXTFILNM,FILELNK1
          APPEND    "');>",FILELNK1
          APPEND    EXTFILNM,FILELNK1
          APPEND    "</a>",FILELNK1
          RESET     FILELNK1
.
          MOVE      "LookupIcon.gif",ICONFILN
          MATCH     "5",PMNHSTAT            * File Failure?
          IF        @EQUAL
            MOVE      "Delete.gif",ICONFILN
.
            CLEAR     ERRORFIL
            APPEND    PMNHDHBC,ERRORFIL
            APPEND    PMNHBTCH,ERRORFIL
            APPEND    ".err",ERRORFIL
            RESET     ERRORFIL
.
            CLEAR     FILELNK2
            APPEND    "<a href=javascript:LinkError('",FILELNK2
            APPEND    ERRORFIL,FILELNK2
            APPEND    "');>",FILELNK2
            APPEND    ERRORFIL,FILELNK2
            APPEND    "</a>",FILELNK2
            RESET     FILELNK2
          ENDIF
.
TABNMH60  MOVE      PMNHSTAT,STATCODE
          CALL      STATMH00
.
          CLEAR     HTMLLNK2                * Status update link
          APPEND    "javascript:LinkStatus('",HTMLLNK2
          APPEND    PMNHXDAT,HTMLLNK2
          APPEND    PMNHDHBC,HTMLLNK2
          APPEND    PMNHBTCH,HTMLLNK2
          APPEND    PMNHSTAT,HTMLLNK2
          APPEND    "');",HTMLLNK2
          RESET     HTMLLNK2
.
          MOVE      ZERO,RECCNTR1
          MOVE      PMNHRECS,RECCNTR1
          MOVE      ZERO,RECCNTR2
          MOVE      PMNHPROC,RECCNTR2
          MOVE      ZERO,RECCNTR3
          MOVE      PMNHRDEL,RECCNTR3
          MOVE      ZERO,RECCNTR4
          MOVE      PMNHRINS,RECCNTR4
          MOVE      ZERO,RECCNTR5
          MOVE      PMNHRREJ,RECCNTR5
.
TABNMH90  WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":                * 0
                             "#"",FILELNK1,"#",":                         * 1
                             "#"",ICONFILN,"#",":                         * 2
                             "#"",PMNHXDAT,"#",":                         * 3
                             "#"",STATSTRG,"#",":                         * 4
                             "#"",PMNHSTAT,"#",":                         * 5
                             "#"",HTMLLNK2,"#",":                         * 6
                             "#"",PMNHFDAT,"#",":                         * 7
                             "#"",PMNHTDAT,"#",":                         * 8
                             "#"",PMNHPENV,"#",":                         * 9 
                             "#"",RECCNTR1,"#",":                         * 10
                             "#"",RECCNTR2,"#",":                         * 11
                             "#"",RECCNTR3,"#",":                         * 12
                             "#"",RECCNTR4,"#",":                         * 13
                             "#"",RECCNTR5,"#",":                         * 14
                             "#"",PMNHLDAT,"#",":                         * 15
                             "#"",FILELNK2,"#"":                          * 16
                             ");"
.
          GOTO      TABNMH10
.
TABNMH99  RETURN
+
.------------------------------------------------------------------------------
.         Table output for NMDS Transmissions Details (pmsnmdaf)
.------------------------------------------------------------------------------
TABNMD00  PACK      KEY19,BATCHNUM,SP70
          CALL      RSPMNMD4
TABNMD10  CALL      RKPMNMD4
          BRANCH    OVRCD,TABNMD99
.
          MATCH     PMNDBTCH,BATCHNUM
          GOTO      TABNMD99 IF NOT EQUAL
.
          MATCH     SP1,FILTSTAT            * No Status filter?
          GOTO      TABNMD20 IF EQUAL
.
          MATCH     "7",FILTSTAT            * Filter by 'Error' & 'Corrected'?
          GOTO      TABNMD11 IF NOT EQUAL
.
          MATCH     "2",PMNDSTAT
          GOTO      TABNMD20 IF EQUAL
          MATCH     "4",PMNDSTAT
          GOTO      TABNMD20 IF EQUAL
.
          GOTO      TABNMD10
.
TABNMD11  MATCH     FILTSTAT,PMNDSTAT
          GOTO      TABNMD10 IF NOT EQUAL
.
TABNMD20  MOVE      SP70,DISPDATE
          MOVE      PMNDADMN,KEY8
.
          CALL      RDMISS1
          BRANCH    OVRCD,TABNMD10
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,TABNMD10
.
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          CALL      PATLN000
.
          MOVE      AURNO,URNUMBER
          REP       " +",URNUMBER
          MOVE      AADMNO,ADMISSNO
          REP       " +",ADMISSNO
.
          CLEAR     HTMLLINK
          APPEND    "javascript:LinkPatient('",HTMLLINK
          APPEND    URNUMBER,HTMLLINK
          APPEND    "','",HTMLLINK 
          APPEND    ADMISSNO,HTMLLINK
          APPEND    "');",HTMLLINK
          RESET     HTMLLINK
.
          REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
.         CALL      PACDATE
.         REP       " 0",CPCDATE
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,SP1,ATIME
.
          MOVE      PMNDSTAT,STATCODE
          CALL      STATMD00
.
          CLEAR     HTMLLNK2
          APPEND    "javascript:LinkDetails('",HTMLLNK2
          APPEND    PMNDADMN,HTMLLNK2
          APPEND    PMNDETYP,HTMLLNK2
          APPEND    PMNDDIAG,HTMLLNK2
          APPEND    PMNDBTCH,HTMLLNK2
          APPEND    PMNDERRC,HTMLLNK2
          APPEND    "');",HTMLLNK2
          RESET     HTMLLNK2
.
.         PACK      HTMLLNK3,SP70
.         MOVE      ZERO,F1
.         MOVE      PMNDSTAT,F1
.         BRANCH    F1,TABNMD80,TABNMD80,TABNMD80
.         GOTO      TABNMD90
.
TABNMD80  CLEAR     HTMLLNK3
          APPEND    "javascript:LinkStatus('",HTMLLNK3
          APPEND    PMNDADMN,HTMLLNK3
          APPEND    PMNDETYP,HTMLLNK3
          APPEND    PMNDDIAG,HTMLLNK3
          APPEND    PMNDBTCH,HTMLLNK3
          APPEND    PMNDERRC,HTMLLNK3
          APPEND    "');",HTMLLNK3
          RESET     HTMLLNK3
.
TABNMD90  WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":                * 0
                             "#"",PMNDADMN,"#",":                         * 1
                             "#"",DISPDATE,"#",":                         * 2
                             "#"",FORMATNM,"#",":                         * 3
                             "#"",SP1,"#",":                              * 4
                             "#"",HTMLLNK2,"#",":                         * 5
                             "#"",PMNDETYP,"#",":                         * 6
                             "#"",PMNDELID,"#",":                         * 7
                             "#"",STATSTRG,"#",":                         * 8 
                             "#"",PMNHSTAT,"#",":                         * 9 
                             "#"",HTMLLNK3,"#",":                         * 10
                             "#"",PMNDDIAG,"#",":                         * 11
                             "#"",PMNDLSCD,"#",":                         * 12
                             "#"",PMNDLSDT,"#",":                         * 13
                             "#"",PMNDERRC,"#",":                         * 14
                             "#"",PMNDERRN,"#",":                         * 15
                             "#"",PMNDERRT,"#"":                          * 16
                             ");"
.
          GOTO      TABNMD10
.
TABNMD99  RETURN
+
.------------------------------------------------------------
.         NBRS Transmissions
.------------------------------------------------------------
NBRSTR00  CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          CLOCK     TIME,CTIMEIS
.
          MATCH     SP70,UPDTTYPE
          GOTO      NBRSTR70 IF EQUAL
.
          MATCH     "U",UPDTTYPE
          GOTO      NBRSTR30 IF EQUAL
.
          GOTO      NBRSTR90
.
NBRSTR30  PACK      KEY17,RECRDKEY,SP70
          CALL      RDWTNBH1
          IF        OVRCD=0
            MOVE      WTNH0001,WTNHSTAT
            MOVE      WBSEUID,WTNHUSRS
            MOVE      CURRDATE,WTNHUDAT
            MOVE      CTIMEIS,WTNHUTIM
            CALL      UPWTNBH1
          ENDIF
          GOTO      NBRSTR99
.
NBRSTR70  PACK      KEY3,WBSEHOSP,SP70
          CALL      RDPTHSP1
          IF        OVRCD=1
            CALL      CLPATHSP
          ENDIF
.
          PACK      KEY17,RECRDKEY,SP70
          CALL      RDWTNBH1
          IF        OVRCD=1
            CALL      CLWATNBH
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "NBRS Transmissions",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,NBRSTR99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      NBRSTR99
.
NBRSTR90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NMDSTR99
.
NBRSTR99  RETURN
+
.------------------------------------------------------------
.         Table output for NBRS Transmissions (watnbhaf)
.------------------------------------------------------------
TABWTH00  PACK      KEY17,CURRDATE,Z70
          CALL      RSWTNBH1
TABWTH10  CALL      RPWTNBH1
          BRANCH    OVRCD,TABWTH99
.
          MATCH     SP1,FILTSTAT            * No Status filter?
          GOTO      TABWTH20 IF EQUAL
.
          MATCH     FILTSTAT,WTNHSTAT
          GOTO      TABWTH10 IF NOT EQUAL
.
TABWTH20  MATCH     SP70,FILTRDHB           * Filter DHB code?
          GOTO      TABWTH50 IF EQUAL
.
          MATCH     FILTRDHB,WTNHDHBC
          GOTO      TABWTH10 IF NOT EQUAL
.
TABWTH50  MOVE      ZERO,F1
          MOVELPTR  WTNHFNAM,F1
          IF        F1=0
            MOVE      SP70,EXTFILNM
          ELSE
            MOVE      WTNHFNAM,EXTFILNM
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:LinkDetails('",HTMLLINK
          APPEND    WTNHXDAT,HTMLLINK
          APPEND    WTNHDHBC,HTMLLINK
          APPEND    WTNHBTCH,HTMLLINK
          APPEND    WTNHSTAT,HTMLLINK
          APPEND    "');",HTMLLINK
          RESET     HTMLLINK
.
          PACK      FILELNK1,SP70
          PACK      FILELNK2,SP70
.
          MATCH     SP70,EXTFILNM
          GOTO      TABWTH60 IF EQUAL
.
          CLEAR     FILELNK1
          APPEND    "<a href=javascript:LinkExtract('",FILELNK1
          APPEND    EXTFILNM,FILELNK1
          APPEND    "');>",FILELNK1
          APPEND    EXTFILNM,FILELNK1
          APPEND    "</a>",FILELNK1
          RESET     FILELNK1
.
          MOVE      "LookupIcon.gif",ICONFILN
          MATCH     "5",WTNHSTAT            * File Failure?
          IF        @EQUAL
            MOVE      "Delete.gif",ICONFILN
.
            CLEAR     ERRORFIL
            APPEND    WTNHDHBC,ERRORFIL
            APPEND    WTNHBTCH,ERRORFIL
            APPEND    ".ERR",ERRORFIL
            RESET     ERRORFIL
.
            CLEAR     FILELNK2
            APPEND    "<a href=javascript:LinkError('",FILELNK2
            APPEND    ERRORFIL,FILELNK2
            APPEND    "');>",FILELNK2
            APPEND    ERRORFIL,FILELNK2
            APPEND    "</a>",FILELNK2
            RESET     FILELNK2
          ENDIF
.
TABWTH60  MOVE      WTNHSTAT,STATCODE
          CALL      STATMH00
.
          CLEAR     HTMLLNK2                * Status update link
          APPEND    "javascript:LinkStatus('",HTMLLNK2
          APPEND    WTNHXDAT,HTMLLNK2
          APPEND    WTNHDHBC,HTMLLNK2
          APPEND    WTNHBTCH,HTMLLNK2
          APPEND    WTNHSTAT,HTMLLNK2
          APPEND    "');",HTMLLNK2
          RESET     HTMLLNK2
.
          MOVE      ZERO,RECCNTR1
          MOVE      WTNHRECS,RECCNTR1
          MOVE      ZERO,RECCNTR2
          MOVE      WTNHPROC,RECCNTR2
          MOVE      ZERO,RECCNTR3
          MOVE      WTNHRDEL,RECCNTR3
          MOVE      ZERO,RECCNTR4
          MOVE      WTNHRINS,RECCNTR4
          MOVE      ZERO,RECCNTR5
          MOVE      WTNHRERR,RECCNTR5
          MOVE      ZERO,RECCNTR6
          MOVE      WTNHRWRN,RECCNTR6
.
TABWTH90  WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":                * 0
                             "#"",FILELNK1,"#",":                         * 1
                             "#"",ICONFILN,"#",":                         * 2
                             "#"",WTNHXDAT,"#",":                         * 3
                             "#"",STATSTRG,"#",":                         * 4
                             "#"",WTNHSTAT,"#",":                         * 5
                             "#"",HTMLLNK2,"#",":                         * 6
                             "#"",WTNHPENV,"#",":                         * 7 
                             "#"",RECCNTR1,"#",":                         * 8 
                             "#"",RECCNTR2,"#",":                         * 9 
                             "#"",RECCNTR3,"#",":                         * 10
                             "#"",RECCNTR4,"#",":                         * 11
                             "#"",RECCNTR5,"#",":                         * 12
                             "#"",RECCNTR6,"#",":                         * 13
                             "#"",WTNHLDAT,"#",":                         * 14
                             "#"",FILELNK2,"#"":                          * 15
                             ");"
.
          GOTO      TABWTH10
.
TABWTH99  RETURN
+
.------------------------------------------------------------
.         NBRS Transmissions Details
.------------------------------------------------------------
NBRSDT00  CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          CLOCK     TIME,CTIMEIS
.
          MATCH     SP70,UPDTTYPE
          GOTO      NBRSDT70 IF EQUAL
.
          MATCH     "U",UPDTTYPE
          GOTO      NBRSDT30 IF EQUAL
.
          GOTO      NBRSDT90
.
NBRSDT30  
          GOTO      NBRSDT99
.
NBRSDT70  PACK      KEY3,WBSEHOSP,SP70
          CALL      RDPTHSP1
          IF        OVRCD=1
            CALL      CLPATHSP
          ENDIF
.
          PACK      KEY17,RECRDKEY,SP70
          CALL      RDWTNBH1
          IF        OVRCD=1
            CALL      CLPMSNMH
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "NBRS Transmissions Details",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,NBRSDT99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      NBRSDT99
.
NBRSDT90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NBRSDT99
.
NBRSDT99  RETURN
+
.------------------------------------------------------------------------------
.         Table output for NBRS Transmissions Details (watnbdaf)
.------------------------------------------------------------------------------
TABWTD00  PACK      KEY31,BATCHNUM,SP70
          CALL      RSWTNBD4
TABWTD10  CALL      RKWTNBD4
          BRANCH    OVRCD,TABWTD99
.
          MATCH     WTNDBTCH,BATCHNUM
          GOTO      TABWTD99 IF NOT EQUAL
.
          MATCH     SP1,FILTSTAT            * No Status filter?
          GOTO      TABWTD20 IF EQUAL
.
          MATCH     "7",FILTSTAT            * Filter by 'Error' & 'Corrected'?
          GOTO      TABWTD11 IF NOT EQUAL
.
          MATCH     "2",WTNDSTAT
          GOTO      TABWTD20 IF EQUAL
          MATCH     "4",WTNDSTAT
          GOTO      TABWTD20 IF EQUAL
.
          GOTO      TABWTD10
.
TABWTD11  MATCH     "3",FILTSTAT            * Filter by 'Accepted' ?
          GOTO      TABWTD12 IF NOT EQUAL
.
          MATCH     "3",WTNDSTAT            * 'Accepted'
          GOTO      TABWTD20 IF EQUAL
          MATCH     "6",WTNDSTAT            * 'Accepted with Warning'
          GOTO      TABWTD20 IF EQUAL
          GOTO      TABWTD10
.
TABWTD12  MATCH     FILTSTAT,WTNDSTAT
          GOTO      TABWTD10 IF NOT EQUAL
.
TABWTD20  MOVE      WTNDPNHI,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,TABWTD10
.
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          CALL      PATLN000
.
          CLEAR     HTMLLNK2
          APPEND    "javascript:LinkDetails('",HTMLLNK2
          APPEND    WTNDLBID,HTMLLNK2
          APPEND    WTNDBSCD,HTMLLNK2
          APPEND    WTNDBSDT,HTMLLNK2
          APPEND    WTNDLINE,HTMLLNK2
          APPEND    WTNDBTCH,HTMLLNK2
          APPEND    WTNDERRC,HTMLLNK2
          APPEND    "');",HTMLLNK2
          RESET     HTMLLNK2
.
          MOVE      SP70,CASEKEYS
.
          PACK      KEY19,WTNDPNHI,SP70
          CALL      RDSTREA1
TABWTD21  CALL      RDKTREA1
          BRANCH    OVRCD,TABWTD30
.
          MATCH     WTNDPNHI,WMURNO
          GOTO      TABWTD30 IF NOT EQUAL
.
          MATCH     WTNDLBID,WTWMECNT       * matching WL Booking ID?
          GOTO      TABWTD21 IF NOT EQUAL
.
          MATCH     SP70,FILTRDHB
          IF        !@EQUAL & !@EOS
            MATCH     SP70,WTWMIHSP
            IF        !@EQUAL & !@EOS
              PACK      KEY3,WTWMIHSP,SP70
              CALL      RDPTHSP1
              IF        OVRCD=0
                PACK      KEY5,CAT_dh,PTHSHDHB,SP70
                CALL      RDCODE1
                IF        OVRCD=0
                  MOVE      THCSCOD,DIM3
                  MATCH     FILTRDHB,DIM3
                  GOTO      TABWTD10 IF NOT EQUAL
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          PACK      CASEKEYS,WMURNO,WMCODE,DWMCNT,SP70
.
TABWTD30  CLEAR     HTMLLINK
          APPEND    "javascript:LinkPatient('",HTMLLINK
          APPEND    CASEKEYS,HTMLLINK
          APPEND    "');",HTMLLINK
          RESET     HTMLLINK
.
          MOVE      WTNDSTAT,STATCODE
          CALL      STATWD00

.
.         PACK      HTMLLNK3,SP70
.         MOVE      ZERO,F1
.         MOVE      WTNDSTAT,F1
.         BRANCH    F1,TABWTD80,TABWTD80,TABWTD80
.         GOTO      TABWTD90
.
TABWTD80  CLEAR     HTMLLNK3
          APPEND    "javascript:LinkStatus('",HTMLLNK3
          APPEND    CASEKEYS,HTMLLNK3
          APPEND    "');",HTMLLNK3
          RESET     HTMLLNK3
.
TABWTD90  WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":                * 0
                             "#"",WTNDLBID,"#",":                         * 1
                             "#"",WTNDXDAT,"#",":                         * 2
                             "#"",FORMATNM,"#",":                         * 3
                             "#"",SP1,"#",":                              * 4
                             "#"",HTMLLNK2,"#",":                         * 5
                             "#"",WTNDBSCD,"#",":                         * 6
                             "#"",WTNDBSDT,"#",":                         * 7
                             "#"",STATSTRG,"#",":                         * 8 
                             "#"",WTNDSTAT,"#",":                         * 9 
                             "#"",HTMLLNK3,"#",":                         * 10
                             "#"",WTNDLINE,"#",":                         * 11
                             "#"",WTNDERRC,"#",":                         * 12
                             "#"",WTNDMSGN,"#",":                         * 13
                             "#"",WTNDMSGT,"#"":                          * 14
                             ");"
.
          GOTO      TABWTD10
.
TABWTD99  RETURN
+
.------------------------------------------------------------
.         Output Extract Record BE Details Text
.------------------------------------------------------------
NBRDET00  MOVE      NBRRECKY,KEY31
          CALL      RDWTNBD1
          BRANCH    OVRCD,NBRDET99
.
          WRITE     HTMLFILE;WTNDRDET;
.
NBRDET99  RETURN
+
.------------------------------------------------------------
.
          INC       IBAWEBCD
          INC       WEBDATCD
          INC       CALCAGE
          INC       DAYOFWEK
          INC       NAMSTRCD
          INC       CLPMSPX2
          INC       CLNHIMAS
          INC       CLPATHSP
          INC       CLPMSNMD
          INC       CLPMSNMH
          INC       CLWATNBD
          INC       CLWATNBH
          INC       PATNAMCD
          INC       PATDOCTM
          INC       PATDOCCD
          INC       PATMASTM
          INC       PATMSXTM
          INC       PMSHCGTM
          INC       PMSHCPTM
          INC       PMSPX2TM
.
          INC       APSMASIO/INC       * Creditor/Supplier Master File
          INC       MRTMASIO/INC       * MRT Master Details
          INC       NHIETHIO/INC       * NHI Ethnicity Codes
          INC       NHIMASIO/INC       * NHI/MWS HCU Details
          INC       OUTSITIO/INC       * Outpatients Site Code
          INC       PATALRIO/INC       * Patient Alerts
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATDO1IO/INC       * Doctor File
          INC       PATFN1IO/INC       * Patient Health Fund
          INC       PATHSPIO/INC       * Hospital Details
          INC       PATIN1IO/INC       * Insurance Details
          INC       PATMA1IO/INC       * Patient Master File
          INC       PATMI1IO/INC       * Patient Admissions File
          INC       PATVISIO/INC       * Patient Visit File
          INC       PMSVX1IO/INC       * Patient Visit Extension File
          INC       PATWR1IO/INC       * Patient Ward/Bed Details
.
          INC       PMSCCDIO/INC       * Concession Card Details
          INC       PMSHCGIO/INC       * HCP Practice
          INC       PMSHCPIO/INC       * Healthcare Profession
          INC       PMSNMDIO/INC       * NMDS Extract Records
          INC       PMSNMHIO/INC       * NMDS Transmissions File Batches
          INC       PMSPX2IO/INC       * PMI extension table
          INC       WATNBDIO/INC       * NBRS Extract Records
          INC       WATNBHIO/INC       * NBRS Transmissions File Batches
          INC       WATTR1IO/INC       * Waiting List Patient Procedures
          INC       WATHISIO/INC       * Waiting List History File
.

.******************************************************************************
.*              P R O G R A M  S U M M A R Y                                  *
.*              -------------  -------------                                  *
.*                                                                            *
.*     PROGRAM NAME:     IBAHOS53                                             *
.*                                                                            *
.*     FUNCTION:         SEPARATION REPORT                                    *
.*                                                                            *
.*     DATE WRITTEN:     07/06/88                                             *
.*                                                                            *
.*     AUTHOR:           KARIN PEAK    (I.B.A)                                *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*        V10.04.02 17/06/2014 Steve Armstrong  CAR 301639                    *
.*                  Moved call to TFILENAM for FNAMET to start of program.    *
.*        V10.04.01 15/04/2014 Ebon Clements  CAR 298721                      *
.*                  Added multi hospital functionality                        *
.******************************************************************************
.*        V10.03.01 27/12/2012 Patrick Adair  CAR 261630                      *
.*                  Removed port number from temp file name                   *
.******************************************************************************
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.******************************************************************************
.*        V9.10.01  12/06/2008  Ebon Clements  CAR 169228                     *
.*                  Added PTCNDRSM - to display dstat or ddest                *
.******************************************************************************
.*        V9.02.01  30/08/2002  Jill Habasque  SRF 17573                      *
.*                  Changed to use RTIODAYS instead of NHOSPDAY               *
.******************************************************************************
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.******************************************************************************
.*        V5.07.02  30/09/1999  Steve Armstrong                               *
.*                  Fixed header display                                      *
.*        V5.07.01  17/03/1999  Jim Stathopoulos                              *
.*                  Date modification                                         *
.*        V5.07.B02 28/01/1999  Nicole Harrington                             *
.*                  507 rebounds                                              *
.*        V5.07.B01 12/11/1998  Davin                                         *
.*                  Mods for 507                                              *
.******************************************************************************
.*        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
.*                  Changed temp file name "pstidx" to standard format        *
.******************************************************************************
. *                       V5.01 04.05.89 S.Barcham         *
. *                             4356 I * D                 *
. *                             SRF100428                  *
. *                       V5.02 09.05.89 K.Peak            *
. *                             Fixed length of stay       *
. *                             SRF  100473                *
. *                       V5.03 18/05/89 DIG               *
. *                             Changed to standard and    *
. *                             modified print for 8 digit *
. *                             U/R's etc                  *
. *                       V5.04 27/05/89 J.Tan             *
. *                             Fixed Mother Adm. to form8 *
. *                             (PATMI1FD).     SRF 100750 *
. *                       V5.05 06/06/89 J.Tan             *
. *                             Mods Locking master file   *
. *                             (PATIOMAS)     SRF 100809  *
. *                    V5.01.01 05/07/89 K.Peak            *
. *                             Changed Adm & U/R to 8 chrs*
. *                    V5.01.02 24/11/89 K.Peak            *
. *                             Fixed spelling of separate *
. *                             SRF 103133                 *
. *                   V5.01.121 27/11/90 Glen Hobby        *
. *                            Recompiled for changes to   *
. *                            PATMX1FD                    *
. *        V5.01.122 08.03.93 Neeriem "I Hate Support" Dye *
. *                  Use Keydate                           *
. *                                                        *
. **********************************************************
.
          INC           STD001FD
          INC       IBACONFD/INC
          INC           PATDSCFD/INC
          INC       PATMA1FD/INC

          INC           PATCONFD/INC
          INC           PATCODFD/INC
          INC       PATMI1FD/INC
          INC       PATHSPFD/INC
          INC       PMSVX1FD/INC
          INC           PATTRNFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.        	WORK AREA
.
.
.
PSTROL          FILE      ASCII, FIXED=256
SORTSTR         DIM       30
SORT1           INIT      "u37-44,1-20,61-85,29-36,21-28"
SORT2           INIT      "u1-20,61-85,29-36,21-28"
SORT3           INIT      "u53-60,29-36,21-28"
.
TEMPFILE        IFILE     SQL, FIXED=118, KEY=SORTSTR 
.
. ----- Tempfile consists of following vars -----
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
.PSNAME         DIM       20         20             1
DUNIQUE         DIM        8          8            21
.DDADMNO        DIM        8          8            29
.DDATE          DIM        8          8            37
.DTIME          DIM        8          8            45
.DURNO          DIM        8          8            53
.PGNAME         DIM       25         25            61
.DSTAT          DIM        3          3            86
.PTITL          DIM        4          4            89
.ADATE          DIM        8          8            93
.ATIME          DIM        8          8           101
.TOTDAY         FORM       4          3           109
.ATYPE          DIM        3          3           112
.DDEST          DIM        3          3           115
. ----- EOR 118 -----
.
FNAMET          DIM     8
UNIQUE          FORM    8
CMDLINE         DIM     50
.
CODED           INIT    "D "
CODEDD          INIT    "DD"
.
.      EXTRA VARIABLES
.
DANS             DIM    3
.
DIM5             DIM    5
DIM17            DIM    17
.
DALERG           DIM    3
DILLN            DIM    3
DHOSP            DIM    3
DDSTAT           DIM    19
.
HOSPCODE  DIM       3
HOSPDESC  DIM       50
.
DSTIM            DIM    5
ADTIM            DIM    5
.
SKEY30           DIM    30
RTDAYS           FORM   4
NBRDAYS          FORM   4
PSNAMEDS         DIM    19
TOTDAY           FORM   4
TOTPATS          FORM   8
CALDAYS          FORM   4
JYEAR            FORM   2
WDATE1           DIM    8
WDATE2           DIM    8
.
XDAY1           DIM     2
XMON1           DIM     2
XYEAR1          DIM     2
XCENT1          DIM     2
.
XDAY2           DIM     2
XMON2           DIM     2
XYEAR2          DIM     2
XCENT2          DIM     2
.
FROMDATE        DIM     8        * FROM DATE IN (CCYYMMDD) FORMAT
FDATE           DIM     8
.
TODATE          DIM     8        * TO DATE IN (CCYYMMDD) FORMAT
DATET           DIM     8
.
SP24            INIT    "                        "
SP40            INIT    "                                        "
.
PRGID     INIT      "IBAHOS53"
PRGNAM    INIT      "Separation Report"
VERSION   INIT      "V12.01.00"
.
.         START OF PROGRAM
.
          CALL          DISPHEAD
          DISPLAY       *P56:24,"Opening patdschf";
          OPEN          PATDSCH1,"patdschf"
.
          DISPLAY       *P64:24,"patdschf";
          OPEN          PATDSCH2,"patdschf"
.
          DISPLAY       *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY       *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY       *P64:24,"patcodes";
          OPEN          PATCODE1,"patcodes"
.
          DISPLAY       *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY       *P64:24,"pattranf";
          OPEN          PATTRAN2,"pattranf"
.
          DISPLAY       *P64:24,"controlf";
          OPEN          CONTROLF,"controlf"
          READ          CONTROLF,TWENTY1;*45,PTCNDRSM
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMET
.
.        START OF PROGRAM
.
.           MAIN        MENU
.
START   MOVE       ZERO,CPAGENO
.
        HLINE     *G37,2,49,80
        DISPLAY   *P1:3,*EF:
                  *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                  *P1:5,*V2LON,ONE,*HOFF," = Discharge Date/Alpha Sequence":
                  *P1:6,*V2LON,TWO,*HOFF," = Alphabetic Sequence":
                  *P1:7,*V2LON,THREE,*HOFF," = U/R Number Sequence"
.
        KEYIN     *P1:9,"Option : ",*EL,*V2LON,OPTION
.
        COMPARE   ZERO TO OPTION
        GOTO      ENDIT IF EQUAL
.
BRNCH   PACK      SORTSTR,SP30
        BRANCH    OPTION OF KDATE,KSURN,KNUMB
        BEEP
        GOTO      START
.
KDATE   DISPLAY   *P49:2,*V2LON,"- Discharge Date/Alpha Sequence "
        MOVE      SORT1,SORTSTR
        GOTO      KDDATE
.
KSURN   DISPLAY   *P49:2,*V2LON,"- Alphabetic Sequence "
        MOVE      SORT2,SORTSTR
        GOTO      KDDATE
.
KNUMB   DISPLAY   *P49:2,*V2LON,"- U/R Number Sequence "
        MOVE      SORT3,SORTSTR
        GOTO      KDDATE
.
NODSCH  DISPLAY   *P20:24,*EL,*V2LON,"** No Discharges on File **",*W2;
        GOTO      START
.
.             KEYIN TO DATE
.
KDDATE  DISPLAY   *P1:4,*EF,"Keyin From Date : ":
                    *P1:6,"Keyin To   Date : "
.
KADAT1 
....
            MOVE      SP10,CDAY        * no default day/mon
            MOVE      SP10,CMON
            MOVE      SP10,CYEAR
            MOVE      SP10,CCENT
.
          MOVE      " 4",CVERT         * set up row
          MOVE      "20",CCOL          * set up collumn
.
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
            CALL      KEYDATE          * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
          MOVE      CDAY,XDAY
          MOVE      CMON,IMON
          MOVE      CYEAR,IYEAR
          MOVE      CCENT,ICENT
.
        COMPARE         ZERO TO IDAY
        GOTO            START IF EQUAL
....
.
ENDAD1  PACK            FDATE,ICENT,IYEAR,IMON,IDAY
        REP             " 0",FDATE
        UNPACK          FDATE,XCENT1,XYEAR1,XMON1,XDAY1
.
.             DISPLAY TO DATE 
.
KADAT2 
....
          IF        IDAY=0
            MOVE      SP10,CDAY        * no default day/mon
            MOVE      SP10,CMON
            MOVE      SP10,CYEAR
            MOVE      SP10,CCENT
          ELSE
            MOVE      IDAY,CDAY        * set up day/mon
            MOVE      IMON,CMON
            MOVE      IYEAR,CYEAR
            MOVE      ICENT,CCENT
          ENDIF
.
          MOVE      " 6",CVERT         * set up row
          MOVE      "20",CCOL          * set up collumn
.
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
            CALL      KEYDATE          * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
          MOVE      CDAY,XDAY
          MOVE      CMON,IMON
          MOVE      CYEAR,IYEAR
          MOVE      CCENT,ICENT
.
....
.
ENDAD2  PACK            DATET,ICENT,IYEAR,IMON,IDAY
        REP             " 0",DATET
        UNPACK          DATET,XCENT2,XYEAR2,XMON2,XDAY2
.
CNTCHK  MATCH           FDATE TO DATET * 'FROM' DATE MUST BE 
        GOTO            REKEY IF NOT LESS           * >= 'TO' DATE
.
INVDATG     
        DISPLAY        *P20:24,*EL,*B,*V2LON:
                       "** From Date Must be Less Than To Date **":
                       *W2,*P20:24,*EL;
        GOTO           KADAT1
.
.        CHECK IF DATES ARE OK OR NOT
.
REKEY   CALL      KHOS0000                     * Keyin hospital ID
        BRANCH    EXIT,KDDATE
.
        KEYIN      *P1:24,*EF," Date ok (":
	           *V2LON,"Y",*HOFF,*DV,SLASH:
		   *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
        MATCH      "N" TO ANS
        GOTO       KADAT1 IF EQUAL
.
        MATCH      "Y" TO ANS
        GOTO       SURNAM IF EQUAL
.
        BEEP
        GOTO       REKEY
.
INVMASTD
        DISPLAY    *P20:23,*B,*EL:
                   "** Discharge ",*V2LON,DADMNO,*HOFF:
                   " has an invalid U/R ":
                   *V2LON,DURNO,*HOFF," ***",*W2
.
        PRINT      "** Discharge ",DADMNO," has an invalid U/R ":
                   DURNO," ... Please Contact I.B.A **"
.
        GOTO       NEXTA
.
INVADMN DISPLAY   "** Discharge ",DADMNO," has an no Admission Information ":
                      " ... Please Contact I.B.A **"
.
        GOTO       NEXTA
.
SURNAM  CALL       KILLIDX
        CALL       CREAIDX
.
        OPEN       TEMPFILE,FNAMET
        MOVE       ONE TO UNIQUE
.
        MOVE       "60",CLNO
.
.                 DISPLAY ADMISSIONS IN RANGE
.
        DISPLAY    *P1:24,*EL,*P20:24,"Admission Number : "
.
        PACK       KEY16,FDATE,SP20
        CALL       RDSDSCH2
NEXTA   CALL       RDKDSCH2
        BRANCH     OVRCD OF SORTD
.
        DISPLAY    *P40:24,*EL,*V2LON,DADMNO
.
        MOVE       DURNO,KEY8
        CALL       RDMAST1
        BRANCH     OVRCD OF INVMASTD
.
        MATCH      DDATE,DATET
        GOTO       SORTD IF LESS
.
        PACK       KEY8,DADMNO,SP70
        CALL       RDPMVX11
        BRANCH     OVRCD,NEXTA
.
        IF         IBCNMHOS=1
          MATCH    SP70,HOSPCODE
          IF       !@EQUAL
            MATCH    HOSPCODE,PMVXMHOS   * Check hospital ID
            GOTO     NEXTA IF NOT EQUAL
          ENDIF
        ENDIF
.
        MOVE       DADMNO,KEY8
        CALL       RDMISS1
        BRANCH     OVRCD OF INVADMN
.
        MOVE       ADATE,FROMDATE
        MOVE       DDATE,TODATE
.
        CALL       RTIODAYS
        MOVE       NBRDAYS,TOTDAY
.
        BRANCH     OPTION OF TEMP1,TEMP2,TEMP3
.
TEMP1   MOVE       DADMNO,DDADMNO 
        MOVE       UNIQUE,DUNIQUE
        PACK       KEY69 WITH DDATE,PSNAME,PGNAME,DDADMNO,DUNIQUE
        WRITE      TEMPFILE,KEY69;PSNAME,DUNIQUE,DDADMNO,DDATE:
                                  DTIME,DURNO,PGNAME,DSTAT:
                                  PTITL,ADATE,ATIME,TOTDAY,ATYPE,DDEST
.
        ADD        ONE TO UNIQUE
        GOTO       NEXTA
.
TEMP2   MOVE       DADMNO,DDADMNO
        MOVE       UNIQUE,DUNIQUE
        PACK       KEY61 WITH PSNAME,PGNAME,DDADMNO,DUNIQUE
        WRITE      TEMPFILE,KEY61;PSNAME,DUNIQUE,DDADMNO,DDATE:
                                  DTIME,DURNO,PGNAME,DSTAT:
                                  PTITL,ADATE,ATIME,TOTDAY,ATYPE,DDEST
        ADD        ONE TO UNIQUE
        GOTO       NEXTA
.
TEMP3   MOVE       DADMNO,DDADMNO
        MOVE       UNIQUE,DUNIQUE
        PACK       KEY24 WITH DURNO,DDADMNO,DUNIQUE
        WRITE      TEMPFILE,KEY24;PSNAME,DUNIQUE,DDADMNO,DDATE:
                                  DTIME,DURNO,PGNAME,DSTAT:
                                  PTITL,ADATE,ATIME,TOTDAY,ATYPE,DDEST
        ADD        ONE TO UNIQUE
        GOTO       NEXTA
.
NOOPEN  DISPLAY    *P1:24,*B,*EL,"** TEMPORARY STATISTICS  FILE ":
                                       FNAMET," NOT FOUND **",*W2;
        RETURN
.
.       End of loop, calculate the number of patients
.
SORTD   MOVE       UNIQUE,TOTPATS
        SUB        ONE,TOTPATS
.
        DISPLAY    *P1:24,*EL,*P20:24,"** Processing Statistics **",*W;
.
        CLOSE      TEMPFILE
.
.       Process the temporary file
.
SORTFI  TRAP       NOOPEN IF IO
        OPEN       TEMPFILE,FNAMET
        TRAPCLR    IO
.
        BRANCH     OPTION,RDK1,RDK2,RDK3
.
RDK1    PACK       KEY69,SP40,SP40
        READ       TEMPFILE,KEY69;;
        GOTO       RDKDIS
.
RDK2    PACK       KEY61,SP40,SP40
        READ       TEMPFILE,KEY61;;
        GOTO       RDKDIS
.
RDK3    MOVE       SP24 TO KEY24
        READ       TEMPFILE,KEY24;;
.
RDKDIS  DISPLAY    *P1:24,*EL,*P20:24,"Patient Name :"
.
READLOP READKS    TEMPFILE;PSNAME,DUNIQUE,DDADMNO,DDATE:
                           DTIME,DURNO,PGNAME,DSTAT:
                           PTITL,ADATE,ATIME,TOTDAY,ATYPE,DDEST
        GOTO      ENDP IF OVER
.
        UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY
        MOVE      PSNAME,PSNAMEDS
        MOVE      DUNIQUE,UNIQUE
        MOVE      DDADMNO,DADMNO
.
        DISPLAY   *P40:24,*EL,*V2LON,PSNAME
.
        CALL      DISPDA
        GOTO      READLOP
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP     COMPARE      ZERO,CPAGENO
         GOTO         NODSCH IF EQUAL
.
         CALL        PAGEND
         PRINT        *N,"Total Number of Patients : ",TOTPATS,*N:
                      *N,"  ***  END OF REPORT  ***"
.
         DISPLAY      *P20:24,*EL,*V2LON:
                      "** Report Generation Completed **",*W2;
.
         CALL         KILLIDX
         GOTO         START
.
.     PRINT ACTUAL DATA 
.
DISPDA  COMPARE         "55",CLNO
        CALL            HEAD IF NOT LESS
.
        MOVE            SP20,TDESC
.
        IF              PTCNDRSM=2
          MATCH           SP70,DDEST
          IF              !@EQUAL
            PACK            KEY5 WITH CODEDD,DDEST
            CALL            RDCODE1
          ENDIF
        ELSE
          MATCH           SP70,DSTAT
          IF              !@EQUAL
            PACK            KEY5 WITH CODED,DSTAT
            CALL            RDCODE1
          ENDIF
        ENDIF
.
        MOVE            TDESC,DDSTAT
        MOVE            DDSTAT,DIM17
.
        MOVE            PGNAME,DIM19
        MOVE            DTIME,DIM5
        MOVE            DIM5,DSTIM
        MOVE            ATIME,DIM5
        MOVE            DIM5,ADTIM
.
        UNPACK     ADATE,CCENT,CYEAR,CMON,CDAY
.
        PRINT       *1,"!",PSNAMEDS,SP1,PTITL,SP1,DIM19,*46,"!",XDAY,"/":
                    XMON,"/",XYEAR,*55,"! ",DSTIM,*64,"!",DIM17,*82,"! ":
                    TOTDAY,*88,"!",CDAY,"/",CMON,"/",CYEAR,*97,"! ",ADTIM:
                    *106,"!",DURNO,*115,"!",DADMNO,*124,"!  ",ATYPE,*132,"!"
.
          ADD       ONE,CLNO
.
DISPEND   RETURN
.
.          LAST LINE ON THE PAGE
.
PAGEND    PRINT     "*---------------------------------------------------":
                    "--------------------------------------------------------":
                    "-----------------------*"
          ADD       ONE,CLNO
          RETURN
.
.                 HEADINGS FOR OUTPUT
.
HEAD      COMPARE   ZERO,CPAGENO
          CALL      PAGEND IF  NOT EQUAL
.
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          BRANCH    OPTION OF PRNTA,PRNTB,PRNTC
.
PRNTA     PRINT     *N,*40,"(DISCHARGE DATE/ALPHA - SEQUENCE)"
          GOTO      PRTCONT
.
PRNTB     PRINT     *N,*40,"(ALPHABETIC - SEQUENCE)"
          GOTO      PRTCONT
.
PRNTC     PRINT     *N,*40,"(U/R NUMBER - SEQUENCE)"
.
PRTCONT   PRINT     *40,"From Date : ",XDAY1,"/",XMON1,"/",XCENT1,XYEAR1:
                    *65,"To Date : ",XDAY2,"/",XMON2,"/",XCENT2,XYEAR2
.
          IF        IBCNMHOS=1
            PRINT    *40,"Hospital : ",HOSPDESC
          ENDIF
.
          CALL      PAGEND
.
          PRINT     *1,"!",*46,"!":
                    *55,"!",*64,"!":
                    *82,"!No of":
                    *88,"!":
                    *97,"!",*106,"!":
                    *115,"!",*124,"!Patient",*132,"!",*N;
.
          IF        PTCNDRSM=2
            PRINT     *1,"!Patients Name",*46,"!Dis Date":
                      *55,"!Dis Time",*64,"!Discharge Dest. ":
                      *82,"!Days ":
                      *88,"!Adm Date":
                      *97,"!Adm Time",*106,"!U/R No":
                      *115,"!Adm No",*124,"!Class.",*132,"!"
          ELSE
            PRINT     *1,"!Patients Name",*46,"!Dis Date":
                      *55,"!Dis Time",*64,"!Discharge Status":
                      *82,"!Days ":
                      *88,"!Adm Date":
                      *97,"!Adm Time",*106,"!U/R No":
                      *115,"!Adm No",*124,"!Class.",*132,"!"
          ENDIF
.
          CALL     PAGEND
.
          MOVE      EIGHT,CLNO
          RETURN
.
.       Deleting  AN INDEXED FILE BASED ON PORT
.
KILLIDX CLOSE         TEMPFILE
        CLEAR         CMDLINE
        APPEND        "iserase ",CMDLINE
        APPEND        FNAMET,CMDLINE
        RESET         CMDLINE
        EXECUTE       CMDLINE,TASKID
        RETURN
.
CREAIDX  CLEAR         CMDLINE
        APPEND        "isbuild ",CMDLINE
        APPEND        FNAMET,CMDLINE
        APPEND        " 118 ",CMDLINE
        APPEND        SORTSTR,CMDLINE
        RESET         CMDLINE
.
        DISPLAY       *P1:13,*EF;
        EXECUTE       CMDLINE,TASKID
        DISPLAY       *P1:13,*EF;
        RETURN  
.
+
.*******************************************************************************
.       Keyin hospital ID
.*******************************************************************************
KHOS0000  MOVE       ZERO,EXIT
          MOVE       SP70,HOSPCODE
          MOVE       SP70,HOSPDESC
          COMPARE    ONE,IBCNMHOS
          GOTO       KHOS9999 IF NOT EQUAL
.
          DISPLAY    *P1:8,*EL,"Hospital Id : ";
          MOVE       TEN5,ECOL
          MOVE       EIGHT,EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ZERO,CKYIMAND           * Not Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOS1000,KHOS9000
          MOVE       PTHSHOSP,HOSPCODE
          MOVE       PTHSNAME,HOSPDESC
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS1000  MOVE       "All Hospitals",PTHSNAME
          MOVE       PTHSNAME,HOSPDESC
          MOVE       SP70,HOSPCODE
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS9000  MOVE       ONE,EXIT
KHOS9999  RETURN
+
         INC        STD001IO
         INC        PATHSPKY
         INC       PATTRNIO/INC
         INC       PATMA1IO/INC
         INC       PATDSCIO/INC
         INC       PATCODIO/INC
         INC       PATMI1IO/INC
         INC       PATHSPIO/INC
         INC       PMSVX1IO/INC
         INCLUDE    RTIODAYS
          INC       TFILENAM                     * Generate Temp File Name
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       WEBERRIO/INC                 * Web Server Error Logging
.
ENDIT    CHAIN      PGM
          STOP

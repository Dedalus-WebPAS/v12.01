.***************************************************************************
.* System    :   PRS/2                                                     *
.* Program   :   IBAINP32                                                  *
.* Desc      :   PRS/2 Account Status Validation Master Listing            *
.***************************************************************************
.* Date      :   14/08/95                                                  *
.* Author    :   Paul Howells                                              *
.* Mods      :                                                             *
.***************************************************************************
.*  V9.09.01  17/08/2007  Mike Laming    CAR 147525                        *
.*            Added Medicare Suffix "Name, C-U, N-E, P-N" for PTACMEDI     *
.*  V9.05.B01 27/01/2006  Sandra Barcham   91730                           *
.*            Change PATACTFD to increase Key to 6 & PTACCARE to 2 bytes   *
.***************************************************************************
.*  V9.02.01  15/06/2004  Mike Laming  CAR 49016  PRS/2 2004 Changes       *
.*            Change PATACTFD to increase Key to 6 & PTACCARE to 2 bytes   *
.*  V9.02.00  17/12/2002  Tracey Nguyen    srf 34722                       *
.*            Ported program from V5.10                                    *
.*-------------------------------------------------------------------------*
.*  V5.08.01  16/05/2000  Greg Horvat      SRF 1887                        *
.*            Added 8 as a valid patactaf medicare suffix                  *
.*  V6.03.01  01/02/1996  Whirlpool                                        *
.*            Recompiled for PATACTFD - PTACCARE now a DIM                 *
.***************************************************************************
          INC       STD001FD
          INC       PATACTFD/INC  
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
SUFDESC   DIM       24 
SUF1      INIT      "N-E                     "
SUF2      INIT      "Name, C-U               "
SUF3      INIT      "Name, C-U, BAB          "
SUF4      INIT      "Name, C-U, BAB, P-N     "
SUF5      INIT      "Name, C-U, BAB, P-N, N-E"
SUF6      INIT      "Name, C-U, N-E          "
SUF7      INIT      "Name, C-U, BAB, N-E     "
SUF8      INIT      "C-U                     "
SUF9      INIT      "Name, P-N               "
SUF10     INIT      "Name, C-U, N-E, P-N     "
.
PRGNAM    INIT      "Account Status Master Listing"
PRGID     INIT      "IBAINP32"
VERSION   INIT      "V12.01.00"
.**********************************************************************
.*                           MAINLINE                                 *
.*                       Controlling Logic                            *
.**********************************************************************
ML0000    CALL      INIT0000            * initialization
.                                       * open files
.                                       * display header
.
ML1000    CALL      OPTN0000            * select option
          BRANCH    EXIT,ML9999
.
ML2000    CALL      PRIN0000            * print all details
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*               Open files and display header                        *
.**********************************************************************
INIT0000    
          CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening ":
                    *P60:24,"patactaf";
          OPEN      PATACTA1,"patactaf"                           
.
INIT9999  RETURN
+
.**********************************************************************
.*                           OPTN0000                                 *
.*                   Display the main option screen                   *
.**********************************************************************
OPTN0000  MOVE       FALSE,EXIT
          DISPLAY    *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                     *P1:5,*V2LON,ONE,*HOFF," = Run Master Listing";
.
OPTN1000  KEYIN      *P1:7,*EL,"Please Select : ",*V2LON,OPTION;
.
          COMPARE    TWO,OPTION
          GOTO       OPTN1000 IF NOT LESS
.
          BRANCH     OPTION,OPTN9999
          MOVE       TRUE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                             PRIN0000                               *
.*                Read file and print all details                     *
.**********************************************************************
PRIN0000  MOVE       ZERO,CPAGENO
          CLOCK      TIME,CTIMEIS
.
          MOVE       SP6,KEY6                                          *C-49016
          CALL       RSPTACT1                      * position to start of file
          CALL       RKPTACT1
          BRANCH     OVRCD,PRIN9000
.
          DISPLAY   *P10:24,"Printing Account Class : "
          DISPLAY    *P35:24,*EL,*V2LON,PTACCLSS
.
          CALL       HEAD0000                      * print header
          CALL       PREC0000                      * print a Record  
.
PRIN1000  CALL       RKPTACT1
          BRANCH     OVRCD,PRIN8000
.
          DISPLAY    *P35:24,*EL,*V2LON,PTACCLSS
.
          CALL       PREC0000                       * print a Record  
          GOTO       PRIN1000
.
PRIN8000  CALL       UND80
          PRINT      *N,*N,"*** END OF REPORT  ***";
          GOTO       PRIN9999
.
PRIN9000  DISPLAY   *P1:24,*EL,*B,"No Account Status Data Available. ";
          CALL      HITENTER
.
PRIN9999  RETURN
+
.**********************************************************************
.*                                HEAD0000                            *
.*                          Print page header                         *
.**********************************************************************
HEAD0000  CALL      HEAD80
.
          PRINT     *N;
          CALL      UND80
          PRINT     *01,"| Account Class ":
                    *17,"| Accommodation Type ":
                    *37,"| Care Type ":
                    *49,"| Medicare Suffix ":
                    *80,"|"
          ADD       THREE,CLNO
          CALL      UND80
.
HEAD9999  RETURN
+
.**********************************************************************
.*                                PREC0000                             *
.*                   Routine to print a PRECord                        *
.**********************************************************************
PREC0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
. ------- get medicare suffix --------------------------------------
.
          MOVE      "Unknown Medicare Suffix ",SUFDESC
          LOAD      SUFDESC,PTACMEDI,SUF1,SUF2,SUF3,SUF4,SUF5,SUF6,SUF7,SUF8:
                                     SUF9,SUF10
.
          PRINT     *01,"| ",PTACCLSS:
                    *17,"| ",PTACACCM:
                    *37,"| ",PTACCARE:
                    *49,"| ",SUFDESC:
                    *80,"|" 
.
          ADD       ONE,CLNO
.
PREC9999   RETURN
+
.
          INC       STD001IO
          INC       PATACTIO/INC

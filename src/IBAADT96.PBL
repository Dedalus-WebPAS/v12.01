.******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAADT96                                                 *
.* Description    :  Upload Weight FIM to Impairment Group Table              *
.******************************************************************************
.* Date           :  24/04/2018                                               *
.* Author         :  Tracey Nguyen                                            *
.* Function       :  Upload Weight FIM to Impairment Group Table              *
.* Modifications  :							      *
.*                                                                            *
.******************************************************************************
.* V10.12.02  10/07/2018  Thanh T        TSK 0859076                          *
.*            Changed to update data only when data is changed                *
.* V10.12.02  10/07/2018  Thanh T        TSK 0859076                          *
.*            Changed to upload all the valid data regardless whethere there  *
.*            are invalid data in file or not.                                * 
.* V10.12.01  24/04/2018  Tracey Nguyen  TSK 0841644                          *
.*            Created program                                                 *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCONFD/INC
          INC       PMSIGRFD/INC            * Impairment Group file

.
.         Impairment Group Upload File
.
IMPGPTXT  FILE       HL7, FIXED=1000
.
.Name     Type     Size     Description
.----     ----     ----     -----------
XIGCODE    DIM         3    Impairment Group Code
XIGDESC    DIM        80    Impairment Group Description
XIGEATI    DIM         9    FIM Eating (form 5.3)
XIGROOM    DIM         9    FIM Grooming (form 5.3)
XIGBATH    DIM         9    FIM Bathing (form 5.3)
XIGUPBD    DIM         9    FIM Dressing Upper Body (form 5.3)
XIGLWBD    DIM         9    FIM Dressing Lower Body (form 5.3)
XIGTOIL    DIM         9    FIM Toilet (form 5.3)
XIGBLAD    DIM         9    FIM Bladder (form 5.3)
XIGBOWE    DIM         9    FIM Bowel (form 5.3)
XIGTBED    DIM         9    FIM Tranfer Bed/Chair (form 5.3)
XIGTTOI    DIM         9    FIM Transfer Toilet (form 5.3)
XIGTTUB    DIM         9    FIM Transfer Tub/Shower (form 5.3)
XIGWALK    DIM         9    FIM Walk (form 5.3)  
XIGSTAI    DIM         9    FIM Stairs (form 5.3)
.
.End of Record
.
. ----- Program Variables -----
.
ADDCOUNT  FORM      8
BATCHNO   FORM      8
CRETURN   INIT      015                * carriage return character
CMDLINE   DIM       100
DIM45     DIM       45 
ERRCOUNT  FORM      8
ERRFLAG   FORM      1
ERRORMSG  DIM       65
FLDDIM9   DIM       9
FLDNAME   DIM       20
FNAMEI    DIM       150
FNAMEB    DIM       8
FORM5P3   FORM      5.3
LINECNTR  FORM      8
USERID    DIM       10
PATHNAME  DIM       100
PNAME     DIM       25
SAVDIM9   DIM       9
SEC1      FORM      "00001"
TIMEIS    DIM       8
UPDCOUNT  FORM      8
ZERO8     INIT      "0000    "
ZERO5P3   INIT      "00000.000"
DATACHG   FORM      1
.
XFLD0001  DIM       20
XFLD0002  DIM       80
XFLD0003  DIM       20
XFLD0004  DIM       20
XFLD0005  DIM       20
XFLD0006  DIM       20
XFLD0007  DIM       20
XFLD0008  DIM       20
XFLD0009  DIM       20
XFLD0010  DIM       20
XFLD0011  DIM       20
XFLD0012  DIM       20
XFLD0013  DIM       20
XFLD0014  DIM       20
XFLD0015  DIM       20
.
.
PRGID     INIT      "IBAADT96"
PRGNAM    INIT      "Upload Impairment Group Table"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000              * Initailize variables & open files
          CALL      KOPT0000              * Keyin Upload or Update option
          BRANCH    EXIT,ML9999
.
          CALL      KASC0000              * Keyin ascii file
          BRANCH    EXIT,ML9900
.
          CALL      KUSR0000              * Keyin user id
          BRANCH    EXIT,ML0000
.
          CALL      KPTH0000              * Keyin pathname
          BRANCH    EXIT,ML0000
.
          CALL      PRHD0000              * Print header
.
          CALL      CTOT0000              * Clear total vars
.
          CALL      POST0000              * Yes, Update/Write to pmsigraf table
.
ML2000    CALL      PTOT0000              * Print report totals
.
          GOTO      ML9999
.
ML9900    CALL      HEAD132
          PRINT     *N,"Failed to open upload file. "
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000                                   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                         * Display screen heading
.
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,"Opening pmsigraf";
.
          DISPLAY   *P64:24,"pmsigraf"               * Impairment Groups
          OPEN      PMSIGRA1,"pmsigraf"
.
          MOVE      ZERO,ERRFLAG
INIT9999  RETURN
+
.******************************************************************************
.*                                 KOPT0000                                   *
.*                            Upload Casemix Funding for TLOS                 *
.******************************************************************************
KOPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COPTION
          DISPLAY   *P1:6,*EF:
                    *P1:6,*V2LON,"0",*HOFF," = Exit":
                    *P1:7,*V2LON,"1",*HOFF," = Upload Impairment Group":
                    *P1:10,"Select Option : ";
.
KOPT1000  KEYIN     *P17:10,*V2LON,COPTION;
          BRANCH    COPTION,KOPT9999
.
          COMPARE   ZERO,COPTION
          GOTO      KOPT9000 IF EQUAL
          BEEP
          GOTO      KOPT1000
.
KOPT9000  MOVE      ONE,EXIT
KOPT9999  RETURN
+
.******************************************************************************
.*                                 KASC0000                                   *
.*                            Keyin ASCI file                                 *
.******************************************************************************
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEI WITH SP70,SP70,SP70
.
          DISPLAY   *P19:13,*EF,"Impairment Group Load File";
.
          KEYIN     *P1:14,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEI
.
          PACK      FNAMEI,FNAMEI,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEI
          GOTO      KASC9000 IF EQUAL
.
          SCAN      ".txt",FNAMEI
          IF        !@EQUAL
            SQUEEZE   FNAMEI
            ENDSET    FNAMEI
            APPEND    ".txt",FNAMEI      * add ".txt" if there is a path
            APPEND    SP70,FNAMEI
            RESET     FNAMEI
          ENDIF
          RESET     FNAMEI
.
          DISPLAY   *P26:14,*V2LON,*EL,FNAMEI;
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      IMPGPTXT,FNAMEI
          TRAPCLR   IO
          BRANCH    OVRCD,KASC8000
.
          CALL      EXCUS000
.
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
+
.******************************************************************************
.*                                  KUSR0000                                  *
.*                     Keyin user id                                          *
.******************************************************************************
KUSR0000  KEYIN     *P1:16,*EL,"Keyin Username: ",*P26:16,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.******************************************************************************
.*                                  KPTH0000                                  *
.*                     Keyin path name                                        *
.******************************************************************************
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P1:17,*EL,"Keyin Pathname: ",*P26:17,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.******************************************************************************
.*                                  VALD0000                                  *
.* Validate all records from input file                                       *
.******************************************************************************
VALD0000  MOVE      ZERO,LINECNTR
          MOVE      ZERO,EXIT
.
          OPEN      IMPGPTXT,FNAMEI         * Open input file
VALD1000  READ      IMPGPTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008,XFLD0009,XFLD0010:
                                 XFLD0011,XFLD0012,XFLD0013,XFLD0014,XFLD0015
          GOTO      VALD9000 IF OVER        * Read input file until EOF
.
          ADD       ONE,LINECNTR            * Input file line counter for error
.
          CALL      VREC0000                * Validate input record
.
          CALL      CLXFLD00                * Clear XFLD input fields
.
          GOTO      VALD1000                * Read next input record
.
VALD9000  CLOSE     IMPGPTXT                * Close input file
.
VALD9999  RETURN
+
.******************************************************************************
.*                                  POST0000                                  *
.* Update all validated input records                                         *
.******************************************************************************
POST0000  OPEN      IMPGPTXT,FNAMEI         * Open input file
          MOVE      ZERO,LINECNTR
.
POST1000  READ      IMPGPTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008,XFLD0009,XFLD0010:
                                 XFLD0011,XFLD0012,XFLD0013,XFLD0014,XFLD0015
          GOTO      POST9000 IF OVER        * Read input file until EOF
.
          ADD       ONE,LINECNTR            * Input file line counter
          MOVE      ZERO,EXIT
          CALL      VREC0000                * Validate input record 
          BRANCH    EXIT,POST8000           * No, read next record
.
          CALL      SETF0000                * Set extract fields
.
          CALL      UIGP0000                * Update/Add Impairment Group Record
.
POST8000  CALL      CLXFLD00                * Clear XFLD input fields
          GOTO      POST1000                * Read next input record
.
POST9000  CLOSE     IMPGPTXT                * Close input file
.
POST9999  RETURN
+
.******************************************************************************
.*                                  SETF0000                                  *
.* Set fields for update                                                      *
.******************************************************************************
SETF0000  PACK      XIGCODE,XFLD0001,SP70   * Impairment Group Code
          PACK      XIGDESC,XFLD0002,SP70   * Impairment Group Description
.
          MOVE      SP70,SAVDIM9            * FIM Eating
          SQUEEZE   XFLD0003
          MOVE      XFLD0003,SAVDIM9
          CALL      SETLN000
          MOVE      SAVDIM9,XIGEATI
.
          MOVE      SP70,SAVDIM9            * FIM Grooming
          SQUEEZE   XFLD0004
          MOVE      XFLD0004,SAVDIM9
          CALL      SETLN000
          MOVE      SAVDIM9,XIGROOM
.
          MOVE      SP70,SAVDIM9            * FIM Bathing
          SQUEEZE   XFLD0005
          MOVE      XFLD0005,SAVDIM9
          CALL      SETLN000
          MOVE      SAVDIM9,XIGBATH
.
          MOVE      SP70,SAVDIM9            * FIM Dressing Upper Body
          SQUEEZE   XFLD0006
          MOVE      XFLD0006,SAVDIM9
          CALL      SETLN000
          MOVE      SAVDIM9,XIGUPBD
.
          MOVE      SP70,SAVDIM9            * FIM Dressing Lower Body
          SQUEEZE   XFLD0007
          MOVE      XFLD0007,SAVDIM9
          CALL      SETLN000
          MOVE      SAVDIM9,XIGLWBD
.
          MOVE      SP70,SAVDIM9            * FIM Toilet
          SQUEEZE   XFLD0008
          MOVE      XFLD0008,SAVDIM9
          CALL      SETLN000
          MOVE      SAVDIM9,XIGTOIL
.
          MOVE      SP70,SAVDIM9            * FIM Bladder
          SQUEEZE   XFLD0009
          MOVE      XFLD0009,SAVDIM9
          CALL      SETLN000
          MOVE      SAVDIM9,XIGBLAD
.
          MOVE      SP70,SAVDIM9            * FIM Bowel 
          SQUEEZE   XFLD0010
          MOVE      XFLD0010,SAVDIM9
          CALL      SETLN000
          MOVE      SAVDIM9,XIGBOWE
.
          MOVE      SP70,SAVDIM9            * FIM Transfer Bed/Chair
          SQUEEZE   XFLD0011
          MOVE      XFLD0011,SAVDIM9
          CALL      SETLN000
          MOVE      SAVDIM9,XIGTBED
.
          MOVE      SP70,SAVDIM9            * FIM Transfer Toilet
          SQUEEZE   XFLD0012
          MOVE      XFLD0012,SAVDIM9
          CALL      SETLN000
          MOVE      SAVDIM9,XIGTTOI
.
          MOVE      SP70,SAVDIM9            * FIM Transfer Tub/Shower
          SQUEEZE   XFLD0013
          MOVE      XFLD0013,SAVDIM9
          CALL      SETLN000
          MOVE      SAVDIM9,XIGTTUB
.
          MOVE      SP70,SAVDIM9            * FIM Walk/Locomotion
          SQUEEZE   XFLD0014
          MOVE      XFLD0014,SAVDIM9
          CALL      SETLN000
          MOVE      SAVDIM9,XIGWALK
.
          MOVE      SP70,SAVDIM9            * FIM Stairs
          SQUEEZE   XFLD0015
          MOVE      XFLD0015,SAVDIM9
          CALL      SETLN000
          MOVE      SAVDIM9,XIGSTAI
.
SETF9999  RETURN
+
.******************************************************************************
.*                                  SETLN000                                  *
.* Set numeric fields format with decimal places for update.                  *
.* This is to stop automatic rounding up when moving a dim field to a form    *
.* field.                                                                     *
.******************************************************************************
SETLN000  SCAN      ".",SAVDIM9                    * Check for decimal point
          GOTO      SETLN999 IF NOT EQUAL          * Not found, exit
.
SETLN010  BUMP      SAVDIM9                        * First decimal place
          GOTO      SETLN030 IF EOS                
          BUMP      SAVDIM9                        * Second decimal place
          GOTO      SETLN030 IF EOS
          BUMP      SAVDIM9                        * Third decimal place
          GOTO      SETLN030
.
SETLN030  LENSET    SAVDIM9                        * Set decimal places
          RESET     SAVDIM9                        * Reset fptr back to 1
.
SETLN999  RETURN
.
.----------------------------------------------------------------------
. Update/Add Impairment Group Record
.----------------------------------------------------------------------
UIGP0000  CALL      CLRIGP00                * Clear pmsigraf fields
          PACK      KEY3,XIGCODE,SP70
          CALL      RDPMIGR1                * Check if pmgigraf exists
.
          IF        OVRCD=0
            CALL      CHKDAT00 
            IF        DATACHG = 1
              CALL      MVDATA00
              CALL      UPPMIGR1            * Update existing pmsigraf rec
              ADD       ONE,UPDCOUNT        * Increment updated rec counter
            ENDIF
          ELSE
            CALL      MVDATA00
            CALL      WRPMIGR1              * Write new record to pmsigraf
            ADD       ONE,ADDCOUNT          * Increment added rec counter
          ENDIF
.
UIGP9999  RETURN
.
.----------------------------------------------------------------------
. Move new data to Impairment Group Record                            
.----------------------------------------------------------------------
CHKDAT00  MOVE      ZERO,DATACHG
.
          MATCH     XIGDESC,PMIGDESC   
          IF        !@EQUAL
            MOVE      ONE,DATACHG
            GOTO      CHKDAT99
          ENDIF
.         
          MOVE      XIGEATI,FORM5P3         * FIM Eating 
          COMPARE   FORM5P3,PMIGEATI                  
          IF        !@EQUAL   
            MOVE      ONE,DATACHG
            GOTO      CHKDAT99
          ENDIF
.
          MOVE      XIGROOM,FORM5P3         * FIM Grooming 
          COMPARE   FORM5P3,PMIGROOM                  
          IF        !@EQUAL
            MOVE      ONE,DATACHG
            GOTO      CHKDAT99
          ENDIF
.
          MOVE      XIGBATH,FORM5P3         * FIM Bathing
          COMPARE   FORM5P3,PMIGBATH                  
          IF        !@EQUAL
            MOVE      ONE,DATACHG
            GOTO      CHKDAT99
          ENDIF
.
          MOVE      XIGUPBD,FORM5P3         * FIM Dressing Upper Body          
          COMPARE   FORM5P3,PMIGUPBD                  
          IF        !@EQUAL
            MOVE      ONE,DATACHG
            GOTO      CHKDAT99
          ENDIF
.
          MOVE      XIGLWBD,FORM5P3         * FIM Dressing Lower Body 
          COMPARE   FORM5P3,PMIGLWBD                  
          IF        !@EQUAL
            MOVE      ONE,DATACHG
            GOTO      CHKDAT99
          ENDIF
.
          MOVE      XIGTOIL,FORM5P3         * FIM Toilet          
          COMPARE   FORM5P3,PMIGTOIL                  
          IF        !@EQUAL
            MOVE      ONE,DATACHG
            GOTO      CHKDAT99
          ENDIF
.
          MOVE      XIGBLAD,FORM5P3         * FIM Bladder           
          COMPARE   FORM5P3,PMIGBLAD                  
          IF        !@EQUAL
            MOVE      ONE,DATACHG
            GOTO      CHKDAT99
          ENDIF
.
          MOVE      XIGBOWE,FORM5P3         * FIM Bowel       
          COMPARE   FORM5P3,PMIGBOWE                  
          IF        !@EQUAL
            MOVE      ONE,DATACHG
            GOTO      CHKDAT99
          ENDIF
.
          MOVE      XIGTBED,FORM5P3         * FIM Transfer Bed/Chair   
          COMPARE   FORM5P3,PMIGTBED                        
          IF        !@EQUAL
            MOVE      ONE,DATACHG
            GOTO      CHKDAT99
          ENDIF
.
          MOVE      XIGTTOI,FORM5P3         * FIM Transfer Toilet         
          COMPARE   FORM5P3,PMIGTTOI                  
          IF        !@EQUAL
            MOVE      ONE,DATACHG
            GOTO      CHKDAT99
          ENDIF
.
          MOVE      XIGTTUB,FORM5P3         * FIM Transfer Tub/Shower         
          COMPARE   FORM5P3,PMIGTTUB                  
          IF        !@EQUAL
            MOVE      ONE,DATACHG
            GOTO      CHKDAT99
          ENDIF
.
          MOVE      XIGWALK,FORM5P3         * FIM Walk/Locomotion         
          COMPARE   FORM5P3,PMIGWALK                  
          IF        !@EQUAL
            MOVE      ONE,DATACHG
            GOTO      CHKDAT99
          ENDIF
.
          MOVE      XIGSTAI,FORM5P3         * FIM Stairs         
          COMPARE   FORM5P3,PMIGSTAI                  
          IF        !@EQUAL
            MOVE      ONE,DATACHG
            GOTO      CHKDAT99
          ENDIF
.
CHKDAT99  RETURN
.
.----------------------------------------------------------------------
. Move new data to Impairment Group Record
.----------------------------------------------------------------------
MVDATA00  MOVE      XIGCODE,PMIGCODE        * Impairment Group Code
          MOVE      XIGDESC,PMIGDESC        * Impairment Group Description
          MOVE      XIGEATI,PMIGEATI        * FIM Eating
          MOVE      XIGROOM,PMIGROOM        * FIM Grooming
          MOVE      XIGBATH,PMIGBATH        * FIM Bathing
          MOVE      XIGUPBD,PMIGUPBD        * FIM Dressing Upper Body
          MOVE      XIGLWBD,PMIGLWBD        * FIM Dressing Lower Body
          MOVE      XIGTOIL,PMIGTOIL        * FIM Toilet
          MOVE      XIGBLAD,PMIGBLAD        * FIM Bladder
          MOVE      XIGBOWE,PMIGBOWE        * FIM Bowel
          MOVE      XIGTBED,PMIGTBED        * FIM Transfer Bed/Chair
          MOVE      XIGTTOI,PMIGTTOI        * FIM Transfer Toilet
          MOVE      XIGTTUB,PMIGTTUB        * FIM Transfer Tub/Shower
          MOVE      XIGWALK,PMIGWALK        * FIM Walk/Locomotion
          MOVE      XIGSTAI,PMIGSTAI        * FIM Stairs
.
MVDATA99  RETURN
+
.******************************************************************************
.*                                  PRHD0000                                  *
.*                        Print the selection details                         *
.******************************************************************************
PRHD0000  CALL      HEAD132
.
          PRINT     *N,"Date                 : ",CDATE:
                    *N,"Time                 : ",CTIMEIS:
                    *N,"User ID              : ",USERID:
                    *N,"Weight FIM Load File : ",PATHNAME
.
          CALL      UND132
.
          PRINT     *1,"| Line",*12,"| Error ",*80,"| Record Detail":
                    *132,"|"
.
          CALL      UND132
.
PRHD9999  RETURN
+
.******************************************************************************
.         Execute us1 command before processing
.******************************************************************************
EXCUS000  CLEAR     CMDLINE
          APPEND    "ibaadt96.us1 ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
EXCUS999  RETURN
+
.******************************************************************************
.         Clear XFLDnnnn variables
.******************************************************************************
CLXFLD00  MOVE      SP70,XFLD0001
          MOVE      SP70,XFLD0002
          PACK      XFLD0002,SP70,SP70
          MOVE      SP70,XFLD0003
          MOVE      SP70,XFLD0004
          MOVE      SP70,XFLD0005
          MOVE      SP70,XFLD0006
          MOVE      SP70,XFLD0007
          MOVE      SP70,XFLD0008
          MOVE      SP70,XFLD0009
          MOVE      SP70,XFLD0010
          MOVE      SP70,XFLD0011
          MOVE      SP70,XFLD0012
          MOVE      SP70,XFLD0013
          MOVE      SP70,XFLD0014
          MOVE      SP70,XFLD0015
.
CLXFLD99  RETURN
+
.******************************************************************************
.         Clear PMSIGRFD variables               
.******************************************************************************
CLRIGP00  MOVE      SP70,PMIGCODE
          MOVE      SP70,PMIGDESC
          PACK      PMIGDESC,SP70,SP70
          MOVE      ZERO,PMIGEATI
          MOVE      ZERO,PMIGROOM
          MOVE      ZERO,PMIGBATH
          MOVE      ZERO,PMIGUPBD
          MOVE      ZERO,PMIGLWBD
          MOVE      ZERO,PMIGTOIL
          MOVE      ZERO,PMIGBLAD
          MOVE      ZERO,PMIGBOWE
          MOVE      ZERO,PMIGTBED
          MOVE      ZERO,PMIGTTOI
          MOVE      ZERO,PMIGTTUB
          MOVE      ZERO,PMIGWALK
          MOVE      ZERO,PMIGSTAI
          MOVE      SP70,PMIGSPAR
.
CLRIGP99  RETURN
+
.******************************************************************************
. Clear total vars
.******************************************************************************
CTOT0000  MOVE      ZERO,ERRCOUNT
          MOVE      ZERO,UPDCOUNT
          MOVE      ZERO,ADDCOUNT
.
CTOT999   RETURN
+
.******************************************************************************
. Print total line
.******************************************************************************
PTOT0000  CALL      UND132
.
          PRINT     *1,"| Total Errors  : ",ERRCOUNT,*132,"|"
          PRINT     *1,"| Total Updated : ",UPDCOUNT,*132,"|"
          PRINT     *1,"| Total Added   : ",ADDCOUNT,*132,"|"
.
          CALL      UND132
.
PTOT9999  RETURN
+
.******************************************************************************
. Validate input record fields
.******************************************************************************
VREC0000  PACK      XIGCODE,XFLD0001,SP70          * Impairment Group Code 
          MATCH     SP70,XFLD0001
          IF        @EQUAL | @EOS
            CLEAR     ERRORMSG
            APPEND    "Impairment Group Code cannot be spaces",ERRORMSG
            APPEND    SP70,ERRORMSG
            RESET     ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
          ENDIF
.
          PACK      XIGDESC,XFLD0002,SP70,SP70      * Impairment Group Desc
          MATCH     SP70,XFLD0002
          IF        @EQUAL | @EOS
            CLEAR     ERRORMSG
            APPEND    "Impairment Group Description cannot be spaces",ERRORMSG
            APPEND    SP70,ERRORMSG
            RESET     ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
          ENDIF
.
.         Validate numeric FIM values and ranges
.
          MOVE      SP70,FLDDIM9       
          MOVE      XFLD0003,FLDDIM9       
          MOVE      "FIM Eating",FLDNAME
          CALL      VRNG0000
.
          MOVE      SP70,FLDDIM9
          MOVE      XFLD0004,FLDDIM9
          MOVE      "FIM Grooming",FLDNAME
          CALL      VRNG0000
.
          MOVE      SP70,FLDDIM9
          MOVE      XFLD0005,FLDDIM9
          MOVE      "FIM Bath",FLDNAME
          CALL      VRNG0000
.
          MOVE      SP70,FLDDIM9
          MOVE      XFLD0006,FLDDIM9
          MOVE      "FIM Upper",FLDNAME
          CALL      VRNG0000
.
          MOVE      SP70,FLDDIM9
          MOVE      XFLD0007,FLDDIM9
          MOVE      "FIM Lower",FLDNAME
          CALL      VRNG0000
.
          MOVE      SP70,FLDDIM9
          MOVE      XFLD0008,FLDDIM9
          MOVE      "FIM Toilet",FLDNAME
          CALL      VRNG0000
.
          MOVE      SP70,FLDDIM9
          MOVE      XFLD0009,FLDDIM9
          MOVE      "FIM Bladder",FLDNAME
          CALL      VRNG0000
.
          MOVE      SP70,FLDDIM9
          MOVE      XFLD0010,FLDDIM9
          MOVE      "FIM Bowel",FLDNAME
          CALL      VRNG0000
.
          MOVE      SP70,FLDDIM9
          MOVE      XFLD0011,FLDDIM9
          MOVE      "FIM Transfer Bed",FLDNAME
          CALL      VRNG0000
.
          MOVE      SP70,FLDDIM9
          MOVE      XFLD0012,FLDDIM9
          MOVE      "FIM Transfer Toilet",FLDNAME
          CALL      VRNG0000
.
          MOVE      SP70,FLDDIM9
          MOVE      XFLD0013,FLDDIM9
          MOVE      "FIM Transfer Tub",FLDNAME
          CALL      VRNG0000
.
          MOVE      SP70,FLDDIM9
          MOVE      XFLD0014,FLDDIM9
          MOVE      "FIM Walk",FLDNAME
          CALL      VRNG0000
.
          MOVE      SP70,FLDDIM9
          MOVE      XFLD0015,FLDDIM9
          MOVE      "FIM Stairs",FLDNAME
          CALL      VRNG0000
.
VREC9999  RETURN
.
.******************************************************************************
. Validate numeric input record fields  
.******************************************************************************
VRNG0000  MATCH     SP10,FLDDIM9                         * if spaces/empty
          IF        @EQUAL | @EOS
            CLEAR     ERRORMSG
            APPEND    FLDNAME,ERRORMSG
            APPEND    " cannot be spaces  ",ERRORMSG
            APPEND    SP70,ERRORMSG
            RESET     ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
            GOTO      VRNG9999
          ENDIF
.
          MOVE      SP70,SAVDIM9
          MOVE      FLDDIM9,SAVDIM9
          SQUEEZE   SAVDIM9
.
          TYPE      SAVDIM9                               * If not numeric  
          IF        !@EQUAL
            CLEAR     ERRORMSG
            APPEND    FLDNAME,ERRORMSG
            APPEND    " must be numeric: ",ERRORMSG
            APPEND    FLDDIM9,ERRORMSG
            APPEND    SP70,ERRORMSG
            RESET     ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
            GOTO      VRNG9999
          ENDIF     
.
          MOVE      ZERO5P3,FORM5P3 
          CALL      SETLN000                         
          MOVE      SAVDIM9,FORM5P3                     * Test Out of range
.
          IF        FORM5P3 < 0
            CLEAR     ERRORMSG
            APPEND    FLDNAME,ERRORMSG
            APPEND    " value cannot be negative :  ",ERRORMSG
            APPEND    SAVDIM9,ERRORMSG
            APPEND    SP70,ERRORMSG
            RESET     ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
            GOTO      VRNG9999
          ENDIF
.
          IF        FORM5P3 < 10 & FORM5P3 > 0
            GOTO    VRNG9999
          ELSE
            CLEAR     ERRORMSG
            APPEND    FLDNAME,ERRORMSG
            APPEND    " value must be >= 0.001 & <= 9.999 :  ",ERRORMSG
            APPEND    SAVDIM9,ERRORMSG
            APPEND    SP70,ERRORMSG
            RESET     ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
            GOTO      VRNG9999
          ENDIF
.
VRNG9999  RETURN
+
.******************************************************************************
. Print error line
.******************************************************************************
PERR0000  ADD       ONE,ERRCOUNT              * Total error count
.
          PACK      XIGCODE,XFLD0001,SP70     * Impairment Group Code
          MOVE      XFLD0002,DIM45            * Impairment Group Description
.
          PRINT     *1,"|",LINECNTR,*12,"|",ERRORMSG,*80,"|":
                    XIGCODE,*84," | ",DIM45,*132,"|"
.
PERR9999  RETURN
+
.******************************************************************************
          INC       STD001IO
.
          INC       PMSIGRIO/INC            * Impairment Group
.

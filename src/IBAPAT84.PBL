.******************************************************************************
.* System         : PATIENT                                                   *
.* Program        : IBAPAT84                                                  *
.* Desc.          : Ward Report                                               *
.******************************************************************************
.* Date           : 13/10/89                                                  *
.* Author         : K.Peak                                                    *
.* Modifications  :                                                           *
.*        V12.00.01 23/05/2025  Bella Turco    Task 0955096                   *
.*                  Alphanumeric changes                                      *
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*        V10.08.01 03/10/2016 Ebon Clements   TSK 0813912                    *
.*                  Added multi hospital functionality                        *
.******************************************************************************
.*        V10.03.01 31/12/2012 Patrick Adair   CAR 261630                     *
.*                       Removed port number from temp file name              *
.******************************************************************************
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.******************************************************************************
.*        V9.08.01  22/03/2007  Neelkamal PYLA CAR 136631                     *
.*                  Commented moving Unknown value to sex field in PRT4000 to *
.*                  display the exact sex description.                        *
.******************************************************************************
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.******************************************************************************
.*        V5.08.03  29/08/2000  Tonii                                         *
.*                  Mods to accept Unknown & Indeterminate as patient sex desc*
.*        V5.08.02  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 10/04/2000  Greg Horvat                                   *
.*                  Removed MATADAFD MATADBFD & MATADBIO                      *
.******************************************************************************
.*        V5.07.B02 25/01/1999  Nicole Harrington 507 rebound 134             *
.*                  Mods to use HEAD132                                       *
.*                   09/11/1998  Glenn Berry      5.07 changes                *
.******************************************************************************
.*        V5.01.02  13/10/89 K.Peak                                           *
.*                  Rewrote program                                           *
.*                  Added standby & on leave patients SRF 102655              *
.*        V5.01.121 27/11/90 Glen Hobby                                       *
.*                  Recompiled for changes to PATMX1FD                        *
.*        V5.01.122 19/12/90 i chung           SRF 107377                     *
.*                  Fixed days in hospital calculation.                       *
.*                  Added Wards scan (PATWRDDS) for Option 2                  *
.*        V5.01.123 21/05/91 Steve O'Gorman                                   *
.*                  Fixed Errors from the New Compiler                        *
.*        V5.01.124 30/08/1991    Justin Coates                               *
.*                  If on leave must be Short-term leave.                     *
.*        V5.01.125 11/06/93 J.Tan    SRF 121660                              *
.*                  Recompiled for PATWRDDS (check for active wards)          *
.*        V5.01.126 29/10/93 Whirlpool SRF 124607                             *
.*                  Added parameter to print closed wards or not              *
.*        V5.03.001 20/07/95 Howellsy  SRF 640508                             *
.*                  Fixed Ward on report when there is no data found.         *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC                 * Codes file
          INC       PATDO1FD/INC                 * Doctors file
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC                 * Patients Masterfile details
          INC       PATMI1FD/INC                 * Inpatients details file
          INC       PMSVX1FD/INC                 * Inpatients details file
          INC       PATNOBFD/INC                 * No Bed file            
          INC       PATONLFD/INC                 * On Leave file          
          INC       PATTRNFD/INC                 * Transfer file          
          INC       PATWR1FD/INC                 * Ward Bed file          
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.**********************************************************************
.*                       VARIABLES                                    *
.**********************************************************************
.
A4YEAR    DIM       4           * century and year of admission date
AJDAY     FORM      3           * julian admission day
.
CODEM     INIT      "M "
CODEC     INIT      "C "
CDESC     DIM       20
CJDAY     FORM      4           * julian current day
CMDLINE   DIM       50
.
DHHMM     DIM       5
DPSKIP    DIM       3
DALPHA    DIM       6
DRLGTYP   DIM       30
.
FNAMEU    DIM       8
FNAMER    DIM       8
FNAMEI    DIM       8
.
HOSPID    DIM       3
HOSPNAME  DIM       50
.
MIN1      FORM      1
.
NAME      DIM       22          * mfname.msname
.
OPCND     FORM      1
.
PAGENO    FORM      3
PCLSWARD  FORM      1                       * Print close ward ? 0 = no 1 = yes
PSKIP     FORM      1
PNAME     DIM       22
PSTWRDP1  IFILE     SQL, FIXED=118, KEY="U1-36"             * was 116  *C-49016
PSTSRT    FILE      ASCII, FIXED=256
PSTROL    FILE      ASCII, FIXED=256
PTCNTLVE  FORM      1         * using type of leave ?
PTCNPRCW  FORM      1         * Don't Print closed wards 
.
SAVBED    DIM       3
SAVDESC   DIM       20
STATUS    FORM      1
SRLGTYP   DIM       3
SWARD     DIM       3
SBED      DIM       3
SAWARD    DIM       3
.
.
UNIQUE    FORM      8
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
.------------------------------------------------------------
.         Temporary file variables
.
.Name     Type    Length    Physical     Start
.----     ----    ------    --------     -----
.PSNAME   DIM       20           20         1
.PURNO    DIM       8            8        21
DUNIQUE   DIM       8            8        29
.PTITL    DIM       4            4        37
.PGNAME   DIM       25           25        41
.AADMNO   DIM       8            5        66
WRDBED    DIM       7            7        71
DSEX      DIM       8            8        78
DMSTAT    DIM       9            9        86
DNAME     DIM       17           17       95
.PSAGE    FORM      3            3       112
PHDAYS    FORM      4            3       115
.
.End of Record                              118
.
.  redefine form fields from key
.  -----------------------------
.Name     Type      Length    Start
.----     ----      ------    -----
.UNIQUE   FORM         6     
.------------------------------------------------------------
.
PRGID     INIT      "IBAPAT84"
PRGNAM    INIT      "Ward Report"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                       MAINLINE                                     *
.**********************************************************************
.
ML0000    CALL      INIT0000           * initialisation
.
ML1000    CALL      CLR0000            * clear variables
.
          CALL      SCR0000            * select option
          BRANCH    EXIT,ML9000
.
ML1500    BRANCH    OPTION,ML2000,ML4000
.
.         Surname Sequence
.
ML2000    CALL      DOPT0000           * display which option
          CALL      KHOS0000           * keyin hospital
          BRANCH    EXIT,ML1000
          CALL      SUR0000            * keyin surname
          BRANCH    EXIT,ML9000
          CALL      RTMP0000           * read temporary file
          GOTO      ML8000
.
.         Ward/Bed Sequence
.
ML4000    CALL      DOPT0000           * display which option
          CALL      KHOS0000           * keyin hospital
          BRANCH    EXIT,ML1000
          CALL      KWRD0000           * keyin ward/bed sequence 
          BRANCH    EXIT,ML1000
.
ML8000    CALL      END0000            * end of report
          GOTO      ML1000
.
ML9000    CHAIN     PGM
+
.**********************************************************************
.*                       INIT0000                                     *
.*                    Initialisation                                  *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patonlvf";
          OPEN      PATONLV1,"patonlvf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A2,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
.
          DISPLAY   *P64:24,"patnobef";
          OPEN      PATNOBE1,"patnobef"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TEN;*13,PTCNTLVE
          READ      CONTROLF,SEVENTY7;*198,PTCNPRCW
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        PTCNTLVE = 1
            DISPLAY   *P64:24,"pattranf";
            OPEN      PATTRAN2,"pattranf"
          ENDIF
.
.         set CNEWDS for PATWRDDS
.
          MOVE      ONE,CNEWDS
.
          RETURN
+
.**********************************************************************
.*                       CLR0000                                      *
.*                    Clear Variables                                 *
.**********************************************************************
.
CLR0000   MOVE      ZERO,UNIQUE        * counter for temporary file
          MOVE      "60",CLNO          * page length
          MOVE      ZERO,PAGENO       * page number
          MOVE      SP20,SAVDESC       * Option 2 header description
          RETURN
+
.**********************************************************************
.*                       SCR0000                                      *
.*                    Select Option                                   *
.**********************************************************************
.
SCR0000   MOVE      FALSE,EXIT
          HLINE     *G37,2,46,80
          DISPLAY   *P1:3,*EF: 
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Patient Surname Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Ward/Bed Sequence":
                    *P1:8,"Select Option :";
.
SCR1000   KEYIN     *P17:8,*EL,*DV,UNDLN1,*P17:8,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      SCR9000 IF EQUAL
.
          BRANCH    OPTION,SCR9999,SCR9999
          BEEP
          GOTO      SCR1000
.
SCR9000   MOVE      TRUE,EXIT
.
SCR9999   RETURN
+
.**********************************************************************
.*                       DOPT0000                                     *
.*                    Which Option ?                                  *
.**********************************************************************
.
DOPT0000  BRANCH    OPTION,DOPT1000,DOPT2000
.
DOPT1000  DISPLAY   *P46:2,*V2LON,"- Surname Sequence "
          GOTO      DOPT9999
.
DOPT2000  DISPLAY   *P46:2,*V2LON,"- Ward/Bed Sequence "
.
DOPT9999  RETURN
+
.**********************************************************************
.*                       SUR0000                                      *
.*                    Surname Sequence                                *
.**********************************************************************
.
SUR0000   MOVE      FALSE,EXIT
          DISPLAY   *P1:24,*EL,*P26:24,*V2LON:
                    "** Initialising Report File **",*W2,*P1:12;
.
          CALL      IND0000                * create temporary file
          BRANCH    OPCND,SUR9000
.
          MOVE      TWO,ASTAT              * current admission
.
SUR1000   PACK      KEY9 WITH ASTAT,SP8
          CALL      RDSMISS2
.
          DISPLAY   *P1:12,*EF,*P30:24,"Scanning :";
.
SUR2000   CALL      RDKMISS2
          BRANCH    OVRCD OF SUR9500
.
          COMPARE   FIVE,ASTAT             * cancelled
          GOTO      SUR9500 IF EQUAL
.
          COMPARE   THREE,ASTAT            * discharged
          GOTO      SUR3000 IF NOT EQUAL
.
.         Process on leave patients
.
          MOVE      FOUR,ASTAT             * on leave
          GOTO      SUR1000
.
SUR3000   DISPLAY   *P41:24,*V2LON,AADMNO;
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF SUR2000
.
          IF         IBCNMHOS=1
            MATCH     SP70,HOSPID
            IF        !@EQUAL
              MATCH     PMVXMHOS,HOSPID          * Check hospital id
              GOTO      SUR2000 IF NOT EQUAL
            ENDIF
          ENDIF
.
SUR4000   PACK      WRDBED WITH AWARD,SLASH,ABED
          MATCH     SP3,AWARD
          GOTO      SUR4500 IF NOT EQUAL
          MOVE      "STANDBY",WRDBED
.
SUR4500   BRANCH    ASTAT OF SUR2000,SUR6000,SUR2000,SUR5000
          GOTO      SUR2000
.
SUR5000   MOVE      ZEROVISN,WADMNO
          PACK      KEY14 WITH AWARD,ABED,AADMNO
          CALL      RDONLV1
          BRANCH    OVRCD OF SUR2000
.
          MATCH     OADMNO,AADMNO
          GOTO      SUR2000 IF NOT EQUAL
.
          CALL      VOLP0000                * check if right type of leave
          BRANCH    EXIT,SUR2000            * not correct type
.
          MOVE      "LEAVE  ",WRDBED
          MOVE      SP3,AWARD
          MOVE      SP3,ABED
.
SUR6000   MOVE      SP30,DNAME
.
          MATCH     SP3,PCONT
          GOTO      SUR7000 IF EQUAL
.
          PACK      KEY5 WITH CODEC,PCONT
          CALL      RDCODE1
          BRANCH    OVRCD OF SUR7000
.
          MOVE      TDESC,DNAME
.
SUR7000   MOVE      SP30,DMSTAT
          MATCH     SP3,PMSTAT
          GOTO      SUR7500 IF EQUAL
.
          PACK      KEY5 WITH CODEM,PMSTAT
          MOVE      SP30,TDESC
          CALL      RDCODE1
          MOVE      TDESC,DMSTAT
.
SUR7500   CALL      CALC0000
.
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DSEX
.* ******************************************   End of Changes         *C-49016
.
SUR8000   ADD       ONE,UNIQUE
          PACK      KEY36 WITH PSNAME,PURNO,UNIQUE
.
          WRITE     PSTWRDP1,KEY36;KEY36,PTITL,PGNAME: 
                                   AADMNO,WRDBED,DSEX,DMSTAT,DNAME:
                                   PSAGE,PHDAYS
          GOTO      SUR2000
.
SUR9000   MOVE      ONE,EXIT
          GOTO      SUR9999
.
SUR9500   MOVE      ZERO,EXIT
.
SUR9999   RETURN
+
.**********************************************************************
.*                       KWRD0000                                     *
.*                    Ward/Bed Sequence                               *
.**********************************************************************
.
KWRD0000  MOVE      FALSE,EXIT
          MOVE      "16" TO CVERT
.
          DISPLAY   *P1:12,*EF,"Enter Ward :":
                    *P30:12,"<",*V2LON,"ENTER",*HOFF,"> for all Wards";
.
KWRD0100  KEYIN     *P14:12,*DV,UNDLN3,*P14:12,*V2LON,SAWARD
          RESET     SAWARD
          GOTO      KWRD1000 IF EOS
.
          ENDSET    SAWARD
          APPEND    SP3,SAWARD
          RESET     SAWARD
.
          CMATCH    QUEST,SAWARD
          GOTO      KWRD0500 IF NOT EQUAL
.
          CALL      PATWRDDS                          * Wards scan
.
KWRD0200  DISPLAY   *P1:24,*EF,"Enter Ward :":
                    *P30:24,"<",*V2LON,"ENTER",*HOFF,"> for all Wards";
          KEYIN     *P14:24,*DV,UNDLN3,*P14:24,*V2LON,SAWARD
          RESET     SAWARD
          GOTO      KWRD0400 IF EOS
.
          ENDSET    SAWARD
          APPEND    SP3,SAWARD
          RESET     SAWARD
.
          PACK      KEY6 WITH SAWARD,SP6
          CALL      RDWARD1
          BRANCH    OVRCD OF KWRD0300
.
          IF        PTCNPRCW = 1
            IF        WACTIVE = 1
              DISPLAY   *P1:24,*B,"This ward is not active. ";
              CALL      HITENTER
              GOTO      KWRD0200
            ENDIF
          ENDIF
.
          CALL      RDSP0000
          DISPLAY   *P14:12,*V2LON,SAWARD;
          GOTO      KWRD0600
.
KWRD0300  DISPLAY   *P1:24,*EL,*B,"Ward does not exist - ";
          CALL      HITENTER
          GOTO      KWRD0200
.
KWRD0400  CALL      RDSP0000
          GOTO      KWRD1000
.
KWRD0500  PACK      KEY6 WITH SAWARD,SP6
          CALL      RDWARD1
          BRANCH    OVRCD OF KWRD8000
.
          IF        PTCNPRCW = 1
            IF        WACTIVE = 1
              DISPLAY   *P1:24,*B,"This ward is not active. ";
              CALL      HITENTER
              GOTO      KWRD0100
            ENDIF
          ENDIF
.
KWRD0600  MOVE      WBDESC,SAVDESC
          DISPLAY   *P22:12,*EL,SAVDESC
          GOTO      KWRD2000
.
KWRD1000  DISPLAY   *P14:12,*EL,*V2LON,"ALL",*HOFF," wards";
          MOVE      SP6,KEY6
          MOVE      SP3,SAWARD
          CALL      RDSWARD1
.
KWRD1100  KEYIN     *P1:14,*EL,"Would you like each Ward ":
                               "on a separate Page (":
                               *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                               *V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
          RESET     ANS
          GOTO      KWRD1200 IF EOS
.
          MOVE      ZERO,PSKIP
          MATCH     ANSY,ANS
          GOTO      KWRD2000 IF EQUAL
.
          MATCH     ANSN,ANS
          GOTO      KWRD1200 IF EQUAL
          BEEP
          GOTO      KWRD1100
.
KWRD1200  MOVE      ONE,PSKIP 
          DISPLAY   *P53:14,*V2LON,ANSN
.
.
KWRD2000  CALL      CONTQST                          * Ok to continue ?
          BRANCH    CEXIT,KWRD3000,KWRD0000,KWRD9000
.                          1=Yes,    2=No,  3=Cancel
.
KWRD3000  DISPLAY   *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :"
          MATCH     SP3,SAWARD
          GOTO      KWRD5000 IF NOT EQUAL
.
KWRD4000  CALL      RDKWARD1
          BRANCH    OVRCD OF KWRD9500
.
          IF        PTCNPRCW = 1
            IF        WACTIVE = 1
              MATCH     SP3,WBED
              IF        @EQUAL
                PACK      KEY6,WWARD,Z30
                CALL      RDSWARD1
              ENDIF
              GOTO      KWRD4000
            ENDIF
          ENDIF
.
          IF         IBCNMHOS=1
            MATCH     SP70,HOSPID
            IF        !@EQUAL
              MATCH     PTWDHOSN,HOSPID          * Check hospital id 
              IF        !@EQUAL
                PACK      KEY6,WWARD,Z30         * Skip and move to next ward
                CALL      RDSWARD1
                GOTO      KWRD4000
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP3,SAWARD
          GOTO      KWRD5000 IF EQUAL
.
          MATCH     SAWARD,WWARD
          IF        !@EQUAL
            MOVE      SAWARD,WWARD
            GOTO      KWRD9500
          ENDIF
.
KWRD5000  MOVE      WBED,SAVBED
.
          MATCH     SP3,WBED
          GOTO      KWRD7000 IF NOT EQUAL
          MOVE      WBDESC,SAVDESC
.
          COMPARE   ONE,WINPUT
          GOTO      KWRD4000 IF NOT EQUAL
.
          PACK      KEY13 WITH WWARD,SP10
          CALL      RDSNOBE1
.
KWRD6000  CALL      RDKNOBE1
          BRANCH    OVRCD OF KWRD6200
.
          MATCH     WWARD,NBWARD
          GOTO      KWRD6200 IF NOT EQUAL
.
          MOVE      NBADMNO,WADMNO
          MOVE      NBPLUR,WPLUR
          CALL      GDAT0000
          GOTO      KWRD6000
.
KWRD6200  PACK      KEY14,WWARD,SP20
          CALL      RDSONLV1
KWRD6500  CALL      RDKONLV1
          BRANCH    OVRCD,KWRD4000
.
          MATCH     WWARD,OWARD
          GOTO      KWRD4000 IF NOT EQUAL
.
          CALL      VOLP0000                * check if right type of leave
          BRANCH    EXIT,KWRD6500           * not correct type
.
          MOVE      OADMNO,WADMNO
          MOVE      "LVE",SAVBED
          MOVE      SP20,WBDESC
          CALL      GDAT0000
          GOTO      KWRD6500
.
KWRD7000  CALL      GDAT0000
          MATCH     ZEROVISN,WSTBY
          GOTO      KWRD7100 IF EQUAL
.
          MOVE      WSTBY,WADMNO
          MOVE      "STB",SAVBED
          CALL      GDAT0000
.
KWRD7100  PACK      KEY14,WWARD,WBED,SP20
          CALL      RDSONLV1
KWRD7200  CALL      RDKONLV1
          BRANCH    OVRCD,KWRD4000
.
          MATCH     WWARD,OWARD
          GOTO      KWRD4000 IF NOT EQUAL
.
          MATCH     WBED,OBED
          GOTO      KWRD4000 IF NOT EQUAL
.
KWRD7400  MOVE      OADMNO,WADMNO
          CALL      VOLP0000                * check if right type of leave
          BRANCH    EXIT,KWRD7200           * not correct type
.
          MOVE      "LVE",SAVBED
          MOVE      SP20,WBDESC
          CALL      GDAT0000
          GOTO      KWRD7200
.
KWRD8000  DISPLAY   *P1:24,*EL,*B,"Ward does not exist - ";
          CALL      HITENTER
          GOTO      KWRD0100
.
KWRD9000  MOVE      TRUE,EXIT
          GOTO      KWRD9999
KWRD9500  MOVE      FALSE,EXIT
KWRD9999  RETURN
+
.**********************************************************************
.*                       RDSP0000                                     *
.*               Re-display screen after Wards scan                   *
.**********************************************************************
RDSP0000
          DISPLAY   *P1:3,*EF: 
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Patient Surname Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Ward/Bed Sequence":
                    *P1:8,"Select Option :",*V2LON,OPTION,*HOFF:
                    *P1:12,"Enter Ward :"
          RETURN
+
.**********************************************************************
.*                       GDAT0000                                     *
.*               Get Data ready for Printing                          *
.**********************************************************************
.
GDAT0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MATCH     WWARD,SWARD
          GOTO      GDAT2000 IF EQUAL
.
.         We must check if a ward per page
.
          BRANCH    PSKIP OF GDAT1000
          CALL      HEAD0000
          GOTO      GDAT2000
.
GDAT1000  CALL      HEAD1000
.
.         Zero admission number indicates bed is empty
.
GDAT2000  MATCH     ZEROVISN,WADMNO
          GOTO      GDAT3000 IF EQUAL
.
          CALL      PRT0000
          RETURN
.
GDAT3000  CALL      BED0000
          RETURN
+
.**********************************************************************
.*                       NDAT0000                                     *
.*                    No data available                               *
.**********************************************************************
.
NDAT0000  DISPLAY   *P1:24,*EL,*B,*V2LON,*P26:24:
                    "** No Available Data on File **",*W2;
          RETURN         
+
.**********************************************************************
.*                       END0000                                      *
.*                    End of Report                                   *
.**********************************************************************
.
END0000   COMPARE   ZERO,PAGENO
          GOTO      END4000 IF NOT EQUAL
          CALL      NDAT0000
          BRANCH    OPTION OF END1000,END2000
.
END1000   CALL      HEAD3000
          GOTO      END3000
END2000   CALL      HEAD0000
.
END3000   PRINT     *N,"  ***  End of Report  ***"
          BRANCH    OPTION OF END5000,END9999
.
END4000   CALL      PGE0000
          PRINT     *N,"  ***  End of Report  ***"
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W;
          BRANCH    OPTION OF END5000,END9999
.
END5000   CALL      KILL0000
END9999   RETURN    
+
.**********************************************************************
.*                       PRT0000                                      *
.*                    Print actual data                               *
.**********************************************************************
.
PRT0000   MOVE      WADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF PRT9999       * missing
.
PRT2000   COMPARE   FOUR,ASTAT
          GOTO      PRT2500 IF EQUAL
          COMPARE   TWO,ASTAT
          CALL      NCUR0000 IF NOT EQUAL
.
PRT2500   MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF NMST0000 
.
          MOVE      SP30,DNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
.
          MOVE      DGNAME,ANS
          PACK      DNAME WITH ANS,DOT,DSNAME
          MOVE      DSNAME,DALPHA
.
          MOVE      PGNAME,ANS
          PACK      PNAME WITH ANS,DOT,PSNAME
.
PRT3000   MOVE      SP9,DMSTAT
          MATCH     SP3,PMSTAT
          GOTO      PRT4000 IF EQUAL
.
          PACK      KEY5 WITH CODEM,PMSTAT
          CALL      RDCODE1
          BRANCH    OVRCD OF PRT4000
          MOVE      TDESC,DMSTAT
.
PRT4000   CALL      CALC0000
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DSEX
.* ******************************************   End of Changes         *C-49016
.
.          MOVE      DUNK,DSEX
.
PRT5000   PRINT     *1,"| ",SAVBED," | ",WBDESC," | ",PNAME:
                    *56,"| ",PSAGE," | ",DSEX,"| ",DMSTAT," | ",PURNO:
                        " | ",WADMNO," | ",PHDAYS," | ",DNAME,*132,"|"
.
          MOVE      WWARD,SWARD
          ADD       ONE,CLNO
          DISPLAY   *P27:24,*V2LON,WWARD,*P46:24,SAVBED
          GOTO      PRT9999
.
.         Print actual data via surname 
.
PRT6000   COMPARE   "55",CLNO
          CALL      HEAD3000 IF NOT LESS
.
          PRINT     *1,"| ",PSNAME,PGNAME,"| ",WRDBED:
                    " | ",PSAGE," | ",DSEX,"| ",DMSTAT:
                    " | ",PURNO," |",AADMNO," | ",PHDAYS," | ",DNAME,*132,"|"

          ADD       ONE,CLNO
.
PRT9999   RETURN
+
.**********************************************************************
.*                       BUMP0000                                     *
.*               Get rid of trailing blanks                           *
.**********************************************************************
.
BUMP0000  CMATCH    " ",PNAME
          RETURN    IF NOT EQUAL
          BUMP      PNAME BY -1
          RETURN    IF EOS
          GOTO      BUMP0000
+
.**********************************************************************
.*                       NMST0000                                     *
.*               No master record for admission                       *
.**********************************************************************
.
NMST0000  DISPLAY   *P1:21,*EL,*P17:21,*B,*V2LON:
                    "** No master record for Admission ",WADMNO," **",*W3;
          RETURN
+
.**********************************************************************
.*                       NCUR0000                                     *
.*               Admission is not current                             *
.**********************************************************************
.
NCUR0000  DISPLAY   *P1:21,*EL,*P19:21,*B,*V2LON:
                    "** ",WADMNO," is not a current admission **",*W3;
          RETURN
+
.**********************************************************************
.*                       BED0000                                      *
.*                 Empty bed printout                                 *
.**********************************************************************
.
BED0000   PRINT     *1,"| ",SAVBED," | ",WBDESC:
                    " | ***  EMPTY BED  ***":
                    *56,"|",*62,"|":
                    *71,"| ",*83,"|",*94,"|",*105,"|",*112,"|",*132,"|"
.
          MOVE      WWARD,SWARD
          ADD       ONE,CLNO
          DISPLAY   *P27:24,*V2LON,WWARD,*P46:24,SAVBED
          RETURN
+
.**********************************************************************
.*                       CALC0000                                     *
.*                 Calculate days in hospital                         *
.**********************************************************************
CALC0000
          MOVE      ZERO,PHDAYS             * initialize "days in hospital" var
.
          UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
.
          MOVE      XDAY,IDAY
          MOVE      XMON,IMON
          MOVE      XYEAR,IYEAR
          MOVE      XCENT,ICENT
.
.         Calculate admission date julian day
.
          MOVE      ZERO TO OVRCD
          MOVE      IDAY TO DD  * SET UP ADMISSION DATE
          MOVE      IMON TO MM  * AS INPUT 
          MOVE      IYEAR TO YY * TO 'DMYCON'
          MOVE      ICENT TO CC * TO 'DMYCON'
          CALL      DMYCON
.
          MOVE      JULDAY TO AJDAY   * SAVE ADMISSION JULIAN DAY
.
.         Calculate current date julian day
.
          MOVE      ZERO TO OVRCD
          MOVE      CDD TO DD  * SET UP CURRENT DATE
          MOVE      CMM TO MM  * AS INPUT 
          MOVE      CYY TO YY  * TO 'DMYCON'
          MOVE      CCC TO CC 
          CALL      DMYCON
.
          MOVE      JULDAY TO CJDAY   * SAVE TODAY'S JULIAN DAY
.
.         if admission date's century/year is not the same as current date 
.         calculate the difference in no. of years and then convert to no. of
.         days and add it to current date julian day
.
          PACK      A4YEAR,XCENT,XYEAR
          REP       " 0",A4YEAR
.
          PACK      C4YEAR,CCC,CYY
          REP       " 0",C4YEAR
.
          MATCH     A4YEAR,C4YEAR
          GOTO      CALC1000 IF EQUAL
          GOTO      CALC9999 IF LESS
.
          MOVE      A4YEAR,FORM4
          MOVE      C4YEAR,FORM4A
          SUB       FORM4,FORM4A
          MULT      "365",FORM4A
          ADD       FORM4A,CJDAY
.
.         COMPARE   AJDAY,CJDAY
.         GOTO      CALC1000 IF NOT LESS
.
.         Current day is less than admission day
.         therfore we must sub 365 form phdays
.         (ie add 365 days which then gets subtracted away)
.
.         ADD       "365",CJDAY
.
CALC1000  MOVE      CJDAY,PHDAYS
          SUB       AJDAY,PHDAYS
          ADD       ONE,PHDAYS         *** INCLUDE ADMISSION DAY AS WELL
.
CALC9999  RETURN
+
.*********************************************************************
.*                  VOLP0000                    Called by : KWRD0000 *
.*        Check that the patient is on Short term leave.    SUR0000  *
.*        Para's  : OADMNO        admission number                   *
.*        Returns : EXIT = 1      not a valid patient                *
.*********************************************************************
VOLP0000  COMPARE   ONE,PTCNTLVE
          GOTO      VOLP9000 IF NOT EQUAL   * not using type of leave
.
          PACK      KEY30,OADMNO,Z30
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,VOLP9100          * no tran record
.
          MATCH     OADMNO,TADMN
          GOTO      VOLP9100 IF NOT EQUAL   * no tran record
.
          MATCH     "L",TMOVE
          GOTO      VOLP9100 IF NOT EQUAL   * not a leave record
.
          MATCH     SP3,PTTRLTYP
          GOTO      VOLP9100 IF EQUAL       * no on-leave type
.
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1
          BRANCH    OVRCD,VOLP9100          * invalid type of leave
.
          MATCH     SP1,TCINDC1
          GOTO      VOLP9100 IF NOT EQUAL   * not short term leave
.
VOLP9000  MOVE      FALSE,EXIT
          GOTO      VOLP9999
.
VOLP9100  MOVE      TRUE,EXIT
.
VOLP9999  RETURN
+
.**********************************************************************
.*                       RTMP0000                                     *
.*                    Read Temporary file                             *
.**********************************************************************
.
RTMP0000  PACK      KEY36 WITH SP20,SP20
          DISPLAY   *P24:24,*EL,"Generating :";
          READ      PSTWRDP1,KEY36;;
.
RTMP1000  READKS    PSTWRDP1;PSNAME,PURNO,DUNIQUE,PTITL,PGNAME:
                             AADMNO,WRDBED,DSEX,DMSTAT,DNAME:
                             PSAGE,PHDAYS
          GOTO      RTMP9999 IF OVER
.
          DISPLAY   *P37:24,*V2LON,PSNAME;
          CALL      PRT6000
          GOTO      RTMP1000
.
RTMP9999  RETURN
+
.**********************************************************************
.*                       IND0000                                      *
.*                Create Temporary file                               *
.**********************************************************************
.
IND0000   MOVE      ZERO,STATUS
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEI
.
.         Check if previously exists..if so deleted it 
.
          CALL      KILL0000
.
          CLEAR     CMDLINE
          APPEND    "isbuild " TO CMDLINE
          APPEND    FNAMEI TO CMDLINE
          APPEND    " 118 u1-36" TO CMDLINE                            *C-49016
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          MOVE      ZERO,OPCND
          TRAP      IND2000 IF IO
          OPEN      PSTWRDP1,FNAMEI
          TRAPCLR   IO
IND1000   RETURN
.
IND2000   MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Temporary Index File ":
                    FNAMEI," Not Found **",*W4;
          TRAPCLR   IO
          RETURN
+
.**********************************************************************
.*                       KILL0000                                     *
.*                 Delete the Temporary file                          *
.**********************************************************************
.
KILL0000  CLOSE     PSTWRDP1
.
          MOVE      ZERO,STATUS
.
          DISPLAY   *P1:24,*EL,*P26:24,*V2LON:
                    "** Deleting Indexed File **";
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P1:12,*EF
          RETURN
+
.**********************************************************************
.*                       HEAD0000                                     *
.*               Print the headings for the reports                   *
.**********************************************************************
.
HEAD0000  ADD       ONE,PAGENO
.
          COMPARE   ONE,PAGENO
          CALL      PGE0000 IF NOT EQUAL
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          PRINT     *40,"(Ward/Bed Sequence)"
          MOVE      THREE,CLNO
.
          IF       IBCNMHOS =1
            PRINT    *40,"Hospital : ",HOSPID,"    ",HOSPNAME
            ADD      ONE,CLNO
          ENDIF
.
          GOTO      HEAD2000
.
HEAD1000  CALL      PGE0000
          PRINT     " "
          ADD       ONE,CLNO
.
HEAD2000  PRINT     *40,"WARD : ",WWARD,*51,SAVDESC,*N
          ADD       TWO,CLNO
.
          CALL      PGE0000
.
          PRINT     *1,"| Bed | Bed Description":
                    *30,"| Patient Name":
                    *56,"| Age |  Sex",*72,"| Marital St|  U/R No.":
                    *95,"|  Adm No. | Days | Attending Doctor",*132,"|"
.
          ADD       ONE,CLNO
          CALL      PGE0000
          MOVE      WWARD,SWARD
          RETURN
.
.         Headings for output
.
HEAD3000  ADD       ONE,PAGENO
.
          COMPARE   ONE,PAGENO
          CALL      PGE0000 IF NOT EQUAL
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          PRINT     *40,"(Surname Sequence)"
          MOVE      FOUR,CLNO
.
          IF       IBCNMHOS =1
            PRINT    *40,"Hospital : ",HOSPID,"    ",HOSPNAME
            ADD      ONE,CLNO
          ENDIF
.
          CALL      PGE0000
.
          PRINT     *1,"| Patient Name":
                    *48,"|Ward/Bed | Age |  Sex":
                    *74,"| Marital St|  U/R No.":
                    *96,"| Adm No. | Days | Nationality",*132,"|"
          ADD       ONE,CLNO
.
          CALL      PGE0000
          RETURN
+
.**********************************************************************
.*                       PGE0000                                      *
.*                   Print the page end                               *
.**********************************************************************
.
PGE0000   PRINT     *1,"*-----------------------------------------":
                       "-------------------------------------------":
                       "----------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.         ======================================================================
.         KHOS0000: Keyin hospital ID
.         ======================================================================
KHOS0000  MOVE       ZERO,EXIT
          MOVE       SP10,HOSPID
.
          COMPARE    ONE,IBCNMHOS                * Exit if not multi hospital
          GOTO       KHOS9999 IF NOT EQUAL
.
          DISPLAY    *P1:10,*EL,"Hospital Id : ";
          MOVE       TEN5,ECOL
          MOVE       "10",EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ZERO,CKYIMAND           * Not Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOS1000,KHOS2000
          MOVE       PTHSHOSP,HOSPID
          MOVE       PTHSNAME,HOSPNAME
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS1000  MOVE       "All Hospitals",HOSPNAME
          MOVE       SP10,PTHSHOSP
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS2000  MOVE       ONE,EXIT
KHOS9999  RETURN
+
.**********************************************************************
.*             I/O INCLUDES FOR FILES AND GENERAL ROUTINES            *
.**********************************************************************
.
          INC       STD001IO
          INC       TFILENAM                     * Generate Temp File Name
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       SEXDSCIO
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATHSPIO/INC
          INC       PATHSPKY
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATNOBIO/INC
          INC       PATONLIO/INC
          INC       PATTRNIO/INC                 * Transfer file          
          INC       PATWR1IO/INC
          INC       PATWRDDS
          INC       WEBERRIO/INC                 * Web Server Error Logging
...............................................................................

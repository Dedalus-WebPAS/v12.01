.******************************************************************************
.*              P R O G R A M  S U M M A R Y                                  *
.*              -------------  -------------                                  *
.*                                                                            *
.*     PROGRAM NAME:    IBAEMR07                                              *
.*                                                                            *
.*     FUNCTION:        ED STATUS REPORT                                      *
.*                                                                            *
.*     DATE WRITTEN:    17/06/09                                              *
.*                                                                            *
.*     AUTHOR:          WeeLee Koo (I.B.A)                                    *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*        V12.00.02 22/09/2025  J.Tan          TSK 0966921                    *
.*                  Mod to remove tempfiles at end of process                 *
.*        V12.00.01 18/07/2025  Ebon Clements   TSK 0961623                   *
.*                  Call OPICD1 from INTI0000                                 *
.*        V10.06.01 02/04/2015  Ania P          CAR 261630                    *
.*                  Removed use of temp file name                             *
.*        V10.05.02 11/08/2014  Don Nguyen      CAR 297774                    *
.*                  Recompiled for changes to PMSBRQFD/IO                     *
.*        V10.05.01 06/08/2014  Don Nguyen      CAR 297703                    *
.*                  Modified GRNT0000; added Authorisation Doctor, Nurse,     *
.*                  and Co-ordinator.                                         *
.*        V10.00.03 08/04/2010 Ebon Clements    CAR 218773                    *
.*                  Fixed Unk patient name, diagnosis, ready to depart,       *
.*                  time bed allocated and accumm time printing               *
.*        V10.00.02 31/03/2010 Ebon Clements    CAR 218773                    *
.*                  Don't print patients on non current bypass reports        *
.*        V10.00.01 26/03/2010 Ebon Clements    CAR 218773                    *
.*                  Changed comments to print on seperate lines               *
.*        V9.12.02  27/10/2009 WEELEE KOO       CAR 197930                    *
.*                  Edited Reason for Comments Display                        *
.*        V9.12.01  17/06/2009 WEELEE KOO       CAR 197930                    *
.*                  Created new report for ED Status                          *
.******************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. --------------------------
.
          INC       EMRCONFD/INC
          INC       EMRVISFD/INC
          INC       EMRBPSFD/INC          * By-Pass/Pre By-Pass File
          INC       EMRICDFD/INC
          INC       EMRUNKFD/INC          * Unknown patents
          INC       EMRVCDFD/INC
          INC       PATMA1FD/INC          * patient master file 
.          
          INC       PATDO1FD/INC          * patient doctor file 
          INC       PATICDFD/INC          
          INC       PATCODFD/INC          * patcodes
          INC       PATCONFD/INC          * inpatient control file
          INC       PMSBRQFD/INC          * patient bed request
          INC       PMSHCPFD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
.  TEMPORARY FILE DEFINITION
. --------------------------
.
TEMP1     IFILE     SQL,FIXED=32, KEY="U1-3,4-23,24-31"

.Name     Type      Length    Start    Description
.----     ----      ------    -----    -----------
TRIGCODE  DIM       3           1      Triage Code
PATTNAME  DIM       20          4      Patient Name
VISTNUMB  DIM       8           24     Visit Number
.End of Record                  32
.
.
. LOCAL VARIABLE DEFINITION
. --------------------------.
.
AARRTIME  FORM      4
CATEL     INIT      "el"
CODEDESC  DIM       40
CATQA     INIT      "qa"
CMDLINE   DIM       80
FNAMEA    DIM       8
ISERASE   INIT      "iserase "
DOWNCASE  INIT      "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz"
.
REPTITLE  DIM       70       * Report Title
ADMRDISP  DIM       7
BPASSKEY  DIM       20       * Key to selected bypass record
CPASSKEY  DIM       20       * Key to current active bypass record if any
DESCOPT   DIM       23       * option description in heading
DISDIAG   DIM       20
DIM20     DIM       20       * patient name to print
DIM22     DIM       22       * doctor name to print
DIM5      DIM       5        * format time (EMVITIME-arrival time) 
DISPNAME  DIM       60
NAME35    DIM       35
FIRSTDAT  DIM       8
LASTDATE  DIM       8
FIRSTIME  DIM       4
LASTTIME  DIM       4
CALCTIME  FORM      6
HOUR      DIM       2
MIN       DIM       2
LASTHOUR  DIM       2
LASTMIN   DIM       2
HOUR1     FORM      6
HOUR2     FORM      6
MINUTE1   FORM      3
MINUTE2   FORM      3
FIRSTHR   DIM       2
FIRSTMIN  DIM       2
CDYSDAYS  FORM      6
CDYSYEAR  FORM      2
CDYSTDTE  DIM       8
CDYSFDTE  DIM       8
WADTIME   DIM       5        * format time (Ward Allocation Time)
ONDATE    DIM       12    
ONTIME    DIM       5
OFDATE    DIM       12    
OFTIME    DIM       5
NOTCURRF  FORM      1        * 1=yes , 0=No (Used for print patients test) 
RSONDESC  DIM       20
NOAMB     DIM       3
ONTITLE   DIM       33
OFFTITLE  DIM       33
REASON    DIM       33
COMTITLE  DIM       33
.
CHOICE    FORM      1
TOTPAT    FORM      3        * Total number of patients
TRIONE    FORM      3        * Total patients in Triage 1 
TRITWO    FORM      3        * Total patients in Triage 2 
TRITHREE  FORM      3        * Total patients in Triage 3 
TRIFOUR   FORM      3        * Total patients in Triage 4 
TRIFIVE   FORM      3        * Total patients in Triage 5 
TRIBLANK  FORM      3        * Total patients with no triage cat
WAITTIME  FORM      4
.
. CONSTANT DEFINITION
. -------------------
.
PRGID     INIT      "IBAEMR07"
PRGNAM    INIT      "ED Status Report"
VERSION   INIT      "V12.01.00"
.
.*****************************************************************************
.*                              MAIN0000                                     *
.*                       Controlling Logic (Mainline code)                   *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN1000  CALL      INPT0000               * get user input
          BRANCH    EXIT,MAIN9999
.
MAIN2000  CALL      SELC0000               * Current or Selected By Pass Report?
          CALL      SELB0000               * Keyin Selected By Pass Record Key
          BRANCH    EXIT,MAIN1000
.
          CALL      COFM0000               * ok to continue?
          BRANCH    EXIT,MAIN1000
.
MAIN3000  CALL      RRUN0000               * Run report 
          BRANCH    EXIT,MAIN9999
.
          CALL      GRNT0000               * print grand total
.
MAIN9999  CHAIN     PGM
+
.
.*****************************************************************************
.*                       Initialization                                      *
.*                                                                           *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"emrvisaf";
          OPEN      EMRVISA1,"emrvisaf"
          OPEN      EMRVISA2,"emrvisaf"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"emrunkaf";
          OPEN      EMRUNKA1,"emrunkaf"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"emrvcdaf";
          OPEN      EMRVCDA1,"emrvcdaf"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"emricdaf";
          OPEN      EMRICDA1,"emricdaf"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"paticddf";
          OPEN      PATICDD1,"paticddf"
.
          CALL      OPICD1
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"

          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmsbrqaf";
          OPEN      PMSBRQA2,"pmsbrqaf"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND12;*106,PTCNBEDM
          READ      CONTROLF,HUND12;*106,PTCNBEDM
          READ      CONTROLF,EIGHTY9;*224,EMCNDPRS
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"emrbpsaf";
          OPEN      EMRBPSA1,"emrbpsaf"
          OPEN      EMRBPSA2,"emrbpsaf"
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEA
.
INIT9999  RETURN
.
.*****************************************************************************
.*                      Get User Option                                      *
.*                                                                           *
.*****************************************************************************
.
INPT0000  MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
          MOVE      ZERO,TOTPAT
          MOVE      ZERO,TRIONE
          MOVE      ZERO,TRITWO
          MOVE      ZERO,TRITHREE
          MOVE      ZERO,TRIFOUR
          MOVE      ZERO,TRIFIVE
          MOVE      ZERO,TRIBLANK
          MOVE      ZERO,EXIT
.
          HLINE     *G37,2,50,80
          DISPLAY   *P39:4,*EF:
                    *P1:5,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:6,*V2LON,ONE,*HOFF," = Run Report"
.
INPT0500  KEYIN     *P1:9,"Select Option : ",*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      INPT9000 IF EQUAL
.
          BRANCH    OPTION,INPT1000
.
          BEEP
          GOTO      INPT0500
.
INPT1000  MOVE      ZERO,EXIT
          GOTO      INPT9999
.
INPT9000  MOVE      ONE,EXIT
.
INPT9999  RETURN
.
.*****************************************************************************
.*  Current or Selected (Pre) By Pass Report?                                *
.*                                                                           *
.*****************************************************************************
.
SELC0000  DISPLAY   *P1:12,*V2LON,ZERO,*HOFF," = Print Current (Pre) By-Pass":
                                             " Report":
                    *P1:13,*V2LON,ONE,*HOFF," = Print Selected(Pre) By-Pass":
                                             " Report"
.
SELC1000  KEYIN     *P1:16,"Report Type : ",*V2LON,CHOICE
.
          COMPARE   ZERO,CHOICE
          GOTO      SELC9999 IF EQUAL
.
          COMPARE   ONE,CHOICE
          GOTO      SELC9999 IF EQUAL
.
          BEEP
          GOTO      SELC1000
.
SELC9999  RETURN
.
.
.*****************************************************************************
.*                     Pre By Pass/By Pass Report?                           *
.*                                                                           *
.*****************************************************************************
.
SELB0000  MOVE      SP70,BPASSKEY
          COMPARE   ZERO,CHOICE
          GOTO      SELB9999 IF EQUAL
.
          KEYIN     *P1:17,"(Pre) By-Pass Record Key : ",*V2LON,BPASSKEY
.
          PACK      KEY20,BPASSKEY,SP70
          CALL      RDEMBPS1
          BRANCH    OVRCD,SELB9000
.
          MOVE      ZERO,EXIT
          GOTO      SELB9999
.
SELB9000  MOVE      ONE,EXIT
          GOTO      SELB9999
.
SELB9999  RETURN
.
.*****************************************************************************
.*                       Get User Confirmation                               *
.*                                                                           *
.*****************************************************************************
.
COFM0000  KEYIN     *P1:23,*EL,"Ok to Continue (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS;
. 
          REP       "nN",ANS 
          CMATCH    ANSN,ANS
          GOTO      COFM2000 IF EQUAL
.
          REP       "yY",ANS
          CMATCH    ANSY,ANS
          GOTO      COFM1000 IF EQUAL
.
          BEEP
          GOTO      COFM0000
.
COFM1000  MOVE      ZERO,EXIT
          GOTO      COFM9999
.
COFM2000  MOVE      ONE,EXIT
.
COFM9999  RETURN
.
.******************************************************************************
.*                                  CRTF0000              Called by: RRUN0000 *
.*                  Open and Create the Tempfile                              *
.******************************************************************************
.
CRTF0000  CALL      RMTM0000        * remove tempfile
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          APPEND    " 32 u1-3,4-23,24-31",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMP1,FNAMEA
.
CRTF9999  RETURN
+
.******************************************************************************
.         Remove tempfile
.******************************************************************************
RMTM0000  CLOSE     TEMP1,DELETE
          CLEAR     CMDLINE
          PACK      CMDLINE,ISERASE,FNAMEA
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
RMTM9999  RETURN
+
.******************************************************************************
.         Clear all records
.******************************************************************************
.
CLER0000  MOVE      SP70,KEY31
          CALL      RSTEMP1
          CALL      RKTEMP1
          IF        OVRCD=0
            PACK      KEY31,TRIGCODE,PATTNAME,VISTNUMB,SP70
            CALL      DETEMP1
            GOTO      CLER0000
          ENDIF
          GOTO      CLER9999
.
CLER9999  RETURN
.
.******************************************************************************
.         Write/Update the tempfile
.******************************************************************************
UFIL0000  PACK      KEY31,EMVITRIG,DIM20,EMVIADMN,SP70
          CALL      RDTEMP1
          IF        OVRCD=1
            MOVE      EMVITRIG,TRIGCODE
            MOVE      DIM20,PATTNAME
            MOVE      EMVIADMN,VISTNUMB
          ENDIF

          IF        OVRCD=1
            CALL      WRTEMP1
          ELSE
            CALL      UPTEMP1
          ENDIF
.
UFIL9999  RETURN
.
.*****************************************************************************
.*           Get Records where Status = Current Patient (1)                  *
.*           Only print patients for currently active bypass records         *
.*****************************************************************************
.
RRUN0000  CALL      CRTF0000        * Create and open Tempfile
          CALL      CLER0000        * Clear all records of tempfiles
          CALL      IBACLOCK
.
          CALL      BPAS0000        * Read the current (Pre) By pass record
          BRANCH    EXIT,RRUN9000
.
          COMPARE   ZERO,CHOICE     * Print Current (Pre) By-Pass
          GOTO      RRUN0500 IF EQUAL
.         
          BRANCH    NOTCURRF,RRUN9000       * Print patients if current by pass 
.
RRUN0500  PACK      KEY9,ONE,SP20
          CALL      RSEMVIS2
RRUN1000  CALL      RKEMVIS2
          BRANCH    OVRCD,RRUN2000
.
          COMPARE   ONE,EMVISTAT
          GOTO      RRUN2000 IF NOT EQUAL
.
          CALL      PTNM0000        * get patient name
.
          CALL      UFIL0000        * Update Tempfile
          GOTO      RRUN1000
.
RRUN2000  PACK      KEY31,SP70
          CALL      RSTEMP1
RRUN3000  CALL      RKTEMP1           
          BRANCH    OVRCD,RRUN9000

          PACK      KEY8,VISTNUMB,SP20
          CALL      RDEMVIS1
.
          CALL      DCNM0000        * get Doctor Name
.
          CALL      ADMR0000        * get Admission Status
.
          CALL      PRNT0000        * Print Record
.
          GOTO      RRUN3000
.
RRUN9000  CALL      RMTM0000        * remove tempfile
.
RRUN9999  RETURN
.
.-------------------------------------------------------------
. Get Admission Status based on Ward Bed Requests
.-------------------------------------------------------------
.
ADMR0000  MOVE      SP70,ADMRDISP
.
          MATCH     SP70,EMVIUC28
          IF        !@EQUAL
            PACK      ADMRDISP,ANSB,EMVIUC28,SP70 * Ready for admission display
          ENDIF
.
          MATCH     "1",PTCNBEDM
          GOTO      ADMR9999 IF NOT EQUAL
.
ADMR0500  PACK      KEY25,VISTNUMB,SP70
          CALL      RSPMBRQ2
ADMR1000  CALL      RKPMBRQ2
          BRANCH    OVRCD,ADMR9999
.
          MATCH     VISTNUMB,PMBRVISN
          GOTO      ADMR9999 IF NOT EQUAL
.
          MATCH     " ",PMBRRSTA
          GOTO      ADMR2000 IF EQUAL
.
          MATCH     "1",PMBRRSTA
          GOTO      ADMR2000 IF EQUAL
.
          MATCH     "2",PMBRRSTA
          GOTO      ADMR3000 IF EQUAL
.
          GOTO      ADMR1000
.
ADMR2000  PACK      ADMRDISP,ANSR,ANSE,ANSQ,SP70
          GOTO      ADMR9999
.
ADMR3000  PACK      ADMRDISP,PMBREWRD,SLASH,PMBREBED,SP70
          GOTO      ADMR9999
.
ADMR9999  RETURN
.
.*****************************************************************************
.*                       Print Record Lines                                  *
.*                                                                           *
.*****************************************************************************
.
PRNT0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      EMVITIME,DIM5
.
          CALL      BEDR0000       * Get bed request ward allocation date/time
.
          MOVE      SP70,DISDIAG
          MATCH     SP70,EMVIUC20   * Get presenting complaint
          IF        !@EQUAL
            PACK      KEY5,CATEL,EMVIUC20,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      EMVIUC20,TDESC
            ENDIF
            MOVE      TDESC,DISDIAG
          ENDIF
.
          MATCH     "1",EMCNDPRS       * Display diagnosis over presenting comp.
          IF        @EQUAL
            CALL     DIAG0000
          ENDIF
.
          MOVE      SP70,AARRTIME
          CALL      GETWAT00                *   Routine to get time diff
          MOVE      WAITTIME,AARRTIME
.
          PRINT     *1,"|",EMVITRIG,*5,"|",PATTNAME:
                    *26,"|",EMVIURNO,*35,"| ",DIM5:
                    *43,"|",DIM22,*70,"|",DISDIAG:
                    *91,"|",EMVILOCN,*97,"|",ADMRDISP:
                    *106,"|",EMVIUT01,*117,"|",WADTIME:
                    *125,"|",AARRTIME,*132,"|"
          ADD       ONE,CLNO
          ADD       ONE,TOTPAT
.
          MOVE      SP70,TCINDC1
          MATCH     SP70,EMVITRIG
          IF        !@EQUAL
            PACK      KEY5,ANSA,ANSA,EMVITRIG,SP70
            CALL      RDCODE1
          ENDIF
.
          MATCH     "1",TCINDC1  
          GOTO      PRNT9100 IF EQUAL
.
          MATCH     "2",TCINDC1  
          GOTO      PRNT9200 IF EQUAL
.
          MATCH     "3",TCINDC1  
          GOTO      PRNT9300 IF EQUAL
.
          MATCH     "4",TCINDC1  
          GOTO      PRNT9400 IF EQUAL
.
          MATCH     "5",TCINDC1  
          GOTO      PRNT9500 IF EQUAL
.
          GOTO      PRNT9600
. 
PRNT9100  ADD       ONE,TRIONE
          GOTO      PRNT9999
.    
PRNT9200  ADD       ONE,TRITWO
          GOTO      PRNT9999
.
PRNT9300  ADD       ONE,TRITHREE
          GOTO      PRNT9999
.
PRNT9400  ADD       ONE,TRIFOUR
          GOTO      PRNT9999
.
PRNT9500  ADD       ONE,TRIFIVE
          GOTO      PRNT9999
.
PRNT9600  ADD       ONE,TRIBLANK
          GOTO      PRNT9999
.
PRNT9999  RETURN
.
.*****************************************************************************
.*  Read the seleted bypass record (CHOICE=1) or           
.*  Get Last Record bypass record and see if it's active(No off date/time)
.*  If not report that the ED department is not currently on by pass (CHOICE=0) 
.*****************************************************************************
BPAS0000  MOVE      SP70,CPASSKEY                * Clear current bypass key
          MOVE      ZERO,NOTCURRF                * Clear current flag
.
          COMPARE   ZERO,CHOICE
          GOTO      BPAS4000 IF EQUAL
.
          MOVE      ONE,NOTCURRF                
.
          PACK      KEY20,Z70
          CALL      RSEMBPS2                     * Get current bypass record 
          CALL      RPEMBPS2                     * if any and save key
          BRANCH    OVRCD,BPAS1000
.
          MATCH     SP70,EMBPOFDT                * Off (pre) by pass date
          GOTO      BPAS1000 IF NOT EQUAL
.
          MATCH     SP70,EMBPOFTM                * Off (pre) by pass time
          GOTO      BPAS1000 IF NOT EQUAL
.
          PACK      CPASSKEY,EMBPSTCD,EMBPPTYP,EMBPONDT,EMBPONTM,SP70
          MATCH     CPASSKEY,BPASSKEY
          IF        @EQUAL
            MOVE      ZERO,NOTCURRF         * Yes current by pass record
          ENDIF                             * so print patients
.
BPAS1000  PACK      KEY20,BPASSKEY,SP70
          CALL      RDEMBPS1                * Re Read selected by pass record
          BRANCH    OVRCD,BPAS9100
.
          GOTO      BPAS5000
.
BPAS4000  PACK      KEY20,Z70
          CALL      RSEMBPS2
          CALL      RPEMBPS2
          BRANCH    OVRCD,BPAS9000
.
          MATCH     SP70,EMBPOFDT                * Off (pre) by pass date
          GOTO      BPAS9000 IF NOT EQUAL
.
          MATCH     SP70,EMBPOFTM                * Off (pre) by pass time
          GOTO      BPAS9000 IF NOT EQUAL
.
BPAS5000  CLEAR     REPTITLE
          MATCH     "0",EMBPPTYP                 * Valid bypass record found
          IF        @EQUAL
            IF        NOTCURRF=1
              APPEND    "Non Current Pre By-Pass ED Status ",REPTITLE
            ELSE
              APPEND    "Pre By-Pass ED Status ",REPTITLE
              APPEND    "as at ",REPTITLE
              APPEND    CTIMEIS,REPTITLE
            ENDIF
          ELSE
            IF        NOTCURRF=1
              APPEND    "Non Current By-Pass ED Status ",REPTITLE
            ELSE
              APPEND    "By-Pass ED Status ",REPTITLE
              APPEND    "as at ",REPTITLE
              APPEND    CTIMEIS,REPTITLE
            ENDIF
          ENDIF
          RESET     REPTITLE
.
          UNPACK    EMBPONDT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE      
          MOVE      CPCDATE,ONDATE
.
          MOVE      EMBPONTM,ONTIME
.
          UNPACK    EMBPOFDT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE      
          MOVE      CPCDATE,OFDATE
.
          MOVE      EMBPOFTM,OFTIME
.
          MOVE      SP70,RSONDESC
          MATCH     SP70,EMBPRSON
          IF        !@EQUAL
            PACK      KEY5,CATQA,EMBPRSON,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TDESC,RSONDESC
            ENDIF
          ENDIF
.
          MOVE      EMBPNAMB,NOAMB
          MOVE      ZERO,EXIT
          GOTO      BPAS9999
.
BPAS9000  MOVE      "No Active Pre By-Pass or By-Pass Details",REPTITLE
          CALL      HEAD0000
.
          MOVE      ONE,EXIT
          GOTO      BPAS9999
.
BPAS9100  MOVE      "Invalid Bypass Record Details",REPTITLE
          CALL      HEAD0000
.
          MOVE      ONE,EXIT
          GOTO      BPAS9999
.
BPAS9999  RETURN
.
.*****************************************************************************
.*                       Print Grand Totals                                  *
.*                                                                           *
.*****************************************************************************
.
GRNT0000  IF        CPAGENO=0
             CALL     HEAD0000
          ENDIF
.
          MATCH     "0",EMBPPTYP        (0=pre by-pass,1=by-pass)
          IF        @EQUAL
            MOVE    "On Pre By-Pass Date/Time        : ",ONTITLE
            MOVE    "Off Pre By-Pass Date/Time       : ",OFFTITLE
            MOVE    "Reason for Pre By-Pass Code     : ",REASON
            MOVE    "Reason for Pre By-Pass Comments : ",COMTITLE
          ELSE 
            MOVE    "On By-Pass Date/Time            : ",ONTITLE
            MOVE    "Off By-Pass Date/Time           : ",OFFTITLE
            MOVE    "Reason for By-Pass Code         : ",REASON
            MOVE    "Reason for By-Pass Comments     : ",COMTITLE
          ENDIF
.
          IF        NOTCURRF<>1
            CALL      PDOT0000
            PRINT     *N,"Number of Triage 1 Patients     : ",TRIONE:
                      *N,"Number of Triage 2 Patients     : ",TRITWO:
                      *N,"Number of Triage 3 Patients     : ",TRITHREE:
                      *N,"Number of Triage 4 Patients     : ",TRIFOUR:
                      *N,"Number of Triage 5 Patients     : ",TRIFIVE:
                      *N,"Number of Non Triaged Patients  : ",TRIBLANK:
                      *N,"Total Number of Patients        : ",TOTPAT
          ENDIF
.
          PRINT     *N,ONTITLE,SP2,ONDATE,ONTIME:
                    *N,OFFTITLE,SP2,OFDATE,OFTIME:
                    *N,REASON,SP2,RSONDESC:
                    *N,"Number of Ambulances            : ",SP1,NOAMB:
                    *N,COMTITLE
.
          PRINT      EMBPFFL1
.
          MATCH     SP70,EMBPFFL2
          IF        !@EQUAL
            PRINT     EMBPFFL2
          ENDIF
.
          MATCH     SP70,EMBPFFL3
          IF        !@EQUAL
            PRINT     EMBPFFL3
          ENDIF
.
          MATCH     SP70,EMBPFFL4
          IF        !@EQUAL
            PRINT     EMBPFFL4
          ENDIF
.
          MATCH     SP70,EMBPFFL5
          IF        !@EQUAL
            PRINT     EMBPFFL5
          ENDIF
.
          PACK      KEY10,EMBPAHCP,SP30
          CALL      RDPMHCP1
          BRANCH    OVRCD,GRNT1000
.
          CLEAR     DISPNAME
          PACK      NAME35,PMHCTITL
          CALL      NAMSTR00
          PACK      NAME35,PMHCGNAM
          CALL      NAMSTR00
          PACK      NAME35,PMHCSNAM
          CALL      NAMSTR00
          RESET     DISPNAME
.
GRNT1000  PRINT     *N,"Authorised By":
                    *N,"Doctor                          : ",DISPNAME:
                    *N,"Nurse                           : ",EMBPFFNA:
                    *N,"Co-ordinator                    : ",EMBPFFCA
                    
.
          PRINT     *N,*N,"** END OF REPORT **"
          DISPLAY   *P1:24,*EL:
                    *P40:24,*V2LON,"*** Generation Completed ***",*W2;
.
GRNT9999  RETURN
.
.*****************************************************************************
.*                       Display Heading                                     *
.*                                                                           *
.*****************************************************************************
.
HEAD0000  COMPARE   ZERO,CPAGENO
          CALL      PDOT0000 IF NOT EQUAL
.
          MOVE      ONE,CNOUNDLN
          MOVE      DESCOPT,CPHDROPT
          CALL      HEAD132
.
          PRINT     *35,REPTITLE
.
          IF        NOTCURRF<>1
            CALL      PDOT0000
            PRINT     *1,"|Tri",*5,"|Patient Name":
                      *26,"|U/R",*35,"|Arrival":
                      *43,"|ED Doctor ",*70,"|Diagnosis":
                      *91,"|LOC ",*97,"|Adm.":
                      *106,"|ED Ready",*117,"|TimeBed":
                      *125,"|Accum",*132,"|":
                      *N,"|Cat",*5,"|":
                      *26,"|Number",*35,"|Time":
                      *43,"|",*70,"|Presenting Comp.":
                      *91,"|  ",*97,"|Status":
                      *106,"|Discharge",*117,"|Alloc.":
                      *125,"|Time",*132,"|"
            CALL      PDOT0000
            MOVE      EIGHT,CLNO
          ELSE
            CALL      PDOT0000
            MOVE      FOUR,CLNO
          ENDIF
.
HEAD9999  RETURN
.
.*****************************************************************************
.*                       Print dotted lines                                  *
.*                                                                           *
.*****************************************************************************
.
PDOT0000  PRINT     *1,"*--------------------------------------------":
                    "--------------------------------------------":
                    "------------------------------------------*"
          ADD       ONE,CLNO
.
PDOT9999  RETURN
.
.*****************************************************************************
.*                       Get doctor name in a dim22                          *
.*                                                                           *
.*****************************************************************************
.
DCNM0000  MOVE        SP30,DIM22
          PACK        KEY6,EMVIDOCT,SP10
          CALL        RDDOCT1
          BRANCH      OVRCD,DCNM9999
.
          SQUEEZE     DSNAME
          SQUEEZE     DGNAME
          MOVE        DGNAME,ANS
 
          PACK        DIM22,DSNAME,COMMA,SP1,ANS
.
DCNM9999  RETURN
.
.*****************************************************************************
.*                       Get PATIENT name in a dim20                         *
.*                                                                           *
.*****************************************************************************
.
PTNM0000  PACK        DIM20,SP20,SP20
          PACK        KEY8,EMVIURNO,SP70
          CALL        RDMAST1
          IF          OVRCD=1
            PACK      KEY8,EMVIADMN,SP70
            CALL      RDEMUNK1
            IF        OVRCD=1
              MOVE      SP70,PURNO
              MOVE      SP70,PSNAME
              MOVE      SP70,PGNAME
              MOVE      SP70,PBDATE
              MOVE      SP70,PSEX
              MOVE      SP70,PTITL
            ELSE
              MOVE      "       0",PURNO
              MOVE      EMUNDET2,PGNAME
              MOVE      EMUNDET1,PSNAME
              MOVE      EMUNBDAT,PBDATE
              MOVE      EMUNSEX,PSEX
              MOVE      SP70,PTITL
            ENDIF
          ENDIF
.
          SQUEEZE     PSNAME
          SQUEEZE     PGNAME
.
          PACK        DIM20,PSNAME,COMMA,SP1,PGNAME
.
PTNM9999  RETURN
.
.------------------------------------------------------------
. Get the time in waiting
.------------------------------------------------------------
GETWAT00  MOVE      EMVIDATE,FIRSTDAT
          UNPACK    EMVITIME,HOUR,D1,MIN
          PACK      FIRSTIME,HOUR,MIN
          REP       " 0",FIRSTDAT
          REP       " 0",FIRSTIME
.
          PACK      LASTDATE,CCC,CYY,CMM,CDD
          REP       " 0",LASTDATE
          UNPACK    CTIMEIS,HOUR,D1,MIN
          PACK      LASTTIME,HOUR,MIN
          REP       " 0",LASTTIME
          CALL      TIMEDIFF
          MOVE      CALCTIME,WAITTIME
.
GETWAT99  RETURN
.
.------------------------------------------------------------
. If display diagnosis over presenting complaint is set to yes
. get the primary diagnosis to this visit
.------------------------------------------------------------
DIAG0000  MATCH     "1",EMCNDPRS
          GOTO      DIAG9999 IF NOT EQUAL
.
          PACK      KEY14,EMVIADMN,SP70
          CALL      RSEMVCD1
DIAG1000  CALL      RKEMVCD1
          BRANCH    OVRCD,DIAG9999
.
          MATCH     DEMVIADM,EMVCVIST
          GOTO      DIAG9999 IF NOT EQUAL
.
          COMPARE   EMVCCSYS,ONE
          GOTO      DIAG2000 IF EQUAL
.
          COMPARE   EMVCCSYS,FIVE
          GOTO      DIAG1000 IF NOT EQUAL
.
DIAG2000  MATCH     "000",EMVCSEQU
          GOTO      DIAG1000 IF NOT EQUAL
.
          CALL      LOKCOD00
.
          MATCH     SP70,CODEDESC
          GOTO      DIAG9999 IF EQUAL
.
          CLEAR     DISDIAG
          APPEND    CODEDESC,DISDIAG
          RESET     DISDIAG
          GOTO      DIAG9999
.
DIAG9999  RETURN
.
.------------------------------------------------------------
.  Lookup Code to get description
.------------------------------------------------------------
LOKCOD00  PACK      CODEDESC,SP70
          BRANCH    EMVCCSYS,LOKCOD10,LOKCOD99,LOKCOD99,LOKCOD99,LOKCOD50
.
.   User Defined
.
          GOTO      LOKCOD99
.
LOKCOD10  PACK      KEY9,EMVCMNCD,SP70
          CALL      RDICD1
          IF        OVRCD=1
            MOVE      "Invalid Diagnosis Code ",CODEDESC
            GOTO      LOKCOD99
          ENDIF
.
          MOVE      DDESC,CODEDESC
          GOTO      LOKCOD99
.
LOKCOD50  PACK      KEY9,EMVCMNCD,SP70
          CALL      RDEMICD1
          IF        OVRCD=1
            MOVE      "Invalid Emergency Diagnosis Code",CODEDESC
            GOTO      LOKCOD99
          ENDIF
.
          MOVE      EMICDESC,CODEDESC
          GOTO      LOKCOD99
.
LOKCOD99  RETURN
.
.------------------------------------------------------------
. Check if using ward bed requests and get the ward allocation 
. date and time
.------------------------------------------------------------
BEDR0000  MOVE      EMVIUT04,WADTIME
.  
          MATCH     "1",PTCNBEDM
          GOTO      BEDR9999 IF NOT EQUAL
.
BEDR0500  PACK      KEY25,VISTNUMB,SP70
          CALL      RSPMBRQ2
BEDR1000  CALL      RKPMBRQ2
          BRANCH    OVRCD,BEDR9999
.         
          MATCH     VISTNUMB,PMBRVISN
          GOTO      BEDR9999 IF NOT EQUAL
.
          MATCH     "2",PMBRRSTA
          GOTO      BEDR3000 IF EQUAL
.         
          GOTO      BEDR1000
.
BEDR3000  MATCH     SP70,PMBREWRD
          GOTO      BEDR9999 IF EQUAL
.
          MOVE      PMBRATIM,WADTIME
          GOTO      BEDR9999
.
BEDR9999  RETURN
.
.******************************************************************************
.*                       I/O Routines For The Tempfile                        *
.******************************************************************************
.
RATEMP1   MOVE      ZERO,OVRCD
          RESET     KEY31
          READ      TEMP1,KEY31;ANS
          GOTO      OVERCOND IF OVER
          RETURN             
.                            
RSTEMP1   RESET     KEY31
          READ      TEMP1,KEY31;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMP1;TRIGCODE,PATTNAME,VISTNUMB
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY31
          READ      TEMP1,KEY31;TRIGCODE,PATTNAME,VISTNUMB
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   UPDATE    TEMP1;TRIGCODE,PATTNAME,VISTNUMB
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY31
          WRITE     TEMP1,KEY31;TRIGCODE,PATTNAME,VISTNUMB
          RETURN
.
DETEMP1   DELETE    TEMP1,KEY31
          RETURN
.
.*****************************************************************************
.*                        I/O's Includes                                     *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001IO
          INC       TIMEDIFF
          INC       CALCDAYS
          INC       NAMSTRCD
.
          INC       EMRVISIO/INC
          INC       EMRBPSIO/INC         * by-pass/pre by-pass
          INC       EMRUNKIO/INC          * Unknown patents
          INC       EMRICDIO/INC
          INC       EMRVCDIO/INC
          INC       PATDO1IO/INC         * doctor IO pat
          INC       PATICDIO/INC          
          INC       PATMA1IO/INC
          INC       PATCODIO/INC         * patcodes
          INC       PMSBRQIO/INC         * patient bed request
          INC       PMSHCPIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.
.

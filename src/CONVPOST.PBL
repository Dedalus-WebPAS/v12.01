.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   CONVPOST                                                  *
.* Desc      :   Load Postcode data into ibapostf                          *
.***************************************************************************
.* Function  :   This program will read the DHS SLA text file              *
.*               and load into the Post Code Table (ibapostf).             *
.* Mods      :                                                             *
.*  V11.04.02 30/10/2024 Nikitha Bhaskar  TSK  0929823                     *
.*            Added a check in EROR0000 to handle blank lines in txt file. *
.*            Added blank lines in PROC9999 to differentiate errs & summary*
.*  V11.04.01 15/10/2024 Nikitha Bhaskar  TSK  0929823                     *
.*            Added EROR0000 to print error msg when mandatory fields are  *
.*            missing. LJUSTIFY - XPOSASGC to remove leading white spaces. *    
.*  V11.02.01 01/06/2022 J.Tan            Task 0906596                     *
.*            Renamed field for Regional/Remote Area Post as per spec.     *
.*  V10.14.02 08/05/2019 J.Tan            Task 0857392                     *
.*            Mod to upload IBPORRAV (Regional and Remote) from upload file*
.*  V10.14.01 25/03/2019 Peter Vela       Task 0857392                     *
.*            Initialised IBPORRAV before WRIBPOS1                         *
.*  V10.05.01 13/11/2014 Sandra Barcham   CAR 307061                       *
.*            Added Geographical classification to upload file             *
.*  V10.03.02 06/03/2012  Ebon Clements   CAR 260677                       *
.*            Added state to end of post code upload file                  *
.*  V10.03.01 17/11/2011  Mike Laming     CAR 240184                       *
.*            Changes to IBAPOSTF Post Code table - added State to Keys    *
.*   V9.04.01 25/11/2004 J.Tan       CAR 55403                             *
.*            Mods for WEB front page                                      *
.*   V9.03.00 19/05/2003 Lina Vo     CAR 37504                             *
.*            Created new.                                                 *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       IBAPOSFD/INC                 * Post code table
.
.
.Layout of text file   
.---------------------
POSTPD   FILE      ASCII, FIXED=112                                            
.
.Name     Type    Length  Pos  Description
.----     ----    ------  ---  -----------
XPOSCODE  DIM       4     1    Post Code                                       
XPOSSUBR  DIM       45    5    Suburb                                          
XPOSSLA1  DIM       5     50   DHS SLA 1                                        
XPOSWT1   DIM       4     55   DHS WT 1                                         
XPOSSLA2  DIM       5     59   DHS SLA 2                                        
XPOSWT2   DIM       4     64   DHS WT 2                                         
XPOSSLA3  DIM       5     68   DHS SLA 3                                        
XPOSWT3   DIM       4     73   DHS WT 3                                         
XPOSSLA4  DIM       5     77   DHS SLA 4                                        
XPOSWT4   DIM       4     82   DHS WT 4                                         
XPOSSLA5  DIM       5     86   DHS SLA 5                                        
XPOSWT5   DIM       4     91   DHS WT 5                                         
XPOSSTST  DIM       3     95   State (calculated if blank)
XPOSASGC  DIM       9     98   ABS Geographical Classification
XPOSRRAV  DIM       5    107   Regional and Remote Area value
.
.        End of record   112 
.
BYPPOST   FORM      1
COUNT     FORM      6
COUNTADD  FORM      6
COUNTERR  FORM      6
COUNTIN   FORM      6
COUNTUPD  FORM      6
CMDLINE   DIM       80       * command line
CURDTE    DIM       8
.
DATAERR   FORM      1                                                  *I-90202
DISPVAR   FORM      6
.
FILENAME  DIM       55
FILENAM1  DIM       55
.
WDD       DIM       2
WMM       DIM       2
WCC       DIM       2
WYY       DIM       2
.
.                    ....*....1....*....2....*....3....*....4..
SP127     INIT      "                                          ":
                    "                                          ":
                    "                                           "
.
PRGID     INIT      "CONVPOST"
PRGNAM    INIT      "Load Postcode into ibapostf"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and check files
          BRANCH    EXIT,ML9999            * if conversion files don't exist
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      OPEN0000               * Open file
          BRANCH    EXIT,ML9999            * EXIT = 1 if EXECUTE fails     
.
          CALL      PROC0000               * process the details
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and check files. *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
INIT1000  KEYIN     *P1:4,*EF,"Keyin Path & File Name : ",*V2LON,FILENAME
.
          PACK      FILENAME,FILENAME,SP70
          MATCH     SP70,FILENAME
          GOTO      INIT9000 IF EQUAL
.
          STRIP     FILENAME
          DISPLAY   *P18:4,*EL,*V2LON,FILENAME;
.         
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      POSTPD,FILENAME        
          MOVE      OVRCD,BYPPOST
          TRAPCLR   IO
.
          MOVE      ZERO,EXIT
.                                           * If no conversion files exist
          IF        BYPPOST=1
            DISPLAY   *P1:23,*EL,*B,"File ",FILENAME," does not exist ":
                      *P1:24,"Hit <",*V2LON,"ENTER",*HOFF,"> to continue ";
            KEYIN     ANS;
            GOTO      INIT1000
          ENDIF 
.
          PACK      CURDTE,CCC,CYY,CMM,CDD  * Set up the current date for use
.
          PACK      FILENAM1,SP70
.
          KEYIN     *P1:8,"Heading File Name   : ",*P23:13,*RV,*DV,FILENAM1:
                    *P23:8,*EL,*V2LON,FILENAM1
          GOTO      INIT3000 IF EOS
          GOTO      INIT9999
.
INIT3000  MOVE      FILENAME,FILENAM1
          GOTO      INIT9999
.
INIT9000  MOVE      ONE,EXIT
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.****************************************************************************

OPTN0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:6,"This program will load postcode data":
                    " from the ",FILENAME," file";
          KEYIN     *P1:24,*EL,"Ok to Continue (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          GOTO      OPTN9000 IF EQUAL
.
          MATCH     ANSN,ANS
          GOTO      OPTN9500 IF EQUAL
.
          BEEP
          GOTO      OPTN0000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
+
.**************************************************************************
.*                               OPEN0000              Called by: ML0000  *
.*                              Open Files                                *
.**************************************************************************
.
OPEN0000  MOVE      ZERO,OVRCD                   * 
.
          CLOSE     POSTPD
.
          DISPLAY   *P1:24,*EL,"Converting ",FILENAME,*W:
                    *P1:8,*EL;
          CLEAR     CMDLINE
          APPEND    "convpost.us1 ",CMDLINE
          APPEND    FILENAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          MOVE      ZERO,EXIT 
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      POSTPD,"postoutp.txt"
          MOVE      OVRCD,BYPPOST
          TRAPCLR   IO
.                                           * If no conversion files exist
          IF        BYPPOST=1
            DISPLAY   *P1:23,*EL,*B,"Can't find file postoutp.txt created":
                      " above - TASKID=",TASKID," - Process Terminated":  
                      *P1:24,"Hit <",*V2LON,"ENTER",*HOFF,"> to continue ";
            KEYIN     ANS;
            MOVE      ONE,EXIT
          ENDIF
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"ibapostf";
          OPEN      IBAPOST1,"ibapostf"
.                                                                
OPEN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                     Process the conversion files                       *
.**************************************************************************
.
.
PROC0000  CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CPAGENO
          MOVE      "100",CLNO              * Force 1st page Heading
          CALL      HEAD0000
.
          MOVE      ONE,COUNT
          MOVE      ZERO,COUNTIN                                      
          MOVE      ZERO,COUNTERR
          MOVE      ZERO,COUNTUPD
          MOVE      ZERO,COUNTADD
          DISPLAY   *P1:14,*EF,"* Processing ",*V2LON,FILENAME,*HOFF," *":
                    *P1:16,"Started : ",*V2LON,CTIMEIS,*HOFF:
                    *P1:18,"Post Code  : ":
                    *P1:19,"Locality   : ":
                    *P1:20,"ABS ASGC   : " 

PROC1000  MOVE      ZERO,OVRCD
          MOVE      ZERO,DATAERR                                      
          CALL      CLPS0000
          TRAP      FERR0000 IF FORMAT                               
.
          READ      POSTPD,SEQ;XPOSCODE,XPOSSUBR,XPOSSLA1,XPOSWT1:          
                               XPOSSLA2,XPOSWT2,XPOSSLA3,XPOSWT3:
                               XPOSSLA4,XPOSWT4,XPOSSLA5,XPOSWT5,XPOSSTST:
                               XPOSASGC,XPOSRRAV
.
          TRAPCLR   FORMAT                                             
          GOTO      PROC9999 IF OVER        * Check for EOF
          BRANCH    DATAERR,PROC1000        * Get next record         
.
          CALL      EROR0000                * Check for mandatory fields
          BRANCH    EXIT,PROC1000           * Skip if any mandatory field blank
.
          TYPE      XPOSCODE
          GOTO      PROC1000 IF NOT EQUAL
.
          SQUEEZE   XPOSCODE                                         
          MOVE      XPOSCODE,FORM4          * Strip leading zeros by 
          MOVE      FORM4,XPOSCODE          * moving to FORM4 and back
          REP       " 0",XPOSCODE
          PACK      XPOSSUBR,XPOSSUBR,SP70
.
          PACK      KEY8,XPOSCODE,SP10      *I-240184
.
          MATCH     SP70,XPOSSTST
          IF        @EQUAL
            CALL      STAT0000                * calculate State code
          ELSE
             PACK     IBPOSTAT,XPOSSTST,SP70
          ENDIF
.
          PACK      KEY56,KEY8,XPOSSUBR,IBPOSTAT,SP70
          CALL      RDIBPOS1
          IF        OVRCD=0
            UNPACK    XPOSSLA1,D1,D4
.           PACK      IBPOASGC,D1,SP4,D4,SP10
            LJUSTIFY  XPOSASGC
            MOVE      XPOSASGC,IBPOASGC
            PACK      IBPORRAV,XPOSRRAV,SP70
            CALL      UPIBPOS1
            ADD       ONE,COUNTUPD
          ELSE
            PACK      IBPOPCOD,XPOSCODE,SP10
            MOVE      XPOSSUBR,IBPOSUBR
            UNPACK    XPOSSLA1,D1,D4
.           PACK      IBPOASGC,D1,SP4,D4,SP10
....        CALL      STAT0000              *calc State code above 
            LJUSTIFY  XPOSASGC
            MOVE      XPOSASGC,IBPOASGC
            PACK      IBPORRAV,XPOSRRAV,SP70
            CALL      WRIBPOS1
            ADD       ONE,COUNTADD
          ENDIF
.
          ADD       ONE,COUNT
          ADD       ONE,COUNTIN
          ASSIGN    COUNT % 100,DISPVAR
          IF        DISPVAR = 0
            DISPLAY   *P14:18,*V2LON,XPOSCODE:
                      *P14:19,*V2LON,XPOSSUBR:
                      *P14:20,*V2LON,IBPOASGC
          ENDIF
.
          GOTO      PROC1000
.
PROC9999  MOVE      ZERO,OVRCD
.
          PRINT     *1,"| ",*10,"| ",*56,"| ",*62,"| ",*132,"|"
          PRINT     *1,"| ",*10,"| ",*56,"| ",*62,"| ",*132,"|"
          PRINT     *1,"| ",*10,"| ",*56,"| ",*62,"| ",COUNTIN:
                    " rec ",FILENAM1,*132,"|":
                    *N,*1,"| ",*10,"| ",*56,"| ",*62,"| ",COUNTUPD:
                    " ibapostf records updated.",*132,"|":
                    *N,*1,"| ",*10,"| ",*56,"| ",*62,"| ",COUNTADD:
                    " ibapostf records added.",*132,"|":
                    *N,*1,"| ",*10,"| ",*56,"| ",*62,"| ",COUNTERR:
                    " errors detected.",*132,"|"
          CALL      UND132
.
          RETURN
.****************************************************************************
.*                        CLPS0000                     Called by: GPMI0000  *
.*             Clear IBAPOSFD fields                                        *
.****************************************************************************
.
CLPS0000  PACK      IBPOPCOD,SP10
          PACK      IBPOSUBR,SP70
          PACK      IBPOSTAT,SP10
          PACK      IBPOASGC,SP10
          PACK      IBPORRAV,SP10
          PACK      IBPOSPAR,SP70
.
CLPS9999  RETURN
+
.****************************************************************************
.*                        STAT0000                                          *
.*                Find State of Postcode                                    *
.****************************************************************************
.
STAT0000  MOVE      XPOSCODE,FORM4          * Strip leading zeros by 
          IF        FORM4<1000
            MOVE      "NT ",IBPOSTAT
            GOTO      STAT9999
          ENDIF
.
          IF        FORM4>=2000 & FORM4<2900
            MOVE      "NSW",IBPOSTAT
            GOTO      STAT9999
          ENDIF
.       
          IF        FORM4>=2900 & FORM4<3000
            MOVE      "ACT",IBPOSTAT
            GOTO      STAT9999
          ENDIF
.       
          IF        FORM4>=3000 & FORM4<4000
            MOVE      "VIC",IBPOSTAT
            GOTO      STAT9999
          ENDIF
.       
          IF        FORM4>=4000 & FORM4<5000
            MOVE      "QLD",IBPOSTAT
            GOTO      STAT9999
          ENDIF
.       
          IF        FORM4>=5000 & FORM4<6000
            MOVE      "SA ",IBPOSTAT
            GOTO      STAT9999
          ENDIF
.       
          IF        FORM4>=6000 & FORM4<7000
            MOVE      "WA ",IBPOSTAT
            GOTO      STAT9999
          ENDIF
.       
          IF        FORM4>=7000 & FORM4<8000
            MOVE      "TAS",IBPOSTAT
            GOTO      STAT9999
          ENDIF
.       
....      MOVE      IBPOSTAT,SP10           * naughty, naughty
.
STAT9999  RETURN
.******************************************************************************
.*                                     PRNT0000                               *
.*                                   Print A Line                             *
.******************************************************************************
PRNT0000  ADD       ONE,CLNO
.
          IF        CLNO>=58
            CALL      HEAD0000              * print the page header
          ENDIF
.
          PRINT     *1,"|  ",XPOSCODE,*10,"|",XPOSSUBR,*56,"|",XPOSSLA1:
                    *62,"| Error occurred in this record. ",*132,"|"
          ADD       ONE,COUNTERR                                      
.
PRNT9999  RETURN
+
+
.******************************************************************************
.*                                     FERR0000                               *
.*                   Print TRAP data errors in text file                      *
.******************************************************************************
.
FERR0000  MOVE      ONE,DATAERR
          PRINT     *1,"|  ",XPOSCODE,*10,"|",XPOSSUBR,*56,"|",XPOSSLA1:
                    *62,"| Data error occurred in this record. ",*132,"|"
          READ      POSTPD,SEQ;XPOSCODE     * Bypass rest of record
.
FERR9999  RETURN
+
.******************************************************************************
.*                                     EROR0000                               *
.*               Print Error message if any mandotary fields are blank        *
.******************************************************************************
EROR0000  MOVE      ZERO,EXIT
.
          MATCH     XPOSCODE,SP70 
          IF        @EQUAL
            MATCH     XPOSSUBR,SP70
            IF        @EQUAL
               MATCH     XPOSSTST,SP70
               IF        @EQUAL
                 MOVE      ONE,EXIT
                 GOTO      EROR9999         * Skip blank lines/record
               ENDIF
            ENDIF
          ENDIF
.
          MATCH     XPOSCODE,SP70
          IF        @EQUAL
            PRINT     *1,"|  ",XPOSCODE,*10,"|",XPOSSUBR,*56,"|",XPOSSLA1:
                      *62,"| Postcode not populated. ",*132,"|"
            MOVE      ONE,EXIT
            ADD       ONE,COUNTERR
          ENDIF
.
          MATCH     XPOSSUBR,SP70
          IF        @EQUAL
            PRINT     *1,"|  ",XPOSCODE,*10,"|",XPOSSUBR,*56,"|",XPOSSLA1:
                      *62,"| Suburb not populated. ",*132,"|"
            MOVE      ONE,EXIT
            ADD       ONE,COUNTERR
          ENDIF
.
          MATCH     XPOSSTST,SP70
          IF        @EQUAL
            PRINT     *1,"|  ",XPOSCODE,*10,"|",XPOSSUBR,*56,"|",XPOSSLA1:
                      *62,"| State not populated. ",*132,"|"
            MOVE      ONE,EXIT
            ADD       ONE,COUNTERR
          ENDIF
.
EROR9999  RETURN
.******************************************************************************
.*                                    HEAD0000                                *
.*                  Routine to print the error report page header             *
.******************************************************************************
HEAD0000  CALL      HEAD132                 * print the page header
.
          CALL      UND132
          PRINT     *1,"|Postcode",*10,"| Locality",*56,"| SLA":
                    *62,"| Error Message       ",*132,"|"
          CALL      UND132
.      
          MOVE      ONE,CLNO
.
HEAD9999  RETURN
.
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       IBAPOSIO/INC                 * Postcode file 
          

.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   FX832066                                                    *
.* Desc      :   Fixit for DVA Numbers                                       *
.*****************************************************************************
.* Date      :   17/03/2017                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will loop through the PMI master file and for  *
.*               each record found where the DVA number is populated in      *
.*               PREPAT and where there is no pmsccdaf DVA record, it will   *
.*               create a DVA pmsccdaf record.                               *
.* Mods      :                                                               *
.*        V10.10.01 13/04/2017  Steve Armstrong  TSK 0832066                 *
.*                  Modified reports to include DVA Card Colour.             *
.*        V10.10.00 28/03/2017  Steve Armstrong  TSK 0832066                 *
.*                  Created program.                                         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCODFD/INC
          INC       PATMA1FD/INC
          INC       PMSCCDFD/INC
          INC       PMSPX2FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CURRDATE  DIM       8
DIM60     DIM       60
RECCOUNT  FORM      10
SAVECODE  DIM       3
.
.
. PROGRAM CONSTANTS
. -----------------
CATCT     INIT      "ct"
PIPE      INIT      "|"
TILDA40   INIT      "~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~"
.
PRGID     INIT      "FX832066"
PRGNAM    INIT      "DVA Number Fixit"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN2000:      * report differences
                            MAIN2000:      * report potential updates
                            MAIN1000       * update and report
.
MAIN1000  CALL      GCOD0000               * get DVA concession card code
          BRANCH    EXIT,MAIN0100          * no DVA code found
.
MAIN2000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN5000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pmsccdaf";
          OPEN      PMSCCDA1,"pmsccdaf"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Report Differences Only": 
                    *P1:6,*V2LON,TWO,*HOFF,". Report Potential Updates Only": 
                    *P1:7,*V2LON,THREE,*HOFF,". Run Update and Report"
.
OPTN0500  KEYIN     *P1:9,*EL,"Select Option : ":
                    *P17:9,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run fixit - report only
                            OPTN9000:            * run fixit
                            OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                               PROC0000              Called by: MAIN0000  *
.*                  Process the records for printing                        *
.****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing:";
.
          MOVE      ZERO,CPAGENO                 * initialise print variables
          MOVE      ZERO,CNOUNDLN
          CALL      IBACLOCK                     * load current date
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          MOVE      ZERO,RECCOUNT                * initialise record count
.
          CALL      HEAD0000                     * print header
.
          MOVE      SP8,KEY8
          CALL      RDSMAST1                     * position at start of PMI
PROC0500  CALL      RDKMAST1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          DISPLAY   *P13:24,*EL,*V2LON,PURNO;
.
          MATCH     SP10,PREPAT                  * blank PMI DVA number ?
          GOTO      PROC0500 IF EQUAL            * yes - ignore record
.
.         The patient has a PMI level DVA number, so check if one exists
.         on the concession card file as well
.
          PACK      KEY19,PURNO,TILDA40
          CALL      RSPMCCD1                     * position on U/R
PROC1500  CALL      RPPMCCD1                     * read previous record
          BRANCH    OVRCD,PROC5000               * eof - check option
.
          MATCH     PURNO,PMCDURNO               * same U/R still ?
          GOTO      PROC5000 IF NOT EQUAL        * no - check option
.
.         Check if the card type is DVA or Pension (Cat ct, Indicator 1)
.
          PACK      KEY5,CATCT,PMCDCTYP
          CALL      RDCODE1                      * code on file ?
          BRANCH    OVRCD,PROC1500               * no - ignore record
.
          MATCH     SP1,TCINDC1                  * blank indicator 1 ?
          GOTO      PROC1500 IF EQUAL            * yes - ignore record
.
          MATCH     "3",TCINDC1                  * DVA card type ?
          GOTO      PROC1500 IF NOT EQUAL        * no - ignore record
.
.         A Concession card DVA number record has been found, so check if
.         it matches the PMI DVA number
.
          MATCH     PMCDCNUM,PREPAT              * same DVA number ?
          IF        @EQUAL
            MOVE      PURNO,KEY8
            CALL      RDPMPX21                   * PMI extension found ?
            BRANCH    OVRCD,PROC0500             * no - no change required
.
            MATCH     SP3,PMPXDVAC               * yes - blank PMI DVA Colour ?
            GOTO      PROC0500 IF EQUAL          * yes - no change required
.
            MATCH     PMPXDVAC,PMCDDVAC          * same DVA Card Colour?
            GOTO      PROC0500 IF EQUAL          * yes - no change required
          ELSE
            MOVE      SP3,PMPXDVAC               * initialise DVA colour field
            MOVE      PURNO,KEY8
            CALL      RDPMPX21                   * PMI extension found ?
          ENDIF
.
.         A non-matching DVA Concession Card record was found for the patient
.
          BRANCH    COPTION,PROC7000:            * Differences Only Report
                            PROC0500:            * Potential Updates Report
                            PROC0500             * Update and Report
.
.         No DVA Concession Card record was found for the patient
.
PROC5000  BRANCH    COPTION,PROC0500:            * Differences Only Report
                            PROC7000:            * Potential Updates Report
                            PROC6000             * Update and Report
.
PROC6000  MOVE      SP3,PMPXDVAC                 * initialise DVA colour field
          MOVE      PURNO,KEY8
          CALL      RDPMPX21                     * PMI extension found ?
.
.         Report Record (all options)
.
PROC7000  CALL      DISP0000                     * print record
.
          IF        COPTION = 3
            CALL      WCCD0000                   * Write concession card record
          ENDIF
          GOTO      PROC0500
.
PROC9000  CALL      LINE0000
.
          PRINT     *N,*1,"Total Record Count: ",RECCOUNT:
                    *N,*N,*1,"*** End of Report ***"
.
PROC9999  RETURN
+
.****************************************************************************
.*                            WCCD0000                 Called by: PROC0000  *
.*                 Write a new DVA concession crad record for the patient   *
.* Requires: SAVECODE - DVA category-code                                   *
.*           PURNO - Patient U/R Number                                     *
.****************************************************************************
.
WCCD0000  MOVE      PURNO,PMCDURNO               * load record variables
          MOVE      SAVECODE,PMCDCTYP
          PACK      PMCDCNUM,PREPAT,SP20
          MOVE      SP8,PMCDEXDT
          MOVE      CURRDATE,PMCDCDAT
          MOVE      CTIMEIS,PMCDCTIM
          MOVE      "FX832066",PMCDCUID
          MOVE      SP8,PMCDUDAT
          MOVE      SP8,PMCDUTIM
          MOVE      SP10,PMCDUUID
          MOVE      PMPXDVAC,PMCDDVAC
          MOVE      SP70,PMCDSPAR
.
          PACK      KEY19,PMCDURNO,PMCDEXDT,PMCDCTYP
          CALL      RAPMCCD1
          IF        OVRCD = 1
            CALL      WRPMCCD1                   * write record
          ENDIF
.
WCCD9999  RETURN
+
.****************************************************************************
.*                            GCOD0000                 Called by: PROC0000  *
.*                      Get the first active DVA category-code record       *
.* Returns: EXIT  0 = Ok to continue                                        *
.*                1 = DVA code not found                                    *
.****************************************************************************
.
GCOD0000  PACK      KEY5,CATCT,SP5
          CALL      RDSCODE1                     * position at start of cat.
GCOD0500  CALL      RDKCODE1                     * read next record
          BRANCH    OVRCD,GCOD9500               * eof - error
.
          MATCH     CATCT,TCODE                  * same category still ?
          GOTO      GCOD9500 IF NOT EQUAL        * no - error
.
          MATCH     "3",TCINDC1                  * DVA code ?
          GOTO      GCOD0500 IF NOT EQUAL        * no - get next record
.
          MATCH     ANSI,PTCOACTV                * code inactive ?
          GOTO      GCOD0500 IF EQUAL            * yes - get next record
.
.         A valid (active) DVA concession card code has been found
.
GCOD9000  MOVE      ACODE,SAVECODE               * save code
          MOVE      ZERO,EXIT
          GOTO      GCOD9999
.
GCOD9500  DISPLAY   *P1:24,*EL,*B,"DVA concession card code not found.  ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
GCOD9999  RETURN
+
.****************************************************************************
.*                            DISP0000                 Called by: PROC0000  *
.*                  Valid record so print it                                *
.* Requires: PURNO - U/R Number                                             *
.*           PSNAME - Patient Surname                                       *
.*           PGNAME - Patient First Given Name                              *
.*           PTITL - Patient Title                                          *
.*           RECCOUNT - record count                                        *
.*           PREPAT - PMI DVA Number                                        *
.*           PMCDCNUM - Concession Card DVA Number                          *
.****************************************************************************
.
DISP0000  COMPARE   SIXTY,CLNO                   * page full ?
          CALL      HEAD0000 IF NOT LESS         * yes
.
          ADD       ONE,RECCOUNT                 * increment record count
.
          MOVE      PSNAME,PACSNAME              * format patient name
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DIM60
.
          PRINT     *1,PIPE,*3,PURNO,*14,PIPE,*16,DIM60:
                    *77,PIPE,*79,PREPAT,*93,SLASH,*95,PMPXDVAC,*101,PIPE;
.
.         We only need to print the concession card number for option 1
.         so we can see the difference to the PMI DVA number
.
          IF        COPTION = 1
            PRINT     *105,PMCDCNUM,*124,SLASH,*126,PMCDDVAC;
          ENDIF
.
          PRINT     *132,PIPE
.
          ADD       ONE,CLNO                     * increment line count
.
DISP9999  RETURN
+
.****************************************************************************
.*                            HEAD0000                 Called by: PROC0000  *
.*                       Print page heading                       DISP0000  *
.****************************************************************************
.
HEAD0000  CALL      HEAD132                      * display page header
.
          PRINT     *N;
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"U/R Number",*14,PIPE,*16,"Patient Name":
                    *77,PIPE,*79,"PMI DVA Number/Colour",*101,PIPE:
                    *103,"Conc. Card DVA Number/Colour",*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
          MOVE      SEVEN,CLNO                   * increment line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by: PROC0000  *
.*                      Draw line across page                     DISP0000  *
.*                                                                HEAD0000  *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PMSCCDIO/INC
          INC       PMSPX2IO/INC

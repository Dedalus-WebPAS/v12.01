.*******************************************************************************
.* System    :   Referrals Management                                          *
.* Program   :   FX293758                                                     *
.*                                                                             *
.* Date      :   18/09/2013                                                    *
.* Author    :   Ania P                                                        *
.* Function  :   This program will compare the Medicare 'Valid To' Date stored *
.*               in the eAdmissions table against the date in patient master   *
.*               table and print the details if they do not match.             *
.*                                                                             *
.* Mods      :                                                                 * 
.*               V10.03.00 12/11/2013 Ania P               CAR 293758          *
.*                         Created Fixit                                       *
.*                                                                             *
.*******************************************************************************
.
          INC       STD001FD
          INC       PATMA1FD/INC
.         INC       PATMX1FD/INC
          INC       PMSEDBFD/INC
          INC       PMSEHBFD/INC
          INC       PMSPX2FD/INC
.
.
. VARIABLES
. -------------------------
.
COUNT     FORM      8                  * Row count
DIM8      DIM       8                  * Formatted row count
EADMDATE  DIM       8                  * PMSEDBAF.PMDBNVAL
PMPXDATE  DIM       8                  * PMSPX2AF.PMPMMEDC
.
DAY       DIM       2
MON       DIM       2
FULLYEAR  DIM       4
.
HEADLIN1  INIT      "|    UR    |  MEDICARE  | PMI Valid | eAdmission  |"
HEADLIN2  INIT      "|  Number  |   Number   |  to Date  |Valid to Date| "
HEADUNDL  INIT      "---------------------------------------------------"
PIPE      INIT      "|"
.
ADFR0183  INIT      "ADFR0183"
HYP       INIT      "-"
.
PRGID     INIT      "FX293758"
PRGNAM    INIT      "Fix eAdmission Medicare #"Valid To#" Date"
VERSION   INIT      "V12.01.00"
+
.*******************************************************************************
.*                              ML0000                                         *
.*                      Controlling Logic (Mainline code)                      *
.*******************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with fixit
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.*******************************************************************************
.*                                  INIT0000              Called by: ML0000    *
.*                               initialisation                                *
.*******************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmsedbaf";
          OPEN      PMSEDBA1,"pmsedbaf"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmsehbaf";
          OPEN      PMSEHBA3,"pmsehbaf"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
INIT9999  RETURN
+
.*******************************************************************************
.*                                 OPTN0000               Called by: ML0000    *
.*                         get user options or exit                            *
.*                                                                             *
.*    Returns:  EXIT  = FALSE (0)  Run fixit                                   *
.*                      TRUE  (1)  Exit option selected                        *
.*******************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*******************************************************************************
.*                                PROC0000                Called by: ML0000    *
.*******************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing: ";
          MOVE      ZERO,COUNT
          CALL      PRHD0000
.
          PACK      KEY37,Z70
          CALL      RSPMEHB3
PROC1000  CALL      RPPMEHB3                     * Read e-Adm Header Table
          BRANCH    OVRCD,PROC9000
.
PROC1100  MATCH     SP70,PMHBURNO                * Only want URNO <> Blank
          GOTO      PROC9000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,"Checking UR: ":
                    PMHBURNO
.          
PROC2000  PACK      KEY28,PMHBUNIQ,ADFR0183,SP70 * Get M'care Valid To Date (1)
          CALL      RDPMEDB1
          BRANCH    OVRCD,PROC1000               
.
PROC3000  PACK      KEY8,PMHBURNO,SP70           * Read Patient Extension Table
          CALL      RDPMPX21                     * Get M'care Valid To Date (2)
          BRANCH    OVRCD,PROC1000
.
          UNPACK    PMDBVALU,DAY,MON,FULLYEAR    * Reverse Date Component Order
          PACK      PMDBVALU,FULLYEAR,MON,DAY
.
          MATCH     PMPXMEDC,PMDBVALU            * If no match, *** PRINT ***
          IF        !@EQUAL
            PACK      KEY8,PMHBURNO,SP70
            CALL      RDMAST1
.
          COMPARE   FIFTY5,CLNO
          IF        !@LESS
            CALL      PRHD0000
          ENDIF
.
            PRINT     PIPE,SP1,PMHBURNO,SP1,PIPE,SP1,PMEDI,SP1,PIPE:
                      SP1,PMPXMEDC,SP2,PIPE,SP2,PMDBVALU,SP3,PIPE
            ADD       ONE,COUNT
            ADD       ONE,CLNO
          ENDIF
.
          GOTO      PROC1000

PROC9000  PRINT     HEADUNDL
          MOVE      COUNT,DIM8
          SQUEEZE   DIM8
          DISPLAY   *P1:24,*EL,"Fixit completed. ":
                    *P18:24,"No. bad dates found: ",DIM8
          CALL      HITENTER
.
PROC9999  RETURN
+
.*******************************************************************************
.*                                PRHD0000                Called by: PROC0000  *
.*******************************************************************************
PRHD0000  MOVE      ONE,CLNO
          CALL      HEAD80
          ADD       FOUR,CLNO
          PRINT     HEADUNDL
          ADD       ONE,CLNO
          PRINT     HEADLIN1
          ADD       ONE,CLNO
          PRINT     HEADLIN2
          ADD       ONE,CLNO
          PRINT     HEADUNDL
          ADD       ONE,CLNO
.
PRHD9999  RETURN
.==============================================================================
.         I/O Includes
.==============================================================================
.
          INC       STD001IO
          INC       PATMA1IO/INC
          INC       PMSEDBIO/INC
          INC       PMSEHBIO/INC
          INC       PMSPX2IO/INC

.

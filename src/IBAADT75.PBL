.***************************************************************************
.* System    :   Patient Management                                        *
.* Program   :   IBAADT75                                                  *
.* Desc      :   Discharge Planning Report                                 *
.***************************************************************************
.* Date      :   30/10/2001                                                *
.*                                                                         *
.* Author    :   Mike Laming                                               *
.*                                                                         *
.* Function  :   This program will allow the user to select a ward and     *
.*               start date. It will then read all current admissions for  *
.*               the selected ward, and calculate the number of days to    *
.*               Discharge. All current admissions will be print, if the   *
.*               discharge date is within the next 5 days, an X will be    *
.*               displayed in the date matrix area of the Planning Report  *
.*                                                                         *
.* Mods      :                                                             *
.* V10.03.01  19/12/2012 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATDINVS/INC
.
          INC       PATCMCFD/INC            * Casemix Codes Table
          INC       PATMA1FD/INC            * Patient Master
.         INC       PATMX1FD/INC            * Patient Master Extn.1
          INC       PATMI1FD/INC            * Patient Admissions
          INC       PMSVX1FD/INC            * Patient Admissions
          INC       PATTRNFD/INC            * Patient Transfers 
          INC       PATWR1FD/INC            * Ward/Bed Details  
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.
.         Temporary File Definition
.         -------------------------
.
SEQTEMP1  IFILE SQL, FIXED=90, KEY="U1-8"
SEQTEMP2  IFILE SQL, FIXED=90, KEY="U17-22,1-8"
.
.NAME     TYPE    LENGTH     PHYSICAL     START     DESCRIPTION
.----     ----    ------     --------     -----     -----------
TMPADMNO  DIM       8           8           1       Patient Admission No.
TMPAURNO  DIM       8           8           9       Patient UR No.
TMPAWARD  DIM       3           3          17       Patient Ward No.
TMPABED   DIM       3           3          20       Patient Bed No.
TMPNAME   DIM      24          24          23       Formatted Patient Name
TMPPSEX   DIM       1           1          47       Patient Sex
TMPADATE  DIM       8           8          48       Admission Date (CCYYMMDD)
TMPADOCT  DIM       6           6          56       Doctor Attending Patient
TMPCSMIX  DIM       9           9          62       Casemix Code
TMPASTAY  DIM       3           3          71       Patient Estimated LOS
TMPCSLOS  DIM       4           4          74       Casemix Length of Stay
TMPCULOS  DIM       4           4          78       Calculated Length of Stay
TMPDAY1   DIM       1           1          82       Discharge day 1
TMPDAY2   DIM       1           1          83       Discharge day 2
TMPDAY3   DIM       1           1          84       Discharge day 3
TMPDAY4   DIM       1           1          85       Discharge day 4
TMPDAY5   DIM       1           1          86       Discharge day 5
.
.End of Record                             88
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
BYPASS    FORM      1
CALDAYS   FORM      8
CMDLINE   DIM       80
.COUNT     FORM      3
COUNTOU   FORM      4
COUNT1    FORM      4
COUNT2    FORM      4
COUNT3    FORM      4
COUNT4    FORM      4
COUNT5    FORM      4
COUNT8    FORM      4
COUNT9    FORM      4
COUNT11   FORM      4
COUNT12   FORM      4
COUNT13   FORM      4
COUNT14   FORM      4
COUNT15   FORM      4
COUNT99   FORM      4.1
CURRLOS   FORM      4
DATEFROM  DIM       8
DATEDSCH  DIM       8
DATETO    DIM       8
DATEWORK  DATE      8
DDATE     DIM       8
DHEAD     DIM       5[5]
DATES     DIM       4[5]
.DIM8      DIM       8
.DIM10     DIM       10
NUMB      FORM      4
PREVWARD  DIM       3
REPRTEND  DIM       1
SHRTNAME  DIM       21
TEMPFILE  DIM       8
TEMPNAME  DIM       40
TMPKEY    DIM       14[32]
WARDFROM  DIM       3
WARDTO    DIM       3
XXX       DIM       1[5]
.
.
. PROGRAM CONSTANTS
. -----------------
ANS2      INIT      "2"    
DATE1     INIT      " 1st"    
DATE2     INIT      " 2nd"    
DATE3     INIT      " 3rd"    
DATE4     INIT      "21st"    
DATE5     INIT      "22nd"    
DATE6     INIT      "23rd"    
DATE7     INIT      "31st"    
.
.ERASE     INIT      "iserase "
LTH       INIT      "th"
SLSH      INIT      "/"
.ISBUILD   INIT      "isbuild "
UKEY2     INIT      " 90 U1-8 U17-22,1-8"
ZED14     INIT      "zzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAADT75"
PRGNAM    INIT      "DISCHARGE PLANNING REPORT"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
          CALL      CREA0000               * create temporary file
.
ML1000    CALL      GWRD0000               * get ward code/s           
          BRANCH    EXIT,ML9000            * nothing entered or exitchar entered
.
ML2000    CALL      GDAT0000               * get start date & setup array
          BRANCH    EXIT,ML9000            * nothing entered or exitchar entered
.
          CALL      CLER0000               * clear all temp file records
          CALL      LOAD0000               * load temp file records
.
          COMPARE   ZERO,COUNTOU           * If nothing found, ask again
          GOTO      ML1000 IF EQUAL
.
ML3000    CALL      PRNT0000               * print temp file records
.
ML9000    CALL      KILL0000               * erase temporary file
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A6,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patcmcaf";
          OPEN      PATCMCA1,"patcmcaf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.**************************************************************************
.*                              GWRD0000               Called by: ML0000  *
.*             get the user to keyin the required ward code               *
.* Returns:  EXIT  0 = valid ward selected                                *
.*                 1 = nothing entered or exitchar entered                *
.*           WWARD   = ward code                                          *
.*           WBDESC  = ward description                                   *
.**************************************************************************
.
GWRD0000  DISPLAY   *P1:4,*EF,"Ward Code  :":
                    *P1:5,*EF,"       to  :":
                    *P1:7,*EF,"Start Date :";
.
          MOVE      SP3,WARDFROM
          MOVE      SP3,WARDTO
.
          MOVE      TEN4,ECOL                    * initialise keyin variables
          MOVE      FOUR,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          MOVE      ZERO,CKYIMODE
.
          CALL      PATWRDKY                     * keyin code
          BRANCH    EXIT,GWRD1000:               * nothing entered
                         GWRD9500                * exitchar entered
.
          MOVE      WWARD,WARDFROM
          DISPLAY   *P20:4,WBDESC
          GOTO      GWRD5000
.
GWRD1000  MOVE      "000",WARDFROM
          DISPLAY   *P14:4,"Start"
.
GWRD5000  MOVE      FIVE,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          MOVE      ZERO,CKYIMODE
.
          CALL      PATWRDKY                     * keyin code
          BRANCH    EXIT,GWRD7000:               * nothing entered
                         GWRD9500                * exitchar entered
.
          MATCH     WARDFROM,WWARD
          IF        @LESS
            MOVE      WARDFROM,WARDTO
            MOVE      WWARD,WARDFROM
          ELSE
            MOVE      WWARD,WARDTO
          ENDIF
. 
          DISPLAY   *P20:5,WBDESC
          GOTO      GWRD9000
.
GWRD7000  MOVE      "~~~",WARDTO
          DISPLAY   *P14:5,"End"
.
. --- or GWRD8000  MOVE      WARDFROM,WARDTO
.
GWRD9000  MOVE      ZERO,EXIT
          GOTO      GWRD9999
.
GWRD9500  MOVE      ONE,EXIT
.
GWRD9999  RETURN
+
.**************************************************************************
.*                              GDAT0000               Called by: ML0000  *
.*                 get start date and setup date arrays                   *
.**************************************************************************
.
GDAT0000  MOVE      SP8,KEY8
.
          MOVE      SEVEN,CVERT
          MOVE      TEN4,CCOL
          UNPACK    SP6,CDAY,CMON,CYEAR
          MOVE      CCC,CCENT
          REP       " 0",CCENT
          MOVE      ZERO,CHIGHLT
.
          CALL      KEYDATE                 * keyin the date
          BRANCH    OVRCD,GDAT9999
.
          PACK      DATEFROM,CCENT,CYEAR,CMON,CDAY
.
.                                           * calculate report date range
          MOVE      DATEFROM,DATEWORK
          ADD       FIVE,DATEWORK
          MOVE      DATEWORK,DATETO
          GOTO      GDAT9999
.
GDAT9000  MOVE      ONE,EXIT
.
GDAT9999  RETURN
+
.**************************************************************************
.*                              CLER0000               Called by: ML0000  *
.*             clear all records from the temporary file                  *
.**************************************************************************
.
CLER0000  MOVE      SP8,KEY8
          CALL      RSTEMP1                      * position at start of file
          CALL      RKTEMP1                      * read next record
          BRANCH    OVRCD,CLER9999               * eof - finished
.
          MOVE      TMPADMNO,KEY8
          CALL      DETEMP1                      * delete record
          GOTO      CLER0000                     * get next record
.
CLER9999  RETURN
+
.**************************************************************************
.*                              LOAD0000               Called by: ML0000  *
.*             load records into the temp file for selected ward          *
.**************************************************************************
.
LOAD0000  MOVE      ZERO,COUNTOU 
          DISPLAY   *P1:24,*EL,"Loading records.....";
.
          PACK      KEY20,ANS2,WARDFROM,SP20
          CALL      RDSMISS6                     * position for ward
LOAD0500  CALL      RDKMISS6                     * read next record
          BRANCH    OVRCD,LOAD9999               * eof - finished
.
          COMPARE   TWO,ASTAT                    * patient still admitted ?
          GOTO      LOAD9999 IF NOT EQUAL        * no - finished
.
          MATCH     AWARD,WARDTO                 * still in Ward range ?
          GOTO      LOAD9999 IF LESS             * no - finished
.
          CALL      CKDT0000                     * check admission date
          BRANCH    BYPASS,LOAD0500              * bypass if outside range
.
.         A valid record has been found, so load it into the temp file
.
          CALL      FNAM0000                     * format the patient's name
.
          MOVE      AADMNO,TMPADMNO              * load file variables
          MOVE      AURNO,TMPAURNO      
          MOVE      AWARD,TMPAWARD
          MOVE      ABED,TMPABED
          MOVE      TEMPNAME,TMPNAME       
          MOVE      PSEX,TMPPSEX
          MOVE      ADATE,TMPADATE               * Admission Date
          MOVE      ADOCTA,TMPADOCT              * Curr Attending Doctor
          MOVE      ASTAY,TMPASTAY               * Expected Length of Stay
. ---     MOVE      PTMIPCMX,TMPCSMIX            * Casemix code ex PATMI1FD
          MOVE      PTMIPDRG,TMPCSMIX            * Casemix code ex PATMI1FD
          MOVE      SP5,TMPCSLOS
          MATCH     SP9,TMPCSMIX
          IF        !@EQUAL
            MOVE      PTCAELOS,TMPCSLOS          * Casemix LOS ex PATCMCFD
          ENDIF
          MOVE      CURRLOS,TMPCULOS             * calculated Current LOS
          MOVE      XXX[1],TMPDAY1
          MOVE      XXX[2],TMPDAY2
          MOVE      XXX[3],TMPDAY3
          MOVE      XXX[4],TMPDAY4
          MOVE      XXX[5],TMPDAY5
.
          MOVE      TMPADMNO,KEY8
          CALL      WRTEMP1                      * write temp file record
          ADD       ONE,COUNTOU
          DISPLAY   *P22:24,*EL,COUNTOU
          GOTO      LOAD0500                     * get next record
.
LOAD9999  RETURN
+
.**************************************************************************
.*                              CKDT0000               Called by: LOAD0000*
.*              check admission date + LOS  vs  date range                *
.**************************************************************************
.
CKDT0000  MOVE      ONE,BYPASS
.
          MATCH     ADATE,DATEFROM          * bypass if admitted after start
          GOTO      CKDT9999 IF LESS
.
          IF        ASTAY <> 0
            MOVE      ADATE,DATEWORK
            ADD       ASTAY,DATEWORK        * add No.of days to Admiss.Date
            MOVE      DATEWORK,DATEDSCH     * this is expected Discharge date
          ELSE
            MOVE      DATEFROM,DATEWORK
            ADD       TEN,DATEWORK
            MOVE      DATEWORK,DATEDSCH     * this is fictional Discharge date
          ENDIF
.
.                   Check the expected Discharge date against date range
.
          MATCH     DATEFROM,DATEDSCH       * bypass if Discharge <= from date
          GOTO      CKDT9999 IF LESS
.
.                   Do the other Length of Stay (LOS) and date processing
. 
CKDT5000  MOVE      ZERO,BYPASS
. ----    MOVE      PTMIPCMX,KEY9
          MOVE      PTMIPDRG,KEY9
          CALL      RDPTCMC1                * Read Casemix Codes table
          IF        OVRCD = 1
            MOVE      ZERO,PTCAELOS         * just in case
          ENDIF
.
.                   Calculate the current LOS using NHOSPDAY
.
          MOVE      ZERO,NBRDAYS
          MOVE      ADATE,FROMDATE
          MOVE      DATEFROM,TODATE         * todate = end date of period + 1
          CALL      NHOSPDAY                * bed day calculating routine
          MOVE      NBRDAYS,CURRLOS
.
.                   Now decide which day in the Day matrix
.
          UNPACK    SP10,XXX[1],XXX[2],XXX[3],XXX[4],XXX[5]
          DAYSEP    DATEFROM,DATEDSCH,NUMB
          IF        NUMB > 0 & NUMB < 6
            MOVE      ANSX,XXX[NUMB]
          ENDIF
.
CKDT9999  RETURN
+
.**************************************************************************
.*                              FNAM0000               Called by: LOAD0000*
.*                    format the patient name                             *
.**************************************************************************
.
FNAM0000  MOVE      "Unknown Patient",TEMPNAME   * format patient name
          MOVE      AURNO,KEY8
          CALL      RDMAST1                      * pmi record on file ?
          IF        OVRCD = 0
            MOVE      PSNAME,PACSNAME            * yes
            MOVE      PGNAME,PACGNAME
.           MOVE      PTITL,PACTITLE
            MOVE      SP2,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,TEMPNAME
          ENDIF
.
FNAM9999  RETURN
+
.**************************************************************************
.*                              CREA0000               Called by: ML0000  *
.*             create a new temporary file                                *
.**************************************************************************
.
CREA0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFILE
.
          CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY2
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      SEQTEMP1,TEMPFILE            * open temp index file
          OPEN      SEQTEMP2,TEMPFILE            * open temp index file
.
CREA9999  RETURN
+
.****************************************************************************
.*                              PRNT0000               Called by: ML0000    *
.*                  print the discharge planning report                     *
.****************************************************************************
.
PRNT0000  MOVE      SP3,PREVWARD
          CALL      HARR0000                     * set up the heading array
          MOVE      "000",KEY14
          CALL      RSTEMP2                      * position on first record
.
PRNT1000  CALL      RKTEMP2
          BRANCH    OVRCD,PRNT9000,PRNT9000      * end of file
.
          MATCH     PREVWARD,TMPAWARD            * check paging
          CALL      PEND0000 IF NOT EQUAL        * print Ward totals
.
          COMPARE   FIFTY9,CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      TMPAWARD,PREVWARD
          UNPACK    TMPADATE,CCENT,CYEAR,CMON,CDAY
          PACK      DIM10,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR
          MOVE      TMPNAME,SHRTNAME
.
          PRINT     *1,TMPAWARD,"/",TMPABED,*9,TMPAURNO,*18,SHRTNAME:
                    *41,TMPPSEX,*44,TMPADMNO,*54,DIM10,*66,TMPADOCT:
                    *75,TMPCSMIX,*84,TMPASTAY,*92,TMPCSLOS,*100,TMPCULOS:
                    *110,TMPDAY1,*115,TMPDAY2,*120,TMPDAY3,*125,TMPDAY4:
                    *130,TMPDAY5
.
.                                                  Calculate Average LOS
          ADD       ONE,COUNT8
          MOVE      TMPASTAY,NUMB
          ADD       NUMB,COUNT9
.                                                  Count discharges for ward
          MATCH     ANSX,TMPDAY1
          IF        @EQUAL
            ADD       ONE,COUNT1
          ENDIF
          MATCH     ANSX,TMPDAY2
          IF        @EQUAL
            ADD       ONE,COUNT2
          ENDIF
          MATCH     ANSX,TMPDAY3
          IF        @EQUAL
            ADD       ONE,COUNT3
          ENDIF
          MATCH     ANSX,TMPDAY4
          IF        @EQUAL
            ADD       ONE,COUNT4
          ENDIF
          MATCH     ANSX,TMPDAY5
          IF        @EQUAL
            ADD       ONE,COUNT5
          ENDIF
.
          GOTO PRNT1000
.
PRNT9000  MOVE      ZERO,EXIT
          MOVE      ANSY,REPRTEND
          CALL      PEND0000                * print totals for last page
          PRINT     *N,*1,"*** End of Report ***"
.
PRNT9999  RETURN
+
.****************************************************************************
.*                              PEND0000               Called by: PRNT0000  *
.*                  print totals for a ward                       ML0000    *
.****************************************************************************
.
PEND0000  MATCH     SP3,PREVWARD
          GOTO      PEND9000 IF EQUAL
.
          COMPARE   FIFTY9,CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      COUNT9,COUNT99
          DIV       COUNT8,COUNT99
.
          PRINT     *83,"------",*108,"-------------------------":
                    *N,*55,"Ward Totals":
                    *75,"Av.LOS",*83,COUNT99,*98,"Discharges":
                    *108,COUNT1,*113,COUNT2,*118,COUNT3,*123,COUNT4:
                    *128,COUNT5
.
          MATCH     WARDFROM,WARDTO
          GOTO      PEND9000 IF EQUAL
.
          ADD       COUNT1,COUNT11
          ADD       COUNT2,COUNT12
          ADD       COUNT3,COUNT13
          ADD       COUNT4,COUNT14
          ADD       COUNT5,COUNT15
.
          MATCH     ANSY,REPRTEND
          GOTO      PEND9000 IF NOT EQUAL
.
          COMPARE   FIFTY9,CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *108,"=========================":
                    *N,*85,"Report Totals":
                    *108,COUNT11,*113,COUNT12,*118,COUNT13,*123,COUNT14:
                    *128,COUNT15
.
PEND9000  MOVE      "99",CLNO
          MOVE      ZERO,COUNT1
          MOVE      ZERO,COUNT2
          MOVE      ZERO,COUNT3
          MOVE      ZERO,COUNT4
          MOVE      ZERO,COUNT5
          MOVE      ZERO,COUNT8
          MOVE      ZERO,COUNT9
.
PEND9999  RETURN
+
.****************************************************************************
.*                              HARR0000               Called by: PRNT0000  *
.*                  set up the heading array of days              ML0000    *
.****************************************************************************
.
HARR0000  MOVE      DATE1,DATES[1]
          MOVE      DATE2,DATES[2]
          MOVE      DATE3,DATES[3]
          MOVE      DATE4,DATES[4]
          MOVE      DATE5,DATES[5]
          MOVE      ZERO,COUNT                                  
          REPEAT
            ADD       ONE,COUNT          
            MOVE      DATEFROM,DATEWORK
            ADD       COUNT,DATEWORK
.                                           * set up report column headings
            UNPACK    DATEWORK,CCENT,CYEAR,CMON,CDAY
            CMATCH    "0",CDAY
            IF        @EQUAL
              REP       "0 ",CDAY           * get rid of zero in 01, 02, etc
            ENDIF
            SEARCH    CDAY,DATE1,SIX,NUMB
            IF        NUMB = 0
              PACK      DHEAD[COUNT],CDAY,LTH
            ELSE
              MOVE      DATES[NUMB],DHEAD[COUNT]
            ENDIF
          UNTIL     COUNT = FIVE
.
HARR9999  RETURN
+
.****************************************************************************
.*                              HEAD0000               Called by: PRNT0000  *
.*                      print the report headings                 ML0000    *
.****************************************************************************
.
HEAD0000  CALL      HEAD132                 * print the page header
.
          UNPACK    DATEFROM,CCENT,CYEAR,CMON,CDAY
          PACK      DIM10,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR
.
          PRINT     *1,"Selected Plan Date : ",DIM10:
.                          ....*....1....*....2....*....3....*....4....*...
                    *N,*1,"Ward     UR.No.  Patient Name           S  Admn.":
.                    .5....*....6....*....7....*....8....*....9....*...
                    "No.  Admn.Date   Current  Casemix  Patient Casemix":
.                    .0....*..  ..1..    ..*..    ..2..    ..*..    ..3..
                    " Current ",DHEAD[1],DHEAD[2],DHEAD[3],DHEAD[4],DHEAD[5]:
                    *N,*1,"/Bed                                    e       ":
                    "                 Attend.  Code     Estim.  Expect.":
                    " LOS":
                    *N,*1,"                                        x       ":
                    "                 Doctor            LOS     LOS",*N
.
          MOVE      EIGHT,CLNO
.
HEAD9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by: CREA0000  *
.*               close and erase the temporary file               ML0000    *
.****************************************************************************
.
KILL0000  CLOSE     SEQTEMP1                     * close file
          CLOSE     SEQTEMP2                     * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
+
.****************************************************************************
.*        IO ROUTINES FOR TEMPORARY FILE                                    *
.****************************************************************************
.
.         Index 1 Routines
.
RATEMP1   MOVE      ZERO,OVRCD
          READ      SEQTEMP1,KEY8;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   READ      SEQTEMP1,KEY8;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          READ      SEQTEMP1,KEY8;TMPADMNO,TMPAURNO,TMPAWARD,TMPABED:
                                  TMPNAME,TMPPSEX,TMPADATE,TMPADOCT:
                                  TMPCSMIX,TMPASTAY,TMPCSLOS,TMPCULOS:
                                  TMPDAY1,TMPDAY2,TMPDAY3,TMPDAY4,TMPDAY5
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    SEQTEMP1;TMPADMNO,TMPAURNO,TMPAWARD,TMPABED:
                                  TMPNAME,TMPPSEX,TMPADATE,TMPADOCT:
                                  TMPCSMIX,TMPASTAY,TMPCSLOS,TMPCULOS:
                                  TMPDAY1,TMPDAY2,TMPDAY3,TMPDAY4,TMPDAY5
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMP1   MOVE      ZERO,OVRCD
          READKP    SEQTEMP1;TMPADMNO,TMPAURNO,TMPAWARD,TMPABED:
                                  TMPNAME,TMPPSEX,TMPADATE,TMPADOCT:
                                  TMPCSMIX,TMPASTAY,TMPCSLOS,TMPCULOS:
                                  TMPDAY1,TMPDAY2,TMPDAY3,TMPDAY4,TMPDAY5
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     SEQTEMP1,KEY8;TMPADMNO,TMPAURNO,TMPAWARD,TMPABED:
                                  TMPNAME,TMPPSEX,TMPADATE,TMPADOCT:
                                  TMPCSMIX,TMPASTAY,TMPCSLOS,TMPCULOS:
                                  TMPDAY1,TMPDAY2,TMPDAY3,TMPDAY4,TMPDAY5
          RETURN
.
DETEMP1   DELETE    SEQTEMP1,KEY8
          RETURN
.
.         Index 2 Routines
.
RSTEMP2   READ      SEQTEMP2,KEY14;;
          RETURN
.
RKTEMP2   MOVE      ZERO,OVRCD
          READKS    SEQTEMP2;TMPADMNO,TMPAURNO,TMPAWARD,TMPABED:
                                  TMPNAME,TMPPSEX,TMPADATE,TMPADOCT:
                                  TMPCSMIX,TMPASTAY,TMPCSLOS,TMPCULOS:
                                  TMPDAY1,TMPDAY2,TMPDAY3,TMPDAY4,TMPDAY5
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMP2   MOVE      ZERO,OVRCD
          READKP    SEQTEMP2;TMPADMNO,TMPAURNO,TMPAWARD,TMPABED:
                                  TMPNAME,TMPPSEX,TMPADATE,TMPADOCT:
                                  TMPCSMIX,TMPASTAY,TMPCSLOS,TMPCULOS:
                                  TMPDAY1,TMPDAY2,TMPDAY3,TMPDAY4,TMPDAY5
          GOTO      OVERCOND IF OVER
          RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       NHOSPDAY
.
          INC       PATWRDKY
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATCMCIO/INC            * Casemix Codes table
          INC       PATTRNIO/INC            * Transfer file for NHOSPDAY
          INC       PATWR1IO/INC            * Ward/Bed Details  
          INC       WEBERRIO/INC                 * Web Server Error Logging

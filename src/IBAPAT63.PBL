. ***********************************************************************
. *                       P R O G R A M  S U M M A R Y                  *
. *                       -------------  -------------                  *
. *                                                                     *
. *     PROGRAM NAME:     IBAPAT63                                      *
. *                                                                     *
. *     FUNCTION:         PATIENT DISCHARGE AUDIT PRINTING              *
. *                                                                     *
. *     DATE WRITTEN:     08/08/85                                      *
. *                                                                     *
. *     AUTHOR:           MALCOLM HAYSE (I.B.A)                         *
. *                                                                     *
. *     MODIFICATIONS:                                                  *
. *           V5.08.01  08/09/2000  Jill Habasque SRF 1268              *
. *                     Recompiled                                      *
. *           V5.07.01  11/10/1999  Steve Downing                       *
. *                     Added Selection screen & option to run without  *
. *                     resetting audit file                            *
. *           V5.07.B02 25/01/1999  Nicole Harrington 507 rebound 137   *
. *                     Mods to use HEAD132                             *
. ***********************************************************************
. *           V5.05.01  16/01/1998  Steve Armstrong   SRF 642548 (AUS)  *
. *                     Recompiled for changes to PATADBFD & PATIOADB   *
. ***********************************************************************
. *                       V5.01.02 19/07/90 i. chung        SRF 105683  *
. *                                Fixed report time                    *
. *                                                                     *
. ***********************************************************************
.
          INC       STD001FD
          INC       PATDSCFD/INC
          INC       PATADBFD/INC
+
.
.                 WORK AREA
.
ADESC     DIM       13
.
ADD       DIM       2
AMM       DIM       2
AYY       DIM       2
ACC       DIM       2
SEC1      FORM      "00001" 
.
ENDSEC    FORM      5
ACTION    DIM       1
OPERCODE  DIM       4
.
LNO       FORM      2
PAGENO    FORM      3
.
RESAUDIT  FORM      1
SEC       FORM      1
.
.CONTROLF   FILE      ASCII, FIXED=256
.
.      EXTRA VARIABLES
.
INSEC     DIM       6
TIMEIS    DIM       8
TIMENOW   DIM       8
.
FIELD     FORM      2
.
PRGID     INIT      "IBAPAT63"
PRGNAM    INIT      "Discharge Audit File Listing"
VERSION   INIT      "V12.01.00"
+
.
.         START OF PROGRAM
.
          DISPLAY   *ES;
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patbaud";
          OPEN      PATBAUD,"patbaud"
          DISPLAY   *P1:24,*EL;
+
.
.           MAIN        MENU
.
.             SELECT OPTION
.
STARTX    MOVE      ZERO,PAGENO
          MOVE      SP1,ANS
          MOVE      ZERO,RESAUDIT
.
START     KEYIN     *P1:4,*EL,*V2LON,"0",*HOFF," = Exit ":
                    *P1:5,*EL,*V2LON,"1",*HOFF," = Print Audit Listing ":
                    *P1:6,*EL,*V2LON,"2",*HOFF," = Print Audit Listing":
                    " without Reset ":
                    *P1:10,*EL,"Select Option: ",*V2LON,ANS
.
          PACK      ANS,ANS,SP1
          MATCH     "0",ANS
          GOTO      ENDIT IF EQUAL
.
          MATCH     "1",ANS
          GOTO      START2R IF EQUAL
.
          MATCH     "2",ANS
          GOTO      START2 IF EQUAL
.
          MATCH     SP1,ANS
          GOTO      ENDIT IF EQUAL
.
          BEEP
          GOTO      START
.
START2R   MOVE      ONE,RESAUDIT
.
START2    KEYIN     *P1:5,*EF," Do you want to Print ":
                    "the Discharge Audit Listing (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS
.
          REP       UPPLOW,ANS
.
          MATCH     ANSN,ANS
          GOTO      START IF EQUAL
.
          MATCH     ANSY,ANS
          GOTO      PRNTPG IF EQUAL
.
          BEEP
          GOTO      START2

+
.
.        SUBROUTINE TO PRINT PAGES ONTO PRINTER
.
PRNTPG 
.
          CALL      RDSBAUD
          MOVE      SECTOR,ENDSEC
          COMPARE   SEC1,ENDSEC
          GOTO      NODSCH IF EQUAL
.
.      START AT FIRST SECTOR IN AUDIT TRAIL
.
          MOVE      SEC1,SECTOR
.  
          MOVE      "60",LNO
          DISPLAY   *P20:24,*V2LON:
                    "** Printing DATA **",*W2,*HOFF:
                    *P20:24,*EL,"U/R Number : "
.
NEXTA     COMPARE   SECTOR,ENDSEC
          GOTO      ENDP IF EQUAL
.
          CALL      RDBAUD
.
          CALL      DISPDA
          DISPLAY   *P35:24,*V2LON,SDURNO,"  ",ADESC
          ADD       ONE,SECTOR
          GOTO      NEXTA
.
NODSCH    DISPLAY   *P20:24,*V2LON:
                    "** Patient Discharge Audit File is Empty **",*W2;
          GOTO      ENDIT
+
.
.           PRINTING HAS FINISHED 
.
ENDP    
          PRINT     *N,"  ***  END OF REPORT  ***"
          DISPLAY   *P10:24,*EL,*V2LON:
                    "** Report Generation Completed **",*W2;
.
.         Clear the audit file
.
          COMPARE   ONE,RESAUDIT
          GOTO      ENDIT IF NOT EQUAL
.
CLREP     CALL      CRBAUD
          DISPLAY   *P10:24,*EL,*V2LON:
                    "** Audit File Reset **",*W2;
          GOTO      ENDIT
+
.
.     PRINT ACTUAL DATA 
.
DISPDA    COMPARE   "55",LNO
          CALL      HEAD IF NOT LESS
. 
.         PRINT DATA TO PRINTER
.
          CALL      ACTDES
.
          UNPACK    SDDATE,XCENT,XYEAR,XMON,XDAY
.
          PRINT     *1,"!",ADD,"/",AMM,"/",AYY,*10,"!",TIMEIS:
                    *19,"!",OPERCODE,*24,"!",ADESC:
                    *38,"!",SDADMNO,*47,"!",SDDEST:
                    *52,"!",SDSTAT,*57,"! ",XDAY,"/",XMON,"/",XYEAR:
                    *68,"! ",SDTIME,*79,"! ",SDDIAG,*132,"!"
.
          CALL      PAGEND
.
ENDPRT    ADD       ONE,LNO
.
DISPEND   RETURN
+
.
.           ACTION MOVE DESCRIPTION IN
.
ACTDES    MATCH     "A",ACTION
          GOTO      ADD IF EQUAL
.
          MATCH     "B",ACTION
          GOTO      BEF IF EQUAL
.
          MATCH     "C",ACTION
          GOTO      CHG IF EQUAL
.
          MATCH     "D",ACTION
          GOTO      UND IF EQUAL
.
ADD       MOVE      "NEW DISCHARGE",ADESC
          RETURN
.
BEF       MOVE      "CHANGED FROM ",ADESC
          RETURN
.
CHG       MOVE      "CHANGED TO   ",ADESC
          RETURN
.
UND       MOVE      "UNDISCHARGED ",ADESC
          RETURN
+
.
.          LAST LINE ON THE DSCH
.
PAGEND    PRINT     *1,"*-------------------------------":
                        "------------------------------":
                        "------------------------------":
                        "---------------------------------------*"
          ADD       ONE,LNO
          RETURN
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,PAGENO
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK                     * added for V5.01.02
          CALL      HEAD132
.
          PRINT     *N,*1,"*---------------------------------":
                    "----------------------------------------":
                    "--------------------------------------------":
                    "-------------*":
                    *N,"! Dates",*10,"! Times",*19,"!Oper":
                    *24,"! Action Taken":
                    *38,"!Adm No",*47,"!Dest",*52,"!Stat":
                    *57,"! Dis-Date",*68,"! Dis-Time":
                    *79,"! Discharge Diagnosis",*132,"!"
.
          CALL      PAGEND
.
          MOVE      FIVE,LNO
          MOVE      SP1,ANS
          RETURN
+
.
          INC       PATADBIO/INC
          INCLUDE   STD001IO      
.
ENDIT     CHAIN     PGM
          STOP

.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   IBAADT64                                                  *
.* Desc      :   Alert Review Report                                       *
.***************************************************************************
.* Date      :   17/05/2011                                                *
.* Author    :   Davin Sloan                                               *
.* Mods.     :                                                             *
.* V10.03.04  19/12/2012 Patrick Adair                          CAR 261630 *
.*                       Removed port number from temp file name           *
.*        V10.03.03 07/02/2012  Ebon Clements  CAR 259075                  *
.*                  Populate TEMPCNTR prior to temp file write             *
.*        V10.03.02 25/01/2012  Davin          CAR 259075                  *
.*                  Changed 2 alert keys                                   *
.*        V10.03.01 29/11/2011  Jill Parkinson CAR 249362                  *
.*                  Changed alert keys                                     *
.*        V10.02.00 17/05/2011 Davin          CAR 239540                   *
.*                  Created program                                        *
.***************************************************************************
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCALAG/INC            * Data variables for CALCAGE
          INC       PATDHEAD/INC            * Data variables for CALCAGE
          INC       PATALRFD/INC  
          INC       PATCODFD/INC  
          INC       PATCONFD/INC            * Control file
          INC       PATMA1FD/INC  
          INC       PMSHCPFD/INC  
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBSECFD/INC  
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
TEMP1     IFILE    SQL, FIXED=52, KEY="U1-8,9-10,11-13,14-16"
FILELIN1  INIT      " 52 UC1-8,9-10,11-13,14-16"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
TEMPURNO  DIM       8      8        1     Patients UR Number
TEMPCATG  DIM       2      2        9     Codes File Category (H1-H9)
TEMPCODE  DIM       3      3        11    Codes File Code (for Category)
TEMPCNTR  DIM       3      3        14    Codes File Counter
TEMPDATE  DIM       8      8        17    Alert Start Date (CCYYMMDD)
TEMPLSEV  DIM       1      1        25    Severity Level (0-9)
TEMPRDTE  DIM       8      8        26    Review Date (ccyymmdd)
TEMPEDAT  DIM       8      8        34    End Date/Date Cleared (ccyymmdd)
TEMPRQBY  DIM       10     10       42    Requested By (HCP Code)
.End of Record                      52
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
AFRM1     FORM      1
ALERTSEC  DIM       1            * alert security level
ALRTTYPE  DIM       3
BJDAY     FORM      3
CJDAY     FORM      3
CURRDATE  DIM       8
DISPDAT1  DIM       10
DISPDAT2  DIM       10
DISPDAT3  DIM       10
ENDDTE8   DIM       8                       * End date (ccyymmdd)
FNAMEA    DIM       8
HCPFNAM   DIM       20                      * formatted hcp name
INDICATR  DIM       7
PATNAME   DIM       20
RECCOUNT  FORM      10
REDCT     FORM      5                       * Read counter
STRTDTE8  DIM       8                       * Start date (ccyymmdd)
USERID    DIM       10
WRTCT     FORM      5                       * Write counter
.
PRGNAM    INIT      "Alert Review Report"
PRGID     INIT      "IBAADT64"
VERSION   INIT      "V12.01.00"
.**********************************************************************
.*                           MAINLINE                                 *
.*                       Controlling Logic                            *
.**********************************************************************
ML0000    CALL      INIT0000            * open files/display header
.
ML1000    CALL      OPTN0000            * select option
          BRANCH    EXIT,ML9999
.
          CALL      ALRT0000            * Keyin Alert Type
.
          CALL      DATE0000            * Keyin the alert review date range
          BRANCH    EXIT,ML1000
.
          CALL      KUSR0000            * Keyin the user id
          BRANCH    EXIT,ML1000
.
ML2400    CALL      CONTQST             * OK to continue ?
          BRANCH    CEXIT,ML3000,ML1000,ML9999
          BEEP
          GOTO      ML2400
.
ML3000    CALL      OPEN0000            * Open the tempfile
          CALL      LOAD0000            * Load the tempfile
          CALL      PRIN0000            * print all details
          CALL      KILL0000            * Delete the tempfile
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*               Open files and display header                        *
.**********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening ";
.
          DISPLAY   *P60:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P60:24,"patalrtf";
          OPEN      PATALRT1,"patalrtf"
.
          DISPLAY   *P60:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P60:24,"websecaf";
          OPEN      WEBSECA1,"websecaf"
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPEN0000                                  *
.*                              Open The Tempfile                             *
.******************************************************************************
OPEN0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEA
.
          CALL      KILL0000                * Delete the tempfile
.
          CLEAR     KEY99
          APPEND    "isbuild ",KEY99
          APPEND    FNAMEA,KEY99
          APPEND    FILELIN1,KEY99
          RESET     KEY99
          EXECUTE   KEY99,TASKID
.
          OPEN      TEMP1,FNAMEA
.
OPEN9000  MOVE      ZERO,EXIT
OPEN9999  RETURN
+
.******************************************************************************
.*                                  KILL0000                                  *
.*                             Delete The Tempfile                            *
.******************************************************************************
KILL0000  CLOSE     TEMP1
.
          CLEAR     KEY99
          APPEND    "iserase ",KEY99
          APPEND    FNAMEA,KEY99
          RESET     KEY99
          EXECUTE   KEY99,TASKID
.
KILL9000  MOVE      ZERO,EXIT
KILL9999  RETURN
+
.**********************************************************************
.*                           OPTN0000                                 *
.*                   Display the main option screen                   *
.**********************************************************************
OPTN0000  MOVE       FALSE,EXIT
          DISPLAY    *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                     *P1:5,*V2LON,ONE,*HOFF:
                     " = Alert Review Date Sequence";
.
OPTN1000  KEYIN      *P1:7,*EL,"Please Select : ",*V2LON,OPTION;
          COMPARE    ZERO,OPTION
          GOTO       OPTN9000 IF EQUAL
.
          BRANCH     OPTION,OPTN9999
          BEEP
          GOTO       OPTN1000
.
OPTN9000  MOVE       TRUE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                  DATE0000              Called by: ML0000   *
.*                        Keyin The Invoice Date Range                        *
.******************************************************************************
DATE0000  MOVE      "21",CCOL
          MOVE      TEN4,CVERT
          DISPLAY   *P1:CVERT,*EF,"Starting Date     : ";
          ADD       ONE,CVERT
          DISPLAY   *P1:CVERT,"Ending Date       : ";
.
          MOVE      ONE,CCANLDTE            * Blank cancel default
          MOVE      ZERO,CHIGHLT            * Use highlight
.
. ----- Enter starting date -----
.
DATE1000  SUB       ONE,CVERT
DATE2000  MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9500
          BRANCH    CQUEST,DATE2000
.
          PACK      STRTDTE8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDTE8
.
.>>>>     MATCH     STRTDTE8,CURRDATE
.>>>>     IF        @LESS
.>>>>       DISPLAY   *P1:24,*B,*EL,"Start date must not be after the current ":
.>>>>                 "date.  ";
.>>>>       CALL      HITENTER
.>>>>       GOTO      DATE2000
.>>>>     ENDIF
.
. ----- Enter ending date -----
.
          ADD       ONE,CVERT
DATE3000  UNPACK    STRTDTE8,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE1000
          BRANCH    CQUEST,DATE3000
.
          PACK      ENDDTE8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDTE8
.
.>>>>     MATCH     ENDDTE8,CURRDATE
.>>>>     IF        @LESS
.>>>>       DISPLAY   *P1:24,*B,*EL,"End date must not be after the current ":
.>>>>                 "date.  ";
.>>>>       CALL      HITENTER
.>>>>       GOTO      DATE3000
.>>>>     ENDIF
.
.>>>>     MATCH     STRTDTE8,ENDDTE8
.>>>>     IF        @LESS
.>>>>       DISPLAY   *P1:24,*B,*EL,"End date must be after the start date.  ";
.>>>>       CALL      HITENTER
.>>>>       GOTO      DATE3000
.>>>>     ENDIF
.
.>>>>     MOVE      STRTDTE8,KEY4
.>>>>     MATCH     KEY4,ENDDTE8
.>>>>     IF        !@EQUAL
.>>>>       DISPLAY   *P1:24,*B,*EL,"Both dates must be from the same ":
.>>>>                 "financial year.  ";
.>>>>       CALL    HITENTER
.>>>>       GOTO    DATE0000
.>>>>     ENDIF
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.******************************************************************************
.*                                  KUSR0000                                  *
.*                     Keyin user id                                          *
.******************************************************************************
KUSR0000  KEYIN     *P1:18,*EL,"User ID           : ",*V2LON,USERID
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.******************************************************************************
.*                                 ALRT0000                                   *
.*                          Select Alert Type                                 *
.******************************************************************************
ALRT0000  MOVE      SP6,ALRTTYPE
          KEYIN     *P1:9,*EL,"Alert Type        : ",*V2LON,ALRTTYPE
          ENDSET    ALRTTYPE
          APPEND    SP70,ALRTTYPE
          RESET     ALRTTYPE
.
          MATCH     SP70,ALRTTYPE
          IF        @EQUAL
            DISPLAY   *P21:9,*EL,"All  ";
          ENDIF
.
ALRT9999  RETURN
+
.******************************************************************************
.*                                  LOAD0000                                  *
.*                              Load The Tempfile                             *
.******************************************************************************
LOAD0000  MOVE      ZERO,REDCT
          MOVE      ZERO,WRTCT
          DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Loading : ";
.
          MOVE      SP1,ALERTSEC
          MOVE      USERID,KEY10
          CALL      RDWBSE1                  * get View Alert Security Level
          BRANCH    OVRCD,LOAD9000
.
          PACK      KEY16,SP70
          CALL      RSPTALR1                * Position on & read an alert
LOAD1000  CALL      RKPTALR1                  file record
          BRANCH    OVRCD,LOAD9000
.
          ADD       ONE,REDCT
          IF        REDCT%20=1
            DISPLAY   *P12:24,*V2LON,PTALURNO,SP2,PTALCATG;
          ENDIF
.
          MATCH     SP70,ALRTTYPE
          IF        !@EQUAL
            MATCH     PTALCATG,ALRTTYPE          * same alert type?
            GOTO      LOAD1000 IF NOT EQUAL      * no
          ENDIF
.
          MATCH     SP70,STRTDTE8
          IF        !@EQUAL
            MATCH     STRTDTE8,PTALRDTE          * within selected date range?
            GOTO      LOAD1000 IF LESS           * no
          ENDIF
.
          MATCH     SP70,ENDDTE8
          IF        !@EQUAL
            MATCH     PTALRDTE,ENDDTE8           * within selected date range?
            GOTO      LOAD1000 IF LESS           * no
          ENDIF
.
.         check if category based alert security is being used
.
LOAD2000  PACK      KEY5,PTALCATG,PTALCODE   * Key is cat and code
          CALL      RDCODE1                  * Read patient codes file
          BRANCH    OVRCD,LOAD1000           * no record, get next alert
.         
          UNPACK    PTALCATG,KEY1,DIM1
          MOVE      DIM1,AFRM1               * save alert category
.
          MOVE      SP1,ALERTSEC
          LOAD      ALERTSEC,AFRM1,WBSEVH1L,WBSEVH2L,WBSEVH3L,WBSEVH4L,WBSEVH5L:
                                   WBSEVH6L,WBSEVH7L,WBSEVH8L,WBSEVH9L
.
          MATCH     ALERTSEC,SP1
          IF        @EQUAL
            MOVE      WBSEVWSL,ALERTSEC      * default to standard security
          ENDIF
.
LOAD3000  MATCH     ALERTSEC,TCINDC2         * Compare Sec Level to Indicator 2
          GOTO      LOAD4000 IF EQUAL
          GOTO      LOAD4000 IF LESS
          GOTO      LOAD1000
.
LOAD4000  PACK      KEY16,PTALURNO,PTALCATG,PTALCODE,PTALCNTR
          CALL      RATEMP1                 * Position read a tempfile record
          COMPARE   ONE,OVRCD
          GOTO      LOAD1000 IF NOT EQUAL   * Read already exists
.
          MOVE      PTALURNO,TEMPURNO
          MOVE      PTALCATG,TEMPCATG
          MOVE      PTALCODE,TEMPCODE
          MOVE      PTALCNTR,TEMPCNTR
          MOVE      PTALDATE,TEMPDATE
          MOVE      PTALLSEV,TEMPLSEV
          MOVE      PTALRDTE,TEMPRDTE
          MOVE      PTALEDAT,TEMPEDAT
          MOVE      PTALRQBY,TEMPRQBY
.
          CALL      WRTEMP1                 * Write a tempfile record
.
          ADD       ONE,WRTCT
          IF        WRTCT%20=1
            DISPLAY   *P45:24,*V2LON,PTALURNO,SP2,PTALCATG;
          ENDIF
          GOTO      LOAD1000
.
LOAD9000  MOVE      ZERO,EXIT
LOAD9999  RETURN
+
.**********************************************************************
.*                             PRIN0000                               *
.*                Read tempfile and print details                     *
.**********************************************************************
PRIN0000  MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          MOVE      "60",CLNO
          MOVE      ZERO,RECCOUNT
          CALL      HEAD0000
.
          DISPLAY   *P1:24,"Alert for U/R Number        : ";
.
          PACK      KEY16,SP70
          CALL      RSTEMP1                       * position to start of file
PRIN1000  CALL      RKTEMP1  
          BRANCH    OVRCD,PRIN8000
.
          DISPLAY   *P35:24,*EL,*V2LON,TEMPURNO;
.
          CALL      PREC0000                       * print a Record  
          GOTO      PRIN1000
.
PRIN8000  CALL      UND132
          PRINT     *N,"Total Records: ",RECCOUNT
          PRINT     *N,"*** END OF REPORT  ***";
          GOTO      PRIN9999
.
PRIN9999  RETURN
+
.**********************************************************************
.*                                HEAD0000                            *
.*                          Print page header                         *
.**********************************************************************
HEAD0000  CALL      HEAD132
.
          PRINT     *33,"Alert Review Date From : ";
          MATCH     SP8,STRTDTE8
          IF        !@EQUAL
            UNPACK    STRTDTE8,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            REP       " 0",CPCDATE
            PRINT     *58,CPCDATE;
          ELSE
            PRINT     *58,"START";
          ENDIF
.
          PRINT     *68," to ";
.
          MATCH     "ZZZZZZ",ENDDTE8
          IF        !@EQUAL
            UNPACK    ENDDTE8,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            REP       " 0",CPCDATE
            PRINT     *72,CPCDATE,*N;
          ELSE
            PRINT     *72,"END",*N;
          ENDIF
.
          PRINT     *40,"Alert Type      : ";
          MATCH     SP8,ALRTTYPE
          IF        !@EQUAL
            PACK      KEY5,ALRTTYPE,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              PRINT     *58,ALRTTYPE,*N;
            ELSE
              PRINT     *58,ALRTTYPE,SP2,TDESC,*N;
            ENDIF
          ELSE
            PRINT     *58,"All  ",*N;
          ENDIF
.
          CALL      UND132
.
. UR Number | Code | Patients Name        | Age | Sex | Indicator | Attending Doctor     | Start Date | End Date   | Review Date|Sv|
. 12345678  | 123  | 12345678901234567890 | 123 | 123 | 123456789 | 12345678901234567890 | 1234567890 | 1234567890 | 1234567890 |  |
.23456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
.
          PRINT     *1,"| UR Number ",*13,"| Code ":
                    *20,"| Patient  Name        ":
                    *43,"| Age ",*49,"| Sex ",*55,"| Indicator ":
                    *67,"| Authorising Clinician":
                    *90,"| Start Date ",*103,"| End Date   ":
                   *116,"| Review Date",*129,"|Sv",*132,"|"
.
          CALL      UND132
          ADD       THREE,CLNO
.
HEAD9999  RETURN
+
.**********************************************************************
.*                                PREC0000                            *
.*                   Routine to print a PRECord                       *
.**********************************************************************
PREC0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
.23456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
.
          MOVE      SP70,DISPDAT1
          MATCH     SP70,TEMPDATE
          IF        !@EQUAL
            UNPACK    TEMPDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            REP       " 0",CPCDATE
            MOVE      CPCDATE,DISPDAT1      * Alert Start Date
          ENDIF
.
          MOVE      SP70,DISPDAT2
          MATCH     SP70,TEMPRDTE
          IF        !@EQUAL
            UNPACK    TEMPRDTE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            REP       " 0",CPCDATE
            MOVE      CPCDATE,DISPDAT2      * Review Date
          ENDIF
.
          MOVE      SP70,DISPDAT3
          MATCH     SP70,TEMPEDAT
          IF        !@EQUAL
            UNPACK    TEMPEDAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            REP       " 0",CPCDATE
            MOVE      CPCDATE,DISPDAT3      * End Date/Date Cleared
          ENDIF
.
          MOVE      SP7,INDICATR
          PACK      KEY5,TEMPCATG,TEMPCODE,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            MATCH     ANSS,TCINDC6
            IF        @EQUAL
              MOVE      "Staff",INDICATR    * risk alert indicator (Staff)
            ENDIF
            MATCH     ANSP,TCINDC6
            IF        @EQUAL
              MOVE      "Patient",INDICATR  * risk alert indicator (Patient)
            ENDIF
          ENDIF
.
          PACK      KEY8,TEMPURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PREC9999
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME        * Patients name
.
          PACK      KEY10,TEMPRQBY,SP70
          CALL      GHCP0000                * get formatted hcp name
.
          PACK     AGEDATE,CURRDATE
          CALL     CALCAGE                  * Calculate age
.
          PRINT     *1,"| ",TEMPURNO,*13,"| ",TEMPCODE,*20,"| ",PATNAME:
                    *43,"| ",PSAGEYRS,*49,"|  ",PSEX,*55,"|  ",INDICATR:
                    *67,"| ",HCPFNAM,*90,"| ",DISPDAT1,*103,"| ",DISPDAT3:
                    *116,"| ",DISPDAT2,*129,"| ",TEMPLSEV,*132,"|"
.
          ADD       ONE,RECCOUNT
          ADD       ONE,CLNO
PREC9999  RETURN
+
.****************************************************************************
.*                                GHCP0000                                  *
.*                         format the hcp's name                            *
.* Paramaters : KEY10   (HCP Code)                                          *
.* Returns    : HCPFNAM (HCP name formatted)                                *
.****************************************************************************
GHCP0000  CLEAR     HCPFNAM
          MOVE      "Blank / Unknown ",HCPFNAM
          CALL      RDPMHCP1
          BRANCH    OVRCD,GHCP9999
.
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,HCPFNAM
.
GHCP9999  RETURN
+
.******************************************************************************
.*                       I/O Routines For The Tempfile                        *
.******************************************************************************
RATEMP1   MOVE      ZERO,OVRCD
          RESET     KEY16
          READ      TEMP1,KEY16;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY16
          READ      TEMP1,KEY16;;
          RETURN    
.         
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMP1;TEMPURNO,TEMPCATG,TEMPCODE,TEMPCNTR,TEMPDATE:
                          TEMPLSEV,TEMPRDTE,TEMPEDAT,TEMPRQBY
          GOTO      OVERCOND IF OVER
          RETURN    
.
RPTEMP1   MOVE      ZERO,OVRCD
          READKP    TEMP1;TEMPURNO,TEMPCATG,TEMPCODE,TEMPCNTR,TEMPDATE:
                          TEMPLSEV,TEMPRDTE,TEMPEDAT,TEMPRQBY
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY16
          READ      TEMP1,KEY16;TEMPURNO,TEMPCATG,TEMPCODE,TEMPCNTR,TEMPDATE:
                                TEMPLSEV,TEMPRDTE,TEMPEDAT,TEMPRQBY
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   UPDATE    TEMP1;TEMPURNO,TEMPCATG,TEMPCODE,TEMPCNTR,TEMPDATE:
                          TEMPLSEV,TEMPRDTE,TEMPEDAT,TEMPRQBY
          RETURN
.
WRTEMP1   RESET     KEY16
          WRITE     TEMP1,KEY16;TEMPURNO,TEMPCATG,TEMPCODE,TEMPCNTR,TEMPDATE:
                                TEMPLSEV,TEMPRDTE,TEMPEDAT,TEMPRQBY
          RETURN
+
.
          INC       STD001IO
          INC       CALCAGE
          INC       TFILENAM                     * Generate Temp File Name
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATALRIO/INC
          INC       PATCODIO/INC  
          INC       PATMA1IO/INC  
          INC       PMSHCPIO/INC  
          INC       WEBSECIO/INC  
          INC       WEBERRIO/INC                 * Web Server Error Logging

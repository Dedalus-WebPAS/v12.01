.**********************************************************************
.* System   : Operating Room System                                   *
.* Program  : IBAOPR25                                                *
.* Desc     : Nurse File Enquiry                                      *
.**********************************************************************
.* Date     :  08/12/89                                               *
.* Author   :  K.Peak                                                 *
.* Mods.    :  20/03/90  Bill Dew
.              Fix the user defines not to display category code headers
.              Fix a ? routine for nurse codes.  A job well done
.*        V5.00     18/12/1990    Justin Coates                       *
.*                  Converted to Version 5                            *
.*        V5.01.01  21/01/91      ROD                                 *
.*                  Converted to Release 5.01                         *
.*        V5.01.02  05/03/91 J.Tan        SRF 108015                  *
.*                  Changes to OPRNURDS                               *
.*        V5.01.03  19/08/92 J.Tan     SRF 116653                     *
.*                  Recompiled for OPRNURDS                           *
.**********************************************************************
.
.$$$$$
.    Operating Room System Nurse File Enquiry (IBAOPR25 )
.    ----------------------------------------------------
.
.    - Initialisation
.
.    - Display Header
.            - Open Files
.               OPRNURA1
.               PATCODE1
.               CONTROLF
.
.    - Clear Input Variables
.
.    - Display Screen Format
.
.    - Input Nurse Code
.            - Read OPRNURA1, check if exists
.
.    - Display Data
.            - Surname
.            - Given Name(s)
.            - Status 
.            - Read CONTROLF sector 40 position *213 onwards
.                   - If user defined fields switched on accept fields....
.                             - User Defined 1.
.                             - User Defined 2.
.                             - User Defined 3.
.                             - User Defined 4.
.                             - User Defined 5.
.
.    - END
.$$$$$
.
          INC       STD001FD
          INC       OPRNURFD/INC           * Nurse File
          INC       OPRCONFD/INC           * Operating Room Control file
          INC       PATCODFD/INC           * patient codes file
+
.**********************************************************************
.*                           CONSTANTS                                *
.**********************************************************************
.
CODEQ1    INIT      "Q1"
CODEQ2    INIT      "Q2"
CODEQ3    INIT      "Q3"
CODEQ4    INIT      "Q4"
CODEQ5    INIT      "Q5"
.
STATUS    DIM       10
STATUS1   INIT      "Active    "
STATUS2   INIT      "Non Active"
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
CODE      DIM       5
.
.
LNO       FORM      2
.
NDESC1    DIM       20              * user define code description
NDESC2    DIM       20
NDESC3    DIM       20
NDESC4    DIM       20
NDESC5    DIM       20
.
PAGENO    FORM      3
.
STATIND   DIM       1               * status
SCRNFLAG  FORM      1
.
USERDES1  DIM       20              * user define category description
USERDES2  DIM       20
USERDES3  DIM       20
USERDES4  DIM       20
USERDES5  DIM       20
UVERT1    FORM      2
.
VAR       FORM      2
.
PRGID     INIT      "IBAOPR25"
PRGNAM    INIT      "Nurse Enquiry"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
.
ML0000    CALL      INIT0000               * initialization and open files
.
ML0100    CALL      CLR0000                * clear input variables
          CALL      SCR0000                * display screen format 
.
          CALL      GNUR0000           * get nurse code
          BRANCH    EXIT,ML9999
.
          CALL      DISP0000               * display details
          CALL      USER0000               * display user define details
          CALL      SFLD0000               * select choice of exit
          BRANCH    EXIT,ML9999,ML0100
          CALL      PRNT0000               * print enquiry
          GOTO      ML0100
.
ML9999    CHAIN     PGM                    * chain back to program
+
.**********************************************************************
.*                             INIT0000                               *
.*                      Initialization  Routine                       *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"oprnuraf";
          OPEN      OPRNURA1,"oprnuraf"
.
          DISPLAY   *P64:24,"oprnuraf";
          OPEN      OPRNURA2,"oprnuraf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          READ      CONTROLF,FORTY;*213,OPCNUNU1,OPCNUNM1,OPCNUNU2,OPCNUNM2:
                                        OPCNUNU3,OPCNUNM3,OPCNUNU4,OPCNUNM4:
                                        OPCNUNU5,OPCNUNM5
INIT9999  RETURN
+
.**********************************************************************
.*                             CLR0000                                *
.*                 Routine to Clear Local Variables                   *
.**********************************************************************
.
CLR0000   MOVE      ZERO,PAGENO
.
. ----- Clear the Nurse File Variables -----
.
          MOVE      SP6,OPNRCODE
          PACK      OPNRSNAM,SP20,SP20
          PACK      OPNRGNAM,SP20,SP20
          UNPACK    SP30,OPNRUSR1,OPNRUSR2,OPNRUSR3,OPNRUSR4,OPNRUSR5
.
CLR9999   RETURN
+
.**********************************************************************
.*                            SCRN0000                                *
.*                     Main Data Screen Display                       *
.**********************************************************************
.
SCR0000   DISPLAY   *P1:3,*EF:
                    *P1:4,"Nurse Code               : ":
                    *P1:6,"Surname                  : ":
                    *P1:7,"Given Name(s)            : ":
                    *P1:8,"Status                   : ";
.
.------ process screen for user defined fields -----
.
          MOVE      "9",CVERT
          COMPARE   ONE,OPCNUNU1
          GOTO      SCR1000 IF NOT EQUAL
          PACK      KEY5,CODEQ1,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,SCR1000
          DISPLAY   *P1:CVERT,TDESC,*P25:CVERT," : ";
          ADD       ONE,CVERT
.
.------ display user defined field 2, if valid -----
.
SCR1000   COMPARE   ONE,OPCNUNU2
          GOTO      SCR1100 IF NOT EQUAL
          PACK      KEY5,CODEQ2,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,SCR1100
          DISPLAY   *P1:CVERT,TDESC,*P25:CVERT," : ";
          ADD       ONE,CVERT
.
.------ display user defined field 3, if valid ------
.
SCR1100   COMPARE   ONE,OPCNUNU3
          GOTO      SCR1200 IF NOT EQUAL
          PACK      KEY5,CODEQ3,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,SCR1200
          DISPLAY   *P1:CVERT,TDESC,*P25:CVERT," : ";
          ADD       ONE,CVERT
.
.------- display user defined field 4, if vaild -----
.
SCR1200   COMPARE   ONE,OPCNUNU4
          GOTO      SCR1300 IF NOT EQUAL
          PACK      KEY5,CODEQ4,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,SCR1300
          DISPLAY   *P1:CVERT,TDESC,*P25:CVERT," : ";
          ADD       ONE,CVERT
.
.------- display user defined field 5, if valid ------
.
SCR1300   COMPARE   ONE,OPCNUNU5
          GOTO      SCR9999 IF NOT EQUAL
          PACK      KEY5,CODEQ5,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,SCR9999
          DISPLAY   *P1:CVERT,TDESC,*P25:CVERT," : ";
.
SCR9999   RETURN
.*******************************************************************************
.                                 GNUR0000
.    Get nurse code : This routine was brought to you by Bill Dew
.*******************************************************************************
GNUR0000
          MOVE      ONE,SCRNFLAG
          DISPLAY   *P28:4,*EL,UNDLN6,*P28:4;
GNUR1111
          KEYIN     *V2LON,OPNRCODE
          ENDSET    OPNRCODE
          APPEND    SP6,OPNRCODE
          RESET     OPNRCODE
.
          MATCH     SP6,OPNRCODE
          GOTO      GNUR9990 IF EQUAL
.
          MATCH     QUEST,OPNRCODE
          GOTO      GNUR6666 IF NOT EQUAL
          MOVE      ONE,CNEWDS
          CALL      OPRNURDS                * nurse "?" option
          MOVE      ZERO,SCRNFLAG
GNUR4444
          DISPLAY   *P1:24,*EL,"Nurse Code : ",UNDLN6,*P14:24;
          GOTO      GNUR1111
GNUR6666  
          MOVE      OPNRCODE,KEY6
          CALL      RDOPNUR1           * validate the nurscode
          BRANCH    OVRCD,GNUR8000
          MOVE      OPNRSTAT,STATIND
          BRANCH    SCRNFLAG,GNUR7777
          CALL      SCR0000
GNUR7777
          DISPLAY   *P28:4,*V2LON,OPNRCODE
          MOVE      ZERO,EXIT          * nurse code valid lets go
          GOTO      GNUR9999
GNUR8000
          DISPLAY   *P1:24,*EL,*B,"Nurse code not on file - ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,GNUR0000
          GOTO      GNUR4444
.
GNUR9990  MOVE      ONE,EXIT           * return hit on nurse code
GNUR9999  RETURN
+
.*******************************************************************************
.*                               DISP0000                                 *
.*                   Display Data from the Nurse File                     *
.**************************************************************************
.
DISP0000  MOVE      SP30,TDESC
.
          DISPLAY   *P28:6,*V2LON,OPNRSNAM:
                    *P28:7,OPNRGNAM;
.
          MOVE      STATUS1,STATUS
          LOAD      STATUS,OPNRSTAT,STATUS2        * status
.
          DISPLAY   *P28:8,*V2LON,STATIND,*HOFF:
                    *P34:8,STATUS;
          RETURN
+
.**************************************************************************
.*                         USER0000                                       *
.*                     process user defined fields                        *
.**************************************************************************
.
USER0000  MOVE      "9",UVERT1
          COMPARE   ONE,OPCNUNU1
          GOTO      USER1000 IF NOT EQUAL
.
          MOVE      SP20,USERDES1
          MATCH     SP3,OPNRUSR1
          GOTO      USER1000 IF EQUAL
          DISPLAY   *P28:UVERT1,*V2LON,OPNRUSR1,*HOFF;
          PACK      KEY5,CODEQ1,OPNRUSR1
          CALL      RDCODE1
          BRANCH    OVRCD,USER0700
          MOVE      TDESC,USERDES1
.
USER0700  DISPLAY   *P34:UVERT1,USERDES1;
          ADD       ONE,UVERT1
.
.------- process user defined field 2. ----
.
USER1000  COMPARE   ONE,OPCNUNU2
          GOTO      USER2000 IF NOT EQUAL
.
          MOVE      SP20,USERDES2
          MATCH     SP3,OPNRUSR2
          GOTO      USER2000 IF EQUAL
          DISPLAY   *P28:UVERT1,*V2LON,OPNRUSR2,*HOFF;
.
          PACK      KEY5,CODEQ2,OPNRUSR2
          CALL      RDCODE1
          BRANCH    OVRCD,USER1700
          MOVE      TDESC,USERDES2
.
USER1700  DISPLAY   *P34:UVERT1,USERDES2;
          ADD       ONE,UVERT1
.
.------ process user defined field 3 ----
.
USER2000  COMPARE   ONE,OPCNUNU3
          GOTO      USER3000 IF NOT EQUAL
.
          MOVE      SP20,USERDES3
          MATCH     SP3,OPNRUSR3
          GOTO      USER3000 IF EQUAL
          DISPLAY   *P28:UVERT1,*V2LON,OPNRUSR3,*HOFF;
.
          PACK      KEY5,CODEQ3,OPNRUSR3
          CALL      RDCODE1
          BRANCH    OVRCD,USER2700
          MOVE      TDESC,USERDES3
.
USER2700  DISPLAY   *P34:UVERT1,USERDES3;
          ADD       ONE,UVERT1
.
.------- process user defined field 4. -----
.
USER3000  COMPARE   ONE,OPCNUNU4
          GOTO      USER4000 IF NOT EQUAL
.
          MOVE      SP20,USERDES4
          MATCH     SP3,OPNRUSR4
          GOTO      USER4000 IF EQUAL
          DISPLAY   *P28:UVERT1,*V2LON,OPNRUSR4,*HOFF;
.
          PACK      KEY5,CODEQ4,OPNRUSR4
          CALL      RDCODE1
          BRANCH    OVRCD,USER3700
          MOVE      TDESC,USERDES4
.
USER3700  DISPLAY   *P34:UVERT1,USERDES4;
          ADD       ONE,UVERT1
.
.-------- process user defined field 5 ------
.
USER4000  COMPARE   ONE,OPCNUNU5
          GOTO      USER4900 IF NOT EQUAL
.
          MOVE      SP20,USERDES5
          MATCH     SP3,OPNRUSR5
          GOTO      USER4900 IF EQUAL
          DISPLAY   *P28:UVERT1,*V2LON,OPNRUSR5,*HOFF;
.
          PACK      KEY5,CODEQ5,OPNRUSR5
          CALL      RDCODE1
          BRANCH    OVRCD,USER4700
          MOVE      TDESC,USERDES5
.
USER4700  DISPLAY   *P34:UVERT1,USERDES5;
USER4900  RETURN
+
.**************************************************************************
.*                         SFLD0000                                       *
.*                     Select choice of exit                              *
.**************************************************************************
.
SFLD0000  MOVE      FALSE,EXIT
.
          KEYIN     *P1:24,*EL," (",*V2LON,"P",*HOFF,")rint, (":
                               *V2LON,"E",*HOFF,")xit, (":
                               *V2LON,"N",*HOFF,")ext ? ",*V2LON,ANS;
.
          CMATCH    "P",ANS
          GOTO      SFLD9999 IF EQUAL
.
          CMATCH    "E",ANS
          GOTO      SFLD9000 IF EQUAL
.
          CMATCH    "N",ANS
          GOTO      SFLD9500 IF EQUAL
          BEEP  
          GOTO      SFLD0000
.
SFLD9000  MOVE      TRUE,EXIT
          GOTO      SFLD9999
.
SFLD9500  MOVE      TWO,EXIT
SFLD9999  RETURN
+
.**************************************************************************
.*                         PRNT0000                                       *
.*                     Print Enquiry                                      *
.**************************************************************************
.
PRNT0000  DISPLAY   *P1:24,*EL,*P40:24,*V2LON:
                    "** Printing Enquiry **"
          CALL      HEAD0000
.
          MOVE      STATUS1,STATUS
          LOAD      STATUS,OPNRSTAT,STATUS2        * status
.
          PRINT     *N,*N,*1,"Nurse Code           : ",OPNRCODE,*N 
.
          PRINT     *N,*1,"Surname              : ",OPNRSNAM,*N:
                       *1,"Given Name(s)        : ",OPNRGNAM,*N:
                       *1,"Status               : ",OPNRSTAT,SP4,STATUS,*N;
.
.------- print user defined field 1, if valid ------
.
          COMPARE   ONE,OPCNUNU1
          GOTO      PRNT1200 IF NOT EQUAL
.
          MOVE      SP20,USERDES1
          PACK      KEY5,CODEQ1,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT0500
          MOVE      TDESC,USERDES1
.
PRNT0500  PRINT     *N,USERDES1," : ";
.
          MOVE      SP20,NDESC1
          MATCH     SP3,OPNRUSR1
          GOTO      PRNT1200 IF EQUAL
          PACK      KEY5,CODEQ1,OPNRUSR1
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT1000
          MOVE      TDESC,NDESC1
.
PRNT1000  PRINT     *24,OPNRUSR1,SP2,NDESC1;
.
.------- print user defined field 2, if valid ------
.
PRNT1200  COMPARE   ONE,OPCNUNU2
          GOTO      PRNT2200 IF NOT EQUAL
.
          MOVE      SP20,USERDES2
          PACK      KEY5,CODEQ2,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT1500
          MOVE      TDESC,USERDES2
.
PRNT1500  PRINT     *N,USERDES2," : ";
.
          MOVE      SP20,NDESC2
          MATCH     SP3,OPNRUSR2
          GOTO      PRNT2200 IF EQUAL
          PACK      KEY5,CODEQ2,OPNRUSR2
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT2000
          MOVE      TDESC,NDESC2
.
PRNT2000  PRINT     *24,OPNRUSR2,SP2,NDESC2;
.
.------- print user defined field 3, if valid ------
.
PRNT2200  COMPARE   ONE,OPCNUNU3
          GOTO      PRNT3200 IF NOT EQUAL
.
          MOVE      SP20,USERDES3
          PACK      KEY5,CODEQ3,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT2500
          MOVE      TDESC,USERDES3
.
PRNT2500  PRINT     *N,USERDES3," : ";
.
          MOVE      SP20,NDESC3
          MATCH     SP3,OPNRUSR3
          GOTO      PRNT3200 IF EQUAL
          PACK      KEY5,CODEQ3,OPNRUSR3
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT3000
          MOVE      TDESC,NDESC3
.
PRNT3000  PRINT     *24,OPNRUSR3,SP2,NDESC3;
.
.------- print user defined field 4, if valid ------
.
PRNT3200  COMPARE   ONE,OPCNUNU4
          GOTO      PRNT4200 IF NOT EQUAL
.
          MOVE      SP20,USERDES4
          PACK      KEY5,CODEQ4,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT3500
          MOVE      TDESC,USERDES4
.
PRNT3500  PRINT     *N,USERDES4," : ";
.
          MOVE      SP20,NDESC4
          MATCH     SP3,OPNRUSR4
          GOTO      PRNT4200 IF EQUAL
          PACK      KEY5,CODEQ4,OPNRUSR4
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT4000
          MOVE      TDESC,NDESC4
.
PRNT4000  PRINT     *24,OPNRUSR4,SP2,NDESC4;
.
.------- print user defined field 5, if valid ------
.
PRNT4200  COMPARE   ONE,OPCNUNU5
          GOTO      PRNT5200 IF NOT EQUAL
.
          MOVE      SP20,USERDES5
          PACK      KEY5,CODEQ5,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT4500
          MOVE      TDESC,USERDES5
.
PRNT4500  PRINT     *N,USERDES5," : ";
.
          MOVE      SP20,NDESC5
          MATCH     SP3,OPNRUSR5
          GOTO      PRNT5200 IF EQUAL
          PACK      KEY5,CODEQ5,OPNRUSR5
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT5000
          MOVE      TDESC,NDESC5
.
PRNT5000  PRINT     *24,OPNRUSR5,SP2,NDESC5;
.
PRNT5200  PRINT     *N,*N,"** END OF ENQUIRY **"
          DISPLAY   *P1:24,*EL,*P40:24,*V2LON:
                    "** Report Generation Completed **"
          RETURN
+
.**************************************************************************
.*                         HEAD0000                                       *
.*                     Print Heading                                      *
.**************************************************************************
.
HEAD0000  CLOCK     TIME,CTIMEIS
.
          ADD       ONE,PAGENO
.
          PRINT     *F,*N,*N,PRGID,*22,CNAME,*64,"DATE - ",CDATE:
                    *N,VERSION,*22,PRGNAM,*64,"TIME - ",CTIMEIS:
                    *N,*64,"PAGE - ",SP5,PAGENO,*N
          ADD       FIVE,LNO
          RETURN
+
.********************************************************************** 
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
         INC       STD001IO
         INC       PATCODKY                * patient codes file routine
         INC       PATCODIO/INC            * patient i/o for codes
         INC       OPRNURIO/INC            * nurse i/o
         INC       OPRNURDS
.**********************************************************************

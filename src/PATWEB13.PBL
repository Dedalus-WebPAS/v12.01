.*******************************************************************************
. Program       : PATWEB13
.
. Function      : Hospital HCP AN-SNAP server
.                 handles Impairment Code Maintenance and Key Word Search
.                         Classification Code Maintenance and Key Word Search
.                         Mini Mental State Examination
.                         Modified Elderly Mobility Scale  
.                         Motor Assessment Scale
.                         Lawton's Assessment of Daily Living
.                         Auto Classification Code Maintenance
.
. Modifications :
.-------------------------------------------------------------------------------
. V12.01.01 29/01/2026  Thanh T        TSK 0930214
.           Move SP70 to OBAUORNO, OBAUOVIS and OBAUOCOR fields
. V11.05.01 04/03/2025  Thanh T        TSK 0956940
.           Recompiled for OBSAUDFD changes
.-------------------------------------------------------------------------------
. V11.04.01 30/05/2024  Sunny          TSK 0943764
.           Fixed CCSHKC00
. V11.02.02 23/09/2022  Bella Turco    TSK 0921957
.           Added NOMORE00 - Display "No more records" at end of list
.           Added PTCNNRTD - Displays number of rows set by parameter value
. V11.02.01 10/02/2022  Thanh T        TSK 0905641
.           Recompiled as OUTHEDFD/OUTMA1FD changes
. V11.00.01 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
.-------------------------------------------------------------------------------
. V10.13.01 05/12/2018  Don Nguyen     TSK 0838558
.           Deleted temp file (TEMPFILE) after processing (NACD0000).
.           Deleted temp file (VSACTFIL) after processing (MOTA0000).
.           Moved temp file (TEMPFILE) creation into CRCLDT00 and only create
.           when needed instead of at server startup.
.-------------------------------------------------------------------------------
. V10.12.02 24/04/2018  Peter Vela     TSK 0856151
.           Recompiled for PMSSNCTM
. V10.12.01 23/04/2018  Tracey Nguyen  TSK 0841644
.           1)Added reportno 09 for Impairment Group Maintenance 
.           2)Added IGTAB000 tag to display Impairment Group List
.           3)Added PIMG0000 tag to output Impairment Group tags
.           4)Added PMIMGCDE in ICROW900
.           5)Added IGSEL000 tag for Impairment Group selection list
.-------------------------------------------------------------------------------
. V10.11.03 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.02 30/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
. V10.11.01 14/07/2017  Thanh T.       TSK 0821710
.           Audit Amission/outpatient visit comments. PATMISTM changed.
.-------------------------------------------------------------------------------
. V10.08.01 29/03/2016  Peter Vela      CAR 0813961
.                       Recompiled for PMSSNPTM
. V10.07.01 29/10/2015  Ebon Clements   CAR 268970
.                       Removed open of OUTCLIA2
. V10.06.04 10/06/2015  Patrick Adair    CAR 290369
.                       Recompiled for changes to PMSSNPTD and PMSSNPTM
. V10.06.03 20/04/2015  Steve Armstrong  CAR 290369
.                       Recompiled for changes to PMSSNPFD
. V10.06.02 24/03/2015  Don Nguyen       CAR 314369
.                       Added clear for MRGEFLAG in CLRPAR00
. V10.06.01 12/03/2015  Patrick Adair    CAR 308415
.                       Recompiled for changes to PMSSNCFD/IO/TM/TD
.-------------------------------------------------------------------------------
. V10.05.02 14/10/2014  Peter Vela                                    CAR 301518
.                       Added Clinical Documents audit functionality OBSAUDFD
. V10.05.01 21/07/2014  Patrick Adair                                 CAR 277093
.                       Added Auto Classification Code Maintenance
.-------------------------------------------------------------------------------
. V10.04.02 07/04/2014  Peter Vela      CAR 286258
.           Recompiled for PATMASTM
. V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.-------------------------------------------------------------------------------
. V10.03.02 30/08/2013  Don Nguyen        CAR 273426
.                       Recompiled for changes to CLALLREF
. V10.03.01 06/03/2013  Patrick Adair                                 CAR 281971
.                       Changed SETPAR00 for CLASSCDE
. V10.03.00 05/03/2012  Patrick Adair                                 CAR 257776
.                       Created Web Server
.*******************************************************************************
          INC       IBAWEBDF
          INC       WEBDATDF
          INC       VISACMDF                     * Visit Assessment Comments
.
          INC       PMSEMSFD/INC	         * Modified Elderly Mob Scale
          INC       PMSEMSTD/INC	         * Mod. Elderly Mob. Scale CGIA
.08641644-start
          INC       PMSGKIFD/INC                 * Impairment Group Keywords
          INC       PMSIGRFD/INC                 * Impairment Group
          INC       PMSIGRTD/INC                 * Impairment Group CGI
.08641644-end
          INC       PMSIKIFD/INC                 * Impairment Code Keywords
          INC       PMSIMPFD/INC                 * Impairment Code
          INC       PMSIMPTD/INC                 * Impairment Code CGI
          INC       PMSLADFD/INC                 * Lawton's Assess of Dly Living
          INC       PMSLADTD/INC                 * Lawtons's ADL CGI
          INC       PMSMMSFD/INC	         * Mini Mental State Exam
          INC       PMSMMSTD/INC	         * Mini Mental State Exam CGI
          INC       PMSMOTFD/INC	         * Motor Assessment Scale
          INC       PMSMOTTD/INC	         * Motor Assessment Scale CGI
          INC       PMSSKIFD/INC	         * Classification Code Keywords
          INC       PMSSNPFD/INC                 * Classification Code
          INC       PMSSNPTD/INC                 * Classification Code CGI
          INC       PMSSNCFD/INC                 * Auto Classification Code
          INC       PMSSNCTD/INC                 * Auto Classification Code CGI
          INC       VISACMFD/INC                 * Visit Assessment Comments
.
          INC       ALLCONFD/INC
          INC       ALLREFFD/INC                 * Referrals File
          INC       APSMASFD/INC
          INC       EMRVISFD/INC                 * Emergency Visit File
          INC       MRTMASFD/INC
          INC       NHIETHFD/INC
          INC       NHIMASFD/INC
          INC       OBSAUDFD/INC
          INC       OBSPCOFD/INC                 * Patient Correspondence
          INC       OBSPCTFD/INC                 * Correspondence Storage Location
          INC       OUTBOAFD/INC                 * Outpatients Bookings
          INC       OUTBOCFD/INC                 * Outpatients Visits
          INC       OUTCLIFD/INC                 * Outpatients Clinic
          INC       OUTCTYFD/INC                 * Outpatients Clinic Type
          INC       OUTHEDFD/INC                 * Outpatients
          INC       OUTMA1FD/INC                 * Outpatients Master File
          INC       OUTSESFD/INC                 * Outpatients Sessions
          INC       OUTSITFD/INC                 * Outpatients Site File
          INC       PATALRFD/INC
          INC       PATASFFD/INC
          INC       PATCALAG/INC
          INC       PATCMCFD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATCONFD/INC
          INC       PATDADFD/INC
          INC       PATDGWFD/INC
          INC       PATDO1FD/INC                 * Doctor File
          INC       PATDSCFD/INC
          INC       PATFN1FD/INC
          INC       PATHSPFD/INC                 * Hospital file
          INC       PATIN1FD/INC
          INC       PATITMFD/INC
          INC       PATMA1FD/INC                 * Patient Master File
          INC       PATMI1FD/INC                 * Patient Admission File
          INC       PATPR1FD/INC
          INC       PATRE1FD/INC
          INC       PATVADFD/INC
          INC       PATVISFD/INC                 * Patient Visit File
          INC       PATWR1FD/INC
.
          INC       PMSCCDFD/INC   * Concession Card Details
          INC       PMSHCGFD/INC
          INC       PMSHCGTD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCPTD/INC
          INC       PMSPX2FD/INC
          INC       PMSNUTFD/INC
          INC       PMSNUTTD/INC
          INC       PMSPX2TD/INC
          INC       PMSVX1FD/INC                 * Visit Extension File
          INC       VISCMTFD/INC                 * Visit Comments File
.
.------------------------------------------------------------------------------
. Variables
.------------------------------------------------------------------------------
BLNKFLAG  FORM      1                * prevent too many blank lines in comments
CCCARRAY  DIM       7[50]                        * Class codes search results
CCCAREND  FORM      3                            * Num Class Codes in CCCARRAY
CFILEPRE  DIM       3                            * Site prefix
CLAMDESC  DIM       20                           * Insurance type
CLASSCDE  DIM       3                            * Class Code Remote Script Key
CLASSVRS  DIM       2                            * Class Vers Remote Script Key
CLCDFRM3  FORM      3                            * Right Justify Class Code
CLINDOCM  DIM       20                           * Clinical Doc File name
CMDLINE   DIM       127                          * Command line instruction
CORRESID  FORM      4                            * Clinical Doc Id
CURRDATE  DIM       8                            * Current Date
DATEFROM  FORM      8                            * Input Date from
DATETO    FORM      8                            * Input Date to
DEFFFROM  FORM      8                            * Date Effective from
DEFFTO    FORM      8                            * Date Effective to
DISPCDAT  DIM       12                           * Formatted Date record created
DISPDACT  DIM       12                           * Formatted Active Date
DISPDIAC  DIM       12                           * Formatted Inactive Date
DISPDIM8  DIM       8                            * Active/Inactive literal
DISPDM30  DIM       30                           * Episode Type
DISPDM50  DIM       50                           * Classification Code Description
DISPMUR   DIM       8                            * Display Merged U/R Number
DISPSUR   DIM       8                            * Display Saved U/R Number
DOCTCODE  DIM       8                            * HCP Code
DISPUDAT  DIM       12                           * Formatted Date record updated
FORM3     FORM      3
FORM8     FORM      8
HTMLCNT   FORM      6                            * HTML Count
HTMLLINE  DIM       200                          * Clinical Doc input
IGDESC40  DIM       40                           * Impairment group description
IMCARRAY  DIM       7[50]                        * Impair Codes search results
IMCAREND  FORM      3                            * Impair Codes in IMCARRAY
MBSDESC   DIM       70
MRGEFLAG  FORM      1                            * Flag merged U/R numbers
NEXTPAGE  DIM       1                            * Nextpage CGI parameter
PSAGETYP  DIM       3
PSAGECON  DIM       3
REMOTENO  FORM      1                            * Impair Code Remote Script No
SAVKEY3   DIM       3                            * Impairment Group Keyword key
SAVKEY5   DIM       5                            * Class Code Keyword Code key
SAVKEY7   DIM       7	                         * Impair Code Keyword Code key
SAVKEY27  DIM       27                           * Assessment Comments clear key
SAVKEY34  DIM       34                           * Impairment Group Keywords key
SAVKEY35  DIM       35                           * Class Code Keywords key
SAVKEY37  DIM       37                           * Impairment Code Keywords key
SAVEURNO  DIM       8                            * used for merged UR in
STARTKEY  DIM       7                            * Starting key
TEMPFILE  FILE      ASCII,FIXED=127              * Temp File for Clinical Doc
TEMPNAME  DIM       8                            * Temp Store for Clinical Doc
TEMPNUMB  FORM      1                            * Calculate MMSE Score
TEMPTOTL  FORM      2                            * MMSE Total Score
TFILENME  DIM       20                           * Clinical Doc Temp File Name
UPDTTYPE  DIM       1                            * Update Type
UPDATEKY  DIM       24                           * admissno,visitdte,visittme
VALDCODE  DIM       7                            * Impair Code Remote Script Key
VERSFRM2  FORM      2                            * Right Justify Class version
VISITDTE  DIM       8                            * Date of Visit
VISITTME  DIM       8                            * Time of Visit
.------------------------------------------------------------------------------
. Constants
.------------------------------------------------------------------------------
ASSESSMT  INIT      "A"                          * Assessment Form
CATCZ     INIT      "cz"
CATCL     INIT      "CL"                         * Insurance Type
CATCT     INIT      "CT"
CATD1     INIT      "D1"
CATD2     INIT      "D2"
CATD3     INIT      "D3"
CATD4     INIT      "D4"
CATD5     INIT      "D5"
CATU6     INIT      "U6"
CATU7     INIT      "U7"
CATFA     INIT      "fa"
CATOB     INIT      "OB"
CATPN     INIT      "pn"
CATPO     INIT      "po"
CATPP     INIT      "pp"
CATSA     INIT      "SA"
CATVA     INIT      "VA"                         * Indigenous Status
CATWI     INIT      "wi"                         * Type of Correspondance
CMDMOVE   INIT      "mv "                        * Move command for command line
DASH      INIT      "-"
EPSDTYPC  INIT      "Outpatient/Community"
EPSDTYPO  INIT      "Overnight"
EPSDTYPS  INIT      "Same Day"
EXPNDTIM  INIT      ":00"
TEMPTIME  INIT      "09:07:50"
.
.------------------------------------------------------------------------------
PRGID     INIT      "PATWEB13"
VERSION   INIT      "V12.01.01"
PRGNAM    INIT      "AN-SNAP Web Server"
.------------------------------------------------------------------------------
.
          CALL      WEBINT00                     * Initialise Fifo Etc.
          CALL      INIT0000                     * Open Files
.
MAIN1000  CALL      WEBRED00                     * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO               * End Proc on Rpt Option 999
          GOTO      MAIN9999 IF EQUAL
.
          CALL      OUTFIL00                     * Check Site Files Are Open
          BRANCH    EXIT,MAIN1000
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400,MAIN1500:
                             MAIN1600,MAIN1700,MAIN1800,MAIN1900
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00                     * Create HTML File/Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      IMPC0000                     * Impairment Code
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      REMOTE00                     * Remote scripts
          GOTO      MAIN1000
.
MAIN1300  CALL      SNPC0000                     * Classification Code
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1400  CALL      MOTA0000                     * Motor Assessment Scale
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1500  CALL      MMSA0000                     * Mini Mental State Exam
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1600  CALL      EMSA0000                     * Mod. Elderly Mob. Scale
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1700  CALL      LADL0000                     * Lawton's Assessment of
          BRANCH    EXIT,MAIN1000                * Daily Living
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1800  CALL      SNAC0000                     * Auto Classification Code
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1900  CALL      IGRP0000                     * Impairment Group   C-0841644
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.
.------------------------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.
.------------------------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------------------------
INIT0000  CALL      INTMAS00                     * Open routine for PATMASTM
.
          OPEN      PMSIMPA1,"pmsimpaf"          * Impairment Code
          OPEN      PMSIKIA1,"pmsikiaf"          * Impairment Code Keywords
          OPEN      PMSIKIA2,"pmsikiaf"          * Impairment Code Keywords
          OPEN      PMSEMSA1,"pmsemsaf"          * Mod. Elderly Mob. Scale
          OPEN      PMSLADA1,"pmsladaf"          * Lawton's Assess of Dly Living
          OPEN      PMSMMSA1,"pmsmmsaf"          * Mini Mental State Exam
          OPEN      PMSMOTA1,"pmsmotaf"          * Motor Assessment Scale
          OPEN      PMSSNPA1,"pmssnpaf"          * Classification Code
          OPEN      PMSSKIA1,"pmsskiaf"          * Classification Code Keywords
          OPEN      PMSSKIA2,"pmsskiaf"          * Classification Code Keywords
          OPEN      PMSSNCA1,"pmssncaf"          * Auto Classification Code
          OPEN      VISACMA1,"visacmaf"          * Visit Assessment Comments
.
          OPEN      EMRVISA3,"emrvisaf"          * Emergency Visits File
          OPEN      MRTMASA1,"mrtmasaf"
          OPEN      MRTMASA2,"mrtmasaf"
          OPEN      OBSAUDA1,"obsaudaf"
          OPEN      OBSPCOA1,"obspcoaf"          * Patient Correspondence
          OPEN      OBSPCTA1,"obspctaf"          * Correspondence Storage Location
          OPEN      PATASFA1,"patasfaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATDGWA1,"patdgwaf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATITEM1,"patitemn"
          OPEN      PATRE1A1,"patre1af"
          OPEN      PATVISA2,"patvisaf"          * Patient Visits File
          OPEN      PMSVX1A1,"pmsvx1af"          * Patient Visits Extension File
          OPEN      PATDADA1,"patdadaf"
          OPEN      PATDGWA1,"patdgwaf"
          OPEN      PATIN1A1,"patin1af"
.
          OPEN      PMSCCDA1,"pmsccdaf"
          OPEN      PMSHCGA1,"pmshcgaf"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSNUTA1,"pmsnutaf"         * Nutrition file
.
.0841644-start
          OPEN      PMSIGRA1,"pmsigraf"          * Impairment Group
          OPEN      PMSGKIA1,"pmsgkiaf"          * Impairment Group Keywords
          OPEN      PMSGKIA2,"pmsgkiaf"          * Impairment Group Keywords

          READ      CONTROLF,HUND25;*172,PTCNFIMW
          READ      CONTROLF,HUND25;*238,PTCNNRTD
.
          MOVE      ZERO,F2
          MOVE      PTCNNRTD,F2
          IF        F2 = 0
            MOVE      "10",PTCNNRTD
          ENDIF
.
.0841644-end
.
INIT9999  RETURN
.
.------------------------------------------------------------------------------
. Check Correct Outpatient Files are open for Site
.------------------------------------------------------------------------------
OUTFIL00  MOVE      WBSESIT,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,OUTFIL90
          MATCH     OSTFILE,CFILEPRE             * Save Current File Prefix
          IF        !@EQUAL
            MOVE      OSTFILE,CFILEPRE           * Save Current File Prefix
            CALL      OPNOUT00                   * Open Outpatient Files
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      OUTFIL99
.
OUTFIL90  MOVE      "Outpatient Site Invalid",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
.
OUTFIL99  RETURN
.
.------------------------------------------------------------------------------
. Open Outpatient Files
.------------------------------------------------------------------------------
OPNOUT00  CLOSE     OUTCTYA1
          PACK      CFNAME,CFILEPRE,FILCTYA1     * Get name of the CTYA1 file
          OPEN      OUTCTYA1,CFNAME              * Open the file
.
          CLOSE     OUTCTYA2
          PACK      CFNAME,CFILEPRE,FILCTYA2     * Get name of the CTYA2 file
          OPEN      OUTCTYA2,CFNAME              * Open the file
.
          CLOSE     OUTCTYA3
          PACK      CFNAME,CFILEPRE,FILCTYA3     * Get name of the CTYA3 file
          OPEN      OUTCTYA3,CFNAME              * Open the file
.
          CLOSE     OUTCLIA1
          PACK      CFNAME,CFILEPRE,FILCLIA1     * Get name of the CLIA1 file
          OPEN      OUTCLIA1,CFNAME              * Open the file
.
          CLOSE     OUTBOKA6
          PACK      CFNAME,CFILEPRE,FILBOKA6     * Get name of the BOKA6 file
          OPEN      OUTBOKA6,CFNAME              * Open the file
.
          CLOSE     OUTBOKA3
          PACK      CFNAME,CFILEPRE,FILBOKA3     * Get name of the BOKA3 file
          OPEN      OUTBOKA3,CFNAME              * Open the file
.
          CLOSE     OUTBOKA1
          PACK      CFNAME,CFILEPRE,FILBOKA1     * Get name of the BOKA1 file
          OPEN      OUTBOKA1,CFNAME              * Open the file
.
          CLOSE     OUTMA1A1
          PACK      CFNAME,CFILEPRE,FILMA1A1     * Get name of the MA1A1 file
          OPEN      OUTMA1A1,CFNAME              * Open the file
.
          PACK      CFNAME,CFILEPRE,FILHEDA1     * Get name of the HEDA1 file
          CLOSE     OUTHEDA1
          OPEN      OUTHEDA1,CFNAME              * Open the file
.
          PACK      CFNAME,CFILEPRE,FILSESA1     * Get name of the SESA1 file
          CLOSE     OUTSESA1
          OPEN      OUTSESA1,CFNAME              * Open the file
.
          PACK      CFNAME,CFILEPRE,FILBOKC1     * Get name of the BOKC1 file
          CLOSE     OUTBOKC1
          OPEN      OUTBOKC1,CFNAME              * Open the file
.
OPNOUT99  RETURN
+
.------------------------------------------------------------------------------
.         Create Clinical Document HTML temp file
.------------------------------------------------------------------------------
CRCLDT00  CLEAR     TEMPNAME
          APPEND    "PATW13",TEMPNAME
          APPEND    PORT,TEMPNAME
          RESET     TEMPNAME
          REP       " 0",TEMPNAME
          CLEAR     KEY13
          APPEND    TEMPNAME,KEY13
          APPEND    ".html",KEY13
          RESET     KEY13
.
          PREP      TEMPFILE,KEY13
          MOVE      ZERO,HTMLCNT
.
CRCLDT99  RETURN
+
.------------------------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------------------------
CLRPAR00  MOVE      SP70,KEYWORDS
          MOVE      SP70,NEXTPAGE
          MOVE      SP70,NORECORD
          MOVE      SP70,REDIRECT
          MOVE      SP70,STARTKEY
          MOVE      SP70,TEMPLATE
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,CLASSCDE
          MOVE      SP70,CLASSVRS
          MOVE      SP70,VALDCODE
.
          MOVE      ZERO,REMOTENO
          MOVE      ZERO,REPORTNO
          MOVE      ZERO,STARTNUM
.
          MOVE      SP70,CLINDOCM
          MOVE      SP70,TFILENME
.
.         CLEAR     TEMPNAME
.         APPEND    "PATW13",TEMPNAME
.         APPEND    PORT,TEMPNAME
.         RESET     TEMPNAME
.         REP       " 0",TEMPNAME
.         CLEAR     KEY13
.         APPEND    TEMPNAME,KEY13
.         APPEND    ".html",KEY13
.         RESET     KEY13
.
.         PREP      TEMPFILE,KEY13
.         MOVE      ZERO,HTMLCNT
.
          CALL      CPPMIM00                     * Impairment Code
          CALL      CPPMSN00                     * Classification Code
          CALL      CPPMSC00                     * Auto Classification Code
          CALL      CPPLAD00                     * Lawton's Assess of Dly Living
          CALL      CPPMMA00                     * Motor Assessment Scale
          CALL      CPPMMS00                     * Mini Mental State Exam
          CALL      CPPEMS00                     * Mod. Elderly Mob. Scale
.
          CALL      CPPMIG00                     * Impairment Group   C-0841644
.
          MOVE      ZERO,MRGEFLAG
.
          CALL      CLRACM00           * Create comments input temp file
.
CLRPAR99  RETURN
+
.------------------------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTKEY
            PACK      STARTKEY,STARTKEY,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "keywords=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,KEYWORDS
            REP      "~'",KEYWORDS
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "classcde=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,CLASSCDE
            PACK      CLASSCDE,CLASSCDE,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "classvrs=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,CLASSVRS
            PACK      CLASSVRS,CLASSVRS,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "valdcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VALDCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "remoteno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REMOTENO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.                                                * Clinical Document
          MATCH     "htmlline=",QRYNAME
          IF        @EQUAL
            CALL      CRCLDT00                   * Create comments temp file
            CALL      GETCLD00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmimp",QRYNAME              * Impairment Code
          IF        @EQUAL
            CALL      SPPMIM00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmsnp",QRYNAME              * Classification Code
          IF        @EQUAL
            CALL      SPPMSN00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmsnc",QRYNAME              * Auto Classification Code
          IF        @EQUAL
            CALL      SPPMSC00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmmot",QRYNAME              * Motor Assessment Scale
          IF        @EQUAL
            CALL      SPPMMA00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "vsamc001",QRYNAME           * Motor Assessment Comments
          IF        @EQUAL
            CALL      SETACM00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmmms",QRYNAME              * Mini Mental State Exam
          IF        @EQUAL
            CALL      SPPMMS00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmems",QRYNAME              * Mod. Elderly Mob. Scale
          IF        @EQUAL
            CALL      SPPEMS00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmlad",QRYNAME              * Lawton's Assess of Dly Living
          IF        @EQUAL
            CALL      SPPLAD00
            GOTO      SETPAR99
          ENDIF
.
.0841644-start
          MATCH     "pmigr",QRYNAME              * Impairment Group   
          IF        @EQUAL
            CALL      SPPMIG00
            GOTO      SETPAR99
          ENDIF
.0841644-end
.
SETPAR99  RETURN
.
.------------------------------------------------------------------------------
. Process Fields
.------------------------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30,PROFLD40,PROFLD50:
                             PROFLD60,PROFLD70,PROFLD80,PROFLD90
.
          GOTO      PROFLD99
.                                                * Impairment Codes
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12
          GOTO      PROFLD99
.
PROFLD11  CALL      PMIM0000
          GOTO      PROFLD99
.
PROFLD12  CALL      GTAG0000
          GOTO      PROFLD99
.                                                * Remote Scripts
PROFLD20  GOTO      PROFLD99
.                                                * Classification Codes
PROFLD30  BRANCH    FLDSETNO,PROFLD31,PROFLD32
          GOTO      PROFLD99
.
PROFLD31  CALL      PMSN0000
          GOTO      PROFLD99
.
PROFLD32  CALL      GTAG0000
          GOTO      PROFLD99
.                                                * Motor Assessment Scale
PROFLD40  BRANCH    FLDSETNO,PROFLD41,PROFLD42,PROFLD43
          GOTO      PROFLD99
.
PROFLD41  CALL      MASF0000
          GOTO      PROFLD99
.
PROFLD42  CALL      GTAG0000
          GOTO      PROFLD99
.
PROFLD43  CALL      PMMA0000
          GOTO      PROFLD99
.                                                * Mini Mental State Exam
PROFLD50  BRANCH    FLDSETNO,PROFLD51,PROFLD52,PROFLD53
          GOTO      PROFLD99
.
PROFLD51  CALL      MASF0000
          GOTO      PROFLD99
.
PROFLD52  CALL      GTAG0000
          GOTO      PROFLD99
.
PROFLD53  CALL      PMMS0000
          GOTO      PROFLD99
.                                                * Mod. Elderly Mob. Scale
PROFLD60  BRANCH    FLDSETNO,PROFLD61,PROFLD62,PROFLD63
          GOTO      PROFLD99
.
PROFLD61  CALL      MASF0000
          GOTO      PROFLD99
.
PROFLD62  CALL      GTAG0000
          GOTO      PROFLD99
.
PROFLD63  CALL      PEMS0000
          GOTO      PROFLD99
.                                                * Lawton's Assess of Dly Living
PROFLD70  BRANCH    FLDSETNO,PROFLD71,PROFLD72,PROFLD73
          GOTO      PROFLD99
.
PROFLD71  CALL      MASF0000
          GOTO      PROFLD99
.
PROFLD72  CALL      GTAG0000
          GOTO      PROFLD99
.
PROFLD73  CALL      PLAD0000
          GOTO      PROFLD99
.                                                * Auto Classification Codes
PROFLD80  BRANCH    FLDSETNO,PROFLD81,PROFLD82
          GOTO      PROFLD99
.
PROFLD81  CALL      PMSC0000
          GOTO      PROFLD99
.
PROFLD82  CALL      GTAG0000
          GOTO      PROFLD99
.
PROFLD90  BRANCH    FLDSETNO,PROFLD91,PROFLD92
          GOTO      PROFLD99
.
PROFLD91  CALL      GTAG0000
          GOTO      PROFLD99
.
PROFLD92  CALL      PMIG0000
          GOTO      PROFLD99
.
PROFLD99  RETURN
.
.------------------------------------------------------------------------------
. Alert that this is a merged U/R
.------------------------------------------------------------------------------
MRGALT00  MATCH     SP70,URNUMBER
          GOTO      MRGALT99 IF EQUAL
.
          MATCH     SP70,SAVEURNO
          GOTO      MRGALT99 IF EQUAL
.
          MOVE      SAVEURNO,DISPSUR
          SQUEEZE   DISPSUR
          MOVE      URNUMBER,DISPMUR
          SQUEEZE   DISPMUR
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " alert('U/R Number ",DISPSUR," is merged with ":
                             DISPMUR," \n Using ",DISPMUR,"');":
                             "}":
                             "</script>"
          GOTO      MRGALT99
.
MRGALT99  RETURN

.
.------------------------------------------------------------------------------
. Output General tags
.------------------------------------------------------------------------------
GTAG0000  BRANCH    FLDITMNO,GTAG0100,GTAG0200,GTAG0300,GTAG0400,GTAG0500:
                             GTAG0600,GTAG0700,GTAG0800,GTAG0900,GTAG1000:
                             GTAG1100,GTAG1200,GTAG1300,GTAG1400,GTAG1500:
                             GTAG1600
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      GTAG9999
.
GTAG0100  CALL      ICTAB000                    * Display Impairment Code list
          GOTO      GTAG9999
.
GTAG0200  CALL      PREVGR00                    * Display previous list
          GOTO      GTAG9999
.
GTAG0300  CALL      NEXTGR00                    * Display next list
          GOTO      GTAG9999
.
GTAG0400  CALL      IPSRH000                    * Impairment Code Search
          GOTO      GTAG9999
.
GTAG0500  CALL      CCTAB000                    * Display Classification
          GOTO      GTAG9999                    * Code list
.
GTAG0600  CALL      CCSRH000                    * Classification Code Search
          GOTO      GTAG9999
.
GTAG0700  CALL      MOTLS000                    * Motor Assessment Scale List
          GOTO      GTAG9999
.
GTAG0800  CALL      MMSLS000                    * Mini Mental State Exam List
          GOTO      GTAG9999
.
GTAG0900  CALL      EMSLS000                    * Mod. Elderly Mob. Scale
          GOTO      GTAG9999
.
GTAG1000  CALL      LADLS000                    * Lawton's Assessment of
          GOTO      GTAG9999                    * Daily Living
.
GTAG1100  CALL      FNAM0000                    * Get formatted patient name
          GOTO      GTAG9999
.
GTAG1200  CALL      ABRG0000                    * Get indigenous status
          GOTO      GTAG9999
.
GTAG1300  CALL      INST0000                    * Get insurance type
          GOTO      GTAG9999
.
GTAG1400  CALL      ACTAB000                    * Display Auto Classification
          GOTO      GTAG9999                    * Code list
.
GTAG1500  CALL      IGTAB000                    * Display Impairment Group list
          GOTO      GTAG9999
.
GTAG1600  CALL      IGSEL000                    * Impairment Group List
          GOTO      GTAG9999
.
GTAG9999  RETURN
.
.*******************************************************************************
. Impairment Code Maintenance
.*******************************************************************************
IMPC0000  MATCH     SP70,UPDTTYPE
          GOTO      IMPC7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE                 * Add Impairment Code
          GOTO      IMPC1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE                 * Update Impairment Code
          GOTO      IMPC2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE                 * Delete Impairment Code
          GOTO      IMPC3000 IF EQUAL
.
          GOTO      IMPC9000
.
.------------------------------------------------------------------------------
. Add new Impairment Code
.------------------------------------------------------------------------------
IMPC1000  PACK      KEY7,PMIMP001,SP70
          CALL      RDPMIMP1
          IF        OVRCD <> 1
            GOTO      IMPC9100
          ENDIF
.
          CALL      CLPMIM00                     * Clear File Variables
          CALL      MVPMIM00                     * move cgi to file
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,PMIMCDAT                * System date
          CLOCK     TIME,PMIMCTIM                * System time
          MOVE      USERID,PMIMCUID              * web user id
.
          CALL      WRPMIMP1                     * write new record
.
          PROC      PMSJKIUP                     * generate keywords
.
          MOVE      ZERO,EXIT
          GOTO      IMPC9999
.
.------------------------------------------------------------------------------
. Update Impairment Code Details
.------------------------------------------------------------------------------
IMPC2000  PACK      KEY7,PMIMP001,SP70
          CALL      RDPMIMP1
          IF        OVRCD = 1
            GOTO      IMPC9200
          ENDIF
.
          CALL      MVPMIM00                     * move cgi to file
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,PMIMUDAT                * System date
          CLOCK     TIME,PMIMUTIM                * System time
          MOVE      USERID,PMIMUUID              * web user id
.
          CALL      UPPMIMP1                     * update details
.
          PROC      PMSJKIUP                     * generate keywords
.
          MOVE      ZERO,EXIT
          GOTO      IMPC9999
.
.------------------------------------------------------------------------------
. Delete Impairment Code
.------------------------------------------------------------------------------
IMPC3000  PACK      KEY7,PMIMP001,SP70
          CALL      RDPMIMP1
          IF        OVRCD = 1
            GOTO      IMPC9200
          ENDIF
.
          CALL      DEPMIMP1
.
          PROC      PMSJKIUP                     * generate keywords
.
          MOVE      ZERO,EXIT
          GOTO      IMPC9999
.
.------------------------------------------------------------------------------
. List Impairment Code
.------------------------------------------------------------------------------
IMPC7000  CLEAR     WEBTITLE
          MOVE      "Impairment Code Maintenance",WEBTITLE
          RESET     WEBTITLE
.
IMPC7100  PACK      KEY7,PMIMP001,SP70
          CALL      RDPMIMP1
          IF        OVRCD = 1
            CALL      CLPMIM00
          ENDIF
.
          CALL      WEBHED00                     * Create HTML File/Page Header
          BRANCH    EXIT,IMPC9999
          CALL      WEBBOD00                     * Process Web Body
          CALL      WEBEND00                     * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      IMPC9999
.
IMPC9000  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      IMPC9999
.
IMPC9100  MOVE      "Code Record already exists on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      IMPC9999
.
IMPC9200  MOVE      "Code Record does not exist on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      IMPC9999
.
IMPC9999  RETURN
.
.*******************************************************************************
. Impairment Code Maintenance Table
.*******************************************************************************
ICTAB000  IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD          * Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY7,STARTKEY,SP70
          CALL      RSPMIMP1                     * Position on Startkey
          CALL      RPPMIMP1                     * Read one back from position
.
ICTAB100  CALL      RKPMIMP1                     * Read on first Startkey found
          BRANCH    OVRCD,ICTAB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      ICTAB100
          ENDIF
.
          CALL      ICROW000                     * Display Impairment code Row
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      ICTAB100 IF NOT EQUAL
          GOTO      ICTAB999
.
ICTAB900  CALL      NOMORE00             * Display No More Records Message
.
ICTAB999  RETURN
.------------------------------------------------------------------------------
.  Impairment Code Maintenance Table Row Detail
.------------------------------------------------------------------------------
ICROW000  MATCH     "1",PMIMACTV
          IF        @EQUAL
            MOVE      "Active",DISPDIM8
          ELSE
            MOVE      "Inactive",DISPDIM8
          ENDIF
.
          MOVE      SP70,DISPDACT
          MOVE      SP70,DISPDIAC
.
          MATCH     PMIMDACT,SP70
          IF        !@EQUAL
            UNPACK    PMIMDACT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDACT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          MATCH     PMIMDIAC,SP70
          IF        !@EQUAL
            UNPACK    PMIMDIAC,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDIAC,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          MATCH     "1",PTCNFIMW
          GOTO      ICROW900 IF NOT EQUAL
.
          MOVE      SP30,IGDESC40
          MATCH     SP70,PMIMGCDE
          IF        @EQUAL | @EOS
            GOTO      ICROW900
          ELSE
            PACK    KEY3,PMIMGCDE,SP70
            CALL    RDPMIGR1
            BRANCH  OVRCD,ICROW900
            MOVE    PMIGDESC,IGDESC40
          ENDIF
.
ICROW900  WRITE     HTMLFILE;"<tr><td nowrap>":
                             "<img src=../images/MaintenanceIcon.gif ":
                             "class=ListIcon ":
                             "onclick=#"ImpDetails('",PMIMCODE,"');#"> ":
                             PMIMCODE,"</td>":
                             "<td>",PMIMDESC,"</td>":
                             "<td align=center>",DISPDIM8,"</td>":
                             "<td align=center>",DISPDACT,"&nbsp;</td>":
                             "<td align=center>",DISPDIAC,"&nbsp;</td>";
.
          MATCH     "1",PTCNFIMW
          IF        @EQUAL
            WRITE     HTMLFILE;"<td>",IGDESC40,"&nbsp;</td>":
                               "</tr>";
          ELSE
            WRITE     HTMLFILE;"</tr>";
          ENDIF
.
ICROW999 RETURN
.
.------------------------------------------------------------------------------
. Link to Next Page
.------------------------------------------------------------------------------
NEXTGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",STARTKEY
          REP       "'~",KEYWORDS
.
          WRITE     HTMLFILE;"patweb13.pbl":
                             "?reportno=",F2:
                             "&template=",FLDPARA:
                             "&startnum=",KEY3:
                             "&norecord=",KEY2:
                             "&startkey=",STARTKEY:
                             "&keywords=",KEYWORDS;
.
          REP       "+ ",STARTKEY
          REP       "~'",KEYWORDS
.
NEXTGR99  RETURN
.
.------------------------------------------------------------------------------
. Link to Prev Page
.------------------------------------------------------------------------------
PREVGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",STARTKEY
          REP       "'~",KEYWORDS
.
          WRITE     HTMLFILE;"patweb13.pbl":
                             "?reportno=",F2:
                             "&template=",FLDPARA:
                             "&startnum=",KEY3:
                             "&norecord=",KEY2:
                             "&startkey=",STARTKEY:
                             "&keywords=",KEYWORDS;
.
          REP       "+ ",STARTKEY
          REP       "~'",KEYWORDS
.
PREVGR99  RETURN
.
.*******************************************************************************
. Search Impairment Code Table and List
.*******************************************************************************
IPSRH000  CALL      IPSHD000                     * Output Impair Table Heading
.
          IF        NORECORD=0
            MOVE      "10",NORECORD              * number of records to display
          ENDIF
.
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
          MOVE      ZERO,IMCAREND                * Number of Impair codes in IMCARRAY
.
          CALL      GETWRD00

          STRIP     KEYWRD01
          PACK      KEY37,KEYWRD01,SP70
          CALL      RSPMIKI2
IPSRH100  CALL      RKPMIKI2
          BRANCH    OVRCD,IPSRH999
.
          PACK      KEY7,PMIKCODE,SP70
          CALL      RDPMIMP1                     * read Impairment Code table
          BRANCH    OVRCD,IPSRH100
.
          CALL      IPSHKB00
          BRANCH    EXIT,IPSRH100,IPSRH999
.
IPSRH200  ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      IPSRH100
          ENDIF
.
          CALL      IPSRW000                     * Impairment Code Details Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          IF        !@EQUAL
            GOTO      IPSRH100
          ENDIF
.
IPSRH999  RETURN
.
.------------------------------------------------------------------------------
. Impairment Code Keyword Search Table Heading
.------------------------------------------------------------------------------
IPSHD000  WRITE     HTMLFILE;"<tr align=center>":
                               "<td class=HeadingCell colspan=5>":
                               "You Searched for ":
                                KEYWORDS,"</td>":
                               "</tr>"
.
          WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell>":
                               "Impairment Code</td>":
                               "<td class=HeadingCell>":
                               "Description</td>":
                               "</tr>"
.
IPSHD999  RETURN
.
.------------------------------------------------------------------------------
. Impairment Codes Keyword Search Table Row Detail
.------------------------------------------------------------------------------
IPSRW000  MATCH     "0",PMIMACTV
          GOTO       IPSRW999 IF EQUAL
.
          WRITE     HTMLFILE;"<tr valign=#"top#">":
                             "<td><a href=#"":
                             "javascript:updateParent('",PMIMCODE:
                             "','",PMIMDESC,"')#">":
                                   PMIMCODE,"</td>":
                             "<td>",PMIMDESC,"</td>":
                             "</tr>"
.
IPSRW999 RETURN
.
.------------------------------------------------------------------------------
. Check Impairment Record Display
.------------------------------------------------------------------------------
IPSHKB00  MATCH     KEYWRD01,PMIKKKWD
          GOTO      IPSHKB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            CALL      PMSHKC00
            CALL      PMSHKC00
            BRANCH    EXIT,IPSHKB91
            MOVE      SAVKEY37,KEY37
            CALL      RDPMIKI2
          ENDIF
.
IPSHKB90  CALL      CIMP0000                     * Check if Impairment Code has
          BRANCH    EXIT,IPSHKB91                * been displayed before
.
          MOVE      ZERO,EXIT
          GOTO      IPSHKB99
.
IPSHKB91  MOVE      ONE,EXIT
          GOTO      IPSHKB99
.
IPSHKB92  MOVE      TWO,EXIT
.
IPSHKB99  RETURN
.
.------------------------------------------------------------------------------
. Check for Impairment Code Matching Key Words
.------------------------------------------------------------------------------
PMSHKC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY7,PMIKCODE,SP70
.
PMSHKC10  COMPARE   KEYWRDNO,F2                  * Loop through Key Words in Srch
          GOTO      PMSHKC99 IF EQUAL            * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                                KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY37,PMIMCODE,KEYWRDXX
          CALL      RDPMIKI1                     * Check for Exact Match
          COMPARE   ZERO,OVRCD                   * Ok so Continue Loop
          GOTO      PMSHKC10 IF EQUAL
.
          CALL      RKPMIKI1                     * Not Exact, Read Next Record
          BRANCH    OVRCD,PMSHKC90               * No Match So Exit
          PACK      KEY7,PMIKCODE,SP70
.
          MATCH     SAVKEY7,KEY7                 * Match Key Fields
          GOTO      PMSHKC90 IF NOT EQUAL        * No Match So Exit
.
          STRIP     KEYWRDXX
          MATCH     PMIKCODE,KEYWRDXX            * Check Key Word
          GOTO      PMSHKC90 IF NOT EQUAL        * No Match So Exit
.
          GOTO      PMSHKC10                     * Check Next Search Word
.
PMSHKC90  MOVE      ONE,EXIT
.
PMSHKC99  RETURN
.
.------------------------------------------------------------------------------
. Check if this Impairment Code has already been output in the search
.------------------------------------------------------------------------------
CIMP0000  MOVE       ZERO,F3
          MOVE       ZERO,EXIT
.
CIMP1000  ADD        ONE,F3
          IF         F3>IMCAREND
            IF         F3>50
              GOTO       CIMP9999
            ENDIF
            MOVE       F3,IMCAREND
            MOVE       PMIMCODE,IMCARRAY[F3]
            GOTO       CIMP9999
          ENDIF
.
          MATCH      PMIMCODE,IMCARRAY[F3]
          GOTO       CIMP1000 IF NOT EQUAL
.
          MOVE       ONE,EXIT
.
CIMP9999  RETURN
.
.*******************************************************************************
. Remote Scripting for Impairment Code
.*******************************************************************************
.
REMOTE00  CALL      XMLHED00
          BRANCH    EXIT,REMOTE99
.
          BRANCH    REMOTENO,REMOTE10,REMOTE20
          WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                    " INVALID UPDATE TYPE ":
                    "</RETURN_VALUE>"
          GOTO      REMOTE90
.
REMOTE10  CALL      RMICD000                     * Remote script to validate
          GOTO      REMOTE90                     * Impairment code
.
REMOTE20  CALL      RMCLS000                     * Remote script to validate
          GOTO      REMOTE90                     * Classification Code
.
REMOTE90  CALL      XMLEND00
.
REMOTE99  RETURN
.
.------------------------------------------------------------------------------
. Validate Impairment Code remote script
.------------------------------------------------------------------------------
RMICD000  PACK      KEY7,VALDCODE,SP70
          CALL      RDPMIMP1
          BRANCH    OVRCD,RMICD900
.
          MATCH     "1",PMIMACTV                 * check Impair code is active
          GOTO      RMICD920 IF NOT EQUAL
.                                                * found a valid Impair Code
RMICD500  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             PMIMDESC,"|":
                             "</RETURN_VALUE>"
          GOTO      RMICD999
.
RMICD900  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Impairment code is invalid.":
                             "</RETURN_VALUE>"
          GOTO      RMICD999
.
RMICD920  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Impairment Code is not active.":
                             "</RETURN_VALUE>"
          GOTO      RMICD999
.
RMICD999  RETURN
.
.------------------------------------------------------------------------------
. Validate Classification Code remote script
.------------------------------------------------------------------------------
RMCLS000  MATCH     SP70,CLASSCDE
          GOTO      RMCLS900 IF EQUAL
.
          MATCH     SP70,CLASSVRS
          GOTO      RMCLS100 IF EQUAL
.
          RJUSTIFY  CLASSVRS
          PACK      KEY5,CLASSCDE,CLASSVRS,SP70
          CALL      RDPMSNP1
          BRANCH    OVRCD,RMCLS900
.
          GOTO      RMCLS400
.
RMCLS100  PACK      KEY5,CLASSCDE,Z70
          CALL      RSPMSNP1
          CALL      RPPMSNP1
          BRANCH    OVRCD,RMCLS900
.
          MATCH     CLASSCDE,PMSNCLCD
          GOTO      RMCLS900 IF NOT EQUAL
.
RMCLS400  MATCH     "1",PMSNACTV                 * check Class code is active
          GOTO      RMCLS920 IF NOT EQUAL
.                                                * found a valid Class Code
RMCLS500  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             PMSNVERS,"|",PMSNDESC:
                             "</RETURN_VALUE>"
          GOTO      RMCLS999
.
RMCLS900  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "AN-SNAP Classification code is invalid.":
                             "</RETURN_VALUE>"
          GOTO      RMCLS999
.
RMCLS920  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "AN-SNAP Classification code is not active.":
                             "</RETURN_VALUE>"
          GOTO      RMCLS999
.
RMCLS999  RETURN
.
.*******************************************************************************
. Classification Code Maintenance
.*******************************************************************************
SNPC0000  MATCH     SP70,UPDTTYPE
          GOTO      SNPC7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE                 * Add Classification Code
          GOTO      SNPC1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE                 * Update Classification Code
          GOTO      SNPC2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE                 * Delete Classification Code
          GOTO      SNPC3000 IF EQUAL
.
          GOTO      SNPC9000
.
.------------------------------------------------------------------------------
. Add new Classification Code
.------------------------------------------------------------------------------
SNPC1000  PACK      KEY5,PMSNP001,PMSNP002,SP70
          CALL      RDPMSNP1
          IF        OVRCD <> 1
            GOTO      SNPC9100
          ENDIF
.
          CALL      CLPMSN00                     * Clear File Variables
          CALL      MVPMSN00                     * move cgi to file
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,PMSNCDAT                * System date
          CLOCK     TIME,PMSNCTIM                * System time
          MOVE      USERID,PMSNCUID              * web user id
.
          CALL      WRPMSNP1                     * write classification record
.
          PROC      PMSSKIUP                     * generate keywords
.
          MOVE      ZERO,EXIT
          GOTO      SNPC9999
.
.------------------------------------------------------------------------------
. Update Classification Code Details
.------------------------------------------------------------------------------
SNPC2000  PACK      KEY5,PMSNP001,PMSNP002,SP70
          CALL      RDPMSNP1
          IF        OVRCD = 1
            GOTO      SNPC9200
          ENDIF
.
          CALL      MVPMSN00                     * move cgi to file
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,PMSNUDAT                * System date
          CLOCK     TIME,PMSNUTIM                * System time
          MOVE      USERID,PMSNUUID              * web user id
.
          CALL      UPPMSNP1                     * update classification record
.
          PROC      PMSSKIUP                     * generate keywords
.
          MOVE      ZERO,EXIT
          GOTO      SNPC9999
.
.------------------------------------------------------------------------------
. Delete Classification Code
.------------------------------------------------------------------------------
SNPC3000  PACK      KEY5,PMSNP001,PMSNP002,SP70
          CALL      RDPMSNP1
          IF        OVRCD = 1
            GOTO      SNPC9200
          ENDIF
.
          CALL      DEPMSNP1                     * delete classification record
.
          PROC      PMSSKIUP                     * generate keywords
.
          MOVE      ZERO,EXIT
          GOTO      SNPC9999
.
.------------------------------------------------------------------------------
. List Classification Code
.------------------------------------------------------------------------------
SNPC7000  CLEAR     WEBTITLE
          MOVE      "Classification Code Maintenance",WEBTITLE
          RESET     WEBTITLE
.
SNPC7100  PACK      KEY5,PMSNP001,PMSNP002,SP70
          CALL      RDPMSNP1
          IF        OVRCD = 1
            CALL      CLPMSN00
          ENDIF
.
          CALL      WEBHED00                     * Create HTML File/Page Header
          BRANCH    EXIT,SNPC9999
          CALL      WEBBOD00                     * Process Web Body
          CALL      WEBEND00                     * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      SNPC9999
.
SNPC9000  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SNPC9999
.
SNPC9100  MOVE      "Code Record already exists on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SNPC9999
.
SNPC9200  MOVE      "Code Record does not exist on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SNPC9999
.
SNPC9999  RETURN
.
.*******************************************************************************
. Classification Code Maintenance Table
.*******************************************************************************
CCTAB000  IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD          * Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY5,STARTKEY,SP70
          CALL      RSPMSNP1                     * Position on Startkey
          CALL      RPPMSNP1                     * Read one back from position
.
CCTAB100  CALL      RKPMSNP1                     * Read on first Startkey found
          BRANCH    OVRCD,CCTAB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      CCTAB100
          ENDIF
.
          CALL      CCROW000                     * Display Class code Row
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      CCTAB100 IF NOT EQUAL
          GOTO      CCTAB999
.
CCTAB900  CALL      NOMORE00             * Display No More Records Message
.
CCTAB999  RETURN
.------------------------------------------------------------------------------
. Classification Code Maintenance Table Row Detail
.------------------------------------------------------------------------------
CCROW000  MATCH     "1",PMSNACTV
          IF        @EQUAL
            MOVE      "Active",DISPDIM8
          ELSE
            MOVE      "Inactive",DISPDIM8
          ENDIF
.
          MOVE      "Unknown Episode Type",DISPDM30
          MATCH     "C",PMSNETYP
          IF        @EQUAL
            MOVE      EPSDTYPC,DISPDM30
          ENDIF
          MATCH     "O",PMSNETYP
          IF        @EQUAL
            MOVE      EPSDTYPO,DISPDM30
          ENDIF
          MATCH     "S",PMSNETYP
          IF        @EQUAL
            MOVE      EPSDTYPS,DISPDM30
          ENDIF
.
          MOVE      SP70,DISPDACT
          MOVE      SP70,DISPDIAC
.
          MATCH     PMSNDACT,SP70
          IF        !@EQUAL
            UNPACK    PMSNDACT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDACT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          MATCH     PMSNDIAC,SP70
          IF        !@EQUAL
            UNPACK    PMSNDIAC,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDIAC,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
CCROW900  WRITE     HTMLFILE;"<tr><td nowrap>":
                             "<img src=../images/MaintenanceIcon.gif ":
                             "class=ListIcon ":
         "onclick=#"ClassDetails('",PMSNCLCD,"','",PMSNVERS,"');#"> ":
                             PMSNCLCD,"</td>":
                             "<td>",PMSNVERS,"</td>":
                             "<td>",PMSNDESC,"</td>":
                             "<td>",DISPDM30,"</td>":
                             "<td align=center>",DISPDIM8,"</td>":
                             "<td align=center>",DISPDACT,"&nbsp;</td>":
                             "<td align=center>",DISPDIAC,"&nbsp;</td>":
                             "</tr>"
.
CCROW999 RETURN
.
.*******************************************************************************
. Search Classification Code Table and List
.*******************************************************************************
CCSRH000  CALL      CCSHD000                     * Output Table Heading
.
          IF        NORECORD=0
            MOVE      "10",NORECORD              * number of records to display
          ENDIF
.
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
          MOVE      ZERO,CCCAREND                * Number of Class Codes in CCCARRAY
.
          CALL      GETWRD00

          STRIP     KEYWRD01
          PACK      KEY35,KEYWRD01,SP70
          CALL      RSPMSKI2
CCSRH100  CALL      RKPMSKI2
          BRANCH    OVRCD,CCSRH999
.
          PACK      KEY5,PMSKCODE,SP70
          CALL      RDPMSNP1                     * read Classification table
          BRANCH    OVRCD,CCSRH100
.
          CALL      CCSHKB00
          BRANCH    EXIT,CCSRH100,CCSRH999
.
CCSRH200  ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      CCSRH100
          ENDIF
.
          CALL      CCSRW000                     * Classification Code Detail Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          IF        !@EQUAL
            GOTO      CCSRH100
          ENDIF
.
CCSRH999  RETURN
.
.------------------------------------------------------------------------------
. Classification Code Keyword Search Table Heading
.------------------------------------------------------------------------------
CCSHD000  WRITE     HTMLFILE;"<tr align=center>":
                             "<td class=HeadingCell colspan=5>":
                             "You Searched for ":
                              KEYWORDS,"</td>":
                             "</tr>"
.
          WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell>":
                             "Classification Class Code</td>":
                             "<td class=HeadingCell>":
                             "Classification Version</td>":
                             "<td class=HeadingCell>":
                             "Description</td>":
                             "</tr>"
.
CCSHD999  RETURN
.
.------------------------------------------------------------------------------
. Classification Code Keyword Search Table Row Detail
.------------------------------------------------------------------------------
CCSRW000  MATCH     "0",PMSNACTV
          GOTO       CCSRW999 IF EQUAL
.
          WRITE     HTMLFILE;"<tr valign=#"top#">":
                             "<td><a href=#"":
                             "javascript:updateParent('",PMSNCLCD:
                             "','",PMSNVERS:
                             "','",PMSNDESC,"')#">":
                                   PMSNCLCD,"</td>":
                             "<td>",PMSNVERS,"</td>":
                             "<td>",PMSNDESC,"</td>":
                             "</tr>"
.
CCSRW999 RETURN
.
.------------------------------------------------------------------------------
. Check Classification Code Record
.------------------------------------------------------------------------------
CCSHKB00  MATCH     KEYWRD01,PMSKKKWD
          GOTO      CCSHKB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY35,PMSKKKWD,PMSKCODE
            CALL      CCSHKC00
            BRANCH    EXIT,CCSHKB91
            MOVE      SAVKEY35,KEY35
            CALL      RDPMSKI2
          ENDIF
.
CCSHKB90  CALL      CCSN0000                     * Check if Class Code has
          BRANCH    EXIT,CCSHKB91                * been displayed before
.
          MOVE      ZERO,EXIT
          GOTO      CCSHKB99
.
CCSHKB91  MOVE      ONE,EXIT
          GOTO      CCSHKB99
.
CCSHKB92  MOVE      TWO,EXIT
.
CCSHKB99  RETURN
.
.------------------------------------------------------------------------------
. Check for Classification Code Matching Key Words
.------------------------------------------------------------------------------
CCSHKC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY5,PMSKCODE,SP70
.
CCSHKC10  COMPARE   KEYWRDNO,F2                  * Loop through Key Words in Srch
          GOTO      CCSHKC99 IF EQUAL            * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                                KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY35,PMSNCLCD,PMSNVERS,KEYWRDXX
          CALL      RDPMSKI1                     * Check for Exact Match
          COMPARE   ZERO,OVRCD                   * Ok so Continue Loop
          GOTO      CCSHKC10 IF EQUAL
.
          CALL      RKPMSKI1                     * Not Exact so Read Next
          BRANCH    OVRCD,CCSHKC90               * No Match So Exit
.
          PACK      KEY5,PMSKCODE,SP70
          MATCH     SAVKEY5,KEY5                 * Match Key Fields
          GOTO      CCSHKC90 IF NOT EQUAL        * No Match So Exit
.
          STRIP     KEYWRDXX
          MATCH     PMSKCODE,KEYWRDXX            * Check Key Word
          GOTO      CCSHKC90 IF NOT EQUAL        * No Match So Exit
.
          GOTO      CCSHKC10                     * Check Next Search Word
.
CCSHKC90  MOVE      ONE,EXIT
.
CCSHKC99  RETURN
.
.------------------------------------------------------------------------------
. Check if this Classification Code has already been output in the search
.------------------------------------------------------------------------------
CCSN0000  MOVE       ZERO,F3
          MOVE       ZERO,EXIT
.
CCSN1000  ADD        ONE,F3
          IF         F3>CCCAREND
            IF         F3>50
              GOTO       CCSN9999
            ENDIF
            MOVE       F3,CCCAREND
            MOVE       PMSKCODE,CCCARRAY[F3]
            GOTO       CCSN9999
          ENDIF
.
          MATCH      PMSKCODE,CCCARRAY[F3]
          GOTO       CCSN1000 IF NOT EQUAL
.
          MOVE       ONE,EXIT
.
CCSN9999  RETURN
.
.*******************************************************************************
. Auto Classification Code Maintenance
.*******************************************************************************
SNAC0000  MATCH     SP70,UPDTTYPE
          GOTO      SNAC7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE                 * Add Auto Classification
          GOTO      SNAC1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE                 * Update Auto Classification
          GOTO      SNAC2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE                 * Delete Auto Classification
          GOTO      SNAC3000 IF EQUAL
.
          GOTO      SNAC9000
.
.------------------------------------------------------------------------------
. Add new Auto Classification Code
.------------------------------------------------------------------------------
SNAC1000  PACK      KEY20,PMSNC001,PMSNC002,PMSNC003,PMSNC004,SP70
          CALL      RDPMSNC1
          IF        OVRCD <> 1
            GOTO      SNAC9100
          ENDIF
.
          CALL      CLPMSC00                     * Clear File Variables
.
          PACK      KEY20,PMSNC001,PMSNC002,PMSNC003,PMSNC004,Z70
          CALL      RSPMSNC1
          CALL      RPPMSNC1
          IF        OVRCD = 1
            GOTO      SNAC1800
          ENDIF
.
          MATCH     PMSNC001,PMSCCLCD
          GOTO      SNAC1800 IF NOT EQUAL
.
          MATCH     PMSNC002,PMSCVERS
          GOTO      SNAC1800 IF NOT EQUAL
.
          MATCH     PMSNC003,PMSCICDE
          GOTO      SNAC1800 IF NOT EQUAL
.
          MOVE      PMSNC004,DATEFROM
          MOVE      PMSNC005,DATETO
          MOVE      PMSCDEFF,DEFFFROM
          MOVE      PMSCDEFT,DEFFTO
.
          MATCH     SP70,PMSCDEFT
          IF        @EQUAL
            GOTO      SNAC9300
          ENDIF
.
          IF        DATEFROM <= DEFFTO
            GOTO      SNAC9300
          ENDIF
.
          PACK      KEY20,PMSNC001,PMSNC002,PMSNC003,PMSNC004,SP70
          CALL      RSPMSNC1
          CALL      RKPMSNC1
          IF        OVRCD = 1
            GOTO      SNAC1800
          ENDIF
.
          MATCH     PMSNC001,PMSCCLCD
          GOTO      SNAC1800 IF NOT EQUAL
.
          MATCH     PMSNC002,PMSCVERS
          GOTO      SNAC1800 IF NOT EQUAL
.
          MATCH     PMSNC003,PMSCICDE
          GOTO      SNAC1800 IF NOT EQUAL
.
          MOVE      PMSCDEFF,DEFFFROM
          MOVE      PMSCDEFT,DEFFTO
.
          MATCH     SP70,PMSCDEFT
          IF        @EQUAL
            GOTO      SNAC9300
          ENDIF
.
          IF        DATETO >= DEFFFROM
            GOTO      SNAC9300
          ENDIF
.
SNAC1800  CALL      CLPMSC00                     * Clear File Variables
          CALL      MVPMSC00                     * move cgi to file
.
          CALL      WRPMSNC1                     * write auto classification
.
          MOVE      ZERO,EXIT
          GOTO      SNAC9999
.
.------------------------------------------------------------------------------
. Update Auto Classification Code Details
.------------------------------------------------------------------------------
SNAC2000  PACK      KEY20,PMSNC001,PMSNC002,PMSNC003,PMSNC005,SP70
          CALL      RSPMSNC1
          CALL      RPPMSNC1
          IF        OVRCD = 1
            GOTO      SNAC2800
          ENDIF
.
          MATCH     PMSNC001,PMSCCLCD
          GOTO      SNAC2800 IF NOT EQUAL
.
          MATCH     PMSNC002,PMSCVERS
          GOTO      SNAC2800 IF NOT EQUAL
.
          MATCH     PMSNC003,PMSCICDE
          GOTO      SNAC2800 IF NOT EQUAL
.
          MOVE      PMSNC004,DATEFROM
          MOVE      PMSNC005,DATETO
          MOVE      PMSCDEFF,DEFFFROM
          MOVE      PMSCDEFT,DEFFTO
.
          IF        DATEFROM = DEFFFROM
            GOTO      SNAC2800
          ENDIF
.
          IF        DATETO >= DEFFFROM
            GOTO      SNAC9300
          ENDIF
.
SNAC2800  PACK      KEY20,PMSNC001,PMSNC002,PMSNC003,PMSNC004,SP70
          CALL      RDPMSNC1
          IF        OVRCD = 1
            GOTO      SNAC9200
          ENDIF
.
          CALL      MVPMSC00                     * move cgi to file
          CALL      UPPMSNC1                     * update auto classification
.
SNAC2199  MOVE      ZERO,EXIT
          GOTO      SNAC9999
.------------------------------------------------------------------------------
. Delete Auto Classification Code
.------------------------------------------------------------------------------
SNAC3000  PACK      KEY20,PMSNC001,PMSNC002,PMSNC003,PMSNC004,SP70
          CALL      RDPMSNC1
          IF        OVRCD = 1
            GOTO      SNAC9200
          ENDIF
.
          CALL      DEPMSNC1                     * delete auto classification
.
          MOVE      ZERO,EXIT
          GOTO      SNAC9999
.
.------------------------------------------------------------------------------
. List Classification Code
.------------------------------------------------------------------------------
SNAC7000  CLEAR     WEBTITLE
          MOVE      "Auto Classification Code Maintenance",WEBTITLE
          RESET     WEBTITLE
.
SNAC7100  PACK      KEY20,PMSNC001,PMSNC002,PMSNC003,PMSNC004,SP70
          CALL      RDPMSNC1                 
          IF        OVRCD = 1
            CALL      CLPMSC00
          ENDIF
.
          CALL      WEBHED00                     * Create HTML File/Page Header
          BRANCH    EXIT,SNAC9999
          CALL      WEBBOD00                     * Process Web Body
          CALL      WEBEND00                     * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      SNAC9999
.
.------------------------------------------------------------------------------
SNAC9000  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SNAC9999
.
SNAC9100  MOVE      "Auto Code Record already exists on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SNAC9999
.
SNAC9200  MOVE      "Auto Code Record does not exist on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SNAC9999
.
SNAC9300  MOVE      "Auto Code Record overlaps with existing records.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SNAC9999
.
SNAC9999  RETURN
.
.*******************************************************************************
. Auto Classification Code Maintenance Table
.*******************************************************************************
ACTAB000  IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD          * Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY20,STARTKEY,SP70
          CALL      RSPMSNC1                     * Position on Startkey
          CALL      RPPMSNC1                     * Read one back from position
.
ACTAB100  CALL      RKPMSNC1                     * Read on first Startkey found
          BRANCH    OVRCD,ACTAB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      ACTAB100
          ENDIF
.
          CALL      ACROW000                     * Display Auto Class code Row
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      ACTAB100 IF NOT EQUAL
          GOTO      ACTAB999
.
ACTAB900  CALL      NOMORE00            * Display No More Records Message
.
ACTAB999  RETURN
.------------------------------------------------------------------------------
. Auto Classification Code Maintenance Table Row Detail
.------------------------------------------------------------------------------
ACROW000  PACK      PMSNDESC,SP70
          PACK      KEY5,PMSCCLCD,PMSCVERS,KEY70
          CALL      RDPMSNP1
          BRANCH    OVRCD,ACROW100
.
          PACK      DISPDM50,PMSNDESC,SP70
.
ACROW100  MOVE      SP70,DISPDACT
.
          MATCH     PMSCDEFF,SP70
          IF        !@EQUAL
            UNPACK    PMSCDEFF,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDACT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          MOVE      SP70,DISPDIAC
.
          MATCH     PMSCDEFT,SP70
          IF        !@EQUAL
            UNPACK    PMSCDEFT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDIAC,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
ACROW900  WRITE     HTMLFILE;"<tr><td nowrap>":
                             "<img src=../images/MaintenanceIcon.gif ":
                             "class=ListIcon ":
         "onclick=#"autoClassDetails('",PMSCCLCD:
                                  "','",PMSCVERS:
                                  "','",PMSCICDE:
                                  "','",DISPDACT,"');#"> ":
                             PMSCCLCD,"</td>":
                             "<td>",PMSCVERS,"</td>":
                             "<td>",DISPDM50,"</td>":
                             "<td>",PMSCICDE,"</td>":
                             "<td align=center>",DISPDACT,"</td>":
                             "<td align=center>",DISPDIAC,"&nbsp;</td>":
                             "</tr>"
.
ACROW999 RETURN
.
.******************************************************************************
.*                   Lawton's Assessment of Daily Living Maintenance          *
.******************************************************************************
LADL0000  MATCH     SP70,UPDTTYPE
          GOTO      LADL7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE                 * Add Lawton's Assessment
          GOTO      LADL1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE                 * Update Lawton's Assessment
          GOTO      LADL2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE                 * Delete Lawton's Assessment
          GOTO      LADL3000 IF EQUAL
.
          GOTO      LADL9000
.
.------------------------------------------------------------------------------
. Add new Lawton's Assessment of Daily Living
.------------------------------------------------------------------------------
LADL1000  PACK      KEY24,ADMISSNO,PMLAD002,PMLAD003,SP70
          CALL      RDPMLAD1
          IF        OVRCD <> 1
            GOTO      LADL9100
          ENDIF
.
          CALL      CLPLAD00                     * Clear File Variables
          CALL      MVPLAD00                     * move cgi to file
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,PMEMCDAT                * System date
          CLOCK     TIME,PMEMCTIM                * System time
          MOVE      USERID,PMEMCUID              * web user id
.
          CALL      WRPMLAD1                     * write new record
.
          MOVE      "A",ASSESSMT
          CALL      NACD0000                     * write Clinical Record
.
          MOVE      ZERO,EXIT
          GOTO      LADL9999
.
.------------------------------------------------------------------------------
. Update Lawton's Assessment of Daily Living
.------------------------------------------------------------------------------
LADL2000  PACK      KEY24,ADMISSNO,PMLAD002,PMLAD003,SP70
          CALL      RDPMLAD1
          IF        OVRCD = 1
            GOTO      LADL9200
          ENDIF
.
          CALL      MVPLAD00                     * move cgi to file
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,PMEMUDAT                * System date
          CLOCK     TIME,PMEMUTIM                * System time
          MOVE      USERID,PMEMUUID              * web user id
.
          CALL      UPPMLAD1                     * update details
.
          MOVE      ZERO,EXIT
          GOTO      LADL9999
.
.------------------------------------------------------------------------------
. Delete Lawton's Assessment of Daily Living
.------------------------------------------------------------------------------
LADL3000  PACK      KEY24,ADMISSNO,PMLAD002,PMLAD003,SP70
          CALL      RDPMLAD1
          IF        OVRCD = 1
            GOTO      LADL9200
          ENDIF
.
          CALL      DEPMLAD1
.
          MOVE      ZERO,EXIT
          GOTO      LADL9999
.
.------------------------------------------------------------------------------
. List Lawton's Assessment of Daily Living
.------------------------------------------------------------------------------
LADL7000  CALL      GETPAT00                     * Get Visit Details
          BRANCH    EXIT,LADL9999
.
          CLEAR     WEBTITLE
          MOVE      "Lawton's Assessment of Daily Living Maintenance",WEBTITLE
          RESET     WEBTITLE
.
LADL7100  PACK      KEY24,ADMISSNO,PMLAD002,PMLAD003,SP70
          CALL      RDPMLAD1
          IF        OVRCD = 1
            CALL      CLPLAD00
          ENDIF
.
          CALL      WEBHED00                     * Create HTML File/Page Header
          BRANCH    EXIT,LADL9999
          CALL      WEBBOD00                     * Process Web Body
.
          IF        MRGEFLAG=1
            CALL      MRGALT00
          ENDIF
.
          CALL      WEBEND00                     * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      LADL9999
.
LADL9000  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LADL9999
.
LADL9100  MOVE      "LADL Record already exists on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LADL9999
.
LADL9200  MOVE      "LADL Record does not exist on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LADL9999
.
LADL9999  RETURN
.
.*******************************************************************************
. Lawton's Assessment of Daily Living Table List
.*******************************************************************************
LADLS000  PACK      KEY24,ADMISSNO,SP70
          CALL      RSPMLAD1
LADLS100  CALL      RKPMLAD1
          BRANCH    OVRCD,LADLS999
.
          MATCH     PMLAVISN,ADMISSNO
          GOTO      LADLS999 IF NOT EQUAL
.
          UNPACK    PMLAADAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDACT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      ZERO,TEMPTOTL
          MOVE      PMLAFONE,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMLASHOP,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMLAFDPR,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMLAHKEP,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMLALAUN,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMLAMTRN,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMLARMED,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMLAFINC,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
.
          CLEAR     HTMLLINK
          APPEND    "javascript:LADLDetails('",HTMLLINK
          APPEND    PMLAVISN,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    DISPDACT,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMLAATIM,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
LADLS200  WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",PMLAVISN,"#",":
                             "#"",PMLAADAT,PMLAATIM,"#",":
                             "#"",PMLAFONE,"#",":
                             "#"",PMLASHOP,"#",":
                             "#"",PMLAFDPR,"#",":
                             "#"",PMLAHKEP,"#",":
                             "#"",PMLALAUN,"#",":
                             "#"",PMLAMTRN,"#",":
                             "#"",PMLARMED,"#",":
                             "#"",PMLAFINC,"#",":
                             "#"",TEMPTOTL,"#");"
.
          GOTO      LADLS100
.
LADLS999  RETURN
.
.*******************************************************************************
.*                     Motor Assessment Scale Maintenance
.*******************************************************************************
MOTA0000  MATCH     SP70,UPDTTYPE
          GOTO      MOTA7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE                 * Add Motor Assessment Scale
          GOTO      MOTA1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE                 * Update Motor Assessment Scale
          GOTO      MOTA2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE                 * Delete Motor Assessment Scale
          GOTO      MOTA3000 IF EQUAL
.
          GOTO      MOTA9000
.
.------------------------------------------------------------------------------
. Add new Motor Assessment Scale
.------------------------------------------------------------------------------
MOTA1000  PACK      KEY24,ADMISSNO,PMMOT002,PMMOT003,SP70
          CALL      RDPMMOT1
          IF        OVRCD <> 1
            GOTO      MOTA9100
          ENDIF
.
          CALL      CLPMMA00                     * Clear File Variables
          CALL      MVPMMA00                     * move cgi to file
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,PMMACDAT                * System date
          CLOCK     TIME,PMMACTIM                * System time
          MOVE      USERID,PMMACUID              * web user id
.
          CALL      WRPMMOT1                     * write new record
.
          PACK      UPDATEKY,ADMISSNO,PMMAADAT,PMMAATIM,SP70
          CALL      UPACM000                     * add Assess Comment
.
          MOVE      "M",ASSESSMT
          CALL      NACD0000                     * write Clinical Record
.
          MOVE      ZERO,EXIT
          GOTO      MOTA9999
.
.------------------------------------------------------------------------------
. Update Motor Assessment Scale
.------------------------------------------------------------------------------
MOTA2000  PACK      KEY24,ADMISSNO,PMMOT002,PMMOT003,SP70
          CALL      RDPMMOT1
          IF        OVRCD = 1
            GOTO      MOTA9200
          ENDIF
.
          CALL      MVPMMA00                     * move cgi to file
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,PMMAUDAT                * System date
          CLOCK     TIME,PMMAUTIM                * System time
          MOVE      USERID,PMMAUUID              * web user id
.
          CALL      UPPMMOT1                     * update details
.
          PACK      UPDATEKY,ADMISSNO,PMMAADAT,PMMAATIM,SP70
          CALL      UPACM000                     * update Assess comment
.
          MOVE      ZERO,EXIT
          GOTO      MOTA9999
.
.------------------------------------------------------------------------------
. Delete Motor Assessment Scale
.------------------------------------------------------------------------------
MOTA3000  PACK      KEY24,ADMISSNO,PMMOT002,PMMOT003,SP70
          CALL      RDPMMOT1
          IF        OVRCD = 1
            GOTO      MOTA9200
          ENDIF
.
          CALL      DEPMMOT1
.
          PACK      UPDATEKY,ADMISSNO,PMMAADAT,PMMAATIM,SP70
          CALL      CLACM000                     * del Assess comment
.
          MOVE      ZERO,EXIT
          GOTO      MOTA9999
.
.------------------------------------------------------------------------------
. List Motor Assessment Scale
.------------------------------------------------------------------------------
MOTA7000  CALL      GETPAT00                     * Get Visit Details
          BRANCH    EXIT,MOTA9999
.
          CLEAR     WEBTITLE
          MOVE      "Motor Assessment Scale Maintenance",WEBTITLE
          RESET     WEBTITLE
.
MOTA7100  PACK      KEY24,ADMISSNO,PMMOT002,PMMOT003,SP70
          CALL      RDPMMOT1
          IF        OVRCD = 1
            CALL      CLPMMA00
            MOVE      SP70,UPDATEKY
          ELSE
            PACK      UPDATEKY,ADMISSNO,PMMAADAT,PMMAATIM,SP70  * Assess comment
          ENDIF
.
          CALL      WEBHED00                     * Create HTML File/Page Header
          BRANCH    EXIT,MOTA9999
          CALL      WEBBOD00                     * Process Web Body
.
          IF        MRGEFLAG=1
            CALL      MRGALT00
          ENDIF
.
          CALL      WEBEND00                     * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      MOTA9999
.
MOTA9000  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MOTA9999
.
MOTA9100  MOVE      "Motor Assessment Scale Record already exists on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MOTA9999
.
MOTA9200  MOVE      "Motor Assessment Scale Record does not exist on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MOTA9999
.
MOTA9999  CLOSE     VSACTFIL,DELETE    * Delete temp file created by CLRACM00
          RETURN
+
.*******************************************************************************
. Motor Assessment Scale Maintenance Table List
.*******************************************************************************
MOTLS000  PACK      KEY24,ADMISSNO,SP70
          CALL      RSPMMOT1
MOTLS100  CALL      RKPMMOT1
          BRANCH    OVRCD,MOTLS999
.
          MATCH     PMMAVISN,ADMISSNO
          GOTO      MOTLS999 IF NOT EQUAL
.
          UNPACK    PMMAADAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDACT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          CLEAR     HTMLLINK
          APPEND    "javascript:MotAssDetails('",HTMLLINK
          APPEND    PMMAVISN,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    DISPDACT,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMMAATIM,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
MOTLS200  WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",PMMAVISN,"#",":
                             "#"",PMMAADAT,PMMAATIM,"#",":
                             "#"",PMMASUSL,"#",":
                             "#"",PMMASUSI,"#",":
                             "#"",PMMABASI,"#",":
                             "#"",PMMASIST,"#",":
                             "#"",PMMAWALK,"#",":
                             "#"",PMMAUARM,"#",":
                             "#"",PMMAHMOV,"#",":
                             "#"",PMMAAHAN,"#");"
          GOTO      MOTLS100
.
MOTLS999  RETURN
.
.******************************************************************************
.* Mini Mental State Examination Maintenance
.******************************************************************************
MMSA0000  MATCH     SP70,UPDTTYPE
          GOTO      MMSA7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE                 * Add Mini Mental State Exam
          GOTO      MMSA1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE                 * Update Mini Mental State Exam
          GOTO      MMSA2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE                 * Delete Mini Mental State Exam
          GOTO      MMSA3000 IF EQUAL
.
          GOTO      MMSA9000
.
.------------------------------------------------------------------------------
. Add new Mini Mental State Examination
.------------------------------------------------------------------------------
MMSA1000  PACK      KEY24,ADMISSNO,PMMMS002,PMMMS003,SP70
          CALL      RDPMMMS1
          IF        OVRCD <> 1
            GOTO      MMSA9100
          ENDIF
.
          CALL      CLPMMS00                     * Clear File Variables
          CALL      MVPMMS00                     * move cgi to file
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,PMMMCDAT                * System date
          CLOCK     TIME,PMMMCTIM                * System time
          MOVE      USERID,PMMMCUID              * web user id
.
          CALL      WRPMMMS1                     * write new record
.
          MOVE      "H",ASSESSMT
          CALL      NACD0000                     * write Clinical Record
.
          MOVE      ZERO,EXIT
          GOTO      MMSA9999
.
.------------------------------------------------------------------------------
. Update Mini Mental State Examination
.------------------------------------------------------------------------------
MMSA2000  PACK      KEY24,ADMISSNO,PMMMS002,PMMMS003,SP70
          CALL      RDPMMMS1
          IF        OVRCD = 1
            GOTO      MMSA9200
          ENDIF
.
          CALL      MVPMMS00                     * move cgi to file
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,PMMMUDAT                * System date
          CLOCK     TIME,PMMMUTIM                * System time
          MOVE      USERID,PMMMUUID              * web user id
.
          CALL      UPPMMMS1                     * update details
.
          MOVE      ZERO,EXIT
          GOTO      MMSA9999
.
.------------------------------------------------------------------------------
. Delete Mini Mental State Examination
.------------------------------------------------------------------------------
MMSA3000  PACK      KEY24,ADMISSNO,PMMMS002,PMMMS003,SP70
          CALL      RDPMMMS1
          IF        OVRCD = 1
            GOTO      MMSA9200
          ENDIF
.
          CALL      DEPMMMS1
.
          MOVE      ZERO,EXIT
          GOTO      MMSA9999
.
.------------------------------------------------------------------------------
. List Mini Mental State Examination
.------------------------------------------------------------------------------
MMSA7000  CALL      GETPAT00                     * Get Visit Details
          BRANCH    EXIT,MMSA9999
.
          CLEAR     WEBTITLE
          MOVE      "Mini Mental State Examination Maintenance",WEBTITLE
          RESET     WEBTITLE
.
MMSA7100  PACK      KEY24,ADMISSNO,PMMMS002,PMMMS003,SP70
          CALL      RDPMMMS1
          IF        OVRCD = 1
            CALL      CLPMMS00
          ENDIF
.
          CALL      WEBHED00                     * Create HTML File/Page Header
          BRANCH    EXIT,MMSA9999
          CALL      WEBBOD00                     * Process Web Body
.
          IF        MRGEFLAG=1
            CALL      MRGALT00
          ENDIF
.
          CALL      WEBEND00                     * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      MMSA9999
.
MMSA9000  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MMSA9999
.
MMSA9100  MOVE      "MMSE Record already exists on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MMSA9999
.
MMSA9200  MOVE      "MMSE Record does not exist on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MMSA9999
.
MMSA9999  RETURN
.
.*******************************************************************************
. Mini Mental State Examination Table List
.*******************************************************************************
MMSLS000  PACK      KEY24,ADMISSNO,SP70
          CALL      RSPMMMS1
MMSLS100  CALL      RKPMMMS1
          BRANCH    OVRCD,MMSLS999
.
          MATCH     PMMMVISN,ADMISSNO
          GOTO      MMSLS999 IF NOT EQUAL
.
          UNPACK    PMMMADAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDACT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
.                             * Calculate Mini Mental State Examination Score
          MOVE      ZERO,TEMPTOTL
          MOVE      PMMMORT1,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMMMORT2,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMMMREG1,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMMMREG2,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMMMATTN,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMMMRECL,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMMMLNG1,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMMMLNG2,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMMMLNG3,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMMMLNG4,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMMMLNG5,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMMMLNG6,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
.
          CLEAR     HTMLLINK
          APPEND    "javascript:MMSExamDetails('",HTMLLINK
          APPEND    PMMMVISN,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    DISPDACT,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMMMATIM,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
MMSLS200  WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",PMMMVISN,"#",":
                             "#"",PMMMADAT,PMMMATIM,"#",":
                             "#"",TEMPTOTL,"#");"
          GOTO      MMSLS100
.
MMSLS999  RETURN
.
.******************************************************************************
.* Modified Elderly Mobility Scale Maintenance
.******************************************************************************
EMSA0000  MATCH     SP70,UPDTTYPE
          GOTO      EMSA7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE                 * Add Mod Elderly Mob Scale
          GOTO      EMSA1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE                 * Update Mod Elderly Mob Scale
          GOTO      EMSA2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE                 * Delete Mod Elderly Mob Scale
          GOTO      EMSA3000 IF EQUAL
.
          GOTO      EMSA9000
.
.------------------------------------------------------------------------------
. Add new Modified Elderly Mobility Scale
.------------------------------------------------------------------------------
EMSA1000  PACK      KEY24,ADMISSNO,PMEMS002,PMEMS003,SP70
          CALL      RDPMEMS1
          IF        OVRCD <> 1
            GOTO      EMSA9100
          ENDIF
.
          CALL      CLPEMS00                     * Clear File Variables
          CALL      MVPEMS00                     * move cgi to file
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,PMEMCDAT                * System date
          CLOCK     TIME,PMEMCTIM                * System time
          MOVE      USERID,PMEMCUID              * web user id
.
          CALL      WRPMEMS1                     * write new record
.
          MOVE      "E",ASSESSMT
          CALL      NACD0000                     * write Clinical Record
.
          MOVE      ZERO,EXIT
          GOTO      EMSA9999
.
.------------------------------------------------------------------------------
. Update Modified Elderly Mobility Scale
.------------------------------------------------------------------------------
EMSA2000  PACK      KEY24,ADMISSNO,PMEMS002,PMEMS003,SP70
          CALL      RDPMEMS1
          IF        OVRCD = 1
            GOTO      EMSA9200
          ENDIF
.
          CALL      MVPEMS00                     * move cgi to file
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,PMEMUDAT                * System date
          CLOCK     TIME,PMEMUTIM                * System time
          MOVE      USERID,PMEMUUID              * web user id
.
          CALL      UPPMEMS1                     * update details
.
          MOVE      ZERO,EXIT
          GOTO      EMSA9999
.
.------------------------------------------------------------------------------
. Delete Modified Elderly Mobility Scale
.------------------------------------------------------------------------------
EMSA3000  PACK      KEY24,ADMISSNO,PMEMS002,PMEMS003,SP70
          CALL      RDPMEMS1
          IF        OVRCD = 1
            GOTO      EMSA9200
          ENDIF
.
          CALL      DEPMEMS1
.
          MOVE      ZERO,EXIT
          GOTO      EMSA9999
.
.------------------------------------------------------------------------------
. List Modified Elderly Mobility Scale
.------------------------------------------------------------------------------
EMSA7000  CALL      GETPAT00                     * Get Visit Details
          BRANCH    EXIT,EMSA9999
.
          CLEAR     WEBTITLE
          MOVE      "Modified Elderly Mobility Scale Maintenance",WEBTITLE
          RESET     WEBTITLE
.
EMSA7100  PACK      KEY24,ADMISSNO,PMEMS002,PMEMS003,SP70
          CALL      RDPMEMS1
          IF        OVRCD = 1
            CALL      CLPEMS00
          ENDIF
.
          CALL      WEBHED00                     * Create HTML File/Page Header
          BRANCH    EXIT,EMSA9999
          CALL      WEBBOD00                     * Process Web Body
.
          IF        MRGEFLAG=1
            CALL      MRGALT00
          ENDIF
.
          CALL      WEBEND00                     * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      EMSA9999
.
EMSA9000  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMSA9999
.
EMSA9100  MOVE      "EMS Record already exists on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMSA9999
.
EMSA9200  MOVE      "EMS Record does not exist on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMSA9999
.
EMSA9999  RETURN
.
.*******************************************************************************
. Modified Elderly Mobility Scale Table List
.*******************************************************************************
EMSLS000  PACK      KEY24,ADMISSNO,SP70
          CALL      RSPMEMS1
EMSLS100  CALL      RKPMEMS1
          BRANCH    OVRCD,EMSLS999
.
          MATCH     PMEMVISN,ADMISSNO
          GOTO      EMSLS999 IF NOT EQUAL
.
          UNPACK    PMEMADAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDACT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      ZERO,TEMPTOTL
          MOVE      PMEMLYSI,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMEMSILY,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMEMSIST,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMEMSTAN,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMEMGAIT,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMEMTWLK,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMEMFUNR,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
          MOVE      PMEMSTPS,TEMPNUMB
          ASSIGN    TEMPNUMB+TEMPTOTL,TEMPTOTL
.
          CLEAR     HTMLLINK
          APPEND    "javascript:MEMScaleDetails('",HTMLLINK
          APPEND    PMEMVISN,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    DISPDACT,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMEMATIM,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
EMSLS200  WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",PMEMVISN,"#",":
                             "#"",PMEMADAT,PMEMATIM,"#",":
                             "#"",PMEMLYSI,"#",":
                             "#"",PMEMSILY,"#",":
                             "#"",PMEMSIST,"#",":
                             "#"",PMEMSTAN,"#",":
                             "#"",PMEMGAIT,"#",":
                             "#"",PMEMTWLK,"#",":
                             "#"",PMEMFUNR,"#",":
                             "#"",PMEMSTPS,"#",":
                             "#"",PMEMGUGO,"#",":
                             "#"",TEMPTOTL,"#");"
.
          GOTO      EMSLS100
.
EMSLS999  RETURN
.
.*******************************************************************************
. Get Clinical Document Details from htmlline input
.*******************************************************************************
GETCLD00  ADD       ONE,HTMLCNT
          PACK      HTMLLINE,QRYDATA,SP70,SP70
          WRITE     TEMPFILE,SEQ;HTMLLINE
.
GETCLD10  READ      CONFFILE,SEQ;QRYNAME,QRYDATA * Read CONFFILE sequentially
          GOTO      GETCLD99 IF OVER             * If end of file
.
          MATCH     SP70,QRYNAME                 * If next CGI parameter
          GOTO      GETCLD80 IF NOT EQUAL
.
          ADD       ONE,HTMLCNT
          PACK      HTMLLINE,QRYDATA,SP70,SP70
          WRITE     TEMPFILE,SEQ;HTMLLINE
          GOTO      GETCLD10
.
GETCLD80  MOVE      ONE,TEXTAREA                 * Flag for Next CGI Parameter
.
GETCLD99  RETURN
.
.*******************************************************************************
. New Assessment Clinical Document
.*******************************************************************************
NACD0000  CALL      CLNACD00                     * Clear Clinical Doc Details
          MOVE      ONE,CORRESID
.
          PACK      KEY20,URNUMBER,ADMISSNO,Z70
          CALL      RSOBPC1
          CALL      RPOBPC1                      * Positions on last value
          BRANCH    OVRCD,NACD1000
.
          MATCH     URNUMBER,OBPCURNO            * Check for UR number change
          GOTO      NACD1000 IF NOT EQUAL
.
          MATCH     ADMISSNO,OBPCCVIS            * Check for Visit number change
          GOTO      NACD1000 IF NOT EQUAL
.
          MOVE      OBPCCPID,CORRESID            * Update Clinical Doc ID
          ADD       ONE,CORRESID
.
NACD1000  MOVE      CORRESID,OBPCCPID            * Add Clinical Doc Record
          MOVE      ADMISSNO,OBPCCVIS
          MOVE      URNUMBER,OBPCURNO
.
          CALL      IBACLOCK
          PACK      OBPCDTLU,CCC,CYY,CMM,CDD     * Last update date
          REP       " 0",OBPCDTLU
          MOVE      CTIMEIS,OBPCTMLU             * Last Update time
          MOVE      OBPCDTLU,OBPCINDT            * Set Input date
          MOVE      OBPCTMLU,OBPCINTM            * Input time
.
          PACK      KEY5,CATWI,SP70              * Get Type of Clinical Doc
          CALL      RDSCODE1
NACD2000  CALL      RDKCODE1
          BRANCH    OVRCD,NACD3000
.
          MATCH     CATWI,TCODE
          GOTO      NACD3000 IF NOT EQUAL
.
          MATCH     ASSESSMT,TCINDC1
          GOTO      NACD4000 IF EQUAL
.
          GOTO      NACD2000
.
NACD3000  MOVE      SP70,OBPCCTYP                * No Clinical Doc Type found
          GOTO      NACD5000
.
NACD4000  MOVE      ACODE,OBPCCTYP               * Type of Clinical Doc
.
NACD5000  MOVE      USERID,OBPCINUS              * Web User ID
          MOVE      USERID,OBPCLUID              * Last Web User ID
          MOVE      ZERO,OBPCMDEL                * Delete indicator not set
.
          COMPARE   ZERO,HTMLCNT
          GOTO      NACD9200 IF EQUAL
          CLEAR     TFILENME
          APPEND    TEMPNAME,TFILENME
          APPEND    ".html",TFILENME
          RESET     TFILENME
.
          SCAN      ".",TFILENME
          MOVE      TFILENME,OBPCFEXT
          RESET     TFILENME
.
          PACK      CLINDOCM,OBPCCVIS,DASH,OBPCCPID,OBPCFEXT
          REP       " 0",CLINDOCM
.
          READ      CONTROLF,NINTY1;*219,CWEBCLOC
          PACK      KEY4,CWEBCLOC
          CALL      RDOBPT1
          BRANCH    OVRCD,NACD9100
.
          MOVE      OBPTSLID,OBPCSLID            * Storage Location ID
          STRIP     OBPTSDIR
          PACK      CMDLINE,CMDMOVE,TFILENME,SP1,OBPTSDIR,CLINDOCM
          EXECUTE   CMDLINE,TASKID
.
          MOVE      ADMISSNO,OBPCCVIS
NACD8000  PACK      KEY12,OBPCCVIS,OBPCCPID
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WROBPC1
          TRAPCLR   IO
          IF        OVRCD=1
            ADD       ONE,CORRESID
            MOVE      CORRESID,OBPCCPID          * Clinical Doc ID
            GOTO      NACD8000
          ENDIF
.
          MOVE      ANSA,OBAUTYPE           * Add
          CALL      OBSAUD00                * Clinical Documents Audit
.
          PACK      KEY8,URNUMBER                * Update Patient Master
          CALL      RDMAST1
          BRANCH    OVRCD,NACD9999
          MATCH     "1",PTMXSIN4
          IF        !@EQUAL
            PACK      KEY8,URNUMBER
            CALL      RLPTMAS1
            BRANCH    OVRCD,NACD9999,NACD9999
            MOVE      "1",PTMXSIN4
            CALL      UPMAST1
            CALL      UUPTMAS1
          ENDIF
          GOTO      NACD9999
.
NACD9100  CLEAR     WEBTITLE
          APPEND    "Invalid Storage Location ID From Control File - ",WEBTITLE
          APPEND    KEY4,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NACD9999
.
NACD9200  MOVE      "Invalid Clinical Document File - ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NACD9999
.
NACD9999  CLOSE     TEMPFILE,DELETE
          RETURN
.
.*******************************************************************************
. Clear Clinical Document Details
.*******************************************************************************
CLNACD00  MOVE      SP70,OBPCURNO                 * U/R Number
          MOVE      SP70,OBPCCVIS                 * Visit Number
          MOVE      SP70,OBPCCPID                 * Correspondance ID
          MOVE      SP70,OBPCINDT                 * Input Date
          MOVE      SP70,OBPCCTYP                 * Type of Correspondance
          MOVE      SP70,OBPCINTM                 * Time hh:mm:ss
          MOVE      SP70,OBPCINUS                 * Input Web User ID
          MOVE      SP70,OBPCFEXT                 * File Extension
          MOVE      SP70,OBPCMDEL                 * Marked as Deleted
          MOVE      SP70,OBPCSLID                 * Storage Location ID
          MOVE      SP70,OBPCSLEV                 * Security Level
          MOVE      SP70,OBPCCOM1                 * Comment 1
          MOVE      SP70,OBPCCOM2                 * Comment 2
          MOVE      SP70,OBPCCFRM                 * From
          MOVE      SP70,OBPCCTOO                 * To
          MOVE      SP70,OBPCUDF1                 * User Def Code 1 (Cat xx)
          MOVE      SP70,OBPCUDF2                 * User Def Code 2 (Cat xx)
          MOVE      SP70,OBPCUYN1                 * User Def Y/N 1
          MOVE      SP70,OBPCUYN2                 * User Def Y/N 2
          MOVE      SP70,OBPCUDD1                 * User Def Date 1
          MOVE      SP70,OBPCUDD2                 * User Def Date 2
          MOVE      SP70,OBPCDTLU                 * Date Last Update
          MOVE      SP70,OBPCTMLU                 * Time Last Update
          MOVE      SP70,OBPCLUID                 * Last Web User ID
          MOVE      SP70,OBPCACAT                 * Alert File Category (H1-H9)
          MOVE      SP70,OBPCACOD                 * Alert File Code (for catgry)
          MOVE      SP70,OBPCCNTR                 * Alert File Counter
          MOVE      SP70,OBPCSPAR                 * Spare
.
CLNACD99  RETURN
.*******************************************************************************
. Get Formatted Patient Name for Clinical Document
.*******************************************************************************
FNAM0000  CALL      GETPAT00                      * Get Patient/Visit Details
          BRANCH    EXIT,FNAM9999
.                                                 * Format Patient Name
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          WRITE     HTMLFILE;PACFNAME;
.
FNAM9999 RETURN
.*******************************************************************************
. Get Indigenous Status for Clinical Document
.*******************************************************************************
ABRG0000  PACK      KEY8,ADMISSNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,ABRG9999
.
          MOVE      CATVA,CATEGORY
          MOVE      PMPXABRG,CODE
          CALL      CODITM00
.
ABRG9999  RETURN
.                                                 * Get Insurance Type
.*******************************************************************************
. Get Insurance Type for Clinical Document
.*******************************************************************************
INST0000  PACK      KEY8,ADMISSNO,SP70
          CALL      RDPTMIS1
          BRANCH    OVRCD,INST9999
.
          MOVE      SP70,CLAMDESC
          MATCH     SP70,ACLAIM
          IF        !@EQUAL
            PACK      KEY5,CATCL,ACLAIM,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      ACLAIM,TDESC
            ENDIF
            MOVE      TDESC,CLAMDESC
          ENDIF
          WRITE     HTMLFILE;CLAMDESC;
.
INST9999 RETURN
.
.-------------------------------------------------------------------------------
. Get Patient Visit details
.-------------------------------------------------------------------------------
GETPAT00  MOVE      SP70,DOCTCODE
          CALL      IBACLOCK
          CALL      CLPATMAS
          CALL      CLPATMIS
          CALL      CLEMRVIS
          CALL      CLALLREF
          CALL      CLPATDSC
          CALL      CLPMSPX2
          CALL      CLPMSNUT
          CALL      CLPATHSP
          CALL      CLPMSVX1
.
          REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
.
          MOVE      URNUMBER,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,GETPAT90
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          IF        PSTAT=1 | PSTAT=2
            MOVE      PURNO,SAVEURNO
            MOVE      PPSNAME,URNUMBER
            MOVE      ONE,MRGEFLAG               * Alert for merged UR
            GOTO      GETPAT00
          ENDIF
.
          CALL      FNVIS000
          BRANCH    EXIT,GETPAT99
.
          GOTO      GETPAT99
.
GETPAT90  MOVE      "Unable to find patient master details",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      GETPAT99
.
GETPAT99  RETURN
.-------------------------------------------------------------------------------
. Identify the visit type
.-------------------------------------------------------------------------------
FNVIS000  MOVE      ADMISSNO,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,FNVIS020
.
          BRANCH    PVITYPE,FNVIS010:            * Emergency
                            FNVIS020:            * Outpatient
                            FNVIS030             * Inpatient
.
          COMPARE   NINE,PVITYPE
          IF        !@EQUAL
            GOTO      FNVIS900
          ENDIF
.
          MATCH     "10",PTVITYPE                * Allied health
          IF        @EQUAL
            GOTO      FNVIS100
          ENDIF
.
          GOTO       FNVIS900
.
.----------------- A & E -------------------------------------------------------
FNVIS010  PACK      KEY8,PVIBILL,SP70
          CALL      RDEMVIS1
          BRANCH    OVRCD,FNVIS900
.
          COMPARE   FOUR,EMVISTAT                * ignore cancelled visit
          GOTO      FNVIS900 IF EQUAL
.
          MOVE      EMVIDATE,VISITDTE
          MOVE      EMVITIME,VISITTME
          GOTO      FNVIS998
.
.----------------- Outpatient --------------------------------------------------
FNVIS020  PACK      KEY36,ADMISSNO,SP70
          CALL      RDSBOKA6
FNVIS021  CALL      RDKBOKA6
          BRANCH    OVRCD,FNVIS900
.
          MATCH     DOBAOUTN,ADMISSNO             * Check if same Admission
          GOTO      FNVIS900 IF NOT EQUAL
.
          MOVE      OBADATE,VISITDTE
          PACK      VISITTME,OBASTRT,EXPNDTIM,SP70
          GOTO      FNVIS998
.
.----------------- Inpatient----------------------------------------------------
FNVIS030  PACK      KEY8,PVIBILL,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,FNVIS900
.                                                * Not Pre-admission | Cancelled
          IF        ASTAT=1|ASTAT=5
            GOTO      FNVIS900
          ENDIF
.
          MOVE      ADATE,VISITDTE
          MOVE      ATIME,VISITTME
          GOTO      FNVIS998
.
.----------------- Allied health -----------------------------------------------
FNVIS100  PACK      KEY8,PVIBILL,SP70
          CALL      RDALREF1
          BRANCH    OVRCD,FNVIS900
.
          MOVE      ALREDACT,VISITDTE
          PACK      VISITTME,SP70
          GOTO      FNVIS998
.
.----------------- No valid Visit Found ----------------------------------------
FNVIS900  MOVE      "Unable to find patient visit details",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      FNVIS999
.
FNVIS998  MOVE      ZERO,EXIT
.
FNVIS999  RETURN
.
.------------------------------------------------------------
. Clinical Documents Audit: Add, Update, View and Delete
. Input - OBAUTYPE (A,U,V,D)
.------------------------------------------------------------
OBSAUD00  MOVE      OBPCURNO,OBAUURNO
          MOVE      OBPCCVIS,OBAUVISN
          MOVE      OBPCCPID,OBAUCORR
          CALL      IBACLOCK
          PACK      OBAUDATE,CCC,CYY,CMM,CDD
          REP       " 0",OBAUDATE
          PACK      OBAUTIME,CTIMEIS
          MOVE      USERID,OBAUWEBU
          MOVE      SP70,OBAUREAS
          MOVE      SP70,OBAUORNO
          MOVE      SP70,OBAUOVIS
          MOVE      SP70,OBAUOCOR
          PACK      OBAUSPAR,SP70
.
          MATCH     ANSU,OBAUTYPE
          IF        @EQUAL
            MOVE      OBPCUDF1,OBAUREAS
          ENDIF
.
          PACK      KEY47,OBAUURNO,OBAUVISN,OBAUCORR,OBAUDATE:
                          OBAUTIME,OBAUWEBU,OBAUTYPE,SP70
          CALL      RAOBAUD1
          IF        OVRCD=1
            CALL      WROBAUD1
          ENDIF
.
OBSAUD99  RETURN
+
.0841644-start
.*******************************************************************************
. Impairment Code Maintenance
.*******************************************************************************
IGRP0000  MATCH     SP70,UPDTTYPE
          GOTO      IGRP7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE                 * Add Impairment Group
          GOTO      IGRP1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE                 * Update Impairment Group
          GOTO      IGRP2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE                 * Delete Impairment Group
          GOTO      IGRP3000 IF EQUAL
.
          GOTO      IGRP9000
.
.------------------------------------------------------------------------------
. Add new Impairment Group
.------------------------------------------------------------------------------
IGRP1000  PACK      KEY3,PMIGR001,SP70
          CALL      RDPMIGR1
          IF        OVRCD <> 1
            GOTO      IGRP9100
          ENDIF
.
          CALL      CLPMIG00                     * Clear File Variables
          CALL      MVPMIG00                     * move cgi to file
.
          CALL      WRPMIGR1                     * write new record
.
          PROC      PMSGKIUP                     * generate keywords
.
          MOVE      ZERO,EXIT
          GOTO      IGRP9999
.
.------------------------------------------------------------------------------
. Update Impairment Group Details
.------------------------------------------------------------------------------
IGRP2000  PACK      KEY3,PMIGR001,SP70
          CALL      RDPMIGR1
          IF        OVRCD = 1
            GOTO      IGRP9200
          ENDIF
.
          CALL      MVPMIG00                     * move cgi to file
.
          CALL      UPPMIGR1                     * update details
.
          PROC      PMSGKIUP                     * generate keywords
.
          MOVE      ZERO,EXIT
          GOTO      IGRP9999
.
.------------------------------------------------------------------------------
. Delete Impairment Group
.------------------------------------------------------------------------------
IGRP3000  PACK      KEY3,PMIGR001,SP70
          CALL      RDPMIGR1
          IF        OVRCD = 1
            GOTO      IGRP9200
          ENDIF
.
          CALL      DEPMIGR1
.
          PROC      PMSGKIUP                     * generate keywords
.
          MOVE      ZERO,EXIT
          GOTO      IGRP9999
.
.------------------------------------------------------------------------------
. List Impairment Group
.------------------------------------------------------------------------------
IGRP7000  CLEAR     WEBTITLE
          MOVE      "Impairment Group Maintenance",WEBTITLE
          RESET     WEBTITLE
.
IGRP7100  PACK      KEY3,PMIGR001,SP70
          CALL      RDPMIGR1
          IF        OVRCD = 1
            CALL      CLPMIG00
          ENDIF
.
          CALL      WEBHED00                     * Create HTML File/Page Header
          BRANCH    EXIT,IGRP9999
          CALL      WEBBOD00                     * Process Web Body
          CALL      WEBEND00                     * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      IGRP9999
.
IGRP9000  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      IGRP9999
.
IGRP9100  MOVE      "Group Record already exists on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      IGRP9999
.
IGRP9200  MOVE      "Group Record does not exist on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      IGRP9999
.
IGRP9999  RETURN
.
.*******************************************************************************
. Impairment Group Maintenance Table
.*******************************************************************************
IGTAB000  IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD          * Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY3,STARTKEY,SP70
          CALL      RSPMIGR1                     * Position on Startkey
          CALL      RPPMIGR1                     * Read one back from position
.
IGTAB100  CALL      RKPMIGR1                     * Read on first Startkey found
          BRANCH    OVRCD,IGTAB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      IGTAB100
          ENDIF
.
          CALL      IGROW000                     * Display Impairment Group Row
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      IGTAB100 IF NOT EQUAL
          GOTO      IGTAB999
.
IGTAB900  CALL      NOMORE00               * Display No More Records Message
.
IGTAB999  RETURN
.------------------------------------------------------------------------------
.  Impairment Group Maintenance Table Row Detail
.------------------------------------------------------------------------------
IGROW000  WRITE     HTMLFILE;"<tr><td nowrap>":
                             "<img src=../images/MaintenanceIcon.gif ":
                             "class=ListIcon ":
                             "onclick=#"ImpGrpDetails('",PMIGCODE,"');#"> ":
                             PMIGCODE,"</td>":
                             "<td>",PMIGDESC,"</td>":
                             "</tr>"
.
IGROW999 RETURN
+
.*******************************************************************************
. Impairment Group Selection List
.*******************************************************************************
IGSEL000  PACK      KEY3,SP70
          CALL      RSPMIGR1                     * Position 
IGSEL100  CALL      RKPMIGR1                     * Read next 
          BRANCH    OVRCD,IGSEL999
.
          MATCH     PMIMP013,PMIGCODE                                      
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"",PMIGCODE,"#" selected>":
                                 PMIGDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=#"",PMIGCODE,"#">":
                                 PMIGDESC,"</option>"
          ENDIF
.
          GOTO      IGSEL100 
.
IGSEL999  RETURN
.------------------------------------------------------------
. NOMORE00 - Display "No more records" at end of list
.------------------------------------------------------------
NOMORE00  WRITE     HTMLFILE;"<tr>":
                             "<td class=RedCell colspan=20 align=center>":
                             "No more records</td>"
.
NOMORE99  RETURN
+
.*******************************************************************************
. Generic Include List
.*******************************************************************************
.
          INC       IBAWEBCD
          INC       WEBDATCD
.
.0841644-start
          INC       PMSIGRTM                     * Impairment Group tags
          INC       PMSIGKPR                     * Impairment Group Keywords
.0841644-end  
. 
          INC       PMSIMPTM                     * Impairment Code tags
          INC       PMSEMSTM                     * Mod. Elderly Mob. Scale tags
          INC       PMSLADTM                     * Lawton's Assess of Dly Living
          INC       PMSMMSTM                     * Mini Mental State Exam tags
          INC       PMSMOTTM                     * Motor Assessment Scale tags
          INC       PMSSNPTM                     * Classification Code tags
          INC       PMSSNCTM                     * Auto Classification Code tags
          INC       PMSJKIPR                     * Impairment Code key words
          INC       PMSSKIPR                     * Classification code keywords
          INC       VISACMCD                     * Assessment Visit Comments
.
          INC       DAYOFWEK
          INC       CALCAGE
          INC       CLNHIMAS                     * Clear nhimasaf file variables
          INC       CLPMSPX2
          INC       NAMSTRCD
          INC       PATDOCCD
          INC       PATDOCTM
          INC       PATDSCTM
          INC       PATMASTM                     * Patient Master tags
          INC       PATMSXTM
          INC       PMSHCGTM
          INC       PMSHCPTM
          INC       PMSPX2TM
          INC       PMSNUTTM

.
          INC       CLPATMAS
          INC       CLPATMIS
          INC       CLEMRVIS
          INC       CLPMSVX1
          INC       CLPATHSP
          INC       CLPATDSC
          INC       CLPMSNUT
          INC       CLALLREF
.
.0841644-start
          INC       PMSGKIIO/INC                 * Impairment Group Key Words
          INC       PMSIGRIO/INC                 * Impairment Group
.0841644-end
          INC       PMSIKIIO/INC                 * Impairment Code Key Words
          INC       PMSIMPIO/INC                 * Impairment Code
          INC       PMSEMSIO/INC	         * Mod. Elderly Mob. Scale
          INC       PMSLADIO/INC                 * Lawton's Assess of Dly Living
          INC       PMSMMSIO/INC	         * Mini Mental State Exam
          INC       PMSMOTIO/INC	         * Motor Assessment Scale
          INC       PMSSKIIO/INC                 * Classification code keywords
          INC       PMSSNPIO/INC                 * Classification code
          INC       PMSSNCIO/INC                 * Classification code
          INC       VISACMIO/INC                 * Assessment Comments
.
          INC       ALLREFIO/INC                 * Referrals File
          INC       APSMASIO/INC
          INC       EMRVISIO/INC                 * Emergency Visits File
          INC       MRTMASIO/INC
          INC       NHIETHIO/INC
          INC       NHIMASIO/INC
          INC       OBSAUDIO/INC
          INC       OBSPCOIO/INC                 * Patient Correspondence
          INC       OBSPCTIO/INC                 * Correspondence Storage Location
          INC       OUTBOAIO/INC                 * Outpatients Visits
          INC       OUTBOCIO/INC                 * Outpatients Visits
          INC       OUTCLIIO/INC                 * Outpatients Clinic
          INC       OUTCTYIO/INC                 * Outpatients Clinic Type
          INC       OUTHEDIO/INC                 * Outpatients
          INC       OUTMA1IO/INC                 * Outpatients Master File
          INC       OUTSITIO/INC                 * Outpatients Site File
          INC       OUTSESIO/INC                 * Outpatients Sessions
          INC       PATALRIO/INC
          INC       PATASFIO/INC
          INC       PATCMCIO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATDADIO/INC
          INC       PATDGWIO/INC
          INC       PATDO1IO/INC                 * Doctor File
          INC       PATDSCIO/INC
          INC       PATFN1IO/INC
          INC       PATHSPIO/INC                 * Hospital file
          INC       PATIN1IO/INC
          INC       PATITMIO/INC
          INC       PATMA1IO/INC                 * Patient Master File
          INC       PATMI1IO/INC                 * Patient Admission File
          INC       PATPR1IO/INC
          INC       PATRE1IO/INC
          INC       PATVADIO/INC
          INC       PATVISIO/INC                 * Patient Visit File
          INC       PATWR1IO/INC
.
          INC       PMSCCDIO/INC   * Concession Card Details
          INC       PMSHCGIO/INC
          INC       PMSHCPIO/INC
          INC       PMSNUTIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC                 * Visit Extension File
          INC       VISCMTIO/INC                 * Visit Comments File
.

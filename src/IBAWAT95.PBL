.*******************************************************************************
.  System       : Waiting List
.  Program      : IBAWAT95
.  Function     : Emergency flag clear for patient master file.
.*******************************************************************************
.  Date         : 11/08/89
.  Author       : MONIKA OHLEITER (I.B.A)
.  MODIFICATIONS: 
.******************************************************************************
.* V12.00.01 29/05/2025  J.Tan          TSK 0955096                           *
.*           Added Alphanumeric visit number changes                          *
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V9.03.01  29/08/2003 J.Tan   CAR 42933                              *
.*                  Recompiled for PATSRCH                                    *
.*        V5.07.01  27/10/1999 J Habasque                                     *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
.         V5.01.02  30/08/89 Graeme Williams
.                   New PATCOMN1 routine
.         V5.01.03  02/10/89 L.P.
.                   Recompiled with new WATAUXFD.INC(V5.01) SRF102514
.         V5.01.10  04/07/90 Bill Dew
.*******************************************************************************
.    Programmers notes and developement changes
.    05/07/1990 Bill Dew : As soon as I saw MMO wrote this prog I decided it
.    was a re-write.
.    Option 2 as and enhancement display all the 
.    procedure details on screen and allow operator to directly select 
.    the record to be released.  Otherwise the current method means 
.    known records are unlocked.
.    13/08/1990 : Ensure Operator enters correct patient treatment details
.*******************************************************************************
.                   Re-write IBAWAT95 to new waiting list system specifications
.                   and standards.
.                   25/07/90 Paul Duncan         
.                   implemented second surname file
.                   03/08/90 Justin Coates
.                   Added the use of WTCNTPDS : Treatment/Procedure
.         V5.01.121 07/11/90 Karin Peak
.                   Fixed the screen display in WATDSTRE                       
.         V5.01.122 24/01/91 Justin Coates
.                   Changed to use BOKRC1FD (rather then BOKMASFD)
.                   V5.01.02  23/05/91  Steve O'Gorman
.                             Fixed Errors found by the New Compiler 
.         V5.01.123 26/11/91 Dude              
.                   Increment version
. As soon as i saw Bill Dew had re-written this code i thought 'SHIT!'
.         V5.01.124 26/08/93  RODdude          
.                   Recompile for NZHIS 
.         V5.01.125 27/05/1994 Ian Rutt                                      
.                   Fixed Global Recompile Bugs                              
.                                                                   
.
.
          INC       STD001FD
          INC       NZHISIFD/INC                 * Include for NZHIS vars
          INC       NHIMASFD/INC                 * Include for NZHIS vars
          INC       PATCONFD/INC 
          INC       PATCALAG/INC
.
          INC       OUTPREFD/INC
          INC       PATCOMM/INC
          INC       PATDHEAD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATPR1FD/INC
          INC       PATGSRFD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
.
          INC       BOKRC1FD/INC
.
          INC       WATCONFD/INC
          INC       WATTR1FD/INC
          INC       WATPROFD/INC
.*******************************************************************************
.                            VARIABLES DECLARATION
ADMISNUM  DIM       8
.
BJDAY     FORM      3
CJDAY     FORM      3
.
COL       FORM      2
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
SCNDGNAM  DIM       40
VERT      FORM      2
NMPNUMB   DIM       20
PATNAME   DIM       40
PROCODE   DIM       9        * procedure code
PROCOUNT  FORM      2        * procedure counter
SCRNFLAG  FORM      1
NUM37     FORM      "37"
.
PRGID     INIT      "IBAWAT95"
PRGNAM    INIT      "Emergency Flag Clear"
VERSION   INIT      "V12.01.00"
.******************************************************************************
.                        MAINLINE Rock & Roll
.******************************************************************************
ML0000
          CALL      INIT0000           * initialise variables
ML0110
          CALL      MENU0000
          BRANCH    MOPTION,ML1000,ML2000,ML3000
ML9999
          CHAIN     PGM
ML1000                                 * Unlock patient master 
          CALL      CLRV0000           * clear program vars
          CALL      SCRN1000
          CALL      GURN0000           * get UR number
          BRANCH    EXIT,ML0110
          CALL      UNLK0000           * unlock patient master record
          GOTO      ML1000
ML2000
          CALL      CLRV0000           * clear program vars
          CALL      SCRN1000
          CALL      GURN0000           * get UR number
          BRANCH    EXIT,ML0110
ML2222    CALL      GPRO0000           * patient procedure
          BRANCH    EXIT,ML2000
ML2444    CALL      PCNT0000           * get procedure count
          BRANCH    EXIT,ML2222
          CALL      CONTQST
          BRANCH    CEXIT,ML2666,ML2444,ML0110
ML2666    CALL      UNLK0000           * unlock patient procedure details file
          GOTO      ML2444
ML3000
          CALL      CLRV0000           * clear program vars
          CALL      SCRN1000
          CALL      GADM0000           * get admission number
          BRANCH    EXIT,ML0110
          CALL      UNLK0000           * unlock patient master record
          GOTO      ML3000
.******************************************************************************
.                                 INIT0000
.    Initialise files in use.
.******************************************************************************
INIT0000
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN1,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
          DISPLAY   *P64:24,"watproaf";
          OPEN      WATPROA1,"watproaf"
          OPEN      WATPROA3,"watproaf"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
.
          DISPLAY   *P64:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,NUM37;*185,WTCNTPDS
.
          MOVE      ONE,CNEWDS
INIT9999  RETURN
.******************************************************************************
.                                 MENU0000
.******************************************************************************
MENU0000  HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,"0",*HOFF," = Exit":
                    *P2:5,*V2LON,"1",*HOFF," = U/R Number":
                    *P2:6,*V2LON,"2",*HOFF," = Patient ",WTCNTPDS:
                    *P2:7,*V2LON,"3",*HOFF," = Booking Number":
                    *P1:9,"Select Option : _"
MENU1111
          KEYIN     *P17:9,*V2LON,MOPTION
          COMPARE   ZERO,MOPTION
          GOTO      MENU9999 IF EQUAL
          BRANCH    MOPTION,MENU2222,MENU3333,MENU4444
          BEEP
          GOTO      MENU1111
MENU2222
          DISPLAY   *P50:2,*V2LON,"- U/R Number "
          GOTO      MENU9999
MENU3333
          DISPLAY   *P50:2,*V2LON,"- Patient ",*+,WTCNTPDS,SP1
          GOTO      MENU9999
MENU4444
          DISPLAY   *P50:2,*V2LON,"- Booking Number "
MENU9999  RETURN
.******************************************************************************
CLRV0000
          UNPACK    SP30,PROCODE,PURNO
          PACK      WPDESC,SP30,SP30
          PACK      PATNAME,SP30,SP30
          MOVE      ZERO,PROCOUNT
CLRV9999  RETURN
.******************************************************************************
.                                 GURN0000
.    INPUT U/R NUMBER by calling KURN 
.******************************************************************************
GURN0000
          MOVE      TEN1,CVERT
          MOVE      "19",CCOL
          MOVE      TWO,SRCHOPT
          MOVE      ZERO,SRCHSYS
.
          CALL      KURN               * get UR number
          BRANCH    EXIT,GURN9999
          BRANCH    OVRCD,GURN8888
.
          CALL      PATN0000           * setup patients name
          DISPLAY   *P31:11,PATNAME 
          MOVE      ZERO,EXIT
          GOTO      GURN9999
GURN8888
          DISPLAY   *P1:24,*B,*EL,"U/R Number does not Exist.  ";
          CALL      HITENTER
          GOTO      GURN0000
GURN9999  RETURN
.******************************************************************************
.    Set up patients name in PATNAME given patient title, suname and given name
PATN0000
          MOVE      ONE,PACFRMT
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME
PATN9999  RETURN
.******************************************************************************
.                                 GADM0000
.    Get admission number
.******************************************************************************
GADM0000  MOVE      SP70,ADMISNUM
          DISPLAY   *P19:11,UNDLN8,*P19:11;
          KEYIN     *P19:11,*V2LON,ADMISNUM
          MATCH     SP70,ADMISNUM
          GOTO      GADM9990 IF EQUAL
.
          MATCH     ZEROVISN,ADMISNUM      * test for return hit
          GOTO      GADM9990 IF EQUAL
.
          PACK      KEY8,ADMISNUM,SP8
          CALL      RDBKREC1           * read booking file
          BRANCH    OVRCD,GADM8888
.
          MATCH     SP2,BKRELOCK       * test if record is locked
          GOTO      GADM8000 IF EQUAL
          MOVE      ZERO,EXIT
          GOTO      GADM9999
GADM8000
          DISPLAY   *P1:24,*B,*EL,"Booking Record is ALREADY cleared.  ";
          CALL      HITENTER
          GOTO      GADM0000
GADM8888
          DISPLAY   *P1:24,*B,*EL,"Booking not on File.  ";
          CALL      HITENTER
          GOTO      GADM0000
GADM9990
          MOVE      ONE,EXIT
GADM9999  RETURN
.******************************************************************************
.                                 UNLK0000
.    Option 1 : Unlock Patient master record
.    Option 2 : Unlock Waiting list procedure 
.******************************************************************************
UNLK0000
          BRANCH    MOPTION,UNLK1111,UNLK2222,UNLK3333
UNLK1111
          MATCH     SP2,PPORT          * test if port is locked
          GOTO      UNLK8888 IF EQUAL
.
          DISPLAY   *P60:24,*V2LON,"*** CLEARED ***",*W,*P1:24,*EL
          MOVE      PURNO,KEY8
          CALL      UUPTMAS1           * call unlock routine
          GOTO      UNLK9999
.------------------------------------------------------------------------------
UNLK2222
          MATCH     SP2,WTWMPORT
          GOTO      UNLK8888 IF EQUAL
.
          DISPLAY   *P60:24,*V2LON,"*** CLEARED ***",*W,*P1:24,*EL
          MOVE      SP2,WTWMPORT
          CALL      UPTREA1
          GOTO      UNLK9999
.------------------------------------------------------------------------------
UNLK3333
          DISPLAY   *P60:24,*V2LON,"*** CLEARED ***",*W,*P1:24,*EL
          MOVE      SP2,BKRELOCK
          CALL      UPBKREC1
          MOVE      ZERO,EXIT
          GOTO      UNLK9999
.
UNLK8888  DISPLAY   *P1:24,*B,*EL,"Record is not Busy, ALREADY Clear.  ";
          CALL      HITENTER
.
UNLK9999  RETURN
.******************************************************************************
.                                 GPRO0000
.                        input procedure code
.    Created By Bill Dew 27/06/1990
.*******************************************************************************
GPRO0000
          MOVE      ONE,SCRNFLAG
          DISPLAY   *P19:12,*EL,UNDLN9,*P19:12;
GPRO1111
          KEYIN     *V2LON,PROCODE
.
          ENDSET    PROCODE
          APPEND    SP9,PROCODE
          RESET     PROCODE
.
          MATCH     SP9,PROCODE        * test for a return hit
          GOTO      GPRO9990 IF EQUAL
.
          MATCH     QUEST,PROCODE      * test for a ? routine
          GOTO      GPRO3333 IF EQUAL
.
.         validate the code
.
          PACK      KEY9,PROCODE,SP9
          CALL      RDPROA1            * validate procedure code on file
          BRANCH    OVRCD,GPRO8888
.
.         valid code entered
.
          CALL      REDP0000
          DISPLAY   *P19:12,*V2LON,PROCODE,*HOFF,SP3,WPDESC
          MOVE      ZERO,EXIT
          GOTO      GPRO9999
.
.         ? entered
.
GPRO3333  CALL      WATDSTRE           * display all procedure codes
          MOVE      ZERO,SCRNFLAG
.
GPRO4444  DISPLAY   *P1:24,*EL,WTCNTPDS," code : ",UNDLN9,*P18:24;
          GOTO      GPRO1111           * keyin procedure code
.
.         invalid code entered
.
GPRO8888  DISPLAY   *P1:24,*EL,WTCNTPDS," code does not exist.  ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,GPRO0000
          GOTO      GPRO4444
.
.         nothing entered
.
GPRO9990  PACK      WPDESC,SP30,SP20
          CALL      REDP0000
.
          COMPARE   ZERO,SCRNFLAG           * go back to keyin if at the 
          GOTO      GPRO0000 IF EQUAL       * ? option
.
          DISPLAY   *P19:12,*EL
          MOVE      TRUE,EXIT
.
GPRO9999  RETURN
+
.******************************************************************************
REDP0000  BRANCH    SCRNFLAG,REDP9999
          CALL      SCRN0000
.
REDP9999  RETURN
+
.******************************************************************************
.                                 PCNT0000
.    Keyin procedure counter and read file check record exist.
.******************************************************************************
PCNT0000
          DISPLAY   *P19:13,"__"
          KEYIN     *P19:13,*V2LON,PROCOUNT
          DISPLAY   *P19:13,*V2LON,PROCOUNT
.
          COMPARE   ZERO,PROCOUNT
          GOTO      PCNT9990 IF EQUAL
.
          PACK      KEY19,PURNO,PROCODE,PROCOUNT
.PCNTQQ1   DISPLAY   *P1:3,"PCNT> key19 [",KEY19,"]"
          CALL      RDTREA1            * patient procedure details
          BRANCH    OVRCD,PCNT8888
.PCNTQQ2   DISPLAY   *P40:3,"wtwmport [",WTWMPORT,"]";
.PCNTQQK2  KEYIN     ANS
          MOVE      ZERO,EXIT
          GOTO      PCNT9999
PCNT8888
          DISPLAY   *P1:24,*EL,*B,WTCNTPDS," Details not on file.  ";
          CALL      HITENTER
          GOTO      PCNT0000
PCNT9990
          DISPLAY   *P19:13,*EL
          MOVE      ONE,EXIT
PCNT9999  RETURN
.******************************************************************************
SCRN0000
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,"0",*HOFF," = Exit":
                    *P2:5,*V2LON,"1",*HOFF," = U/R Number":
                    *P2:6,*V2LON,"2",*HOFF," = Patient ",WTCNTPDS:
                    *P2:7,*V2LON,"3",*HOFF," = Booking Number":
                    *P1:9,"Select Option : ",MOPTION
SCRN1000
          BRANCH    MOPTION,SCRN1111,SCRN2222,SCRN3333
SCRN1111
          DISPLAY   *P1:11,"U/R Number      : ",*V2LON,PURNO,*HOFF,SP4,PATNAME;
          GOTO      SCRN9999
SCRN2222
          DISPLAY   *P1:11,"U/R Number      : ",*V2LON,PURNO,*HOFF,SP4,PATNAME;
          DISPLAY   *P1:12,WTCNTPDS," Code  : ",*V2LON,PROCODE,*HOFF,SP3,WPDESC:
                    *P1:13,WTCNTPDS," Count :"
          GOTO      SCRN9999
SCRN3333
          DISPLAY   *P1:11,"Admission Number: "
SCRN9999  RETURN
.******************************************************************************
GETSVAR   RETURN
.
REDISPS
          CALL      SCRN0000
          RETURN
.******************************************************************************
          INC       STD001IO
          INC       CALCAGE 
          INC       PATCOMN1
          INC       PATSNDX
          INC       PATSNX2
.
          INC       OUTPREIO/INC
          INC       PATPR1IO/INC
          INC       PATGSRIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
.
          INC       BOKRC1IO/INC
          INC       WATTR1IO/INC
          INC       WATPROIO/INC
          INC       WATDSTRE
          INC       NZCOMPIO/INC
AGECHK
CLPATMAS  RETURN
.******************************************************************************

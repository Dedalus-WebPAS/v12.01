.******************************************************************************
.*  System       :  Waiting List                                              *
.*  Program      :  IBAWAT16                                                  *
.*  Function     :  Waiting List By    Procedure                              *
.******************************************************************************
.*  Date         :  30/04/93                                                  *
.*  Author       :  Whirlpool                                                 *
.*  Modifactions :                                                            *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*        V10.05.01 05/01/2015 Ebon Clements  CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V5.08.02  01/09/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.01  26/10/1999 J Habasque                                     *
.*                  Recompiled for WATDSPRO                                   *
.                   22/10/1998  Glenn Berry      5.07 changes
.******************************************************************************
. *       V5.04.01  15/04/1997  howellsy   RHA                                *
. *                 Recompiled for WATDSPRO                                   *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       TFILEVAR/INC
          INC       WATTR1FD/INC
          INC       WATPROFD/INC
          INC       WATSUSFD/INC
          INC       WATCATFD/INC
          INC       PATDSCFD/INC
          INC       BOKRC1FD/INC
          INC       WEBERRFD/INC
.
.
PTCNURHA  FORM      1
ASATDTE   DIM       8
BIRTHDAT  DIM       10
CLINDATE  DIM       10
CMDLINE   DIM       50
CODEPROC  DIM       9
COL       FORM      3
DATE10A   DIM       10
DESCPROC  DIM       50
DIM3      DIM       3
DIM8      DIM       8
DIM25     DIM       25
DIM30     DIM       30
DISPACT   FORM      1
DOCCODE   DIM       6
ENDCODE   DIM       9
EDESC     DIM       30
EDOL      DIM       10
EDOCTOR   DIM       6
FSTAT     FORM      1
PLADDATE  DIM       10
PTCNDCQS  FORM      1
QUPER     FORM      1
REPTDATE  DIM       8
SAVDATE   DIM       8 
SAVDOCT   DIM       6
SDOCTOR   DIM       6
SDOL      DIM       10
SDESC     DIM       30
SEX       DIM       8                                         * was 7  *C-49016
STRCODE   DIM       9
URGENCY   DIM       9
VERT      FORM      2
WTCNWLSC  FORM      1         * using W/L security
WTSPDTCK  DIM       8         * date to check for suspension
WTSPSTRT  DIM       8         * start of suspension
WTSPSTOP  DIM       8         * end   of suspension
WTCNTPDS  DIM       9
.
START     INIT      "Start "
FINISH    INIT      "Finish"
Z10       INIT      "ZZZZZZZZZZ"
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
NEXTDAY   DIM       8
CODECC    INIT      "CC"
.
.
ENDDATE   DIM       8
STRTDATE  DIM       8
CDFRDATE  DIM       8
CDTODATE  DIM       8
.
.
TEMPFILE  IFILE  SQL, FIXED=52, KEY="D1-6"
.
TDOCT     DIM       6      1
TDATE     DIM       8      7                * reverse chronological order
TPROC     DIM       9      15
TUR       DIM       8      24
TPADATE   DIM       8      41
TURG      DIM       3      49
.
.                          52
.                       
PRGID     INIT      "IBAWAT16"
PRGNAM    INIT      "WAITING LIST BY PROCEDURE REPORT"
VERSION   INIT      "V12.01.00"
.
.*****************************************************************************
.*                              ML0000                                       *
.*                             Mainline                                      *
.*****************************************************************************
.
ML0000    CALL      INIT0000                * initialistion
.
...ML1000    CALL      MENU0000                * Program Menu
...          BRANCH    EXIT,ML9999
.
ML1000    CALL      INST0000                * enter ranges
          BRANCH    EXIT,ML9999             * exit
.
ML2000    CALL      CHGE0000                * modify ranges 
          BRANCH    EXIT,ML1000             * cancel
.
ML5000    CALL      CTBF0000                * create the tempfile

          CALL      EXTR0000
          BRANCH    EXIT,ML1000
.
          CALL      PRNT0000
          GOTO      ML1000
.
ML9999    CALL      KTBF0000
          CHAIN     PGM
.
.*****************************************************************************
.*                              INIT0000                                     *
.*                        Initialisation section                             *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      WATTR1A6,"wattr1af"
          OPEN      WATPROA1,"watproaf"
          OPEN      WATPROA2,"watproaf"
          OPEN      WATPROA3,"watproaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      WATSUSA1,"watsusaf"
.
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      WATCATA1,"watcataf"
          OPEN      WATCATA2,"watcataf"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
          OPEN      PATMI1A1,"patmi1af"
.
.
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,THIRTY7;*185,WTCNTPDS
.
          MOVE      ONE,CNEWDS
          
INIT9999  RETURN
.
.*****************************************************************************
.*                              MENU0000                                     *
.*                        Program Menu Routine                               *
.*****************************************************************************
.
MENU0000  MOVE      ZERO,EXIT
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Print Report":
                    *P1:7,"Select Option ? "

MENU1000  KEYIN     *P17:7,*V2LON,MOPTION:
                    *P17:7,*DV,MOPTION
.
          BRANCH    MOPTION,MENU9999
.
          COMPARE   ZERO,MOPTION
          GOTO      MENU9000 IF EQUAL
.
          BEEP
          GOTO      MENU1000
.
MENU9000  MOVE      ONE,EXIT
.
MENU9999  RETURN
+
.*********************************************************************
.         display the input screen
.*********************************************************************
DSCR0000  DISPLAY    *P1:03,*EF,*V2LON:
                     *P2:04,"1",*P2:06,"2",*P2:07,"3",*P2:09,"4",*P2:10,"5":
                     *HOFF:
                     *P3:04,". As At Date         : ":
                     *P3:06,". From  Procedure    : ":
                     *P3:07,". To    Procedure    : ":
                     *P3:09,". From  Consultant   : ":
                     *P3:10,". To    Consultant   : "
.
          UNPACK     SP20,REPTDATE
.
DSCR9999  RETURN
+
.*********************************************************************
.         insert the details
.*********************************************************************
INST0000  
.
          CALL      DSCR0000                * display the screen
.
          MOVE      ONE,CCITEM
          CALL      DATE0000                * enter as at date 
          BRANCH    EXIT,INST9999           * exit
.
          MOVE      TWO,CCITEM
          CALL      KPRC0000                * enter procedure range 
          BRANCH    EXIT,INST9999           * exit
.
          MOVE      FOUR,CCITEM
          CALL      KDOC0000                * enter consultant
          BRANCH    EXIT,INST9999           * exit
.
INST9999  RETURN
+
.*********************************************************************
.         change the details
.*********************************************************************
CHGE0000  CALL      MAINQST
          BRANCH    CCITEM,CHGE1000,CHGE3000,CHGE3000,CHGE7000,CHGE7000
.
          COMPARE   ZERO,CCITEM
          GOTO      CHGE9000 IF EQUAL       * post 
          GOTO      CHGE9100 IF LESS        * cancel
.
          BEEP
          GOTO      CHGE0000
.
.         change the details
.
CHGE1000  CALL      DATE0000                * enter as at date 
          BRANCH    EXIT,CHGE9100           * exit
          GOTO      CHGE0000
.
CHGE3000  CALL      KPRC0000                * enter Procedure range
          BRANCH    EXIT,CHGE9100           * exit
          GOTO      CHGE0000
.
CHGE7000  CALL      KDOC0000                * enter consultant
          BRANCH    EXIT,CHGE9100           * exit
          GOTO      CHGE0000
.
CHGE9000  MOVE      ZERO,EXIT               * POST
          GOTO      CHGE9999
.
CHGE9100  MOVE      ONE,EXIT                * CANCEL
.
CHGE9999  RETURN
+
.*****************************************************************************
.*                              KDOC0000                                     *
.*                        Keyin the consultant code                          *
.*****************************************************************************
.
KDOC0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          MOVE      NINE,EVERT
          MOVE      "26",ECOL
.
          DISPLAY   *PECOL:EVERT,*EL:
                    *P22:11,*EL
          CALL      PATDOCKY
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KDOC9999
          ENDIF
.
          IF        EXIT = 1
            DISPLAY   *PECOL:EVERT,*EL,*V2LON,START
            MOVE      SP6,SDOCTOR
            PACK      SDESC,START,SP30,SP10
            GOTO      KDOC5000
          ENDIF
.
          MOVE      DOCCODE,SDOCTOR
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DIM25
          DISPLAY   *P36:EVERT,DIM25
          PACK      SDESC,DOCCODE,SP2,DIM25
.
KDOC5000  MOVE      ZERO,EXIT
.
          MOVE      TEN,EVERT
          DISPLAY   *PECOL:EVERT,*EL
          CALL      PATDOCKY
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KDOC9999
          ENDIF
.
          IF        EXIT = 1
            DISPLAY   *PECOL:EVERT,*EL,*V2LON,FINISH
            MOVE      "zzzzzzz",EDOCTOR
            MOVE      ZERO,EXIT
            PACK      EDESC,FINISH,SP30,SP10
            GOTO      KDOC9999
          ENDIF
.
          MOVE      DOCCODE,EDOCTOR
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DIM25
          DISPLAY   *P36:EVERT,DIM25
          PACK      EDESC,DOCCODE,SP2,DIM25
.
          MATCH     SDOCTOR,EDOCTOR
          IF        @LESS
            BEEP
            DISPLAY   *P1:24,*EL,"Consultant codes must be in sequence. ";
            CALL      HITENTER
            GOTO      KDOC0000
          ENDIF
          
.
KDOC9999  RETURN
+
.*********************************************************************
.         enter the as at date
.*********************************************************************
DATE0000  MOVE      "26",CCOL
          MOVE      FOUR,CVERT
          UNPACK    REPTDATE,CCENT,CYEAR,CMON,CDAY
          MATCH     SP2,CCENT
          IF        @EQUAL
            MOVE      CCC,CCENT              * default to current century
          ENDIF
.
          CALL      KEYDATE
          BRANCH    CQUEST,DATE0000          * ? entered
          BRANCH    OVRCD,DATE9100           * nothing entered
.
          PACK      REPTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",REPTDATE
.
          UNPACK    REPTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATE10A
.
. ******* set exit flags *******
.
DATE9000  MOVE      ZERO,EXIT                * continue
          GOTO      DATE9999
DATE9100  MOVE      ONE,EXIT                 * exit
.
DATE9999  RETURN
+
.*********************************************************************
.        keyin procedure codes 
.*********************************************************************
KPRC0000
.
. keyin from procedure
.
          MOVE      TWENTY6,CCOL               * column for input
          MOVE      SIX,CVERT                * row for input
          DISPLAY   *PCCOL:CVERT,*EL
          CALL      KPRO0000                 * keyin procedure
.
          BRANCH    EXIT,KPRC9100
.
          MATCH     SP9,CODEPROC
          IF        @EQUAL
            MOVE      START,CODEPROC
            MOVE      SP10,STRCODE
          ELSE
            MOVE      CODEPROC,STRCODE
          ENDIF      
.
          DISPLAY   *PCCOL:CVERT,*V2LON,CODEPROC,*HOFF,SP1,DESCPROC
.
. keyin to procedure
.
          MOVE      TWENTY6,CCOL               * column for input
          MOVE      SEVEN,CVERT              * row for input
          DISPLAY   *PCCOL:CVERT,*EL
          CALL      KPRO0000                 * keyin procedure
.
          BRANCH    EXIT,KPRC9100
.
          MATCH     SP9,CODEPROC
          IF        @EQUAL
            MOVE      FINISH,CODEPROC
            MOVE      Z10,ENDCODE
          ELSE
            MOVE      CODEPROC,ENDCODE
          ENDIF      
.
          DISPLAY   *PCCOL:CVERT,*V2LON,CODEPROC,*HOFF,SP1,DESCPROC
.
          MATCH     SP10,STRCODE
          GOTO      KPRC9000 IF EQUAL
.
          MATCH     Z10,ENDCODE
          GOTO      KPRC9000 IF EQUAL
.
          MATCH     STRCODE,ENDCODE
          IF        @LESS
            BEEP
            DISPLAY   *P1:24,*EL,"Procedure codes must be in sequence. ";
            CALL      HITENTER
            GOTO      KPRC0000
          ENDIF
.
KPRC9000  MOVE      ZERO,EXIT                * continue
          GOTO      DATE9999
KPRC9100  MOVE      ONE,EXIT                 * exit
.
KPRC9999  RETURN
.*********************************************************************
.*                  KPRO0000                    Called by : KOPT0000 *
.*        Enter the procedure code to search for                     *
.*        Returns : CODEPROC      the procedure code                 *
.*                  DESCPROC      the procedure description          *
.*********************************************************************
KPRO0000  MOVE      ONE,QUPER               * set ? as not performed
.
KPRO0500  KEYIN     *PCCOL:CVERT,*V2LON,CODEPROC:
                    *PCCOL:CVERT,*DV,CODEPROC
.
          ENDSET    CODEPROC
          APPEND    SP9,CODEPROC
          RESET     CODEPROC
.
          MATCH     EXITCHAR,CODEPROC       * exitchar ?
          GOTO      KPRO9100 IF EQUAL      
.
          MATCH     SP9,CODEPROC            * was anything entered ?
          GOTO      KPRO4000 IF EQUAL       * no, use default
.
          MATCH     QUEST,CODEPROC          * was ? entered ?
          GOTO      KPRO1000 IF NOT EQUAL   * no
.
. ------ ? entered ------
.
          CALL      GETSCR00
          MOVE      ZERO,DISPACT
          CALL      WATDSPRO                * ? routine
          MOVE      ZERO,QUPER              * set ? as performed
          MOVE      SP9,CODEPROC            * clear variable
.
KPRO0700  KEYIN     *P1:24,*EL,"Procedure Code : ",*V2LON,CODEPROC
.
          ENDSET    CODEPROC
          APPEND    SP9,CODEPROC
          RESET     CODEPROC
.
          MATCH     EXITCHAR,CODEPROC            * was anything entered ?
          GOTO      KPRO9100 IF EQUAL       * no, use default
.
. ------ validate what was entered ------
.
KPRO1000  PACK      KEY9,CODEPROC           * procedure code as key
          CALL      RDPROA1                 * read procedure file
          BRANCH    OVRCD,KPRO7000          * invalid code
.
          COMPARE   ONE,WTWPACTV            * test for inactive code
          GOTO      KPRO5000 IF EQUAL
.
          PACK      DESCPROC,WPDESC         * set up description
          GOTO      KPRO8000
.
. ------ only enter hit ------
.
KPRO4000  PACK      CODEPROC,SP10           * clear 
          PACK      DESCPROC,SP30,SP30      * clear description
          GOTO      KPRO8000
.
. ------ ERROR: invalid code entered ------
.
KPRO5000  DISPLAY   *P1:24,*B,"Inactive Procedure Code Entered.  ",*EL;
          CALL      HITENTER
          GOTO      KPRO7500
.
KPRO7000  DISPLAY   *P1:24,*B,"Invalid Procedure Code Entered.  ",*EL;
          CALL      HITENTER
.
KPRO7500  BRANCH    QUPER,KPRO0500          * reenter on line 16
.
          GOTO      KPRO0700
.
. ------ valid entry ------
.
KPRO8000  BRANCH    QUPER,KPRO9000          * ? not entered
.
          CALL      PUTSCR00                * redisplay the screen
.
KPRO9000  MOVE      ZERO,EXIT                * continue
          GOTO      KPRO9999
KPRO9100  MOVE      ONE,EXIT                 * exit
.
KPRO9999  RETURN
.*****************************************************************************
.*                           EXTR0000                                        *
.*                        Extract the relevant data                          *
.*****************************************************************************
.
EXTR0000
          DISPLAY   *P1:24,*EL,"Extracting for : "
.
          MOVE      ONE,EXIT
          MOVE      SP8,DIM8
.
          PACK      KEY28,REPTDATE,Z30
          CALL      RDSTREA6
EXTR1000  CALL      RDPTREA6
          BRANCH    OVRCD,EXTR9000 
.
          MATCH     WTWMDTAD,REPTDATE
          GOTO      EXTR9000 IF LESS 
.
          MATCH     DIM8,WTWMDTAD
          IF        !@EQUAL
            UNPACK    WTWMDTAD,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            DISPLAY   *P18:24,*V2LON,CPCDATE
            MOVE      WTWMDTAD,DIM8
          ENDIF
.
          MOVE      REPTDATE,ASATDTE
          CALL      CHKSTAT
.
          COMPARE   FSTAT,ONE               
          GOTO      EXTR1000 IF NOT EQUAL   * unscheduled only
.
          MATCH     STRCODE,WMCODE
          GOTO      EXTR1000 IF LESS
.
          MATCH     WMCODE,ENDCODE
          GOTO      EXTR1000 IF LESS
.
          MATCH     SDOCTOR,WMDOCTOR
          GOTO      EXTR1000 IF LESS
.
          MATCH     WMDOCTOR,EDOCTOR
          GOTO      EXTR1000 IF LESS
.
.******** check if planned patient as at date ********
.
          UNPACK    ASATDTE,XCENT,XYEAR,XMON,XDAY
          MOVE      XCENT,CC
          MOVE      XYEAR,YY
          MOVE      XMON,MM
          MOVE      XDAY,DD
          CALL      DMYCON
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      NEXTDAY,CC,YY,MM,DD
          REP       " 0",NEXTDAY
.
          PACK      KEY29,WMURNO,WMCODE,WMCNT,ANSC,ANSC,NEXTDAY,SP10
          CALL      RSWTCAT2
          CALL      RPWTCAT2
          BRANCH    OVRCD,EXTR4000
.
          MATCH     WMURNO,WTCAURNO
          GOTO      EXTR4000 IF NOT EQUAL
.
          MATCH     WMCODE,WTCAPROC
          GOTO      EXTR4000 IF NOT EQUAL
.
          COMPARE   WMCNT,WTCAPCNT
          GOTO      EXTR4000 IF NOT EQUAL
.
          MATCH     CODECC,WTCACATF
          GOTO      EXTR4000 IF NOT EQUAL
.
          PACK      KEY5,ANSC,ANSC,WTCACODT,SP5
          CALL      RDCODE1
          BRANCH    OVRCD,EXTR4000
          MATCH     ANSP,TCINDC3
          GOTO      EXTR1000 IF EQUAL          * exclude planned patients
.
. ******* see if suspended at report date *******
.
EXTR4000  MOVE      REPTDATE,WTSPDTCK       * set date to check for
          CALL      WATONSUS                * see if suspended
          COMPARE   ZERO,EXIT
          GOTO      EXTR1000 IF EQUAL       * suspended at given date
.
          MOVE      ZERO,EXIT
.
          MOVE      WMDOCTOR,TDOCT
          MOVE      WTWMDTAD,TDATE
          MOVE      WMURNO,TUR
          MOVE      WMCODE,TPROC
          MOVE      WMDATE3,TPADATE
          MOVE      WMPTY,TURG
.
          PACK      KEY6,TDOCT
          CALL      WRWLBPR1
          GOTO      EXTR1000
.
EXTR9000  IF        EXIT = 1
            DISPLAY   *P1:24,*EL,"No data found. "; 
            CALL      HITENTER
          ENDIF
        
EXTR9999  RETURN
.
.*****************************************************************************
.*                           PRNT0000                                        *
.*                        Print the report                                   *
.*****************************************************************************
.
PRNT0000  MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          DISPLAY   *P1:24,*EL,"Printing....."
.
          UNPACK    SP30,SAVDOCT,SAVDATE
.
          PACK      KEY6,SP30
          CALL      RSWLBPR1
PRNT1000  CALL      RKWLBPR1
          BRANCH    OVRCD,PRNT9000
.
          DISPLAY   *P14:24,*EL,*V2LON,TDOCT
.
          IF        CLNO > 54 
            UNPACK    SP30,SAVDOCT,SAVDATE
          ENDIF
.
          MATCH     TDOCT,SAVDOCT
          IF        !@EQUAL
            CALL      HEAD0000
            MOVE       TDOCT,SAVDOCT
            MOVE       TDATE,SAVDATE
          ENDIF
.
          CALL      FRMT0000                * format details for print

          PRINT     *1,"|",*3,TPROC,*13,DIM30,*44,"|",CLINDATE,*58,"|",*60,TUR:
                    *69,"|",*71,DIM25,*95,"|":
                   *97,BIRTHDAT,*108,"|",*110,SEX,*117,"|",*119,TURG:
                   *123,URGENCY,*132,"|"
          CALL      UND132
          ADD       ONE,CLNO
.
          GOTO      PRNT1000
.
PRNT9000  PRINT     *N:
                    "***** End of Report *****"

PRNT9999  RETURN
.
.*****************************************************************************
.*                           FRMT0000                                        *
.*                     Format all details to print                           *
.*****************************************************************************
.
FRMT0000
          MOVE      SP30,DIM25
          UNPACK    SP30,BIRTHDAT,PLADDATE
          MOVE      SP3,DIM3
          MOVE      SP10,SEX
.
          PACK      KEY8,TUR
          CALL      RDMAST1
          BRANCH    OVRCD,FRMT5000
.
          MOVE      PTITL,PACTITLE
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DIM25
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,BIRTHDAT
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,SEX
.* ******************************************   End of Changes         *C-49016
.
FRMT5000  UNPACK    TDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,CLINDATE
.
          MOVE      SP30,TDESC
          PACK      KEY5,ANST,ANSP,TURG
          CALL      RDCODE1
.
          MOVE      TDESC,URGENCY
.
          MOVE      SP30,DIM30
          PACK      KEY9,TPROC
          CALL      RDPROA1
          BRANCH    OVRCD,FRMT9999
.
          MOVE      WPDESC,DIM30
.
FRMT9999  RETURN
.
.
.*****************************************************************************
.*                           CHED0000                                        *
.*                     Print the coloumn headings                            *
.*****************************************************************************
.
CHED0000  CALL      UND132
          PRINT     *1,"|",*44,"| CLINICAL",*58,"|",*69,"|",*95,"|",*108,"|":
                    *117,"|":
                    *132,"|"
          PRINT     *1,"|",*3,"PROCEDURE",*44,"| DECISION",*58,"|",*60,"U/R":
                    *69,"|",*71,"PATIENT NAME":
                    *95,"|",*97,"BIRTH DATE",*108,"|",*110,"SEX":
                    *117,"|",*119,"URGENCY":
                    *132,"|"
          CALL      UND132
.
          ADD       TWO,CLNO
CHED9999  RETURN
.
.*****************************************************************************
.*                           HEAD0000                                        *
.*                        Print the report heading                           *
.*****************************************************************************
.
HEAD0000  CALL      IBACLOCK 
          CALL      HEAD132
.
          PRINT     *40,"Consultant from : ",SDESC:
                    *N,*40,"             to : ",EDESC,*N:
                    *40,"As at Date      : ",DATE10A
.
          PACK      PACFNAME,SP30,SP30
          PACK      KEY6,TDOCT
          CALL      RDDOCT1
          BRANCH    OVRCD,HEAD2000
.
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DIM25
.
HEAD2000  PRINT      *N,"Consultant    : ",*17,TDOCT,*17,TDOCT,SP2,PACFNAME
.
          ADD        SEVEN,CLNO  
.
          CALL       CHED0000
.
HEAD9999  RETURN
.
.*****************************************************************************
.*                           CTBF0000                                        *
.*                        Create the 1st Tempfile                            *
.*****************************************************************************
.
CTBF0000  CALL      TFILENAM * Get temp file name in create
.
          CALL      KTBF0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    " 52 D1-6",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      TEMPFILE,TFILNAME

CTBF9999  RETURN
.
.
.*****************************************************************************
.*                           KTBF0000                                        *
.*                        Kill the 1st Tempfile                              *
.*****************************************************************************
.
KTBF0000  CLOSE     TEMPFILE
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          
KTBF9999  RETURN
.
.==========================
.
RDWLBPR1  RESET     KEY6
          MOVE      ZERO,OVRCD
          READ      TEMPFILE,KEY6;TDOCT,TDATE,TPROC,TUR,TPADATE:
                                   TURG
          GOTO      OVERCOND IF OVER
          RETURN
.
RSWLBPR1  RESET     KEY6
          READ      TEMPFILE,KEY6;;         
          RETURN
.
RKWLBPR1  MOVE      ZERO,OVRCD
          READKS    TEMPFILE;TDOCT,TDATE,TPROC,TUR,TPADATE:
                                   TURG
          GOTO      OVERCOND IF OVER
          RETURN
.
RPWLBPR1  MOVE      ZERO,OVRCD
          READKP    TEMPFILE;TDOCT,TDATE,TPROC,TUR,TPADATE:
                                   TURG
          GOTO      OVERCOND IF OVER
          RETURN
.
WRWLBPR1  RESET     KEY6
          WRITE     TEMPFILE,KEY6;TDOCT,TDATE,TPROC,TUR,TPADATE:
                                   TURG
          RETURN
.
UPWLBPR1  UPDATE    TEMPFILE;TDOCT,TDATE,TPROC,TUR,TPADATE:
                                   TURG
          RETURN
.
.*****************************************************************************
.*                            I/O Section                                    *
.*****************************************************************************
.
          INC       STD001IO
          INC       TFILENAM
          INC       SEXDSCIO
          INC       WATTR1IO/INC
          INC       IBASEQIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATDO1IO/INC
          INC       WATPROIO/INC
          INC       PATCODIO/INC
          INC       WATSUSIO/INC
          INC       WATCATIO/INC
          INC       PATDSCIO/INC
          INC       BOKRC1IO/INC
          INC       WEBERRIO/INC
.
          INC       PATDOCKY
          INC       WATDSPRO
          INC       WATONSUS
          INC       CHKSTAT 

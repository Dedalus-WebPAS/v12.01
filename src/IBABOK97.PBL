. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBABOK97                                            *
. *                                                                           *
. *     FUNCTION:         EMERGENCY FLAG CLEAR                                *
. *                                                                           *
. *     DATE WRITTEN:     24/02/88                                            *
. *                                                                           *
. *     AUTHOR :          MALCOLM HAYSE   (I.B.A)                             *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *                                                                           *
. *             V12.00.01  21/05/2025 Bella Turco  TSK 0955096                *
. *                        Alphanumeric changes                               *
. *              V9.03.00  02/07/2003 Tonii CAR 40346	                      *
. *                        Ported from r5.10                                  *
. *****************************************************************************
. *                 22/10/1998  Glenn Berry      5.07 changes                 *
. *****************************************************************************
. *                       V5.01 28/03/89 Graeme Wiliams                       *
. *                             Changed BOKMASFD                              *
. *                             SRF 5643                                      *
. *                       V5.02 05/06/89 M.Ohleiter                           *
. *                             Changed BOKMASFD                              *
. *                    V5.01.01 28/06/89 M.Ohleiter                           *
. *                             Updated for release 5.01                      *
. *                             11/07/89 K.Peak                               *
. *                             Added 13x4 periods                            *
. *                    V5.01.02 12/12/89 K.(twin) Peak(s)                     *
. *                             Recompiled for 5th index in                   *
. *                             BOKMASFD SRF 103377                           *
. *                    V5.01.03 24/01/1991 Peter Eddey                        *
. *                             changed to use BOKRC1FD                       *
. *                             instead of BOKMASFD                           *
. *****************************************************************************
.
          INC       STD001FD
.
          INC       PATCOMM/INC
          INC       PATCONFD/INC
          INC       PATCODFD/INC
          INC       PATDRGFD/INC
.
          INC       BOKRC1FD/INC
          INC       BOKSTAFD/INC
.
.        EXTRA   VARIABLES
.
.
MONTH     DIM       9
.
PPORT     FORM      2
.
DIM6      DIM       6
.
FILETYP   DIM       6
.
INPFILE   FILE      ASCII, FIXED=256
.
SAVRECT   DIM       8
SEC       FORM      6
LASTSEC   FORM      6
.
SECTPOST  FORM      3       *** SECTOR POSTION FOR PICK FILE INDICATOR
.
CODEBK    INIT      "BK"
VERSION   INIT      "V12.01.00"
PRGID     INIT      "IBABOK97"
PRGNAM    INIT      "Emergency Flag Clear"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
.
          DISPLAY   *P64:24,"bokstaaf";
          OPEN      BOKBSTA1,"bokstaaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,FIFTY9;*27,CPERMTH
.
START     HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Booking Master File":
                    *P2:6,*V2LON,TWO,*HOFF," = Booking Statistics"
.
REKSTRT1  KEYIN     *P1:8,*EF,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF KMAST,KBSTA
.
          DISPLAY   *P1:24,*B,*V2LON,"*** Invalid selection *** ";
          CALL      HITENTER
          GOTO      REKSTRT1
.
.             MASTER BOOKING FILE
.
KMAST     DISPLAY   *P1:24,*EL,*P50:2,*V2LON,"- Booking Groups "
.
          MOVE      ZEROVISN,BKREBOOK
          KEYIN     *P1:4,"Booking Number : ",*EF,*V2LON,BKREBOOK
          MATCH     ZEROVISN,BKREBOOK
          GOTO      START IF EQUAL
.
          MOVE      BKREBOOK,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD OF NOMASTS
.
          MATCH     SP2,BKRELOCK
          GOTO      NOTBUSA IF EQUAL
.
          CALL      UUBKREC1
          DISPLAY   *P1:24,*V2LON,"*** CLEARED *** ";
          CALL      HITENTER
          GOTO      KMAST
.
NOMASTS   DISPLAY   *P1:24,*B,*V2LON:
                    "*** Booking does not Exist *** ";
          CALL      HITENTER
          GOTO      KMAST
.
NOTBUSA   DISPLAY   *P1:24,*B,*V2LON:
                    "*** Booking is not Busy, NOT Cleared *** ";
          CALL      HITENTER
          GOTO      KMAST
.
.             Booking Type
.
KBSTA     DISPLAY   *P50:2,*V2LON,"- Booking Statistics "
.
          MOVE      SP5,BSTYPE
          KEYIN     *P1:4,"Booking Type : ",*EF,*V2LON,BSTYPE
          RESET     BSTYPE
          GOTO      START IF EOS
.
          ENDSET    BSTYPE
          APPEND    SP5,BSTYPE
          RESET     BSTYPE
.
          PACK      KEY5 WITH CODEBK,BSTYPE
          CALL      RDCODE1
          BRANCH    OVRCD OF INVBSTY
.
          DISPLAY   *P30:4,*V2LON,TDESC
.
          DISPLAY   *P1:6,*EL,"Date (MM/YY) : ",UNDLN2,SLASH,UNDLN2
.
          UNPACK    SP4,CYEAR,CMON
          MOVE      CCC,CCENT
          MOVE      TEN6,CCOL
          MOVE      SIX,CVERT
          MOVE      ZERO,CHIGHLT
.
          CALL      KEYPER
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,INVPER
.
          MOVE      DRGMDSC,MONTH
          DISPLAY   *P25:6,*V2LON,MONTH,CCC,CYEAR
.
          PACK      BSDATE,DRGYR,DRGNUM
          REP       " 0",BSDATE
.
          PACK      KEY9 WITH BSTYPE,BSDATE
          CALL      RDBSTA1
          BRANCH    OVRCD OF INVBSTA
.
          MATCH     SP2,BSLOCK
          GOTO      NOTBUSB IF EQUAL
.
          CALL      UUBKBST1
          DISPLAY   *P1:24,*V2LON,"*** CLEARED *** ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL
          GOTO      KBSTA
.
INVPER    DISPLAY    *P1:24,*V2LON,*B,"** Invalid ",CPERMTH,"/Year ** ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL
          GOTO       KBSTA
.
INVBSTY   DISPLAY   *P1:24,*B,*V2LON:
                    "*** Booking Type does not Exist *** ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL
          GOTO      KBSTA
.
INVBSTA   DISPLAY   *P1:24,*B,*V2LON:
                    "*** Booking Statistic does not Exist *** ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL
          GOTO      KBSTA
.
NOTBUSB   DISPLAY   *P1:24,*B,*V2LON:
                    "*** Booking Statistic is not Busy, NOT Cleared *** ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL
          GOTO      KBSTA
.
          INC       PATCODIO/INC
          INCLUDE   PATDRGIO/INC
.
          INC       BOKRC1IO/INC
          INC       BOKSTAIO/INC
.
          INCLUDE   STD001IO
          INCLUDE   PATCOMN3
.
ENDIT     CHAIN     PGM
          STOP

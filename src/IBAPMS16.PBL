.******************************************************************************
.* System   : PATIENT MANAGEMENT                                              *
.* Program  : IBAPMS16                                                        *
.* Desc     : Admission and Discharge Statistics 2                            *
.******************************************************************************
.* Date     : 02/05/1990                                                      *
.* Author   : DIG                                                             *
.* Mods     :                                                                 *
.* V10.03.02  31/12/2012 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
.*        V10.03.01 18/10/2012  Jill Parkinson CAR 273375                     *
.*                  Recompiled for DATECONV - first day of year calculation   *
.*        V9.08.01  01/06/2007  Mike Laming  CAR 140164                       *
.*                  Added DDEST/DSTAT Indicator selection at INIT6000         *
.*      V9.04.00   11/11/2004 Lina Vo  CAR 54862                              *
.*                 Ported from V6.12                                          *
.******************************************************************************
.*      V6.09.B01  29/01/2001 Tonii                                           *
.*                 Mods to print columns Unknown & Indeterminate in report    *
.*        V6.05.02  08/12/1998  Nicole Harrington                             *
.*                  Removed CCENTRY                                           *
.*        V6.05.01   17/11/1998  Nicole Harrington                            *
.*                   Changed to use century in JULCON call                    *
.*        V6.04.01   21/01/1996  Greg Horvat                                  *
.*                   Fixed core dump error                                    *
.*        V6.03.02   15/04/1996  Steve Armstrong                              *
.*                   Mods to check if total occ. days or active bed days      *
.*                   is zero prior to occupancy calculation.                  *
.*        V6.03.01   12/01/1996  Steve Armstrong  SRF 613154                  *
.*                   Mods to use WDACTVDY to calculate active bed days.       *
.*        V6.02.01   27/09/94 J.Tan     SRF 132070                            *
.*                   Fixed Av.LOS figures for report with no female patients  *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDASFD/INC
          INC       PATDRGFD/INC
          INC       PATWBHFD/INC
          INC       PATWR1FD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WDACTVVR/INC
          INC       WEBERRFD/INC                 * Web Server Error Logging
+
.**********************************************************************
.*                             CONSTANTS                              *
.**********************************************************************
.
.
UNDLNN1   INIT      "*--------------------------------------------------":
                    "--------------"
UNDLNN2   INIT      "---------------------------------------------------":
                    "---------------*"
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
ADMTMP    DIM       8
ADMTMPXX  IFILE SQL, FIXED=23, KEY="u1-3"
.
CHKZER    FORM      8
CHKPER    FORM      8.2
CMDLINE   DIM       50
CODE      DIM       2
CODDESC   DIM       20
CPTDATE   DIM       8
CURRDESC  DIM       37
.
DAYFLG    FORM      1                * calculating alos being done ?
DECADTOT  FORM      7.2
DECCHTOT  FORM      7.2
DECFMTOT  FORM      7.2
DECMLTOT  FORM      7.2
DECINTOT  FORM      7.2
DECUNTOT  FORM      7.2
DECTOTAL  FORM      7.2
DECGRTOT  FORM      8.2
DDCODE    DIM       2
DDIND     DIM       2
DTIND     DIM       2
DIM8      DIM       8
.
FORM8P2   FORM      8.2
FPERIOD   DIM       6
FROMDATE  DIM       10
.
GNDTOTL   FORM      8
.
.
NODAYS    FORM      8
.
PERCOCC   FORM      8.2
.
REFERENCE DIM       2
.
SAVKEY11  DIM       11
.
TODATE    DIM       10
TOTAL     FORM      8
TOTBEDAY  FORM      8
TOTLBEDS  FORM      8
TOTLREF2  FORM      8.2
TOTREF11  FORM      8.2
TOTREF12  FORM      8.2
.
.---------------------------------------------------
.Name     Type      Length      Physical     Start
.----     ----      ------      --------     -----
TMCODE    DIM            3             3         1
TMADULT   FORM           8             5         4
TMCHILD   FORM           8             5         9
TMMALE    FORM           8             5        13
TMFMALE   FORM           8             5        18
TMUNKN    FORM           8             5        23
TMINDET   FORM           8             5        28
.
.End of Record                                  33
.---------------------------------------------------
.
ZZZ       INIT      "zzz"
.
PRGID     INIT      "IBAPMS16"
PRGNAM    INIT      "ADMISSION AND DISCHARGE STATISTICS 2"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.*                       Controlling Logic                            *
.**********************************************************************
.
ML0000    CALL      INIT0000            * initialization
.                                         open files
.                                         display header
ML1000    CALL      OPTN0000            * main option screen
          BRANCH    EXIT,ML9999
.
ML2000    CALL      PERD0000            * enter the report period
          BRANCH    EXIT,ML1000
.
          CALL      CONT0000            * O.K. To Continue ?
          BRANCH    EXIT,ML3000,ML2000,ML1000
.
ML3000    CALL      PRNT0000            * Print the Report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*               Open files and display header                        *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening ":
                    *P64:24,"patdasaf";
          OPEN      PATDASA1,"patdasaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
.
          DISPLAY   *P64:24,"patwbhaf";
          OPEN      PATWBHA1,"patwbhaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TWENTY1;*45,PTCNDRSM                     *I-140164
          READ      CONTROLF,FIFTY9;*147,PTCNLOS1,PTCNLOS2,PTCNLOS3,PTCNELDR
.
.                                                                     *I-140164
INIT6000  IF        PTCNDRSM = 1
            MOVE      "D ",DDCODE
            MOVE      " 5",DDIND            * use DSTAT as Separation Mode
            MOVE      " 6",DTIND
          ELSE
            MOVE      "DD",DDCODE
            MOVE      "22",DDIND            * use DDEST as Separation Mode
            MOVE      "23",DTIND
          ENDIF
.
INIT9999  RETURN
+
.**********************************************************************
.*                           OPTN0000                                 *
.*                   Display the main option screen                   *
.**********************************************************************
.
OPTN0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:4,*EF,*V2LON,ZERO:
                    *P1:5,ONE:
                    *P2:4,*HOFF," = Exit":
                    *P2:5," = Generate Report":
                    *P1:7,"Select Option : ";
.
.------ keyin option ------
.
OPTN1000  KEYIN     *P17:7,*EL,*DV,UNDLN1,*P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION                 * compare to zero
          GOTO      OPTN9000 IF EQUAL
.
          BRANCH    OPTION,OPTN9999
          BEEP                                  * invalid option entered
          GOTO      OPTN1000
.
.------ Exit option chosen ------
.
OPTN9000  MOVE      TRUE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                           PERD0000                                 *
.*                   Enter the period for the report                  *
.**********************************************************************
.
PERD0000  DISPLAY   *P1:11,*EF,"Enter the Report Period : ";
          MOVE      TWENTY7,CCOL
          MOVE      TEN1,CVERT
          MOVE      ZERO,CHIGHLT
          MOVE      CCC,CCENT
          REP       " 0",CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
.
          CALL      KEYPER                  * keyin the period code
          BRANCH    OVRCD,PERD9000
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
.
          CALL      RDDRGA3                 * read the period file
          BRANCH    OVRCD,PERD8000
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      DRGFDTE,ACTVFDTE
          PACK      FROMDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      DRGTDTE,ACTVTDTE
          PACK      TODATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          DISPLAY   *P38:11,DRGLDSC:
                    *P38:12,FROMDATE," To ",TODATE;
          MOVE      FALSE,EXIT
          GOTO      PERD9999
.
.------ invalid period entered ------
.
PERD8000  DISPLAY   *P1:24,*EL,*B,"Invalid Period entered - ";
          CALL      HITENTER
          GOTO      PERD0000
.
.------ no period entered ------
.
PERD9000  MOVE      TRUE,EXIT
.
PERD9999  RETURN
+
.**************************************************************************
.*                              CONT0000                                  *
.*                         check if OK to Continue                        *
.*         1 = Yes                                                        *
.*         2 = No                                                         *
.*         3 = Cancel                                                     *
.**************************************************************************
.
CONT0000  MOVE      FALSE,EXIT
          CALL      CONTQST                    * see if O.K. to continue
          MOVE      ANS,EXIT
.
CONT9999  RETURN
+
.**************************************************************************
.*                               PRNT0000                                 *
.*                   Routine to print the report                          *
.**************************************************************************
.
PRNT0000  CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,CLNO
          MOVE      ONE,CNOUNDLN
          MOVE      SP20,CPHDROPT
          PACK      FPERIOD,DRGYR,DRGNUM
.
          DISPLAY   *P1:24,*EL,*P21:24,*V2LON,"** Generating Report - ":
                           *BLINKON,"Please wait",*HOFF,*V2LON," **";
.
          CALL      HEAD0000           * Print the report header
.
          CALL      PBOP0000           * Print patients at beginning of period
.
          CALL      ADDP0000           * Print admissions during period
.
          CALL      DSDP0000           * Print disharges during period
.
          CALL      PHEP0000           * Print patients in hospital at the end
.                                        of the period
.
          CALL      NOBD0000           * Print the number of bed days
.
          CALL      ADBO0000           * Print average daily bed occupancy
.
          CALL      CREA0000           * Create temporary file
          CALL      ALOS0000           * Print average length of stay
.
          CALL      SDYP0000           * Print same day patients
.
          CALL      PSXD0000           * Print patients staying X days
.
          CALL      PSYD0000           * Print patients staying Y days
.
          CALL      PSXM0000           * Print patients staying X or more days
.
          CALL      POXY0000           * Print patients over X years old
.
          CALL      ENDR0000           * Print the end of the report
.
PRNT9999  RETURN
+
.**************************************************************************
.*                               HEAD0000                                 *
.*                   Routine to print the report header                   *
.**************************************************************************
.
HEAD0000  CALL      HEAD132                 * Print the first 3 lines
.
          PRINT     *N,*40,DRGLDSC,*63,FROMDATE," To ",TODATE:
                    *N,*N,UNDLNN1,UNDLNN2:
                    *N,*1,"|",*40,"|",*51,"|",*62,"|",*73,"|",*84,"|":
                       *95,"|",*106,"|",*117,"|",*132,"|":
                    *N,*1,"|",*5,"Description",*40,"|",*43,"Adult",*51,"|":
                       *54,"Child",*62,"|",*65,"Total",*73,"|",*76,"Male":
                       *84,"|",*87,"Female",*95,"|",*97,"Unknown":
                       *106,"|",*108,"Indeter",*117,"|",*119,"Grand Total":
                       *132,"|":
                    *N,UNDLNN1,UNDLNN2:
                    *N,*1,"|",*40,"|",*51,"|",*62,"|",*73,"|",*84,"|":
                       *95,"|",*106,"|",*117,"|",*132,"|";
.
          MOVE      TEN1,CLNO
.
HEAD9999  RETURN
+
.**************************************************************************
.*                                CLRT0000                                *
.*                        Clear the total variables                       *
.**************************************************************************
.
CLRT0000  MOVE      ZERO,TOTAL
          MOVE      ZERO,DECCHTOT
          MOVE      ZERO,DECADTOT
          MOVE      ZERO,DECFMTOT
          MOVE      ZERO,DECMLTOT
          MOVE      ZERO,DECTOTAL
.
CLRT9999  RETURN
+
.**************************************************************************
.*                                PDSC0000                                *
.*                 Print the description line of the report               *
.**************************************************************************
.
PDSC0000  PRINT     *N,*1,"|",*3,CURRDESC,*40,"|",*51,"|",*62,"|",*73,"|":
                       *84,"|",*95,"|",*106,"|",*117,"|",*132,"|";
          ADD       ONE,CLNO
.
PDSC9999  RETURN
+
.**************************************************************************
.*                                READ0000                                *
.*       read the patient discharge and admission statistics file         *
.*       and print the current reference                                  *
.**************************************************************************
.
READ0000  CALL      CLRT0000                * clear total variables 
          MOVE      ZERO,GNDTOTL
.
          BRANCH    DAYFLG,READ0200         * calculating alos totals
.
          COMPARE   "57",CLNO               * see if new page is required
          GOTO      READ0100 IF LESS
.
          PRINT     *N,UNDLNN1,UNDLNN2
.
          CALL      HEAD0000                * print new page header
.
READ0100  CALL      PDSC0000                * print the description line
.
.------ position on patient discharge and admission statistics file ------
.
READ0200  PACK      KEY11,REFERENCE,FPERIOD,SP3
          CALL      RDPTDAS1
          BRANCH    OVRCD,READ1000
          GOTO      READ1100
.
.------ read through the patient discharge and admission statistics file ------
.
READ1000  CALL      RKPTDAS1
          BRANCH    OVRCD,READ9000
.
READ1100  MATCH     FPERIOD,PTDADATE        * match periods
          GOTO      READ9000 IF NOT EQUAL
.
          MATCH     REFERENCE,DPTDAREF      * match references
          GOTO      READ9000 IF NOT EQUAL
.
          BRANCH    DAYFLG,READ1200         * calculating alos totals
.
          COMPARE   "57",CLNO               * see if a new page is required
          GOTO      READ1200 IF LESS
.
          PRINT     *N,*1,"|",*40,"|",*51,"|",*62,"|",*73,"|",*84,"|":
                       *95,"|",*106,"|",*117,"|",*132,"|":
                    *N,UNDLNN1,UNDLNN2
.
          CALL      HEAD0000                * print new page header
          CALL      PDSC0000                * print the description line
.
.------ print the current record ------
.
READ1200  MOVE      PTDAADLT,TOTAL
          ADD       PTDACHLD,TOTAL
          ADD       TOTAL,GNDTOTL
.
          MOVE      "Unknown Code : ",CODDESC
          ENDSET    CODDESC
          APPEND    PTDACODE,CODDESC
          RESET     CODDESC
.
          MATCH     SP3,PTDACODE
          GOTO      READ1300 IF EQUAL
.
          PACK      KEY5,CODE,PTDACODE
          CALL      RDCODE1                 * read patient code file
          BRANCH    OVRCD,READ1300
          MOVE      TDESC,CODDESC
.
READ1300  BRANCH    DAYFLG,READ2000         * calculating alos totals
.
          MOVE      PTDAADLT,CHKZER
          ADD       PTDACHLD,CHKZER
          ADD       TOTAL,CHKZER
          ADD       PTDAMALE,CHKZER
          ADD       PTDAFEML,CHKZER
          ADD       PTDAUNKN,CHKZER
          ADD       PTDAINDT,CHKZER
.
          COMPARE   ZERO,CHKZER
          GOTO      READ4000 IF EQUAL
.
          PRINT     *N,*1,"|",*10,"-",*12,CODDESC,*40,"|",PTDAADLT,*51,"|":
                       PTDACHLD,*62,"|",TOTAL,*73,"|",PTDAMALE:
                       *84,"|",PTDAFEML,*95,"|",PTDAUNKN,*106,"|",PTDAINDT:
                       *117,"|",*132,"|";
          ADD       ONE,CLNO
          GOTO      READ4000
.
.         To calculate the average length of stay totals you need 
.         to subtract the bed days for daycases from reference eleven -
.         "Bed days in period". Done by using temporary file.
.
READ2000  MOVE      PTDACODE,KEY3
          CALL      RDTMP1
          BRANCH    OVRCD,READ3000
.
          ADD       PTDAADLT,TMADULT
          ADD       PTDACHLD,TMCHILD
          ADD       PTDAMALE,TMMALE
          ADD       PTDAFEML,TMFMALE
          ADD       PTDAUNKN,TMUNKN
          ADD       PTDAINDT,TMINDET
          CALL      UPTMP1
          GOTO      READ4000
.
READ3000  MOVE      PTDACODE,TMCODE 
          MOVE      PTDAADLT,TMADULT
          MOVE      PTDACHLD,TMCHILD
          MOVE      PTDAMALE,TMMALE
          MOVE      PTDAFEML,TMFMALE
          MOVE      PTDAUNKN,TMUNKN
          MOVE      PTDAINDT,TMINDET
          CALL      WRTMP1
.
READ4000  CALL      CLRT0000                * clear the total variables
          GOTO      READ1000
.
.------ end of current reference so print grand total line ------
.
READ9000  BRANCH    DAYFLG,READ9999         * calculating alos totals
.
          MATCH     DDIND,REFERENCE         * " 5" or "22"            *C-140164
          GOTO      READ9100 IF EQUAL
.
          PRINT     *N,*1,"|",*40,"|",*51,"|",*62,"|",*73,"|",*84,"|":
                       *95,"|",*106,"|",*117,"|",GNDTOTL,*132,"|":
..                    *117,GNDTOTL:
                    *N,*1,"|",*40,"|",*51,"|",*62,"|",*73,"|",*84,"|":
                       *95,"|",*106,"|",*117,"|",*132,"|";
.
          ADD       TWO,CLNO
          GOTO      READ9999
.
.------ we are doing discharges so process reference 6 as well ------
.
READ9100  MOVE      DTIND,REFERENCE         * " 6" or "23"            *C-140164
          CALL      CLRT0000                * clear the total variables
          GOTO      READ0200
.
READ9999  RETURN
+
.**************************************************************************
.*                                 PBOP0000                               *
.*          Print patients at the beginning of the period                 *
.**************************************************************************
.
PBOP0000  MOVE      " 1",REFERENCE
          MOVE      "PATIENTS AT BEGINNING OF PERIOD",CURRDESC
          MOVE      "A ",CODE
.
          CALL      READ0000                * read the patient discharge and 
.                                             admission statistics file
PBOP9999  RETURN
+
.**************************************************************************
.*                                 ADDP0000                               *
.*                    Print admissions during the period                  *
.**************************************************************************
.
ADDP0000  MOVE      " 3",REFERENCE
          MOVE      "ADMISSIONS DURING PERIOD",CURRDESC
          MOVE      "A ",CODE
.
          CALL      READ0000                * read the patient discharge and 
.                                             admission statistics file
ADDP9999  RETURN
+
.**************************************************************************
.*                                 DSDP0000                               *
.*                    Print discharges during the period                  *
.**************************************************************************
.
DSDP0000  MOVE      DDIND,REFERENCE         * " 5" or "22"            *C-140164
          MOVE      "DISCHARGES DURING PERIOD",CURRDESC
          MOVE      DDCODE,CODE                                       *C-140164
.
          CALL      READ0000                * read the patient discharge and 
.                                             admission statistics file
DSDP9999  RETURN
+
.**************************************************************************
.*                                 PHEP0000                               *
.*         Print patients in hospital at the end of the period            *
.**************************************************************************
.
PHEP0000  MOVE      " 9",REFERENCE
          MOVE      "PATIENTS IN HOSPITAL AT END OF PERIOD",CURRDESC
          MOVE      "A ",CODE
.
          CALL      READ0000                * read the patient discharge and 
.                                             admission statistics file
PHEP9999  RETURN
+
.**************************************************************************
.*                                 NOBD0000                               *
.*                          Print the number of bed days                  *
.**************************************************************************
.
NOBD0000  MOVE      "11",REFERENCE
          MOVE      "NUMBER OF BED DAYS ",CURRDESC
          MOVE      "A ",CODE
          MOVE      ZERO,TOTBEDAY
.
          CALL      READ0000                * read the patient discharge and 
.                                             admission statistics file
          MOVE      GNDTOTL,TOTBEDAY
.
NOBD9999  RETURN
+
.**************************************************************************
.*                                 ADBO0000                               *
.*                   Print the average daily bed occupancy                *
.**************************************************************************
.
ADBO0000  MOVE      "11",REFERENCE
          MOVE      "AVERAGE DAILY BED OCCUPANCY ",CURRDESC
          MOVE      "A ",CODE
          MOVE      ONE,NODAYS
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
.
.------ calculate the number of days in the period ------
.
ADBO0100  MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          MOVE      CCENT,CC
.
          CALL      DMYCON                  * convert to julian date
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
.
          CALL      JULCON                  * convert back to YY,MM,DD format
.
          PACK      DIM8,CC,YY,MM,DD
          REP       " 0",DIM8
.
          MATCH     DIM8,DRGTDTE            * see if we have reached the TO 
          GOTO      ADBO0200 IF LESS          date yet
.
          ADD       ONE,NODAYS
          UNPACK    DIM8,CCENT,CYEAR,CMON,CDAY
          GOTO      ADBO0100
.
.------ we have the number of days in the period ------
.
ADBO0200  MOVE      ZERO,DECGRTOT
          CALL      CLRT0000                * clear total variables 
.
          COMPARE   "57",CLNO               * see if a new page is required
          GOTO      ADBO0800 IF LESS
.
          PRINT     *N,UNDLNN1,UNDLNN2
          CALL      HEAD0000                * print new page header
.
ADBO0800  CALL      PDSC0000                * print the description line
.
          PACK      KEY11,REFERENCE,FPERIOD,SP3
          CALL      RSPTDAS1
.
.------ read through the patient discharge and admission statistics file ------
.
ADBO1000  CALL      RKPTDAS1
          BRANCH    OVRCD,ADBO9000
.
          MATCH     FPERIOD,PTDADATE        * match periods
          GOTO      ADBO9000 IF NOT EQUAL
.
          MATCH     REFERENCE,DPTDAREF      * match references
          GOTO      ADBO9000 IF NOT EQUAL
.
          COMPARE   "57",CLNO               * see if a new page is required
          GOTO      ADBO1050 IF LESS
.
          PRINT     *N,*1,"|",*40,"|",*51,"|",*62,"|",*73,"|",*84,"|":
                       *95,"|",*106,"|",*117,"|",*132,"|":
                    *N,UNDLNN1,UNDLNN2
.
          CALL      HEAD0000                * print new page header
          CALL      PDSC0000                * print the description line
.
ADBO1050  MOVE      PTDAADLT,DECADTOT
          DIV       NODAYS,DECADTOT
          MOVE      PTDACHLD,DECCHTOT
          DIV       NODAYS,DECCHTOT
          MOVE      DECADTOT,DECTOTAL
          ADD       DECCHTOT,DECTOTAL
          ADD       DECTOTAL,DECGRTOT
          MOVE      PTDAMALE,DECMLTOT
          DIV       NODAYS,DECMLTOT
          MOVE      PTDAFEML,DECFMTOT
          DIV       NODAYS,DECFMTOT
          MOVE      PTDAUNKN,DECUNTOT
          DIV       NODAYS,DECUNTOT
          MOVE      PTDAINDT,DECINTOT
          DIV       NODAYS,DECINTOT
.
          MOVE      "Unknown Code : ",CODDESC
          ENDSET    CODDESC
          APPEND    PTDACODE,CODDESC
          RESET     CODDESC
.
          PACK      KEY5,CODE,PTDACODE
          CALL      RDCODE1                 * read patient code file
          BRANCH    OVRCD,ADBO1100
          MOVE      TDESC,CODDESC
.
ADBO1100  MOVE      DECADTOT,CHKPER
          ADD       DECCHTOT,CHKPER
          ADD       DECTOTAL,CHKPER
          ADD       DECMLTOT,CHKPER
          ADD       DECFMTOT,CHKPER
.
          COMPARE   ZERO,CHKPER
          GOTO      ADBO1000 IF EQUAL
.
          PRINT     *N,*1,"|",*10,"-",*12,CODDESC,*40,"|",DECADTOT,*51,"|":
                       DECCHTOT,*62,"|",DECTOTAL,*73,"|",DECMLTOT:
                       *84,"|",DECFMTOT,*95,"|",DECUNTOT,*106,"|",DECINTOT:
                       *117,"|",*132,"|";
          ADD       ONE,CLNO
.
          CALL      CLRT0000                * clear the total variables
          GOTO      ADBO1000
.
.------ we have reached the end of this reference so print the grand total ----
.
ADBO9000  PRINT     *N,*1,"|",*40,"|",*51,"|",*62,"|",*73,"|",*84,"|":
                       *95,"|",*106,"|",*117,"|",DECGRTOT,*132,"|":
..                    *115,DECGRTOT:
                    *N,*1,"|",*40,"|",*51,"|",*62,"|",*73,"|",*84,"|":
                       *95,"|",*106,"|",*117,"|",*132,"|";
          ADD       TWO,CLNO
.
ADBO9999  RETURN
+
.**************************************************************************
.*                                 ALOS0000                               *
.*                   Print the average length of stay                     *
.**************************************************************************
.
ALOS0000  MOVE      ONE,DAYFLG          * calculating of alos totals to be done
          CALL      SDYP0000            * calculate same day patients
          MOVE      ZERO,DAYFLG
.
          MOVE      "11",REFERENCE
          MOVE      "AVERAGE LENGTH OF STAY ",CURRDESC
          MOVE      "A ",CODE
          PACK      SAVKEY11,REFERENCE,FPERIOD,SP3
          MOVE      ZERO,TOTLREF2
          MOVE      ZERO,TOTREF11
          MOVE      ZERO,TOTREF12
.
          CALL      CLRT0000                * clear total variables 
.
          COMPARE   "57",CLNO               * see if a new page is required
          GOTO      ALOS0100 IF LESS
.
          PRINT     *N,UNDLNN1,UNDLNN2
          CALL      HEAD0000                * print new page header
.
ALOS0100  CALL      PDSC0000                * print the description line
.
.------ position on the patient discharge and admission statistics file ------
.
ALOS0200  MOVE      SAVKEY11,KEY11
          CALL      RSPTDAS1
.
.------ read through the patient discharge and admission statistics file ------
.
ALOS1000  CALL      RKPTDAS1
          BRANCH    OVRCD,ALOS9000
.
          MATCH     FPERIOD,PTDADATE        * match periods
          GOTO      ALOS9000 IF NOT EQUAL
.
          MATCH     REFERENCE,DPTDAREF      * match references
          GOTO      ALOS9000 IF NOT EQUAL
.
          COMPARE   "57",CLNO               * see if a new page is required
          GOTO      ALOS1050 IF LESS
.
          PRINT     *N,*1,"|",*40,"|",*51,"|",*62,"|",*73,"|",*84,"|":
                       *95,"|",*106,"|",*117,"|",*132,"|":
                    *N,UNDLNN1,UNDLNN2
.
          CALL      HEAD0000                * print new page header
          CALL      PDSC0000                * print the description line
.
ALOS1050  PACK      SAVKEY11,DPTDAREF,PTDADATE,PTDACODE
          MOVE      PTDAADLT,DECADTOT
          MOVE      PTDACHLD,DECCHTOT
          MOVE      PTDAMALE,DECMLTOT
          MOVE      PTDAFEML,DECFMTOT
          MOVE      PTDAUNKN,DECUNTOT
          MOVE      PTDAINDT,DECINTOT
.
.         Subtract the number of bed days for daycases from reference
.         eleven - "Bed days during period", to get the correct average
.         length of stay totals.
.
          MOVE      PTDACODE,KEY3
          CALL      RDTMP1
          BRANCH    OVRCD,ALOS1070
.
          SUB       TMADULT,DECADTOT
          SUB       TMCHILD,DECCHTOT
          SUB       TMMALE,DECMLTOT
          SUB       TMFMALE,DECFMTOT
          SUB       TMUNKN,DECUNTOT
          SUB       TMINDET,DECINTOT
.
ALOS1070  MOVE      DECADTOT,FORM8P2
          ADD       DECCHTOT,FORM8P2
          MOVE      FORM8P2,DECTOTAL
          ADD       FORM8P2,TOTREF11
.
          MOVE      "Unknown Code : ",CODDESC
          ENDSET    CODDESC
          APPEND    PTDACODE,CODDESC
          RESET     CODDESC
.
          PACK      KEY5,CODE,PTDACODE
          CALL      RDCODE1                 * read patient code file
          BRANCH    OVRCD,ALOS1090
          MOVE      TDESC,CODDESC
.
ALOS1090  MOVE      " 2",DIM2
          PACK      KEY11,DIM2,PTDADATE,PTDACODE
          CALL      RDPTDAS1                * read the patient discharge and
          BRANCH    OVRCD,ALOS1100            admission statistics file
.
          ADD       PTDAADLT,DECADTOT
          ADD       PTDACHLD,DECCHTOT
          ADD       PTDAMALE,DECMLTOT
          ADD       PTDAFEML,DECFMTOT
          ADD       PTDAUNKN,DECUNTOT
          ADD       PTDAINDT,DECINTOT
          MOVE      PTDAADLT,FORM8P2
          ADD       PTDACHLD,FORM8P2
          ADD       FORM8P2,DECTOTAL
          ADD       FORM8P2,TOTLREF2
.
ALOS1100  MOVE      "12",DIM2
          PACK      KEY11,DIM2,PTDADATE,PTDACODE
          CALL      RDPTDAS1                * read the patient discharge and
          BRANCH    OVRCD,ALOS1200            admission statistics file
.
          COMPARE   ZERO,PTDAADLT           * test for zero
          GOTO      ALOS1110 IF EQUAL
.
          DIV       PTDAADLT,DECADTOT
.
ALOS1110  COMPARE   ZERO,PTDACHLD           * test for zero
          GOTO      ALOS1120 IF EQUAL
.
          DIV       PTDACHLD,DECCHTOT
.
ALOS1120  COMPARE   ZERO,PTDAMALE           * test for zero
          GOTO      ALOS1130 IF EQUAL
.
          DIV       PTDAMALE,DECMLTOT
.

ALOS1130  COMPARE   ZERO,PTDAFEML           * test for zero
          GOTO      ALOS1140 IF EQUAL
.
          DIV       PTDAFEML,DECFMTOT
.
ALOS1140  COMPARE   ZERO,PTDAUNKN
          GOTO      ALOS1150 IF EQUAL
.
          DIV       PTDAUNKN,DECUNTOT
.
ALOS1150  COMPARE   ZERO,PTDAINDT
          GOTO      ALOS1160 IF EQUAL
.
          DIV       PTDAINDT,DECINTOT
.
ALOS1160  MOVE      PTDAADLT,FORM8P2
          ADD       PTDACHLD,FORM8P2
.
          COMPARE   ZERO,FORM8P2           * test for zero
          GOTO      ALOS1200 IF EQUAL
          DIV       FORM8P2,DECTOTAL
.
.------ print the current record ------
.
ALOS1200  MOVE      DECADTOT,CHKPER
          ADD       DECCHTOT,CHKPER
          ADD       DECTOTAL,CHKPER
          ADD       DECMLTOT,CHKPER
          ADD       DECFMTOT,CHKPER
.
          COMPARE   ZERO,CHKPER
          GOTO      ALOS0200 IF EQUAL
.
          PRINT     *N,*1,"|",*10,"-",*12,CODDESC,*40,"|",DECADTOT,*51,"|":
                       DECCHTOT,*62,"|",DECTOTAL,*73,"|",DECMLTOT:
                       *84,"|",DECFMTOT,*95,"|",DECUNTOT,*106,"|",DECINTOT:
                       *117,"|",*132,"|";
          ADD       ONE,CLNO
.
          CALL      CLRT0000                * clear the total variables
          GOTO      ALOS0200
.
.------ end of current reference so work out and print grand total ------
.
ALOS9000  UNPACK    SAVKEY11,DPTDAREF,PTDADATE,PTDACODE
          MOVE      "12",REFERENCE
          PACK      KEY11,REFERENCE,PTDADATE,SP3
          CALL      RDPTDAS1                * read the patient discharge and
          BRANCH    OVRCD,ALOS9100            admission statistics file
.
          MOVE      PTDAADLT,TOTREF12
          ADD       PTDACHLD,TOTREF12
.
ALOS9100  ADD       TOTREF11,TOTLREF2
.
          COMPARE   ZERO,TOTREF12           * test for zero
          GOTO      ALOS9200 IF EQUAL
.
          DIV       TOTREF12,TOTLREF2
.
.------ print the grand total ------
.
ALOS9200  PRINT     *N,*1,"|",*40,"|",*51,"|",*62,"|",*73,"|",*84,"|":
                       *95,"|",*106,"|",*117,"|",TOTLREF2,*132,"|":
..                    *115,TOTLREF2:
                    *N,*1,"|",*40,"|",*51,"|",*62,"|",*73,"|",*84,"|":
                       *95,"|",*106,"|",*117,"|",*132,"|";
          ADD       TWO,CLNO
.
ALOS9999  RETURN
+
.**************************************************************************
.*                                 SDYP0000                               *
.*                       Print the same day patients                      *
.**************************************************************************
.
SDYP0000  MOVE      "13",REFERENCE
          MOVE      "SAME DAY PATIENTS ",CURRDESC
          MOVE      "A ",CODE
.
          CALL      READ0000                * read the patient discharge and 
.                                             admission statistics file
SDYP9999  RETURN
+
.**************************************************************************
.*                                 PSXD0000                               *
.*                       Print patients staying XXX days                  *
.**************************************************************************
.
PSXD0000  MOVE      "14",REFERENCE
          MOVE      "PATIENTS STAYING",CURRDESC
          ENDSET    CURRDESC
          APPEND    SP1,CURRDESC
          APPEND    PTCNLOS1,CURRDESC
          APPEND    " DAYS",CURRDESC
          RESET     CURRDESC
          MOVE      "A ",CODE
.
          CALL      READ0000                * read the patient discharge and 
.                                             admission statistics file
PSXD9999  RETURN
+
.**************************************************************************
.*                                 PSYD0000                               *
.*                       Print patients staying YYY days                  *
.**************************************************************************
.
PSYD0000  MOVE      "15",REFERENCE
          MOVE      "PATIENTS STAYING",CURRDESC
          ENDSET    CURRDESC
          APPEND    SP1,CURRDESC
          APPEND    PTCNLOS2,CURRDESC
          APPEND    " DAYS",CURRDESC
          RESET     CURRDESC
          MOVE      "A ",CODE
.
          CALL      READ0000                * read the patient discharge and 
.                                             admission statistics file
PSYD9999  RETURN
+
.**************************************************************************
.*                                 PSXM0000                               *
.*                  Print patients staying XXX or more days               *
.**************************************************************************
.
PSXM0000  MOVE      "16",REFERENCE
          MOVE      "PATIENTS STAYING",CURRDESC
          ENDSET    CURRDESC
          APPEND    SP1,CURRDESC
          APPEND    PTCNLOS3,CURRDESC
          APPEND    " OR MORE DAYS",CURRDESC
          RESET     CURRDESC
          MOVE      "A ",CODE
.
          CALL      READ0000                * read the patient discharge and 
.                                             admission statistics file
PSXM9999  RETURN
+
.**************************************************************************
.*                                 POXY0000                               *
.*                  Print no of patients over XXX years old               *
.**************************************************************************
.
POXY0000  MOVE      "17",REFERENCE
          MOVE      "NUMBER OF PATIENTS OVER",CURRDESC
          ENDSET    CURRDESC
          APPEND    SP1,CURRDESC
          APPEND    PTCNELDR,CURRDESC
          APPEND    " YEARS OLD",CURRDESC
          RESET     CURRDESC
          MOVE      "A ",CODE
.
          CALL      READ0000                * read the patient discharge and 
.                                             admission statistics file
POXY9999  RETURN
+
.**************************************************************************
.*                                 ENDR0000                               *
.*                  Print the end of the report                           *
.**************************************************************************
.
ENDR0000  MOVE      ZERO,TOTLBEDS                * initialise active bed days
          PACK      KEY6,SP6
          CALL      RDSWARD1                     * posn. at start of ward file
ENDR1000  CALL      RDKWARD1                     * read next record
          BRANCH    OVRCD,ENDR2000               * eof - finished
.
.         Check if the ward was active during the period.  If not, then
.         ignore all the records for the ward and position on the ward header
.         record for the next ward.
.
          MATCH     SP3,WBED                     * ward header record ?
          IF        @EQUAL
            MOVE      ONE,ACTVWFLG               * don't want weekend days
            PROC      WDACTVDY                   * get days ward was active
            COMPARE   ZERO,ACTVDAYS              * ward active during period ?
            IF        @EQUAL
              PACK      KEY6,WWARD,ZZZ
              CALL      RDSWARD1                 * posn. on next ward header rec
              GOTO      ENDR1000
            ENDIF
            ADD         ACTVBDAY,TOTLBEDS        * update total active bed days
          ENDIF
.
          PACK        KEY6,WWARD,ZZZ
          CALL        RDSWARD1
          GOTO        ENDR1000
.
.------ print the end of report info ------
.
ENDR2000  MOVE      ZERO,PERCOCC
          IF        TOTBEDAY<>0 | TOTLBEDS<>0
            ASSIGN    ((TOTBEDAY*100)/TOTLBEDS),PERCOCC
          ENDIF
.
          COMPARE   "47",CLNO               * see if a new page is required
          GOTO      ENDR3000 IF LESS
.
          PRINT     *N,UNDLNN1,UNDLNN2
.
          CALL      HEAD132                 * Print the first 3 lines
.
          PRINT     *N,*40,DRGLDSC,*63,FROMDATE," To ",TODATE;
          GOTO      ENDR3100
.
ENDR3000  PRINT     *N,UNDLNN1,UNDLNN2;
.
ENDR3100  PRINT     *N,*N,*10,"NOTE : Number of Bed Days includes day cases":
                    *N,*N,*17,"Average Length of Stay      = Number of Bed ":
                          "Days between Admission and End of Period / Number":
                          " of Patients.":
                    *N,*N,*17,"Average Daily Bed Occupancy = Number of Period":
                       " Bed Days / Number of Days in the Period",*N:
                    *N,*N,*10,"TOTAL BED DAYS AVAILABLE : ",TOTLBEDS:
                    *N,*N,*10,"PERCENTAGE OF OCCUPANCY  : ",PERCOCC,"%":
                    *N,*N,*N,"*** End of Report ***";
.
ENDR9999  CALL      KILL0000              * remove temporary file
          RETURN
+
.**************************************************************************
.*                                KILL0000                                *
.*                          Remove Temporary File                         *
.**************************************************************************
.
KILL0000  CLOSE     ADMTMPXX
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    ADMTMP,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.**************************************************************************
.*                                CREA0000                                *
.*                          Create Temporary File                         *
.**************************************************************************
.
CREA0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,ADMTMP
.
          CALL      KILL0000
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    ADMTMP,CMDLINE
          APPEND    " 23 u1-3",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      ADMTMPXX,ADMTMP
          RETURN
+
.**************************************************************************
.*                 I/O for Temporary File                                 *
.**************************************************************************
.
RDTMP1    MOVE      ZERO,OVRCD
          RESET     KEY3
          READ      ADMTMPXX,KEY3;TMCODE,TMADULT,TMCHILD,TMMALE,TMFMALE:
                                  TMUNKN,TMINDET
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMP1    UPDATE    ADMTMPXX;TMCODE,TMADULT,TMCHILD,TMMALE,TMFMALE:
                                  TMUNKN,TMINDET
          RETURN
.
WRTMP1    RESET     KEY3
          WRITE     ADMTMPXX,KEY3;TMCODE,TMADULT,TMCHILD,TMMALE,TMFMALE:
                                  TMUNKN,TMINDET
          RETURN
+
.------------------------------------------------------------------------------
.
          INC       STD001IO
          INC       CALCDAYS
          INC       DATECONV
          INC       PATCOMN3
          INC       TFILENAM                     * Generate Temp File Name
          INC       WDACTVDY
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATDASIO/INC
          INC       PATDRGIO/INC
          INC       PATCODIO/INC
          INC       PATWR1IO/INC
          INC       PATWBHIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging

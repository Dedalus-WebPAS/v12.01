.******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAADT07                                                 *
.* Description    :  Suggested Classification Upload                          *
.******************************************************************************
.* Date           :  19/09/2017                                               *
.* Author         :  J.Tan      Task 0827346                                  *
.* Function       :  Upload Suggested Class.from ASCII file                   *
.*                                                                            *
.*                   Note: Change of Upload file will require increasing the  *
.*                   Version number of Spreadsheet                            *
.* Modifications  :                                                           *
.* V11.00.01 12/08/2020  J.Tan          TSK 0895522                           *
.*                       Added Effective date to patitemn                     *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       PATSGCFD/INC
          INC       PATITMFD/INC
          INC       PATFN1FD/INC
          INC       PATCONFD/INC
.
.         ICD10 Suggested Classfication Upload file
.
MBSSGTXT  FILE       HL7, FIXED=1000        * Upload file for Sugg.Class.
.
.Name     Type     Size     Start
.----     ----     ----     -----
XCLAIM    DIM         3     Claim code (Catg CL)
XHFUND    DIM         6     Health Fund 
XMBSIT    DIM         9     MBS Item Code
XSCLAS    DIM         3     Suggested Classification
.
. ----- Program Variables -----
.
CRETURN   INIT      015                * carriage return character
CMDLINE   DIM       100
ERRFLAG   FORM      1
ERRNUMB   FORM      6
FNAMEI    DIM       150
FORM12P2  FORM      12.2
RECNUMB   FORM       6
RECWRDT   FORM       6
RECUPDT   FORM       6
USERID    DIM       10
PATHNAME  DIM       100
TIMEIS    DIM       8
.
XFLD0001  DIM       30
XFLD0002  DIM       30
XFLD0003  DIM       30
XFLD0004  DIM       30
.
PRGID     INIT      "IBAADT07"
PRGNAM    INIT      "Upload Suggested Classification"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000              * Initailize variables & open files
          CALL      KOPT0000              * Keyin Upload or Update option
          BRANCH    EXIT,ML9999
.
          CALL      KASC0000              * Keyin ascii file
          BRANCH    EXIT,ML9900,ML9900
.
          CALL      KUSR0000              * Keyin user id
          BRANCH    EXIT,ML0000
.
          CALL      KPTH0000              * Keyin pathname
          BRANCH    EXIT,ML0000
.
          CALL      PRHD0000              * Print header
          CALL      POST0000              * Updating file
          GOTO      ML9999
.
ML9900    CALL      HEAD132
          PRINT     *N,"Failed to open upload file. "
          IF        EXIT=2
            PRINT     *N,"Check Version of the Spreadsheet. "
          ENDIF
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000                                   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,"Opening patsgcaf";
          OPEN      PATSGCA1,"patsgcaf"
          OPEN      PATITEM1,"patitemn"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATFN1A1,"patfn1af"
INIT9999  RETURN
+
.******************************************************************************
.*                                 KOPT0000                                   *
.*                            Upload Suggested Classification                 *
.******************************************************************************
KOPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COPTION
          DISPLAY   *P1:6,*EF:
                    *P1:6,*V2LON,"0",*HOFF," = Exit":
                    *P1:7,*V2LON,"1",*HOFF," = Upload Suggested Class.":
                    *P1:10,"Select Option : ";
.
KOPT1000  KEYIN     *P17:10,*V2LON,COPTION;
          BRANCH    COPTION,KOPT9999
.
          COMPARE   ZERO,COPTION
          GOTO      KOPT9000 IF EQUAL
          BEEP
          GOTO      KOPT1000
.
KOPT9000  MOVE      ONE,EXIT
KOPT9999  RETURN
+
.******************************************************************************
.*                                 KASC0000                                   *
.*                            Keyin ASCI file                                 *
.******************************************************************************
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEI WITH SP70,SP70,SP70
.
          DISPLAY   *P19:13,*EF,"Suggested Classification Load File";
.
          KEYIN     *P1:14,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEI
.
          PACK      FNAMEI,FNAMEI,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEI
          GOTO      KASC9000 IF EQUAL
.
          SCAN      ".txt",FNAMEI
          IF        !@EQUAL
            SQUEEZE   FNAMEI
            ENDSET    FNAMEI
            APPEND    ".txt",FNAMEI      * add ".txt" if there is a path
            APPEND    SP70,FNAMEI
            RESET     FNAMEI
          ENDIF
          RESET     FNAMEI
.
          DISPLAY   *P26:14,*V2LON,*EL,FNAMEI;
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      MBSSGTXT,FNAMEI
          TRAPCLR   IO
          BRANCH    OVRCD,KASC8000
.
KASC1000  READ      MBSSGTXT,SEQ;XFLD0001
          GOTO      KASC8200 IF OVER
.
          SCAN      "Description",XFLD0001
          GOTO      KASC1000 IF NOT EQUAL
.
          SCAN      "V001",XFLD0001
          GOTO      KASC8400 IF NOT EQUAL
.
          CALL      EXCUS000
.
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC8200  DISPLAY   *P1:24,*EL,*B,"File is empty.  ";
          CALL      HITENTER
          GOTO      KASC9000
.
KASC8400  DISPLAY   *P1:24,*EL,*B,"Check Version of the Spreadsheet.";
          CALL      HITENTER
          MOVE      TWO,EXIT
          GOTO      KASC9999
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
+
.******************************************************************************
.*                                  KUSR0000                                  *
.*                     Keyin user id                                          *
.******************************************************************************
KUSR0000  KEYIN     *P1:16,*EL,"Keyin Username: ",*P26:16,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.******************************************************************************
.*                                  KPTH0000                                  *
.*                     Keyin path name                                        *
.******************************************************************************
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P1:17,*EL,"Keyin Pathname: ",*P26:17,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
.         PACK      PATHNAME,PATHNAME,SP70,SP70,SP70
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.******************************************************************************
.*                                  POST0000                                  *
.*                        Post record                                         *
.******************************************************************************
POST0000  MOVE      ZERO,RECNUMB
          MOVE      ZERO,RECWRDT
          MOVE      ZERO,RECUPDT
          MOVE      ZERO,ERRNUMB
.
          OPEN      MBSSGTXT,FNAMEI
POST1000  READ      MBSSGTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004
          GOTO      POST9000 IF OVER
.
          UNPACK    SP70,XCLAIM,XHFUND,XMBSIT,XSCLAS
          PACK      XCLAIM,XFLD0001,SP70
          PACK      XHFUND,XFLD0002,SP70
          PACK      XMBSIT,XFLD0003,SP70
          PACK      XSCLAS,XFLD0004,SP70
          CALL      CRET0000              * Check for carriage return
.
          PACK      KEY21,XCLAIM,XHFUND,XMBSIT,XSCLAS,SP70
          MATCH     SP70,KEY21
          GOTO      POST1000 IF EQUAL
.
          ADD       ONE,RECNUMB
          CALL      VALC0000              * Validating
          BRANCH    EXIT,POST1000
.
          CALL      USGC0000              * Update Sugg.Class Record
          GOTO      POST1000
.
POST9000  CLOSE     MBSSGTXT
.
          PRINT     *N,*1,"Number of Records in txt file : ",RECNUMB:
                    *N,*1,"Number of Records Added       : ",RECWRDT:
                    *N,*1,"Number of Records Updated     : ",RECUPDT:
                    *N,*1,"Number of Errors occured      : ",ERRNUMB
.
POST9999  RETURN
+
.******************************************************************************
.         Check for Carriage return
.******************************************************************************
CRET0000  MOVE      SP70,KEY3
          CLEAR     KEY3
          SQUEEZE   XSCLAS
          RESET     XSCLAS
          GOTO      CRET1200
.
CRET1000  BUMP      XSCLAS
          GOTO      CRET2000 IF EOS
.
CRET1200  MATCH     CRETURN,XSCLAS
          GOTO      CRET2000 IF EQUAL
.
          MOVE      XSCLAS,ANS
          APPEND    ANS,KEY3
          GOTO      CRET1000
.
CRET2000  APPEND    SP70,KEY3
          RESET     KEY3
          MOVE      KEY3,XSCLAS
CRET9999  RETURN
+
.******************************************************************************
.                             VALC0000
.                         Validation
.******************************************************************************
VALC0000  MOVE      ZERO,ERRFLAG
.
          MATCH     SP70,XCLAIM
          GOTO      VALC1000 IF NOT EQUAL
.
          MATCH     SP70,XHFUND
          IF        @EQUAL
            MOVE      THREE,F2
            CALL      PERR0000                * Claim/HF can not be blank
          ENDIF
          GOTO      VALC1200
.
VALC1000  MOVE      "CL",KEY2
          PACK      KEY5,KEY2,XCLAIM,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      ONE,F2
            CALL      PERR0000                * Invalid Claim code
            GOTO      VALC1200
          ENDIF
.
          MATCH     "A",PTCOACTV
          IF        !@EQUAL
            MOVE      FOUR,F2
            CALL      PERR0000                * Inactive Claim code
          ENDIF
.
VALC1200  MATCH     SP70,XHFUND
          GOTO      VALC4000 IF EQUAL
.
          MOVE      "0000    ",KEY8
          PACK      KEY14,XHFUND,KEY8,SP70
          CALL      RDFUND1
          IF        OVRCD=1
            MOVE      TWO,F2
            CALL      PERR0000                * Invalid Health fund
            GOTO      VALC4000
          ENDIF
.
          MATCH     "1",DHFTACT
          IF        @EQUAL
            MOVE      FIVE,F2
            CALL      PERR0000                * Inactive Health fund
          ENDIF
.
VALC4000  MATCH     SP70,XMBSIT
          IF        @EQUAL
            MOVE      SIX,F2
            CALL      PERR0000                * Blank MBS item code
            GOTO      VALC5000
          ENDIF
.
          PACK      KEY17,XMBSIT,SP70
          CALL      PATITMRD
          IF        OVRCD=1
            MOVE      SEVEN,F2
            CALL      PERR0000                * Invalid MBS item code
            GOTO      VALC5000
          ENDIF
.
          MATCH     "I",PTITACTV
          IF        @EQUAL
            MOVE      EIGHT,F2
            CALL      PERR0000                * Inactive MBS item code
          ENDIF
.
VALC5000  MATCH     SP70,XSCLAS
          IF        @EQUAL
            MOVE      NINE,F2
            CALL      PERR0000                * Blank Suggested Class.
            GOTO      VALC8000
          ENDIF
.
          MOVE      "A ",KEY2
          PACK      KEY5,KEY2,XSCLAS,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      TEN,F2
            CALL      PERR0000                * Invalid Suggested Class.
            GOTO      VALC8000
          ENDIF
.
          MATCH     "A",PTCOACTV
          IF        !@EQUAL
            MOVE      TEN1,F2
            CALL      PERR0000                * Inactive Suggested Class.
          ENDIF
.
VALC8000  BRANCH    ERRFLAG,VALC9000
.
          MOVE      ZERO,EXIT
          GOTO      VALC99999
.
VALC9000  MOVE      ONE,EXIT
VALC9999  RETURN
.
.----------------------------------------------------------------------
.         Printing error message
.----------------------------------------------------------------------
PERR0000  COMPARE   ZERO,ERRFLAG
          GOTO      PERR0002 IF NOT EQUAL
.
          MOVE      ONE,ERRFLAG
          CALL      PREC0000                  * Print record details
.
PERR0002  BRANCH    F2,PERR0010,PERR0020,PERR0030,PERR0040,PERR0050:
                       PERR0060,PERR0070,PERR0080,PERR0090,PERR0100:
                       PERR0110
          GOTO      PERR9999
.
PERR0010  PRINT     *58,"Invalid Claim Code"
          GOTO      PERR9000
.
PERR0020  PRINT     *58,"Invalid Health Fund"
          GOTO      PERR9000
.
PERR0030  PRINT     *58,"Claim Code or Health Fund can not be blank"
          GOTO      PERR9000
.
PERR0040  PRINT     *58,"Claim Code is Inactive."
          GOTO      PERR9000
.
PERR0050  PRINT     *58,"Health Fund Code is Inactive."
          GOTO      PERR9000
.
PERR0060  PRINT     *58,"MBS Item Code can not be blank"
          GOTO      PERR9000
.
PERR0070  PRINT     *58,"Invalid MBS Item Code"
          GOTO      PERR9000
.
PERR0080  PRINT     *58,"MBS Item Code is Inactive."
          GOTO      PERR9000
.
PERR0090  PRINT     *58,"Suggested Classification Code can not be blank"
          GOTO      PERR9000
.
PERR0100  PRINT     *58,"Invalid Suggested Classification Code"
          GOTO      PERR9000
.
PERR0110  PRINT     *58,"Suggested Classification Code is Inactive."
          GOTO      PERR9000
.
PERR9000  MOVE      ONE,EXIT
          ADD       ONE,ERRNUMB
PERR9999  RETURN
+
.----------------------------------------------------------------------
. Subroutine to post Suggested Class.
.----------------------------------------------------------------------
USGC0000  PACK      KEY18,XCLAIM,XHFUND,XMBSIT,SP70
          CALL      RDPTSGC1
          IF        OVRCD=1
            UNPACK    KEY18,PTSGCLMN,PTSGFUND,PTSGMBS
            MOVE      XSCLAS,PTSGCLSS
            MOVE      SP70,PTSGSPAR
            CALL      WRPTSGC1
            ADD       ONE,RECWRDT
          ELSE
            MOVE      XSCLAS,PTSGCLSS
            CALL      UPPTSGC1
            ADD       ONE,RECUPDT
          ENDIF
USGC9999  RETURN
+
.******************************************************************************
.         Print record
.******************************************************************************
PREC0000  PRINT     *1,XCLAIM,*8,XHFUND,*28,XMBSIT,*40,XSCLAS;
PREC9999  RETURN
+
.******************************************************************************
.*                                  PRHD0000                                  *
.*                        Print the selection details                         *
.******************************************************************************
PRHD0000  CALL      HEAD132
          PRINT     *N,"Date               : ",CDATE:
                    *N,"Time               : ",CTIMEIS:
                    *N,"User ID            : ",USERID:
                    *N,"ICD10 Load file    : ",PATHNAME:
                    *N,*N,*1,"Claim",*8,"Health Fund":
                    *28,"MBS Item",*40,"Suggested Class.":
                    *58,"Error Description":
                    *N,*1,"-----",*8,"-----------------":
                    *28,"----------",*40,"---------------":
                    *58,"--------------------------------------"
PRHD9999  RETURN
+
.******************************************************************************
.         Execute us1 command before processing
.******************************************************************************
EXCUS000  CLEAR     CMDLINE
          APPEND    "ibaadt07.us1 ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
EXCUS999  RETURN
+
.******************************************************************************
          INC       STD001IO
          INC       PATITMRD
.
          INC       PATCODIO/INC            * Codes file
          INC       PATITMIO/INC
          INC       PATSGCIO/INC
          INC       PATFN1IO/INC
.
.
.
.

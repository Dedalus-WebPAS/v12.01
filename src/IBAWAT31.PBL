.******************************************************************************
.* System    :   WAITING LISTS                                                *
.* Program   :   IBAWAT31                                                     *
.* Desc      :   Waiting lists - Ad-Hoc Reporting                             *
.******************************************************************************
.* Date      :   09/10/88                                                     *
.* Author    :   N.S.                                                         *
.* Comments  :   V5.00  22.02.89  Eric Phan     SRF 003361 & 003362           *
.*               Added "?" options.                                           *
.* Modified  :                                                                *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V5.08.02  01/09/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.02  03/11/1999 J Habasque                                     *
.*                  Changed to display inactive codes                         *
.*        V5.07.01  26/10/1999 J Habasque                                     *
.*                  Recompiled for WATDSPRO                                   *
.                   22/10/1998  Glenn Berry      5.07 changes
.******************************************************************************
.*        V5.01     07.04.89  L.P.                                            *
.*                  Screen Display, no SRF num.                               *
.*        V5.02     03.06.89 S.Barcham                                        *
.*                  Recompiled for locking in PATIOMAS   SRF 100809           *
.*        V5.01.01  19/06/89 M.Ohleiter                                       *
.*                  Changes for new release 5.01                              *
.*                  09/07/89 J.Clark                                          *
.*                  Added Key26 variable added to PATIODOC by SOG             *
.*        V5.01.02  23/02/90  E.Phan                                          *
.*                  Recompiled for WATPROFD.                                  *
.*        V5.01.121 27/11/90 Glen Hobby                                       *
.*                  Recompiled for changes to PATMX1FD                        *
.*        V5.01.122 29/01/1992  Graeme Williams                               *
.*                  Use category description for category "A "                *
.*        V5.01.123 19/02/1993    Justin Coates  (DUDLEY)                     *
.*                  Allow for status 7/8                                      *
.*        V5.01.124 26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
. *       V5.04.01  15/04/1997  howellsy   RHA                                *
. *                 Recompiled for WATDSPRO                                   *
.*        V5.04.02  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.***************************************************************************
.
          INC       STD001FD
          INC       WATTR1FD/INC
          INC       WATPROFD/INC
          INC       WATCONFD/INC
          INC       PATMA1FD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATCONFD/INC
.
CATADES   DIM       20
CHOICE    FORM      2
CODE      DIM       2
CODEA     INIT      "A "          *          A  (patient catagory)
CODETP    INIT      "TP"          * catagory TP (priority)
CODEW1    INIT      "W1"          * catagory W1
CODEW2    INIT      "W2"          * catagory W2
CODEW3    INIT      "W3"          * catagory W3
CODEW4    INIT      "W4"          * catagory W4
CODEWS    INIT      "WS"          *          WS (short notice)
CODERS    INIT      "T "          *          RS (resident status)
CODEWU    INIT      "WU"          * catagory WU (clinic/unit)
CODEWX    DIM       3             * store codew.. (1-4)
COL       FORM      2             * for WATDS*
COUNT     FORM      2
DDESCWU   DIM       20            * display variable for referral/unit
.
DATE10    DIM       10           * wmdate1  (dd/mm/ccyy)
DATE20    DIM       10           * wmdate2  (dd/mm/ccyy)
DATE30    DIM       10           * wmdate3  (dd/mm/ccyy)
DATE40    DIM       10           * wmdate4  (dd/mm/ccyy)
DESC20    DIM       20           * store wmdesc
DESCDEF1  DIM       20           * description for code in W1
DESCDEF2  DIM       20           *             for code in W2
DESCDEF3  DIM       20           *             for code in W3
DESCDEF4  DIM       20           *             for code in W4
DESCDEFX  DIM       20           * store descdef..
DESCNOT   DIM       20           * description for short notice (cata WS)
DESCPCAT  DIM       20           * description for patient catag (cata A)
DESCPTYP  DIM       20           * description for Waiting type (cata WT)
DESCTP    DIM       20           * description for priority (cata TP)
DESCWU    DIM       20           * description for clinic/unit (cata WU)
DIM10     DIM       10
DIM3      DIM       3            * patdsdoc
DIM8      DIM       8            * store wmurno
DIMC01    DIM       19
DIMC02    DIM       19
DIMC03    DIM       19
DIMC04    DIM       19
DIMC05    DIM       19
DIMC06    DIM       19
DIMC07    DIM       19
DIMC08    DIM       19
DIMC09    DIM       19
DIMC10    DIM       19
DIMC11    DIM       19
DIMC12    DIM       19
DIMC13    DIM       19
DIMC14    DIM       19
DIMC15    DIM       19
DIMC16    DIM       19
DISPACT   FORM      1
DOCNAME   DIM       32           * doctor title initial.surname for display
DOCCODE   DIM       6
DSEX      DIM       8            * descr. sex                 * was 6  *C-49016
. ----- flags for parameters keyed in or not  : 0= no  1= yes
FLGCO     FORM      1        * proccode
FLGD1     FORM      1        * date on list
FLGD2     FORM      1        * last review date
FLGD3     FORM      1        * scheduled admission
FLGDO     FORM      1        * doctor
FLGES     FORM      1        * estimated stay
FLGPC     FORM      1        * patient category
FLGPT     FORM      1        * priority
FLGSN     FORM      1        * short notice
FLGST     FORM      1        * treatment status
FLGUN     FORM      1        * unit
FLGUR     FORM      1        * UR
FLGRS     FORM      1        * resident status 
.
KSTCODE   DIM       9
KSTDATE1  DIM       8
KSTDATE2  DIM       8
KSTDATE3  DIM       8
KSTDOC    DIM       6
KSTNOT    DIM       3
KSTPCAT   DIM       3
KSTPTY    DIM       3
KSTSTAT   FORM      1
KSTSTAY   FORM      5
KSTUNIT   DIM       3
KSTUR     DIM       8
KSTWTYP   DIM       3
KTOCODE   DIM       9
KTODATE1  DIM       8
KTODATE2  DIM       8
KTODATE3  DIM       8
KTODOC    DIM       6
KTOSTAY   FORM      5
KTOUNIT   DIM       3
KTOUR     DIM       8
.
PATDOB    DIM       10           * DOB patient
PATNAME   DIM       26           * name patient
.
STAT1     INIT      "Unscheduled  "
STAT2     INIT      "Scheduled    "
STAT3     INIT      "Pre-admitted "
STAT4     INIT      "Admitted     "
STAT5     INIT      "Discharged   "
STAT6     INIT      "Removed      "
STAT7     INIT      "Performed    "
STAT8     INIT      "Not Performed"
STATUS    DIM       13
STATUS6   DIM       6
.
WAT5FLAG  FORM      "1"          * watdspro : =1 display rec type P and H
.                                             =0 only rec type P (IBAWAT01)
VERT      FORM      2
.
PRGNAM    INIT      "Ad-Hoc Reporting"
PRGID     INIT      "IBAWAT31"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
.
          CALL      INIT0000               * initialization and open files
.
ML1000    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999
.
ML3000    CALL      CLRP0000               * clear parameter vars
          CALL      SCRP0000               * parameter screen + keyin params
          CALL      SELT0000               * select to update
          BRANCH    EXIT,ML1000            * (C)ancel selected
          CALL      EXTR0000               * extract details
          BRANCH    EXIT,ML3000
          CALL      DETT0000               * get treatm descriptions
          CALL      PDOB0000               * get patient details
          CALL      SCRT0000               * detailed treatment screen
          CALL      DIST0000               * display details treatment
          CALL      PRTT0000               * print treatment ?
          BRANCH    EXIT,ML3000
          CALL      PRND0000               * print treatment details
          GOTO      ML3000
.
ML9999    CHAIN     PGM
          STOP
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialization                               *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P52:24,"Opening :";
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A2,"wattr1af"
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A3,"wattr1af"
          DISPLAY   *P64:24,"watproaf";
          OPEN      WATPROA1,"watproaf"
          OPEN      WATPROA3,"watproaf"
.
          MOVE      SP20,TDESC
          PACK      KEY5 WITH CODEWU,SP5
          CALL      RDCODE1
          MOVE      TDESC,DDESCWU
.
          MOVE      SP20,TDESC
          PACK      KEY5 WITH CODEA,SP5
          CALL      RDCODE1
          MOVE      TDESC,CATADES
.
          DISPLAY   *P1:24,*EL;
.
INIT9999  RETURN
+
.****************************************************************************
.*          OPTN0000                                                        *
.*          get user options or exit                                        *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000  MOVE      FALSE,EXIT
          HLINE     *G37,2,48,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF,". Exit":
                    *P1:5,*V2LON,"1",*HOFF,". Reporting":
                    *P1:7,"Select Option : _";
.
. ---- select and validate option selected ----
.
OPTN1000  KEYIN     *P17:7,*V2LON,OPTION,*P1:4,*EF;
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL              * exit
          BRANCH    OPTION,OPTN9999
          GOTO      OPTN1000
.
OPTN9000  MOVE      TRUE,EXIT
OPTN9999  RETURN
.
.**************************************************************************
.*          CLRP0000                                                      *
.*          clear keyin parameter variables                               *
.**************************************************************************
.
CLRP0000  MOVE      SP8,KSTUR
          MOVE      SP8,KTOUR
          PACK      KSTCODE,SP10
          PACK      KTOCODE,SP10
          MOVE      ZERO,KSTSTAT
          PACK      KSTDATE1,SP10
          PACK      KTODATE1,SP10
          PACK      KSTDATE2,SP10
          PACK      KTODATE2,SP10
          PACK      KSTDATE3,SP10
          PACK      KTODATE3,SP10
          PACK      KSTUNIT,SP10
          PACK      KTOUNIT,SP10
          PACK      KSTDOC,SP10
          PACK      KTODOC,SP10
          MOVE      ZERO,KSTSTAY
          MOVE      "99999",KTOSTAY
          PACK      KSTPTY,SP10
          PACK      KSTNOT,SP10
          PACK      KSTPCAT,SP10
          PACK      KSTWTYP,SP10
          MOVE      ONE,FLGUR
          MOVE      ONE,FLGCO
          MOVE      ONE,FLGST
          MOVE      ONE,FLGD1
          MOVE      ONE,FLGD2
          MOVE      ONE,FLGD3
          MOVE      ONE,FLGUN
          MOVE      ONE,FLGDO
          MOVE      ONE,FLGES
          MOVE      ONE,FLGPT
          MOVE      ONE,FLGSN
          MOVE      ONE,FLGPC
          MOVE      ONE,FLGRS
.
          RETURN
.
.**************************************************************************
.*          SCRP0000                                                      *
.*          parameter screen layout                                       *
.**************************************************************************
.
SCRP0000  DISPLAY   *P1:3,*EF:
                    *P1:4,SP1,*V2LON,ONE,*HOFF,". U/R Number          : ":
                    "________   To ________  ":
                    *P1:5,SP1,*V2LON,TWO,*HOFF,". Procedure Code      : ":
                    "_________  To _________ ":
                    *P1:6,SP1,*V2LON,THREE,*HOFF,". Treatment Status    : ":
                    "_ ":
                    *P1:7,SP1,*V2LON,FOUR,*HOFF,". Date On List        : ":
                    "__/__/____ To __/__/____ ":
                    *P1:8,SP1,*V2LON,FIVE,*HOFF,". Last Review Date    : ":
                    "__/__/____ To __/__/____ ":
                    *P1:9,SP1,*V2LON,SIX,*HOFF,". Scheduled Admission : ":
                    "__/__/____ To __/__/____ ":
                    *P1:10,SP1,*V2LON,SEVEN,*HOFF,". ",DDESCWU,*P25:10,": ":
                    "___        To ___ ":
                    *P1:11,SP1,*V2LON,EIGHT,*HOFF,". Doctor              : ":
                    "______     To ______ ":
                    *P1:12,SP1,*V2LON,NINE,*HOFF,". Estimated Stay      : ":
                    "_____      To _____ ":
                    *P1:13,*V2LON,TEN,*HOFF,". Priority            : ":
                    "___ ":
                    *P1:14,*V2LON,TEN1,*HOFF,". Short Notice        : ":
                    "___ ":
                    *P1:15,*V2LON,TEN2,*HOFF,". ",CATADES,*P25:15,": ":
                    "___ ":
                    *P1:16,*V2LON,TEN3,*HOFF,". Resident Status     : ":
                    "___ "
          RETURN
.
.**************************************************************************
.*          SCRP1000                                                      *
.*          parameter screen details                                      *
.**************************************************************************
.
SCRP1000  BRANCH    FLGUR,SCRP1010
          DISPLAY   *P27:4,*V2LON,KSTUR,*P41:4,KTOUR
.
SCRP1010  BRANCH    FLGCO,SCRP1020
          DISPLAY   *P27:5,*V2LON,KSTCODE,*P41:5,KTOCODE
.
SCRP1020  BRANCH    FLGST,SCRP1030
          DISPLAY   *P27:6,*V2LON,KSTSTAT
.
SCRP1030  BRANCH    FLGD1,SCRP1040
          UNPACK    KSTDATE1,CCENT,CYEAR,CMON,CDAY
          DISPLAY   *P27:7,*V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          UNPACK    KTODATE1,CCENT,CYEAR,CMON,CDAY
          DISPLAY   *P41:7,*V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
SCRP1040  BRANCH    FLGD2,SCRP1050
          UNPACK    KSTDATE2,CCENT,CYEAR,CMON,CDAY
          DISPLAY   *P27:8,*V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          UNPACK    KTODATE2,CCENT,CYEAR,CMON,CDAY
          DISPLAY   *P41:8,*V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
SCRP1050  BRANCH    FLGD3,SCRP1060
          UNPACK    KSTDATE3,CCENT,CYEAR,CMON,CDAY
          DISPLAY   *P27:9,*V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          UNPACK    KTODATE3,CCENT,CYEAR,CMON,CDAY
          DISPLAY   *P41:9,*V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
SCRP1060  BRANCH    FLGUN,SCRP1070
          DISPLAY   *P27:10,*V2LON,KSTUNIT,*P41:10,KTOUNIT
.
SCRP1070  BRANCH    FLGDO,SCRP1080
          DISPLAY   *P27:11,*V2LON,KSTDOC,*P41:11,KTODOC
.
SCRP1080  BRANCH    FLGES,SCRP1090
          DISPLAY   *P27:12,*V2LON,KSTSTAY,*P41:12,KTOSTAY
.
SCRP1090  BRANCH    FLGPT,SCRP1100
          DISPLAY   *P27:13,*V2LON,KSTPTY
.
SCRP1100  BRANCH    FLGSN,SCRP1110
          DISPLAY   *P27:14,*V2LON,KSTNOT
.
SCRP1110  BRANCH    FLGPC,SCRP1120
          DISPLAY   *P27:15,*V2LON,KSTPCAT
.
SCRP1120  BRANCH    FLGRS,SCRP1130
          DISPLAY   *P27:16,*V2LON,KSTWTYP
.
SCRP1130  RETURN
.
.*****************************************************************************
.*           SELT0000                                                        *
.*           select parameter(s) to fill in                                  *
.*****************************************************************************
.
SELT0000  CALL      MAINQST
          COMPARE   ZERO,CCITEM
          GOTO      SELT1000 IF EQUAL
          COMPARE   SEQ,CCITEM
          GOTO      SELT2000 IF EQUAL
          BRANCH    CCITEM,SELT4000,SELT5000,SELT6000,SELT7000,SELT8000:
                           SELT9000,SELU1000,SELU2000,SELU3000,SELU4000:
                           SELU5000,SELU6000,SELU7000
          BEEP
          GOTO      SELT0000
.
SELT1000  MOVE      ZERO,EXIT         * Continue with reporting
          RETURN
SELT2000  MOVE      ONE,EXIT          * Cancel
          RETURN
.
.------------------------------------------------------------------------------
. ----- Keyin UR range -----
.------------------------------------------------------------------------------
.
SELT4000  MOVE      SP8,KSTUR
          MOVE      ONE,FLGUR
          DISPLAY   *P27:4,*EL,"________   To ________ ";
          KEYIN     *P27:4,*V2LON,*RV,KSTUR,*P27:4,*JR,*DV,KSTUR
          MOVE      "99999999",KTOUR
          KEYIN     *P41:4,*V2LON,*RV,KTOUR,*P41:4,*JR,*DV,KTOUR
          MATCH     KSTUR,KTOUR
          GOTO      SELT4500 IF LESS
          GOTO      SELT4600
.
SELT4500  DISPLAY   *P50:4,*V2LON,"End UR must be > ":
                    "start UR",*W,*P50:4,*EL
          GOTO      SELT4000
.
SELT4600  MATCH     SP8,KSTUR
          GOTO      SELT4999 IF EQUAL            * no value keyed in
          MOVE      ZERO,FLGUR                   * got a value
SELT4999  GOTO      SELU9000
.
.------------------------------------------------------------------------------
. ----- Keyin procedure code range -----
.------------------------------------------------------------------------------
.
SELT5000  MOVE      ONE,FLGCO
          DISPLAY   *P27:5,*EL,"_________  To _________ ";
.
SELT5100  KEYIN     *P27:5,*V2LON,KSTCODE:
                    *P27:5,*DV,KSTCODE
          ENDSET    KSTCODE
          APPEND    SP9,KSTCODE
          RESET     KSTCODE
          CMATCH    QUEST,KSTCODE
          GOTO      SELT5200 IF NOT EQUAL
          MOVE      ONE,DISPACT
          CALL      WATDSPRO
          CALL      SCRP0000
          CALL      SCRP1000
          GOTO      SELT5100
.
SELT5200  KEYIN     *P41:5,*V2LON,*EL,KTOCODE:
                    *P41:5,*DV,KTOCODE
          ENDSET    KTOCODE
          APPEND    SP9,KTOCODE
          RESET     KTOCODE
          CMATCH    QUEST,KTOCODE
          GOTO      SELT5300 IF NOT EQUAL
          MOVE      ONE,DISPACT
          CALL      WATDSPRO
          CALL      SCRP0000
          CALL      SCRP1000
          DISPLAY   *P27:5,*V2LON,KSTCODE
          GOTO      SELT5200
.
SELT5300  MATCH     KTOCODE,KSTCODE
          GOTO      SELT5500 IF EQUAL
          GOTO      SELT5500 IF LESS
          DISPLAY   *P50:5,*V2LON,"End must be > than start",*W,*P50:5,*EL
          GOTO      SELT5000
.
SELT5500  MATCH     SP9,KSTCODE
          GOTO      SELT5999 IF EQUAL            * no value keyed in
          MOVE      ZERO,FLGCO                   * got a value
.
SELT5999  GOTO      SELU9000
.
.------------------------------------------------------------------------------
. ----- Keyin treatment status -----
.------------------------------------------------------------------------------
.
SELT6000  MOVE      SP6,STATUS6
          MOVE      ONE,FLGST
          KEYIN     *P27:6,*EL,"_":
                    *P27:6,*V2LON,ANS:
                    *P27:6,*DV,ANS
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
          CMATCH    QUEST,ANS
          GOTO      SELT6100 IF NOT EQUAL
          CALL      WATDSSTA
          CALL      SCRP0000
          CALL      SCRP1000
          GOTO      SELT6000
.
SELT6100  REP       " 0",ANS
          TYPE      ANS
          GOTO      SELT6200 IF NOT EQUAL
          MOVE      ANS,KSTSTAT
          COMPARE   ZERO,KSTSTAT
          GOTO      SELT6999 IF EQUAL            * no value keyed in
          COMPARE   NINE,KSTSTAT
          GOTO      SELT6200 IF NOT LESS
          LOAD      STATUS6,KSTSTAT,STAT1,STAT2,STAT3,STAT4:
                                    STAT5,STAT6,STAT7,STAT8
          DISPLAY   *P30:6,*V2LON,STATUS6
          MOVE      ZERO,FLGST                   * got a value
          GOTO      SELT6999
.
SELT6200  BEEP
          GOTO      SELT6000
.
SELT6999  GOTO      SELU9000
.
.------------------------------------------------------------------------------
. ----- Keyin date on list range -----
.------------------------------------------------------------------------------
.
SELT7000  MOVE      SP10,KSTDATE1
          MOVE      ONE,FLGD1
          UNPACK    KSTDATE1 INTO CDAY,CMON,CCENT,CYEAR
          MOVE      CCC,CCENT
          DISPLAY   *P27:7,*EL,"__/__/____ To __/__/____ ";
          MOVE      ZERO,CHIGHLT
          MOVE      TWENTY7,CCOL
          MOVE      SEVEN,CVERT
          CALL      KEYDATE
          PACK      KSTDATE1 WITH CCENT,CYEAR,CMON,CDAY
          MOVE      SP10,KTODATE1
          UNPACK    KTODATE1 INTO CDAY,CMON,CCENT,CYEAR
          MOVE      CCC,CCENT
          MOVE      "41",CCOL
          MOVE      SEVEN,CVERT
          CALL      KEYDATE
          PACK      KTODATE1 WITH CCENT,CYEAR,CMON,CDAY
.
          MATCH     KTODATE1,KSTDATE1
          GOTO      SELT7500 IF EQUAL
          GOTO      SELT7500 IF LESS
          DISPLAY   *P53:7,*EL,*V2LON,"To date > from date",*W,*P53:7,*EL
          GOTO      SELT7000
.
SELT7500  MATCH     SP10,KSTDATE1
          GOTO      SELT7999 IF EQUAL
          MOVE      ZERO,FLGD1
SELT7999  GOTO      SELU9000
.
.------------------------------------------------------------------------------
. ----- Keyin last review date range -----
.------------------------------------------------------------------------------
.
SELT8000  MOVE      SP10,KSTDATE2
          MOVE      ONE,FLGD2
          UNPACK    KSTDATE2 INTO CDAY,CMON,CCENT,CYEAR
          MOVE      CCC,CCENT
          DISPLAY   *P27:8,*EL,"__/__/____ To __/__/____ ";
          MOVE      ZERO,CHIGHLT
          MOVE      TWENTY7,CCOL
          MOVE      EIGHT,CVERT
          CALL      KEYDATE
          PACK      KSTDATE2 WITH CCENT,CYEAR,CMON,CDAY
          MOVE      SP10,KTODATE2
          UNPACK    KTODATE2 INTO CDAY,CMON,CCENT,CYEAR
          MOVE      CCC,CCENT
          MOVE      "41",CCOL
          MOVE      EIGHT,CVERT
          CALL      KEYDATE
          PACK      KTODATE2 WITH CCENT,CYEAR,CMON,CDAY
.
          MATCH     KTODATE2,KSTDATE2
          GOTO      SELT8500 IF EQUAL
          GOTO      SELT8500 IF LESS
.
          DISPLAY   *P53:8,*EL,*V2LON,"To date > from date",*W,*P53:8,*EL
          GOTO      SELT8000
.
SELT8500  MATCH     SP10,KSTDATE2
          GOTO      SELT8999 IF EQUAL
          MOVE      ZERO,FLGD2
SELT8999  GOTO      SELU9000
.
.------------------------------------------------------------------------------
. ----- Keyin scheduled admission date range -----
.------------------------------------------------------------------------------
.
SELT9000  MOVE      SP10,KSTDATE3
          MOVE      ONE,FLGD3
          UNPACK    KSTDATE3 INTO CDAY,CMON,CCENT,CYEAR
          MOVE      CCC,CCENT
          DISPLAY   *P27:9,*EL,"__/__/____ To __/__/____ ";
          MOVE      ZERO,CHIGHLT
          MOVE      TWENTY7,CCOL
          MOVE      NINE,CVERT
          CALL      KEYDATE
          PACK      KSTDATE3 WITH CCENT,CYEAR,CMON,CDAY
          MOVE      SP10,KTODATE3
          UNPACK    KTODATE3 INTO CDAY,CMON,CCENT,CYEAR
          MOVE      CCC,CCENT
          MOVE      "41",CCOL
          MOVE      NINE,CVERT
          CALL      KEYDATE
          PACK      KTODATE3 WITH CCENT,CYEAR,CMON,CDAY
.
          MATCH     KTODATE3,KSTDATE3
          GOTO      SELT9500 IF EQUAL
          GOTO      SELT9500 IF LESS
          DISPLAY   *P53:9,*EL,*V2LON,"To date > from date",*W,*P53:9,*EL
          GOTO      SELT9000
.
SELT9500  MATCH     SP10,KSTDATE3
          GOTO      SELT9999 IF EQUAL
          MOVE      ZERO,FLGD3
SELT9999  GOTO      SELU9000
.
.------------------------------------------------------------------------------
. ----- Keyin unit range -----
.------------------------------------------------------------------------------
.
SELU1000  MOVE      ONE,FLGUN
          DISPLAY   *P27:10,*EL,"___        To ___        ";
.
SELU1100  KEYIN     *P27:10,*V2LON,KSTUNIT,*P27:10,*DV,KSTUNIT
          ENDSET    KSTUNIT
          APPEND    SP3,KSTUNIT
          RESET     KSTUNIT
          CMATCH    QUEST,KSTUNIT
          GOTO      SELU1200 IF NOT EQUAL
          MOVE      CODEWU,CODE
          CALL      PATCODDS
          CALL      SCRP0000
          CALL      SCRP1000
          GOTO      SELU1100
.
SELU1200  KEYIN     *P41:10,*V2LON,KTOUNIT,*P41:10,*DV,KTOUNIT
          ENDSET    KTOUNIT
          APPEND    SP3,KTOUNIT
          RESET     KTOUNIT
          CMATCH    QUEST,KTOUNIT
          GOTO      SELU1300 IF NOT EQUAL
          MOVE      CODEWU,CODE
          CALL      PATCODDS
          CALL      SCRP0000
          CALL      SCRP1000
          DISPLAY   *P27:10,*V2LON,KSTUNIT
          GOTO      SELU1200
.
SELU1300  MATCH     KTOUNIT,KSTUNIT
          GOTO      SELU1500 IF EQUAL
          GOTO      SELU1500 IF LESS
.
          DISPLAY   *P50:10,*EL,*V2LON,"End value > start value",*W:
                    *P50:10,*EL
          GOTO      SELU1000
.
SELU1500  MATCH     SP3,KSTUNIT
          GOTO      SELU1999 IF EQUAL
          MOVE      ZERO,FLGUN
SELU1999  GOTO      SELU9000
.
.------------------------------------------------------------------------------
. ----- Keyin doctor range -----
.------------------------------------------------------------------------------
.
SELU2000  MOVE      ONE,FLGDO
          DISPLAY   *P27:11,*EL,"______     To ______     ";
.
SELU2100  MOVE      SP10,KSTDOC
          MOVE      "27",ECOL
          MOVE      "11",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,SELU2200,SELU2100
.
          MOVE      DCODE,KSTDOC
.
SELU2200  MOVE      SP10,KTODOC
          MOVE      "41",ECOL
          MOVE      "11",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,SELU2300,SELU2200
.
          MOVE      DCODE,KTODOC
.
SELU2300  MATCH     KTODOC,KSTDOC
          GOTO      SELU2500 IF EQUAL
          GOTO      SELU2500 IF LESS
.
          DISPLAY   *P50:11,*EL,*V2LON,"End value > start value",*W:
                    *P50:11,*EL
          GOTO      SELU2000
.
SELU2500  MATCH     SP6,KSTDOC
          GOTO      SELU2999 IF EQUAL
          MOVE      ZERO,FLGDO
SELU2999  GOTO      SELU9000
.
.------------------------------------------------------------------------------
. ----- Keyin estimated stay range -----
.------------------------------------------------------------------------------
.
SELU3000  MOVE      ONE,FLGES
          DISPLAY   *P27:12,*EL,"_____      To _____ ";
.
          MOVE      ZERO,KSTSTAY
          KEYIN     *P27:12,*V2LON,KSTSTAY,*P27:12,*DV,KSTSTAY
.
          MOVE      "99999",KTOSTAY
          KEYIN     *P41:12,*V2LON,KTOSTAY,*P41:12,*DV,KTOSTAY
.
          COMPARE   KTOSTAY,KSTSTAY
          GOTO      SELU3500 IF EQUAL
          GOTO      SELU3500 IF LESS
.
          DISPLAY   *P50:12,*EL,*V2LON,"End value > start value",*W:
                    *P50:12,*EL
          GOTO      SELU3000
.
SELU3500  COMPARE   ZERO,KSTSTAY
          GOTO      SELU3999 IF EQUAL
          MOVE      ZERO,FLGES
SELU3999  GOTO      SELU9000
.
.------------------------------------------------------------------------------
. ----- Keyin priority -----
.------------------------------------------------------------------------------
.
SELU4000  MOVE      ONE,FLGPT
          DISPLAY   *P27:13,*EL,"___";
.
SELU4100  KEYIN     *P27:13,*V2LON,KSTPTY,*P27:13,*DV,KSTPTY
          ENDSET    KSTPTY
          APPEND    SP3,KSTPTY
          RESET     KSTPTY
          CMATCH    QUEST,KSTPTY
          GOTO      SELU4200 IF NOT EQUAL
          MOVE      CODETP,CODE
          CALL      PATCODDS
          CALL      SCRP0000
          CALL      SCRP1000
          GOTO      SELU4100
.
SELU4200  MATCH     SP3,KSTPTY
          GOTO      SELU4999 IF EQUAL
.
          PACK      KEY5,CODETP,KSTPTY
          CALL      RDCODE1
          BRANCH    OVRCD,SELU4100
          MOVE      TDESC,DESCTP
          DISPLAY   *P50:13,*EL,*V2LON,DESCTP
          MOVE      ZERO,FLGPT
.
SELU4999  GOTO      SELU9000
.
.------------------------------------------------------------------------------
. ----- Keyin short notice -----
.------------------------------------------------------------------------------
.
SELU5000  MOVE      ONE,FLGSN
          DISPLAY   *P27:14,*EL,"___ ";
.
SELU5100  KEYIN     *P27:14,*V2LON,KSTNOT,*P27:14,*DV,KSTNOT
          ENDSET    KSTNOT
          APPEND    SP3,KSTNOT
          RESET     KSTNOT
          CMATCH    QUEST,KSTNOT
          GOTO      SELU5200 IF NOT EQUAL
          MOVE      CODEWS,CODE
          CALL      PATCODDS
          CALL      SCRP0000
          CALL      SCRP1000
          GOTO      SELU5100
.
SELU5200  MATCH     SP3,KSTNOT
          GOTO      SELU5999 IF EQUAL
.
          PACK      KEY5,CODEWS,KSTNOT
          CALL      RDCODE1
          BRANCH    OVRCD,SELU5100
          MOVE      TDESC,DESCNOT
          DISPLAY   *P50:14,*EL,*V2LON,DESCNOT
          MOVE      ZERO,FLGSN
.
SELU5999  GOTO      SELU9000
.
.------------------------------------------------------------------------------
. ----- Keyin patient category -----
.------------------------------------------------------------------------------
.
SELU6000  MOVE      ONE,FLGPC
          DISPLAY   *P27:15,*EL,"___ ";
.
SELU6100  KEYIN     *P27:15,*V2LON,KSTPCAT,*P27:15,*DV,KSTPCAT
          ENDSET    KSTPCAT
          APPEND    SP3,KSTPCAT
          RESET     KSTPCAT
          CMATCH    QUEST,KSTPCAT
          GOTO      SELU6200 IF NOT EQUAL
          MOVE      CODEA,CODE
          CALL      PATCODDS
          CALL      SCRP0000
          CALL      SCRP1000
          GOTO      SELU6100
.
SELU6200  MATCH     SP3,KSTPCAT
          GOTO      SELU6999 IF EQUAL
.
          PACK      KEY5,CODEA,KSTPCAT
          CALL      RDCODE1
          BRANCH    OVRCD TO SELU6100
          MOVE      TDESC,DESCPCAT
          DISPLAY   *P50:15,*EL,*V2LON,DESCPCAT
          MOVE      ZERO,FLGPC
.
SELU6999  GOTO      SELU9000
.
.------------------------------------------------------------------------------
. ----- Keyin waiting type -----
.------------------------------------------------------------------------------
.
SELU7000  MOVE      ONE,FLGRS
          DISPLAY   *P27:16,*EL,"___ ";
.
SELU7100  KEYIN     *P27:16,*V2LON,KSTWTYP,*P27:16,*DV,KSTWTYP
          ENDSET    KSTWTYP
          APPEND    SP3,KSTWTYP
          RESET     KSTWTYP
          CMATCH    QUEST,KSTWTYP
          GOTO      SELU7200 IF NOT EQUAL
.
          MOVE      CODERS,CODE
          CALL      PATCODDS
          CALL      SCRP0000
          CALL      SCRP1000
          GOTO      SELU7100
.
SELU7200  MATCH     SP3,KSTWTYP
          GOTO      SELU9000 IF EQUAL
.
          PACK      KEY5,CODERS,KSTWTYP
          CALL      RDCODE1
          BRANCH    OVRCD,SELU7400
          MOVE      TDESC,DESCPTYP
          DISPLAY   *P50:16,*EL,*V2LON,DESCPTYP
          MOVE      ZERO,FLGRS
          GOTO      SELU9000
.
SELU7400  DISPLAY   *P50:16,*EL,*V2LON,"Invalid Waiting Type",*W
          GOTO      SELU7100
.
SELU9000  GOTO      SELT0000
.
.******************************************************************************
.*           EXTR0000                                                         *
.*           extract records from treatment file                              *
.*           check if valid ( check parameters)                               *
.******************************************************************************
.
EXTR0000  DISPLAY   *P1:4,*EF:
                    *ULON,*P1:4,"Cnt",*P5:4,"  U/R No",*P14:4,"Code     ":
                    *P24:4,"Description         ",*P45:4,"On List   ":
                    *P56:4,"Review    ",*P67:4,"Status"
          MOVE      ZERO,COUNT
          MOVE      FIVE,VERT
          MOVE      FALSE,EXIT
          PACK      KEY27 WITH KSTDATE1,SP20
          CALL      RDSTREA3
EXTR0500  CALL      RDKTREA3
          BRANCH    OVRCD TO EXTS8700
.
          BRANCH    FLGUR TO EXTR1000             * UR number
          MATCH     KSTUR,WMURNO
          GOTO      EXTR0500 IF LESS
          MATCH     WMURNO,KTOUR
          GOTO      EXTR0500 IF LESS
.
EXTR1000  BRANCH    FLGCO TO EXTR2000             * procedure code
          MATCH     KSTCODE,WMCODE
          GOTO      EXTR0500 IF LESS
          MATCH     WMCODE,KTOCODE
          GOTO      EXTR0500 IF LESS
.
EXTR2000  BRANCH    FLGST TO EXTR3000             * treatment status
          COMPARE   KSTSTAT,WMSTAT
          GOTO      EXTR0500 IF NOT EQUAL
.
EXTR3000  BRANCH    FLGD1 TO EXTR4000             * date on list
          MATCH     KSTDATE1,WMDATE1
          GOTO      EXTR0500 IF LESS
          MATCH     WMDATE1,KTODATE1
          GOTO      EXTR0500 IF LESS
.
EXTR4000  BRANCH    FLGD2 TO EXTR5000             * date last review
          MATCH     KSTDATE2,WMDATE2
          GOTO      EXTR0500 IF LESS
          MATCH     WMDATE2,KTODATE2
          GOTO      EXTR0500 IF LESS
.
EXTR5000  BRANCH    FLGD3 TO EXTR6000             * scheduled admission
          MATCH     KSTDATE3,WMDATE3
          GOTO      EXTR0500 IF LESS
          MATCH     WMDATE3,KTODATE3
          GOTO      EXTR0500 IF LESS
.
EXTR6000  BRANCH    FLGUN TO EXTR7000             * unit
          MATCH     KSTUNIT,WMUNIT
          GOTO      EXTR0500 IF LESS
          MATCH     WMUNIT,KTOUNIT
          GOTO      EXTR0500 IF LESS
.
EXTR7000  BRANCH    FLGDO TO EXTR8000             * doctor
          MATCH     KSTDOC,WMDOCTOR
          GOTO      EXTR0500 IF LESS
          MATCH     WMDOCTOR,KTODOC
          GOTO      EXTR0500 IF LESS
.
EXTR8000  BRANCH    FLGES TO EXTR9000             * estimated stay
          COMPARE   KSTSTAY,WMSTAY
          GOTO      EXTR0500 IF LESS
          COMPARE   WMSTAY,KTOSTAY
          GOTO      EXTR0500 IF LESS
.
EXTR9000  BRANCH    FLGPT TO EXTS1000             * priority
          MATCH     KSTPTY,WMPTY
          GOTO      EXTR0500 IF NOT EQUAL
.
EXTS1000  BRANCH    FLGSN TO EXTS2000             * short notice
          MATCH     KSTNOT,WMNOTICE
          GOTO      EXTR0500 IF NOT EQUAL
.
EXTS2000  BRANCH    FLGPC TO EXTS3000             * patient category
          MATCH     KSTPCAT,WMPCAT
          GOTO      EXTR0500 IF NOT EQUAL
.
EXTS3000  BRANCH    FLGRS TO EXTS4000             * waiting type
          MATCH     KSTWTYP,WMPTYPE
          GOTO      EXTR0500 IF NOT EQUAL
.
. ---- Prepare display variables
.
EXTS4000  MOVE      SP6,STATUS6
          MOVE      SP10,DATE10
          MOVE      SP10,DATE20
          MOVE      SP20,DESC20
          LOAD      STATUS6,WMSTAT,STAT1,STAT2,STAT3,STAT4:
                                   STAT5,STAT6,STAT7,STAT8
          MOVE      WMDESC,DESC20
          MATCH     SP2,WMDATE1
          GOTO      EXTS5000 IF EQUAL
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATE10
EXTS5000  MATCH     SP2,WMDATE2
          GOTO      EXTS6000 IF EQUAL
          UNPACK    WMDATE2,CCENT,CYEAR,CMON,CDAY
          PACK      DATE20,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
. ---- Display of variables
.
EXTS6000  ADD       ONE,COUNT
          DISPLAY   *P1:VERT,*V2LON,COUNT,*HOFF,*P5:VERT,WMURNO:
                    *P14:VERT,WMCODE,*P24:VERT,DESC20,*P45:VERT,DATE10:
                    *P56:VERT,DATE20,*P67:VERT,STATUS6
          PACK      DIM19 WITH WMURNO,WMCODE,WMCNT
.
. ---- Prepare the keys for the "select" later on
.
          STORE     DIM19,COUNT,DIMC01,DIMC02,DIMC03,DIMC04:
                                DIMC05,DIMC06,DIMC07,DIMC08:
                                DIMC09,DIMC10,DIMC11,DIMC12:
                                DIMC13,DIMC14,DIMC15,DIMC16
          ADD       ONE,VERT
          COMPARE   TEN7,COUNT
          GOTO      EXTS7000 IF EQUAL
          GOTO      EXTR0500
.
. ---- The screen is full, select on of the records for detailed display,
. ---- or search further on new screen
.
EXTS7000  MOVE      SP1,ANS
          KEYIN     *P1:24,*EL,"(",*V2LON,*DV,ANSE,*HOFF,")xit, ":
                    "(",*V2LON,*DV,ANSF,*HOFF,")urther search, ":
                    "(",*V2LON,*DV,ANSS,*HOFF,")elect ",*V2LON,ANS
          MATCH     SP1,ANS
          GOTO      EXTS9000 IF EQUAL
          MATCH     ANSF,ANS
          GOTO      EXTS8000 IF EQUAL
          MATCH     ANSS,ANS
          GOTO      EXTS8500 IF EQUAL
          MATCH     ANSE,ANS
          GOTO      EXTS9000 IF EQUAL
          BEEP
          GOTO      EXTS7000
.
. ---- Further search, so clean the screen and read next record
.
EXTS8000  MOVE      ZERO,COUNT
          MOVE      SEVEN,VERT
          DISPLAY   *P1:VERT,*EF
          GOTO      EXTR0500
.
. ---- Select one of the records on the screen
. ---- load the key19 and read treatment file
.
EXTS8500  MOVE      ZERO,CHOICE
          KEYIN     *P1:24,*EL,"Number selected : ",*V2LON,*DV,UNDLN2:
                    *P19:24,CHOICE,*P19:24,*DV,CHOICE
          COMPARE   ZERO,CHOICE
          GOTO      EXTS9000 IF EQUAL
          COMPARE   TEN7,CHOICE
          GOTO      EXTS8500 IF NOT LESS
          COMPARE   COUNT,CHOICE
          GOTO      EXTS8500 IF OVER
          LOAD      KEY19,CHOICE,DIMC01,DIMC02,DIMC03,DIMC04,DIMC05:
                                 DIMC06,DIMC07,DIMC08,DIMC09,DIMC10:
                                 DIMC11,DIMC12,DIMC13,DIMC14,DIMC15:
                                 DIMC16
          RESET     KEY19
          CALL      RDTREA1
          BRANCH    OVRCD TO EXTS8600
          GOTO      EXTS9999
.
EXTS8600  DISPLAY   *P1:24,*EL,*V2LON,"** Invalid U/R Number for ":
                    "waiting-lists file **",*W
          GOTO      EXTR0000
.
EXTS8700  COMPARE   ZERO,COUNT
          GOTO      EXTS8500 IF NOT EQUAL
          DISPLAY   *P1:24,*EL,"No records available",*W,*P1:24,*EL
.
EXTS9000  MOVE      TRUE,EXIT
EXTS9999  RETURN
.
.****************************************************************************
.*               DETT0000                                                   *
.*          Get descriptions for treatment screen variables                 *
.****************************************************************************
.
. ---- Treatment status
.
DETT0000  MOVE      SP5,KEY5
          PACK      DESCWU WITH SP20
          PACK      TDESC WITH SP20
          LOAD      STATUS,WMSTAT,STAT1,STAT2,STAT3,STAT4:
                                  STAT5,STAT6,STAT7,STAT8
          PACK      KEY5 WITH CODEWU,WMUNIT
          CALL      RDCODE1
          BRANCH    OVRCD TO DETT2000
          MOVE      TDESC,DESCWU
.
. ---- Attending doctor
.
DETT2000  MOVE      SP5,KEY5
          PACK      DOCNAME WITH SP30,SP20
          PACK      TDESC WITH SP20
          MOVE      WMDOCTOR,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD TO DETT3000
          CALL      DOCT0000
.
. ---- Priority
.
DETT3000  MOVE      SP5,KEY5
          PACK      DESCTP WITH SP20
          PACK      TDESC WITH SP20
          PACK      KEY5 WITH CODETP,WMPTY
          CALL      RDCODE1
          BRANCH    OVRCD TO DETT4000
          MOVE      TDESC,DESCTP
.
. ---- Short notice
.
DETT4000  MOVE      SP5,KEY5
          PACK      DESCNOT WITH SP20
          PACK      TDESC WITH SP20
          PACK      KEY5 WITH CODEWS,WMNOTICE
          CALL      RDCODE1
          BRANCH    OVRCD TO DETT5000
          MOVE      TDESC,DESCNOT
.
. ---- Patient category
.
DETT5000  MOVE      SP5,KEY5
          PACK      DESCPCAT WITH SP20
          PACK      TDESC WITH SP20
          PACK      KEY5 WITH CODEA,WMPCAT
          CALL      RDCODE1
          BRANCH    OVRCD TO DETT6000
          MOVE      TDESC,DESCPCAT
.
. ---- Waiting type
.
DETT6000  MOVE      SP5,KEY5
          PACK      DESCPTYP WITH SP20
          PACK      TDESC WITH SP20
          PACK      KEY5 WITH CODERS,WMPTYPE
          CALL      RDCODE1
          BRANCH    OVRCD TO DETT9999
          MOVE      TDESC,DESCPTYP
DETT9999  MOVE      SP5,KEY5
          PACK      TDESC WITH SP20
          RETURN
.
.****************************************************************************
.*               PDOB0000                                                   *
.*          Get patient DOB for display and sex-description                 *
.****************************************************************************
.
PDOB0000  UNPACK    KEY19 INTO DIM8,WMCODE,DIM2
          MOVE      SP10,PATDOB
          MOVE      SP20,DSEX
          PACK      PATNAME WITH SP30,SP20
          MOVE      DIM8,WMURNO
PDOB1000  MOVE      WMURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD TO PDOB9999
          UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
          PACK      PATDOB WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DSEX
.* ******************************************   End of Changes         *C-49016
.
PDOB4000  MOVE      PGNAME,DIM10
          PACK      PATNAME WITH PTITL,SP1,DIM10,SP1,PSNAME
.
PDOB9999  RETURN
.
.****************************************************************************
.*               DOCT0000                                                   *
.*          Concatenate doctor name                                         *
.****************************************************************************
.
DOCT0000  MOVE      DGNAME,ANS
          PACK      DOCNAME WITH DTITL,SP1,ANS,DOT,DSNAME
          RETURN
.
.******************************************************************************
.*          SCRT0000                                                          *
.*          Detailed treatment screen                                         *
.******************************************************************************
.
SCRT0000  DISPLAY   *P1:3,*EF:
                    *P1:4,"    U/R Number        : ":
                    *P40:5,"D.O.B. : ":
                    *P1:6,"Procedure Code        : ":
                    *P1:7,"Procedure Time (mins) : ":
                    *P1:8,"Treatment Description : ":
                    *P1:9,"Reason                : ":
                    *P1:10,"Date on List          : ":
                    *P1:11,"Last Review Date      : ":
                    *P1:12,DDESCWU,*P23:12,": ":
                    *P1:13,"Doctor                : ":
                    *P1:14,"Estimated Stay (days) : ":
                    *P1:15,"Priority              : ":
                    *P1:16,"Short Notice          : ":
                    *P1:17,CATADES,*P23:17,": ":
                    *P1:18,"Resident Status       : "
          RETURN
.
.****************************************************************************
.*               DIST0000                                                   *
.*         Display variables of treatment screen                            *
.****************************************************************************
.
DIST0000  DISPLAY   *P25:4,*EL,*V2LON,WMURNO,*P40:4,PATNAME:
                    *P49:5,*EL,*V2LON,PATDOB:
                    *P25:6,*EL,*V2LON,WMCODE:
                    *P25:7,*EL,*V2LON,WMTIME,*P36:7:
                    *P25:8,*EL,*V2LON,WMDESC:
                    *P25:9,*EL,*V2LON,WMREASON
          REP       " 0",WMDATE1
          UNPACK    WMDATE1 INTO CCENT,CYEAR,CMON,CDAY
          DISPLAY   *P25:10,*EL,*V2LON,CDAY,*HOFF,SLASH,*V2LON,CMON,*HOFF:
                    SLASH,*V2LON,CCENT,CYEAR
          REP       " 0",WMDATE2
          UNPACK    WMDATE2 INTO CCENT,CYEAR,CMON,CDAY
          DISPLAY   *P25:11,*EL,*V2LON,CDAY,*HOFF,SLASH,*V2LON,CMON,*HOFF:
                    SLASH,*V2LON,CCENT,CYEAR
          DISPLAY   *P25:12,*EL,*V2LON,WMUNIT,*P36:12,*V2LON,DESCWU:
                    *P25:13,*EL,*V2LON,WMDOCTOR,*P36:13,*V2LON,DOCNAME:
                    *P25:14,*EL,*V2LON,WMSTAY:
                    *P25:15,*EL,*V2LON,WMPTY,*P36:15,*V2LON,DESCTP:
                    *P25:16,*EL,*V2LON,WMNOTICE,*P36:16,*V2LON,DESCNOT:
                    *P25:17,*EL,*V2LON,WMPCAT,*P36:17,*V2LON,DESCPCAT:
                    *P25:18,*EL,*V2LON,WMPTYPE,*P36:18,*V2LON,DESCPTYP
.
          RETURN
.
.******************************************************************************
.*         PRTT0000                                                           *
.*         Print these treatment details or exit ?                            *
.******************************************************************************
.
PRTT0000  MOVE      SP1,ANS
          MOVE      FALSE,EXIT
          DISPLAY   *P1:24,*EL,"(",*V2LON,ANSP,*HOFF,")rint , (",*V2LON:
                    ANSE,*HOFF,")xit ? ";
          KEYIN     *P21:24,*EL,*V2LON,ANS
          RESET     ANS
          MATCH     SP1,ANS
          GOTO      PRTT9000 IF EQUAL
          MATCH     ANSE,ANS
          GOTO      PRTT9000 IF EQUAL
          MATCH     ANSP,ANS
          GOTO      PRTT9999 IF EQUAL
          BEEP
          GOTO      PRTT0000
.
PRTT9000  MOVE      TRUE,EXIT
PRTT9999  RETURN
.
.***************************************************************************
.*                                 PRND0000                                *
.*                        print treatment details                          *
.***************************************************************************
.
PRND0000  ADD       ONE,CPAGENO
          CALL      PRNC0000
          ADD       ONE,CPAGENO
          CALL      PRNH0000
          PRINT     *N,*41,"Procedure Code    :   ",WMCODE:
                    *N,*41,"Procedure Time    :   ",WMTIME," mins.":
                    *N,*41,"Treatment Desc    :   ",WMDESC:
                    *N,*41,"Reason            :   ",WMREASON:
                    *N,*41,"Treatment Status  :   ",WMSTAT,*75,STATUS:
                    *N,*41,"Date on List      :   ",DATE10:
                    *N,*41,"Last Review Date  :   ",DATE20:
                    *N,*41,"Sched Admit Date  :   ",DATE30:
                    *N,*41,DDESCWU,*59,":   ",WMUNIT,*75,DESCWU:
                    *N,*41,"Doctor            :   ",WMDOCTOR,*75,DOCNAME:
                    *N,*41,"Estimate stay     :   ",WMSTAY," days":
                    *N,*41,"Priority          :   ",WMPTY,*75,DESCTP:
                    *N,*41,"Short Notice      :   ",WMNOTICE,*75,DESCNOT:
                    *N,*41,CATADES,*59,":   ",WMPCAT,*75,DESCPCAT:
                    *N,*41,"Resident Status   :   ",WMPTYPE,*75,DESCPTYP
PRND9999  RETURN
.
.*******************************************************************************
.*          PRNH0000                                                           *
.*          Print heading                                                      *
.*******************************************************************************
.
PRNH0000  CLOCK     TIME,CTIMEIS
          CALL      HEAD132
          PRINT     *N,*N,*N,*31,"U/R Number : ",WMURNO,*55,PATNAME:
                    *95,"  D.O.B  ",PATDOB,*N,*N;
.
          RETURN
.
.*******************************************************************************
.*          PRNH0000                                                           *
.*          Print first page with criteria                                     *
.*******************************************************************************
.
PRNC0000  CLOCK     TIME,CTIMEIS
.
          CALL      HEAD132
          PRINT     *N
.
          PRINT     *25,"*-------------------------------":
                    "-------------------------------*"
          PRINT     *25,"|",*39,"S e l e c t i o n     ":
                    "C r i t e r i a ",*88,"|"
          PRINT     *25,"*-------------------------------":
                    "-------------------------------*"
          MOVE      ONE,CLNO
          BRANCH    FLGUR TO PRNC1000
          ADD       ONE,CLNO
          PRINT     *25,"|  U/R Number          : ",KSTUR:
                    *61,"To",*64,KTOUR,*88,"|"
PRNC1000  BRANCH    FLGCO TO PRNC2000
          ADD       ONE,CLNO
          PRINT     *25,"|  Procedure Code      : ",KSTCODE:
                    *61,"To",*64,KTOCODE,*88,"|"
PRNC2000  BRANCH    FLGST TO PRNC3000
          ADD       ONE,CLNO
          PRINT     *25,"|  Treatment Status    : ",KSTSTAT,*88,"|"
PRNC3000  BRANCH    FLGD1 TO PRNC4000
          ADD       ONE,CLNO
          PRINT     *25,"|  Date On List        : ",KSTDATE1:
                    *61,"To",*64,KTODATE1,*88,"|"
PRNC4000  BRANCH    FLGD2 TO PRNC5000
          ADD       ONE,CLNO
          PRINT     *25,"|  Date Last Review    : ",KSTDATE2:
                    *61,"To",*64,KTODATE2,*88,"|"
PRNC5000  BRANCH    FLGD3 TO PRNC6000
          ADD       ONE,CLNO
          PRINT     *25,"|  Scheduled Admission : ",KSTDATE3:
                    *61,"To",*64,KTODATE3,*88,"|"
PRNC6000  BRANCH    FLGUN TO PRNC7000
          ADD       ONE,CLNO
          PRINT     *25,"|  Unit                : ",KSTUNIT:
                    *61,"To",*64,KTOUNIT,*88,"|"
PRNC7000  BRANCH    FLGDO TO PRNC8000
          ADD       ONE,CLNO
          PRINT     *25,"|  Doctor              : ",KSTDOC:
                    *61,"To",*64,KTODOC,*88,"|"
PRNC8000  BRANCH    FLGES TO PRNC9000
          ADD       ONE,CLNO
          PRINT     *25,"|  Estimated Stay      : ",KSTSTAY:
                    *61,"To",*64,KTOSTAY,*88,"|"
PRNC9000  BRANCH    FLGPT TO PRNE1000
          ADD       ONE,CLNO
          PRINT     *25,"|  Priority            : ",KSTPTY
PRNE1000  BRANCH    FLGSN TO PRNE2000
          ADD       ONE,CLNO
          PRINT     *25,"|  Short Notice        : ",KSTNOT
PRNE2000  BRANCH    FLGPC TO PRNE3000
          ADD       ONE,CLNO
          PRINT     *25,"|  Patient Category    : ",KSTPCAT
PRNE3000  BRANCH    FLGRS TO PRNE4000
          ADD       ONE,CLNO
          PRINT     *25,"|  Resident Status     : ",KSTWTYP
.
PRNE4000  COMPARE   ONE,CLNO
          GOTO      PRNE5000 IF NOT EQUAL
          PRINT     *25,"|   No Criteria ",*88,"|"
PRNE5000  PRINT     *25,"*-------------------------------":
                    "-------------------------------*"
          RETURN
.
.******************************************************************************
.
.
          INCLUDE   STD001IO
          INC       SEXDSCIO
          INC       WATPROIO/INC
          INC       WATTR1IO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PATDO1IO/INC
          INC       PATCODKY
          INC       PATDOCKY
          INC       WATDSPRO
          INC       WATDSSTA
.
.
.

.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAPAT40                                             *
.*                                                                            *
.*     FUNCTION:         ADMISSIONS LISTING                                   *
.*                                                                            *
.*     DATE WRITTEN:     13/08/1985                                           *
.*                                                                            *
.*     AUTHOR:           MALCOLM HAYSE                                        *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*        V12.00.01 22/05/2025  Alina Bhari   TSK 0955096                     *
.*                  Added alpanumeric visit number generation -NEXTA          *
.*        V11.02.04 04/05/2022  Jacob Jackson   TSK 0864505                   *
.*                  Replace BRANCH with PERFORM, to fix issue where the way   *
.*                  preferred name was displayed was changed                  *
.*        V11.02.03 01/03/2022  Jacob Jackson   TSK 0864505                   *
.*                  Add preferred names based on "Attending Doctor" selection *
.*        V11.02.02 10/02/2022  Jacob Jackson   TSK 0864505                   *
.*                  Rename 'PRNAME' to 'PRFRNAME' due to conflict             *
.*        V11.02.01 20/01/2022  Jacob Jackson TSK 0864505                     *
.*                  Added optional preferred name(s) to name column           *
.* V10.04.01  15/06/2014 Jill Parkinson CAR 301639                            *
.*            Moved TFILENAM to INIT0000                                      *
.* V10.03.01  31/12/2012 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.09.01  02/04/2008  Ebon Clements  CAR 164657                     *
.*                  Fixed admissions not printing for discharged patients     *
.*                  for Single Ward option.                                   *
.*        V9.04.01  20/08/2004  Lina Vo                                       *
.*                  Changed Report to allow All Hospitals for Mult-Hospital   *
.*        V9.03.01  20/01/2003  Ebon Clements    CAR 44720                    *
.*                  Added unit filter and print sequence.                     *
.*                  Added claim code filter                                   *
.*        V9.02.01  25/10/2001  Dean Cramer                                   *
.*                  Fix bad print offsets for web                             *
.*        V5.08.02  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.01  13/06/2000  Steve Downing                                 *
.*                  Fixed display of patient's names                          *
.******************************************************************************
.*        V5.07.02  28/06/1999  Jill Habasque                                 *
.*                  Fixed name display                                        *
.*        V5.07.01  28/04/1999  Jill Habasque                                 *
.*                  Added option for single or all wards,added ACCLASS & SERV *
.*        V5.07.B03 25/01/1999  Jim Stathopoulos                              *
.*                  Report Modifications                                      *
.*        V5.07.B02 20/11/1998  Jill Habasque                                 *
.*                  Mods to include century                                   *
.******************************************************************************
.*        V5.05.02  29/12/1997  Nick  SRF 643895                              *
.*                  Fixed printing of Doctor Name for Option 3 reports        *
.*        V5.05.01  22/10/1997  Nick  SRF 643442                              *
.*                  Removed pstrol crap and rewrote tempfile to standard      *
.*                  Also fixed a couple of other minor display bugs.          *
.******************************************************************************
.*        V5.01.01  07/07/89  M.Ohleiter                                      *
.*                  Added attending doctor sequence                           *
.*        V5.01.02  19/09/1989  Graeme Williams   SRF 102389                  *
.*                  Fixed bug withADMNO
.*        V5.01.03  27/08/1990  Bill Dew                                      *
.*                  Fixed patient given name to 22 characters                 *
.*        V5.01.04  19/10/90  Peter Eddey                                     *
.*                  Added the "to" date default                               *
.*        V5.01.121 27/11/1990  Glen Hobby                                    *
.*                  Recompiled for changes to PATMX1FD                        *
.*        V5.01.122 21/05/1991  Steve O'Gorman                                *
.*                  Fixed errors from new Compiler                            *
.*        V5.01.123 27/02/1993  i chung           (Dudley changes)            *
.*                  Added parameter IBCNMHOS checks and Hospital Id keyin     *
.*        V5.01.124 11.03.93 Neeriem "I Hate Support" Dye (I.B.A.)            *
.*                  Print WARD if parameter set                               *
.*        V5.01.125 16.03.93 Neeriem "I Hate Support" Dye (I.B.A.)            *
.*                  Fixed to print Attend. Doctor in Attending Doctor Sequence*
.*        V5.01.126 25/03/93  Steve O'Gorman                                  *
.*                  Made Hospital Code of blank Exit the program              *
.*        V5.01.127 16/07/93  Whirlpool  SRF 440228                           *
.*                  Fixed problem with printing the ward                      *
.*        V5.01.128 25/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.01.129 20/07/1994  Rob Leonard                                   *
.*                  Fixed hospital keyin                                      * 
.*        V5.02.01  14/12/1994  Graeme Williams                               *
.*                  Corrected check for hospital.  Use admitting ward, not    *
.*                  current ward.                                             *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PMSPX2FD/INC
          INC       PATTRNFD/INC
          INC       PATWR1FD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
+
.
.         Temporary files Declaration
.
SORT1     INIT      "uc1-8,113-120"
SORT2     INIT      "uc9-39,40-43,113-120"
SORT3     INIT      "uc121-140,144-149,1-8,113-120"
SORT4     INIT      "uc162-164,121-140,144-149,44-53,9-39,40-43,113-120"
UKEY      DIM       50
PATTEMP1  IFILE     SQL, FIXED=168, KEY=UKEY   
..
. Name    Type    Length   Physical     Start   Description
. ----    ----    ------   --------     -----   -----------
XDAADMNO  DIM        8         8          1
XNAME     DIM       31        31          9
XTYPSER   DIM        4         4         40     Type of service (Cat U6)
.ADMDATE  DIM       10        10         44
.DIM15    DIM       15        15         54
.AURNO    DIM        8         8         69
.DNAME    DIM       16        16         77
.DOB      DIM       10        10         93
.DISDATE  DIM       10        10        103
DUNIQUE   DIM        8         8        113
.DSNAME   DIM       20        20        121
.AWARD    DIM        3         3        141
.ADOCTA   DIM        6         6        144
ACCLASS   DIM        3         3        150     Account Class (cat CL)
XATIME    DIM        8         8        153     HH:MM:SS                 
XPSEX     DIM        1         1        161     Sex
XACLSSFT  DIM        3         3        162     Unit
XATYPE    DIM        3         3        165     Admission Type
.
.End of Record                          168
+
.
.         IBACONFD Variable
.
IBCNMHOS  FORM      1
.
.         Work Variables
.
ADMDATE   DIM       10
CMDLINE   DIM       80
CODE      DIM       3
.
DADOCTA   DIM       6
DALERG    DIM       3
DANS      DIM       3
DAYCASE   DIM       3
DHOSP     DIM       3
DILLN     DIM       3
DIM2A     DIM       2
DIM3      DIM       3
DIM5      DIM       5
DIM15     DIM       15
DIM20     DIM       20
DIM26     DIM       26
DISDATE   DIM       10
DISPTDTE  DIM       10
DNAME     DIM       16
DOB       DIM       10
.
FROMDATE  DIM       8                  * From Date in CCYYMMDD format
FILTUNIT  DIM       3
FILTCLAM  DIM       3
.
LNO       FORM      2
.
NAME      DIM       37
.
OPCND     FORM      1
OPTION1   FORM      1
PAGENO    FORM      3
PATNUM    FORM      5
PDSNAME   DIM      16
.
SEC       FORM      2
SINGWARD  DIM       3
STATUS    FORM      1
SWARD     DIM       1                  * 1=Single Ward 2=All wards
.
TEMPFILE  DIM       8
TODATE    DIM       8                  * To Date in CCYMMDD format
.
UNIQUE    FORM      8
.
XDAY1     DIM       2
XDAY2     DIM       2
XMON1     DIM       2
XMON2     DIM       2
XYEAR1    DIM       2
XYEAR2    DIM       2
XCENT1    DIM       2
XCENT2    DIM       2
PRENAOPD  FORM      1
TYPPRDIS  FORM      1
.
.         Constants
.
A         INIT      "A "
COMMASP   INIT      ", "
.
ISERASE   INIT      "iserase "
ISBUILD   INIT      "isbuild "
.
PRGID     INIT      "IBAPAT40"
PRGNAM    INIT      "Admissions Listing"
VERSION   INIT      "V12.01.00"
+
.
.         DISPLAY SCREEN HEADER AND OPEN FILES
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND28;*226,PTCNNMPR
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A3,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A2,"patwr1af"
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A3,"patwr1af"
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          IF        IBCNMHOS = 1
            DISPLAY   *P64:24,"pathspaf";
            OPEN      PATHSPA1,"pathspaf"
          ENDIF
.
          MOVE      PTCNNMPR,PRENAOPT
          IF        PRENAOPT > 0
            DISPLAY   *P64:24,"pmspx2af";
            OPEN      PMSPX2A1,"pmspx2af"
          ENDIF
.
.         set up KEYDATE input requirements
.
          MOVE      ZERO,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFILE
.
+
.
.         START OF PROGRAM
.
          IF        IBCNMHOS = 1
            DISPLAY   *P1:5,*EF,"Hospital Id : ";
            MOVE      TEN5,ECOL
            MOVE      FIVE,EVERT
            MOVE      SP3,CKYIDEF3
            MOVE      ZERO,CKYIMAND
            CALL      PATHSPKY
            BRANCH    EXIT,KYHOSP20,ENDIT
            GOTO      KYHOSP30
.
KYHOSP20    MOVE       "All Hospitals",PTHSNAME
            MOVE       SP10,PTHSHOSP
.
KYHOSP30    DISPLAY   *P22:5,*EL,PTHSNAME,*W1;
          ENDIF
.
.           MAIN  MENU
.
MENU      HLINE     *G37,2,49,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Single Ward":
                    *P1:6,*V2LON,TWO,*HOFF," = All Wards":
                    *P1:9,"Select Option : ";
.
          KEYIN     *P17:9,*EL,*DV,UNDLN1,*P17:9,*V2LON,OPTION1;
.
          COMPARE   ZERO,OPTION1
          GOTO      ENDIT IF EQUAL
.
          COMPARE   ONE,OPTION1
          GOTO      SWARD1 IF EQUAL
.
          COMPARE   TWO,OPTION1
          GOTO      ALLWARD IF EQUAL
.
          GOTO      MENU
.
SWARD1    DISPLAY   *P1:13,"Keyin Ward : "
          MOVE      TEN4,ECOL
          MOVE      TEN3,EVERT
          MOVE      ZERO,CKYIMAND
          CALL      PATWRDKY
          BRANCH    EXIT,MENU 
          MOVE      WWARD,SINGWARD
          MOVE      ONE,SWARD
          GOTO      MENU2D
.
ALLWARD   MOVE      TWO,SWARD
.
MENU2D    DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Admission Number Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Patient Surname Sequence":
                    *P1:7,*V2LON,THREE,*HOFF," = Attending Doctor Sequence":
                    *P1:8,*V2LON,FOUR,*HOFF," = Unit Sequence":
                    *P1:10,"Select Option : ";
.
MENU2     KEYIN     *P17:10,*EL,*DV,UNDLN1,*P17:10,*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      MENU IF EQUAL
.
          BRANCH    OPTION OF KDATE,KSURN,KDOC,KUNIT
          BEEP
          GOTO      MENU2
.
KDATE     DISPLAY   *P49:2,*V2LON,"- Admission Sequence ";
          MOVE      SORT1,UKEY     
          GOTO      KEYUNIT
.
KSURN     DISPLAY   *P49:2,*V2LON,"- Surname Sequence ";
          MOVE      SORT2,UKEY     
          GOTO      KEYUNIT
.
KDOC      DISPLAY   *P49:2,*V2LON,"- Doctor Sequence ";
          MOVE      SORT3,UKEY     
          GOTO      KEYUNIT
.
KUNIT     DISPLAY   *P49:2,*V2LON,"- Unit Sequence ";
          MOVE      SORT4,UKEY     
.
KEYUNIT   DISPLAY   *P1:4,*EF,"Unit : "
          MOVE      "AC",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          MOVE      "4",EVERT
          MOVE      "8",ECOL
.
          CALL      PATCODKY
          IF        EXIT = ONE
            MOVE      SP3,FILTUNIT
            DISPLAY   *P12:4,*EL,*V2LON,"All"
          ELSE
            IF        EXIT = 2
              MOVE      SP3,FILTUNIT
              GOTO      MENU2D
            ELSE
              MOVE      ACODE,FILTUNIT
              DISPLAY   *P12:4,*EL,*V2LON,TDESC
            ENDIF
          ENDIF

KEYCLAM   DISPLAY   *P1:6,*EF,"Claim Code : "
          MOVE      "CL",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          MOVE      "6",EVERT
          MOVE      "14",ECOL
          CALL      PATCODKY
.
          IF        EXIT = ONE
            MOVE      SP3,FILTCLAM
            DISPLAY   *P18:6,*EL,*V2LON,"All"
          ELSE
            IF        EXIT = 2
              MOVE      SP3,FILTCLAM
              GOTO      KEYUNIT
            ELSE
              MOVE      ACODE,FILTCLAM
              DISPLAY   *P18:6,*EL,*V2LON,TDESC
            ENDIF
          ENDIF

KADATE    DISPLAY   *P1:8,*EF,"Keyin From Date : ":
                        *P1:10,"Keyin To   Date : ";
          MOVE      TWENTY,CCOL
+
.         Keyin From Date
.
KADATE1   DISPLAY   *P20:8,*EL,*P20:10,*EF;
          MOVE      EIGHT,CVERT
.
KADATE11  UNPACK    SP8,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    CQUEST,KADATE11
          BRANCH    OVRCD,MENU
.
          PACK      FROMDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
          UNPACK    FROMDATE INTO XCENT1,XYEAR1,XMON1,XDAY1
          PACK      DISPTDTE,XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1
+
.         Keyin To Date 
.
          MOVE      TEN,CVERT
.
KADATE2   UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          BRANCH    CQUEST,KADATE2
.
          PACK      TODATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
          UNPACK    TODATE INTO XCENT2,XYEAR2,XMON2,XDAY2
.
          MATCH     FROMDATE TO TODATE      * To Date must be  >= From Date
          GOTO      REKEY IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** To Date must be greater than or equal From Date **   ";
          CALL      HITENTER
          GOTO      KADATE2
.
.         Check if dates are okay or not
.
REKEY     KEYIN     *P1:24,*EF,"Dates okay (",*V2LON,"Y",*HOFF,"/":
			   *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     ANSN TO ANS
          GOTO      KADATE1 IF EQUAL
          MATCH     ANSY TO ANS
          GOTO      CNTCHK IF EQUAL
          BEEP
          GOTO      REKEY
.
INVMASTA  DISPLAY   *P20:23,*B,*EL,"** Admission ",*V2LON,AADMNO,*HOFF:
                    " has an invalid U/R ",*V2LON,AURNO,*HOFF," **",*W2;
.
          PRINT     "** Admission ",AADMNO," has an invalid U/R ":
                    AURNO," ... Please Contact I.B.A. **"
          GOTO      NEXTA
.
.         Generate Listing
.
CNTCHK    CALL      CREA0000
.
          MOVE      ONE TO UNIQUE
          MOVE      UNIQUE,DUNIQUE
          MOVE      ZERO TO PATNUM
+
.
ADMNNO    DISPLAY   *P1:24,*EL,*V2LON,*P28:24,"***  Extracting Data  ***",*W2:
                    *HOFF,*P15:24,*EL,"Admission Number :",*P48:24,"Scanning :";
.
          MOVE      "60",LNO
          MOVE      ZERO,PAGENO
.
          PACK      KEY16 WITH FROMDATE,SP8
          CALL      RDSMISS3
.
NEXTA     CALL      RDKMISS3
          BRANCH    OVRCD OF SORTD
	  DISPLAY   *P59:24,*EL,AADMNO;
.
          MATCH     ADATE,TODATE
          GOTO      SORTD IF LESS
.
          MATCH     SP70,FILTUNIT                * Unit Filter
          IF        !@EQUAL
            MATCH     FILTUNIT,ACLSSFT
            GOTO      NEXTA IF NOT EQUAL
          ENDIF
.
          MOVE      ACLAIM,ACCLASS
.
          MATCH     SP70,FILTCLAM                * Claim Code Filter 
          IF        !@EQUAL
            MATCH     FILTCLAM,ACCLASS
            GOTO      NEXTA IF NOT EQUAL
          ENDIF
.
          MOVE      ACLSSFT,XACLSSFT
.
.         PATIENT MUST BE ADMITTED, DISCHARGED OR ON LEAVE
.
          COMPARE   ONE,ASTAT
          GOTO      NEXTA IF EQUAL
.
          COMPARE   FIVE,ASTAT
          GOTO      NEXTA IF EQUAL
.
          IF        IBCNMHOS = 1
            MATCH     SP10,PTHSHOSP
            IF        !@EQUAL
              PACK      KEY30,AADMNO,SP20
              CALL      RDSTRAN2
              CALL      RDKTRAN2
              IF        OVRCD = 1              
                GOTO      NEXTA
              ENDIF
              MATCH     AADMNO,TADMN 
              IF        !@EQUAL
                GOTO      NEXTA
              ENDIF
              PACK      KEY6,TWARD,SP3
              CALL      RDWARD1
              BRANCH    OVRCD,NEXTA
              MATCH     PTWDHOSN,PTHSHOSP
              GOTO      NEXTA IF NOT EQUAL
            ENDIF
          ENDIF
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF INVMASTA
.
          MOVE      PSEX,XPSEX
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
.
          CALL      PACNAME
          MOVE      PACFNAME,XNAME
.
          MOVE      AUSR6,XTYPSER
          UNPACK    ADATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,ADMDATE
          MOVE      ATIME,XATIME
.
A1OK      PACK      DNAME,SP30,SP30
          PACK      DSNAME,SP30,SP30
          MATCH     SP6,ADOCTA
          GOTO      A1OK1 IF EQUAL
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD TO A1OK1
.
          MOVE      DGNAME TO ANS
          PACK      DNAME WITH ANS,DOT,DSNAME
          RESET     DNAME
          REP       ", " IN DNAME
          RESET     DNAME
.
A1OK1     MOVE      SP10 TO DOB
          UNPACK    PBDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DOB
.
          MOVE      SP20 TO TDESC
          MOVE      ATYPE,XATYPE
          PACK      KEY5 WITH A,ATYPE
          CALL      RDCODE1
          MOVE      TDESC TO DIM15
.
          MOVE      AADMNO,XDAADMNO
.
          MOVE      SP8 TO DISDATE
          COMPARE   THREE TO ASTAT
          GOTO      A11OK IF NOT EQUAL
.
          MOVE      AADMNO TO KEY8
          CALL      RDDSCH1
          BRANCH    OVRCD TO A11OK
.
          UNPACK    DDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISDATE
.
A11OK     MATCH     "1",SWARD
          IF        @EQUAL
            MATCH     SP3,AWARD                     * Is ward spaces?
            IF        @EQUAL
              PACK      KEY30,DAADMNO,Z70           * If it is then get ward
              CALL      RDSTRAN2                    * from transfer file
              CALL      RDPTRAN2                    * Get the last ward
.
              MATCH     DTADMN,DAADMNO              * Same Admit number?
              GOTO      NEXTA IF NOT EQUAL          * If not read next admis
.
              MATCH     TWARD,SINGWARD              * Same ward?
              GOTO      NEXTA IF NOT EQUAL          * If not read next admis
.
              MOVE      TWARD,AWARD
.                                                   * end I SRF 35112
            ELSE
              MATCH     AWARD,SINGWARD
              GOTO      NEXTA IF NOT EQUAL
            ENDIF
          ENDIF
.
          BRANCH    OPTION TO NUMSEQ,SURSEQ,DOCSEQ,UNITSEQ
.
NUMSEQ    PACK      KEY16 WITH XDAADMNO,DUNIQUE
          WRITE     PATTEMP1,KEY16;XDAADMNO,XNAME,XTYPSER,ADMDATE,DIM15,AURNO:
                                   DNAME,DOB,DISDATE,DUNIQUE,DSNAME,AWARD:
                                   ADOCTA,ACCLASS,XATIME,XPSEX,XACLSSFT,XATYPE
.
          ADD       ONE TO UNIQUE
          MOVE      UNIQUE,DUNIQUE
          DISPLAY   *P34:24,*V2LON,XDAADMNO;
          GOTO      NEXTA
.
SURSEQ    PACK      KEY43 WITH XNAME,XTYPSER,DUNIQUE
          WRITE     PATTEMP1,KEY43;XDAADMNO,XNAME,XTYPSER,ADMDATE,DIM15,AURNO:
                                   DNAME,DOB,DISDATE,DUNIQUE,DSNAME,AWARD:
                                   ADOCTA,ACCLASS,XATIME,XPSEX,XACLSSFT,XATYPE
.
          ADD       ONE TO UNIQUE
          MOVE      UNIQUE,DUNIQUE
          DISPLAY   *P34:24,*V2LON,XDAADMNO;
          GOTO      NEXTA
.
DOCSEQ    PACK      KEY42 WITH DSNAME,ADOCTA,AURNO,DUNIQUE
          WRITE     PATTEMP1,KEY42;XDAADMNO,XNAME,XTYPSER,ADMDATE,DIM15,AURNO:
                                   DNAME,DOB,DISDATE,DUNIQUE,DSNAME,AWARD:
                                   ADOCTA,ACCLASS,XATIME,XPSEX,XACLSSFT,XATYPE
.
          ADD       ONE TO UNIQUE
          MOVE      UNIQUE,DUNIQUE
          DISPLAY   *P34:24,*V2LON,XDAADMNO;
          GOTO      NEXTA
.
UNITSEQ   PACK      KEY82 WITH XACLSSFT,DSNAME,ADOCTA,ADMDATE,XNAME:
                               XTYPSER,DUNIQUE,SP70
          WRITE     PATTEMP1,KEY82;XDAADMNO,XNAME,XTYPSER,ADMDATE,DIM15,AURNO:
                                   DNAME,DOB,DISDATE,DUNIQUE,DSNAME,AWARD:
                                   ADOCTA,ACCLASS,XATIME,XPSEX,XACLSSFT,XATYPE
.
          ADD       ONE TO UNIQUE
          MOVE      UNIQUE,DUNIQUE
          DISPLAY   *P34:24,*V2LON,XDAADMNO;
          GOTO      NEXTA
.
.
SORTD     DISPLAY   *P1:24,*EL,*V2LON,*P27:24,"***  Generating Listing  ***",*W1
          CLOSE     PATTEMP1
.
          TRAP      NOOPEN IF IO
          OPEN      PATTEMP1,TEMPFILE
          TRAPCLR   IO
          GOTO      SETLOOP
.
NOOPEN    
          NORETURN
          MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** The Statistics file is EMPTY **    ";
          CALL      HITENTER
          GOTO      ENDP
.
SETLOOP   DISPLAY   *P1:24,*EL,*P23:24,"Printing Admission Number :";
          BRANCH    OPTION TO READA,READP,READC,READU
.
READA     MOVE      SP20,KEY16
          READ      PATTEMP1,KEY16;;
          GOTO      READLOP
.
READP     PACK      KEY30,SP30
          READ      PATTEMP1,KEY30;;
          GOTO      READLOP
.
READC     PACK      PDSNAME,SP20,SP20
          PACK      DADOCTA,SP10
          PACK      KEY42,SP20,SP20,SP20
          READ      PATTEMP1,KEY42;;
.
READU     PACK      KEY82,SP20,SP20,SP20,SP20
          READ      PATTEMP1,KEY82;;
.
READLOP
          READKS    PATTEMP1;XDAADMNO,XNAME,XTYPSER,ADMDATE,DIM15,AURNO:
                             DNAME,DOB,DISDATE,DUNIQUE,DSNAME,AWARD:
                             ADOCTA,ACCLASS,XATIME,XPSEX,XACLSSFT,XATYPE
          GOTO      ENDP IF OVER
.
          DISPLAY   *P51:24,*EL,XDAADMNO;
          BRANCH    OPTION TO RDLOP1,RDLOP1,RDLOP0,RDLOP1
.
RDLOP0    MATCH     DSNAME,PDSNAME
          GOTO      RDLOP1 IF NOT EQUAL
          MATCH     ADOCTA,DADOCTA
          GOTO      RDLOP1 IF NOT EQUAL
.         MOVE      SP20,DNAME
.
RDLOP1    CALL      DISPDA
          MOVE      DSNAME,PDSNAME
          MOVE      ADOCTA,DADOCTA
          GOTO      READLOP
+
.         OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP0     NORETURN
ENDP      
          COMPARE   ZERO,PAGENO
          GOTO      NOADMN IF EQUAL
.
          CALL      PAGEND
          PRINT     *N,*6,"Total Number of Patients: ",PATNUM:
                    *N,*N,*N,"***  End of Listing  ***"
.
          DISPLAY   *P1:24,*EL,*V2LON,*P22:24:
                    "***  Report Generation Completed  ***",*W2;
.          CALL      KILLIDX
          GOTO      MENU
.
NOADMN    DISPLAY   *P1:24,*B,*V2LON,*P25:24:
                    "***  No Admissions on File  ***",*W3;
          GOTO      MENU
+
.
OPMSFI    IF        PRENAOPT > 0
            MOVE      AURNO,KEY8
            CALL      RDPMPX21
          ENDIF
          RETURN
+ 
.
.         PRINT ACTUAL DATA 
.
DISPDA    COMPARE   "53",LNO
          CALL      HEAD IF NOT LESS
          BRANCH    OPTION TO DISPDAA,DISPDAA,DISPDAD,DISPDAA
.
DISPDAA   
          CALL        NAME0000
          CALL        OPMSFI
          MOVE        XNAME,DIM20
          MOVE        XATIME,DIM5
          PRINT       *1,"|",XDAADMNO,*11,"|",ADMDATE,*23,DIM5:
                      *28,"| ",XATYPE:
                      *34,"|",AURNO,*44,"| ",DIM20,*66,"| ",XPSEX:
                      *71,"|",XTYPSER:
                      *77,"| ",ACCLASS,*82,"| ",XACLSSFT:
                      *87,"| ",AWARD,*92,"|",DOB:
                      *104,"|",DISDATE,*115,"|",DNAME,*132,"|"
.
          IF        PRENAOPT > 0
            CLEAR     PRNAMES
            CLEAR     PRFRNAME
            MOVE      1,TYPPRDIS
            MOVE      PMPXPFGN,PREGNAME
            MOVE      PMPXPFSN,PRESNAME
            BRANCH    PRENAOPT,DISPPNMG,DISPPNMS,DISPPNMB    
          ENDIF
          GOTO      DISPDAX
.       
DISPPNMG  MATCH     SP70,PMPXPFGN
          IF        !@EQUAL 
            CALL      PRNM0000
            PERFORM   TYPPRDIS,DISPPNM,DISPPNMA
          ENDIF
          GOTO      DISPDAX
.
DISPPNMS  MATCH     SP70,PMPXPFSN
          IF        !@EQUAL
            CALL      PRNM0000
            PERFORM   TYPPRDIS,DISPPNM,DISPPNMA
          ENDIF
          GOTO      DISPDAX
.
DISPPNMB  MOVE      PRENAOPT,PRENAOPD
          MATCH     SP70,PMPXPFGN
          IF        !@EQUAL
            MATCH     SP70,PMPXPFSN
            IF        @EQUAL
              MOVE      1,PRENAOPT
            ENDIF
            CALL      PRNM0000
            PERFORM   TYPPRDIS,DISPPNM,DISPPNMA
          ELSE
            MATCH     SP70,PMPXPFSN
            IF        !@EQUAL
              MOVE      2,PRENAOPT
              CALL      PRNM0000
              PERFORM   TYPPRDIS,DISPPNM,DISPPNMA
            ENDIF
          ENDIF
          MOVE      PRENAOPD,PRENAOPT
          GOTO      DISPDAX
.
DISPPNM   PRINT       *1,"|",*11,"|",*28,"| ",*34,"|":
                      *44,"| ",PRFRNAME,*66,"| ":
                      *71,"|",*77,"|",*82,"| ",*87,"| ",*92,"|":
                      *104,"|",*115,"|",*132,"|"
          RETURN
.
DISPPNMA  PRINT       *1,"|",*18,"|",*28,"|",*45,"|",*51,"|":
                      *61,"| ",PRFRNAME,*83,"|",*90,"|",*95,"|":
                      *100,"|",*105,"|",*110,"|",*121,"|":
                      *132,"|"
          RETURN
.
DISPDAD   
          CALL        NAME0000
          CALL        OPMSFI
          MOVE        XNAME,DIM20
          MOVE        XATIME,DIM5
          PRINT       *1,"|",DNAME,*18,"|",XDAADMNO,*28,"|",ADMDATE:
                      *40,DIM5,*45,"| ",XATYPE:
                      *51,"|",AURNO,*61,"| ",DIM20,*83,"| ",XPSEX,*90,"|":
                       XTYPSER,*95,"| ",ACCLASS,*100,"| ",XACLSSFT:
                      *105,"| ",AWARD:
                      *110,"|",DOB,*121,"|",DISDATE,*132,"|"
.         
          IF        PRENAOPT > 0
            CLEAR     PRNAMES
            CLEAR     PRFRNAME
            MOVE      2,TYPPRDIS
            MOVE      PMPXPFGN,PREGNAME
            MOVE      PMPXPFSN,PRESNAME
            BRANCH    PRENAOPT,DISPPNMG,DISPPNMS,DISPPNMB
          ENDIF
.
DISPDAX   ADD       ONE,LNO
          ADD       ONE,PATNUM
          RETURN
+
.         LAST LINE ON THE PAGE
.
PAGEND    PRINT     "*-----------------------------------------------------":
                    "------------------------------------------------------":
                    "-----------------------*"
          ADD       ONE,LNO
          RETURN
+
.
.         Build Name
.
NAME0000  STRIP     PSNAME
          PACK      NAME,PSNAME,COMMASP,PGNAME,SP20,SP20
          RETURN
+
.
.         HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,PAGENO
	         COMPARE   ONE,PAGENO
	         CALL      PAGEND IF NOT EQUAL
          CLOCK     TIME TO CTIMEIS
.
          PRINT     *F,"IBAPAT40",*40,CNAME,*116,"DATE : ",CDATE:
                    *N,VERSION,*40,"*** ADMISSIONS LISTING ***";
.
          BRANCH    OPTION OF PRNTA,PRNTC,PRNTB,PRNTU
.
PRNTA     PRINT     "  (Admission Number Sequence)";
          GOTO      PRTCONT
.
PRNTC     PRINT     "  (Surname Sequence)";
          GOTO      PRTCONT
.
PRNTB     PRINT     "  (Attending Doctor Sequence)";
          GOTO      PRTCONT

PRNTU     PRINT     "  (Unit Sequence)";
.
PRTCONT   PRINT     *116,"TIME :   ",CTIMEIS:
                    *N,*116,"PAGE : ",*130,PAGENO
.
          IF        IBCNMHOS = 1
            PRINT     *40,"Hospital : ",PTHSNAME,*N;
          ENDIF
.
          PRINT     *40,"From Date : ",XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1:
                    *65,"To Date : ",XDAY2,SLASH,XMON2,SLASH,XCENT2,XYEAR2,*N
          IF        OPTION1=2
            PRINT     *40,"All Wards";
          ENDIF
.
          IF        OPTION1=1
            PRINT     *40,"Ward : ",SINGWARD;
          ENDIF
.
          MATCH     SP70,FILTUNIT
          IF        @EQUAL
            PRINT     *60,"Unit : All";
          ELSE
            PRINT     *60,"Unit : ",FILTUNIT;
          ENDIF
. 
          MATCH     SP70,FILTCLAM
          IF        @EQUAL
            PRINT     *80,"F.Class : All"
          ELSE
            PRINT     *80,"F.Class : ",FILTCLAM
          ENDIF
. 
.
          CALL      PAGEND
.
          BRANCH    OPTION TO PRINTA,PRINTA,PRINTD,PRINTA
PRINTA    
          PRINT       *1,"| Adm No.",*11,"| Adm Date":
                      *23,"Time",*28,"|A/Typ":
                      *34,"| U/R No.",*44,"| Patient's Name",*66,"|Sex":
                      *71,"|Serv",*77,"|F.Cl",*82,"|Unit":
                      *87,"|Ward":
                      *92,"|  D.O.B.   ":
                      *104,"| Dis Date",*115,"|Attending Doctor",*132,"|"
          CALL      PAGEND
          GOTO      PRINTX
.
PRINTD    
          PRINT       *1,"|Attending Doctor",*18,"| Adm No.",*28,"| Adm Date":
                      *40,"Time",*45,"|A/Typ":
                      *51,"| U/R No.",*61,"| Patient's Name",*83,"|Sex":
                      *90,"|Serv",*95,"|F.Cl",*100,"|Unit":
                      *105,"|Ward":
                      *110,"|  D.O.B.":
                      *121,"| Dis Date",*132,"|"
          CALL      PAGEND
.
PRINTX    IF        IBCNMHOS = 1
            MOVE      "10",LNO
          ELSE
            MOVE      "8",LNO
          ENDIF
          RETURN
+
.*****************************************************************************
.* CREA0000            Create the Temp Index File                            *
.*****************************************************************************
CREA0000  CALL      KILL0000
.
           MOVE      "168",DIM3
           CLEAR     CMDLINE
           PACK      CMDLINE,ISBUILD,TEMPFILE,SP1,DIM3,SP1,UKEY    
           EXECUTE   CMDLINE,TASKID
.
           OPEN      PATTEMP1,TEMPFILE
.
CREA9999   RETURN  
+
.*****************************************************************************
.* KILL0000            Delete the Temp Index File                            *
.*****************************************************************************
KILL0000   CLOSE     PATTEMP1
.
           CLEAR     CMDLINE
           PACK      CMDLINE,ISERASE,TEMPFILE
           EXECUTE   CMDLINE,TASKID
.
KILL9999   RETURN
+
ENDIT     CALL      KILL0000
          CHAIN     PGM
          STOP
+
.==============================================================================
.
          INC       STD001IO      
          INC       PATHSPKY
          INC       PATWRDKY
          INC       PATCODKY
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATHSPIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PMSPX2IO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
+

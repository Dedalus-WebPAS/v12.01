.******************************************************************************
.*              P R O G R A M  S U M M A R Y                                  *
.*              -------------  -------------                                  *
.*                                                                            *
.*     PROGRAM NAME:     IBAHOS21                                             *
.*                                                                            *
.*     FUNCTION:         IN-PATIENT STATUS REPORT                             *
.*                                                                            *
.*     DATE WRITTEN:     24/06/88                                             *
.*                                                                            *
.*     AUTHOR:           GRAEME WILLIAMS (I.B.A)                              *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                                                                            * 
.*        V12.00.01 30/05/2025  Don Nguyen       TSK 0955096                  *
.*                  Alphanumeric visit number changes                         *
.*        V11.00.02 20/04/2020  Sunny            TSK 0885863                  *
.*                  Fixed Inpatient status report output                      *
.*        V11.00.01 12/03/2020  Sunny            TSK 0885863                  *
.*                  WRDEXTN4 exnteded to 10 chars                             *
.*        V10.13.01 05/12/2018  Don Nguyen       TSK 0838558                  *
.*                  Deleted temp file (PSTROL) after processing               *
.******************************************************************************
.*        V10.04.01 17/06/2014 Steve Armstrong  CAR 301639                    *
.*                  Moved calls to TFILENAM for FNAMEI & FNAMER into INIT0000 *
.******************************************************************************
.*        V10.03.02 27/06/2013 Ebon Clements  CAR 287511                      *
.*                  Fixed temp file layout and key for WEXTN length change    *
.*                  Change to only print 4 character ward extension           *
.*        V10.03.01 27/12/2012 Patrick Adair      CAR 261630                  *
.*                  Removed port number from temp file name                   *
.*        V10.02.02 25/06/2011 Steve Armstrong    CAR 240722                  *
.*                  Mods to cater for field and key changes in PATLOCFD.      *
.*        V10.02.01 14/06/2011 Peter McMullen CAR 240725                      *
.*                  Use ward short name in place of ward code                 *
.******************************************************************************
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.******************************************************************************
.*       V9.10.01  11/09/2008 Sandra Barcham  174004                          *
.*                 Only display standby for correct hospital                  *
.******************************************************************************
.*       V9.09.02  29/02/2008  Peter Vela CAR 162831                          *
.*                 Added option to display patient condition                  *
.*        V9.09.01  22/10/2007  Davin         CAR 151920                      *
.*                  Changed to allow selection of 'All' hospitals for mult hos*
.******************************************************************************
.*        V9.06.01  13/06/2006  Janet George                                  *
.*                  Reduced the Attending Dr column to display doctor code    *
.*                  only and added condition column with Yes to indicate the  *
.*                  patient has a certain condition-patlocaf.LPCONDC(Cat CO)  *
.******************************************************************************
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.01  19/02/1999  Jim Stathopoulos                              *
.*                  Display Modifications                                     *
.*       V5.07.B02  28/01/1999  Nicole Harrington 507 rebounds                *
.*                  Mods to display admission date with century               *
.*       V5.07.B01  10/11/1998  Davin                                         *
.*                  Mods for 507                                              *
.*        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
.*                  Changed temp file name "pstidx" to standard format        *
.******************************************************************************
. *                       V5.01 01.05.89 S.Barcham         *
. *                             Fix isbuild                *
. *                       V5.02 17/05/89 S.O'Gorman        *
. *                             Allow 8 Digit U.R's &      *
. *                             Admission Numbers          *
. *                             Include STD001             *
. *                       V5.03 27/05/89 J.Tan             *
. *                             Fixed Mother Adm. to form8 *
. *                             (PATMI1FD).     SRF 100750 *
. *                       V5.04 06/06/89 J.Tan             *
. *                             Mods Locking master file   *
. *                             (PATIOMAS)     SRF 100809  *
. *                       V5.05 17.06.89 S.Barcham         *
. *                             Compiled for new standbys  *
. *                             SRF 101101                 *
. *                    V5.01.01 05/07/89 M.Ohleiter        *
. *                             Changes for 8 char ur and  *
. *                             admiss no, standards - 5.01*
. *                   V5.01.121 27/11/90 Glen Hobby        *
. *                            Recompiled for changes to   *
. *                            PATMX1FD                    *
. *                  V5.01.122 05/04/93  Steve O'Gorman    *
. *                            Modified for Multi Hospital *
. *                  V5.01.123 23/02/93  Whirlpool 122598  *
. *                            Fixed page numbering etc    *
. *                  V5.01.124 25/05/1994 Ian Rutt         *
. *                            Fixed Global Recompile Bugs * 
. *                  V5.01.125 20/07/1994  Rob Leonard     *
. *                            Fixed hospital keyin        * 
. *                                                        *
. **********************************************************
.
          INC       STD001FD
.
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATHSPFD/INC
          INC       PATLOCFD/INC
          INC       PATMA1FD/INC

          INC       PATMI1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       PATWR1FD/INC
          INC       PATDO1FD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
+
.
.               WORK AREA
.
.-------------------------------------------------------------
.Name        Type     Length     Physical     Start
.----        ----     ------     --------     -----
.PSNAME      DIM         20           20         1
.PGNAME      DIM         25           25        21
.AADMNO      DIM          8            8        46
DUNIQUE      DIM          8            8        54
.WEXTN       DIM         20           20        62
.PTITL       DIM          4            4        82
WRDBED       DIM         14           14        86
.ADOCTA      DIM          6            6       100
.ADATE       DIM          8            8       106
.PTMASNAM    DIM         40           40       114
.PMPXFNAM    DIM         40           40       154
.
.  END OF RECORD                               194
.
.  redefine form fields for key
.  ----------------------------
.Name        Type     Length     Start
.----        ----     ------     -----
.AADMNO      FORM         8
UNIQUE    FORM      8
.-------------------------------------------------------------
.
AFIRST    DIM       20
ALAST     DIM       20
.
CMDLINE   DIM       50
.
DNAME     DIM       27
DFIRST    DIM       20
DLAST     DIM       20
.
FNAMER    DIM       8
FNAMEI    DIM       8
.
IBCNMHOS  FORM      1
.
OPCND     FORM      1
.
PATNCOND  DIM       1
PSTROL    FILE      ASCII, FIXED=256
.
PTCONFLG  DIM       3                           * Flag for patient condition
.
STATUS    FORM      1
.
TEMPFILE  IFILE     SQL, FIXED=194, KEY="U1-61"
TIMEIS    DIM       8
.
WRDEXTN4  DIM       10                 * DIM 10 ward extension
.
.
PRGID     INIT      "IBAHOS21"
PRGNAM    INIT      "In-patient Status Report"
VERSION   INIT      "V12.01.00"
+
.         Start of Program
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patmi1af";
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A4,"patwr1af"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patlocaf";
          OPEN      PATLOCA1,"patlocaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN       CONTROLF,"controlf"
          READ       CONTROLF,ZERO;*43,IBCNMHOS
.
          DISPLAY   *P1:3,*EF;
          CALL      KHOS0000                     * Keyin Hospital Code
          BRANCH    EXIT,ENDIT
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEI
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMER
.
.         Select Option
.
START     MOVE      ZERO,CPAGENO
          MOVE      ONE TO UNIQUE
.
          HLINE     *G37,2,52,80
          DISPLAY   *P1:3,*EL:
                    *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Surname Sequence"
.
OPT1      KEYIN     *P1:7,"Option : ",*EL,*V2LON,OPTION
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF NAMENO
          BEEP
          GOTO      OPT1
.
NOMAST    DISPLAY   *P1:24,*EL,*P40:24,*V2LON:
                    "** No Patients Found **",*W2;
          GOTO      START
+
.
.               PRINT VIA SURNAME SORT KEY
.
NAMENO    DISPLAY   *P52:2,*V2LON,"- Surname Sequence "
          MOVE      SP20 TO AFIRST
          MOVE      SP20 TO DFIRST
          MOVE      "ZZZZZZZZZZZZ" TO ALAST
          MOVE      SP20 TO DLAST
.
          KEYIN     *P1:4,*EF:
                    *P1:6,"Starting Surname : ",*V2LON,AFIRST
.         
          MOVE      AFIRST,DFIRST
          RESET     DFIRST
          MATCH     SP20,DFIRST
          IF        @EQUAL
            MOVE      "Start",DFIRST
          ENDIF
.
NAMENO1   DISPLAY   *P20:6,*V2LON,DFIRST          
          KEYIN     *P1:8,"  Ending Surname : ",*V2LON,ALAST
          RESET     ALAST
          GOTO      MASTA1 IF NOT EOS
.
          MOVE      "ZZZZZZZZZZZZ" TO ALAST
          MOVE      ALAST,DLAST
          MATCH     "ZZZZZZZZZZZZ",DLAST
          GOTO      NAMENO2 IF NOT EQUAL
          MOVE      "Finish",DLAST           
.
NAMENO2   DISPLAY   *P20:8,*V2LON,DLAST         
.
MASTA1    RESET     AFIRST
          GOTO      MASTA2 IF NOT EOS
.
          MOVE      SP6,AFIRST
.
MASTA2    KEYIN     *P1:10,*EL,"Include Patient Condition column ":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF," ? ",*V2LON,PATNCOND;
.
          REP       "yYnN",PATNCOND
.
          CMATCH    "Y",PATNCOND
          GOTO      MASTA3 IF EQUAL
.
          CMATCH    "N",PATNCOND
          GOTO      MASTA3 IF EQUAL
.
          BEEP
          GOTO      MASTA2
.
MASTA3    KEYIN     *P1:20,*EL,"Ok to continue (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       "yYnN",ANS
.
          CMATCH    "Y" TO ANS
          GOTO      INITC IF EQUAL
.
          CMATCH    "N" TO ANS
          GOTO      START IF EQUAL
.
          BEEP
          GOTO      MASTA3
+
.
.       INITIALISE FILE
.
INITC     DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Initialising Status File **",*P1:12;
.
          CALL      INDZD
          BRANCH    OPCND OF ENDIT
.
          MOVE      TWO,ASTAT
.
NEXTAS    PACK      KEY9 WITH ASTAT,SP8
          CALL      RDSMISS2
          MOVE      "60",CLNO
.
          DISPLAY   *P1:12,*EF,*P50:24,"Scanning :";
.
NEXTALP   CALL      RDKMISS2
          BRANCH    OVRCD OF ENDW
.
          COMPARE   FIVE,ASTAT
          GOTO      ENDW IF EQUAL
.
          COMPARE   THREE,ASTAT
          GOTO      CNTALP IF NOT EQUAL
.
.       Check for two consequetive discharged admissions ( to allow for
.       index file corruption )
.
          CALL      RDKMISS2
          BRANCH    OVRCD OF ENDW
.
          COMPARE   THREE,ASTAT
          GOTO      CNTALP IF NOT EQUAL
.
.        PROCESS ON LEAVE PATIENTS
.
          MOVE      FOUR,ASTAT
          GOTO      NEXTAS
.
CNTALP    DISPLAY   *P60:24,*V2LON,AADMNO;
.
          COMPARE   ONE,IBCNMHOS
          GOTO      ENDWRD1 IF NOT EQUAL
.
          MATCH     SP10,PTHSHOSP
          GOTO      ENDWRD1 IF EQUAL
.
          MATCH     SP3,AWARD
          IF        !@EQUAL
            PACK      KEY6,AWARD,SP3
            CALL      RDWARD1
            BRANCH    OVRCD,ENDWRD1
.
            MATCH     PTHSHOSP,PTWDHOSN        * Different Hospital
            GOTO      NEXTALP IF NOT EQUAL
          ENDIF
.
ENDWRD1   MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,NEXTALP
.
          MOVE      AURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,NEXTALP
.
          MATCH     ALAST,PSNAME
          GOTO      CHKNAM2 IF EQUAL
          GOTO      NEXTALP IF NOT LESS
.
CHKNAM2   MATCH     AFIRST,PSNAME
          GOTO      CHKNAM3 IF EQUAL
          GOTO      NEXTALP IF LESS
.
CHKNAM3   PACK      KEY6,AWARD,SP3
          CALL      RDWARD1
          PACK      WRDBED WITH PTWDSDES,SLASH,ABED
          MATCH     SP3,AWARD
          GOTO      CONT1 IF NOT EQUAL
.
          MOVE      "STANDBY",WRDBED
          PACK      KEY14,AADMNO,SP6
          CALL      RDSWARD4
          CALL      RDKWARD4
          BRANCH    OVRCD,NEXTALP
.
          MATCH     AADMNO,WSTBY
          GOTO      NEXTALP IF NOT EQUAL
.
          MATCH     PTHSHOSP,PTWDHOSN        * Different Hospital
          GOTO      NEXTALP IF NOT EQUAL
.
CONT1     BRANCH    ASTAT OF NEXTALP,CURADM,NEXTALP,ONLEAVE
          GOTO      NEXTALP
.
ONLEAVE   MOVE      "LEAVE  ",WRDBED
          MOVE      SP3,AWARD
          MOVE      SP3,ABED
.
CURADM    MOVE      SP70,WEXTN
          PACK      KEY6 WITH AWARD,ABED
          CALL      RDWARD1
.
          PACK      KEY61 WITH PSNAME,PGNAME,AADMNO,UNIQUE
          WRITE     TEMPFILE,KEY61;PSNAME,PGNAME,AADMNO,UNIQUE,WEXTN:
                    PTITL,WRDBED,ADOCTA,ADATE,PTMASNAM,PMPXFNAM
          ADD       ONE TO UNIQUE
          GOTO      NEXTALP
+
.           OVER CONDITION FOUND..
.
ENDW      CALL      IBACLOCK
          PACK      KEY61 WITH SP30,SP30
          DISPLAY   *P10:24,*EL,"Generating :";
          READ      TEMPFILE,KEY61;;
.
NEXTR     READKS    TEMPFILE;PSNAME,PGNAME,AADMNO,UNIQUE,WEXTN:
                    PTITL,WRDBED,ADOCTA,ADATE,PTMASNAM,PMPXFNAM
          GOTO      ENDP IF OVER
.
          DISPLAY   *P25:24,*V2LON,PSNAME;
.
          CALL      DISPDA
          GOTO      NEXTR
+
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP      CALL      KILLIDZ
          COMPARE   ZERO,CPAGENO
          GOTO      NOMAST IF EQUAL
          CALL      PAGEND
          PRINT     *N,"  ***  END OF REPORT  ***"
          DISPLAY   *P1:24,*EL,*P40:24,*V2LON:
                    "** Report Generation Completed **",*W2;
          GOTO      START
+
.
.     PRINT ACTUAL DATA
.
DISPDA    COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
.
          PACK      DNAME WITH SP30,SP30
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF PDATA1
.
          CLEAR     DNAME
          APPEND    DSNAME,DNAME
          CALL      FINDEND
          APPEND    ", ",DNAME
          APPEND    DTITL,DNAME
          CALL      FINDEND
          APPEND    SP1,DNAME
          APPEND    DGNAME,DNAME
          APPEND    SP30,DNAME
          APPEND    SP30,DNAME
          RESET     DNAME
.
.         Default PTCONFLG as blank
.
          MOVE      SP10,PTCONFLG       
          PACK      KEY88,PTMASNAM,PMPXFNAM,AADMNO,SP70,SP70
          CALL      RDLOCA1
          BRANCH    OVRCD OF PDATA1
.
.         Display YES if patient condition exists
.
          MATCH     LPCONDC,SP10
          IF        !@EQUAL
            MOVE      "YES",PTCONFLG
          ENDIF
.
PDATA1    MOVE      WEXTN,WRDEXTN4
          MATCH     "Y",PATNCOND
          IF        @EQUAL
            PRINT     *1,"! ",PSNAME," ",PGNAME," ",PTITL:
                      *56,"! ",WRDBED:
                      *74,"! ",WRDEXTN4,*87,"!",AADMNO:
                      *98,"! ",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                      *111,"! ",ADOCTA,*120,"!",PTCONFLG,*132,"!"
          ELSE
            PRINT     *1,"! ",PSNAME," ",PGNAME," ",PTITL:
                      *56,"! ",WRDBED:
                      *74,"! ",WRDEXTN4,*87,"!",AADMNO:
                      *98,"! ",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                      *111,"! ",DNAME,*132,"!"
          ENDIF
          ADD       ONE,CLNO
          RETURN
.
FINDEND   CMATCH    SP1,DNAME
          RETURN    IF NOT EQUAL
.
          BUMP      DNAME,-1
          GOTO      FINDEND IF NOT EOS
          RETURN
+
.
.          LAST LINE ON THE PAGE
.
PAGEND    PRINT     "*-------------------------------------------":
                    "-------------------------------------------":
                    "--------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      
          CLOCK     TIME,TIMEIS
          CALL      HEAD132
          IF        IBCNMHOS=1
            PRINT     *45,PTHSNAME
          ENDIF
          PRINT     *45,"STARTING SURNAME : ",DFIRST:
                    "  ENDING SURNAME : ",DLAST,*N
.
          CALL      PAGEND
.
          MATCH     "Y",PATNCOND
          IF        @EQUAL
            PRINT     *1,"! Patients Name",*56,"! Ward/Bed":
                      *74,"! Extn",*87,"! Admno":
                      *98,"! Adm. Date",*111,"! Att Dr":
                      *120,"! Pat. Cond":
                      *132,"!"
          ELSE
            PRINT     *1,"! Patients Name",*56,"! Ward/Bed":
                      *74,"! Extn",*87,"! Admno":
                      *98,"! Adm. Date",*111,"! Attending Dr":
                      *132,"!"
          ENDIF     
          CALL      PAGEND
.
          MOVE      TEN,CLNO
          RETURN
+
.
.      Create an indexed file based on port
.       for accumulating Admission Type totals
.
INDZD     MOVE      ZERO,STATUS
.
.       CHECK IF PREVIOUSLY EXISTS..IF SO DELETED IT
.
          CALL      KILLIDZ
          PREP      PSTROL,FNAMER
IDXSUR    WRITE     PSTROL,SEQ;"isbuild ",FNAMEI," 194 U1-61"
          WEOF      PSTROL,SEQ
.
          MOVE      ONE,STATUS
.
          CLEAR     CMDLINE
          APPEND    "sh ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     PSTROL,DELETE
.
          MOVE      ZERO,OPCND
          TRAP      NOINDZ IF IO
          OPEN      TEMPFILE,FNAMEI
          TRAPCLR   IO
ENDC      RETURN
.
NOINDZ    MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Temporary Index File ":
                    FNAMEI," Not Found **",*W4;
          TRAPCLR   IO
          RETURN
+
.
.       Deleting an indexed file based on port
.
KILLIDZ   CLOSE     TEMPFILE
.
          MOVE      ZERO,STATUS
.
          DISPLAY   *P1:24,*EL,*P40:24,*V2LON:
                    "** Deleting Indexed File **";
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P1:11,*EF
          RETURN
+
.******************************************************************************
.*                                  KHOS0000                                  *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:4,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      "4",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:4,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.
+
          INC       STD001IO
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATHSPIO/INC
          INC       PATLOCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATDO1IO/INC
          INC       PATWR1IO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
.
          INC       PATHSPKY
.
ENDIT     CHAIN     PGM
          STOP

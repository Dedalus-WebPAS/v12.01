.******************************************************************************
.*               P R O G R A M  S U M M A R Y                                 *
.*               -------------  -------------                                 *
.*                                                                            *
.*      PROGRAM NAME:     IBAHOS22                                            *
.*                                                                            *
.*      FUNCTION:         Suggested Classification Report                     *
.*                                                                            *
.*      DATE WRITTEN:     07.07.88                                            *
.*                                                                            *
.*      AUTHOR:           S.Barcham                                           *
.*                                                                            *
.*      MODIFICATIONS:                                                        *
.*                                                                            *
.******************************************************************************
.* V12.00.01 28/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.*        V11.05.01 09.12.2024  DA Sarkies     TSK 0951086                    *
.*                  Increased size of ADYHOSP to 4 digits                     *
.*        V11.00.01 11/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*        V10.13.01 05/12/2018  Don Nguyen       TSK 0838558                  *
.*                  Deleted temp file (PSTROL) after processing               *
.*        V10.10.02 04/04/2017 Ania P CAR 260631                              *
.*                  Removed use of PORT                                       *
.*        V10.01.01 19/01/2011 J.Tan  CAR 233034                              *
.*                  Mods Bed fees to use HF Table type                        *
.*        V9.07.01  20/09/06 J.Tan    CAR 119336                              *
.*                  Added claim code to Suggested classification file         *
.*        V9.04.01  08/09/2004  Davin   CAR 53146                             *
.*                  Added Multi-Hospital processing                           *
.*        V9.03.01  22/10/2003  J.Tan   CAR  44172                            *
.*                  Mods for Misc.Theatre item file                           *
.*        V9.02.01  23/08/2001  Jill Habasque                                 *
.*                  Changed Health fund to be stored in a DIM10 for web       *
.*        V5.08.01  16/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.01  02/10/1999  Jill Habasque SRF 633547                      *
.*                  Fixed OPDATE to use century                               *
.*        V5.07.B02 28/01/1999  Nicole Harrington                             *
.*                  507 rebounds                                              *
.*        V5.07.B01 10/11/1998  Davin  507                                    *
.*                  Mods for 507                                              *
.*        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
.*                  Changed temp file name "pstidx" to standard format        *
.******************************************************************************
.*            V4.01 28/10/88 Graeme Williams                                  *
.*                  Fix up code for multiple MBS cdes                         *
.*            V5.01 01/03/89 J.Tan                                            *
.*                  Fixed the heading date                                    *
.*                  SRF 3331                                                  *
.*            V5.02 14/03/89 Graeme Williams                                  *
.*                  Fix I*D in option 2                                       *
.*                  SRF 4788                                                  *
.*            V5.03 13/04/89 Graeme Williams                                  *
.*                  New index added to Dtrans                                 *
.*                  SRF 5645                                                  *
.*            V5.04 17/05/89 S.O'Gorman                                       *
.*                  Allow for 8 Digit U.R's &                                 *
.*                  Admission Numbers                                         *
.*                  Include STD001                                            *
.*            V5.05 27/05/89 J.Tan                                            *
.*                  Fixed Mother Adm. to form8                                *
.*                  (PATMI1FD).     SRF 100750                                *
.*            V5.06 02/06/89 J.Tan                                            *
.*                  Fixed heading on screen                                   *
.*                  SRF 100884                                                *
.*            V5.07 06/06/89 J.Tan                                            *
.*                  Mods Locking master file                                  *
.*                  (PATIOMAS)     SRF 100809                                 *
.*            V5.08 17.06.89 S.Barcham                                        *
.*                  Compiled for new standbys                                 *
.*                  SRF 101101                                                *
.*         V5.01.01 05/07/89 M.Ohleiter                                       *
.*                  Changes for 8 char ur and                                 *
.*                  admiss no, standards - 5.01                               *
.*         V5.01.02 23/05/90 J.Tan  SRF 104901                                *
.*                  Mods PATITMFD                                             *
.*        V5.01.121 27/11/90 Glen Hobby                                       *
.*                  Recompiled for changes to                                 *
.*                  PATMX1FD                                                  *
.*        V5.01.122 24/01/92 J.Tan   SRF 112750                               *
.*                  Fixed suggested class. for                                *
.*                  items more than 4 char.                                   *
.*        V5.01.123 24/04/92 J.Tan   SRF 114169                               *
.*                  Added option to have sugg.                                * 
.*                  class.base on MBS or Amount                               *
.*        V5.01.124 12/06/92  Graeme Williams   SRF 114865                    *
.*                  Changes for 9 character misc items                        *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATHSPFD/INC
          INC       PATMI1FD/INC
          INC       PATBFEFD/INC
          INC       PATCODFD/INC
          INC       PATCFAFD/INC
          INC       PATCONFD/INC
          INC       PMSCIDFD/INC
          INC       PATDTRFD/INC
          INC       PATFN1FD/INC
          INC       PATFX1FD/INC
          INC       PATITMFD/INC
          INC       PATMA1FD/INC
          INC       PATTRNFD/INC
          INC       PATWR1FD/INC
          INC       PATSGCFD/INC
          INC       PMSMTIFD/INC
          INC       PMSVX1FD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.               VARIABLES DEFINITIONS
.-------------------------------------------------------------
.Name      Type      Length      Physical      Start
.----      ----      ------      --------      -----
XWARD      DIM          3              3          1
XBED       DIM          3              3          4
XAADMNO    DIM          8              8          7
DUNIQUE    DIM          8              8         15
PNAME      DIM         15             15         23
DAYNUM     FORM         4              4         46
ODATE      DIM          8              8         63
CURHR      FORM         4.2            4         71
CURPAT     FORM         4.2            4         75
OSUGCLAS   DIM          3              3         79
NEWHR      FORM         4.2            4         82
NEWPAT     FORM         4.2            4         86
.
.  END OF RECORD                                 96
.
.  redefine form fields for key
.  ----------------------------
.Name      Type      Length      Start
.----      ----      ------      -----
UNIQUE     FORM         8           1
.-------------------------------------------------------------
PSTSRT    FILE      ASCII, FIXED=256
PSTROL    FILE      ASCII, FIXED=256
SORT1     INIT      "u7-22"
SORT2     INIT      "u1-6,13-22"
SORTSTR   DIM       10
TEMPFILE  IFILE     SQL, FIXED=95, KEY=SORTSTR
.
FNAMER    DIM       8
FNAMEI    DIM       8
.
CALDAYS   FORM      4
CMDLINE   DIM       50
CODEA     INIT      "A "
CODEBT    INIT      "BT"
CODECL    INIT      "CL"
CUDATE    DIM       8
CEFFDATE  DIM       8
CONTRCID  DIM       6
CNTRCEFR  FORM      1
.
DAYLEFT   FORM      3
DDATE     INIT      "        "
DIM4      DIM       4
DIM5      DIM       5
DIM8      DIM       8
DIM9      DIM       9
DIM10     DIM       10
DIM11     DIM       11
DIMHF     DIM       10
DIM19A    DIM       19
DIM18     DIM       18
DIM18A    DIM       18
DIM23     DIM       23
DIM23A    DIM       23
.
FROMDATE  DIM       8
IBCNMHOS  FORM      1                       * Multi Hospital Indicator
.
JYEAR     FORM      2
.
LYEAR     FORM      "366"
.
MTIFLAG   FORM      1
NBRDAYS   FORM      4
NEWFEE    FORM      1
NOBF      FORM      1
.
OCHARGE   FORM      7.2
OMBS      FORM      5
OYEAR     FORM      "365"
.
PATNUM    FORM      6
.
SAVBED    DIM       3
SAVWARD   DIM       3
SUGHR     DIM       7
SUGPAT    DIM       7
.
TODATE    DIM       8
TRDATE    DIM       8
.
WSTAT     FORM      1
WDATE     DIM       8
WDATE1    DIM       8
WDATE2    DIM       8
.
XDATE1    DIM       8       * Yesterday date CCYYMMDD
XDAY1     DIM       2
XMON1     DIM       2
XYEAR1    DIM       2
XCENT2    DIM       2
XDAY2     DIM       2
XMON2     DIM       2
XYEAR2    DIM       2
.
YTDATE    DIM       8
TEMPADT   DIM       11
.
PRGID     INIT      "IBAHOS22"
PRGNAM    INIT      "Suggested Classification Report"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          DISPLAY   *P56:24,"Opening patmi1af";
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patitemn";
          OPEN      PATITEM1,"patitemn"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patdtraf";
          OPEN      PATDTRA1,"patdtraf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"patbfeef";
          OPEN      PATBFEE1,"patbfeef"
.
          DISPLAY   *P64:24,"pmsmtiaf";
          OPEN      PMSMTIA1,"pmsmtiaf"
          OPEN      PMSMTIA2,"pmsmtiaf"
.
          OPEN      PATFN1A1,"patfn1af"
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN8;*143,CLOWMBS,CLOWADM,CHIGHMBS,CHIGHADM:
                                      CTOPADM
          READ      CONTROLF,TWENTY;*209,PTCNSCLS
          PACK      CUDATE,CCC,CYY,CMM,CDD
          REP       " 0",CUDATE
.
          CALL      TFILENAM
          PACK      FNAMEI,TFILNAME
.
          CALL      TFILENAM
          PACK      FNAMER,TFILNAME
.
          CALL      DISPHEAD
.
+
.         START OF PROGRAM
.
START     MOVE      ZERO,CPAGENO
          MOVE      SP1,ANS
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Admission Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Ward/Bed Sequence"
.
          KEYIN     *P1:8,*EL,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
REPEAT    BRANCH    OPTION OF STPROC,STPROC
          BEEP
          DISPLAY   *P30:7,*EL,*V2LON,"Invalid selection",*W2;
          GOTO      START
.
.
.            PROCESS START
.
STPROC  CALL       KHOS0000
        BRANCH     EXIT,START
.
        CALL        INDZD
        CLOCK       TIME,CTIMEIS
        MOVE        ONE,UNIQUE
        CALL        SETYDAY            * Set up yesterday date
.
        DISPLAY     *P1:24,*EL,*P10:24,*V2LON:
                    "** Processing Current Admission **",*W;
        DISPLAY     *P1:24,*EL,*P20:24,"Current Admission No :":
                    *P55:24,"Scanning :";
        MOVE        TWO,WSTAT
        CALL        ADMFN
.
        GOTO        REPORT
.
.               READ ADMISSION FILE
.
ADMFN   PACK       KEY9 WITH WSTAT,SP8
        CALL       RDSMISS2
.
NEXT5   CALL       RDKMISS2
        COMPARE    ONE,OVRCD
        RETURN     IF EQUAL
.
        COMPARE    WSTAT,ASTAT
        RETURN     IF NOT EQUAL
.
        DISPLAY    *P72:24,AADMNO;
.
        MOVE       ADATE TO WDATE
        REP        " 0",WDATE
.
        MATCH      CUDATE,WDATE
        GOTO       NEXT5 IF NOT LESS
.
        COMPARE   ONE,IBCNMHOS              * check for multihospital
        GOTO      NEXTA1 IF NOT EQUAL
.
        PACK      PTHSHOSP,PTHSHOSP,SP70
        MATCH     SP10,PTHSHOSP
        GOTO      NEXTA1 IF EQUAL
.
        MOVE      AADMNO,KEY8
        CALL      RDPMVX11
        BRANCH    OVRCD,NEXT5
.
        MATCH     PMVXMHOS,PTHSHOSP
        GOTO      NEXT5 IF NOT EQUAL
.
NEXTA1  CALL       TRANS  
        GOTO       NEXT5
.
.
.          Subroutine to process the transfer file for the patient
.
TRANS   MOVE        "00000000",ODATE
        MOVE        ZERO TO OMBS
        MOVE        ZERO TO OCHARGE
        MOVE        SP3 TO OSUGCLAS
        MOVE        ZERO,MTIFLAG
.
        PACK        KEY9,ACLAIM,AFUNDH,SP70
        CALL        GETCNEFF               * Get Contract Effective From
.
        PACK        KEY22 WITH AADMNO,SP20
        CALL        RDSDTRN1
TRAN10  CALL        RDKDTRN1
        BRANCH      OVRCD OF TRAN70 
.
        MATCH       AADMNO,TADMNO
        GOTO        TRAN70 IF NOT EQUAL
.
        COMPARE     TWO,TRECTYPE
        GOTO        TRAN10 IF NOT EQUAL
.
        PACK        TRDATE WITH TFCENT,TFYEAR,TFMONTH,TFDAY
        REP         " 0",TRDATE
.
TRAN20  MATCH       CUDATE,TRDATE
        GOTO        TRAN60 IF NOT LESS
.
.       Check if the item is an MBS number
.
        PACK        KEY9 WITH TITEMNO,SP5
        PACK        KEY17,TITEMNO,TRDATE,SP70
        CALL        PATITMRD
        BRANCH      OVRCD TO TRAN30
        GOTO        TRAN40 
.
.       Try the receipt number as an MBS number
.
TRAN30  PACK        KEY9 WITH TRECEIPT,SP5
        PACK        KEY17,TRECEIPT,TRDATE,SP70
        CALL        PATITMRD
        BRANCH      OVRCD TO TRAN60 
.
.       We have an MBS number
.
TRAN40  BRANCH      PTCNSCLS,TRAN50         * Using highest amount
.
.       Get the suggested classification base on the highest MBS
.
        COMPARE     OMBS,IAMT
        GOTO        TRAN60 IF LESS
        GOTO        TRAN52 IF NOT EQUAL
.
        COMPARE     TPATAMTT,OCHARGE
        GOTO        TRAN60 IF NOT LESS
        GOTO        TRAN52
.
.       Get the suggested classification base on the highest Amount
.
TRAN50  COMPARE     OCHARGE,TPATAMTT
        GOTO        TRAN60 IF LESS
        GOTO        TRAN52 IF NOT EQUAL
.
        COMPARE     IAMT,OMBS
        GOTO        TRAN60 IF NOT LESS
.
TRAN52  MOVE        IAMT TO OMBS
        MOVE        TPATAMTT,OCHARGE
.
        PACK      KEY18,ACLAIM,AFUNDH,ITEMNO,SP70     * key of fund and item
        OPEN      PATSGCA1,"patsgcaf"     * open the file
        CALL      RDPTSGC1                * read the file for PTSGCLSS
        COMPARE   ZERO,OVRCD
        GOTO      TRAN58 IF EQUAL
.
        MATCH     SP70,AFUNDH
        GOTO      TRAN56 IF EQUAL
.
        MOVE      SP70,PTSGCLMN           * blank claim
        PACK      KEY18,PTSGCLMN,AFUNDH,ITEMNO,SP20  * key of fund and item
        CALL      RDPTSGC1                * read exceptions file
        COMPARE   ZERO,OVRCD
        GOTO      TRAN58 IF EQUAL
.
TRAN56  MOVE      SP70,PTSGFUND           * blank health fund
        PACK      KEY18,ACLAIM,PTSGFUND,ITEMNO,SP20  * key of fund and item
        CALL      RDPTSGC1                * read exceptions file
.
TRAN58  MOVE      PTSGCLSS,OSUGCLAS       * default to file class
        LOAD      OSUGCLAS,OVRCD,ICLSS    * use ICLSS if not on file
.
...        MOVE        ICLSS,OSUGCLAS
        MOVE        TRDATE,ODATE        * Save the latest date of theatre
        REP         " 0",ODATE
.
TRAN60  BRANCH    MTIFLAG,TRAN75 
        GOTO      TRAN10
.
.       Check with items in Misc.Theatre item file
.
TRAN70  MOVE      ONE,MTIFLAG
        MOVE      "2",PMMIRTYP
        PACK      KEY15,AADMNO,PMMIRTYP,SP20
        CALL      RSPMMTI2
TRAN75  CALL      RKPMMTI2
        BRANCH    OVRCD,TRAN80
.
        MATCH     PMMIVISN,DAADMNO
        GOTO      TRAN80 IF NOT EQUAL
        MATCH     "2",PMMIRTYP
        GOTO      TRAN80 IF NOT EQUAL     * Not a theatre record
.
        MOVE      PMMIITEM,TITEMNO
        MOVE      PMMIREFN,TRECEIPT
        MOVE      PMMIAMTT,TPATAMTT
        MOVE      PMMITDAT,TRDATE
        GOTO      TRAN20 
.
TRAN80  DISPLAY     *P47:24,*V2LON,AADMNO;
.
        MOVE        ADATE TO FROMDATE
        MOVE        CUDATE,TODATE          * use yesterday date
        REP         " 0",FROMDATE
        REP         " 0",TODATE
        CALL        NHOSPDAY               * Calculate no of days
        MOVE        NBRDAYS,DAYNUM
        ADD         ADYHOSP TO DAYNUM
.
.         Subroutine to get the ward and rate at a given date
.
GETRATE   MOVE      SP3,SAVWARD
          MOVE      SP3,SAVBED
          MOVE      SP3,XWARD
          MOVE      SP3,XBED
          MOVE      SP3 TO STATYPE
          MOVE      SP3 TO STRBTYP
.
          PACK      KEY30 WITH AADMNO,SP20
          CALL      RDSTRAN2
.
NXTTRAN   CALL      RDKTRAN2
          BRANCH    OVRCD OF CALRATE
.
          MATCH     TADMN,AADMNO
          GOTO      CALRATE IF NOT EQUAL
.
          MATCH     CUDATE,TDATE
          GOTO      CALRATE IF NOT LESS
.
          MOVE      TWARD,SAVWARD
          MOVE      TBED,SAVBED
          MOVE      TATYPE,STATYPE
          MOVE      TRBTYP,STRBTYP
          MOVE      TRATEF TO CURPAT
          MOVE      TRATEH TO CURHR
          GOTO      NXTTRAN
.
CALRATE MATCH       SP3,OSUGCLAS
        GOTO        NXT11 IF NOT EQUAL
.
        MOVE        STATYPE TO OSUGCLAS
        COMPARE     OMBS TO CLOWMBS
        GOTO        CHKLESM IF NOT LESS
.
        COMPARE     OMBS TO CHIGHMBS
        GOTO        CHKLEDH IF NOT LESS
.
        MATCH       CTOPADM TO STATYPE
        GOTO        NXT11 IF EQUAL
.
        MOVE        CTOPADM TO OSUGCLAS
        GOTO        NXT11
.
CHKLESM MATCH       CLOWADM TO STATYPE
        GOTO        NXT11 IF EQUAL
.
        MOVE        CLOWADM TO OSUGCLAS
        GOTO        NXT11
.
CHKLEDH MATCH       CHIGHADM TO STATYPE
        GOTO        NXT11 IF EQUAL
.
        MOVE        CHIGHADM TO OSUGCLAS
.
NXT11     MOVE      SAVWARD,XWARD
          MOVE      SAVBED,XBED
.
.              Read the ward file to get the room type
.
          MOVE      ZERO,WEFRBT
          PACK      KEY6 WITH XWARD,XBED
          CALL      RDWARD1
.
          MATCH     SP3 TO STRBTYP
          GOTO      GETBFEE IF NOT EQUAL
.
          PACK      STRBTYP WITH WEFRBT,SP2
.
.              Get the Day left at this rate
.
GETBFEE MOVE        ZERO TO NEWPAT
        MOVE        ZERO TO NEWHR
.
.       The max last day in period for bed fee is only two digits 
.
        COMPARE     "100",DAYNUM            * Check if the daynum > 2 digits
        GOTO        UPFILE IF NOT LESS 
.
        IF          CFEETYP=9
          OPEN        JHSBFEA1,"jhsbfeaf"
          PACK        DIM23 WITH ACLAIM,AFUNDH,AFNDTB,OSUGCLAS,STRBTYP
          PACK        KEY26 WITH ACLAIM,AFUNDH,AFNDTB,OSUGCLAS,STRBTYP,SP70
          CALL        RSJHBFE1
        ELSE
          COMPARE     THREE,CNTRCEFR
          GOTO        UPFILE IF EQUAL       * Invalid Contract Effective details
          MOVE        SP70,PTHFBCAT
          PACK        KEY14,AFUNDH,AFNDTB
          CALL        RDFUND1
          PACK        DIM18 WITH ACLAIM,AFUNDH,PTHFBCAT,OSUGCLAS,STRBTYP
          PACK        KEY27 WITH ACLAIM,AFUNDH,PTHFBCAT,OSUGCLAS,STRBTYP,SP70
          CALL        RDSBFEE1
        ENDIF
.
NXTBFE  IF          CFEETYP=9
          CALL        RDKBFEE1
          BRANCH      OVRCD OF UPFILE
          PACK        DIM23A WITH BFCLM,BFHFUND,BFHFTAB,BFATYPE,BFBTYPE
          MATCH       DIM23,DIM23A
          GOTO        UPFILE IF NOT EQUAL
        ELSE
          CALL        RDKBFEE1
          BRANCH      OVRCD OF UPFILE
          PACK        DIM18A WITH BFCLM,BFHFUND,PTHFBCAT,BFATYPE,BFBTYPE
          MATCH       DIM18,DIM18A
          GOTO        UPFILE IF NOT EQUAL
.
          MOVE        CUDATE,CEFFDATE          * set date for 'As at date'
          LOAD        CEFFDATE,CNTRCEFR,ADATE
          MOVE        PTBFCNID,KEY6           * Contract ID
          CALL        VALCONTR            * Validate Contract ID
          BRANCH      EXIT,NXTBFE
          MOVE        PTBFCNID,CONTRCID       * Contract ID
        ENDIF
.
        COMPARE     DAYNUM,BFENDDY
        GOTO        NXTBFE IF LESS
.
        MOVE        BFPAT TO NEWPAT
        MOVE        BFHF TO NEWHR
.
.              Updating the tempfile
.
UPFILE  MOVE        AURNO,KEY8
        CALL        RDMAST1
        BRANCH      OVRCD OF NOMAST
.
        MOVE        SP20,PNAME
        MOVE        PGNAME TO ANS
        PACK        PNAME WITH ANS,DOT,PSNAME
.
        BRANCH      OPTION TO ADMKEY,WARKEY
.
ADMKEY  PACK        KEY16 WITH AADMNO,UNIQUE
        GOTO        WRTKEY

WARKEY  PACK        KEY16 WITH XWARD,XBED,UNIQUE
.
WRTKEY  CALL        WRTEMP
        ADD         ONE,UNIQUE
        RETURN
.
NOMAST   DISPLAY    *P1:24,*EL,*B,*V2LON:
                    "** Adm.no : ",AADMNO," has No Master File ",*W2:
                    *P1:24,*EL;
        RETURN
.
.              PRINT REPORT
.
REPORT   DISPLAY    *P1:24,*EL,*P10:24,*V2LON:
                    "** Generating Report **",*W2;
         DISPLAY    *P1:24,*EL,*P20:24,"Printing :";
.
         MOVE       SP3,SAVWARD
         MOVE       "60",CLNO
         MOVE       SP20,KEY16
         CALL       RDSTEMP
.
NXTTEMP  CALL       RDKTEMP
         BRANCH     OVRCD OF ENDREP
         DISPLAY    *P40:24,*V2LON,PNAME;
.
NXTT30   COMPARE    "55",CLNO
         CALL       HEAD IF NOT LESS
.
         UNPACK     ADATE,XCENT,XYEAR,XMON,XDAY 
.
         MOVE       NEWHR TO SUGHR
         MOVE       NEWPAT TO SUGPAT
.
         COMPARE    CURHR TO NEWHR
         GOTO       NXTT30A IF NOT EQUAL
         COMPARE    CURPAT TO NEWPAT
         GOTO       NXTT30A IF NOT EQUAL
.
         MOVE       SP7 TO SUGHR
         MOVE       SP7 TO SUGPAT
.
NXTT30A  MATCH      STATYPE TO OSUGCLAS
         GOTO       NXTT31 IF NOT EQUAL
.
         MOVE       SP3 TO OSUGCLAS
.
.           Get the Bed type
.
NXTT31   PACK       DIMHF WITH AFUNDH,SLASH,AFNDTB
.
         MATCH      SP6 TO AFUNDH
         GOTO       NXTT32 IF NOT EQUAL
.
         PACK       KEY5 WITH CODECL,ACLAIM
         CALL       RDCODE1
         BRANCH     OVRCD TO NXTT32
.
         MOVE       TDESC TO DIMHF
.
NXTT32   MOVE       SP8,DIM8
         PACK       KEY5 WITH CODEBT,STRBTYP
         CALL       RDCODE1
         BRANCH     OVRCD OF NOSTR
.
         MOVE       TDESC,DIM8
.
.           Get the admission type
.
NOSTR    MOVE       SP10,DIM10
         PACK       KEY5 WITH CODEA,STATYPE
         CALL       RDCODE1
         BRANCH     OVRCD OF NOATYP
.
         MOVE       TDESC,DIM10
.
NOATYP   PRINT      *1,"!",XWARD,SLASH,XBED,*9,"!",DIM8,*18,"!",AADMNO:
                    *27,"!",PNAME,*43,"!",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    *54,"!",DAYNUM,*59,"!",DIMHF,*71,"!":
                    DIM10,*82,"! ";
.
         MATCH      "00000000" TO ODATE
         GOTO       NOOPER IF EQUAL
.
         UNPACK     ODATE,XCENT,XYEAR,XMON,XDAY
         PRINT      XDAY,SLASH,XMON,SLASH,XCENT,XYEAR;
.
NOOPER   PRINT      *93,"!",CURHR,*101,"!",CURPAT,*109,"!",OSUGCLAS:
                    *113,"!",SUGHR,*121,"!",SUGPAT,*129,"!"
.
         ADD        ONE,CLNO
         GOTO       NXTTEMP
.
ENDREP   COMPARE   ZERO TO CPAGENO
         CALL      PRTLN IF NOT EQUAL
.
         PRINT     *N,*N,*1,"**** End Of Report ****";
.
         DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                   "** Report Generation Completed **",*W2;
         CALL       KILLIDZ
         GOTO      START
+
.                 HEADING
.
HEAD      COMPARE   ZERO TO CPAGENO
          CALL      PRTLN IF NOT EQUAL
.
          MOVE      ONE,CNOUNDLN
.
          BRANCH    OPTION TO HEADADM,HEADWRD
.
HEADADM   MOVE      "(ADMISSION SEQUENCE)",CPHDROPT
          GOTO      HEADOVR
.
HEADWRD   MOVE      "(WARD/BED SEQUENCE)",CPHDROPT
.
HEADOVR   CALL      HEAD132
.
          PRINT     *40,"PATIENTS AT MIDNIGHT - ":
                    XDAY2,SLASH,XMON2,SLASH,XCENT2,XYEAR2
          IF        IBCNMHOS = 1
            PRINT     *40,"Hospital : ",PTHSNAME
          ELSE
            PRINT     *N;
          ENDIF
.
          CALL      PRTLN
          PRINT     *1,"! Ward/",*9,"! Bed",*18,"!Admiss",*27,"!Patient ":
                    "Name",*43,"! Admiss",*54,"!Cur",*58,"!Health",*70,"!":
                    "Admission",*81,"!Operation",*93,"!Current":
                    *101,"!Current",*109,"!Sug",*113,"!Suggest",*121,"!":
                    "Suggest",*129,"!":
                    *N,*1,"!  Bed",*9,"! Type",*18,"!Number",*27,"!":
                    *43,"! Date",*54,"!Day",*58,"!Fund  / Tbl":
                    *70,"! Type",*81,"!   Date",*93,"! Health",*101,"!":
                    "Patient",*109,"!Cls",*113,"!Health",*121,"!Patient":
                    *129,"!"
          CALL      PRTLN
          MOVE      NINE,CLNO
          CLOCK     TIME,CTIMEIS
.
          RETURN
.
PRTLN     PRINT     *1,"*---------------------------------":
                    "----------------------------------------------":
                    "------------------------------------------------*"
          RETURN
.
.            Set up yesterday date
.
SETYDAY MOVE            CDD,DD
        MOVE            CMM,MM
        MOVE            CYY,YY
        MOVE            CCC,CC
        CALL            DMYCON
.
        MOVE            JULDAY,FORM4
        COMPARE         ONE,FORM4
        GOTO            SET10 IF NOT EQUAL
.
        COMPARE         "00",JULYR
        IF              @EQUAL                                                 
          MOVE            "99",JULYR
          SUB             ONE,JULCC
        ELSE
          SUB             ONE,JULYR
        ENDIF
        MOVE            ZERO,JULDAY
        ADD             OYEAR,JULDAY
        ADD             LEAPYR,JULDAY
.
SET10   SUB             ONE,JULDAY
        MOVE            JULDAY,JWKDAY
        MOVE            JULYR,JWKYER
        MOVE            JULCC,JWKCC
.
        CALL            JULCON
        PACK            YTDATE,CC,YY,MM,DD
        REP             " 0",YTDATE
        UNPACK          YTDATE,XCENT2,XYEAR2,XMON2,XDAY2
        RETURN
.
.
.       CHECK IF PREVIOUSLY EXISTS..IF SO DELETED IT 
.
INDZD   CALL        KILLIDZ
.
        LOAD        SORTSTR USING OPTION OF SORT1,SORT2
.
        PREP        PSTROL,FNAMER
        WRITE       PSTROL,SEQ;"isbuild ",FNAMEI," 95 ",SORTSTR
        WEOF        PSTROL,SEQ
.
        CLEAR       CMDLINE
        APPEND      "sh ",CMDLINE
        APPEND      FNAMER,CMDLINE
        APPEND      ".txt",CMDLINE
        RESET       CMDLINE
.
        DISPLAY    *P1:10,*EF,*P1:10
        EXECUTE     CMDLINE,TASKID
.
        CLOSE       PSTROL,DELETE
.
        TRAP        NOINDZ IF IO
        OPEN	    TEMPFILE,FNAMEI
        TRAPCLR     IO
ENDC    RETURN
.
NOINDZ  DISPLAY     *P1:24,*EL,*B,*V2LON:
                    "** Temporary Index File ":
                    FNAMEI," Not Found **",*W4;
        TRAPCLR     IO
        RETURN
+
.
.       Deleting an indexed file based on port
.
KILLIDZ CLOSE      TEMPFILE
.
        DISPLAY    *P1:24,*EL,*P20:24,*V2LON:
                   "** Deleting Indexed File **",*W;
.
        CLEAR      CMDLINE
        APPEND     "iserase ",CMDLINE
        APPEND     FNAMEI,CMDLINE
        RESET      CMDLINE
        EXECUTE    CMDLINE,TASKID
        DISPLAY    *P1:10,*EF,*P1:10
        RETURN
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:10,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TEN,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:10,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.
.
.           I/O FOR THE TEMP FILE
.
WRTEMP   RESET     KEY16
         MOVE      AADMNO,XAADMNO
         MOVE      UNIQUE,DUNIQUE
         WRITE     TEMPFILE,KEY16;XWARD,XBED,XAADMNO,DUNIQUE,PNAME:
                                  ADATE,DAYNUM,AFUNDH,AFNDTB,STATYPE,ODATE:
                                  CURHR,CURPAT,OSUGCLAS,NEWHR,NEWPAT,STRBTYP:
                                  ACLAIM
         RETURN
.
RDKTEMP
         MOVE      ZERO,OVRCD
         READKS    TEMPFILE;XWARD,XBED,XAADMNO,DUNIQUE,PNAME:
                            ADATE,DAYNUM,AFUNDH,AFNDTB,STATYPE,ODATE:
                            CURHR,CURPAT,OSUGCLAS,NEWHR,NEWPAT,STRBTYP:
                            ACLAIM
         GOTO      OVERCOND IF OVER
         MOVE      XAADMNO,AADMNO
         RETURN
.
RDSTEMP
         MOVE      ZERO,OVRCD
         RESET     KEY16
         READ      TEMPFILE,KEY16;;
         GOTO      OVERCOND IF OVER
         RETURN
.
         INC       NHOSPDAY
         INC       PATHSPKY
         INC       PATITMRD
         INC       GETCNEFF
         INC       VALCONTR
         INC       TFILENAM
.
         INC       PATHSPIO/INC
         INC       PATBFEIO/INC
         INC       PATCODIO/INC
         INC       PATCFAIO/INC
         INC       PMSCIDIO/INC
         INC       PATDTRIO/INC
         INC       PATITMIO/INC
         INC       PATMA1IO/INC
         INC       PATMI1IO/INC
         INC       PATTRNIO/INC
         INC       PATFN1IO/INC
         INC       PATFX1IO/INC
         INC       PATWR1IO/INC
         INC       PATSGCIO/INC
         INC       PMSMTIIO/INC
         INC       PMSVX1IO/INC
         INC       IBASEQIO/INC 
         INC       WEBERRIO/INC
.
         INCLUDE    STD001IO
.
ENDIT    CHAIN      PGM
         STOP

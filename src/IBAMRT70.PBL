.*****************************************************************************
.*    Program      : IBAMRT70                                                *
.*    Description  : Medical Records - Current Location Report               *
.*                                                                           *
.*    Author       : ROD                                                     *
.*    Date         : 13/08/96                                                *
.*    Function     : This program is used to report on the current location  *
.*                   of medical records of selected U/R numbers              *
.*    Modifications:                                                         *
.*        V10.06.01 12/03/2015  Steve Armstrong  CAR 314108                  *
.*                  Removed definition of PTCGDATE as PATCGPFD is no longer  *
.*                  in use.                                                  *
.*****************************************************************************
.*        V10.04.01 14/07/2014  Ebon Clements    CAR 261630                  *
.*                  Removed port number from temp file name                  *
.*        V10.03.01 26/07/2013  Steve Armstrong  CAR 287787                  *
.*                  Recompiled for changes to DGCLICUA.                      *
.*****************************************************************************
.*        V10.02.02 04/07/2011  Ebon Clements  CAR 240724                    *
.*                  Mods for MRT hospital/location - Added hospital to       *
.*                  location key                                             *
.*        V10.02.01 25/06/2011 Steve Armstrong    CAR 240722                 *
.*                  Recompiled for changes to PATLOCFD.                      *
.*                  Recompiled for changes to PMSCURFD.                      *
.*****************************************************************************
.*        V10.00.01 26/05/2010  Ebon Clements CAR 222304                     *
.*                  Fixed read of PMI details                                *
.*****************************************************************************
.*        V9.09.01  27/08/2007  Peter Vela   CAR 147747                      *
.*                  Added variables for PATCOMN2 Cabrini Health UR format    *
.*****************************************************************************
.*        V9.08.01  25/01/2007  Peter Vela CAR 127930                        *
.*                  Recompiled for MRTMASFD                                  *
.*****************************************************************************
.*        V9.03.00  23/07/2004  Pat Dirito       CAR 52035                   *
.*                  Ported from V6.12                                        *
.*****************************************************************************
.
          INC       STD001FD
          INC       PATMA1FD/INC
          INC       HL7BMTFD/INC
          INC       MRTLOCFD/INC
          INC       MRTMASFD/INC
          INC       MRTHISFD/INC
.
          INC       BOKRC1FD/INC            * booking file
          INC       IBASEQFD/INC
          INC       OUTPREFD/INC            * OP pre-attendance file
          INC       PATCOMM/INC             * variables for surname search
          INC       PATCONFD/INC            * control file
          INC       PATMI1FD/INC            * admission file
          INC       PATPR1FD/INC            * pre-admission file
          INC       PATGSRFD/INC            * surname file
          INC       NZHISIFD/INC
          INC       PMSVX1FD/INC
          INC       PATINVFD/INC
          INC       PATVISFD/INC
          INC       PATLOCFD/INC
          INC       PMSCURFD/INC
          INC       PMSPX2FD/INC
          INC       PATURAFD/INC
          INC       PATNIDFD/INC
          INC       TFILEVAR/INC
          INC       IBACONFD/INC
          INC       NHIMASFD/INC
          INC       PATCALAG/INC
          INC       PATALRFD/INC
          INC       WEBSECFD/INC
          INC       WEBERRFD/INC
.
ACTION    DIM       1
BJDAY     FORM      3
CJDAY     FORM      3
COUNT     FORM      2
NMPNUMB   DIM       20
PSAGETYP  DIM       1
PSAGECON  DIM       3
.
HLBMDGUP  FORM      1         1       Write Update PMI details to Datagate 1=Yes
HLBMDGUA  FORM      1         2       Write Update Alias details to Datagate
HLBMDGMU  FORM      1         3       Write Merge U/R details to Datagate
HLBMDGUU  FORM      1         4       Write UnMerge U/R details to Datagate
HLBMDGAD  FORM      1       105       Write Admission details to Datagate
HLBMDGDS  FORM      1       106       Write Discharge details to Datagate
HLBMDGTR  FORM      1       107       Write Transfer details to Datagate
HLBMDGRG  FORM      1       108       Write New Pt. Registration to Datagate
.
CALLPOSN  FORM      1
CMDLINE   DIM       70
CNTADMN   FORM      3
DIM4      DIM       4
DIM7      DIM       7
DIM8      DIM       8
FNAME1    DIM       8
FNAME2    DIM       8
FORM3     FORM      3
FORM6     FORM      6
FORM8     FORM      8
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
LOCDESC   DIM       17
RECORDID  DIM       15
SCNDGNAM  DIM       40
TMPLOC    DIM       4
TMPHHOS   DIM       3
TMPTYP    DIM       3
TMPVOL    DIM       2
URNUMB    DIM       8[19]
URCHKDGT  FORM      4
URWTARAY  INIT      "234567"
URNLNGTH  FORM      2
URCHKPRD  FORM      2
URNOWGHT  FORM      1
USERID    DIM       10
ZED20     INIT      "zzzzzzzzzzzzzzzzzzzz"
.
TMPFILA1  IFILE     SQL, FIXED=51, KEY="U1-7,8-15,16-28"
XCURLOC   DIM       7    1    * current location
XURNO     DIM       8    8    * u/r number
XRECORD   DIM      13   16    * Medical Record
XPATNAME  DIM      22   29    * patient name
.   End of record       51
.
TMPFILA2  IFILE     SQL, FIXED=49, KEY="U1-8"
.XURNO    DIM       8    1    * u/r number
XNAME40   DIM       40   9    * patient name
.   End of record       49
.
PRGNAM    INIT      "Med. Record Current Location Report"
PRGID     INIT      "IBAMRT70"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*  ML0000  :  Mainline                                                   *
.**************************************************************************
ML0000    CALL      INIT0000                      * initialisation
. 
ML1000    CALL      KYUR0000                      * keyin U/R list
          BRANCH    EXIT,ML9999                   * cancel selected
.
          CALL      CURL0000                      * load tmp file with cur locs
.
          CALL      RPRT0000                      * print the report
.
ML9999    CALL      KILL0000                      * delete temp files
          CHAIN     PGM
+
.**************************************************************************
.*  INIT0000  :  Initialisation                                           *
.**************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"          * files to open
          READ      CONTROLF,EIGHTY;*1,HLBMDGUP,HLBMDGUA,HLBMDGMU:
                                       HLBMDGUU:
                                  *105,HLBMDGAD,HLBMDGDS,HLBMDGTR:
                                       HLBMDGRG
.
          DISPLAY   *P58:24,"mrtlocaf"
          OPEN      MRTLOCA1,"mrtlocaf"
.
          DISPLAY   *P58:24,"mrtmasaf"
          OPEN      MRTMASA1,"mrtmasaf"
.
          DISPLAY   *P58:24,"mrthisaf"
          OPEN      MRTHISA1,"mrthisaf"
.
          DISPLAY   *P58:24,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
.
          DISPLAY   *P58:24,"outpreaf"
          OPEN      OUTPREA1,"outpreaf"
.
          DISPLAY   *P64:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P58:24,"patpr1af"
          OPEN      PATPR1A1,"patpr1af"
.
          DISPLAY   *P58:24,"patgsrnf"
          OPEN      PATGSRN1,"patgsrnf"     * surname only
          OPEN      PATGSRN2,"patgsrnf"
.
          DISPLAY   *P58:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAME1
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAME2
.
          READ      CONTROLF,FIFTY9;*231,PTCNRGNS
          DISPLAY   *P64:24,"patgsrnf";
          OPEN      PATGSRN1,"patgsrnf"     * surname & given name
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
INIT9999  RETURN
+
.*********************************************************************
.         enter batch of admission numbers
.*********************************************************************
KYUR0000  CALL      CRET0000                * create temp files
.
          DISPLAY   *P1:3,*EF,*V2LON,*ULON:
                    *P1:4,"Cnt",*P6:4,"U/R No. ":
                    *P16:4,"Name                                    "
.
          MOVE      ONE,CNTADMN             * clear count
          MOVE      FIVE,CVERT
.
KYUR1000  CALL      KEYU0000                * enter visit numbers
.
KYUR2000  CALL      RTPA0000
          BRANCH    EXIT,KYUR9999,KYUR3000
.                        Cancel   Add
          GOTO      KYUR9999
.
KYUR3000  MOVE      ONE,CNTADMN             * set for a new page
          MOVE      "23",CVERT
          GOTO      KYUR1000
.
KYUR9999  RETURN
+
.*********************************************************************
.         enter the U/R number
.*********************************************************************
KEYU0000  COMPARE   "23",CVERT
          GOTO      KEYU0100 IF LESS        * continue 
.
          MOVE      FIVE,CVERT
          DISPLAY   *P1:CVERT,*EF;
.
KEYU0100  DISPLAY   *P1:CVERT,*V2LON,CNTADMN,*HOFF,"."
.
          MOVE      SIX,CCOL                 * column for keyin
          MOVE      TWO,SRCHOPT              * display address in surn. search
          MOVE      ZERO,SRCHSYS             * display all 0 U/R numbers
KEYU1000  CALL      KURN                     * keyin the U/R number
          BRANCH    EXIT,KEYU9999            * nothing input
          BRANCH    OVRCD,KEYU9200           * invalid U/R number
.
          PACK      XURNO,PURNO
.
          MOVE      XURNO,KEY8
          READ      TMPFILA2,KEY8;ANS
          GOTO      KEYU9300 IF NOT OVER    * number exists
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,XNAME40        * shorten name
.
          DISPLAY   *P16:CVERT,XNAME40
          WRITE     TMPFILA2,KEY8;KEY8,XNAME40
          ADD       ONE,CVERT
          ADD       ONE,CNTADMN
          GOTO      KEYU0000
.
KEYU9200  DISPLAY   *P1:24,*B,"Invalid U/R Number.  ",*EL;
          CALL      HITENTER
          GOTO      KEYU1000
KEYU9300  DISPLAY   *P1:24,*B,"U/R Number already entered.  ",*EL;
          CALL      HITENTER
          GOTO      KEYU1000
.
KEYU9999  RETURN
+
.*********************************************************************
.         redisplay  U/R  numbers entered from temp file
.*********************************************************************
RTPA0000  DISPLAY   *P1:5,*EF;
          MOVE      ZERO,CNTADMN
          MOVE      FOUR,CVERT
          MOVE      ONE,CPAGENO
          MOVE      SP8,KEY8
          READ      TMPFILA2,KEY8;;
. 
RTPA1000  READKS    TMPFILA2;XURNO,XNAME40
          GOTO      RTPA5000 IF OVER
.
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      RTPA4000 IF NOT LESS    * need a new page
          ADD       ONE,CNTADMN
.
RTPA3000  DISPLAY   *P1:CVERT,*V2LON,CNTADMN,*HOFF,". ":
                    XURNO,SP2,XNAME40
          MOVE      XURNO,URNUMB[CNTADMN]
          GOTO      RTPA1000
.
. ******* need a new page *******
.
RTPA4000  BRANCH    CPAGENO,RTPA4500        * on first page
RTPA4100  MOVE      ONE,CALLPOSN            * Next & Prev
          CALL      KEYA0000
          BRANCH    EXIT,RTPA9999,RTPA7000,RTPA0000,RTPA6000,RTPA9200
.                        Cancel   Next     First    Delete   Add
          GOTO      RTPA9999               * Post
.
RTPA4500  MOVE      TWO,CALLPOSN            * Next
          CALL      KEYA0000
          BRANCH    EXIT,RTPA9999,RTPA7000,RTPA5900,RTPA6000,RTPA9200
.                        Cancel   Next     First    Delete   Add
          GOTO      RTPA9999               * Post
.
. ******* no more data to display *******
.
RTPA5000  BRANCH    CPAGENO,RTPA5500        * on first page
RTPA5100  MOVE      THREE,CALLPOSN          * Prev
          CALL      KEYA0000
          BRANCH    EXIT,RTPA9999,RTPA5900,RTPA0000,RTPA6000,RTPA9200
.                        Cancel   Next     First    Delete   Add
          GOTO      RTPA9999               * Post
.
RTPA5500  MOVE      FOUR,CALLPOSN           * no paging
          CALL      KEYA0000
          BRANCH    EXIT,RTPA9999,RTPA5900,RTPA5900,RTPA6000,RTPA9200
.                        Cancel   Next     First    Delete   Add
          GOTO      RTPA9999               * Post
.
RTPA5900  BEEP
RTPA5910  BRANCH    CALLPOSN,RTPA4100,RTPA4500,RTPA5100,RTPA5500
.
. ******* delete an item *******
.
RTPA6000  DISPLAY   *P1:24,*EL,"Enter the item to delete : ",*EL;
          KEYIN     *P28:24,*DV,UNDLN2:
                    *P28:24,*V2LON,FORM2:
                    *P28:24,*DV,FORM2
          COMPARE   ZERO,FORM2
          GOTO      RTPA5910 IF EQUAL       * exit
          GOTO      RTPA6100 IF LESS        * negative
.
          COMPARE   TEN9,FORM2
          GOTO      RTPA6100 IF NOT LESS    * too high
.
          COMPARE   FORM2,CNTADMN
          GOTO      RTPA6100 IF LESS
.
          MOVE      URNUMB[FORM2],KEY8
          READ      TMPFILA2,KEY8;KEY8
          GOTO      RTPA6100 IF OVER        * invalid
.
          DELETE    TMPFILA2,KEY8;
          GOTO      RTPA0000
.
RTPA6100  BEEP
          GOTO      RTPA6000
.
. ******* set up for next screen *******
.
RTPA7000  CALL      CLRA0000
          ADD       ONE,CPAGENO
          MOVE      ONE,CNTADMN
          MOVE      FIVE,CVERT
          DISPLAY   *P1:CVERT,*EF;
          GOTO      RTPA3000
.
RTPA9200  MOVE      TWO,EXIT                * add
.
RTPA9999  RETURN
+
.*********************************************************************
.         keyin for line 24 for admission number input
.*********************************************************************
KEYA0000  DISPLAY   *P1:24,*EL,"(":
                    *V2LON,ANSP,*HOFF,")ost, (":
                    *V2LON,ANSC,*HOFF,")ancel, (":
                    *V2LON,ANSD,*HOFF,")elete, (":
                    *V2LON,ANSA,*HOFF,")dd";
          MOVE      "33",CCOL
.
          BRANCH    CALLPOSN,KEYA0100,KEYA0200,KEYA0300,KEYA0400
KEYA0100  DISPLAY   ", (",*V2LON,ANSN,*HOFF,")ext, (":
                    *V2LON,ANSF,*HOFF,")irst";
          ADD       "17",CCOL
          GOTO      KEYA0400
.
KEYA0200  DISPLAY   ", (",*V2LON,ANSN,*HOFF,")ext";
          ADD       EIGHT,CCOL
          GOTO      KEYA0400
.
KEYA0300  DISPLAY   ", (",*V2LON,ANSF,*HOFF,")irst";
          ADD       NINE,CCOL
.
KEYA0400  DISPLAY   " ? ";
          ADD       FOUR,CCOL
.
KEYA1000  KEYIN     *PCCOL:24,*DV,UNDLN2:
                    *PCCOL:24,*V2LON,*JR,DIM2:
                    *PCCOL:24,*DV,DIM2
          TYPE      DIM2
          GOTO      KEYA1500 IF EQUAL       * number entered
.
          UNPACK    DIM2,ANS,ANS
          REP       UPPLOW,ANS
          MOVE      ZERO,EXIT
          MATCH     ANSP,ANS
          GOTO      KEYA9999 IF EQUAL       * post
          REP       "C1N2F3D4A5",ANS
          MOVE      ANS,EXIT                * set exit flag
          TYPE      ANS
          GOTO      KEYA9999 IF EQUAL
.
KEYA1500  BEEP
          GOTO      KEYA1000
.
KEYA9999  RETURN
+
.*********************************************************************
.         clear the storage array
.*********************************************************************
CLRA0000  MOVE      ZERO,FORM2
CLRA1000  ADD       ONE,FORM2
          COMPARE   TEN9,FORM2
          GOTO      CLRA9999 IF NOT LESS
          MOVE      ZEROUR,URNUMB[FORM2]
          GOTO      CLRA1000
CLRA9999  RETURN
+
.**************************************************************************
.*  CURL0000  :  get current location of all volumes for a U/R no.        *
.**************************************************************************
CURL0000  PACK      KEY8,SP8
          CALL      RSTEMP2
CURL0500  CALL      RKTEMP2
          BRANCH    OVRCD,CURL9999
.
          PACK      KEY20,XURNO,SP20
          CALL      RSMRMAS1
CURL1000  CALL      RKMRMAS1
          BRANCH    OVRCD,CURL0500
.
          MATCH     XURNO,MRMAURNO                  * still same U/R number?
          GOTO      CURL0500 IF NOT EQUAL
.
          PACK      KEY26,MRMAKEY,ZED20
          CALL      RSMRHIS1
CURL2000  CALL      RPMRHIS1
          BRANCH    OVRCD,CURL1000
.
          MATCH     MRMAKEY,MRHIKEY                 * same medical record?
          GOTO      CURL1000 IF NOT EQUAL
.
. we have the current location for this volume so write it to temp file
.
          PACK      XCURLOC,MRHIDHOS,MRHILOC,SP70   * current location
          PACK      XRECORD,MRMAHHOS,MRMAHLOC,MRMADOTY,MRMAVOLN
.
          PACK      KEY8,XURNO,SP70
          CALL      RDMAST1
.
          MOVE      PGNAME,XPATNAME
          STRIP     XPATNAME
          ENDSET    XPATNAME
          APPEND    " ",XPATNAME
          APPEND    PSNAME,XPATNAME
          RESET     XPATNAME
.
          PACK      KEY28,XCURLOC,XURNO,XRECORD
          CALL      WRTEMP1
          GOTO      CURL1000
.
CURL9999  RETURN
+
.**************************************************************************
.*  RPRT0000  :  print report from temp file                              *
.**************************************************************************
RPRT0000  CALL      IBACLOCK
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      TEN,CLNO                      * line counter
.
          CALL      HEAD80
          CALL      THED0000                      * print table headings
.
          MOVE      "zzzzzzz",DIM7                * init to dummy string
          PACK      KEY28,SP30
          CALL      RSTEMP1 
RPRT0050  CALL      RKTEMP1                       * get next record
          BRANCH    OVRCD,RPRT9000                * no more records
.
. check if we need a new page
.
          COMPARE   "55",CLNO
          GOTO      RPRT1000 IF LESS
.
. we need a new page
.
          CALL      UND80
          CALL      HEAD80
          CALL      THED0000                      * print table headings
          MOVE      "zzzzzzz",DIM7                * init to dummy string
.
RPRT1000  CALL      GDES0000                      * format data for print
.
. check if we need to print a new location
.
          PRINT     *1,"|";
.
          MATCH     XCURLOC,DIM7
          GOTO      RPRT2000 IF EQUAL
.
. print a new current location
.
          CALL      UND80                         * print underlines
          PRINT     *1,"|",*3,XCURLOC,SP1,LOCDESC;
.
. print rest of line
.
RPRT2000  PRINT     *29,"| ",XURNO,*40,"| ",XPATNAME,*64,"|",RECORDID,*80,"|"
.
          ADD       ONE,CLNO                      * increment line counter
          MOVE      XCURLOC,DIM7                  * save location just printed
          GOTO      RPRT0050
.
RPRT9000  CALL      UND80                         * print underlines
          PRINT     *N,*1,"*** End of report ***" 
.
RPRT9999  RETURN
+
.**************************************************************************
.*  GDES0000  :  get descriptions for printing                            *
.**************************************************************************
GDES0000  MOVE      "<Missing code>",LOCDESC
          PACK      KEY7,XCURLOC
          CALL      RDMRLOC1
          MOVE      MRLODESC,LOCDESC
.
          UNPACK    XRECORD,TMPHHOS,TMPLOC,TMPTYP,TMPVOL
          PACK      RECORDID,TMPHHOS,TMPLOC,TMPTYP,TMPVOL
.
GDES9999  RETURN 
+
.**************************************************************************
.*  THED0000  :  print table headings                                     *
.**************************************************************************
THED0000  PRINT     *1
          CALL      UND80
          PRINT     *1,"| Current Location",*29,"| U/R No.",*40,"| ":
                    "Patient Name",*64,"| Record Id",*80,"|"
.
THED9999  RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temp files                                        *
.*************************************************************************
KILL0000  CLOSE     TMPFILA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     TMPFILA2
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME2,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.*************************************************************************
.*  CRET0000  :  create temporary files                                  *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME1,CMDLINE
          APPEND    " 51 U1-7,8-15,16-28",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TMPFILA1,FNAME1
.
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME2,CMDLINE
          APPEND    " 49 U1-8",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TMPFILA2,FNAME2
.
CRET9999  RETURN
+
.*************************************************************************
.*  Temp I/O's                                                           *
.*************************************************************************
RSTEMP1   RESET     KEY28
          READ      TMPFILA1,KEY28;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY28
          READ      TMPFILA1,KEY28;XCURLOC,XURNO,XRECORD,XPATNAME
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TMPFILA1;XCURLOC,XURNO,XRECORD,XPATNAME
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMP1   MOVE      ZERO,OVRCD
          READKP    TMPFILA1;XCURLOC,XURNO,XRECORD,XPATNAME
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   UPDATE    TMPFILA1;XCURLOC,XURNO,XRECORD,XPATNAME
          RETURN
.
WRTEMP1   RESET     KEY28
          WRITE     TMPFILA1,KEY28;XCURLOC,XURNO,XRECORD,XPATNAME
          RETURN
.
DETEMP1   RESET     KEY28
          DELETE    TMPFILA1,KEY28
          RETURN
.
. 2nd temp file IO's
.
RSTEMP2   RESET     KEY8
          READ      TMPFILA2,KEY8;;
          RETURN
.
RKTEMP2   MOVE      ZERO,OVRCD
          READKS    TMPFILA2;XURNO,XNAME40
          GOTO      OVERCOND IF OVER
          RETURN
.
.
REDISPS 
GETSVAR   RETURN
+
          INC       STD001IO
          INC       PATMA1IO/INC
          INC       HL7BMTIO/INC
          INC       MRTLOCIO/INC
          INC       MRTMASIO/INC
          INC       MRTHISIO/INC
          INC       PMSVX1IO/INC
          INC       BOKRC1IO/INC            * booking file
          INC       OUTPREIO/INC            * OP pre-attendance file
          INC       IBASEQIO/INC
          INC       PATMI1IO/INC            * admission file
          INC       PATPR1IO/INC            * pre-admission file
          INC       PATGSRIO/INC            * surname file
          INC       PATINVIO/INC
          INC       PATVISIO/INC
          INC       PATLOCIO/INC
          INC       PMSCURIO/INC
          INC       PMSPX2IO/INC
          INC       PATURAIO/INC
          INC       PATNIDIO/INC
          INC       PATALRIO/INC
          INC       NHIMASIO/INC
          INC       WEBERRIO/INC
.
          INC       NZIBSRCH
          INC       PATCOMN1
          INC       PATCOMN2
          INC       TFILENAM
          INC       NZHISIIO
          INC       CLPATMAS
          INC       CALCAGE

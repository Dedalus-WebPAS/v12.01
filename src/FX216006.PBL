.*****************************************************************************
.* System    :   Allied Health                                               *
.* Program   :   FX216006                                                    *
.* Desc      :   Fixit for Main Health Condition                             *
.*****************************************************************************
.* Date      :   02/10/2010                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will loop through the allied health referral   *
.*               table and for any record where there is no corresponding    *
.*               allrhlaf records (of type "01"), then if the main health    *
.*               diagnosis 1 field (allrefaf.alredia1) is not blank, it      *
.*               will use this code to populate a record into allrhlaf.      *
.* Mods      :                                                               *
.*        V9.12.00  Steve Armstrong    CAR 216006                            *
.*                  Program created                                          *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLCONFD/INC
          INC       ALLREFFD/INC
          INC       ALLRHLFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      12
.
.
PRGID     INIT      "FX216006"
PRGNAM    INIT      "Fixit for Main Health Condition"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"allrefaf";
          OPEN      ALLREFA1,"allrefaf"
.
          DISPLAY   *P64:24,"allrhlaf";
          OPEN      ALLRHLA1,"allrhlaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*        Loop through all referrals and if there are no health              *
.*        conditions on file in allrhlaf for the referral, create one        *
.*        using allrefaf.alredia1.                                           *
.*****************************************************************************
.
PROC0000  CALL      IBACLOCK                     * get current date/time
          MOVE      ZERO,COUNTER                 * initialise counter
.
.         Loop through and check each referral on file
.
          MOVE      SP8,KEY8
          CALL      RSALREF1                     * position at start of file
PROC0500  CALL      RKALREF1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
.         If there is no diagnosis (main health condition), then
.         ignore this referral
.
          MATCH     SP9,ALREDIA1                 * blank diagnosis ?
          GOTO      PROC0500 IF EQUAL            * yes - get next referral
.
.         This referral has a diagnosis, so check if this referral has
.         any health conditions on file (in allrhlaf).
.
          PACK      KEY13,ALREVISN,ZERO,ONE,SP20
          CALL      RSALRHL1                     * position for visit in file
          CALL      RKALRHL1                     * read next record
          BRANCH    OVRCD,PROC1000               * eof - create allrhlaf record
.
          MATCH     ALREVISN,ALRHVISN            * same visit still ?
          GOTO      PROC1000 IF NOT EQUAL        * no - create allrhlaf record
.
          MATCH     "01",ALRHTYPE                * same type still ?
          GOTO      PROC0500 IF EQUAL            * yes - ignore referral
.
.         There are no health conditions for this referral, so write
.         a new record using allrefaf.alredia1 (main health condition)
.
PROC1000  ADD       ONE,COUNTER                  * increment counter
.
          MOVE      ALREVISN,ALRHVISN
          PACK      ALRHTYPE,ZERO,ONE
          PACK      ALRHUNIQ,SP1,SP1,ONE
          MOVE      ALREDIA1,ALRHCODE
          PACK      ALRHDATE,CCC,CYY,CMM,CDD
          REP       " 0",ALRHDATE
          MOVE      ALRHDATE,ALRHCDAT
          MOVE      CTIMEIS,ALRHCTIM
          PACK      ALRHCUID,PRGID,SP10
          PACK      ALRHSPAR,SP30,SP20
          CALL      WRALRHL1
.
          GOTO      PROC0500                     * get next referral
.
PROC9000  DISPLAY   *P1:24,*EL,"Fixit completed.  ",*V2LON,COUNTER,*HOFF:
                    " records created.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       ALLREFIO/INC
          INC       ALLRHLIO/INC

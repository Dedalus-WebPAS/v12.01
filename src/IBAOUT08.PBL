.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAOUT08.PBL                                              *
.* Name           : Input Non-Attendees                                       *
.******************************************************************************
.* Date           : 28/07/1988                                                *
.* Author         : Eric Phane                                                *
.* Modifications  :                                                           *
.******************************************************************************
.*        V11.02.01 09/02/2022  Thanh T       TSK 0905641                     *
.*                  Recompiled as OUTMA1FD changes                            *
.*        V10.07.01 30/10/2015  Ebon Clements  CAR 268970                     *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*        V9.03.01  16/10/2003  Sandra Barcham      43921                     *
.*                  Missed V5.10.02 change for srf 22215                      *
.*                  Deleted link in eoclnkaf when patient is non-attended     *
.*        V9.02.01  31/01/2003  Sylvek Litewka  SRF 22709                     *
.*                  Modified program to open outpatient audit files with site *
.*                  prefix.
.******************************************************************************
.*        V5.10.01  30/08/2002 Raghunandan Surve,HPS  SRF18814                *
.*                  Re-compiled for PATGPCDS                                  *
.******************************************************************************
.*        V5.09.B01 01/02/2001  Greg Horvat                                   *
.*                  Removed any reference to SOBAOUTN                         *
.*        V5.08.02  19/09/2000  Steve Armstrong   srf 647263                  *
.*                  Recompiled for changes to OUTDSCLN                        *
.*        V5.08.01  16/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01  13/03/2000   Steve Armstrong                             *
.*                   Mods for changes to OUTCLIFD (effective date)            *
.******************************************************************************
.*        V5.07.01   30/03/1999   Steve Armstrong                             *
.*                   Fixed read on audit parameters to occur before branch    *
.*                   on audit paramater                                       *
.******************************************************************************
.*        V5.03.01   20/02/1996   Justin Coates  (SUHT mods)                  *
.*                   dont allow the date to be in the future                  *
.******************************************************************************
.*        V5.01.124 24.03.93 Neeriem "I Hate Support" Dye                     *
.*                  Fix to write site code to OTBBSITE                        *
.*        V5.01.125 24/05/1995  Matt Surridge                                 *
.*                  Fixed bugs for global recompile.                          *
.*        V5.02.01  24/03/1995    Paul howells   SRF135690 & 135691           *
.*                  Recompile for PATGPPKY & PATGPCKY.                        *
.*        V5.02.02  08/05/1995    ROD            SRF 135249                   *
.*                  Allow write to outuseaf even if no Bookings               *
.******************************************************************************
.
          INC       STD001FD
          INC       EOCLNKFD/INC
          INC       OUTAUXFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTCLIFD/INC
          INC       OUTCONFD/INC
          INC       OUTMA1FD/INC
          INC       OUTMABFD/INC
          INC       OUTSITFD/INC
          INC       OUTUSEFD/INC
          INC       PATAM1FD/INC
          INC       PATCALAG/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDHEAD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       PATPR1FD/INC
.
AUDIFLAG  FORM      1        * used in audits
CODE      DIM       2
CODECA    INIT      "CA"     * Attendance Code (Booking)
CODECD    INIT      "CD"     * Delay Code (Used Sessions)
CODECL    INIT      "CL"     * Claim Type
CKACTIVE  FORM      1
CKLINKED  FORM      1
.
DATTEND   INIT      "Attended"
DBOOKED   INIT      "Booked  "
DDAY      DIM       9
DESCATTN  DIM       13
DESCCOMP  DIM       13
DESCSTAT  DIM       8
DEXTEND   INIT      "Ext.Slot"
DIM20     DIM       20
DIM3      DIM       3
DIM40     DIM       40
DNOATTN   INIT      "Not Attd"
DNOBOOK   INIT      "N/Booked"
DOCCODE   DIM       6
DUNUSED   INIT      "Not Used"
DVERT     FORM      2    * Date
.
GOTUSERC  FORM      1           * 1 = used session file record exists
.
IVERT     FORM      2    * Clinic Id
.
.
PATFND    FORM      1
.
SLOTOPT   DIM       3
.
TODAY     DIM       8
TMVERT    FORM      2    * Time
.
VACEND    DIM       5
VACST     DIM       5
VCLIN     DIM       6
VDATE     DIM       8
VDAY      FORM      1
VDLY      DIM       3
VSITE     DIM       6
VSLOT     FORM      3
VSTRT     DIM       5
.
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGNAM    INIT      "Input Non-Attendees"
PRGID     INIT      "IBAOUT08"
VERSION   INIT      "V12.01.00"
+
.       START OF PROGRAM
.
          MOVE      IDDD,OBASITE
          MOVE      IDDD,OCASITE
          MOVE      IDDD,OMASITE
          MOVE      IDDD,OUSSITE
          MOVE      IDDD,VSITE
          MOVE      UID,OSTFILE
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening";
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,THIRTY1;*51,HOAUDA,HOAUDB,*64,HOAUDN
.                                           *  SRF 22215 Open CHOSPNUM
          READ      CONTROLF,TEN;*244,CHOSPNUM
.
          PACK      CFNAME,UID,FILBOKA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA1,CFNAME
          BRANCH    HOAUDA,INIT0010
          PACK      CFNAME,UID,FILAUDBA
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTAUDBA,CFNAME
.
INIT0010  BRANCH    HOAUDB,INIT0020
          PACK      CFNAME,UID,FILAUBB1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTAUBB1,CFNAME
.
INIT0020  PACK      CFNAME,UID,FILBOKA2
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA2,CFNAME
.
          PACK      CFNAME,UID,FILBOKA3
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA3,CFNAME
.
          PACK      CFNAME,UID,FILBOKA4
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA4,CFNAME
.
          PACK      CFNAME,UID,FILBB1A1
          DISPLAY   *P64:24,CFNAME;
          CALL       OPOTBB11
.
          PACK      CFNAME,UID,FILCLIA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME,UID,FILMA1A1
          DISPLAY   *P64:24,CFNAME;
          CALL       OPOTMA11
.
          PACK      CFNAME,UID,FILMA1A2
          DISPLAY   *P64:24,CFNAME;
          CALL       OPOTMA12
.
          PACK      CFNAME,UID,FILMASB1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTMASB1,CFNAME
.
          PACK      CFNAME,UID,FILUSEA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTUSEA1,CFNAME
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.                                           *  SRF 22215
          DISPLAY   *P64:24,"eoclnkaf";
          OPEN      EOCLNKA1,"eoclnkaf"
          OPEN      EOCLNKA2,"eoclnkaf"
.                                           *  SRF 22215
          MOVE      SP1,AUMODE
          MOVE      ANSN,AUTYPE                  * Non-Attendees *
          CALL      STRTAUDX
.
MENU      HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Input Non-Attendees"
.
BEGIN     KEYIN     *P1:7,*EL,"Please Select : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
RELPORT   PACK      KEY28,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT
          CALL      ULBOKA1
.
REPEAT    BRANCH    OPTION OF DISPSCR
          BEEP
          GOTO      BEGIN
+
DISPSCR   DISPLAY   *P1:3,*EF;
          CALL      DSPGEN
.
.------------------------------------------------------------------------------
.       ** INPUT CLINIC ID **
.------------------------------------------------------------------------------
.
KBCLIN    MOVE      SP6,VCLIN
          KEYIN     *P20:IVERT,*EL,*DV,UNDLN6:
                    *P20:IVERT,*V2LON,VCLIN,*P20:IVERT,*DV,VCLIN
.
          RESET     VCLIN
          GOTO      MENU IF EOS
.
          ENDSET    VCLIN
          APPEND    SP6,VCLIN
          RESET     VCLIN
.
          MATCH     SP6,VCLIN
          GOTO      MENU IF EQUAL
.
CLINCNT1  CMATCH    QUEST,VCLIN
          GOTO      VALCLIN IF NOT EQUAL
.
          MOVE      SP6,VCLIN
          MOVE      SP30,OCADESC
          MOVE      SP10,OCADOCT
.
          CALL      OUTDSCLN
          DISPLAY   *P1:3,*EF
          CALL      DSPGEN
          CALL      DATGEN
          GOTO      DISPSCR
.
VALCLIN   CMATCH    EXITCHAR,VCLIN
          GOTO      RELPORT IF EQUAL           ** RELEASE PORT LOCK **
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,VSITE,VCLIN,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     VSITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     VCLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          BRANCH    OVRCD,INVCLIN
.
          MATCH     SP6,OCADOCT
          GOTO      DSPBDAT IF EQUAL
.
          MOVE      OCADOCT,KEY6
          CALL      RDDOCT1
          COMPARE   OVRCD,ONE
          GOTO      DSPBDOC IF NOT EQUAL
.
          MOVE      SP30,OCADESC
          GOTO      DSPBDAT
.
DSPBDOC   PACK      OCADESC,DSNAME,COMMA,DGNAME,SP30
.
DSPBDAT   DISPLAY   *P35:IVERT,*V2LON,OCADESC
.
          GOTO      KBDATE
.
INVCLIN   DISPLAY   *P45:IVERT,*B,*V2LON:
                    "** Invalid Clinic Code ** ",*W;
          GOTO      KBCLIN
.
.------------------------------------------------------------------------------
.      ** INPUT SESSION DATE **
.------------------------------------------------------------------------------
.
KBDATE    PACK      VDATE,SP8
          MOVE      DVERT,CVERT
          MOVE      "20",CCOL
.
          MOVE      ZERO,CHIGHLT
.
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
          UNPACK    TODAY,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD OF KBCLIN
.
          PACK      VDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",VDATE
.
          MATCH     VDATE,TODAY
          IF        !@LESS
            PACK      KEY20,VSITE,VCLIN,VDATE
            CALL      RDCLIA1
            IF        OVRCD = 1
              CALL      RDPCLIA1
              IF        OVRCD = 0
                MATCH     VSITE,OCASITE
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ELSE
                  MATCH     VCLIN,OCACLIN
                  IF        !@EQUAL
                    MOVE      ONE,OVRCD
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
            IF        OVRCD = 1
              MOVE      "Unknown Clinic",OCADESC
              PACK      OCADESC,OCADESC,SP30
            ENDIF
            DISPLAY   *P35:IVERT,*V2LON,OCADESC
            GOTO      KBTIME
          ENDIF
.
          DISPLAY   *P1:24,*B,"Date can't be in the future.  ",*EL;
          CALL      HITENTER
          GOTO      KBDATE
.
.------------------------------------------------------------------------------
.        ** INPUT SESSION TIME **
.------------------------------------------------------------------------------
.
KBTIME    MOVE      SP5,VSTRT
.
          UNPACK    VSTRT,CHOUR,ANS,CMIN
          MOVE      "20",CCOL
          MOVE      TMVERT,CVERT
.
          CALL      KEYTIME
          BRANCH    OVRCD OF ERRTIME
.
          PACK      VSTRT,CHOUR,COLON,CMIN
          GOTO      KEYTIMES
.
.
ERRTIME   DISPLAY   *P35:TMVERT,"** A Session Start Time Must Be Entered **":
                    *W,*P35:TMVERT,*EL
          GOTO      KBTIME
.
.------------------------------------------------------------------------------
.        ENTER ACTUAL START & END TIMES & DELAY CODE
.------------------------------------------------------------------------------
.
KEYTIMES  PACK      KEY28,VSITE,VCLIN,VDATE,VSTRT,SP30
          CALL      RDSBOKA1
          CALL      RDKBOKA1
          COMPARE   ZERO,OVRCD
          BRANCH    OVRCD TO NOBOKX
          MATCH     OBASITE,VSITE
          GOTO      NOBOKX IF NOT EQUAL
          MATCH     OBADATE,VDATE
          GOTO      NOBOKX IF NOT EQUAL
          GOTO      GOTBOKX
.
NOBOKX    DISPLAY   *P1:24,*B,*EL,"No Booking Found.  ";
          CALL      HITENTER
          GOTO      REPEAT
.
GOTBOKX   MOVE      ZERO,GOTUSERC
          PACK      KEY25,VSITE,VCLIN,VDATE,VSTRT
          MOVE      SP5,VACST
          MOVE      SP5,VACEND
          CALL      RDUSEA1
          BRANCH    OVRCD TO NORECORD
.
          MOVE      ONE,GOTUSERC
          MOVE      OUSACST,VACST
          MOVE      OUSACEND,VACEND
          MOVE      OUSDLY,VDLY
.
          MOVE      SP30,TDESC
.
          MATCH     SP3,VDLY
          GOTO      NOVDLY IF EQUAL
.
          PACK      KEY5,CODECD,VDLY
          CALL      RDCODE1
.
NOVDLY    DISPLAY   *P22:8,*V2LON,VACST,*P22:9,VACEND,*P22:10,VDLY:
                    *P27:10,TDESC
.
NORECORD  DISPLAY   *P1:8,"Actual Start Time  : ":
                    *P1:9,"Actual End   Time  : ":
                    *P1:10,"Doctor Called Away : ":
                    *P1:11,*EF
.
KACTSTR   UNPACK    VACST,CHOUR,ANS,CMIN
          MOVE      "22",CCOL
          MOVE      TMVERT,CVERT
          ADD       TWO,CVERT
.
          CALL      KEYTIME
          BRANCH    OVRCD OF ERRTIM1
.
          PACK      VACST,CHOUR,COLON,CMIN
          GOTO      KACTEND
.
.
ERRTIM1   BRANCH    GOTUSERC TO KACTEND
          DISPLAY   *P35:CVERT,"** An Actual Start Time Must Be Entered **":
                    *W,*P35:CVERT,*EL
          GOTO      KACTSTR
.
.        Enter Actual End Time
.
KACTEND   UNPACK    VACEND,CHOUR,ANS,CMIN
          MOVE      "22",CCOL
          MOVE      TMVERT,CVERT
          ADD       THREE,CVERT
.
          CALL      KEYTIME
          BRANCH    OVRCD OF ERRTIM2
.
          PACK      VACEND,CHOUR,COLON,CMIN
          MATCH     VACST,VACEND
          GOTO      ERRTIM3 IF LESS
          GOTO      KDELAYC
.
ERRTIM2   BRANCH    GOTUSERC TO KDELAYC
          DISPLAY   *P35:CVERT,"** An Actual End Time Must Be Entered **":
                    *W,*P35:CVERT,*EL
          GOTO      KACTEND
.
ERRTIM3   BRANCH    GOTUSERC TO KDELAYC
          DISPLAY   *P35:CVERT,"** End time cannot be before Start time **":
                    *W,*P35:CVERT,*EL
          GOTO      KACTEND
.
.        Enter Delay Code
.
KDELAYC   KEYIN     *P22:10,*V2LON,VDLY
.
          ENDSET    VDLY
          APPEND    SP3 TO VDLY
          RESET     VDLY
.
          MATCH     SP3,VDLY
          GOTO      KEYPROC IF EQUAL
.
          CMATCH    QUEST TO VDLY
          GOTO      NOTQUE5 IF NOT EQUAL
.
          MOVE      CODECD,CODE
          CALL      PATCODDS
          CALL      DSPGEN
          CALL      DATGEN
          DISPLAY   *P1:8,*EF,"Actual Start Time  : ",*V2LON,VACST,*HOFF:
                    *P1:9,"Actual End   Time  : ",*V2LON,VACEND,*HOFF:
                    *P1:10,"Doctor Called Away : "
          GOTO      KDELAYC
.
NOTQUE5   PACK      KEY5,CODECD,VDLY
          CALL      RDCODE1
          BRANCH    OVRCD TO ERRDELAY
.
          DISPLAY   *P27:10,*V2LON,TDESC
.
KEYPROC   MOVE      SP1,ANS
          KEYIN     *P1:13,"Ok to Post (",*V2LON,*DV,ANSY,*HOFF:
                    *DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS
          CMATCH    ANSY,ANS
          GOTO      WRUSEDET IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      DISPSCR IF EQUAL
          BEEP
          GOTO      KEYPROC
.
ERRDELAY  COMPARE   ZERO TO GOTUSERC
          GOTO      NOUSERC IF EQUAL
.
          MOVE      OUSDLY,VDLY
          DISPLAY   *P22:10,*V2LON,VDLY,*P27:10,TDESC
          GOTO      KEYPROC
.
NOUSERC   DISPLAY   *P30:24,*EL,*B,*V2LON:
                    "** Invalid Delay Code ** ",*W,*P30:24,*EL;
          GOTO      KDELAYC
.
. ----------------------------
. Write the usage details away
. ----------------------------
WRUSEDET  MOVE      VCLIN,OUSCLIN
          MOVE      VDATE,OUSDATE
          MOVE      VSTRT,OUSSTRT
          MOVE      VACST,OUSACST
          MOVE      VACEND,OUSACEND
          MOVE      VDLY,OUSDLY
          MOVE      SP20,OUSSPARE
.
          IF        GOTUSERC=1
            CALL      UPUSEA1
          ELSE
            CALL      WRUSEA1
            MOVE      ONE,GOTUSERC
          ENDIF
.
          GOTO      KBSLOT
.
.------------------------------------------------------------------------------
.        ** DISPLAY SESSION'S BOOKINGS & KEYIN SLOT NUMBER **
.------------------------------------------------------------------------------
.
KBSLOT    CALL      BOOKSRCH
          BRANCH    OVRCD TO REPEAT
.
          PACK      KEY18,VSITE,VCLIN,VDAY,VSTRT
          CALL      RDMASA1
          LOAD      DDAY,OMADAY OF DMON,DTUE,DWED,DTHU,DFRI,DSAT,DSUN
          DISPLAY   *P38:6,*V2LON,DDAY
.
KBSLOT1   MOVE      SP3,SLOTOPT
          KEYIN     *P1:24,*EL,"Keyin Slot Number  (",*V2LON,"E",*HOFF:
                    ")nd  (",*V2LON,"R",*HOFF,")edisplay Screen : ":
                    *V2LON,*JR,SLOTOPT
.
          TYPE      SLOTOPT
          GOTO      SLOTNUM IF EQUAL
.
          MATCH     "  R",SLOTOPT
          GOTO      KBSLOT IF EQUAL
          MATCH     "  E",SLOTOPT
          GOTO      CHKNONAT IF EQUAL
          BEEP
          GOTO      KBSLOT1
.
SLOTNUM   MOVE      SLOTOPT,VSLOT
.
          PACK      KEY28,VSITE,VCLIN,VDATE,VSTRT,VSLOT
          CALL      RDBOKA1
          BRANCH    OVRCD TO ERRSLOT
.
          COMPARE   ZERO,OBAEXSL
          GOTO      ERREXT IF NOT EQUAL
.
          BRANCH    OBASTAT TO GOODBK,ERRSLOT,ERREXT,ERRSLOT,GOODBK
          GOTO      ERRSLOT
.
GOODBK    PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1
          BRANCH    OVRCD TO ERRBOOK2
.
          COMPARE   ONE,OBASTAT
          GOTO      KEYATTN0 IF EQUAL
.
KEYWARN   MOVE      SP1,ANS
          KEYIN     *P1:23,*EF,"This slot has already been flagged ":
                    "as Non-Attended.":
                    *P1:24,"Do you want to alter the Attendance ":
                    "Code (",*V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON:
                    *DV,ANSN,*HOFF,") ? ",*V2LON,ANS,*P1:23,*EF;
          RESET     ANS
          CMATCH    ANSY,ANS
          GOTO      KEYATTN0 IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      KBSLOT1 IF EQUAL
          BEEP
          GOTO      KEYWARN
.
KEYATTN0  MOVE      TWO,AUDIFLAG   * set to write a b audit
          CALL      AUDOTBOB       * write the audit
KEYATTN   KEYIN     *P1:24,*EL,"Slot: ",*V2LON,*DV,VSLOT,*HOFF:
                    "   Enter Attendance Code : ",*V2LON,OBAATTN
.
          ENDSET    OBAATTN
          APPEND    SP3 TO OBAATTN
          RESET     OBAATTN
.
          MATCH     SP3,OBAATTN
          GOTO      NOTSP1 IF NOT EQUAL
          MOVE      FIVE,AUDIFLAG  * set to delete a b audit
          CALL      AUDOTBOB       * delete the audit
.         
          GOTO      KBSLOT 
.
NOTSP1    CMATCH    QUEST TO OBAATTN
          GOTO      NOTQUE4 IF NOT EQUAL
.
          MOVE      CODECA TO CODE
          CALL      PATCODDS
          CALL      DSPGEN
          CALL      DATGEN
          CALL      BOOKSRCH
          DISPLAY   *P38:6,*V2LON,DDAY
          PACK      KEY28,VSITE,VCLIN,VDATE,VSTRT,VSLOT
          CALL      RDBOKA1
.
          PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1
.
          GOTO      KEYATTN
.
NOTQUE4   PACK      KEY5,CODECA,OBAATTN
          CALL      RDCODE1
          BRANCH    OVRCD TO ERRATTN
.
          DISPLAY   *P40:24,*EL,*V2LON,"** Updating **";
.
.------------------------------------------------------------------------------
.       **** WRITE/UPDATE CLINIC BOOKING FILE ***
.------------------------------------------------------------------------------[
.[
CHKBOKA   PACK      KEY28,VSITE,VCLIN,VDATE,VSTRT,VSLOT
.
          CALL      RDBOKA1
          BRANCH    OVRCD OF ERRBOOK1           * Bookings record disappeared *
          MOVE      TWO,AUDIFLAG                * set flag to write a b audit
          CALL      AUDOTBOA                    * write audit
.
          PACK      KEY8,OBAOUTNO
          CALL      RDABOKB1
          CALL      UPBOKB1
          MOVE      THREE,AUDIFLAG              * set flag to write a c audit
          CALL      AUDOTBOB                    * write audit
.
UPSTAT0   MOVE      FIVE,OBASTAT
          CALL      UPBOKA1
          MOVE      THREE,AUDIFLAG              * set flag to write a c audit
          CALL      AUDOTBOA                    * write audit
.
.
.        Funny problem - pointer has to be repositioned back
.                        at the updated record else position
.                        within index file is lost/confused.
.
          PACK      KEY28,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT
          CALL      RDBOKA1
.
.        Update completed
.
ENDPSTA   PACK      AUITEM,OBASITE,SLASH,OBACLIN,SLASH,OBADATE:
                                SLASH,OBASTRT,SP20
.
          BRANCH    OBASTAT TO ENDPSTA1               * Booked slot *
          PACK      AUCONBEF,OBASLOT,SLASH,DNOATTN,SLASH,OBAATTN
          GOTO      ENDPSTA2
.
ENDPSTA1  PACK      AUCONBEF,OBASLOT,SLASH,DBOOKED
.
ENDPSTA2  PACK      AUCONAFT,OBASLOT,SLASH,DNOATTN,SLASH,OBAATTN:
                    SLASH,TDESC
          CALL      WRITAUDX
.                                           *  SRF 22215 Start
          PACK      KEY10,CHOSPNUM,OBAOUTNO,SP70
          CALL      RAECLNK2
          IF        OVRCD=0
            CALL      DEECLNK2
          ENDIF
.                                           *  SRF 22215 End
          DISPLAY   *P40:24,*EL,*V2LON,"** Updated **",*W;
.
          GOTO      KBSLOT
.
.
.------------------------------------------------------------------------------
.        Error Messages
.------------------------------------------------------------------------------
ERRNOBK   DISPLAY   *P50:24,*EL,*B,*V2LON,"** Slot NOT BOOKED **",*W;
          GOTO      KBSLOT1
.
ERRCANC   DISPLAY   *P50:24,*EL,*B,*V2LON,"** Cancelled Slot **",*W;
          GOTO      KBSLOT1
.
ERREXT    DISPLAY   *P50:24,*EL,*B,*V2LON,"** Extended Slot **",*W;
          GOTO      KBSLOT1
.
ERRAT     DISPLAY   *P50:24,*EL,*B,*V2LON,"** Attended Slot **",*W;
          GOTO      KBSLOT1
.
ERRNOAT   DISPLAY   *P50:24,*EL,*B,*V2LON,"** Non-Attended Slot **",*W;
          GOTO      KBSLOT1
.
ERRSLOT   DISPLAY   *P50:24,*EL,*B,*V2LON,"** Invalid Slot **",*W;
          GOTO      KBSLOT1
.
ERRATTN   DISPLAY   *P50:24,*EL,*B,*V2LON,"** Invalid Code **",*W;
          GOTO      KEYATTN
.
ERRBOOK1  KEYIN     *P1:24,*EL,*B,*V2LON,"* ERROR *",*HOFF," Booking record":
                    " disappeared - ",*V2LON,*DV,KEY28,*HOFF:
                    " Call IBA ... ",ANS;
          GOTO      REPEAT
.
ERRBOOK2  KEYIN     *P1:24,*EL,*B,*V2LON,"* ERROR *",*HOFF:
                    " Patient Booking record disappeared - ":
                    *V2LON,*DV,KEY8,*HOFF:
                    " Call IBA ... ",ANS;
          GOTO      REPEAT
.
.
+
.------------------------------------------------------------------------------
.
.        Search booking file for booked slots
.
.------------------------------------------------------------------------------
.
CHKNONAT  PACK      KEY28,VSITE,VCLIN,VDATE,VSTRT,SP30
          CALL      RDSBOKA1
.
CHKLOOP   CALL      RDKBOKA1
          BRANCH    OVRCD OF CHKEXIT
.
          MATCH     OBASITE,VSITE
          GOTO      CHKEXIT IF NOT EQUAL
.
          MATCH     OBADATE,VDATE
          GOTO      CHKEXIT IF NOT EQUAL
.
          BRANCH    OBASTAT TO CHKBKERR
          GOTO      CHKLOOP
.
CHKEXIT   
........  MOVE      VCLIN,OUSCLIN
........  MOVE      VDATE,OUSDATE
........  MOVE      VSTRT,OUSSTRT
........  MOVE      VACST,OUSACST
........  MOVE      VACEND,OUSACEND
........  MOVE      VDLY,OUSDLY
........  MOVE      SP20,OUSSPARE
.
........  BRANCH    GOTUSERC TO CHKUPDAT
.
........  CALL      WRUSEA1
........  GOTO      CHKCANC
.
......... CHKUPDAT  CALL      UPUSEA1
.
CHKCANC   GOTO      REPEAT
.
.        Booked slots still exist ...
.
CHKBKERR  KEYIN     *P1:24,*EL,*B,"** Booked slots still exist ":
                    "for this session. Ok to exit (",*V2LON,*DV:
                    ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ? ":
                    *V2LON,ANS;
          CMATCH    ANSY,ANS
          GOTO      CHKEXIT IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      KBSLOT1 IF EQUAL
          GOTO      CHKBKERR
.
+
.------------------------------------------------------------------------------
.
.        Search booking file for session required & display
.
.------------------------------------------------------------------------------
.
BOOKSRCH  MOVE      VSITE,OBASITE
          MOVE      VCLIN,OBACLIN
          MOVE      VDATE,OBADATE
          MOVE      VSTRT,OBASTRT
.
          DISPLAY   *P32:6,*EF:
                    *P32:6,"Day : ",*V2LON,DDAY,*HOFF:
                    *P52:6,"Session Starts : ",*V2LON,OBASTRT,*HOFF:
                    *P1:8,*ULON,"Slot",*P7:8,"Time ":
                    *P13:8,"V/T",*P17:8,"Status  ",*P27:8,"Name",SP10,SP6:
                    *P49:8,"Compen. Type ",*P63:8,"Non-Attend. Code "
.
          MOVE      NINE,CVERT
          MOVE      ZERO,PATFND
.
          PACK      KEY28,OBASITE,OBACLIN,OBADATE,OBASTRT,SP30
          CALL      RDSBOKA1
.
NEXTSL    CALL      RDKBOKA1
          BRANCH    OVRCD OF SVSLOT
.
          MATCH     OBASITE,VSITE
          GOTO      SVSLOT IF NOT EQUAL
.
          MATCH     OBACLIN,VCLIN
          GOTO      SVSLOT IF NOT EQUAL
.
          MATCH     OBADATE,VDATE
          GOTO      SVSLOT IF NOT EQUAL
.
          MATCH     OBASTRT,VSTRT
          GOTO      SVSLOT IF NOT EQUAL
.
          BRANCH    OBASTAT TO BOOKDSP,NEXTSL,NEXTSL,NEXTSL,BOOKDSP
          GOTO      NEXTSL
.
BOOKDSP   PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1
          BRANCH    OVRCD TO NOBOOK2
.
          MATCH     ZEROUR,OBAURNO
          GOTO      GOTURNO IF NOT EQUAL
.
          MOVE      OBAOUTNO,PURNO
          MOVE      PURNO,KEY8
          CALL      RDPRAM1
          BRANCH    OVRCD TO NOBOOK3
          GOTO      BOOKDSP2
.
.        U/R exists - patient in PMI
.
GOTURNO   MOVE      OBAURNO,PURNO
          PACK      KEY8,PURNO
          CALL      RDMAST1
          BRANCH    OVRCD TO NOURNO1
.
BOOKDSP2  CALL      CONCAT
          MOVE      DIM40,DIM20
.
          MOVE      OBADAY,VDAY
.
          MOVE      SP9,DESCCOMP
          MATCH     SP3,OBACOMP
          GOTO      NOCOMP IF EQUAL
          PACK      KEY5,CODECL,OBACOMP
          CALL      RDCODE1
          MOVE      TDESC,DESCCOMP
.
NOCOMP    MOVE      SP9,DESCATTN
          MATCH     SP3,OBAATTN
          GOTO      NOATTN IF EQUAL
          PACK      KEY5,CODECA,OBAATTN
          CALL      RDCODE1
          MOVE      TDESC,DESCATTN
.
NOATTN    MOVE      DNOBOOK,DESCSTAT
          LOAD      DESCSTAT,OBASTAT,DBOOKED,DUNUSED,DEXTEND,DATTEND,DNOATTN
.
NOTATT    DISPLAY   *P1:CVERT,OBASLOT:
                    *P7:CVERT,OBATIME:
                    *P13:CVERT,OBAVISIT:
                    *P17:CVERT,*V2LON,DESCSTAT,*HOFF:
                    *P27:CVERT,DIM20:
                    *P49:CVERT,DESCCOMP:
                    *P63:CVERT,OBAATTN,SP1,DESCATTN
.
          ADD       ONE,CVERT
          MOVE      ONE,PATFND
.
CHKLNGH   COMPARE   TWENTY3 TO CVERT
          GOTO      MOREBOOK IF NOT LESS
          GOTO      NEXTSL
.
MOREBOOK  MOVE      SP1 TO ANS
          KEYIN     *P1:24,*EL,"(",*V2LON,*DV,ANSA,*HOFF,")ccept this ":
                    "screen or (",*V2LON,*DV,ANSN,*HOFF,")ext screen : ":
                    *V2LON,ANS,*P1:24,*EL;
          CMATCH    ANSA TO ANS
          GOTO      SVSLOT IF EQUAL
          CMATCH    ANSN TO ANS
          GOTO      MOREBOK1 IF EQUAL
          BEEP
          GOTO      MOREBOOK
.
MOREBOK1  MOVE      NINE,CVERT
          DISPLAY   *P1:9,*EF
          GOTO      NEXTSL
.
SVSLOT    MOVE      ZERO,OVRCD
          COMPARE   PATFND,ZERO
          GOTO      NOBOOK1 IF EQUAL
          RETURN
.
NOBOOK1   DISPLAY   *P1:24,*B,*EL,*V2LON,*P20:24,"** No Booking Found **",*W;
          MOVE      ONE,OVRCD
          RETURN
.
NOBOOK2   DISPLAY   *P1:24,*B,*EL,*V2LON:
                    "** Patient Booking File record missing **";
          KEYIN     ANS;
          MOVE      ONE,OVRCD
          RETURN
.
NOBOOK3   DISPLAY   *P1:24,*B,*EL,*V2LON:
                    "** Preattendance Booking File record missing **";
          KEYIN     ANS;
          MOVE      ONE,OVRCD
          RETURN
.
.
NOURNO1   DISPLAY   *P1:24,*B,*EL,*V2LON:
                    "** Patient Master Index record missing **";
          KEYIN     ANS;
          MOVE      ONE,OVRCD
          RETURN
.
+
.******************************************************************************
.
.        Display routines
.
.------------------------------------------------------------------------------
.
DSPGEN    DISPLAY   *P1:4,*EF,"Clinic Id.       :":
                    *P1:5,"Date             :":
                    *P1:6,"Time             :"
          MOVE      FOUR,IVERT
          MOVE      FIVE,DVERT
          MOVE      SIX,TMVERT
          RETURN
.
DATGEN    DISPLAY   *P20:4,*V2LON,VCLIN,*P30:4,OCADESC
          UNPACK    VDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      FIVE,CVERT
          MOVE      TWENTY,CCOL
          CALL      DSPDATE
          UNPACK    VSTRT,CHOUR,ANS,CMIN
          MOVE      SIX,CVERT
          MOVE      TWENTY,CCOL
          CALL      DSPTIME
          RETURN
.
.------------------------------------------------------------------------------
.      MAIN MENU
.------------------------------------------------------------------------------
.
DISPA1    DISPLAY   *P1:8,"U/R Number       :",*EL:
                    *P11:4,*EF,*V2LON,OBAURNO,*HOFF:
                    *P1:5,"Outpatient No.   :",*EL:
                    *P11:5,*V2LON,OBAOUTNO
          RETURN
.
.------------------------------------------------------------------------------
. Formats name string - uses master file variables
.------------------------------------------------------------------------------
.
CONCAT    PACK      DIM40,SP20,SP20
          CLEAR     DIM40
          MATCH     SP4,PTITL
          GOTO      CONCAT1 IF EQUAL
.
          APPEND    PTITL,DIM40
          CALL      BUMPCH
          APPEND    SP1,DIM40
CONCAT1   APPEND    PSNAME,DIM40
          CALL      BUMPCH
          APPEND    ", ",DIM40
          APPEND    PGNAME,DIM40
          RESET     DIM40
          RETURN
.
BUMPCH    CMATCH    SP1,DIM40
          RETURN    IF NOT EQUAL
          BUMP      DIM40,-1
          RETURN    IF EOS
          GOTO      BUMPCH
.
.****************************************************************************
.                        AUDIT ROUTINES for BOKA 
.
.    AUDIFLAG : 1   Write a A audit to file
.               2   Write a B audit to file
.               3   Write a C audit to file
.               4   Write a D audit to file
.               5   Delete  B audit to file
.****************************************************************************
.                                 AUDOTBOA
.    Audits for patient outpatients bookings file (outaudba)
.****************************************************************************
AUDOTBOA
.         DISPLAY   *P1:3,*EL,"HOAUDA ",HOAUDA
          BRANCH    HOAUDA,OTBOA999
.
          COMPARE   FIVE,AUDIFLAG      * is it a delete?
          GOTO      OTBOA600 IF EQUAL
.
          COMPARE   THREE,AUDIFLAG      * is it a after?
          GOTO      OTBOA200 IF EQUAL
.
.
          CALL      IBACLOCK           * get current date
          PACK      OTBAAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OTBAAUDD
          CLOCK     TIME,OTBAAUDT      * clock current time
          MOVE      ONE,OTBAAUDS       * set print status
          MOVE      PASSCODE,OTBAAUDO  * get user ID
          MOVE      PORT,OTBAAUDP      * get port number
.
          BRANCH    AUDIFLAG,OTBOA200,OTBOA200,OTBOA200,OTBOA200,OTBOA600
          DISPLAY   *P60:24,*V2LON,*BLINKON,"Audit flag not set"
          GOTO      OTBOA999
OTBOA200
          MOVE      AUDIFLAG,OTBAAUDR
          REPLACE   "1A2B3C4D",OTBAAUDR
          PACK      KEY19,OTBAAUDD,OTBAAUDT,OTBAAUDP,OTBAAUDR  
          CALL      AWOTBOA           * write to audit file
          GOTO      OTBOA999
OTBOA600                               * Delete B audit
          PACK      KEY19,OTBAAUDD,OTBAAUDT,OTBAAUDP,ANSB
          CALL      ADOTBOA           * delete audit file
OTBOA999  RETURN
.****************************************************************************
.                        AUDIT ROUTINES for WAT01
.
.    AUDIFLAG : 1   Write a A audit to file
.               2   Write a B audit to file
.               3   Write a C audit to file
.               4   Write a D audit to file
.               5   Delete  B audit to file
.****************************************************************************
.                                 AUDOTBOB
.    Audits for patient outpatients bookings details file (outaudbb)
.****************************************************************************
AUDOTBOB
.         DISPLAY   *P1:3,*EL,"HOAUDB ",HOAUDB
          BRANCH    HOAUDB,OTBOB999
.
          COMPARE   THREE,AUDIFLAG      * is it a after?
          GOTO      OTBOB200 IF EQUAL
.
          COMPARE   FIVE,AUDIFLAG      * is it a delete?
          GOTO      OTBOB600 IF EQUAL
.
          CALL      IBACLOCK           * get current date
          PACK      OTBBAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OTBBAUDD
          CLOCK     TIME,OTBBAUDT      * clock current time
          MOVE      ONE,OTBBAUDS       * set print status
          MOVE      PASSCODE,OTBBAUDO  * get user ID
          MOVE      PORT,OTBBAUDP      * get port number
.
          BRANCH    AUDIFLAG,OTBOB200,OTBOB200,OTBOB200,OTBOB200,OTBOB600
          DISPLAY   *P60:24,*V2LON,*BLINKON,"Audit flag not set"
          GOTO      OTBOB999
OTBOB200
          MOVE      AUDIFLAG,OTBBAUDR
          REPLACE   "1A2B3C4D",OTBBAUDR
          PACK      KEY19,OTBBAUDD,OTBBAUDT,OTBBAUDP,OTBBAUDR  
          MOVE      IDDD,OTBBSITE
          CALL      AWOTBOB           * write to audit file
          GOTO      OTBOB999
OTBOB600                               * Delete B audit
          PACK      KEY19,OTBBAUDD,OTBBAUDT,OTBBAUDP,ANSB
          CALL      ADOTBOB           * delete audit file
OTBOB999  RETURN
.------------------------------------------------------------------------------
. The end ...
.------------------------------------------------------------------------------
.
ENDIT     BRANCH    HOAUDN OF ABORT
          CALL      STOPAUDX
.
ABORT     CHAIN     PGM
          STOP
.
          INC       STD001IO
          INC       OUTDSCLN
          INC       EOCLNKIO/INC            *  SRF 22215
          INC       OUTAUXIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTCLIIO/INC
          INC       OUTMA1IO/INC
          INC       OUTUSEIO/INC
          INC       PATCODKY
          INC       PATDOCKY
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATPR1IO/INC
.

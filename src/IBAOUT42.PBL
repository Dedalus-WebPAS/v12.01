.***************************************************************************
.* System    :   OUTPATIENTS                                               *
.* Program   :   IBAOUT42                                                  *
.* Desc      :   Statistics Report                                         *
.***************************************************************************
.* Date      :   01/05/89                                                  *
.* Author    :   Eric Phan                                                 *
.* Function  :   This program prints attendance statistics from the        *
.*               booking file & calculates average waiting times.          *
.* Mods      :                                                             *
.***************************************************************************
.*       V11.04.01  07/05/2024  J.Tan            TSK 0934710               *
.*                  Fixed generating Average Waiting times report          *
.*       V11.02.01  09/02/2022  Thanh T          TSK 0905641               *
.*                  Recompiled as OUTMA1FD changes                         *
.*       V10.05.01  15/07/2014  Ebon Clements    CAR 261630                *
.*                  Removed port number from temp file name                *
.*       V9.02.01   26/10/2001  Dean Cramer                                *
.*                  Fix where option 2 does not request date.              *
.*       V5.08.B01  13/03/2000  Steve Armstrong     5.08 mods              *
.*                  Mods for changes to OUTCLIFD (effective date)          *
.***************************************************************************
.*       V5.07.03   15/10/1999  Steve Armstrong                            *
.*                  Removed reference to CSTRTYR (not used)                *
.*            V5.07.02 13/08/1999  Steve Downing                           *
.*                     Mods to use DAYSEP                                  *
.*            V5.07.01 22/07/1999  Steve Downing                           *
.*                     Modified to use century in wait time calculations   *
.*                     Y/N option accepts upper & lower case               *
.*           V5.07.B02 18/01/1999  Nicole Harrington  507 rebound 095      *
.*                     Mods to use HEAD132                                 *
.*           V5.07.B01 30.10.1998  Jim Stathopoulos                        *
.*                     507 Changes                                         *
.***************************************************************************
.*               V5.02 22.05.89  E.Phan   SRF 100651                       *
.*               Fixed print of average waiting time summary.              *
.*               V5.03 27.05.89 S.Barcham                                  *
.*                     Fixed 12457 I * L by recompiling                    *
.*               V5.04 29.05.89  E.Phan                                    *
.*                     Added average waiting time totals.                  *
.*               V5.05 03.06.89 S.Barcham                                  *
.*                     Split OUTBOK & OUTMAS includes                      *
.*                     SRF 100751                                          *
.*               V5.06 09.06.89 K.Peak                                     *
.*                     Cleared Grand totals  SRF  100203                   *
.*               V5.07 24.06.89 S.Barcham                                  *
.*                     Recompiled for OUTBB1FD                             *
.*               V5.51 31.05.89  E.Phan         SRF 101385                 *
.*                     Problem with av. wait times if current month is     *
.*                     not in range.  Removed date check in calculation    *
.*                     to fix problem.                                     *
.*               V5.52 12.10.89  D.Di.Paola     SRF 101905                 *
.*                     Split report into two options. The first option will*
.*                     produce the Statistics Report without the Average   *
.*                     Waiting Times. The second option will produce the   *
.*                     Average Waiting Times without the Statistics Report.*
.*               V5.53 12.12.89  D.Di.Paola     SRF 101905                 *
.*                     Split Extraction and processing into two separate   *
.*                     routines to allow quicker access of details...      *
.*                     Fix various other bugs in extraction routine.       *
.*            V5.01.03 15.12.89  E.Phan                                    *
.*                     Converted to 5.01 & 13 periods. This replaces vers  *
.*                     v5.01.02 which didn't work properly.                *
.*            V5.01.04 04.01.90  K.Peak                                    *
.*                     Changed duplicate key error message to do an add and*
.*                     update  SRF 103531                                  *
.*            V5.01.05 29.01.89  E.Phan  SRF 103629                        *
.*                     Fixed bug where last clinic id is skipped.          *
.*            V5.01.06 02/03/90  J.Tan   SRF 104157                        *
.*                     Clear varibles at the start of report               *
.*            V5.01.07 11/07/90 Paul Duncan                                *
.*                     recomp for OUTBOA & OUTBOB audits                   *
.*            V5.01.08 04/10/91 Steve O'Gorman                             *
.*                     Recompiled for Change to OUTMA1FD                   *
.*            V5.01.09 02/03/93 ROD                                        *
.*                     Recompiled for Change to OUTMA1FD                   *
.*            V5.03.02 18/09/1995  ROD                  SRF 611661         *
.*                     Print the correct period descriptions in headings   *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTCLIFD/INC
          INC       OUTCTYFD/INC
          INC       OUTMA1FD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATCOMM/INC
          INC       PATDRGFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
CSTPER    DIM       6
CURPER    DIM       6
DIM4      DIM       4
NPYEAR    FORM      2
.
CMDLINE   DIM       50
CODECG    INIT      "CG"          * Group Code
CODECV    INIT      "CV"
COUNT     FORM      2
COUNTY    FORM      2
CURRDATE  DIM       8
.
DIM3      DIM       3
DIM6      DIM       6
DIM10     DIM       10
DIM20     DIM       20
.
FIRSTREC  FORM      1
FORM7     FORM      7
FORM7Y    FORM      7
.
GOTNEWD   FORM      1
GOTREVD   FORM      1
GRTOTNEW  FORM      5
GRTOTREV  FORM      5
GRTOTDOC  FORM      5
.
.
MPERREQD  FORM      2         * Required period (calculated for every record)
MPERUSED  FORM      2         * Number of months used (calculated initially)
MPRTPOS   FORM      3         * Variable print position
.
NEXTDAY   FORM      4
NEXTYEAR  FORM      4
.
OUTTMP    DIM       8
OUTTMPXX  IFILE     SQL, FIXED=105, KEY="U1-15"
.
PDDESC    DIM       6         * Period date description (mmm yy)
PRENDPER  DIM       6
PRSTRPER  DIM       6
.
SAVINDC   DIM       1
SAVCTYPE  DIM       6
SAVGROUP  DIM       3
.
TGROUP    DIM       3         * (3) OUTTMPXX - Group Code
TCTYPE    DIM       6         * (6)          - Clinic Type
TCCLIN    DIM       6         * (6)          - Clinic ID
TCPER1    FORM      7         * (5)          - Totals for period 1
TCPER2    FORM      7         * (5)                       period 2
TCPER3    FORM      7         * (5)                       period 3
TCPER4    FORM      7         * (5)                       period 4
TCPER5    FORM      7         * (5)                       period 5
TCPER6    FORM      7         * (5)                       period 6
TCPER7    FORM      7         * (5)                       period 7
TCPER8    FORM      7         * (5)                       period 8
TCPER9    FORM      7         * (5)                       period 9
TCPER10   FORM      7         * (5)                       period 10
TCPER11   FORM      7         * (5)                       period 11
TCPER12   FORM      7         * (5)                       period 12
TCPER13   FORM      7         * (5)                       period 13
TCNXTNEW  DIM       8         * (8)          - Next date for new slot
TCWAITNW  FORM      5         * (4)          - Next date for new slot
TCNXTREV  DIM       8         * (8)          - Next date for new slot
TCWAITRV  FORM      5         * (4)          - Next date for new slot
TCPERTOT  FORM      7         * period totals for Clinic ID
.
TTPER1    FORM      7         *** CLINIC TYPE TOTALS ***
TTPER2    FORM      7
TTPER3    FORM      7
TTPER4    FORM      7
TTPER5    FORM      7
TTPER6    FORM      7
TTPER7    FORM      7
TTPER8    FORM      7
TTPER9    FORM      7
TTPER10   FORM      7
TTPER11   FORM      7
TTPER12   FORM      7
TTPER13   FORM      7
TTPERTOT  FORM      7
.
TGPER1    FORM      7         *** GROUP TOTALS ***
TGPER2    FORM      7
TGPER3    FORM      7
TGPER4    FORM      7
TGPER5    FORM      7
TGPER6    FORM      7
TGPER7    FORM      7
TGPER8    FORM      7
TGPER9    FORM      7
TGPER10   FORM      7
TGPER11   FORM      7
TGPER12   FORM      7
TGPER13   FORM      7
TGPERTOT  FORM      7
.
TZPER1    FORM      7         *** GRAND TOTALS ***
TZPER2    FORM      7
TZPER3    FORM      7
TZPER4    FORM      7
TZPER5    FORM      7
TZPER6    FORM      7
TZPER7    FORM      7
TZPER8    FORM      7
TZPER9    FORM      7
TZPER10   FORM      7
TZPER11   FORM      7
TZPER12   FORM      7
TZPER13   FORM      7
TZPERTOT  FORM      7
.
VENDPER   DIM       6         * End PERIOD - CCYYMM
VSTRPER   DIM       6         * Start PERIOD - CCYYMM
VENDDATE  DIM       6         * End month - CCYYMM
VSTRDATE  DIM       6         * Start month - CCYYMM
.
WTNUMDOC  FORM      5         * # of doctors in group
WTTOTNEW  FORM      5         * Total new waiting time for group (days)
WTTOTREV  FORM      5         * Total revisit waiting time for group (days)
.
YRMULT    FORM      3
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAOUT42"
PRGNAM    INIT      "Statistics Report"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
.
          CALL      INIT0000          * initialization and open files
.
ML0100    CALL      OPTN0000          * select options
          BRANCH    EXIT,ML9900       * EXIT=1 if "0" chosen in menu
.
          BRANCH    OPTION,ML1000,ML2000      * branch on option chosen
          GOTO      ML0100
.
.-------- option 1 = Attendee Statistics Report --------
.
ML1000    CALL      PROC3000          * Extract and process attendee details
          BRANCH    FIRSTREC,ML9500   * If set, no records found
          CALL      PRNT0000          * Print Statistics Report  
          GOTO      ML0100
.
.-------- option 2 = Average Waiting Times -------
.
ML2000    CALL      PROC0000          * Extract and process waiting time details
          BRANCH    FIRSTREC,ML9500   * If set, no records found
          CALL      PRNX8000          * Print Average Waiting Times
          GOTO      ML0100
.
.--------------------------------------------------------------------------
.         No records found in processing pass ...
.--------------------------------------------------------------------------
.
ML9500    DISPLAY   *P1:24,*EL,*B,*V2LON,"*** No records found *** ";
          CALL      HITENTER
          GOTO      ML0100
.
.--------------------------------------------------------------------------
.         The end ...
.--------------------------------------------------------------------------
.
ML9900    CALL      KILL0000
          CHAIN     PGM               * chain back to appropriate menu
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialization                               *
.****************************************************************************
.
INIT0000  CALL       DISPHEAD
.
          DISPLAY    *P56:24,"Opening controlf"
          OPEN       CONTROLF,"controlf"
.
          DISPLAY    *P64:24,"patcodes"
          OPEN       PATCODE1,"patcodes"
.
          DISPLAY    *P64:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY    *P64:24,"patdrgaf"
          OPEN       PATDRGA1,"patdrgaf"
          OPEN       PATDRGA3,"patdrgaf"
.
          PACK       CFNAME,UID,FILBOKA4
          DISPLAY    *P64:24,CFNAME
          OPEN       OUTBOKA4,CFNAME
.
          PACK       CFNAME,UID,FILCLIA1
          DISPLAY    *P64:24,CFNAME
          OPEN       OUTCLIA1,CFNAME
.
          PACK       CFNAME,UID,FILCTYA1
          DISPLAY    *P64:24,CFNAME
          OPEN       OUTCTYA1,CFNAME
.
          PACK       CFNAME,UID,FILMA1A2
          DISPLAY    *P64:24,CFNAME
          CALL       OPOTMA12
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,OUTTMP
.
          DISPLAY    *P1:24,*EL
.
.         Work out what the current period is, and the first period in the
.         current financial year
.
          PACK       CPTDATE,CCC,CYY,CMM,CDD
          REP        " 0",CPTDATE
          CALL       GPERD                  * Find the current period
          BRANCH     EXIT,INIT9000          * No current period found
.
          PACK       CURPER,DRGCYR,DRGCNUM
.
          PACK       KEY6,DRGYR,SP2
          CALL       RDSDRGA1
          CALL       RDKDRGA1
.
          PACK       CSTPER,DRGCYR,DRGCNUM
.
.         Now count the number of periods in a financial year
.
          MOVE       ZERO,NPYEAR
          MOVE       DRGYR,DIM4
          PACK       KEY6,DRGYR,SP2
          CALL       RDSDRGA1
INIT8000  CALL       RDKDRGA1
          BRANCH     OVRCD,INIT8100
.
          MATCH      DRGYR,DIM4
          GOTO       INIT8100 IF NOT EQUAL
.
          ADD        ONE,NPYEAR
          GOTO       INIT8000
.
.         Finished initialisation
.
INIT8100  DISPLAY    *P1:24,*EL
          MOVE       ZERO,EXIT
          GOTO       INIT9999
.
.         No current period on file
.
INIT9000  KEYIN      *P1:24,*EL,*B,*V2LON:
                     "** Error: Current date not found in Period file. ":
                     "Hit <ENTER> to exit ",*EOFF,ANS,*P1:24;
          MOVE       ONE,EXIT
.
INIT9999  OPEN       PATDRGA2,"patdrgaf"     * Because GPERD closes this file
          RETURN
.
+
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.****************************************************************************
.
OPTN0000  MOVE       ONE,EXIT
          HLINE      *G37,2,52,80
          DISPLAY    *P1:3,*EF:
                     *P1:4,*V2LON,ZERO,*HOFF,". Exit":
                     *P1:5,*V2LON,ONE,*HOFF,". Attendee Statistics":
                     *P1:6,*V2LON,TWO,*HOFF,". Average Waiting Times";
OPTN0500  KEYIN      *P1:8,"Please Select : ",*V2LON,OPTION;
          COMPARE    ZERO,OPTION
          GOTO       OPTN9999 IF EQUAL
          MOVE       ZERO,EXIT
          BRANCH     OPTION,OPTN1000,OPTN2000
          BEEP
          GOTO       OPTN0500
.
.-------- display header according to option chosen ----
.
OPTN1000  DISPLAY    *P52:2,"- Attendee Statistics ";     * option = 1
          GOTO       OPTN3000
.
.-------- if option = 2 then display Average Waiting Times for header ------
.
OPTN2000  DISPLAY    *P52:2,"- Average Waiting Times ";
.          GOTO      OPTN9000
.
.----------------------------------------------------------------------------
.          Keyin dates
.----------------------------------------------------------------------------
.
OPTN3000  DISPLAY   *P1:10,*EF,"Generate Report from "
          UNPACK    CSTPER,CCENT,CYEAR,CMON
          MOVE      ZERO,CHIGHLT
          MOVE      TWENTY2,CCOL
          MOVE      TEN,CVERT
          CALL      KEYPER
          BRANCH    OVRCD,OPTN0000
.
.         Validate the date entered
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,OPTN3050
.
          PACK      VSTRPER,DRGYR,DRGNUM     * Starting period
          PACK      VSTRDATE,DRGFDTE         * Starting date (CCYYMM)
.
          PACK      PRSTRPER,DRGCYR,DRGCNUM  * printing date
.
.         Get the to date
.
OPTN3020  DISPLAY   *P30:10,"to "
          UNPACK    CURPER,CCENT,CYEAR,CMON
          MOVE      "33",CCOL
          CALL      KEYPER
          BRANCH    OVRCD,OPTN3000
.
.         Validate the date entered
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,OPTN3060
.
          PACK      VENDPER,DRGYR,DRGNUM     * Ending period
          PACK      VENDDATE,DRGTDTE         * Ending date (CCYYMM)
.
          PACK      PRENDPER,DRGCYR,DRGCNUM  * printing date
.
          MATCH     VSTRPER,VENDPER
          GOTO      OPTN3100 IF NOT LESS
          DISPLAY   *P10:24,*V2LON,*B,"*** TO date cannot be ":
                    "before FROM date *** ",*W,*P1:24,*EL;
          GOTO      OPTN3000
.
.         From date not on file
.
OPTN3050  MATCH     "IBARSH",PGM
          GOTO      OPTN9999 IF EQUAL
.           
          DISPLAY   *P10:24,*V2LON,*B,"*** Invalid Date *** ",*W,*P1:24,*EL;
          GOTO      OPTN3000
.
.         To date not on file
.
OPTN3060  MATCH     "IBARSH",PGM
          GOTO      OPTN9999 IF EQUAL
.           
          DISPLAY   *P10:24,*V2LON,*B,"*** Invalid Date *** ",*W,*P1:24,*EL;
          GOTO      OPTN3020
.
.         Calculate MPERUSED - # of periods used
.
OPTN3100  MOVE      ZERO,MPERUSED
          PACK      KEY6,VSTRPER
          CALL      RDDRGA1
          BRANCH    OVRCD,OPTN3200
          GOTO      OPTN3250
OPTN3200  CALL      RDKDRGA1
          BRANCH    OVRCD,OPTN3300
.
OPTN3250  PACK      XDATE,DRGYR,DRGNUM
          MATCH     XDATE,VENDPER
          GOTO      OPTN3300 IF LESS
.
          ADD       ONE,MPERUSED
          GOTO      OPTN3200
.
.         Validate the number of periods
.
OPTN3300  COMPARE   TEN4,MPERUSED
          GOTO      OPTN9000 IF LESS
.
          MATCH     "IBARSH",PGM
          GOTO      OPTN9999 IF EQUAL
.           
          DISPLAY   *P10:24,*V2LON,*B,"*** Maximum 13 periods allowed *** ":
                    *W,*P1:24,*EL;
          GOTO      OPTN3000
.
.----------------------------------------------------------------------------
.         Ok to proceed ?
.----------------------------------------------------------------------------
.
OPTN9000  MOVE      SP1,ANS
          KEYIN     *P1:24,"Ok to proceed (",*V2LON,*DV,ANSY,*HOFF:
                    *DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          CMATCH    ANSY,ANS
          GOTO      OPTN9999 IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      OPTN0000 IF EQUAL
          BEEP
          GOTO      OPTN9000
.
OPTN9999  RETURN
+
.**************************************************************************
.*                              PROC0000                                  *
.*                    Extract and process data for average waiting times  *
.**************************************************************************
.
PROC0000  CALL      CREA0000               * Create temp file
          MOVE      ONE,FIRSTREC           * 1st record not read yet
.
          DISPLAY   *P1:12,*EF,*V2LON,"*** Processing Records ***",*HOFF:
                    *P1:14,"Group Code  :",*P1:15,"Clinic Type :":
                    *P1:16,"Clinic ID   :",*P1:17,"Date        :"
.
          PACK      KEY12,IDDD,SP20
          CALL      RDSCTYA1
PROC1000  CALL      RDKCTYA1
          BRANCH    OVRCD,PROC2999         * No more records
          MATCH     OCTSITE,IDDD
          GOTO      PROC2999 IF NOT EQUAL  * Different site - exit
.
          DISPLAY   *P15:14,*V2LON,OCTGRP,*P15:15,OCTCTYP
.
          CALL      CLRV0000          * Clear all vars for this clinic
          MOVE      SP70,OCACLIN
.
.------------------------------------------------------------------------------
.         1st part - check all status 0's for next new & revisit date
.------------------------------------------------------------------------------
.
          PACK      KEY35,OCTSITE,OCTCTYP,SP30
          CALL      RDSBOKA4
          CALL      RDKBOKA4           * Get more details to narrow search
          BRANCH    OVRCD,PROC1000
          MATCH     OBASITE,OCTSITE
          GOTO      PROC1000 IF NOT EQUAL  * Different site - write to datafile
          MATCH     OBACTYP,OCTCTYP
          GOTO      PROC1000 IF NOT EQUAL  * Different type - write to datafile
.
PROC1900  PACK      KEY35,OCTSITE,OCTCTYP,OBACLIN,ZERO,VSTRDATE,SP30
          CALL      RDSBOKA4
PROC2000  CALL      RDKBOKA4
          BRANCH    OVRCD,PROC2600         * End of file - write to datafile[
.
          MATCH     OBASITE,IDDD
          GOTO      PROC2600 IF NOT EQUAL  * Different site - write to datafile
          MATCH     OBACTYP,OCTCTYP
          GOTO      PROC2600 IF NOT EQUAL  * Different type - write to datafile
.
          MATCH     SP6,OCACLIN
          GOTO      PROC2100 IF NOT EQUAL
          MOVE      OBACLIN,OCACLIN
PROC2100  MATCH     OBACLIN,OCACLIN
          GOTO      PROC2600 IF NOT EQUAL  * Different clinic - write data
.
          COMPARE   ZERO,OBASTAT
          GOTO      PROC2600 IF NOT EQUAL  * No more free slots - write data 
.
          PACK      DIM6,OBADATE,SP6         * If date doesn't exist, then sp6
          MATCH     DIM6,VENDDATE
          GOTO      PROC2600 IF LESS         * Out of range - write to datafile
.
          MOVE      OBADATE,DIM6
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON
          DISPLAY   *P15:16,*V2LON,OBACLIN,*P15:17,CMON,SLASH,CCENT,CYEAR
.
          UNPACK    SP6,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5,SAVINDC
          PACK      KEY5,CODECV,OBAVISIT
          CALL      RDCODE1
          MOVE      ONE,FORM1
PROC2200  LOAD      SAVINDC,FORM1,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
          CMATCH    ANSN,SAVINDC
          GOTO      PROC2300 IF EQUAL      * New visit slot
          CMATCH    ANSR,SAVINDC
          GOTO      PROC2400 IF EQUAL      * Revisit slot
          ADD       ONE,FORM1
          BRANCH    FORM1,PROC2200,PROC2200,PROC2200,PROC2200,PROC2200
          GOTO      PROC2500
.
PROC2300  BRANCH    GOTNEWD,PROC2500
          MATCH     TCNXTNEW,OBADATE
          GOTO      PROC2500 IF LESS
          MOVE      OBADATE,TCNXTNEW
          MOVE      ONE,GOTNEWD
          GOTO      PROC2500
.
PROC2400  BRANCH    GOTREVD,PROC2500
          MATCH     TCNXTREV,OBADATE
          GOTO      PROC2500 IF LESS
          MOVE      OBADATE,TCNXTREV
          MOVE      ONE,GOTREVD
.
PROC2500  COMPARE   ZERO,GOTNEWD  * If one component missing keep on reading
          GOTO      PROC2000 IF EQUAL
          COMPARE   ZERO,GOTREVD
          GOTO      PROC2000 IF EQUAL
.
PROC2600  MATCH     SP6,OCACLIN            * No records for this clinic ?
          GOTO      PROC1000 IF EQUAL      * Yep - get next clinic type
.
.-------- Waiting time = next available slot date - current date -------
. ------- New visits -------
.
PROC2700  PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          DAYSEP    CURRDATE,TCNXTNEW,NEXTDAY
          MOVE      NEXTDAY,TCWAITNW
.
. ------- Revisits -------
.
          DAYSEP    CURRDATE,TCNXTREV,NEXTDAY
          MOVE      NEXTDAY,TCWAITRV
.
. ------- Write to datafile -------
.
          PACK      KEY15,OCTGRP,OCTCTYP,OCACLIN
          CALL      RDATMP1
          BRANCH    OVRCD,PROC2850
.
.         Error - duplicate key
.
          GOTO      PROC1000               * re loop
.
.         Write new record
.
PROC2850  MOVE      OCTGRP,TGROUP
          MOVE      OCTCTYP,TCTYPE
          MOVE      OCACLIN,TCCLIN
          CALL      WRTMP1
          MOVE      ZERO,FIRSTREC          * clear first record flag
.
          CALL      CLRV0000               * Clear clinic file variables
          GOTO      PROC2870
.
PROC2860  CALL      RDKBOKA4               * Keep on reading booking file
          BRANCH    OVRCD,PROC2999         * for next free clinic
PROC2870  COMPARE   ZERO,OBASTAT
          GOTO      PROC2880 IF EQUAL
          GOTO      PROC2860               * Keep on going through attendees
.
PROC2880  MATCH     OBASITE,IDDD
          GOTO      PROC2999 IF NOT EQUAL  * Different site - exit
          MOVE      OBASITE,OCTSITE
          MOVE      OBACTYP,OCTCTYP
          MOVE      OBACLIN,OCACLIN
          MOVE      SP3,OCTGRP
          PACK      KEY12,OCTSITE,OCTCTYP
          CALL      RDCTYA1
          GOTO      PROC1900
.
PROC2999  RETURN
.
.***********************************************************************
.*                             PROC3000                                *
.*        Extract and process Attendee Statistics                      *
.***********************************************************************
.
PROC3000  CALL      CREA0000               * Create temp file
          MOVE      ONE,FIRSTREC           * 1st record not read yet
.
          DISPLAY   *P1:12,*EF,*V2LON,"*** Processing Records ***",*HOFF:
                    *P1:14,"Group Code  :",*P1:15,"Clinic Type :":
                    *P1:16,"Clinic ID   :",*P1:17,"Date        :"
.
. ------- 1st level if loop - read clinic types -------
.
          PACK      KEY12,IDDD,SP20
          CALL      RDSCTYA1
PROC4000  CALL      RDKCTYA1
          BRANCH    OVRCD,PROC9999           * No more records
          MATCH     OCTSITE,IDDD
          GOTO      PROC9999 IF NOT EQUAL    * Different site - exit
.
. ------- 2nd level of loop - read clinic ids -------
.
          PACK      KEY20,IDDD,SP20
PROC5000  CALL      RDSCLIA1
          CALL      RDKCLIA1
          BRANCH    OVRCD,PROC4000           * No more records - get next type
.
          MATCH     OCASITE,IDDD
          GOTO      PROC4000 IF NOT EQUAL    * Different site - get next type
.
. ------- Verify that this site/type/id combination exists -------
.
          PACK      KEY24,OCTSITE,OCTCTYP,OCACLIN,SP30
          CALL      RDSMASA2                 * Positioning read
          CALL      RDKMASA2                 * Read the record
          BRANCH    OVRCD,PROC9000           * No such record - get next clinic
          MATCH     OCTSITE,OMASITE
          GOTO      PROC9000 IF NOT EQUAL    * Diff site - get next clinic
          MATCH     OCTCTYP,OMATYP
          GOTO      PROC9000 IF NOT EQUAL    * Diff type - get next clinic
          MATCH     OCACLIN,OMACLIN
          GOTO      PROC9000 IF NOT EQUAL    * Diff clinic - get next clinic
.
          CALL      CLRV0000                 * Clear all vars for this clinic
.
. ------- 3rd level of loop - read attendees -------
.
          PACK      KEY35,OCTSITE,OCTCTYP,OCACLIN,FOUR,VSTRDATE,SP30
          CALL      RDSBOKA4
PROC6000  CALL      RDKBOKA4
          BRANCH    OVRCD,PROC7000           * End of file - write to datafile
.
          MATCH     OBASITE,IDDD
          GOTO      PROC7000 IF NOT EQUAL    * Diff site - write to datafile
          MATCH     OBACTYP,OCTCTYP
          GOTO      PROC7000 IF NOT EQUAL    * Diff type - write to datafile
          MATCH     OBACLIN,OCACLIN
          GOTO      PROC7000 IF NOT EQUAL    * Diff clinic - write to datafile
          COMPARE   FOUR,OBASTAT
          GOTO      PROC7000 IF NOT EQUAL    * No more attended slots
          PACK      DIM6,OBADATE,SP6         * If date doesn't exist, then sp6
          MATCH     DIM6,VENDDATE
          GOTO      PROC7000 IF LESS         * Out of range - write to datafile
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON
          DISPLAY   *P15:14,*V2LON,OCTGRP,*P15:15,OCTCTYP:
                    *P15:16,OBACLIN,*P15:17,CMON,SLASH,CCENT,CYEAR
.
          CALL      CALM0000                 * Calculate period # for this month
          MOVE      ZERO,FIRSTREC
.
          LOAD      FORM7,MPERREQD,TCPER1,TCPER2,TCPER3,TCPER4:
                                   TCPER5,TCPER6,TCPER7,TCPER8:
                                   TCPER9,TCPER10,TCPER11,TCPER12,TCPER13
          ADD       ONE,FORM7
          STORE     FORM7,MPERREQD,TCPER1,TCPER2,TCPER3,TCPER4:
                                   TCPER5,TCPER6,TCPER7,TCPER8:
                                   TCPER9,TCPER10,TCPER11,TCPER12,TCPER13
          GOTO      PROC6000                 * Read another booking record
.
. ------- No more records for this clinic - write to datafile -------
. 
PROC7000  MOVE      TCPER1,TTPER1
          MOVE      TCPER2,TTPER2
          MOVE      TCPER3,TTPER3
          MOVE      TCPER4,TTPER4
          MOVE      TCPER5,TTPER5
          MOVE      TCPER6,TTPER6
          MOVE      TCPER7,TTPER7
          MOVE      TCPER8,TTPER8
          MOVE      TCPER9,TTPER9
          MOVE      TCPER10,TTPER10
          MOVE      TCPER11,TTPER11
          MOVE      TCPER12,TTPER12
          MOVE      TCPER13,TTPER13
.
          PACK      KEY15,OCTGRP,OCTCTYP,OCACLIN
          CALL      RDTMP1
          BRANCH    OVRCD,PROC8000
.
.         Update Records
.
          ADD       TTPER1,TCPER1
          ADD       TTPER2,TCPER2
          ADD       TTPER3,TCPER3
          ADD       TTPER4,TCPER4
          ADD       TTPER5,TCPER5
          ADD       TTPER6,TCPER6
          ADD       TTPER7,TCPER7
          ADD       TTPER8,TCPER8
          ADD       TTPER9,TCPER9
          ADD       TTPER10,TCPER10
          ADD       TTPER11,TCPER11
          ADD       TTPER12,TCPER12
          ADD       TTPER13,TCPER13
.
          CALL      UPTMP1
          GOTO      PROC9000
.
.         Write new record
.
PROC8000  MOVE      OCTGRP,TGROUP
          MOVE      OCTCTYP,TCTYPE
          MOVE      OCACLIN,TCCLIN
.
          CALL      WRTMP1
          CALL      CLRV0000               * Clear clinic file variables
          GOTO      PROC9000
.
PROC9000  PACK      KEY20,IDDD,OCACLIN,Z20
          GOTO      PROC5000
.
PROC9999  RETURN
.
.----------------------------------------------------------------------------
.         Calculate the period for a particular month - value in MPERREQD
.----------------------------------------------------------------------------
.
CALM0000  UNPACK    VSTRPER,XCENT,XYEAR,XMON
          MOVE      XMON,IMON
          MOVE      XYEAR,IYEAR
          MOVE      XCENT,ICENT
.
.         Use the bookings file date to get appropriate period
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON * Bookings date defaults if record
          MOVE      CMON,MPERREQD            * does not exist in per file.
.
          PACK      KEY14,OBADATE,SP20
          CALL      RDSDRGA2
          CALL      RDKDRGA2
          BRANCH    OVRCD,CALM9999
          MOVE      DRGNUM,MPERREQD
          UNPACK    DRGYR,CCENT,CYEAR
.
          MATCH     XYEAR,CYEAR
          GOTO      CALM1000 IF EQUAL
.
          ADD       NPYEAR,MPERREQD          * Different year
.
CALM1000  SUB       IMON,MPERREQD            * Same year
          ADD       ONE,MPERREQD
.
CALM9999  RETURN
.
.----------------------------------------------------------------------------
.         Clear temp variables
.----------------------------------------------------------------------------
.         Clinic ID
.
CLRV0000  PACK      TCNXTNEW,CCC,CYY,CMM,CDD
          REP       " 0",TCNXTNEW
          MOVE      TCNXTNEW,TCNXTREV
.
          MOVE      ZERO,COUNT
CLRV0100  ADD       ONE,COUNT
          COMPARE   COUNT,TEN7
          RETURN    IF LESS
          STORE     ZERO,COUNT,TCPER1,TCPER2,TCPER3,TCPER4,TCPER5:
                               TCPER6,TCPER7,TCPER8,TCPER9,TCPER10:
                               TCPER11,TCPER12,TCPER13,TCWAITNW,TCWAITRV:
                               GOTNEWD,GOTREVD
          GOTO      CLRV0100
.
.         Clinic type
.
CLRV1000  MOVE      ZERO,COUNT
CLRV1100  ADD       ONE,COUNT
          COMPARE   COUNT,TEN3
          RETURN    IF LESS
          STORE     ZERO,COUNT,TTPER1,TTPER2,TTPER3,TTPER4,TTPER5:
                               TTPER6,TTPER7,TTPER8,TTPER9,TTPER10:
                               TTPER11,TTPER12,TTPER13
          GOTO      CLRV1100
.
.         Group type
.
CLRV2000  MOVE      ZERO,COUNT
CLRV2100  ADD       ONE,COUNT
          COMPARE   COUNT,TEN3
          RETURN    IF LESS
          STORE     ZERO,COUNT,TGPER1,TGPER2,TGPER3,TGPER4,TGPER5:
                               TGPER6,TGPER7,TGPER8,TGPER9,TGPER10:
                               TGPER11,TGPER12,TGPER13
          GOTO      CLRV2100
.
.         Grand totals
.
CLRV3000  MOVE      ZERO,COUNT
CLRV3100  ADD       ONE,COUNT
          COMPARE   COUNT,TEN3
          RETURN    IF LESS
          STORE     ZERO,COUNT,TZPER1,TZPER2,TZPER3,TZPER4,TZPER5:
                               TZPER6,TZPER7,TZPER8,TZPER9,TZPER10:
                               TZPER11,TZPER12,TZPER13
          GOTO      CLRV3100
.
+
.**************************************************************************
.*                              PRNT0000                                  *
.*                              Print data                                *
.**************************************************************************
.
PRNT0000  MOVE      "80",CLNO
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,WTNUMDOC
          MOVE      ZERO,WTTOTNEW
          MOVE      ZERO,WTTOTREV
          MOVE      SP6,SAVCTYPE
.
          CALL      CLRV1000           * Clear clinic type variables
          CALL      CLRV2000           * Clear group type variables
          CALL      CLRV3000
.
          DISPLAY   *P1:12,*EF,*V2LON,"*** Printing Records ***",*HOFF:
                    *P1:14,"Group Code  :",*P1:15,"Clinic Type :":
                    *P1:16,"Clinic ID   :"
          PACK      KEY15,SP20
          CALL      RDSTMP1
PRNT1000  CALL      RDKTMP1
          BRANCH    OVRCD,PRNT9000
.
          DISPLAY   *P15:14,*V2LON,TGROUP,*P15:15,TCTYPE,*P15:16,TCCLIN
.
          MATCH     SP6,SAVCTYPE
          GOTO      PRNT2000 IF EQUAL        * 1st time round
          MATCH     SAVCTYPE,TCTYPE
          CALL      PRNX3000 IF NOT EQUAL    * Print last clinic type totals
          MATCH     SAVGROUP,TGROUP
          CALL      PRNX4000 IF NOT EQUAL    * Print last group totals
.
PRNT2000  MOVE      TCTYPE,SAVCTYPE
          MOVE      TGROUP,SAVGROUP
          CALL      PRNX1000                 * Print record
          GOTO      PRNT1000
.
PRNT9000  CALL      PRNX3000                 * Print last clinic type totals
          CALL      PRNX4000                 * Print last group totals
          CALL      PRNX5000                 * Print grand totals
.
.------- finished ----
.
          PRINT     *N,*N,"*** END OF REPORT ***"
          RETURN
.
+
.****************************************************************************
.*                                                                          *
.*                        Printing subroutines                              *
.*                                                                          *
.****************************************************************************
.         Print Record
.----------------------------------------------------------------------------
.
PRNX1000  COMPARE   "60",CLNO
          GOTO      PRNX1100 IF LESS
          CALL      PRNX2000           * Print new page head
.
PRNX1100  MOVE      "**ERROR** Unknown Clinic ID   ",OCADESC
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,IDDD,TCCLIN,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     IDDD,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     TCCLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
            GOTO      PRNX1110
          ENDIF
.
          MATCH     SP6,OCADOCT
          GOTO      PRNX1110 IF EQUAL
.
          PACK      KEY6,OCADOCT
          CALL      RDDOCT1
          BRANCH    OVRCD,PRNX1110
          CALL      GETN0000            * Format doctor's name
.
PRNX1110  MOVE      OCADESC,DIM20
          PRINT     TCCLIN,*8,DIM20;
.
          MOVE      ZERO,TCPERTOT
          MOVE      "28",MPRTPOS          * Print position
          MOVE      ZERO,COUNT
.
PRNX1200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX1500 IF LESS
          LOAD      FORM7,COUNT,TCPER1,TCPER2,TCPER3,TCPER4,TCPER5,TCPER6:
                                TCPER7,TCPER8,TCPER9,TCPER10,TCPER11,TCPER12:
                                TCPER13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to clinic type monthly figures
.
          LOAD      FORM7Y,COUNT,TTPER1,TTPER2,TTPER3,TTPER4,TTPER5,TTPER6:
                                 TTPER7,TTPER8,TTPER9,TTPER10,TTPER11,TTPER12:
                                 TTPER13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TTPER1,TTPER2,TTPER3,TTPER4,TTPER5,TTPER6:
                                 TTPER7,TTPER8,TTPER9,TTPER10,TTPER11,TTPER12:
                                 TTPER13
          ADD       FORM7,TCPERTOT     *  Add to clinic id total
          GOTO      PRNX1200
.
PRNX1500  PRINT     *MPRTPOS,TCPERTOT
          ADD       ONE,CLNO
          RETURN
.
.------------------------------------------------------------------------------
.         Format doctor's name
.------------------------------------------------------------------------------
.
GETN0000  PACK      OCADESC,SP30
          CLEAR     OCADESC
.
          MATCH     SP9,DTITL
          GOTO      GETN3000 IF EQUAL   * No title
          APPEND    DTITL,OCADESC
          RESET     OCADESC,30
GETN1000  CMATCH    SP1,OCADESC
          GOTO      GETN2000 IF NOT EQUAL
          BUMP      OCADESC,-1
          GOTO      GETN1000
GETN2000  APPEND    SP1,OCADESC
.
GETN3000  MATCH     SP30,DGNAME
          GOTO      GETN6000 IF EQUAL   * No given name
          APPEND    DGNAME,OCADESC
          RESET     OCADESC,30
GETN4000  CMATCH    SP1,OCADESC
          GOTO      GETN5000 IF NOT EQUAL
          BUMP      OCADESC,-1
          GOTO      GETN4000
GETN5000  APPEND    SP1,OCADESC
.
GETN6000  APPEND    DSNAME,OCADESC
          RESET     OCADESC
          RETURN
+
.
.----------------------------------------------------------------------------
.         Print New Page
.----------------------------------------------------------------------------
.
PRNX2000  CLOCK     TIME,CTIMEIS
.
PRNX2100  MOVE      ONE,CNOUNDLN 
          CALL      IBACLOCK
          CALL      HEAD132 
          UNPACK    PRSTRPER,XCENT,XYEAR,XMON      * printing vars srf 611661
          UNPACK    PRENDPER,CCENT,CYEAR,CMON
...       UNPACK    VSTRPER,XCENT,XYEAR,XMON
...       UNPACK    VENDPER,CCENT,CYEAR,CMON
          PRINT     *N,*40,"ATTENDEE STATISTICS":
                    *N,*40,"PERIOD FROM ",XMON,SLASH,XCENT,XYEAR," TO ":
                       CMON,SLASH,CCENT,CYEAR:
                    *N,*N,"Clinic ID/Description";
.
          MOVE      XMON,IMON
          MOVE      XYEAR,IYEAR
          MOVE      "28",MPRTPOS          * Print position
          MOVE      ZERO,COUNT
.
PRNX2200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX2300 IF LESS
.
.         Get description from date range file
.
          UNPACK    SP6,DRGSDSC,DIM3
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          MOVE      DRGSDSC,DIM3
.
          PACK      PDDESC,DIM3,SP1,IYEAR
          PRINT     *MPRTPOS,SP1,PDDESC;
          ADD       ONE,IMON
          MOVE      NPYEAR,IHOUR             * Check that number of periods is
          ADD       ONE,IHOUR                * not greater than total in year
          COMPARE   IHOUR,IMON
          GOTO      PRNX2200 IF LESS
          SUB       NPYEAR,IMON
          ADD       ONE,IYEAR
          GOTO      PRNX2200
.
PRNX2300  PRINT     *MPRTPOS,"  Total":
                    *N,"----------------------------------";
.
          CALL      PRNX6000         * Print rest of separator lines
.
          MOVE      SEVEN,CLNO
          RETURN
.
.----------------------------------------------------------------------------
.         Print clinic type totals
.----------------------------------------------------------------------------
.
PRNX3000  CALL      PRNX6000           * Print separators
          COMPARE   "60",CLNO
          GOTO      PRNX3100 IF LESS
          CALL      PRNX2000           * Print new page head
.
PRNX3100  MOVE      "*Unknown Type                 ",OCTDESC
          PACK      KEY12,IDDD,SAVCTYPE
          CALL      RDCTYA1
          MOVE      OCTDESC,DIM6
          PRINT     "Total for Clinic Type ",SAVCTYPE,SP1,DIM6;
.
          MOVE      ZERO,TTPERTOT
          MOVE      "28",MPRTPOS          * Print position
          MOVE      ZERO,COUNT
.
PRNX3200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX3500 IF LESS
          LOAD      FORM7,COUNT,TTPER1,TTPER2,TTPER3,TTPER4,TTPER5,TTPER6:
                                TTPER7,TTPER8,TTPER9,TTPER10,TTPER11,TTPER12:
                                TTPER13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to group type monthly figures
.
          LOAD      FORM7Y,COUNT,TGPER1,TGPER2,TGPER3,TGPER4,TGPER5,TGPER6:
                                 TGPER7,TGPER8,TGPER9,TGPER10,TGPER11,TGPER12:
                                 TGPER13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TGPER1,TGPER2,TGPER3,TGPER4,TGPER5,TGPER6:
                                 TGPER7,TGPER8,TGPER9,TGPER10,TGPER11,TGPER12:
                                 TGPER13
          ADD       FORM7,TTPERTOT     *  Add to clinic type total
          GOTO      PRNX3200
.
PRNX3500  PRINT     *MPRTPOS,TTPERTOT
          CALL      PRNX6000           * Print separators
          ADD       THREE,CLNO
          CALL      CLRV1000           * Clear clinic type variables
          RETURN
.
.----------------------------------------------------------------------------
.         Print group totals
.----------------------------------------------------------------------------
.
PRNX4000  COMPARE   "60",CLNO
          GOTO      PRNX4100 IF LESS
          CALL      PRNX2000           * Print new page head
.
PRNX4100  MOVE      "*Unknown Group*     ",TDESC
          PACK      KEY5,CODECG,SAVGROUP
          CALL      RDCODE1
          MOVE      TDESC,DIM10
          PRINT     "Total for Group Type ",SAVGROUP,SP1,DIM10;
.
          MOVE      ZERO,TGPERTOT
          MOVE      "28",MPRTPOS          * Print position
          MOVE      ZERO,COUNT
.
PRNX4200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX4500 IF LESS
          LOAD      FORM7,COUNT,TGPER1,TGPER2,TGPER3,TGPER4,TGPER5,TGPER6:
                                TGPER7,TGPER8,TGPER9,TGPER10,TGPER11,TGPER12:
                                TGPER13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to grand total figures
.
          LOAD      FORM7Y,COUNT,TZPER1,TZPER2,TZPER3,TZPER4,TZPER5,TZPER6:
                                 TZPER7,TZPER8,TZPER9,TZPER10,TZPER11,TZPER12:
                                 TZPER13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TZPER1,TZPER2,TZPER3,TZPER4,TZPER5,TZPER6:
                                 TZPER7,TZPER8,TZPER9,TZPER10,TZPER11,TZPER12:
                                 TZPER13
          ADD       FORM7,TGPERTOT     *  Add to group type total
          GOTO      PRNX4200
.
PRNX4500  PRINT     *MPRTPOS,TGPERTOT:
                    *N,"----------------------------------";
          CALL      PRNX6000           * Print separators
          ADD       TWO,CLNO
          CALL      CLRV2000           * Clear group type variables
          RETURN
.
.----------------------------------------------------------------------------
.         Print grand totals
.----------------------------------------------------------------------------
.
PRNX5000  COMPARE   "60",CLNO
          GOTO      PRNX5100 IF LESS
          CALL      PRNX2000           * Print new page head
.
PRNX5100  PRINT     "Grand Total";
.
          MOVE      ZERO,TZPERTOT
          MOVE      "28",MPRTPOS          * Print position
          MOVE      ZERO,COUNT
.
PRNX5200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX5500 IF LESS
          LOAD      FORM7,COUNT,TZPER1,TZPER2,TZPER3,TZPER4,TZPER5,TZPER6:
                                TZPER7,TZPER8,TZPER9,TZPER10,TZPER11,TZPER12:
                                TZPER13
          PRINT     *MPRTPOS,FORM7;
          ADD       FORM7,TZPERTOT     *  Add to group type total
          GOTO      PRNX5200
.
PRNX5500  PRINT     *MPRTPOS,TZPERTOT
          CALL      PRNX7000           * Print separators
          CALL      CLRV3000
          RETURN
.
.----------------------------------------------------------------------------
.         Print page separator type 1
.----------------------------------------------------------------------------
.
PRNX6000  MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
PRNX6100  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX6200 IF LESS
          PRINT     *MPRTPOS," ------";
          GOTO      PRNX6100
PRNX6200  PRINT     *MPRTPOS," ------"
          RETURN
.
.----------------------------------------------------------------------------
.         Print page separator type 2
.----------------------------------------------------------------------------
.
PRNX7000  MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
PRNX7100  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX7200 IF LESS
          PRINT     *MPRTPOS," ======";
          GOTO      PRNX7100
PRNX7200  PRINT     *MPRTPOS," ======"
          RETURN
.
.----------------------------------------------------------------------------
.         Process & print summary for average waiting times
.----------------------------------------------------------------------------
.
PRNX8000  MOVE      "80",CLNO
          MOVE      ZERO,CPAGENO             * clear page number
.
          MOVE      ZERO,WTNUMDOC
          MOVE      ZERO,WTTOTNEW
          MOVE      ZERO,WTTOTREV
          MOVE      ZERO,GRTOTNEW            * Group total for new visits
          MOVE      ZERO,GRTOTREV            * Group total for revisits
          MOVE      ZERO,GRTOTDOC            * No of doctors in group
          MOVE      ONE,FORM2                * Skip 1st print of group totals
.
.-------- display details ----
.
          DISPLAY   *P1:12,*EF,*V2LON,"*** Printing Records ***",*HOFF:
                    *P1:14,"Group Code  :",*P1:15,"Clinic Type :":
                    *P1:16,"Clinic ID   :"
.          
          UNPACK    SP30,KEY15,SAVCTYPE,SAVGROUP
          CALL      RDSTMP1
PRNX8100  CALL      RDKTMP1
          BRANCH    OVRCD,PRNX8999
.
          DISPLAY   *P15:14,*V2LON,TGROUP,*P15:15,TCTYPE,*P15:16,TCCLIN
.
          MATCH     SP3,SAVGROUP
          GOTO      PRNX8300 IF EQUAL
          MATCH     TGROUP,SAVGROUP
          GOTO      PRNX8250 IF NOT EQUAL
.
          MATCH     TCTYPE,SAVCTYPE
          GOTO      PRNX8500 IF NOT EQUAL
.
PRNX8200  ADD       ONE,WTNUMDOC             * Another doctor for this clin type
          ADD       TCWAITNW,WTTOTNEW
          ADD       TCWAITRV,WTTOTREV
          ADD       ONE,GRTOTDOC             * Another doctor for this group
          ADD       TCWAITNW,GRTOTNEW
          ADD       TCWAITRV,GRTOTREV
          GOTO      PRNX8100
.
PRNX8250  CALL      PRNX8900                 * Print previous clinic record
          CALL      PRNX8950                 * Print group totals
.
PRNX8300  MOVE      TGROUP,SAVGROUP
          MOVE      TCTYPE,SAVCTYPE
          MOVE      ZERO,FORM1               * Next time print group code
          GOTO      PRNX8200
.
PRNX8500  CALL      PRNX8900                 * Print previous clinic record
          MOVE      TCTYPE,SAVCTYPE          * Start adding for new clinic
          GOTO      PRNX8200
.
. ------- Print clinic type -------
.
.         FORM1 :  0=Print Group Code  1=Skip print of Group Code
.
PRNX8900  COMPARE   "60",CLNO
          GOTO      PRNX8910 IF LESS
          CALL      PRNX9000           * Print new page head
.
PRNX8910  DIV       WTNUMDOC,WTTOTNEW  * Total days/No of clinics = Avg days
          MOVE      WTTOTNEW,FORM7
          DIV       SEVEN,FORM7        * Avg days/7 = Avg weeks
          DIV       WTNUMDOC,WTTOTREV
          MOVE      WTTOTREV,FORM7Y
          DIV       SEVEN,FORM7Y
.
          BRANCH    FORM1,PRNX8920     * Skip print of group desc
.
PRNX8915  MOVE      ZERO,FORM2         * Don't skip print of group totals again
          PACK      KEY5,CODECG,SAVGROUP
          CALL      RDCODE1
          PRINT     *1,SAVGROUP,SP1,TDESC;
.
PRNX8920  MOVE      "*** Unknown Clinic Type ***   ",OCTDESC
          PACK      KEY12,IDDD,SAVCTYPE
          CALL      RDCTYA1
          PRINT     *26,SAVCTYPE,*37,OCTDESC,*76,WTTOTNEW:
                    *89,FORM7,*106,WTTOTREV,*123,FORM7Y
          MOVE      ZERO,WTNUMDOC
          MOVE      ZERO,WTTOTNEW
          MOVE      ZERO,WTTOTREV
          MOVE      ONE,FORM1                * Next time don't print group
          ADD       ONE,CLNO
          RETURN
.
. ------- Print group totals -------
.
PRNX8950  DIV       GRTOTDOC,GRTOTNEW  * Total days/No of clinics = Avg days
          MOVE      GRTOTNEW,FORM7
          DIV       SEVEN,FORM7        * Avg days/7 = Avg weeks
          DIV       GRTOTDOC,GRTOTREV
          MOVE      GRTOTREV,FORM7Y
          DIV       SEVEN,FORM7Y
          PRINT     *73,"----------",*86,"-----------":
                       *101,"--------------",*118,"---------------":
                    *N,*37,"Average Waiting Time for Group ",SAVGROUP:
                       *76,GRTOTNEW,*89,FORM7,*106,GRTOTREV,*123,FORM7Y:
                    *N,*73,"----------",*86,"-----------":
                       *101,"--------------",*118,"---------------"
          MOVE      ZERO,GRTOTNEW
          MOVE      ZERO,GRTOTREV
          MOVE      ZERO,GRTOTDOC
          ADD       THREE,CLNO
          RETURN
.
. ------- Finished -------
.
PRNX8999  CALL      PRNX8900           * Print last clinic's record
          CALL      PRNX8950           * Print group totals
          PRINT     *N,*N,"*** END OF REPORT ***"
          RETURN
.
.----------------------------------------------------------------------------
.         Print New Page
.----------------------------------------------------------------------------
.
PRNX9000  CLOCK     TIME,CTIMEIS
.
          UNPACK    VSTRPER,XCENT,XYEAR,XMON
          UNPACK    VENDPER,CCENT,CYEAR,CMON
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
          PRINT     *N,*40,"AVERAGE WAITING TIMES":
                    *N,*40,"PERIOD FROM ",XMON,SLASH,XCENT,XYEAR," TO ":
                       CMON,SLASH,CCENT,CYEAR:
                    *N,*N,"Group Type",*24,"Clinic Type",*37,"Description":
                       *73,"New (Days)",*86,"New (Weeks)":
                       *101,"Revisit (Days)",*118,"Revisit (Weeks)":
                    *N,"--------------------",*24,"-----------":
                       *37,"------------------------------":
                       *73,"----------",*86,"-----------":
                       *101,"--------------",*118,"---------------"
          MOVE      SEVEN,CLNO
          RETURN
.
+
.**************************************************************************
.*                              CREA0000                                  *
.*                   Create temp file                                     *
.**************************************************************************
.
CREA0000  CALL      KILL0000
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    OUTTMP,CMDLINE
          APPEND    " 105 u1-15",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      OUTTMPXX,OUTTMP
          RETURN
+
.**************************************************************************
.*                              KILL0000                                  *
.*                   Remove temp file                                     *
.**************************************************************************
.
KILL0000  PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    OUTTMP,CMDLINE
          RESET     CMDLINE
          CLOSE     OUTTMPXX
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.------------------------------------------------------------------------------
.         Temp file I/O's
.------------------------------------------------------------------------------
.
RDATMP1   MOVE      ZERO,OVRCD
          RESET     KEY15
          READ      OUTTMPXX,KEY15;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTMP1    MOVE      ZERO,OVRCD
          RESET     KEY15
          READ      OUTTMPXX,KEY15;TGROUP,TCTYPE,TCCLIN:
                             TCPER1,TCPER2,TCPER3,TCPER4:
                             TCPER5,TCPER6,TCPER7,TCPER8:
                             TCPER9,TCPER10,TCPER11,TCPER12,TCPER13:
                             TCNXTNEW,TCWAITNW,TCNXTREV,TCWAITRV
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSTMP1   MOVE      ZERO,OVRCD
          RESET     KEY15
          READ      OUTTMPXX,KEY15;;
          RETURN
.
RDKTMP1   MOVE      ZERO,OVRCD
          READKS    OUTTMPXX;TGROUP,TCTYPE,TCCLIN:
                             TCPER1,TCPER2,TCPER3,TCPER4:
                             TCPER5,TCPER6,TCPER7,TCPER8:
                             TCPER9,TCPER10,TCPER11,TCPER12,TCPER13:
                             TCNXTNEW,TCWAITNW,TCNXTREV,TCWAITRV
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMP1    MOVE      ZERO,OVRCD
          RESET     KEY15
          WRITE     OUTTMPXX,KEY15;KEY15:
                                  TCPER1,TCPER2,TCPER3,TCPER4:
                                  TCPER5,TCPER6,TCPER7,TCPER8:
                                  TCPER9,TCPER10,TCPER11,TCPER12,TCPER13:
                                  TCNXTNEW,TCWAITNW,TCNXTREV,TCWAITRV
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMP1    UPDATE    OUTTMPXX;KEY15,TCPER1,TCPER2,TCPER3,TCPER4:
                                   TCPER5,TCPER6,TCPER7,TCPER8:
                                   TCPER9,TCPER10,TCPER11,TCPER12,TCPER13:
                                   TCNXTNEW,TCWAITNW,TCNXTREV,TCWAITRV
          RETURN
+
. =========================================================================
. Includes for all file I/O would be included here.
. Includes for all procedural routines would be included here.
. =========================================================================
.
           INC        STD001IO
           INC       TFILENAM
           INC       IBASEQIO/INC
           INC       OUTBOAIO/INC
           INC       OUTCLIIO/INC
           INC       OUTCTYIO/INC
           INC       OUTMA1IO/INC
           INC       PATCODIO/INC
           INC       PATDO1IO/INC
           INC        PATDRGIO/INC
           INC       WEBERRIO/INC
           INC        PATCOMN3
.

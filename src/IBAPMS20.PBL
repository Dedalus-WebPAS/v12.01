. ****************************************************************************
. *              P R O G R A M  S U M M A R Y                                *
. *              -------------  -------------                                *
. *                                                                          *
. *     PROGRAM NAME:     IBAPMS20                                           *
. *                                                                          *
. *     FUNCTION:         HOSPITAL CENSUS REPORT                             *
. *                                                                          *
. *     DATE WRITTEN:     14/05/90                                           *
. *                                                                          *
. *     AUTHOR:           DIG (Adapted from IBABIL61)                        *
. *                                                                          *
. *     MODIFICATIONS:                                                       *
.*        V12.00.01 21/05/2025 Nikitha B       TSK 0955096                    *
.*                  Alphanumeric changes                                      *
.*        V11.01.01 13/04/2021  J.Tan           TSK 0863287                   *
.*                  Recompiled for STEPDOWN -Patient Invoice new functionality*
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*        V10.10.01 13/06/2017  J.Tan          TSK 0839991                    *
.*                  Recompiled for PATDBTYP (PATCMSTP)                        *
.*        V10.04.02 15/04/2014  J.Tan          CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V10.04.01 27/05/2014  J.Tan          CAR 300784                     *
.*                  Recompiled for STEPDOWN - force stepdown prior inv.fromdat*
.*        V10.03.03 28/06/2013  J.Tan          CAR 287554                     *
.*                  Changed FROMDATE to KFRDATE                               *
.*        V10.03.02 06/05/2013  J.Tan          CAR 284902                     *
.*                  Recompiled for STEPDOWN - force stepdown prior Inv.Fromdat*
.*        V10.03.01 12/09/2012  J.Tan         CAR 267784                      *
.*                  Recompiled for STEPDOWN - Checking TCINDC19 claim code    *
.*        V10.01.01 24/02/2011  Peter Vela    CAR 233034                      *
.*                  Removed PATBF2FD and PATBF2IO                             *
.*        V10.00.01 14/07/2010  J.Tan         CAR 226158                      *
.*                  Recompiled for STEPDOWN - bed fees update                 *
.*        V9.12.04  11/03/2010  J.Tan         CAR 216044                      *
.*                  Mods to include Leave days in stepdown count for TAC      *
.*        V9.12.03  01/02/2010  Mike Laming  CAR 212840                       *
.*                  Mods to correct 3 bugs - see "212840" - some code cleanup *
.*        V9.12.02  19/01/2010  J.Tan         CAR 166439                      *
.*                  Recompiled for PATCMSTP - C/P additional charge stepdown  *
.*        V9.12.01  21/09/2009  J.Tan         CAR 204390                      *
.*                  Recompiled for STEPDOWN - mods to old bed fees file       *
.*        V9.08.01  02/02/2007  Ebon Clements CAR 120001                      *
.*                  Added nz private billing files                            *
.*        V9.07.01  27/07/2006  Peter Vela    CAR 112261                      *
.*                  Modified to date in PTRN7000                              *
.*                  Added TMOVE=D check PTRN5000                              *
.*        V9.04.02  29/11/2004  Mike Laming   CAR 54534                       *
.*                  Re-comple for PATCMSTP - fix Addit.Charge Description     *
.*        V9.04.01  20/10/2004  Lina Vo       CAR 54095                       *
.*                  Fixed census report to display all admission types even   *
.*                  ones that are invalid.  This is so the report bed days    *
.*                  and amount matches.                                       *
.*                  Fixed looping in PTRN5000 after STEPDOWN.                 *
.*                  Fixed bed days and amout calculations to be similar to    *
.*                  IBAHMS21.                                                 *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
. *        V9.02.06  30/10/2002  J.Tan                                       *
. *                  Recompiled for PATCMSTP                                 *
. *        V9.02.05  20/06/2002  Mike Laming  SRF 15847                      *
. *                  Add a Reposition on TRAN2 after STEPDOWN                *
. *        V9.02.04  06/06/2002  J.Tan                                       *
. *                  Removed checking for hospital types                     *
. *        V9.02.03  07/05/2002  J.Tan                                       *
. *                  Recompiled for release 6.09 changes                     *
. *        V9.02.02  06/12/2001  Tonii                                       *
. *                  Removed episode type for casemix payment                *
. *        V9.02.01  22/09/2001  Tonii  SRF 13075                            *
. *                  Recompiled for PATDINVS                                 *
. ****************************************************************************
. *        V5.08.02  04/09/2000  Tonii                                       *
. *                  Mods to accept Unknown and Indeterminate as valid       *
. *                  patient sex description                                 *
. *        V5.08.01  16/08/2000  Caleb Sharman                               *
. *                  Changed Health Fund variables to be 8 chars             *
. *        V5.07.02   25/11/1999  Davin                                      *
. *                   Changed for 4 character drgs                           *
. *        V5.07.01   16/06/1999  J.Tan                                      *
. *                   Mods to initialise episode type                        *
. *        V5.07.B01  18/05/1999  Steve Armstrong    5.07 mods               *
. ****************************************************************************
. *                   V5.01.121 27/11/90 Glen Hobby        *
. *                            Recompiled for changes to   *
. *                            PATMX1FD                    *
. *                                                        *
. *        V5.01.122 23.02.92 Neeriem "I Hate Support" Dye *
. *                  Modify to use standard routine KEYDATE*
. *        V5.01.123 10.03.92 Neeriem "I Hate Support" Dye *
. *                  Fix display if nothing in date range  *
. *        V5.01.124 19/04/93 DIG                          *
. *                  Made sure the TEXTRA variable is now  *
. *                  used for additional charging.         *
. **********************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
          INC       IBACONFD/INC
          INC       NZPBFEFD/INC
          INC       PATASDFD/INC
          INC       PATHDFFD/INC
          INC       PATHLFFD/INC
          INC       PATHSPFD/INC
          INC       PATLSDFD/INC
          INC       PATLDFFD/INC
          INC       PATAFEFD/INC
          INC       PATFN1FD/INC
          INC       PATFX1FD/INC
          INC       PATBFEFD/INC
          INC       PATRCAUD/INC
          INC       PATWR1FD/INC
          INC       PATDINVS/INC
.
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          iNC       PATDAYFD/INC
          INC       PATDSCFD/INC
          INC       PATTRNFD/INC
          INC       PATCODFD/INC
          INC       PATCFAFD/INC
          INC       PMSCIDFD/INC
          INC       PMSVX1FD/INC
          INC       WEBSECFD/INC
.
.
.          Temporary index files for report
.
.
.         Filename : pms20axx.dat          (xx = port id)
.
PATTEMP1  IFILE     SQL, FIXED=14, KEY="U1-8"
DCOUNT    DIM       5
.
.
.         Filename : pms20bxx.dat          (xx = port id)
.
PATTEMP2  IFILE     SQL, FIXED=31, KEY="U1-4"
.
. NAME    TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----    ----   ------   --------  ----------   -----------
DCNDAYC   DIM       1         1         1        CENSUS DAYCASE FLAG
CNCODE    DIM       3         3         2        CENSUS ADMISSION TYPE
CNABED    FORM      8         5         5        CENSUS ADULTS BED DAYS
CNAAMT    FORM    12.2        8        10        CENSUS ADULTS AMOUNT
CNCBED    FORM      8         5        18        CENSUS CHILD BED DAYS
CNCAMT    FORM    12.2        8        23        CENSUS CHILD AMOUNT
.
. End of Record                        31
.
.  redefine form fields from key
.  -----------------------------
. NAME    TYPE      LENGTH     START LOC.   DESCRIPTION
. ----    ----      ------     ----------   -----------
CNDAYC    FORM         1           1        CENSUS DAYCASE FLAG
+
.
.
. LOCAL VARIABLES
. ---------------
.
AMTCODE   FORM      12.2
ASDATE    DIM       8
.
BEDCODE   FORM      8
BOBED     FORM      8
BOAMT     FORM      12.2
.
CADAYS    FORM      8
CAGECOFF  FORM      2
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CHG       FORM      1
CMDLINE   DIM       50
CUDATE    DIM       8
CAUDQ     FORM      1
CDLSTEP   FORM      1
CHOSTYP   FORM      1
CSTDOWN   FORM      1
CFEETYP   FORM      1
CALDAYS   FORM      3
.
DIM4      DIM       4
DIM18     DIM       18
DIM18A    DIM       18
DIM19A    DIM       19
DIM30     DIM       30
DNAME     DIM       30
DISCFLG   FORM      1
EFTDATE   DIM       8         * Effective date (ccyymmdd)
.
.....FEAMT     FORM      12.2                                           D-49016
.....FEDAYS    FORM      8                                              D-49016
FNAMEI    DIM       8
FORM3A    FORM      3
FORM5B    FORM      5
.
GATOTAL   FORM      12.2
GBTOTAL   FORM      8
.
.....IDAMT     FORM      12.2    * Amount of Indeterminate patient      D-49016
.....IDDAYS    FORM      8       * Number of bed days for Indeterm      D-49016
.
IGNWRD    DIM       3
.
JDAYS     FORM      3
JYEAR     FORM      2
KFRDATE   DIM       8
.
LNO       FORM      2
LYR       FORM      1                 * Leap year indicator - 0=No, 1=Yes
LSTDATE   DIM       8
.
.....MAAMT     FORM      12.2                                           D-49016
.....MADAYS    FORM      8                                              D-49016
.
OPNFLAG   FORM      "0"
PTCNCNDY  FORM      1
PTCNIDES  FORM      1
PTRESDAY  FORM      1
PTCNADES  FORM      1
PTCNBCUT  DIM       4
PTCNDCLM  DIM       3
PTCNXCHS  DIM       1
PTCNCLEV  DIM       3
PTCNPPIN  DIM       1
.
SAVAMT    FORM      12.2
SAVDISC   FORM      12.2
SAVALLW   FORM      12.2
SAVEXTRA  FORM      12.2
SAVBEND   FORM      3
SAV30     DIM       30                                                 *I-90205
.
SDAYMON   DIM       4
SEC       FORM      2
SEXAMT    FORM      12.2[9]                                            *I-49016
SEXDAYS   FORM      8[9]                                               *I-49016
.
SRBDAY    DIM       4
SRYEAR    FORM      4
STATUS    FORM      1
SYEAR     DIM       4
.
TDAYMON   DIM       4
TEMPFIL1  DIM       8
TEMPFIL2  DIM       8
TMPADMN   DIM       8
TTDATE0   DIM       8                       * version of TTDATE for "paydayaf"
TTDATE    DIM       8
RYEAR     DIM       4
.
.....IDAMT     FORM      12.2    * Amount of Unknown patients           D-49016
.....IDDAYS    FORM      8       * Number of bed days for Unknown       D-49016
.
WDATE     DIM       8
WDATE1    DIM       8
WDATE2    DIM       8
WSTAT     FORM      1
.
XDATE1    DIM       8
XDAY2     DIM       2
XMON2     DIM       2
XYEAR2    DIM       2
XCENT2    DIM       2
.
.
. PROGRAM CONSTANTS
. -----------------
.
INVALID   INIT      "Invalid Code "
SP14      INIT      "              "
UKEY1     INIT      " 14 U1-8"
UKEY2     INIT      " 31 U1-4"
.
.
PRGID     INIT      "IBAPMS20"
PRGNAM    INIT      "Hospital Census Report"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patmi1af";
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"nzpbfeaf";
          OPEN      NZPBFEA1,"nzpbfeaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pmscidaf";
          OPEN      PMSCIDA1,"pmscidaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*94,CAGECOFF,*217,CAUDQ
          READ      CONTROLF,TEN3;*245,CHOSTYP
          READ      CONTROLF,TEN5;*247,CSTDOWN
          READ      CONTROLF,TWENTY;*164,PTRESDAY,*199,PTCNBCUT,*211,PTCNCNDY
          READ      CONTROLF,TWENTY8;*120,IGNWRD
          READ      CONTROLF,FIFTY9;*1,CDLSTEP
          READ      CONTROLF,EIGHTY8;*233,PTCNADES
          READ      CONTROLF,TEN9;*212,CFEETYP
          READ      CONTROLF,HUND29;*89,PTCNPPIN
.
          CALL      IBACLOCK
          PACK      CUDATE,CCC,CYY,CMM,CDD
          REP       " 0",CUDATE
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TEMPFIL1
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TEMPFIL2
+
.
.        START OF PROGRAM
.
START     
+
.
.             KEYIN TO DATE
.
KADAT1    DISPLAY   *P1:4,*EF,"Keyin From date :":
                    *P1:6,"Keyin To   date :"
.
          MOVE      ZERO,CCANLDTE
          MOVE      ONE,CDEFDTE
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      FOUR,CVERT
          MOVE      TEN9,CCOL
          CALL      KEYDATE
          BRANCH    OVRCD,ENDIT
.
          PACK      KFRDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KFRDATE
.
          MATCH     KFRDATE,CUDATE
          GOTO      INVDTE1 IF LESS
.
          UNPACK    KFRDATE,XCENT1,XYEAR1,XMON1,XDAY1
.
.             Key in from date
.
KADAT2    MOVE      SIX,CVERT
          MOVE      TEN9,CCOL
          CALL      KEYDATE
          BRANCH    OVRCD,KADAT1
.
          PACK      TTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TTDATE
.
          MATCH     TTDATE,CUDATE
          GOTO      INVDTE2 IF LESS
.
          MATCH     KFRDATE,TTDATE
          GOTO      INVDATG IF LESS
.
          MOVE      TTDATE,TTDATE0
          UNPACK    TTDATE,XCENT2,XYEAR2,XMON2,XDAY2
.
.       Add one to the TTDATE. This is because initially the program did not
.       include the end date, so it looks at all the data upto but including
.       the TTDATE.
.
          MOVE      CDAY,DD
          MOVE      CMON,MM
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          CALL      DMYCON
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
.
          PACK      TTDATE,CC,YY,MM,DD
          REP       " 0",TTDATE
          GOTO      REKEY
.
INVDATG   DISPLAY   *P1:24,*B,*EL,*V2LON,"** From Date must be <= ":
                    "To Date **",*W2;
          GOTO      KADAT1
.
INVDTE1   DISPLAY   *P1:24,*B,*EL,*V2LON,"** From Date must be <= ":
                    "current Date **",*W2;
          GOTO      KADAT1
.
INVDTE2   DISPLAY   *P1:24,*B,*EL,*V2LON,"** To Date must be Less Than or":
                    " Equal to current Date **",*W2;
          GOTO      KADAT2
.
.        Check if dates are OK or not
.
REKEY     KEYIN     *P1:24,*EF," Dates ok (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     "N" TO ANS
          GOTO      START IF EQUAL
.
          MATCH     "Y" TO ANS
          GOTO      PROC0000 IF EQUAL
.
          BEEP
          GOTO      REKEY
.
.******************************************************************************
.            PROCESS START
.******************************************************************************
PROC0000  CALL      INITV000                * initialise variables
          CALL      INITC000                * initialise Census file
.
          CALL      DAYA0000     * load Admissions temp file from patdayaf file
.
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Processing Admission Numbers **",*W;
          DISPLAY   *P1:24,*EL,*P20:24,"Admission No :":
                    *P56:24,"Scanning :";
.
.
.------ read through the indexed temp file and process each admission ------
.
          MOVE      SP8,KEY8
          CALL      RSTMPR1
PROC1000  CALL      RKTMPR1
          BRANCH    OVRCD,REPORT00
.
          MOVE      TMPADMN,KEY8
          CALL      RDMISS1              * read the admissions file
          BRANCH    OVRCD,PROC1000
.
PROC1500  MATCH     TTDATE,ADATE         * make sure admission is in date range
          GOTO      PROC1000 IF NOT LESS
.
          DISPLAY   *P68:24,AADMNO;
          MOVE      SP8,DDATE
.
..                        1=PreAd  2=CurrA  3=Disch  4=OnLev  5=Cancel
          BRANCH    ASTAT,PROC1000,PROC3000,PROC2000,PROC3000,PROC1000
.
PROC2000  MOVE      AADMNO,KEY8
          CALL      RDDSCH1                 * read discharge file
          BRANCH    OVRCD,PROC1000
.
          MATCH     KFRDATE,DDATE          * check Discharge is in date range
          GOTO      PROC1000 IF LESS
.
.------ we have a valid admission so extract details ------
.------ and write to summary file ------
.
PROC3000  DISPLAY   *P35:24,*V2LON,AADMNO;
.
          CALL      PTRN0000                * read Transfer records
          GOTO      PROC1000
+
.*****************************************************************************
.             INITIALISE WORKING VARIABLES
.*****************************************************************************
INITV000  MOVE      ZERO,FORM1 
.
          CALL      CLRSEX00                * Clear counts by sex  
.
          MOVE      ZERO,GBTOTAL
          MOVE      ZERO,GATOTAL
          MOVE      ZERO,AMTCODE
          MOVE      ZERO,BEDCODE
          MOVE      ZERO,BOAMT
          MOVE      ZERO,BOBED
          MOVE      ZERO,CADAYS
          MOVE      ZERO,SAVAMT
          RETURN
+
.******************************************************************************
.              INITIALISE CENSUS FILE
.******************************************************************************
INITC000  DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Initialising Census File **",*W2,*P1:10;
.
          CALL      CREA0000                     * create temp files
.
          DISPLAY   *P1:10,*EF,*P20:24,"Initialising Census Code :"
          MOVE      "A ",TCODE
.
          PACK      KEY5 WITH TCODE,SP3
          CALL      RDSCODE1
INITC100  CALL      RDKCODE1
          BRANCH    OVRCD,INITC999
.
          MATCH     "A ",TCODE
          GOTO      INITC999 IF NOT EQUAL
.
          DISPLAY   *P47:24,*V2LON,ACODE;
.
          MOVE      ZERO,CNDAYC
          PACK      KEY4,CNDAYC,ACODE,SP4
.
          CALL      RDCENS1
          BRANCH    OVRCD OF INITC200
.
          MOVE      ZERO,CNABED
          MOVE      ZERO,CNCBED
          MOVE      ZERO,CNAAMT
          MOVE      ZERO,CNCAMT
          CALL      UPCENS1
          GOTO      INITC300
.
INITC200  MOVE      ZERO,CNABED
          MOVE      ZERO,CNCBED
          MOVE      ZERO,CNAAMT
          MOVE      ZERO,CNCAMT
          CALL      WRCENS1
.
.          Day case records
.
INITC300  MOVE      ONE,CNDAYC
          PACK      KEY4,CNDAYC,ACODE,SP4
.
          CALL      RDCENS1
          BRANCH    OVRCD,INITC200
.
          MOVE      ZERO,CNABED
          MOVE      ZERO,CNCBED
          MOVE      ZERO,CNAAMT
          MOVE      ZERO,CNCAMT
          CALL      UPCENS1
          GOTO      INITC100
.
INITC400  MOVE      ZERO,CNABED
          MOVE      ZERO,CNCBED
          MOVE      ZERO,CNAAMT
          MOVE      ZERO,CNCAMT
.
          CALL      WRCENS1
          GOTO      INITC100
.
INITC999  RETURN
+
.**********************************************************************
.*                            DAYA0000                                *
.*     Process patdayaf Data writing to temp indexed file patmp1xx    *
.**********************************************************************
DAYA0000  MOVE      ZERO,FORM5A
          MOVE      ZERO,FORM5B
          MOVE      SP5,DCOUNT
...       UNPACK    TTDATE,CCENT,CYEAR,TDAYMON                        *D-212840
          UNPACK    TTDATE0,CCENT,CYEAR,TDAYMON                       *I-212840
          PACK      RYEAR,CCENT,CYEAR
          REP       " 0",RYEAR
.
          UNPACK    KFRDATE,CCENT,CYEAR,SDAYMON
          PACK      SYEAR,CCENT,CYEAR
          REP       " 0",SYEAR
.
          DISPLAY   *P1:24,*EL,*V2LON,*BLINKON,"Initialising the Indexed Temp":
                    " File",*HOFF," - Please Wait";
.
.------ close previous day file and set up the name of the next one ------
.
DAYA1000  CLOSE     PATDAYA1
          PACK      CFNAME,FILDAYA1,SYEAR
.
          TRAP      DAYA4900 IF IO        * trap if file does not exist
          OPEN      PATDAYA1,CFNAME
          TRAPCLR   IO
.
          PACK      KEY12,SDAYMON,SP10    * position on daily patient list file
          CALL      RSPTDAY1
.
.------ read through the patient daily list file ------
.
DAYA2000  CALL      RKPTDAY1
          BRANCH    OVRCD,DAYA5000
.
          MATCH     RYEAR,SYEAR            * see if the current file is the one
          GOTO      DAYA3000 IF NOT EQUAL    for the last year in the date range
.
          MATCH     PTDYDATE,TDAYMON       * if so, make sure the month and day
          GOTO      DAYA9999 IF LESS         are still in range
.
DAYA3000
.------ Only need to write to the temporary file if the admission number -----
.------ is not already there ------
.
          MOVE      PTDYADMN,KEY8
.
DAYA3500  ADD       ONE,FORM5A              * count "paydayaf" Bed days
          CALL      RDTMPR1                 * read the temp file
          BRANCH    OVRCD,DAYA4000
.
          GOTO      DAYA2000
.
.------ write a new record to the temp file ------
.
DAYA4000  MOVE      SP5,DCOUNT
          CALL      WRTMPR1
          GOTO      DAYA2000
.
.------ need to pop the return address off the stack if I/O error trapped -----
.
DAYA4900  NORETURN
.
.------ either file does not exist or we have reached the end of the ------
.------ current file ------
.
DAYA5000  MATCH     RYEAR,SYEAR             * test if we have reached the end
          GOTO      DAYA9999 IF NOT LESS      year in the date range
.
          MOVE      SYEAR,FORM4             * if not, increment the start year
          ADD       ONE,FORM4                 to the next year and process
          MOVE      FORM4,SYEAR               the file for this year
          REP       " 0",SYEAR
          MOVE      "0101",SDAYMON
.
          GOTO      DAYA1000
.
DAYA9999  RETURN
+
.**********************************************************************
.              READ TRANSFER FILE
.**********************************************************************
PTRN0000  MOVE      SP8,STDATE
          MOVE      ZERO,SAVAMT
          MOVE      " ",STMOVE
          MOVE      SP3,STATYPE
          MOVE      ZERO,CNDAYC
          MOVE      ZERO,DISCFLG
          MOVE      ZERO,ACDAYCS            * reset daycase flag      *I-212840
.
          MATCH     ANSY,PTMICMXP
          IF        @EQUAL
            IF        ASTAT=3
              MATCH     ADATE,DDATE
              IF        @EQUAL
                MOVE      ONE,ACDAYCS       * set one to daycase flag
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      SP10,SIDATET
          MOVE      SP10,LSTDATE
          MOVE      ZERO,ENDFLAG
          MOVE      ZERO,SPTTREPS
          MATCH     SP8,ALACDTE 
          IF        !@EQUAL
            MOVE      ALACDTE,IDATEF        * set starting date for next inv.
          ENDIF
          MOVE      IDATEF,SIDATET          * set starting date for next inv.
.
          CALL      GTAB0000                * get health fund table
          PACK      CMDRG,PTMIPCMX,SP20     * Default to provisional DRG code
.
          UNPACK    SP10,KEY4,KEY5
          UNPACK    PTMIPCMX,KEY4,KEY5
          MATCH     SP5,KEY5
          IF        @EQUAL
            IF        ASTAT = 3
              MATCH     SP4,PTDSDDRG
              IF        !@EQUAL
                PACK      CMDRG,PTDSDDRG,SP20        * Use discharge DRG Code
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     ADATE,DDATE              * is this a Daycase
          IF        @EQUAL
            CALL      DAYCAS00               * process Daycase
            GOTO      PTRN9999               * & finish
          ENDIF
.
.                                            * process an over-night patient
          PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2
PTRN1000  CALL      RDKTRAN2
          BRANCH    OVRCD OF PTRN5000
.
          MATCH     TADMN,AADMNO
          GOTO      PTRN5000 IF NOT EQUAL
.
..          PACK      SAV30,DTADMN,TDATE,TTIME,TWARD,TBED              *I-90205
.
          MOVE      TMOVE,STMOVE
...       RESET     TDATE
...       REP       " 0",TDATE
...       RESET     TDATE
.
          MOVE      TDATE,XDATE1
          MOVE      TDATE,LSTDATE
.
          MATCH     ANSY,PTMICMXP
          IF        @EQUAL
            BRANCH    ACDAYCS,PTRN1100      * sameday, no stepdown (redundant?)
            CALL      PATCMSTP
            GOTO      PTRN1100  
          ENDIF
.
          COMPARE   ONE,CSTDOWN
          CALL      STEPDOWN IF EQUAL
.
PTRN1100  COMPARE   ZERO,ENDFLAG
          GOTO      PTRN1200 IF EQUAL
.
          MATCH     LSTDATE,TDATE
          GOTO      PTRN2100 IF EQUAL
.
PTRN1200  MATCH     "A",TMOVE
          GOTO      PTRN3500 IF EQUAL
.
          MATCH     "R",TMOVE
          GOTO      PTRN3500 IF EQUAL
.
          MATCH     "C",TMOVE
          GOTO      PTRN2500 IF EQUAL
.
          MATCH     "L",TMOVE
          GOTO      PTRN2500 IF EQUAL
.
.         Discharge Record    TMOVE = "D"
.
          MATCH     KFRDATE,TDATE
          GOTO      PTRN9999 IF LESS
          MOVE      ONE,DISCFLG
          MATCH     TTDATE,TDATE
          GOTO      PTRN5000 IF LESS       * it's a "D", about to finished
          MOVE      TTDATE,TDATE
          GOTO      PTRN5000
.
.
PTRN2100  MOVE      TATYPE,STATYPE
          MOVE      TRBTYP,STRBTYP
          MOVE      TRBEND,STRBEND
          MOVE      PTTRAEND,SPTTRAEN
          MOVE      PTTREPSD,SPTTREPS
          MOVE      TDATE,SIDATET
.
          GOTO      PTRN1000
.
.
.         End of a period, calculate the data needed, and then start new period
.         On-leave and Change Record
.
PTRN2500  MATCH     KFRDATE,TDATE
          GOTO      PTRN3500 IF LESS
.
          MATCH     TTDATE,TDATE
          GOTO      PTRN3000 IF LESS
.
          MOVE      TTDATE,CDYSTDTE
          MOVE      STDATE,CDYSFDTE
.
          GOTO      PTRN9000
.
PTRN3000  MOVE      TDATE,CDYSTDTE
          MOVE      STDATE,CDYSFDTE
          CALL      CALCDY00
.
.       Save new parameters for the start of the new period
.         On-Return and Admission Record
.
PTRN3500  MATCH     TTDATE,TDATE
          GOTO      PTRN9999 IF NOT LESS
          MATCH     KFRDATE,TDATE
          GOTO      PTRN4000 IF NOT LESS
          MOVE      KFRDATE,STDATE
          GOTO      PTRN4500
.
PTRN4000  MOVE      TDATE,STDATE
.
PTRN4500  MOVE      TRATEF,SAVAMT
          ADD       TRATEH,SAVAMT
..          ADD       PTTRADPT,SAVAMT
..          ADD       PTTRADRB,SAVAMT
..          ADD       TEXTRA,SAVAMT
..          SUB       TDISC,SAVAMT
..          SUB       TALLW,SAVAMT
          MOVE      TDISC,STDISC
          MOVE      TALLW,STALLW
.
          MOVE      TATYPE,STATYPE
          GOTO      PTRN2100
.
.                                             * no more "Tran" records - check
.                                             * save data for next action
PTRN5000  MATCH     "L",STMOVE
          GOTO      PTRN9999 IF EQUAL         * on leave - no more calcs.
          MATCH     " ",STMOVE
          GOTO      PTRN9999 IF EQUAL         * no Tran record - no more calcs
.
          MATCH     "D",STMOVE
          IF        @EQUAL
            MATCH     STDATE,DDATE            * if DDATE equal to store date
            GOTO      PTRN9999 IF EQUAL
          ENDIF
.
          IF        DISCFLG = 1
            MOVE      TDATE,XDATE1
          ELSE
            PACK      XDATE1,CCC,CYY,CMM,CDD   * load end XDATE1 with high val.
            REP       " 0",XDATE1
          ENDIF
.
          MATCH     ANSY,PTMICMXP
          IF        @EQUAL
            BRANCH    ACDAYCS,PTRN7000      * sameday, no stepdown (redundant?)
            MOVE      XDATE1,TDATE
            MOVE      SP1,TMOVE
            LOAD      TMOVE,ASTAT,ANSC,ANSC,ANSC,ANSR
            MOVE      STATYPE,TATYPE
            MOVE      STRBTYP,TRBTYP
            MOVE      STRBEND,TRBEND
            MOVE      SPTTRAEN,PTTRAEND
            MOVE      SPTTREPS,PTTREPSD
            CALL      PATCMSTP
            MOVE      ONE,ENDFLAG
            GOTO      PTRN6500
          ENDIF
.
.         Check if stepdown is required
.
          COMPARE   ONE,CSTDOWN
          GOTO      PTRN7000 IF NOT EQUAL
.
          MOVE      XDATE1,TDATE
          MOVE      SP1,TMOVE
          LOAD      TMOVE,ASTAT,ANSC,ANSC,ANSD,ANSR      * "C" to "D" *C-212840
          MOVE      STATYPE,TATYPE
          MOVE      STRBTYP,TRBTYP
          MOVE      STRBEND,TRBEND
          CALL      STEPDOWN
          BRANCH    NOFEE,PTRN7000                     * was PTRN8000 *C-212840
          MOVE      ONE,ENDFLAG
.
.         Check if a stepdown was indicated = shown by a new TDATE
.
PTRN6500  MATCH     TDATE,XDATE1
          GOTO      PTRN7000 IF EQUAL
.
          MATCH     TDATE,LSTDATE
          GOTO      PTRN1100 IF LESS
          GOTO      PTRN2100
.
PTRN7000  MATCH     "D",STMOVE
          GOTO      PTRN8000 IF NOT EQUAL
.
          MATCH     TTDATE,LSTDATE
          GOTO      PTRN8000 IF NOT LESS
.
          MOVE      LSTDATE,CDYSTDTE
          MOVE      STDATE,CDYSFDTE
          GOTO      PTRN9000
.
PTRN8000  MOVE      TTDATE,CDYSTDTE
          MOVE      STDATE,CDYSFDTE
.
PTRN9000  CALL      CALCDY00
.
PTRN9999  RETURN
+
.**********************************************************************
.       Day Case - Get last rate for this patient
.**********************************************************************
DAYCAS00  PACK      KEY30,AADMNO,SP30
          ADD       ONE,FORM5B              * count number of Day Cases
          CALL      RDSTRAN2
DAYCAS10  CALL      RDKTRAN2
          BRANCH    OVRCD OF DAYCAS20
.
          MATCH     TADMN,AADMNO
          GOTO      DAYCAS20 IF NOT EQUAL
.
          CMATCH    "D",TMOVE
          GOTO      DAYCAS20 IF EQUAL
.
          MOVE      TRATEF,SAVAMT
          ADD       TRATEH,SAVAMT
..          ADD       PTTRADPT,SAVAMT
..          ADD       PTTRADRB,SAVAMT
..          ADD       TEXTRA,SAVAMT
          SUB       TDISC,SAVAMT
          SUB       TALLW,SAVAMT
          GOTO      DAYCAS10
.
DAYCAS20  MOVE      AURNO,KEY8
          CALL      RDMAST1
.
          MOVE      ONE,CNDAYC
          PACK      KEY4,CNDAYC,TATYPE,SP4
          CALL      RDCENS1
          IF        OVRCD=1               
            MOVE      ZERO,CNABED
            MOVE      ZERO,CNCBED
            MOVE      ZERO,CNAAMT
            MOVE      ZERO,CNCAMT
            CALL      WRCENS1
            PACK      KEY4,CNDAYC,TATYPE,SP4
            CALL      RDCENS1               * reread record for Update
            BRANCH    OVRCD,DAYCAS50
          ENDIF
.
          MOVE      ADATE,ASDATE
.
          CALL      PATAGED                 * calculate age using admission date
.
          COMPARE   CAGECOFF,PSAGE
          GOTO      DAYCAS30 IF LESS
.
          ADD       ONE,CNABED
          ADD       SAVAMT,CNAAMT
          GOTO      DAYCAS40
.
DAYCAS30  ADD       ONE,CNCBED
          ADD       SAVAMT,CNCAMT
.
DAYCAS40  CALL      UPCENS1

DAYCAS50  ADD       ONE,CADAYS              * For Grandtotal
.
DAYCAS99  RETURN
+
.**********************************************************************
.              CALCULATE DATES DIFFERENCE
.**********************************************************************
CALCDY00  MOVE      ZERO,EXIT
.
          CALL      CALCDAYS
          ASSIGN    (CDYSDAYS-1),TOTDAY
.
.         READ PATIENT MASTER FILE
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          COMPARE   ONE,OVRCD
          GOTO      CALCDY99 IF EQUAL
.
          SUB       STDISC,SAVAMT
          SUB       STALLW,SAVAMT
          MULT      TOTDAY,SAVAMT
.
          COMPARE   ZERO,TOTDAY
          GOTO      CALCDY99 IF EQUAL
.
          IF        @LESS
...         DISPLAY   *P1:23,*EL,"*** Transfer Record Missing for ":
...                   "Admission : ",*V2LON,AADMNO:
...                   *HOFF," Contact I.B.A ***"
.
            PRINT     *N,"*** Transfer Record Missing for ":
                      "Admission : ",AADMNO," Contact I.B.A ***"
          ENDIF
.
CALCDY20  MATCH     IGNWRD,STWARD
          GOTO      CALCDY99 IF EQUAL
.
.
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          ADD       TOTDAY,SEXDAYS[DSEXNO]
          ADD       SAVAMT,SEXAMT[DSEXNO]
.
CALCDY30  MOVE      ZERO,CNDAYC
          PACK      KEY4 WITH CNDAYC,STATYPE
          CALL      RDCENS1
          IF        OVRCD=1                
            MOVE      ZERO,CNABED
            MOVE      ZERO,CNCBED
            MOVE      ZERO,CNAAMT
            MOVE      ZERO,CNCAMT
            CALL      WRCENS1
            PACK      KEY4,CNDAYC,STATYPE,SP4
            CALL      RDCENS1               * re-read ready for Update
....        BRANCH    OVRCD,CALCDY99                                  *D-212840
          ENDIF
.
          MOVE      ADATE,ASDATE
          CALL      PATAGED                 * calculate age using Admn date
.
          COMPARE   CAGECOFF,PSAGE          * on overcondition
          IF        @LESS
            ADD       TOTDAY,CNCBED
            ADD       SAVAMT,CNCAMT
          ELSE
            ADD       TOTDAY,CNABED
            ADD       SAVAMT,CNAAMT
          ENDIF
.
CALCDY90  CALL      UPCENS1
.
CALCDY99  RETURN
+
.******************************************************************************
.              PRINT REPORT
.******************************************************************************
REPORT00  DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Generating Report **",*W2;
          DISPLAY   *P1:24,*EL,*P20:24,"Printing Census Code :"
.
          CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      SP20,CPHDROPT
.
          COMPARE   ZERO,SEXDAYS[1]
          GOTO      REPORT20 IF NOT EQUAL
          COMPARE   ZERO,SEXDAYS[2]
          GOTO      REPORT20 IF NOT EQUAL
          COMPARE   ZERO,SEXDAYS[3]
          GOTO      REPORT20 IF NOT EQUAL
          COMPARE   ZERO,SEXDAYS[4]
          GOTO      REPORT20 IF NOT EQUAL
          COMPARE   ZERO,SEXDAYS[9]
          GOTO      REPORT20 IF NOT EQUAL
          COMPARE   ZERO,CADAYS
          GOTO      NOPAT IF EQUAL
.
REPORT20  CALL      HEAD0000
.
          PRINT     *N,*15,"FEMALES IN HOSPITAL",*49,":",*57,SEXDAYS[2]:
                    *70,"$",*73,SEXAMT[2],*N:
                    *15,"MALES   IN HOSPITAL",*49,":",*57,SEXDAYS[1]:
                    *70,"$",*73,SEXAMT[1],*N:
                    *15,"SEX INDETERMINATE IN HOSPITAL",*49,":",*57,SEXDAYS[3]:
                    *70,"$",*73,SEXAMT[3],*N:
                    *15,"SEX INTERSEX IN HOSPITAL",*49,":",*57,SEXDAYS[4]:
                    *70,"$",*73,SEXAMT[4],*N:
                    *15,"SEX UNKNOWN IN HOSPITAL",*49,":",*57,SEXDAYS[9]:
                    *70,"$",*73,SEXAMT[9],*N 
.
          CALL      PRTLN
.
          MOVE      ZERO,FORM1
          WHILE     FORM1 < 9
            ADD       ONE,FORM1
            ADD       SEXDAYS[FORM1],BOBED
            ADD       SEXAMT[FORM1],BOAMT
          DO
.
          PRINT     *15,"TOTAL",*49,":",*57,BOBED:
                    *70,"$",*73,BOAMT
.
          CALL      PRTLN
          PRINT     *N
.
          ADD       "8" TO LNO
          MOVE      ZERO,FORM1
          MOVE      ZERO,BEDCODE
          MOVE      ZERO,AMTCODE
          CALL      PRTCOD00                * read thru Cencus file & print
.
          ADD       BEDCODE,GBTOTAL
          ADD       AMTCODE,GATOTAL
.
          PRINT     *N,*N,*15,"SAME DAY ADMISSION / DISCHARGES",*N
          ADD       FOUR,LNO
.
          MOVE      ONE,FORM1
          MOVE      ZERO,BEDCODE
          MOVE      ZERO,AMTCODE
          CALL      PRTCOD00
          CALL      PRTLN
.
          ADD       BEDCODE,GBTOTAL
          ADD       AMTCODE,GATOTAL
.
          PRINT     *15,"TOTAL INCLUDING DAY CASES",*49,":",*57,GBTOTAL:
                    *70,"$",*73,GATOTAL
.
          CALL      PRTLN
          PRINT     *N,*N,"  ***  END OF REPORT  ***"
.
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Report Generation Completed **",*W2;
          CALL      KILL0000                     * erase temp files
.
REPORT99  GOTO      START
+
.******************************************************************************
.                 HEADINGS FOR OUTPUT
.
.******************************************************************************
HEAD0000  CALL      HEAD132
          PRINT     *35,"From Date : ",XDAY1,"/",XMON1,"/",XCENT1,XYEAR1:
                    *60,"To Date : ",XDAY2,"/",XMON2,"/",XCENT2,XYEAR2
          PRINT     *N,*67,"PERIOD"
          CALL      PRTLN
          PRINT     *57,"BED DAYS",*82,"AMOUNT",*N
          MOVE      "10",LNO
          RETURN
+
.******************************************************************************
.           PRINT CENSUS FILE
.
.******************************************************************************
PRTCOD00  PACK      KEY4,FORM1,SP4
          CALL      RDCENS1
          COMPARE   ZERO,OVRCD
          GOTO      PRTCOD20 IF EQUAL
.
PRTCOD10  CALL      RDKCENS1
          BRANCH    OVRCD OF PRTCOD90
.
PRTCOD20  COMPARE   FORM1,CNDAYC
          GOTO      PRTCOD90 IF NOT EQUAL
.
          COMPARE   ZERO,CNABED
          GOTO      PRTCOD30 IF NOT EQUAL
          COMPARE   ZERO,CNCBED
          GOTO      PRTCOD10 IF EQUAL
.
PRTCOD30  DISPLAY   *P47:24,*V2LON,CNCODE;
.
          MATCH     SP10,CNCODE
          IF        @EQUAL
            MOVE      "Blank Admisson Code     ",TDESC
          ELSE
            MOVE      "A ",TCODE
            PACK      KEY5 WITH TCODE,CNCODE
            CALL      RDCODE1
            IF        OVRCD=1
              PACK      TDESC,INVALID,CNCODE,SP70
            ENDIF
          ENDIF
.
          COMPARE   "48",LNO
          CALL      HEAD0000 IF NOT LESS
          PRINT     *15,TDESC,*38,"- ADULTS   :",*57,CNABED,*70,"$":
                    *73,CNAAMT,*N,*38,"- CHILDREN :",*57,CNCBED,*70,"$":
                    *73,CNCAMT,*N
          ADD       "3",LNO
          ADD       CNABED,BEDCODE
          ADD       CNCBED,BEDCODE
          ADD       CNAAMT,AMTCODE
          ADD       CNCAMT,AMTCODE
          GOTO      PRTCOD10
.
PRTCOD90  CALL      PRTLN
          PRINT     *57,BEDCODE,*70,"$",*73,AMTCODE
PRTCOD99  RETURN
.
.*****************************************************************************
PRTLN     PRINT     *57,"-------------------------------"
          RETURN
.
.*****************************************************************************
NOPAT     DISPLAY   *P10:24,*EL,*V2LON:
                    "*** No Patient Selected ***",*W3;
          GOTO      KADAT1
+
.**************************************************************************
.*                              CREA0000               Called by:         *
.*             create a new temporary file                                *
.**************************************************************************
.
CREA0000  CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFIL1,UKEY1
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      PATTEMP1,TEMPFIL1            * open temp index file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFIL2,UKEY2
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      PATTEMP2,TEMPFIL2            * open temp index file
.
CREA9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by:           *
.*               close and erase the temporary file                         *
.****************************************************************************
.
KILL0000  CLOSE     PATTEMP1                     * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFIL1       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
          CLOSE     PATTEMP2                     * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFIL2       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999  RETURN
+
.              CALCULATE DATES DIFFERENCE
.****************************************************************************
CALC      PACK      CDYSFDTE,WDATE2                                        
          PACK      CDYSTDTE,WDATE1                                        
          CALL      CALCDAYS
          MOVE      CDYSDAYS,CALDAYS
          SUB       ONE,CALDAYS        * Only want the diff between the 2 dates
          ADD       CALDAYS,NBRDAYS
          RETURN
+
.
.****************************************************************************
CLRSEX00  MOVE      ZERO,FORM1
          WHILE     FORM1 < 9
            ADD       ONE,FORM1
            MOVE      ZERO,SEXDAYS[FORM1]
            MOVE      ZERO,SEXAMT[FORM1]
          DO
.
          RETURN
.
.*******************************************************************************
.         Get the default charging claim code for multi hospital
.*******************************************************************************
DCLM0000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          IF        IBCNMHOS=1
            OPEN      PATHSPA1,"pathspaf"
            MOVE      PMVXMHOS,KEY3
            CALL      RDPTHSP1
            IF        OVRCD=0
              MOVE      PTHSDCLM,PTCNDCLM     * default claim code charging
            ENDIF
          ENDIF
DCLM9999  RETURN
.
.****************************************************************************
.*        IO ROUTINES FOR TEMPORARY FILE                                    *
.****************************************************************************
WRCENS1   RESET     KEY4
          WRITE     PATTEMP2,KEY4;KEY4,CNABED,CNAAMT,CNCBED,CNCAMT
          RETURN
.
RDCENS1   MOVE      ZERO,OVRCD
          RESET     KEY4
          READ      PATTEMP2,KEY4;DCNDAYC,CNCODE,CNABED,CNAAMT,CNCBED,CNCAMT
          GOTO      OVERCOND IF OVER
          MOVE      DCNDAYC,CNDAYC
          RETURN
.
RDKCENS1  MOVE      ZERO,OVRCD
          READKS    PATTEMP2;DCNDAYC,CNCODE,CNABED,CNAAMT,CNCBED,CNCAMT
          GOTO      OVERCOND IF OVER
          MOVE      DCNDAYC,CNDAYC
          RETURN
.
RDSCENS1  MOVE      ZERO,OVRCD
          RESET     KEY4
          READ      PATTEMP2,KEY4;;
          GOTO      OVERCOND IF OVER
          RETURN
.
DECENS1   RESET     KEY4
          DELETE    PATTEMP2,KEY4
          RETURN
.
UPCENS1   MOVE      CNDAYC,DCNDAYC
          UPDATE    PATTEMP2;DCNDAYC,CNCODE,CNABED,CNAAMT,CNCBED,CNCAMT
          RETURN
.
.
.------ I/O Routines for temp file ------
.
RATMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATTEMP1,KEY8;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATTEMP1,KEY8;;
          RETURN
.
RDTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATTEMP1,KEY8;TMPADMN,DCOUNT
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READKS    PATTEMP1;TMPADMN,DCOUNT
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READKP    PATTEMP1;TMPADMN,DCOUNT
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          WRITE     PATTEMP1,KEY8;KEY8,DCOUNT
          RETURN
.
UPTMPR1   UPDATE    PATTEMP1;TMPADMN,DCOUNT
          RETURN
.
DETMPR1   RESET     KEY8
          DELETE    PATTEMP1,KEY8
          RETURN
+
.******************************************************************************
.
          INC       STD001IO
          INC       SEXDSCIO
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       CALCDAYS
          INC       STEPDOWN
          INC       PATCMSTP
          INC       PATAGED
          INC       GETBFEES
.
          INC       NZPBFEIO/INC
          INC       PATASDIO/INC
          INC       PATHDFIO/INC
          INC       PATHLFIO/INC
          INC       PATLSDIO/INC
          INC       PATLDFIO/INC
          INC       PATAFEIO/INC
          INC       PATFN1IO/INC
          INC       PATFX1IO/INC
          INC       PATBFEIO/INC
          INC       PATTRNIO/INC
          INC       PATHSPIO/INC
          INC       PATMI1IO/INC
          INC       PATMA1IO/INC
          INC       PATDAYIO/INC
          INC       PATDSCIO/INC
          INC       PATCFAIO/INC
          INC       PATCODIO/INC
          INC       PATRCAIO/INC
          INC       PMSCIDIO/INC
          INC       PMSVX1IO/INC
          INC       PATWR1IO/INC
          INC       GETCNEFF
.
ENDIT     CHAIN     PGM
          STOP

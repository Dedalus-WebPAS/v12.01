.*****************************************************************************
.*    Program      : IBAOUT78                                                *
.*    Description  : Outpatients - Cancelled Clinic List for 14 days         *
.*                                                                           *
.*    Author       : ROD                                                     *
.*    Date         : 28/04/93                                                *
.*    Modifications:                                                         *
.*****************************************************************************
.*        V11.02.01 09/02/2022  Thanh T          TSK 0905641                 *
.*                  Recompiled as OUTMA1FD changes                           *
.*        V10.07.01 29/10/2015  Ebon Clements  CAR 268970                    *
.*                  Change to use OUTCLIFD index 1 instead of index 2        *
.*        V10.05.01 18/12/2014  Ebon Clements  CAR 261630                    *
.*                  Removed port number from temp file name                  *
.*        V10.03.01 17/10/2012  Jill Parkinson CAR 273375                    *
.*                  Recompiled for DATECONV - first day of year calculation  *
.*        V5.08.B01 15/03/2000  Steve Armstrong    5.08 Mods                 *
.*                  Mods for changes to OUTCLIFD (effective date)            *
.*****************************************************************************
.*        V5.07.B01 18/01/1999  Nicole Harrington 507 rebound 099            *
.*                  Fixed I*I on patdo1af - now patdo1af                     *
.*****************************************************************************
.*        V5.01.02  24/05/1995  Matt Surridge                                *
.*                  Fixed bugs for global recompile.                         *
.*        V5.01.03  20/07/1994  Rob Leonard                                  *
.*                  Fixed hospital keyin                                     * 
.*****************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       PATHSPFD/INC
          INC       PATDO1FD/INC
          INC       PATCODFD/INC
          INC       OUTCLIFD/INC
          INC       OUTMA1FD/INC
          INC       OUTCSLFD/INC
          INC       OUTSITFD/INC
          INC       TFILEVAR/INC         
          INC       WEBERRFD/INC
.
DIM4      DIM       4
FNDDATA   FORM      1
CLINDESC  DIM       21
CLINDATE  DIM       10
REASDESC  DIM       20
STRTDAYS  FORM      3         * START DATE variables
STRTYR    FORM      2
STRTCENT  FORM      2
STRTLEAP  FORM      1
FROMDATE  DIM       8         * start date
DURATION  FORM      "14"      * duration
DATEEND   DIM       8         * end date
CMDLINE   DIM       70
TODATE    DIM       8
CODE      DIM       2
FNAME     DIM       8
KHOSP     DIM       3
SAVSITE   DIM       6
.
. Temp file definition
TEMPFILE  IFILE     SQL, FIXED=34, KEY="U1-6,7-12,13-20,21-25,26-27"
XXSITE    DIM       6      1      * site
XXCLIN    DIM       6      7      * clinic
XXDATE    DIM       8     13      * Date
XXTIME    DIM       5     21      * Time
DXXUNIQ   DIM       2     26      * Unique identifier
XXREAS    DIM       3     28      * Cancellation reason
XXPRFX    DIM       3     31      * Site prefix
.       End of record     34
XXUNIQ    FORM      2
.
PRGID     INIT      "IBAOUT78"
PRGNAM    INIT      "Cancelled Clinic List (14 Day Period)"
VERSION   INIT      "V12.01.00"
+
.***********************************************************************
.*  ML0000  :  Mainline code                                           *
.***********************************************************************
ML0000    CALL      DISPHEAD
          CALL      INIT0000                      * initialisation
.
ML1000    CALL      MENU0000                      * menu
          BRANCH    EXIT,ML9999                   * Exit selected
.
ML2000    CALL      PROC0000                      * process field keyins
          BRANCH    EXIT,ML1000                   * nothing input
.
          CALL      CONTQST                       * Ok to Continue
          BRANCH    CEXIT,ML5000,ML2000,ML1000
.
ML5000    CALL      CRET0000                      * create temp file
.
          CALL      LOAD0000                      * load temp file
          BRANCH    FNDDATA,ML2000                * no data found
          CALL      RPRT0000                      * print report from temp file
.
          GOTO      ML1000
.
ML9999    CALL      KILL0000                      * erase temp file
          CHAIN     PGM
+
.***********************************************************************
.*  INIT0000  :  Initialisation                                        *
.***********************************************************************
INIT0000  DISPLAY   *P55:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
          DISPLAY   *P55:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          DISPLAY   *P55:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          DISPLAY   *P55:24,"outsitaf"
          OPEN      OUTSITA1,"outsitaf"
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,FNAME
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.***********************************************************************
.*  MENU0000  :  Main Menu                                             *
.***********************************************************************
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Print Report":
                    *P1:7,"Select option ? "
.
MENU1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION;
.
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,MENU9999
          IF        MOPTION<>0
            BEEP
            GOTO      MENU1000
          ENDIF
          MOVE      ONE,EXIT                      * Exit
.
MENU9999  RETURN
+
.***********************************************************************
.*  PROC0000  :  Process fields                                        *
.***********************************************************************
PROC0000  DISPLAY   *P1:10,*EF,"Hospital  : "
          MOVE      "10",EVERT
          MOVE      "13",ECOL
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
.
          CALL      PATHSPKY
          BRANCH    EXIT,PROC9999,PROC0000
.
          MOVE      KEY3,KHOSP
          DISPLAY   *P23:10,PTHSNAME
          MOVE      PTHSNAME,CNAME
.
. keyin a date range
.
PROC0800  DISPLAY   *P1:12,*EF,"From date : ",*P1:13,"To date   : "
.
. keyin from date
.
          MOVE      CCC,CCENT
          UNPACK    SP10,CDAY,CMON,CYEAR
          MOVE      "13",CCOL
          MOVE      "12",CVERT
          MOVE      ZERO,CDEFDTE
.
PROC1000  CALL      KEYDATE
          BRANCH    CQUEST,PROC1000
          BRANCH    OVRCD,PROC0000                * anything input
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE 
.
. keyin to date
.
          MOVE      "14",DURATION
          CALL      CEDT0000                      * calc. default end date
.
          UNPACK    DATEEND,CCENT,CYEAR,CMON,CDAY
          MOVE      "13",CCOL
          MOVE      "13",CVERT
          MOVE      ZERO,CDEFDTE
.
PROC6000  CALL      KEYDATE
          BRANCH    CQUEST,PROC6000
          BRANCH    OVRCD,PROC0800                * anything input?
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE 
.
. check if dates entered are valid (range)
.
          REP       " 0",FROMDATE
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      PROC9000 IF LESS
          MOVE      ZERO,EXIT
          GOTO      PROC9999
.
PROC9000  DISPLAY   *P1:24,*EL,*B,"Invalid date range.  ";
          CALL      HITENTER
          GOTO      PROC0800
.
PROC9999  RETURN
+
.***********************************************************************
.*  RPRT0000  :  Print Report                                          *
.***********************************************************************
RPRT0000  DISPLAY   *P1:24,*EL,*P35:24,"Printing...";
.
          CALL      IBACLOCK
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      "60",CLNO                     * force new page
          MOVE      SP6,SAVSITE
.
          PACK      KEY27,SP30
          CALL      RSTEMP1  
RPRT0500  CALL      RKTEMP1                       * get next record
          BRANCH    OVRCD,RPRT9000                * no more records
.
. check if we need a new page
.
          COMPARE   "55",CLNO
          GOTO      RPRT5000 IF LESS
.
. we need a new page
.
          CALL      PAGE0000
          GOTO      RPRT6000
.
. check if we need a new specialty
.
RPRT5000  MATCH     SP6,SAVSITE                   * 1st site?
          GOTO      RPRT6000 IF EQUAL
.
          MATCH     XXSITE,SAVSITE                * new site?
          GOTO      RPRT6000 IF EQUAL
.
.         we have a new specialty
.
          CALL      UND80
          CALL      PAGE0000                      * print new page stuff
.
. print a line
.
RPRT6000  CALL      FDAT0000                      * format data for display
          PRINT     *1,"| ",XXCLIN,SP1,CLINDESC,*32,"| ",CLINDATE,*45:
                    "| ",XXTIME,*53,"| ",XXREAS,SP1,REASDESC,*80,"|"
.  
          ADD       ONE,CLNO                      * increment line counter
          MOVE      XXSITE,SAVSITE
          GOTO      RPRT0500
.
. no more records
.
RPRT9000  CALL      UND80
          PRINT     *N,*1,"*** End of report ***" 
.
RPRT9999  RETURN
+
.*************************************************************************
.*  FDAT0000  :  Format data for display                                 *
.*************************************************************************
FDAT0000  MOVE      "<<Missing>>",TDESC
          PACK      KEY5,ANSC,ANSB,XXREAS
          CALL      RDCODE1
          MOVE      TDESC,REASDESC
.
          UNPACK    XXDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,CLINDATE
.
          PACK      CFNAME,XXPRFX,FILCLIA1
          OPEN      OUTCLIA1,CFNAME
.
          PACK      KEY20,XXSITE,XXCLIN,XXDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     XXSITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     XXCLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          CLOSE     OUTCLIA1
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
          BRANCH    OVRCD,FDAT9000
.
          MATCH     SP6,OCADOCT
          GOTO      FDAT9000 IF EQUAL
.
          MOVE      "<<Missing Doctor code>>",OCADESC
          PACK      KEY6,OCADOCT
          CALL      RDDOCT1
          BRANCH    OVRCD,FDAT9000
.
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,OCADESC
.
FDAT9000  MOVE      OCADESC,CLINDESC              * shorten description
.
FDAT9999  RETURN
+
.*************************************************************************
.*  PAGE0000  :  Print new page stuff                                    *
.*************************************************************************
PAGE0000  CALL      HEAD80
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *20,"Date from  ",CPCDATE,"  to  ";
          UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     CPCDATE,*N
          MOVE      "<<Missing>>",OSTDESC
          PACK      KEY6,XXSITE
          CALL      RDSITA1
          PRINT     *1,"Site: ",XXSITE,SP2,OSTDESC,*N
          CALL      UND80
          PRINT     *1,"| Clinic",*32,"|   Date",*45,"| Time",*53:
                    "| Cancellation Reason",*80,"|"
          CALL      UND80
          MOVE      TEN,CLNO
.
PAGE9999  RETURN
+
.*************************************************************************
.*  LOAD0000  :  Extract data into temp file                             *
.*************************************************************************
LOAD0000  DISPLAY   *P1:24,*EL,*P30:24,"Loading... ";
          MOVE      ONE,FNDDATA                   * assume no data found
          PACK      KEY6,SP6
          CALL      RDSSITA1
LOAD1000  CALL      RDKSITA1
          BRANCH    OVRCD,LOAD9000
.
          DISPLAY   *P43:24,OSTSITE;
          PACK      CFNAME,OSTFILE,FILCSLA2
          OPEN      OUTCSLA2,CFNAME
.
          PACK      KEY27,OSTSITE,FROMDATE,SP30
          CALL      RSOTCSL2
LOAD2000  CALL      RKOTCSL2
          BRANCH    OVRCD,LOAD1000
.
          DISPLAY   *P52:24,OTCSCLIN;
.
          MATCH     OSTSITE,OTCSSITE              * same site?
          GOTO      LOAD1000 IF NOT EQUAL
.
          MATCH     OTCSDATE,TODATE               * in date range 14 day period
          GOTO      LOAD1000 IF LESS
.
. check if correct hospital
.
          UNPACK    OTCSDATE,CCENT,CYEAR,CMON,CDAY
          CALL      DATECONV
.
          MOVE      OSTFILE,CFNAME
          CALL      OPOTMA11
.
          PACK      KEY18,OTCSSITE,OTCSCLIN,WEEKDAY,OTCSSTRT
          CALL      RDMASA1
          CLOSE     OUTMA1A1
          BRANCH    OVRCD,LOAD2000
.
          MATCH     KHOSP,OTMAHOSP                * correct hospital?
          GOTO      LOAD2000 IF NOT EQUAL
.
. write a record to the temp file
.
          MOVE      OTCSSITE,XXSITE
          MOVE      OTCSCLIN,XXCLIN
          MOVE      OTCSDATE,XXDATE
          MOVE      OTCSSTRT,XXTIME
          MOVE      OTCSUNIQ,XXUNIQ
          MOVE      OTCSREAS,XXREAS
          MOVE      OSTFILE,XXPRFX
.
          PACK      KEY27,XXSITE,XXCLIN,XXDATE,XXTIME,XXUNIQ
          CALL      WRTEMP1
          MOVE      ZERO,FNDDATA                  * we have data
          GOTO      LOAD2000
.
LOAD9000  IF        FNDDATA=1
            DISPLAY   *P1:24,*EL,*B,"No data found.  ";
            CALL      HITENTER
          ENDIF
.
LOAD9999  RETURN
+
.*************************************************************************
.*  CEDT0000  :  Calculate end date (14 days later than start date)      *
.*************************************************************************
CEDT0000
.         turn the starting date into julian days
.
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CDAY,DD                 * set up start day
          MOVE      CMON,MM                 *              month
          MOVE      CYEAR,YY                *              year
          MOVE      CCENT,CC                *              century
.
          CALL      DMYCON                  * get julian day
          MOVE      JULDAY,STRTDAYS         * get current julian day
          MOVE      JULYR,STRTYR            * get current julian year
          MOVE      JULCC,STRTCENT          * get current julian century
          MOVE      LEAPYR,STRTLEAP         * get leap year flag (1=leap)
.
.         add the number of days
.
          ADD       DURATION,STRTDAYS       * get the total days for the year
.
.         convert back to calender date
.
          MOVE      STRTDAYS,JWKDAY         * work days
          MOVE      STRTYR,JWKYER           * work year
          MOVE      STRTCENT,JWKCC          * work century
.
          CALL      JULCON                  * convert to DD,MM,YY,CC
.
.         check for change in centuries
.
          COMPARE   STRTYR,YY
          GOTO      CEDT5000 IF NOT LESS    * ending year >= starting year
.
.         need a change in centuries
.
          ADD       ONE,STRTCENT            * add a century
.
.         set up end date
.
CEDT5000  PACK      DATEEND,STRTCENT,YY,MM,DD
          REP       " 0",DATEEND
.
CEDT9999  RETURN
+
.*************************************************************************
.*  Temp file IO's                                                       *
.*************************************************************************
RSTEMP1   RESET     KEY27
          READ      TEMPFILE,KEY27;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY27
          READ      TEMPFILE,KEY27;XXSITE,XXCLIN,XXDATE,XXTIME,DXXUNIQ,XXREAS:
                                   XXPRFX
          GOTO      OVERCOND IF OVER
          MOVE      DXXUNIQ,XXUNIQ
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;XXSITE,XXCLIN,XXDATE,XXTIME,DXXUNIQ,XXREAS,XXPRFX
          GOTO      OVERCOND IF OVER
          MOVE      DXXUNIQ,XXUNIQ
          RETURN
.
WRTEMP1   RESET     KEY27
          MOVE      XXUNIQ,DXXUNIQ
          WRITE     TEMPFILE,KEY27;XXSITE,XXCLIN,XXDATE,XXTIME,DXXUNIQ,XXREAS:
                                   XXPRFX
          RETURN
.
UPTEMP1   MOVE      XXUNIQ,DXXUNIQ
          UPDATE    TEMPFILE;XXSITE,XXCLIN,XXDATE,XXTIME,DXXUNIQ,XXREAS,XXPRFX
          RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temporary file                                    *
.*************************************************************************
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*  CRET0000  :  create temporary file                                   *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    " 34 U1-6,7-12,13-20,21-25,26-27",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,FNAME
.
CRET9999  RETURN
+
.
          INC       STD001IO
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       PATHSPIO/INC
          INC       PATCODIO/INC
          INC       OUTCSLIO/INC
          INC       PATDO1IO/INC
          INC       OUTCLIIO/INC
          INC       OUTSITIO/INC
          INC       OUTMA1IO/INC
          INC       PATHSPKY
          INC       PATCODKY
          INC       DATECONV
          INC       WEBERRIO/INC

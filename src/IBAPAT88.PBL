.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*      PROGRAM NAME:    IBAPAT88                                             *
.*                                                                            *
.*      FUNCTION:        HANDOVER REPORT 1 (ORIGINAL) - Same as IBAPAT84      *
.*                                                                            *
.*      DATE WRITTEN:    19/09/85                                             *
.*                                                                            *
.*      AUTHOR:          MALCOLM HAYSE                                        *
.*                                                                            *
.*      MODIFICATIONS:                                                        *
.*        V12.00.01 28/05/2025  PJ Le Febour   Task 0955096 Alphanum visitnum *
.*                   replace  COMPARE   ZERO,WADMNO with MATCH ZEROVISN,WADMNO*
.*                            MOVE  OPDAADMN,FORM8 to MOVE  OPDAADMN,DIM8VISN *
.*                            COMPARE WADMNO,FORM8 to MATCH WADMNO,DIM8VISN   *
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*        V10.03.04 27/06/2013 Ebon Clements  CAR 287511                      *
.*                  Changes for WEXTN and NSEXTN length change from 4 to 20   *
.*        V10.03.03 19/12/2012  Ebon Clements     CAR 254116                  *
.*                  Changed to use CALCAGE                                    *
.*        V10.03.02 29/05/2012  J.Tan             CAR 256695                  *
.*                  Added Expected Discharge date                             *
.*        V10.03.01 17/04/2012  Davin             CAR 260236                  *
.*                  Report current operation date (if it exists) rather than  *
.*                  date of a cancelled booking.                              *
.*        V10.02.03 25/05/2011  Saroeun Soeur     CAR 243065                  *
.*                  removed WINPUT                                            *
.*        V10.02.02 16/05/2011  Saroeun Soeur     CAR 243065                  *
.*                  added ward type check WINPUT disp. W/B wards              *
.*        V10.02.01 03/05/2011  Saroeun Soeur     CAR 242329                  *
.*                  added admission to list that dont have allocated beds     *
.*                  CHKNB000                                                  *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.09.02  18/12/2007  Sandra Barcham    CAR 157520                  *
.*                  Fix print of empty beds                                   *
.*        V9.09.01  15/10/2007  Steve Armstrong   CAR 151874                  *
.*                  Mods to add religion column to report.                    *
.******************************************************************************
.*        V9.07.01  07/09/2006  Jill Habasque CAR 109852                      *
.*                  Mods to output alternate ID if PTCNDAUR=1                 *
.******************************************************************************
.*        V9.04.01  03/09/2004  Davin            CAR 53145                    *
.*                  Added Multi-Hospital processing                           *
.******************************************************************************
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.******************************************************************************
.*        V9.02.02  04/09/2002  Jill Habasque SRF 17573                       *
.*                  Changed to use RTIODAYS instead of NHOSPDAY               *
.*        V9.02.01  01/08/2002  Ebon Clements SRF 19284                       *
.*                  Added operation date to the report.                       *
.******************************************************************************
.*        V9.00.B01 05/06/2001  Dean Cramer                                   *
.*                  Tidy entry options for web html CGI file.                 *
.*        V5.08.03  29/08/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.02  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 10/04/2000  Greg Horvat                                   *
.*                  Removed MATADAFD MATADBFD & MATADBIO                      *
.*        V5.07.01  19/02/1999  Jim Stathopoulos                              *
.*                  Added ? searches for Ward and Nurse Stations              *
.*                  and made Report modifications                             *
.*                  09/11/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
. *       V5.03.01  21/07/1995  Greg Horvat      SRF 135374 & 640110          *
. *                 Changed to use NHOSPDAY when calculating numebr of days   *
. *                 in hospital                                               *
. *       V5.03.02  18/04/96 J.Tan   SRF 614625                               *
. *                 Mods to return to the correct options                     *
. *       V5.04.01  30/05/96 J.Tan   SRF 615435                               *
. *                 Mods to print the second line of admission diagnosis      *
. *       V5.04.02  09/07/1996  Greg Horvat      SRF 615966                   *
. *                 Fixed the printing of the 1st & 2nd lines of admission    *
. *                 diagnosis                                                 *
. *                                                                           *
. *****************************************************************************
.
          INC       STD001FD
.
          INC       PATCALAG/INC
          INC       PATCONFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATNOBFD/INC
          INC       PATNURFD/INC
          INC       PATTRNFD/INC
          INC       PATWR1FD/INC
          INC       PMSWORFD/INC
          INC       OPRDEAFD/INC
+
.         WORK AREA
.
ADIAG30   DIM       30
ADIAG30A  DIM       30
ADIAG30B  DIM       30
NBALLOCA  INIT      "*** NO BED ALLOCATED ***"
ALLOCHK   FORM       1
.
AJDAY     FORM      3                     * JULIAN ADMISSION DAY
.
BJDAY     FORM      3
.
CJDAY     FORM      3
CANCFLAG  FORM      1
CDESC     DIM       20
CODEM     INIT      "M "
CURDATE   DIM       8
.
DIM9      DIM       9
DIM10     DIM       10
DIM11     DIM       11
DIM20     DIM       20
.
DHHMM     DIM       5
DMSTAT    DIM       9
DNAME     DIM       18
DPSKIP    DIM       3
DRLGTYP   DIM       30
DSEX      DIM       8                                         * was 6  *C-49016
.
FORM8     FORM      8
FROMDATE  DIM       8
.
GIVDIM20  DIM       20
.
IBCNMHOS  FORM      1        * Multi Hospital Indicator
.
MIN1      FORM      1
NAME      DIM       22                    * MFNAME.MSNAME
.
OPERDATE  DIM      10                     * Print operation date
.
PATCOUNT  FORM      7                     * added for V5.01.121 - patients count
PHDAYS    FORM      4
PSAGECON  DIM       3
PSAGETYP  DIM       1
PSKIP     FORM      1
.
SAVNSTAT  DIM       3
SAVNDESC  DIM       30
SAVNSIST  DIM       30
SAVNEXTN  DIM       20
SAVWEXTN  DIM       20
SAWARD    DIM       3
SBED      DIM       3
SKEY30    DIM       30
SWARD     DIM       3
SRLGTYP   DIM       3
.
TODATE    DIM       8
.
WRDESC    DIM       20
.
. ----- Variables for NHOSPDAY -----
.
BJDAYS    FORM      3
CJDAYS    FORM      3
CALDAYS   FORM      4
JYEAR     FORM      2
NBRDAYS   FORM      6
RTDAYS    FORM      4
WDATE1    DIM       8
WDATE2    DIM       8
.
PRGID     INIT      "IBAPAT88"
PRGNAM    INIT      "Handover Report 1"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patnobef";
          OPEN      PATNOBE1,"patnobef"
.
          DISPLAY   *P64:24,"patnursm";
          OPEN      PATNURS1,"patnursm"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
.
          DISPLAY   *P64:24,"pmsworaf";
          OPEN      PMSWORA1,"pmsworaf"
.
          DISPLAY   *P64:24,"oprdetaf";
          OPEN      OPRDETA2,"oprdetaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TEN5;*188,CMATRN
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          PACK      CURDATE,CCC,CYY,CMM,CDD     * Get current date
          REP       " 0",CURDATE
+
.
.         START OF PROGRAM
.
.         Select Option
.
START     MOVE      ZERO,CPAGENO
          MOVE      ZERO,PATCOUNT              * initialize patients count
.
          DISPLAY   *P1:4,*EF:
                   *P1:5,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:6,*V2LON,ONE,*HOFF," = Particular Ward":
                    *P1:7,*V2LON,TWO,*HOFF," = Particular Nursing Station":
                  *P1:8,*V2LON,THREE,*HOFF," = All Nursing Stations":
                   *P1:9,*V2LON,FOUR,*HOFF," = All Wards":
                  *P1:11,"Select Option : "
.
OPT1      KEYIN     *P17:11,*EL,*DV,UNDLN1,*P17:11,*V2LON,OPTION
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
BRNCH     BRANCH    OPTION OF KWARD,KNURS,ALLNS,ALLWS
          BEEP
          GOTO      OPT1
.
.         INPUT WARD NUMBER
.         -----------------
.
KWARD     KEYIN     *P1:14,*EF,"Enter Ward Number : ",*V2LON,SAWARD
          RESET     SAWARD
          GOTO      START IF EOS
.
          ENDSET    SAWARD
          APPEND    SP3,SAWARD
          RESET     SAWARD
.
          MATCH     QUEST,SAWARD
          GOTO      KWARD1A IF NOT EQUAL
.
          CALL      GETSCR00
          CALL      PATWRDDS
          CALL      PUTSCR00
          GOTO      KWARD
.
KWARD1A   PACK      KEY6 WITH SAWARD,SP6
          CALL      RDWARD1
          BRANCH    OVRCD OF INVWARD
          DISPLAY   *P40:14,*V2LON,WBDESC
.
          BRANCH    WACTIVE,NOTACTIV       * Ignore non-active wards
.
.          CALL      CONTQST
.          BRANCH    CEXIT,KWARD1,KWARD,START
.          GOTO      KWARD
.
KWARD1    MOVE      WBDESC,WRDESC
          MOVE      WEXTN,SAVWEXTN
          MOVE      ONE,PSKIP
          MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL:
                    *P20:24,"Ward :",*P40:24,"Bed :"
.
          GOTO      ALLWRDS
.
NOTACTIV  KEYIN     *P1:24,*EL,*V2LON,*B,"** Ward is not active ** ";
          CALL      HITENTER
          GOTO      KWARD
.
INVWARD   DISPLAY   *P40:14,*B,*V2LON,"** Ward does not exist **",*W2
          GOTO      KWARD
.
.         INPUT NURSING STATION NUMBER
.         ----------------------------
.
KNURS     KEYIN     *P1:14,*EF,"Enter Station Number : ",*V2LON,SAVNSTAT
          RESET     SAVNSTAT
          GOTO      START IF EOS
.
          MATCH     QUEST,SAVNSTAT
          GOTO      KNURS1A IF NOT EQUAL
          CALL      QNUR0000          
          CALL      PUTSCR00
.
KNURS1A   ENDSET    SAVNSTAT
          APPEND    SP3,SAVNSTAT
          RESET     SAVNSTAT
.
          PACK      KEY6 WITH SAVNSTAT,SP3
          CALL      RDNURS1
          BRANCH    OVRCD OF INVNURS
          DISPLAY   *P24:14,*V2LON,*EL,SAVNSTAT
          DISPLAY   *P40:14,*V2LON,*EL,NSDESC
.
.          CALL      CONTQST
.          BRANCH    CEXIT,KNURS1,KNURS,START
.          GOTO      KNURS
.
KNURS1    DISPLAY   *P1:24,*EL
          MOVE      SP3,NSTAT
          MOVE      SP3,NWARD
          MOVE      SP3,SAWARD
.
          MOVE      NSDESC,SAVNDESC
          MOVE      NSIST,SAVNSIST
          MOVE      NSEXTN,SAVNEXTN
.
          CALL      SEPPAGE
.
          MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL:
                    *P20:24,"Ward :",*P40:24,"Bed :"
.
NEXTDSP   CALL      RDKNURS1
          BRANCH    OVRCD OF ENDP
.
          MATCH     NSTAT,SAVNSTAT
          GOTO      ENDP IF NOT EQUAL
.
          PACK      KEY6 WITH NWARD,SP3
          CALL      RDWARD1
          BRANCH    OVRCD OF NEXTDSP
.
          BRANCH    WACTIVE,NEXTDSP       * Ignore non-active wards
.
          MOVE      WBDESC,WRDESC
          MOVE      WEXTN,SAVWEXTN
          GOTO      NEXTW
.
INVNURS   DISPLAY   *P40:14,*B,*V2LON,"** Station does not exist **",*W2
          GOTO      KNURS
.
.         ASK IF SEPERATE PAGE REQUIRED
.
SEPPAGE   DISPLAY   *P1:17,*EL,"Would you like each Ward on a seperate PAGE (":
                        *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ?"
.
SEPPAGE1  KEYIN     *P53:17,*EL,*DV,UNDLN1,*P53:17,*V2LON,ANS;
.
          RESET     ANS
          GOTO      SEPPAGE2 IF NOT EOS
          DISPLAY   *P53:17,*V2LON,ANSN;
          GOTO      SEPPAGE3
.
SEPPAGE2  MOVE      ZERO,PSKIP
          MATCH     "Y",ANS
          GOTO      SEPPAGE4 IF EQUAL
          MATCH     "N",ANS
          GOTO      SEPPAGE3 IF EQUAL
          BEEP
          GOTO      SEPPAGE1
.
SEPPAGE3  MOVE      ONE,PSKIP 
.
SEPPAGE4  RETURN
+
.
.         ALL WARDS ON THE SYSTEM
.         -----------------------
.
ALLWS     CALL      KHOS0000
          BRANCH    EXIT,START
.
          CALL      SEPPAGE
          MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :"
.
          MOVE      SP3,SAWARD
          MOVE      SP6,KEY6
          CALL      RDSWARD1
          CALL      RDKWARD1                   * read 1st record to save Ward
          BRANCH    OVRCD,ENDP
          MOVE      WWARD,SWARD
          GOTO      CNTNXW
.
.         ALL NURSING STATIONS ON THE SYSTEM
.         ----------------------------------
.
ALLNS     CALL      SEPPAGE
          MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,"Station : ":
                    *P20:24,"Ward :",*P40:24,"Bed :"
.
          MOVE      SP6,KEY6
          MOVE      SP3,SAWARD
          CALL      RDSNURS1
.
NEXTS     CALL      RDKNURS1
          BRANCH    OVRCD OF ENDP
.
          BRANCH    WACTIVE,NEXTS          * Ignore non-active wards
.
          MATCH     SP3,NWARD
          GOTO      CNTNS IF NOT EQUAL
.
          DISPLAY   *P12:24,*V2LON,NSTAT;
          MOVE      NSTAT,SAVNSTAT
          MOVE      NSDESC,SAVNDESC
          MOVE      NSIST,SAVNSIST
          MOVE      NSEXTN,SAVNEXTN
          MOVE      "60",CLNO                  **** NEW PAGE EACH STATION
          GOTO      NEXTS
.
CNTNS     PACK      KEY6 WITH NWARD,SP3
          CALL      RDWARD1
          BRANCH    OVRCD OF NEXTS
.
          BRANCH    WACTIVE,NEXTS          * Ignore non-active wards
.
          MOVE      WBDESC,WRDESC
          MOVE      WEXTN,SAVWEXTN
.
.         Process next Ward record
.
NEXTW     CALL      RDKWARD1
          COMPARE   ZERO,OVRCD
          GOTO      CNTNXW IF EQUAL
.
.         WE MUST CHECK NURSING STATION IF OPTION 2,3
.
          BRANCH    OPTION OF ENDP,NEXTDSP,NEXTS,ENDP
.
CNTNXW    BRANCH    WACTIVE,NXTWARD        * Ignore non-active wards
.
          MATCH     SP3,SAWARD
          IF        @EQUAL
            COMPARE   ONE,IBCNMHOS         * Using multi hospital
            GOTO      ALLWRDS IF NOT EQUAL
.
            PACK      PTHSHOSP,PTHSHOSP,SP70
            MATCH     SP10,PTHSHOSP        * All Hospitals
            GOTO      ALLWRDS IF EQUAL
.
            MATCH     PTWDHOSN,PTHSHOSP    * Correct hospital
            GOTO      NXTWARD IF NOT EQUAL
.
            GOTO      ALLWRDS
          ENDIF
.
          MATCH     SAWARD,WWARD
          GOTO      ENDP IF NOT EQUAL
.
ALLWRDS   MATCH     SP3,WBED
          GOTO      CHKWAD IF NOT EQUAL
.
          COMPARE   FOUR,OPTION                * added for V5.01.03 - SRF 106511
          GOTO      ALLWRDS1 IF NOT EQUAL      * not Option 4
          MOVE      WBDESC,WRDESC              * yes Option 4
          MOVE      WEXTN,SAVWEXTN
.
ALLWRDS1  PACK      KEY13 WITH WWARD,SP10
          CALL      RDSNOBE1
NOBLP     CALL      RDKNOBE1
          BRANCH    OVRCD OF NXTOP

          MATCH     WWARD,NBWARD
          GOTO      NXTOP IF NOT EQUAL

          MOVE      NBADMNO,WADMNO
          MOVE      NBPLUR,WPLUR
          CALL      GETDATA
          GOTO      NOBLP
.
NXTWARD   MATCH     SP3,WBED
          GOTO      NEXTW IF NOT EQUAL
          PACK      KEY6,WWARD,ANSZ,ANSZ,ANSZ
          CALL      RDSWARD1
          GOTO      NXTOP
.
CHKWAD    CALL      GETDATA
.
NXTOP     BRANCH    OPTION OF NEXTW,NEXTDSP,NEXTS,NEXTW
          GOTO      NEXTW
.
.         Subroutine to process a record
.
GETDATA   COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          MATCH     WWARD,SWARD
          GOTO      FPRINTA IF EQUAL
.
.         WE MUST CHECK IF A WARD PER PAGE
.
          BRANCH    PSKIP OF FPRINT0
          CALL      HEAD
          GOTO      FPRINTA
.
FPRINT0   COMPARE   "52",CLNO
          GOTO      FPRINT1 IF LESS
          CALL      HEAD
          GOTO      FPRINTA
.
FPRINT1   CALL      HEAD12
.
.         ZERO ADMISSION NUMBER INDICATES BED IS EMPTY
.
FPRINTA   MATCH     ZEROVISN,WADMNO
          GOTO      EMPDA1 IF EQUAL
.
          CALL      DISPDA1
.
          RETURN
+
EMPDA1    CALL      EMPBED1
          RETURN
+
.
NOWARD    DISPLAY   *P20:24,*EL,*B,*V2LON:
                    "** No available data on File **",*W2
          GOTO      START
.
.         OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NOWARD IF EQUAL
.
          PRINT     *N,"  Total Number of Patients : ",PATCOUNT:
                    *N,*N,*N,"  ***  End of Report  ***"
.
          DISPLAY   *P20:24,*EL,*V2LON:
                    "** Report Generation Completed **",*W2
          GOTO      START
+
.         PRINT ACTUAL DATA 
.
DISPDA1   PACK      ADIAG30,SP30,SP30
          PACK      ADIAG30A,SP30,SP30
          PACK      ADIAG30B,SP30,SP30
.
          MOVE      WADMNO,KEY8
          CALL      RDPMWOR1
          IF        OVRCD=1
            MOVE      SP70,PMWOEDAT           * Expected Discharge Date
          ENDIF
.
          COMPARE   ONE,CMATRN
          GOTO      RDCURP IF NOT EQUAL
.
RDCURP    PACK      KEY9 WITH TWO,WADMNO
.
          CALL      RDMISS2
          BRANCH    OVRCD OF CHKONLVE
.
          UNPACK    ADIAG1,ADIAG30,DIM20
          UNPACK    ADIAG2,DIM10,ADIAG30B
          PACK      ADIAG30A,DIM20,DIM10
          PACK      ADIAG30B,ADIAG30B,SP30
          GOTO      CNTADMN
.
.         CHECK IF PATIENT IS ON LEAVE
.
CHKONLVE  PACK      KEY9 WITH FOUR,WADMNO
          CALL      RDMISS2
          BRANCH    OVRCD OF EMPBED1
          MOVE      "*** PATIENT IS ON LEAVE ***",ADIAG30
          PACK      ADIAG30A,SP30,SP30
          PACK      ADIAG30B,SP30,SP30
.
CNTADMN   MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF EMPBED1
.
          CALL      IBACLOCK
          IF        PCEASE=1
            MOVE      PDECDTE,AGEDATE            * Date of Death
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
          CALL      CALCAGE
.
          MOVE      ZERO,CANCFLAG
          MOVE      SP70,OPERDATE
          PACK      KEY31,WADMNO,Z70
          CALL      RSOPDEA2
CNTADM10  CALL      RPOPDEA2
          BRANCH    OVRCD,CNTADM20
.
          MOVE      OPDAADMN,DIM8VISN
          MATCH     WADMNO,DIM8VISN
          GOTO      CNTADM20 IF NOT EQUAL
.
          IF        CANCFLAG=1
            MATCH     "2",DOPDASTA
            IF        @EQUAL
              MOVE      ZERO,CANCFLAG     * booking cancelled flag = no
            ENDIF
            GOTO      CNTADM15            * report this date
          ENDIF
.
          MATCH     "3",DOPDASTA
          IF        @EQUAL
            MOVE      ONE,CANCFLAG        * booking cancelled flag = yes
          ENDIF
.
CNTADM15  UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE               * Format the session date
          MOVE      CPCDATE,OPERDATE
.
          BRANCH    CANCFLAG,CNTADM10     * need to check for booking stat = 2
.
CNTADM20  MOVE      SP30,DNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF DSPCT1
          MOVE      DGNAME,ANS
          PACK      DNAME WITH ANS,DOT,DSNAME
.
DSPCT1    PACK      KEY5 WITH CODEM,PMSTAT
          MOVE      SP9,DMSTAT
          CALL      RDCODE1
          BRANCH    OVRCD OF GETHSP
          MOVE      TDESC,DMSTAT
.
GETHSP    MOVE      ADATE,FROMDATE
          REP       " 0",FROMDATE
          MOVE      CURDATE,TODATE
          REP       " 0",TODATE
          CALL      RTIODAYS                * Calculate number of days in hosp
          MOVE      NBRDAYS,PHDAYS
.
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DSEX
.* ******************************************   End of Changes         *C-49016
.
FPRINT    UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
.
          COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND10;*249,PTCNDAUR
          PACK      DIM9,PURNO,SP70
          MATCH     "1",PTCNDAUR
          IF        @EQUAL
            PACK      KEY20,SP70
            PROC      GETALIDS
            MATCH     SP70,KEY20
            IF        !@EQUAL
              UNPACK    KEY20,DIM11,DIM9
            ENDIF
          ENDIF
.
          MOVE      PGNAME,GIVDIM20
.
          PRINT     *1,"| ",WBED,*7,"|",AADMNO,*16,"|",DIM9,*26,"|",PSNAME:
                    *47,"| ",DSEX,*57,"| ",PREG:
                    *68,"|",XDAY,"/",XMON,"/",XCENT,XYEAR:
                    *79,"| ",DNAME,*100,"| ",ADIAG30,*132,"|",*N:
                    *1,"|",*7,"|",*16,"|",*26,"|",GIVDIM20:
                    *47,"|",PSAGEYRS," YEARS",*57,"|",*68,"| ",ATIME:
                    *79,"| ",OPERDATE,*100,"| ",ADIAG30A,*132,"|",*N:
                    *1,"|",*7,"|",*16,"|",*26,"|",*47,"|",*57,"|":
                    *68,"|",PHDAYS," DAYS",*79,"| ";
.
          MATCH     SP70,PMWOEDAT
          IF        !@EQUAL
            UNPACK    PMWOEDAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE               * Format the session date
            PRINT     CPDATE," Exp.Dsch.";
          ENDIF
.
          PRINT     *100,"| ",ADIAG30B,*132,"|",*N:
                    *1,"|",*7,"|",*16,"|",*26,"|",*47,"|",*57,"|",*68,"|":
                    *79,"|",*100,"|",*132,"|"
.
          CALL      PAGEND
.
          MOVE      WWARD,SWARD
.
          ADD       FOUR,CLNO
          ADD       ONE,PATCOUNT               * increment patients count
.
          DISPLAY   *P27:24,*V2LON,WWARD,*P46:24,WBED
.
DISPEND   RETURN
.*******************************************************************************
.*                             QNUR0000                                        *
.*                        Nursing station ? routine                            *
.*******************************************************************************
.
QNUR0000  CALL       GETSCR00
          DISPLAY    *P5:4,*EF
          DISPLAY    *P20:5,*V2LON,*ULON,"NURSING STATIONS"
          MOVE       SIX,CVERT
          MOVE       ONE,CCOL
          MOVE       SP6,KEY6
          MOVE       SP3,SAWARD
          CALL       RDSNURS1
.
QNUR1000  CALL       RDKNURS1
          BRANCH     OVRCD OF QNUR9000
.
          MATCH      SP3,NWARD
          IF         @EQUAL
            DISPLAY    *PCCOL:CVERT,*V2LON,NSTAT," ",*HOFF,NSDESC
            ADD        ONE,CVERT
          ENDIF
.
          MOVE       NSTAT,SAVNSTAT
.
          MOVE       NSTAT,SAVNSTAT
          MOVE       NSDESC,SAVNDESC
          MOVE       NSIST,SAVNSIST
          GOTO       QNUR1000
.
QNUR9000  MOVE       SP3,SAVNSTAT
          KEYIN      *P1:24,*EL,"Nursing Station: ",SAVNSTAT
          MATCH      SP3,SAVNSTAT
          IF         @EOS
            MOVE       ONE,EXIT
          ELSE
            MOVE       ZERO,EXIT
          ENDIF
.
QNUR9999  RETURN
.*******************************************************************************
+
.
.         EMPTY BED PRINTOUT
.
EMPBED1   MATCH     WWARD,SWARD
          CALL      HEAD IF NOT EQUAL
.
          COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          PRINT     *1,"| ",WBED,*7,"|",*16,"|",*26,"|":
                    *47,"|",*57,"|",*68,"|",*79,"|":
                    *100,"| ***  EMPTY BED  ***",*132,"|":
                    *N,"|",*7,"|",*16,"|",*26,"|",*47,"|":
                    *57,"|",*68,"|",*79,"|",*100,"|",*132,"|":
                    *N,"|",*7,"|",*16,"|",*26,"|",*47,"|":
                    *57,"|",*68,"|",*79,"|",*100,"|",*132,"|":
                    *N,"|",*7,"|",*16,"|",*26,"|",*47,"|":
                    *57,"|",*68,"|",*79,"|",*100,"|",*132,"|"
.
          CALL      PAGEND
.
          MOVE      WWARD,SWARD
          ADD       FOUR,CLNO
          DISPLAY   *P27:24,*V2LON,WWARD,*P46:24,WBED
          RETURN
+
.
.         LAST LINE ON THE CODE
.
PAGEND    PRINT     *1,"*-----------------------------------------":
                       "-------------------------------------------":
                       "----------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
.         HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,CPAGENO
          CLOCK     TIME,CTIMEIS
.
          PRINT     *F,*1,"IBAPAT88",*45,CNAME,*116,"DATE : ":
                    CDATE:
                    *N,*1,VERSION: 
                    *45,"***  HANDOVER REPORT 1 ***": 
                    *116,"TIME :   ",CTIMEIS,*N:
                    *116,"PAGE : ",*130,CPAGENO
          MOVE      THREE,CLNO 
          IF        OPTION=4
            IF        IBCNMHOS=1
              PRINT     *45,"Hospital : ",PTHSNAME
              ADD       ONE,CLNO
            ENDIF
          ENDIF
          BRANCH    OPTION OF HEAD13,HEAD11,HEAD11,HEAD13
          GOTO      HEAD13
.
HEAD11    PRINT     *1,"SISTER : ",SAVNSIST:
                    *45,"STATION : ",SAVNSTAT,*60,SAVNDESC:
                    *95,"Extension : ",SAVNEXTN,*N
          ADD       TWO,CLNO
          GOTO      HEAD13
.
HEAD12    PRINT     *N;
          ADD       ONE,CLNO
.
HEAD13    PRINT     *45,"WARD    : ",WWARD,*60,WRDESC:
                    *95,"Extension : ",SAVWEXTN,*N
          ADD       TWO,CLNO
.
          CALL      PAGEND
.
          PRINT     *1,"| Bed":
                    *7,"| Adm No":
                    *16,"| U/R No":
                    *26,"| Patient Name":
                    *47,"| Sex/Age":
                    *57,"| Religion":
                    *68,"| Admitted":
                    *79,"| Att Doctor/Op Date ":
                    *100,"| Comments",*132,"|"
.
          ADD       ONE,CLNO
          CALL      PAGEND
.
          MOVE      WWARD,SWARD
          RETURN
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:13,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TEN3,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:13,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
.
+
.*****************************************************************************
.*  GETALTID
.*  Procedure to get an alternate ID
.*  Exit=0  No alternate ID
.*       1  Alternate ID's exist
.*****************************************************************************
          DEFPROC   GETALIDS
.
          INC       PMSAIDFD/INC
.
          MOVE      ZERO,EXIT
          PACK      KEY20,SP70
          OPEN      PMSAIDA1,"pmsaidaf"
.
          PACK      KEY31,PURNO,SP70
          CALL      RSPMAID1           * Read alternate id Table
GETALI10  CALL      RKPMAID1
          BRANCH    OVRCD,GETALI99
.
          MATCH     PURNO,PMAIURNO     * Check same UR
          GOTO      GETALI99 IF NOT EQUAL
.
          MATCH     "1",PMAIDISU
          GOTO      GETALI10 IF NOT EQUAL
.
          PACK      KEY20,PMAIALID,SP70
          MOVE      ONE,EXIT
          GOTO      GETALI99
.
          INC       PMSAIDIO/INC
          INC       IBAOVRIO/INC
.
GETALI99  CLOSE     PMSAIDA1
.
          ENDPROC
.
ENDIT    CHAIN      PGM
         STOP
.
.==============================================================================
.
          INC       STD001IO
          INC       RTIODAYS                * Calculate number of days in hosp
          INC       PATHSPKY
          INC       PATWRDDS
          INC       CALCAGE
          INC       SEXDSCIO
.
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATNOBIO/INC
          INC       PATNURIO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       PMSWORIO/INC
          INC       OPRDEAIO/INC

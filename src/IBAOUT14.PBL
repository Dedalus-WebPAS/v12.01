.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAOUT14.PBL                                              *
.* Name           : Site Specific Parameters Maintenance                      *
.******************************************************************************
.* Date           : 04/02/1992                                                *
.* Author         : Justin Coates                                             *
.* Function       : To maintain the Site Specific Parameters file             *
.* Modifications  :                                                           *
.*        V5.01.002 21/09/1992  Graeme Williams                               *
.*                  Removed *FILL on KEYIN's                                  *
.******************************************************************************
.
.         FD's required
.
          INC       STD001FD/PBL
          INC       OUTSIPFD/INC            * site spec. para file
          INC       OUTSITFD/INC            * site file
.
.         program variables
.
SCRNFLAG  FORM      1         * redisplay flag
.
.         program constants
.
PRGID     INIT      "IBAOUT14"
PRGNAM    INIT      "Site Specific Parameters Maintenance"
VERSION   INIT      "V12.01.00"
+
.*********************************************************************
.*                  ML0000                                           *
.*        Mainline Code / Controlling Logic                          *
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      KSIT0000                * enter the site code
          BRANCH    EXIT,ML9999             * exit
.
          CALL      MSIP0000                * maintain the file
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialization routine                                     *
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening outsitaf"
          OPEN      OUTSITA1,"outsitaf"
          DISPLAY   *P58:24,"outsipaf"
          OPEN      OUTSIPA1,"outsipaf"
INIT9999  RETURN
+
.********************************************************************
.*                  KSIT0000                   Called by : ML1000   *
.*        Keyin a Site code                                         *
.*        Returns : EXIT  = 1     nothing entered                   *
.********************************************************************
KSIT0000  MOVE      "4",CVERT               * row
          MOVE      "23",CCOL               * column
          MOVE      ONE,SCRNFLAG            * ? not entered
          DISPLAY   *P1:3,*EF:
                    *P1:4,"Enter the Site code : "
.
KSIT1000  KEYIN     *PCCOL:CVERT,*DV,UNDLN6:
                    *PCCOL:CVERT,*V2LON,OSTSITE:
                    *PCCOL:CVERT,*DV,OSTSITE;
.
          ENDSET    OSTSITE
          APPEND    SP6,OSTSITE
          RESET     OSTSITE
.
          MATCH     EXITCHAR,OSTSITE 
          GOTO      KSIT9000 IF EQUAL       * exitchar entered
.
          MATCH     SP6,OSTSITE 
          GOTO      KSIT9000 IF EQUAL       * nothing entered
.
          MATCH     QUEST,OSTSITE
          GOTO      KSIT2000 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          CALL      OUTSITDS                * ? routine
          MOVE      TWENTY4,CVERT           * row for input
          MOVE      TEN8,CCOL               * column for input
          MOVE      ZERO,SCRNFLAG
          DISPLAY   *P1:24,"Input the Site : ",*EL
          GOTO      KSIT1000
.
.         validate what was entered
.
KSIT2000  MOVE      OSTSITE,KEY6            * key of entered value
          CALL      RDSITA1                 * read site file
          BRANCH    OVRCD,KSIT7000          * invalid input
.
          MOVE      ZERO,EXIT
          GOTO      KSIT9999                * valid input
.
.         ERROR: invalid code entered
.
KSIT7000  DISPLAY   *P1:24,*EL,*B,"Invalid Site code.  ",*EL;
          CALL      HITENTER
          BRANCH    FORM1,KSIT1000          * ? not entered
          DISPLAY   *P1:24,"Input the Site : ",*EL
          GOTO      KSIT1000
.
KSIT9000  MOVE      ONE,EXIT                * exitchar entered
.
KSIT9999  RETURN
+
.*********************************************************************
.*                  MSIP0000                    Called by : ML1000   *
.*        Maintain the OUTSIP file                                   *
.*********************************************************************
MSIP0000  CALL      CLRA0000                * clear fields
          MOVE      OSTSITE,KEY6            * site is the key
          CALL      RDOTSIP1                * read parameters file
          CALL      SCRN0000                * display screen
          COMPARE   ZERO,OVRCD
          GOTO      MSIP5000 IF EQUAL       * change mode
.
.         insert mode
.
          MOVE      ONE,CCITEM              * set counter of field up to
          WHILE     CCITEM <= 20
            CALL      KCAT0000                * keyin the category
            ADD       ONE,CCITEM
          DO
.
.         change mode
.
MSIP5000  CALL      MAINQST
          COMPARE   ZERO,CCITEM
          GOTO      MSIP8000 IF EQUAL       * Post
          GOTO      MSIP9999 IF LESS        * cancel
.
          COMPARE   "21",CCITEM
          GOTO      MSIP5100 IF LESS        * valid number
.
          BEEP
          GOTO      MSIP5000
.
MSIP5100  CALL      KCAT0000                * enter the category
          GOTO      MSIP5000
.
.         Post selected
.
MSIP8000  MOVE      OSTSITE,KEY6
          CALL      RAOTSIP1
          IF        OVRCD = 1
            MOVE      OSTSITE,OTSPSITE
            CALL      WROTSIP1
          ELSE
            CALL      UPOTSIP1
          ENDIF
.
MSIP9999  RETURN
+
.*********************************************************************
.*                  CLRA0000                    Called by : MSIP0000 *
.*        Clear all the input fields                                 *
.*********************************************************************
CLRA0000  UNPACK    SP10,OTSPXC01,OTSPXC02,OTSPXC03,OTSPXC04,OTSPXC05
          UNPACK    SP10,OTSPXC06,OTSPXC07,OTSPXC08,OTSPXC09,OTSPXC10
          UNPACK    SP10,OTSPXC11,OTSPXC12,OTSPXC13,OTSPXC14,OTSPXC15
          UNPACK    SP10,OTSPXC16,OTSPXC17,OTSPXC18,OTSPXC19,OTSPXC20
CLRA9999  RETURN
+
.*********************************************************************
.*                  SCRN0000                    Called by : MSIP0000 *
.*        Display the data input screen                              *
.*********************************************************************
SCRN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,"Enter the Site code : ",*V2LON,OSTSITE,*HOFF:
                    SP1,OSTDESC:
                    *P1:06,*V2LON," 1. ",*HOFF,"Category 01 :    ":
                    *V2LON," 11. ",*HOFF,"Category 11 :    ":
                    *P1:07,*V2LON," 2. ",*HOFF,"Category 02 :    ":
                    *V2LON," 12. ",*HOFF,"Category 12 :    ":
                    *P1:08,*V2LON," 3. ",*HOFF,"Category 03 :    ":
                    *V2LON," 13. ",*HOFF,"Category 13 :    ":
                    *P1:09,*V2LON," 4. ",*HOFF,"Category 04 :    ":
                    *V2LON," 14. ",*HOFF,"Category 14 :    ":
                    *P1:10,*V2LON," 5. ",*HOFF,"Category 05 :    ":
                    *V2LON," 15. ",*HOFF,"Category 15 :    ":
                    *P1:11,*V2LON," 6. ",*HOFF,"Category 06 :    ":
                    *V2LON," 16. ",*HOFF,"Category 16 :    ":
                    *P1:12,*V2LON," 7. ",*HOFF,"Category 07 :    ":
                    *V2LON," 17. ",*HOFF,"Category 17 :    ":
                    *P1:13,*V2LON," 8. ",*HOFF,"Category 08 :    ":
                    *V2LON," 18. ",*HOFF,"Category 18 :    ":
                    *P1:14,*V2LON," 9. ",*HOFF,"Category 09 :    ":
                    *V2LON," 19. ",*HOFF,"Category 19 :    ":
                    *P1:15,*V2LON,"10. ",*HOFF,"Category 10 :    ":
                    *V2LON," 20. ",*HOFF,"Category 20 :    "
.
.         display the current fields
.
          MOVE      ONE,FORM2
          MOVE      SIX,CVERT
          WHILE     FORM2 <= 10
            LOAD      DIM2,FORM2,OTSPXC01,OTSPXC02,OTSPXC03,OTSPXC04,OTSPXC05:
                                 OTSPXC06,OTSPXC07,OTSPXC08,OTSPXC09,OTSPXC10
            DISPLAY   *P19:CVERT,*V2LON,DIM2
            ADD       ONE,CVERT
            ADD       ONE,FORM2
          DO
.
          MOVE      ONE,FORM2
          MOVE      SIX,CVERT
          WHILE     FORM2 <= 10
            LOAD      DIM2,FORM2,OTSPXC11,OTSPXC12,OTSPXC13,OTSPXC14,OTSPXC15:
                                 OTSPXC16,OTSPXC17,OTSPXC18,OTSPXC19,OTSPXC20
            DISPLAY   *P41:CVERT,*V2LON,DIM2
            ADD       ONE,CVERT
            ADD       ONE,FORM2
          DO
.
SCRN9999  RETURN
+
.*********************************************************************
.*                  KCAT0000                    Called by : MSIP0000 *
.*        Keyin the category                                         *
.*        Para's  : CCITEM        category number                    *
.*********************************************************************
KCAT0000  IF        ((CCITEM >= 1) & (CCITEM <= 10))
            MOVE      CCITEM,CVERT            * set up row
            ADD       FIVE,CVERT              * set up row
            MOVE      "19",CCOL               * set col
          ELSE
            MOVE      CCITEM,CVERT            * set up row
            SUB       FIVE,CVERT              * set up row
            MOVE      "41",CCOL               * set col
          ENDIF
.
          KEYIN     *PCCOL:CVERT,*DV,UNDLN2:
                    *PCCOL:CVERT,*V2LON,DIM2:
                    *PCCOL:CVERT,*DV,DIM2
.
          PACK      DIM2,DIM2,SP2
.
          STORE     DIM2,CCITEM,OTSPXC01,OTSPXC02,OTSPXC03,OTSPXC04,OTSPXC05:
                                OTSPXC06,OTSPXC07,OTSPXC08,OTSPXC09,OTSPXC10:
                                OTSPXC11,OTSPXC12,OTSPXC13,OTSPXC14,OTSPXC15:
                                OTSPXC16,OTSPXC17,OTSPXC18,OTSPXC19,OTSPXC20
.
KCAT9999  RETURN
+
.
.         I/O includes
.
          INC       STD001IO/PBL
          INC       OUTSIPIO/INC            * site spec. para file
          INC       OUTSITIO/INC            * site file
          INC       OUTSITDS/PBL            * ? for site file
...............................................................................

. **********************************************************
. *              P R O G R A M  S U M M A R Y              *
. *              -------------  -------------              *
. *                                                        *
. *     PROGRAM NAME:     IBAOUT52                         *
. *                                                        *
. *     FUNCTION:         CLINIC TYPE MASTER LISTING       *
. *                                                        *
. *     DATE WRITTEN:     23.06.88                         *
. *                                                        *
. *     MODIFICATIONS:                                     *
. *                                                        *
. *     V5.07.B01  Nicole Harrington  507 rebound 095      *
. *                Mods to use HEAD132                     *
. **********************************************************
.
          INC       STD001FD
.
          INC       OUTCTYFD/INC
          INC       PATCODFD/INC
.
CODE      INIT      "CG"
.
END3      DIM       3
END6      DIM       6
END30     DIM       30
.
FSTREC    INIT      "START"
.
LN55      FORM      "55"
LSTREC    INIT      "FINISH"
.
START3    DIM       3
START6    DIM       6
START30   DIM       30
.
XDIM6     DIM       6
YDIM6     DIM       6
XDIM30    DIM       30
YDIM30    DIM       30
.
.
PRGID     INIT      "IBAOUT52"
PRGNAM    INIT      "Clinic Type Register"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening";
.
          PACK      CFNAME WITH UID,FILCTYA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCTYA1,CFNAME
.
          PACK      CFNAME WITH UID,FILCTYA2
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCTYA2,CFNAME
.
          PACK      CFNAME WITH UID,FILCTYA3
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCTYA3,CFNAME
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Clinic Type Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Group Code Sequence":
                    *P1:7,*V2LON,THREE,*HOFF," = Clinic Type Description Sequence"
.
BEGIN     KEYIN     *P1:9,*EF,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF SDISEA,KGROUP,KDESC
          DISPLAY   *P35:9,*B,*V2LON,"** Invalid Selection **",*W2;
          GOTO      BEGIN
.
.------------------------------------------------------------------------------
.        Clinic Type Sequence
.------------------------------------------------------------------------------
.
SDISEA    DISPLAY   *P1:11,"Keyin Clinic Type From :":
                    *P1:13,"             Type To   :"
          MOVE      SP6,START6
          MOVE      SP6,END6
.
KEYST     KEYIN     *P26:13,*EL,*P26:11,*EL,*V2LON,START6,*P26:11,*DV,START6
          RESET     START6
          GOTO      CONT1 IF NOT EOS
.
          MOVE      SP6,START6
          DISPLAY   *P26:11,*V2LON,FSTREC
          GOTO      CONT2
.
CONT1     ENDSET    START6
          APPEND    SP6,START6
          RESET     START6
.
          PACK      KEY12 WITH IDDD,START6
          CALL      RDCTYA1
          COMPARE   ZERO,OVRCD
          GOTO      CONT2 IF EQUAL
.
          DISPLAY   *P43:11,*B,*V2LON,"** Clinic Type does Not Exist **",*W2;
          GOTO      KEYST
.
CONT2     KEYIN     *P26:13,*EL,*V2LON,END6
          RESET     END6
          GOTO      CONT3 IF NOT EOS
.
          MOVE      SP6,END6
          DISPLAY   *P26:13,*V2LON,LSTREC
          GOTO      CONFIRM
.
CONT3     ENDSET    END6
          APPEND    SP6,END6
          RESET     END6
.
          MATCH     START6,END6
          GOTO      CONT3X IF NOT LESS
.
          DISPLAY   *P43:13,*B,*V2LON,"** Must be greater than FROM type **",*W2;
          GOTO      KEYST
.
CONT3X    PACK      KEY12 WITH IDDD,END6
          CALL      RDCTYA1
          COMPARE   ZERO,OVRCD
          GOTO      CONFIRM IF EQUAL
.
          DISPLAY   *P43:13,*B,*V2LON,"** Clinic ID does Not Exist **",*W2;
          GOTO      CONT2
.
.------------------------------------------------------------------------------
.        Group Code Sequence
.------------------------------------------------------------------------------
.
KGROUP    DISPLAY   *P1:11,"Keyin Group Code From :":
                    *P1:13,"            Code To   :"
          MOVE      SP3,START3
          MOVE      SP3,END3
.
KEYST1    KEYIN     *P25:13,*EL,*P25:11,*EL,*V2LON,START3,*P25:11,*DV,START3
          RESET     START3
          GOTO      CONT1A IF NOT EOS
.
          MOVE      SP3,START3
          DISPLAY   *P25:11,*V2LON,FSTREC
          GOTO      CONT2A
.
CONT1A    ENDSET    START3
          APPEND    SP3,START3
          RESET     START3
.
          PACK      KEY15 WITH IDDD,START3,SP6
          CALL      RDSCTYA3
          CALL      RDKCTYA3
.
          MATCH     IDDD TO OCTSITE
          GOTO      ERRSIT1 IF NOT EQUAL
.
          MATCH     OCTGRP TO START3
          GOTO      CONT2A IF EQUAL
.
ERRSIT1   DISPLAY   *P43:11,*B,*V2LON,"** Group Code does Not Exist **",*W2;
          GOTO      KEYST1
.
CONT2A    KEYIN     *P25:13,*V2LON,*EL,END3
          RESET     END3
          GOTO      CONT3A IF NOT EOS
.
          MOVE      SP3,END3
          DISPLAY   *P25:13,*V2LON,LSTREC
          GOTO      CONFIRM
.
CONT3A    ENDSET    END3
          APPEND    SP3,END3
          RESET     END3
.
          MATCH     START3,END3
          GOTO      CONT3AX IF NOT LESS
.
          DISPLAY   *P43:13,*B,*V2LON:
                    "** Must be greater than FROM group **",*W2;
          GOTO      CONT2A
.
CONT3AX   PACK      KEY15 WITH IDDD,END3,SP6
          CALL      RDSCTYA3
          CALL      RDKCTYA3
.
          MATCH     IDDD TO OCTSITE
          GOTO      ERRSIT2 IF NOT EQUAL
.
          MATCH     OCTGRP TO END3
          GOTO      CONFIRM IF EQUAL
.
ERRSIT2   DISPLAY   *P43:13,*B,*V2LON,"** Group Code does Not Exist **",*W2;
          GOTO      CONT2A
.
.------------------------------------------------------------------------------
.       Clinic Type Description Sequence
.------------------------------------------------------------------------------
.
KDESC     DISPLAY   *P1:11,"Keyin Clinic Desc. From :":
                    *P1:13,"             Desc. To   :"
          MOVE      SP30,START30
          MOVE      SP30,END30
.
KEYST2    KEYIN     *P27:13,*EL,*P27:11,*EL,*V2LON,START30,*P27:11,*DV,START30
          RESET     START30
          GOTO      CONT1B IF NOT EOS
.
          MOVE      SP30,START30
          DISPLAY   *P27:11,*V2LON,FSTREC
          GOTO      CONT2B
.
CONT1B    ENDSET    START30
          APPEND    SP30,START30
          RESET     START30
.
          PACK      KEY42 WITH IDDD,START30,SP6
          CALL      RDSCTYA2
          CALL      RDKCTYA2
.
          MATCH     IDDD TO OCTSITE
          GOTO      ERRSIT3 IF NOT EQUAL
.
          MATCH     OCTDESC TO START30
          GOTO      CONT2B IF EQUAL
.
ERRSIT3   DISPLAY   *P43:11,*B,*V2LON,"** Description does Not Exist **",*W2;
          GOTO      KEYST2
.
CONT2B    KEYIN     *P27:13,*V2LON,*EL,END30
          RESET     END30
          GOTO      CONT3B IF NOT EOS
.
          MOVE      SP30,END30
          DISPLAY   *P27:13,*V2LON,LSTREC
          GOTO      CONFIRM
.
CONT3B    ENDSET    END30
          APPEND    SP30,END30
          RESET     END30
.
          MATCH     START30,END30
          GOTO      CONT3BX IF NOT LESS
.
          DISPLAY   *P43:13,*B,*V2LON:
                    "** Must be greater than FROM desc. **",*W2;
          GOTO      CONT2B
.
CONT3BX   PACK      KEY42 WITH IDDD,END30,SP6
          CALL      RDSCTYA2
          CALL      RDKCTYA2
.
          MATCH     IDDD TO OCTSITE
          GOTO      ERRSIT4 IF NOT EQUAL
.
          MATCH     OCTDESC TO END30
          GOTO      CONFIRM IF EQUAL
.
ERRSIT4   DISPLAY   *P43:13,*B,*V2LON,"** Description does Not Exist **",*W2;
          GOTO      CONT2B
.
.------------------------------------------------------------------------------
.       Start of print generation
.------------------------------------------------------------------------------
.
CONFIRM   KEYIN     *P1:24,*EL,"Ok to Continue (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSN,*HOFF:
                    ") ? ",*V2LON,ANS;
.
          CMATCH    ANSN,ANS
          GOTO      BEGIN IF EQUAL
.
          CMATCH    ANSY,ANS
          GOTO      CONT4 IF EQUAL
.
          BEEP
          GOTO      CONFIRM
.
CONT4     DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "*** Printing CLINIC TYPE Master File ***",*W2;
.
          DISPLAY   *P1:24,*EL,*P20:24,"Printing :"
.
          CALL      HEADSET     /* Set up print variables for page header */
.
          MOVE      ZERO,CPAGENO
.
          BRANCH    OPTION TO READ1,READ2,READ3
.
READ1     MATCH     SP6,START6
          GOTO      CONT51A IF NOT EQUAL
.
          PACK      KEY12 WITH IDDD,SP6
          CALL      RDSCTYA1
.
LOOP1     CALL      RDKCTYA1
          BRANCH    OVRCD OF EOF
.
          MATCH     IDDD TO OCTSITE
          GOTO      EOF IF NOT EQUAL
.
          GOTO      CONT51
.
CONT51A   PACK      KEY12 WITH IDDD,START6
          CALL      RDCTYA1
.
CONT51    MATCH     SP6,END6
          GOTO      CONT61 IF EQUAL
.
          MATCH     OCTCTYP,END6
          GOTO      EOF IF LESS
.
CONT61    DISPLAY   *P31:24,*V2LON,OCTCTYP;
          GOTO      LOOPOUT
.
. Option 2
.
READ2     PACK      KEY15 WITH IDDD,START3,SP6
          CALL      RDSCTYA3
.
LOOP2     CALL      RDKCTYA3
          BRANCH    OVRCD OF EOF
.
          MATCH     IDDD TO OCTSITE
          GOTO      EOF IF NOT EQUAL
.
.
CONT52    MATCH     SP3,END3
          GOTO      CONT62 IF EQUAL
.
          MATCH     OCTGRP,END3
          GOTO      EOF IF LESS
.
CONT62    DISPLAY   *P31:24,*V2LON,OCTGRP;
          GOTO      LOOPOUT
.
. Option 3
.
READ3     PACK      KEY42 WITH IDDD,START30,SP6
          CALL      RDSCTYA2
.
LOOP3     CALL      RDKCTYA2
          BRANCH    OVRCD OF EOF
.
          MATCH     IDDD TO OCTSITE
          GOTO      EOF IF NOT EQUAL
.
.
CONT53    MATCH     SP30,END30
          GOTO      CONT63 IF EQUAL
.
          MATCH     OCTDESC,END30
          GOTO      EOF IF LESS
.
CONT63    DISPLAY   *P31:24,*V2LON,OCTDESC;
.
LOOPOUT   COMPARE   ZERO,CPAGENO
          CALL      PRINTH IF EQUAL
.
          COMPARE   LN55,CLNO
          CALL      PRINTH IF NOT LESS
.
          CALL      PRINTD
          BRANCH    OPTION TO LOOP1,LOOP2,LOOP3
          GOTO      LOOP1
.
EOF       COMPARE   ZERO,CPAGENO
          GOTO      NOREC IF EQUAL
.
          BRANCH    OPTION TO EORPT1,EORPT2,EORPT3
EORPT1    PRINT     *23,"|",*39,"|",*68,"|",*103,"|",*N:
                    *23,"*----------------------------------------":
                    "---------------------------------------*":
                    *N,*N,*N,*45,"***  END OF REPORT  ***"
          GOTO      EORPTOUT
EORPT2    PRINT     *23,"|",*52,"|",*68,"|",*103,"|",*N:
                    *23,"*----------------------------------------":
                    "---------------------------------------*":
                    *N,*N,*N,*45,"***  END OF REPORT  ***"
          GOTO      EORPTOUT
EORPT3    PRINT     *23,"|",*58,"|",*74,"|",*103,"|",*N:
                    *23,"*----------------------------------------":
                    "---------------------------------------*":
                    *N,*N,*N,*45,"***  END OF REPORT  ***"
EORPTOUT  DISPLAY   *P1:24,*EL,*P15:24,*V2LON:
                    "*** Generation Completed ***",*W2;
          GOTO      BEGIN
.
NOREC     DISPLAY   *P1:24,*EL,*P20:24,*V2LON,*B:
                    "*** No Clinic Type selected ***",*W2;
          GOTO      BEGIN
.
PRINTH    COMPARE   ZERO TO CPAGENO
          GOTO      PG1 IF EQUAL
.
          BRANCH    OPTION TO PGRPT1,PGRPT2,PGRPT3
PGRPT1    PRINT     *23,"|",*39,"|",*68,"|",*103,"|",*N:
                    *23,"*----------------------------------------":
                    "---------------------------------------*"
          GOTO      PG1
PGRPT2    PRINT     *23,"|",*52,"|",*68,"|",*103,"|",*N:
                    *23,"*----------------------------------------":
                    "---------------------------------------*"
          GOTO      PG1
PGRPT3    PRINT     *23,"|",*58,"|",*74,"|",*103,"|",*N:
                    *23,"*----------------------------------------":
                    "---------------------------------------*"
.
PG1       CALL      IBACLOCK    
          MOVE      ONE,CNOUNDLN
          CALL      HEAD132
.
          BRANCH    OPTION TO RPTHDR1,RPTHDR2,RPTHDR3
RPTHDR1   PRINT     *40,"Clinic Type Sequence From : ",XDIM6:
                    *N,*66,"To   : ",YDIM6,*N:
                    *N,*23,"*-----------------------------------":
                    "--------------------------------------------*":
                    *N,*23,"|  Clinic Type  |  Group Code":
                    *68,"|  Clinic Type Description",*103,"|":
                    *N,*23,"*-----------------------------------":
                    "--------------------------------------------*":
                    *N,*23,"|",*39,"|",*68,"|",*103,"|"
          GOTO      RPTHDOUT
RPTHDR2   PRINT     *40,"Group Code Sequence From : ",XDIM6:
                    *N,*65,"To   : ",YDIM6,*N:
                    *N,*23,"*-----------------------------------":
                    "--------------------------------------------*":
                    *N,*23,"|  Group Code",*52,"|  Clinic Type ":
                    " |  Clinic Type Description",*103,"|":
                    *N,*23,"*-----------------------------------":
                    "--------------------------------------------*":
                    *N,*23,"|",*52,"|",*68,"|",*103,"|"
          GOTO      RPTHDOUT
RPTHDR3   PRINT     *40,"Clinic Type Description Sequence From : ",XDIM30:
                    *N,*78,"To   : ",YDIM30,*N:
                    *N,*23,"*-----------------------------------":
                    "--------------------------------------------*":
                    *N,*23,"|  Clinic Type Description",*58,"|  ":
                    "Clinic Type  |  Group Code",*103,"|":
                    *N,*23,"*-----------------------------------":
                    "--------------------------------------------*":
                    *N,*23,"|",*58,"|",*74,"|",*103,"|"
RPTHDOUT
.
          MOVE      "10",CLNO
          RETURN
.
PRINTD    PACK      KEY5 WITH CODE,OCTGRP
          CALL      RDCODE1
          BRANCH    OPTION TO PRINTD1,PRINTD2,PRINTD3
PRINTD1   PRINT     *23,"|",*26,OCTCTYP,*39,"|",*42,OCTGRP:
                    *46,TDESC,*68,"|",*71,OCTDESC,*103,"|"
          ADD       ONE TO CLNO
          RETURN
PRINTD2   PRINT     *23,"|",*26,OCTGRP,*30,TDESC,*52,"|",*55,OCTCTYP:
                    *68,"|",*71,OCTDESC,*103,"|"
          ADD       ONE TO CLNO
          RETURN
PRINTD3   PRINT     *23,"|",*26,OCTDESC,*58,"|",*61,OCTCTYP:
                    *74,"|",*77,OCTGRP,*81,TDESC,*103,"|"
          ADD       ONE TO CLNO
          RETURN
.
HEADSET   BRANCH    OPTION TO HEADSET1,HEADSET2,HEADSET3
HEADSET1  MOVE      SP6,XDIM6
          MOVE      SP6,YDIM6
          MOVE      START6,XDIM6
          MOVE      END6,YDIM6
          MATCH     SP6,START6
          GOTO      CONT8A IF NOT EQUAL
          MOVE      FSTREC,XDIM6
CONT8A    MATCH     SP6,END6
          RETURN    IF NOT EQUAL
          MOVE      LSTREC,YDIM6
          RETURN
.
HEADSET2  MOVE      SP6,XDIM6
          MOVE      SP6,YDIM6
          MOVE      START3,XDIM6
          MOVE      END3,YDIM6
          MATCH     SP3,START3
          GOTO      CONT8B IF NOT EQUAL
          MOVE      FSTREC,XDIM6
CONT8B    MATCH     SP3,END3
          RETURN    IF NOT EQUAL
          MOVE      LSTREC,YDIM6
          RETURN
.
HEADSET3  MOVE      SP30,XDIM30
          MOVE      SP30,YDIM30
          MOVE      START30,XDIM30
          MOVE      END30,YDIM30
          MATCH     SP30,START30
          GOTO      CONT8C IF NOT EQUAL
          MOVE      FSTREC,XDIM30
CONT8C    MATCH     SP30,END30
          RETURN    IF NOT EQUAL
          MOVE      LSTREC,YDIM30
          RETURN
.
ENDIT     CHAIN     PGM
          STOP
.
          INC       OUTCTYIO/INC
          INC       PATCODIO/INC
          INCLUDE   STD001IO
.

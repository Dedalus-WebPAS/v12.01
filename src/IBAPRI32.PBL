.***************************************************************************
.* System    :   Private Practice                                          *
.* Program   :   IBAPRI32                                                  *
.* Desc      :   Medical Practice Register                                 *
.***************************************************************************
.* Date      :   22/08/91                                                  *
.* Author    :   Stephen Armstrong                                         *
.* Function  :   Provides a listing of medical practices over a given      *
.*               range                                                     *
.* Mods      :                                                             *
.* V10.13.01 02/08/2018  Richa Phogat  TSK 0848506                         *
.*           Updated keylength for RSPRDO1, RSPRDO1 from KEY18 to KEY22    *
.*           Matched PRDODOCT with SP70 instead of SP6                     *
.*           Replaced PATDO1FD by PMSHCPFD                                 *
.*           06/08/2018  Tracey Nguyen TSK 0848506                         *
.*           Moved PRPRPDOC to KEY10 instead of KEY6                       *
.* V9.11.01  20/10/2008  J.Tan         CAR 178415                          *
.*           Mods checking for Medical practice user access                *
.* V9.03.00  25/02/2004 Lina Vo    CAR 44154                               *
.*           Ported to V9.03                                               *
.* V5.08.01  05/05/2000  J.Tan                                             *
.*           Recompiled for PRIDOCT file                                   *
.* V6.00.01  12/10/92  Steve Armstrong                                     *
.*           Brought version number in line                                *
.***************************************************************************
.
.$$$$$
.         Medical Practice Register Program (IBAPRI32)
.         --------------------------------------------
.
.         ML0000
.         Initialisation and open files
.         controlf
.         pridoctf
.         pmshcpaf
.         pripracf
.
.         ML0100
.         Get user option
.         If exit selected, return to calling program
.         if valid option, proceed to get starting M.P. code
.
.         ML1000
.         Display input screen
.         Get starting M.P. code
.             If exitchar input, return to get next option
.             If nothing entered, assume first M.P. code, then proceed to get
.                end M.P. code
.             If valid code input, proceed to get end M.P. code
.
.
.         Get ending M.P. code
.             If exitchar input, return to get next option
.             If nothing entered, assume last M.P. code, then proceed to get
.                confirmation to continue
.             If valid code input, proceed to get confirmation to continue
.
.
.         Get confirmation to continue
.             If (Y)es input, proceed to process printout
.             If (N)o input, return to get next starting M.P. code
.             If (C)ancel input, return to get next option
.
.         ML6000
.         Get each medical practice on file and all the doctors associated
.             with those practices and print them
.
.         ML9999
.         Chain back to calling program
.
.$$$$$
.
.
          INC       STD001FD
.
          INC       PRIPRAFD/INC                 * Medical practice file
          INC       PRIDOCFD/INC                 * M.P. doctor file
          INC       PMSHCPFD/INC                 * HCP file
          INC       PRIALSFD/INC                 * Medical Prac.all user access
          INC       PRISECFD/INC                 * Medical Prac.security access
.
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
.
.
. CONSTANTS DEFINITION
. --------------------
.
FINISH    INIT      "Finish"
PIPE      INIT      "|"
START     INIT      "Start"
ZED6      INIT      "zzzzzz"
ZED18     INIT      "zzzzzzzzzzzzzzzzzz"
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
AFLAG     FORM      1                            * assoc doc flag
.                                                   0 = associated doctors found
.                                                   1 = no assoc. doc found yet
AFNAME    DIM       30                           * formatted assoc. doc. name
EPRAC     DIM       6                            * end practice
MPRAC     DIM       6                            * medical practice
PFNAME    DIM       30                           * formatted primary doc. name
SPRAC     DIM       6                            * start practice
USERID    DIM       10
.
.
PRGID     INIT      "IBAPRI32"
PRGNAM    INIT      "Medical Practice Register"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialization and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * debtor sequence
.
ML1000    CALL      SCR0000                * display entry fields
          CALL      SMPR0000               * get starting medical practice
          BRANCH    EXIT,ML0100            * exitchar input - get next option
.
          CALL      EMPR0000               * get ending medical practice
          BRANCH    EXIT,ML0100            * exitchar input - get next option
.
          CALL      USID0000               * Keyin user id
.
          CALL      CONTQST                * confirm continuation
          BRANCH    CEXIT,ML6000:          * Yes
                          ML1000:          * No
                          ML0100           * Cancel
.
ML6000    CALL      PROC0000               * process printout
.
          GOTO      ML0100                 * get next option
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P56:24,"Opening :":
                    *P64:24,"pripracf";
          OPEN      PRIPRAC1,"pripracf"          * medical practice file
.
          DISPLAY   *P64:24,"pridoctf";
          OPEN      PRIDOCT1,"pridoctf"          * medical practice doctor file
.
          OPEN      PRIALSA1,"prialsaf"    
          OPEN      PRISECA1,"prisecaf"    
.
          DISPLAY   *P64:24,"pmshcpaf"
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"          * control file
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                  *
.*                        TRUE  (1)  Exit option selected                   *
.*              COPTION = 1  Medical Practice sequence                      *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Medical Practice Sequence"
.
OPTN1000  KEYIN     *P1:7,*EL,"Select option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9000 IF EQUAL            * exit
.
          BRANCH    COPTION,OPTN9500
.
          BEEP
          GOTO      OPTN1000                     * invalid
.
OPTN9000  MOVE      ONE,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ZERO,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                              SCR0000                Called by: ML0000    *
.*                    display input screen for search                       *
.****************************************************************************
.
SCR0000   DISPLAY  *P1:11,*EF,"From Medical Practice code : ":
                   *P1:13,"To   Medical Practice code :"
.
SCR9999   RETURN
+
.****************************************************************************
.*                              SMPR0000               Called by: ML0000    *
.*                 get the starting medical practice                        *
.****************************************************************************
.
SMPR0000  KEYIN     *P30:11,*DV,UNDLN6:      * get M.P.
                    *P30:11,*V2LON,MPRAC
.
          CALL      CHEK0000                 * see what was entered
          BRANCH    EXIT,SMPR9900:           * exitchar entered
                         SMPR4000:           * ? entered
                         SMPR9000:           * valid M.P.
                         SMPR8000            * nothing entered
.
          GOTO      SMPR0000                 * invalid M.P.
.
SMPR4000  MOVE      ZERO,HLEF
          CALL      GETSCR00
SMPR4200  CALL      PRIPRADS                 * list M.P.'s on file
.
SMPR4500  KEYIN     *P1:24,*EL,"From Medical Practice : ",*DV,UNDLN6:
                    *P25:24,*V2LON,MPRAC
.         
          CALL      CHEK0000                 * see what was entered
          BRANCH    EXIT,SMPR9800:           * exitchar entered
                         SMPR4200:           * ? entered
                         SMPR8900:           * valid M.P. number
                         SMPR7900            * nothing entered
.
          GOTO      SMPR4500                 * invalid M.P.
.
SMPR7900  CALL      PUTSCR00                 * restore screen
SMPR8000  DISPLAY   *P30:11,*EL,*V2LON,START
          GOTO      SMPR9500
.
SMPR8900  CALL      PUTSCR00                 * restore screen
SMPR9000  DISPLAY   *P30:11,*EL,*V2LON,MPRAC:
                    *P40:11,*HOFF,PRPRDESC
.
SMPR9500  MOVE      MPRAC,SPRAC
          MOVE      ZERO,EXIT
          GOTO      SMPR9999
.
SMPR9800  CALL      FRESCR00
SMPR9900  MOVE      ONE,EXIT
.
SMPR9999  RETURN
+ 
.**************************************************************************
.*                                  CHEK0000           Called by: SMPR0000*
.*                         See if anything entered                EMPR0000*
.*         Exit - 0 = invalid entry                                       *
.*                1 = nothing/exitchar entered                            *
.*                2 = ? entered                                           *
.*                3 = valid medical practice
.**************************************************************************
.
CHEK0000  ENDSET    MPRAC
          APPEND    SP6,MPRAC
          RESET     MPRAC
.
          MATCH     SP6,MPRAC               * anything entered ?
          GOTO      CHEK9500 IF EQUAL       * no
.
          MATCH     EXITCHAR,MPRAC          * exitchar entered ?
          GOTO      CHEK7000 IF EQUAL       * yes
.
          MATCH     QUEST,MPRAC             * ? entered ?
          GOTO      CHEK8000 IF EQUAL       * yes
.
.         Medical Practice code entered
.
          MOVE      MPRAC,KEY6
          CALL      RDPRPR1                  * code on file ?
.
          COMPARE   ZERO,OVRCD               * yes
          GOTO      CHEK9000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Medical Practice not on file. ";
          CALL      HITENTER
.
          MOVE      ZERO,EXIT               * invalid entry
          GOTO      CHEK9999
.
CHEK7000  MOVE      ONE,EXIT                * nothing input
          GOTO      CHEK9999
.
CHEK8000  MOVE      TWO,EXIT                * ? input
          GOTO      CHEK9999
.
CHEK9000  MOVE      THREE,EXIT              * valid practice
          GOTO      CHEK9999
.
CHEK9500  MOVE      FOUR,EXIT               * nothing entered
.
CHEK9999  RETURN
+
.****************************************************************************
.*                              EMPR0000               Called by: ML0000    *
.*                 get the ending medical practice code                     *
.****************************************************************************
.
EMPR0000  KEYIN     *P30:13,*DV,UNDLN6:      * get M.P.
                    *P30:13,*V2LON,MPRAC
.
          CALL      CHEK0000                 * see what was entered
          BRANCH    EXIT,EMPR9900:           * exitchar entered
                         EMPR4000:           * ? entered
                         EMPR9000:           * valid M.P.
                         EMPR8000            * nothing entered
.
          GOTO      EMPR0000                 * invalid M.P.
.
EMPR4000  MOVE      ZERO,HLEF
          CALL      GETSCR00
EMPR4200  CALL      PRIPRADS                 * list M.P.'s on file
.
EMPR4500  KEYIN     *P1:24,*EL,"To Medical Practice : ",*DV,UNDLN6:
                    *P23:24,*V2LON,MPRAC
.         
          CALL      CHEK0000                 * see what was entered
          BRANCH    EXIT,EMPR9800:           * exitchar entered
                         EMPR4200:           * ? entered
                         EMPR8900:           * valid M.P.
                         EMPR7900            * nothing entered
.
          GOTO      EMPR4500                 * invalid M.P.
.
EMPR7900  CALL      PUTSCR00                 * restore screen
EMPR8000  MOVE      ZED6,MPRAC
          DISPLAY   *P30:13,*EL,*V2LON,FINISH
          GOTO      EMPR9500
.
EMPR8900  CALL      PUTSCR00                 * restore screen
EMPR9000  MATCH     SPRAC,MPRAC              * start number > end number ?
          GOTO      EMPR9200 IF NOT LESS     * no
.
          DISPLAY   *P30:13,*EL,*V2LON,MPRAC,*HOFF:
                    *P1:24,*EL,*B,"Start practice greater than end practice.  ";
          CALL      HITENTER
          GOTO      EMPR0000
.
EMPR9200  DISPLAY   *P30:13,*EL,*V2LON,MPRAC:
                    *P40:13,*HOFF,PRPRDESC
.
EMPR9500  MOVE      MPRAC,EPRAC
          MOVE      ZERO,EXIT
          GOTO      EMPR9999
.
EMPR9800  CALL      FRESCR00
EMPR9900  MOVE      ONE,EXIT
.
EMPR9999  RETURN
+
.****************************************************************************
.         Keyin user id
.****************************************************************************
USID0000  KEYIN     *P1:17,*EL,"User Id : ",USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      USID0000 IF EQUAL
USID9999  RETURN
+
.****************************************************************************
.*                              PROC0000               Called by: ML0000    *
.*                  process medical practice records                        *
.****************************************************************************
.
PROC0000  MOVE      ZERO,CPAGENO
          MOVE      ZERO,CNOUNDLN
          MOVE      ZERO,CLNO
          CALL      IBACLOCK
.
          CALL      HEAD0000
          MATCH     SP6,SPRAC                    * process from "start"
          GOTO      PROC0500 IF EQUAL            * yes
.
.         Starting from actual M.P. code, so read record
.
          MOVE      SPRAC,KEY6
          CALL      RDPRPR1                      * read record
          BRANCH    OVRCD,PROC9000               * not on file
          GOTO      PROC2000                     * print record
.
PROC0500  MOVE      SP6,KEY6
          CALL      RSPRPR1                      * position at start of file
.
PROC1000  CALL      RKPRPR1                      * read next record
          BRANCH    OVRCD,PROC9000               * end of file
.
          MATCH     PRPRCODE,EPRAC               * record code < end number ?
          GOTO      PROC9000 IF LESS             * no - finish
.
          PACK      KEY10,USERID,SP70
          CALL      RDPRALS1
          COMPARE   ZERO,OVRCD
          GOTO      PROC1200 IF EQUAL       * User has access to all Med.Prac.
.
          PACK      KEY16,PRPRCODE,USERID,SP70
          CALL      RDPRSEC1
          BRANCH    OVRCD,PROC1000          * no access
.
.         Valid record found, so print it
.
PROC1200  COMPARE   FIFTY7,CLNO                  * page full ?
          GOTO      PROC2000 IF LESS             * no
.
          CALL      HEAD0000                     * new page header
.
PROC2000  CALL      DISP0000
          CALL      LINE0000                     * display line after record
          GOTO      PROC1000                     * get next record
.
PROC9000  PRINT     *N,*1,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*V2LON,"** Report Generated **  ";
.
PROC9999  RETURN
+
.****************************************************************************
.*                            DISP0000                 Called by: PROC0000  *
.*                valid record so display it                                *
.****************************************************************************
.
DISP0000  DISPLAY   *P50:24,*EL,PRPRDESC;
.
          PRINT     *1,PIPE,*4,PRPRCODE,*12,PRPRDESC,*44,PIPE;
.
.         Format the primary doctor's name
.
          MOVE      SP30,PFNAME
          PACK      KEY10,PRPRPDOC,SP70
          CALL      RDPMHCP1                     * pmshcpaf record found
          BRANCH    OVRCD,DISP1000
.
          MOVE      PMHCTITL,PACTITLE
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PFNAME
.
DISP1000  UNPACK    PFNAME,KEY26
          PRINT     *47,PRPRPDOC,*59,KEY26,*87,PIPE;
          ADD       ONE,CLNO
.
.         Loop through HCP file for this practice, ignoring the 
.         primary doctor already listed
.
          MOVE      ONE,AFLAG 
          PACK      KEY22,PRPRCODE,SP20
          CALL      RSPRDO1                      * position in file
DISP1500  CALL      RKPRDO1                      * read next record
          BRANCH    OVRCD,DISP9000               * end of file
.
          MATCH     PRPRCODE,PRDOPRAC            * same practice ?
          GOTO      DISP9000 IF NOT EQUAL        * no
.
          MATCH     PRDODOCT,PRPRPDOC            * primary doctor ?
          GOTO      DISP2500 IF EQUAL            * yes - ignore
.
.         Valid associated doctor found so format their name
.
          MOVE      SP30,AFNAME
          PACK      KEY10,PRDODOCT,SP70
          CALL      RDPMHCP1                     * pmshcpaf record found 
          BRANCH    OVRCD,DISP2000               * not on file
.
          MOVE      PMHCTITL,PACTITLE
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,AFNAME
.
DISP2000  BRANCH    AFLAG,DISP2300               * same line as primary doc.
          COMPARE   FIFTY7,CLNO                  * page full ?
          GOTO      DISP2200 IF LESS             * no
.
          CALL      HEAD0000                     * new page header
          PRINT     *1,PIPE,PRPRCODE,*12,PRPRDESC,*44,PIPE,*47,"(contd.)":
                    *87,PIPE;
          GOTO      DISP2300
.
DISP2200  PRINT     *1,PIPE,*44,PIPE,*87,PIPE;
DISP2300  PRINT     *90,PRDODOCT,*102,AFNAME,*132,PIPE
.
          BRANCH    AFLAG,DISP2400
          ADD       ONE,CLNO
.
.         First record for this doctor printed, so jump to the next
.         doctor
.
DISP2400  MOVE      ZERO,AFLAG
DISP2500  PACK      KEY22,PRPRCODE,PRDODOCT,ZED18
          CALL      RSPRDO1                      * position in file
          GOTO      DISP1500                     * Get next doctor
.
DISP9000  COMPARE   ZERO,AFLAG                   * any associated doctors ?
          GOTO      DISP9999 IF EQUAL            * yes
          PRINT     *132,PIPE                    * no - so finish line
.
DISP9999  RETURN
+
.****************************************************************************
.*                            HEAD0000                 Called by: PROC0000  *
.*                       print page heading                       DISP0000  *
.****************************************************************************
.
HEAD0000  CALL      HEAD132                      * display page header
.
          PRINT     *40,"Medical Practice Code From : ";
.
          MATCH     SP6,SPRAC                    * any start code ?
          GOTO      HEAD5000 IF EQUAL            * no 
.
          PRINT     *69,SPRAC
          GOTO      HEAD6000
.
HEAD5000  PRINT     *69,"Start"
.
HEAD6000  PRINT     *62,"To   : ";
.
          MATCH     ZED6,EPRAC                   * end code input ?
          GOTO      HEAD7000 IF EQUAL            * no
.
          PRINT     *69,EPRAC,*N
          GOTO      HEAD7500
.
HEAD7000  PRINT     *69,"Finish",*N
.
HEAD7500  CALL      LINE0000
          PRINT     *1,PIPE,*4,"MEDICAL PRACTICE",*44,PIPE,*47,"PRIMARY DOCTOR":
                    *87,PIPE,*90,"ASSOCIATED DOCTORS",*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
          MOVE      NINE,CLNO                     * increment line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by: PROC0000  *
.*                      draw line across page                     HEAD0000  *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
          ADD       ONE,CLNO
.
LINE9999  RETURN
+
. =========================================================================
.   I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PRIPRADS
.
          INC       PRIPRAIO/INC                 * M.P. file
          INC       PRIDOCIO/INC                 * M.P. doctor file
          INC       PMSHCPIO/INC                 * HCP file
          INC       PRIALSIO/INC                 * Medical Prac.all user access
          INC       PRISECIO/INC                 * Medical Prac.security access

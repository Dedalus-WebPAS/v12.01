.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAPMS88                                             *
.*                                                                            *
.*     FUNCTION:         CASEMIX MANAGEMENT REPORT                            *
.*                                                                            *
.*     DATE WRITTEN:     03/03/2007                                           *
.*                                                                            *
.*     AUTHOR:           Saroeun Soeur                                        *
.*                                                                            *
.*     MODIFICATIONS:    Copied from IBAPMS82                                 *
.*                                                                            *
.*----------------------------------------------------------------------------* 
.*        V12.00.01 28/05/2025 Bella Turco     TSK 0955096                    *
.*                  Alphanumeric changes                                      *
.*        V11.05.01 14/02/2025 J.Tan           TSK 0946490                    *
.*                  Recompiled for PATCATBI - Changes for MV CWO Payment Model*
.*        V11.04.07 23/09/2024  J.Tan          TSK 0949848                    *
.*                  Recompiled for ABFPTINV - DRG List used in for COVID Adj. *
.*        V11.04.06 11/09/2024  J.Tan          TSK 0947315                    *
.*                  Recompiled for ABFPTINV - fixed Total Unqualified         *
.*        V11.04.05 01/08/2024  J.Tan          TSK 0947804                    *
.*                  Recompiled for ABFMHINV - Changes for Disc.after 1/7/2023 *
.*        V11.04.04 17/07/2024  J.Tan          TSK 0947315                    *
.*                  Recompiled for ABFPTINV (LOS ICU adjusted & Newborn)      *
.*        V11.04.03 15/03/2024  J.Tan          TSK 0910994                    *
.*                  Recompiled for CMXMATRX                                   *
.*        V11.04.02 29/01/2024  Jacob Jackson  TSK 0919335                    *
.*                  Add HF History includes                                   *
.*        V11.04.01 29/01/2024  Jacob Jackson  TSK 0919335                    *
.*                  Add new local variable and recompile for GETTFEES         *
.*        V11.03.03 17/11/2023  J.Tan          TSK 0940089                    *
.*                  Recompiled for ABFPTINV - checking for blank Disc.DRG     *
.*        V11.03.02 13/11/2023  J.Tan          TSK 0939019                    *
.*                  Recompiled for ABFPTINV - HAC ICD10 Edition 12            *
.*        V11.03.01 15/06/2023  J.Tan          TSK 0923703                    *
.*                  Recompiled for ABF new calculations                       *
.*        V11.01.07 17/11/2021  J.Tan          TSK 0913621                    *
.*                  Recompiled for ABFPTINV - Mod for Multiple NEP Values     *
.*        V11.01.06 29/09/2021  J.Tan          TSK 0901515                    *
.*                  Recompiled for CMXMATRX - Added Primary Proc.to Matrix    *
.*        V11.01.05 14/09/2021  J.Tan          TSK 0906222                    *
.*                  Recompiled for ABFPTINV - Mod for Multiple NEP Values     *
.*        V11.01.04 07/07/2021  J.Tan          TSK 0908588                    *
.*                  Recompiled for PATCATBI-Exclude Unqualified days for C/P  *
.*        V11.01.03 28/04/2021  Thanh T        TSK 0895165                    *
.*                  Recompiled for OPRARDFD changes                           *
.*                  07/04/2021  Tracey Nguyen   TSK 0901515                   *
.*                  Recompiled for PMSCMTFD - Added new fields                *
.*                  a) 1 File type for ICD procedure (PMCCIPF)                *
.*                  b) 3 new CMBS Field Type (PMCCFTS4/PMCCFT5C/PMCCFT6C)     *
.*                  c) 3 new CMBS Codes From (PMCCPC4F,PMCCSC5F,PMCCTC6F)     *
.*                  d) 3 new CMBS Codes To   (PMCCPC4T/PMCCSC5T,PMCCTC6T)     *
.*                  27/04/2021  J.Tan           TSK 0863287                   *
.*                  Mod for Patient Invoice new functionality                 *
.*        V11.01.02 10/03/2021  Tracey Nguyen   TSK 0901515                   *
.*                  Recompiled for PMSCMTFD - Added Primary ICD Procedure 1-5 *
.*                  (PMCCICP1-5)                                              *
.*        V11.01.01 22/02/2021  J.Tan          TSK 0888639                    *
.*                  Changed patmmbs counter record to DIM3                    *
.*----------------------------------------------------------------------------* 
.*        V11.00.06 10/11/2020  J.Tan           TSK 0899562                   *
.*                  Recompiled for CMXMATRX - setup date for reading MBS item *
.*        V11.00.05 29/09/2020  J.Tan          TSK 0896829                    *
.*                  Recompiled for CMXMATRX - use National(PTPGNDRG)DRG       *
.*        V11.00.04 22/09/2020  J.Tan           TSK 0895330                   *
.*                  Recompiled for CMXMATRX - reposition read of Matrix table *
.*        V11.00.03 14/07/2020  J.Tan          TSK 0892902                    *
.*                  Recompiled for PATCATBI - Mod to initialise CASMXKEY      *
.*        V11.00.02 01/07/2020  J.Tan           TSK 0892765                   *
.*                  Recompiled for PATCATBI-Low outlier exc. unqualified days *
.*        V11.00.01 11/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*        V10.14.01 09/09/2019  J.Tan           TSK 0857392                   *
.*                  Recompiled for ABF invoice                                *
.*        V10.12.01 18/07/2018  J.Tan          TSK 0859911                    *
.*                  Recompiled for PATCATBI - Fixed No Payday Add on charge   *
.*        V10.11.01 07/09/2017  J.Tan         TSK 0837479                     *
.*                  Recompiled for CMXMATRX - OPCNCONS(Consumption type items)*
.*        V10.10.02 13/06/2017  J.Tan          TSK 0839991                    *
.*                  Recompiled for PATDBTYP (PATCMSTP)                        *
.*        V10.10.01 25/01/2017  J.Tan          TSK 0832407                    *
.*                  Recompiled for PATCATBI- multi bed trans on sameday for MV*
.*                  04/04/2017  J.Tan         TSK 0297904                     *
.*                  Recompiled for PATCMSTP - Non-Accumulative inlier ICU/CCU *
.*        V10.08.08 06/10/2016  J.Tan         TSK 0826824                     *
.*                  Recompiled for CALCMVHR - MV Hours                        *
.*        V10.08.07 21/09/2016  J.Tan         TSK 0826370                     *
.*                  Recompiled for CMXMATRX - Chgd MCHGARR MCHGAMT array to F3*
.*        V10.08.06 26/08/2016  Don Nguyen    TSK 0312570                     *
.*                  Recompiled for CMXMATRX - Added check for PTCNDGED=1      *
.*                  Added PMSEDGFD/IO and GETEFDRG                            *
.*        V10.08.05 17/08/2016  J.Tan         TSK 0808745                     *
.*                  Recompiled for PATCATBI - for NPDAYFLG=1                  *
.*        V10.08.04 20/07/2016  J.Tan         TSK 0819914                     *
.*                  Recompiled for PATCATBI - Non ICU MV Hours                *
.*        V10.08.03 15/07/2016  J.Tan         TSK 0819914                     *
.*                  Recompiled for PATCATBI - Calculating non ICU MV hours    *
.*        V10.08.02 20/06/2016  J.Tan         TSK 0820816                     *
.*                  Recompiled for PATCATBI - Fixed No payday for Daycase     *
.*        V10.08.01 02/06/2016  J.Tan         TSK 0816651                     *
.*                  Recompiled for PATCATBI-Continuous Bill.to chk for Matrix *
.*        V10.07.02 04/03/2016  J.Tan         CAR 290448                      *
.*                  Recompiled for PATCATBI - Mods for Addon with No Pay DAy  *
.*        V10.07.01 18/11/2015  J.Tan         CAR 303942                      *
.*                  Added new payment types                                   *
.*        V10.06.01 15/07/2015  J.Tan          CAR 319342                     *
.*                  Recompiled for CMXMATRX - Chg 'Disc.UDF' to 'Care Class'  *
.*        V10.04.05  03/07/2014 J.Tan           CAR 268860                    *
.*                   Recompiled for PATCATBI - ICD10 Suggested Classification *
.*        V10.04.04 19/06/2014 J.Tan           CAR 302452                     *
.*                  Recompiled for PATDINVS - Changed MCHGARR/MCHGAMT         *
.*        V10.04.03 16/06/2014 Steve Armstrong CAR 301639                     *
.*                  Moved call to TFILENAM for FNAMEI into INIT0000           *
.*        V10.04.02 11/06/2014 Sandra Barcham  CAR 301639                     *
.*                  Moved call to TFILENAM to INIT0000                        *
.*        V10.04.01 27/05/2014  J.Tan          CAR 300784                     *
.*                  Recompiled for STEPDOWN - force stepdown prior inv.fromdat*
.******************************************************************************
.*        V10.03.11 13/12/2013  J.Tan          CAR 294609                     *
.*                  Recompiled for CMXMATRX - mods for Default Grouper Version*
.*        V10.03.10 07/11/2013  J.Tan          CAR 283618                     *
.*                  Recompiled for PATCATBI - mods for Ind.Services Billing   *
.*        V10.03.09 02/10/2013  J.Tan          CAR 291588                     *
.*                  Recompiled for PATCATBI - fixed stepdown return from leave*
.*        V10.03.08 14/08/2013  J.Tan          CAR 288416                     *
.*                  Recompiled for PATCATBI - fixed Days of Hosps.            *
.*        V10.03.07 05/08/2013  J.Tan          CAR 288060                     *
.*                  Recompiled for CMXMATRX - fixed Primary/Secondary/Tertiary*
.*        V10.03.06 06/05/2013  J.Tan          CAR 284902                     *
.*                  Recompiled for STEPDOWN - force stepdown prior Inv.Fromdat*
.*        V10.03.05 10/04/2013  J.Tan    CAR 283743                           *
.*                  Recompiled for PATCATBI -  reset PTTRESPD for New Stepdown*
.*        V10.03.04 05/03/2013  Patrick Adair CAR 281898                      *
.*                  Recompiled for PATCATBI                                   *
.*        V10.03.03 02/01/2013 Patrick Adair                       CAR 261630 *
.*                  Removed port number from temp file name                   *
.*        V10.03.02 21/09/2012  J.Tan          CAR 271474                     *
.*                  Recompiled for CMXMATRX - using index 5 of pmscmtaf       *
.*                  04/12/2012  J.Tan          CAR 262673                     *
.*                  Recompiled for PATCATBI - Casepayment perdiem rate        *
.*        V10.03.01 05/09/2012  J.Tan          CAR 267784                     *
.*                  Mods checking for TCINDC19 Claim Code                     *
.*        V10.01.03 24/02/2011  Peter Vela    CAR 233034                      *
.*                  Removed PATBF2FD and PATBF2IO                             *
.*        V10.01.02 21/12/2010  Mike Laming   CAR 233046                      *
.*                  Recompiled for PABXBILL - Mods to Misc.Charges PATMCHFD   *
.*                  28/01/2011 J.Tan          CAR 233050                      *
.*                  Mods to Casemix funding                                   *
.*        V10.00.07 17/08/2010 J.Tan         CAR 227873                       *
.*                  Recompiled for CMXMATRX- getting the grouper version      *
.*        V10.00.06 11/08/2010 J.Tan         CAR 227873                       *
.*                  Recompiled for CMXMATRX-searching for next matrix rules   *
.*        V10.00.05 03/08/2010  J.Tan         CAR 227319                      *
.*                  Recompiled for PATCATBI - Mechanical Ventilation          *
.*        V10.00.04 14/07/2010  J.Tan         CAR 226158                      *
.*                  Recompiled for STEPDOWN - bed fees update                 *
.*        V10.00.03 16/07/2010 J.Tan         CAR 226322                       *
.*                  Recompiled for CMXMATRX - search for next rule if Contract*
.*                  is not valid                                              *
.*        V10.00.02 12/07/2010  J.Tan         CAR 225932                      *
.*                  Recompiled for PATCATBI - set TDATE to IDATET for per-diem*
.*        V10.00.01 07/06/2010  Mike Laming   CAR 212864                      *
.*                  Recompiled for PATCATBI - mods at GCDE6500                *
.*        V9.12.07  11/03/2010  J.Tan         CAR 216044                      *
.*                  Mods to include Leave days in stepdown count for TAC      *
.*        V9.12.06  02/02/2010  Davin         CAR 215234                      *
.*                  Recompiled for CMXMATRX                                   *
.*        V9.12.05  27/01/2010  J.Tan         CAR 214548                      *
.*                  Recompiled for PATCATBI - Stepdown after bed fees update  *
.*                  27/01/2010  J.Tan         CAR 214877                      *
.*                  Recompiled for CMXMATRX - Checking for range of MBS items *
.*        V9.12.04  19/01/2010  J.Tan         CAR 166439                      *
.*                  Recompiled for PATCMSTP - C/P additional charge stepdown  *
.*        V9.12.03  24/11/2009  Davin         CAR 210721                      *
.*                  Recompiled for CHKCMXPT - I*C on patcmxaf file            *
.*        V9.12.02  09/10/2009  J.Tan         CAR 203934                      *
.*                  Recompiled for PATCATBI - display ICD10 Sugg.Class on Inv.*
.*        V9.12.01  21/09/2009  J.Tan         CAR 204390                      *
.*                  Recompiled for STEPDOWN - mods to old bed fees file       *
.*     V9.11.01     Saroeun Soeur CAR 198148                                  *
.******************************************************************************
.
          INC       STD001FD
.
          INC       COMDEPFD/INC
          INC       PATDHEAD/INC
          INC       IBACONFD/INC
          INC       IBATMHFD/INC
          INC       IBAPASFD/INC
          INC       IBAGSTFD/INC
          INC       PATCHXFD/INC
          INC       PMSCIDFD/INC
          INC       PMSCCDFD/INC
          INC       PATHSPFD/INC
          INC       PATHTRFD/INC
          INC       PATRFNFD/INC
          INC       PATFN2FD/INC
          INC       PMSMPRFD/INC
          INC       PATDINVS/INC            * Variables For Printing Invoices
          INC       PATINVFD/INC            * Patient Invoice Details File 
          INC       PATOVBFD/INC            * Overnight Revenue Breakdown File
          INC       PATSRBFD/INC            * Sameday Revenue Breakdown File
          INC       PATRATFD/INC            * Patient Rates Table 
          INC       PATFINFD/INC            * Patient Financial Summary Table
          INC       PATDRGFD/INC            * Patient Period Date Range Table
          INC       PATMCHFD/INC            * Patient Misc Charges Table
          INC       PMSSINFD/INC            * Patient Sameday Invoice Breakdown 
          INC       PMSHATFD/INC            * Hospital Attribute file
          INC       PATIOVFD/INC            * Invoice Breakdown for Overnight
          INC       PATIBLFD/INC            * Lumpsum Invoice Breakdown Table
          INC       PATVISFD/INC            * Patient Visit Details
          INC       PMSVX1FD/INC            * Patient Visit Details
          INC       PATVENFD/INC            * Ventilation Details
          INC       PATCMTFD/INC            * Casemix Code Matrix header Table 
          INC       PMSCMTFD/INC            * Casemix Code Matrix Table 
          INC       OUTPREFD/INC            * O/P Preattendance Booking Table
          INC       PATFMOFD/INC            * Health Fund Moeity Table
          INC       PATRBLFD/INC            * Lumpsum Revenue Breakdown Table
          INC       PATTFEFD/INC            * Patient Theatre Fees Table 
          INC       PATDGWFD/INC            * DRG Desc and Weighting Details
          INC       PATCTFFD/INC            * DRG/Casemix Theatre Fees
          INC       PATICUFD/INC            * ICU Table
          INC       IBAGEDFD/INC            * GST Rates
          INC       PATINMFD/INC            * Patient Invoice Messages
          INC       PATPGRFD/INC            * Patient grouper Details 
          INC       PATECDFD/INC            * Epsiode Disease Coding  
          INC       PATECOFD/INC            * Epsiode Procedure Coding  
          INC       PATFIGFD/INC
          INC       PATRFGFD/INC
          INC       OPRARDFD/INC
          INC       OPRDEAFD/INC
          INC       OPRSESFD/INC
          INC       MLTCFNFD/INC
          INC       PMSPBRFD/INC
          INC       PMSIDEFD/INC
.
          INC       PATELGFD/INC
          INC       PATASDFD/INC            * Additional Charge for Sameday
          INC       PATHDFFD/INC            * Daily Fee for Inlier/High Outlier 
          INC       PATHLFFD/INC            * Lumpsum for Overnight
          INC       PATLSDFD/INC            * Lumpsum for Sameday
          INC       PATLDFFD/INC            * Daily Fee for Low Outliers
          INC       PATAFEFD/INC            * Additional fee for Overnight
          INC       PATFN1FD/INC            * Health Fund Table 
          INC       PATFX1FD/INC            * Health Fund Table 
          INC       PATBFEFD/INC            * Bed Fee Table
          INC       PATCFAFD/INC
          INC       PATCMCFD/INC            * Casemix Codes Table
          INC       PATCODFD/INC            * Patient Codes Table 
          INC       PATCONFD/INC            * Control File
          INC       PATDO1FD/INC            * Doctor File 
          INC       PATDSCFD/INC            * Discharge File
          INC       PATDTRFD/INC            * DTR File 
          INC       PATMA1FD/INC            * Patient Master File
          INC       PATMI1FD/INC            * Patient Admission File
          INC       PATMMBFD/INC            * Medical Records MBS Details
          INC       PATRCAUD/INC            * Patient Billing Rate Change 
          INC       PATTRNFD/INC            * Patient Transfer File
          INC       PATWR1FD/INC            * Ward File
          INC       PATCMXFD/INC            * Casemix Funding Table
          INC       PATITMFD/INC            * Item table
          INC       PMSSGAFD/INC
          INC       PMSMTIFD/INC
          INC       PMSCNOFD/INC
          INC       PABXVARS/INC            * Variables for PABX Billing
          INC       NZPBFEFD/INC
          INC       NZPEXTFD/INC
          INC       NZPFIGFD/INC
          INC       NZPIBRFD/INC
          INC       NZPIVBFD/INC
          INC       NZPRBRFD/INC
          INC       NZPRDTFD/INC
          INC       NZPRVBFD/INC
          INC       NZPSPRFD/INC
          INC       NZPFINFD/INC
          INC       NZPPFEFD/INC
          INC       NZPCMTFD/INC
          INC       NZPCFNFD/INC
          INC       NZPPICFD/INC
          INC       NZPRFNFD/INC
          INC       NZPMCHFD/INC
          INC       NZPMXCFD/INC
          INC       NZPTFEFD/INC
          INC       RCPBNKFD/INC
          INC       RCPDTEFD/INC
          INC       RCPBDTFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
          INC       PMSEDGFD/INC       * Effective DRG Dates
          INC       PATDDHFD/INC
+
.
. ******  VARIABLES DEFINITIONS  ******
. Variables declared for the purpose of compilation           
.
PMCCITEM  DIM       15
CPTDATE   DIM       8
CURSESS   DIM       19 
CURRDATE  DIM       8 
DAYOVR    FORM      8
MBSFLAG   FORM      1
THCFLG    FORM      2
DDAYTARG  DIM       4
DIM1A     DIM       1
DIM4      DIM       4
DIM5A     DIM       5
DIM9      DIM       9
DIM9A     DIM       9
DIM9B     DIM       9
DIM9C     DIM       9
DIM9D     DIM       9
DIM9E     DIM       9
DIM18     DIM       18
DIM14     DIM       14
DIM18A    DIM       18
DIM24A    DIM       24
EFTDATE   DIM       8
MTHNAM3   DIM       3
MTHNAM3A  DIM       3
HTMLFILE  FIFO      ASCII, FIXED=250
PATINVTL  FORM      12.2                *invoice amount as is total
DAYFORMT  DIM       4
NJAN      INIT      "January"
NFEB      INIT      "February"
NMAR      INIT      "March"
NAPR      INIT      "April"
NMAY      INIT      "May"
NJUN      INIT      "June"
NJUL      INIT      "July"
NAUG      INIT      "August"
NSEP      INIT      "September"
NOCT      INIT      "October"
NNOV      INIT      "November"
NDEC      INIT      "December"
.
ADNAME    DIM       10                     * Doctor Name              *C-60902
AVDC      FORM      6.2
AVPAT     FORM      6.2
ASDATE    DIM       8
.
CASEPYMT  DIM       1                      * Casepayment/Periem Patient?
CATCR     INIT      "CR"
DAYCFLAG  FORM      1                      * day case flag
DIM30     DIM       30
.                                              0 = include day cases
.                                              1 = don't include day cases
OUTLIERD  DIM       4                      * Outlier Day
OPCNCONS  DIM       1
FOUTLIER  FORM      4                      * Form field-Outlier Day
ENDDAY    DIM       4                      * Inlier Day
FENDDAY   FORM      4                      * Form filed-Inlier Day
TGTLOS    FORM      4                      * Target LOS
BEDTYPE   DIM       3
DRGDATE   DIM       8                      * Date DRG last assigned 
ODATE     DIM       8                      * Operation Date
XDATE3    DIM       8
XYEAR3    DIM       2
XCENT3    DIM       2
XMON3     DIM       2
XDAY3     DIM       2
HFUND     DIM       9                      * Health Fund Name    
DRGDESC   DIM       18                     * DRG Description 
PCLASS    DIM       8                      * Patient Classification 
CURLOS    FORM      4                      * Current LOS (including leave days)
HOSPDAY   FORM      4                      * Hospitalisation Days 
LVDAYS    FORM      4                      * Leave Days
XURNUM    DIM       8                      * U/R Number  
DAYRATE   FORM      6.2                    * Current Room Rate
DAYCASE   FORM      1                      * Day Case Flag 
DAYLEFT   FORM      4                      * Days Left  
XCBEND    FORM      4                      * Last Day of Stepdown
XCINIT    INIT      "    "
XBED      DIM       3                      * Bed Number 
XDATE1    DIM       8                      * Yesterday date DDMMYY
XMON2     DIM       2
XDAY2     DIM       2
XWARD     DIM       3
XYEAR2    DIM       2
XCENT2    DIM       2
YTDATE    DIM       8
TFEEIND   FORM      1
.
CALDAYS   FORM      4
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CERTINDC  FORM      1
CMDLINE   DIM       90
CRDATE    DIM       8                                                  *I-60902
.
DCNUM     FORM      6
DCRATE    FORM      6.2
.
FORM2A    FORM      2
FORM3A    FORM      3
FNAMEI    DIM       8
FNAMER    DIM       8
FRSTFOOT  DIM       1
HFHIFUND  FORM      2
JYEAR     FORM      2
.
KEY13A    DIM       13
LSTDATE   DIM       8
MAXLINES  FORM      4
NOBF      FORM      1
NUMDAY    FORM      4
.
PATNUM    FORM      6
.PATRATE   FORM      6.2
PATNAM    DIM       14                     * Patient Name
PATNAME1  DIM       13                     * Patient Name
.
PSTSRT    FILE      ASCII, FIXED=256
PNAM1     DIM       1
PNAM2     DIM       1
PHEADFL   FORM      1
PSKIP     FORM      1
.
DSTDOWN   FORM      3
SOUTLIER  FORM      4
SRBDAY    DIM       4
SRYEAR    FORM      4
SEC       FORM      2
STATUS    FORM      1
SAVDISC   FORM      6.2
SAVALLW   FORM      6.2
SAVBEND   FORM      3
.
TDAYNO    FORM      4
TRDATE    DIM       8
.
UNIQUE    FORM      8
.
WDATE     DIM       8
WDATE1    DIM       8
WDATE2    DIM       8
WSTAT     FORM      1
WARDFROM  DIM       3                      * Starting Ward 
WARDTO    DIM       3                      * Ending Ward
WARDFPRT  DIM       5                      * Print Ward From 
WARDTPRT  DIM       6                      * Print Ward To
WBSEUID   DIM       10
.
SP14      INIT      "              "
SWARD     DIM       3
UFNDAY    FORM      3
FNDDAY    FORM      3
URNUM     DIM       8  
.
CFPAYCOD  DIM       3        * 3M Codefinder PAY Code
.
.
.
. ******  Temporary index file for report  ******
.
TEMP3     IFILE     SQL, FIXED=357, KEY="u1-3,4-6,7-20,21-28"          *C-60902
TEMP4     IFILE     SQL, FIXED=357, KEY="u1-3,108-111,4-6,21-28"       *C-60902
.
.Name     Type      Size    Physical    Start
.----     ----      ----    --------    -----
.XWARD    DIM        3         3          1       * Ward Number
.XBED     DIM        3         3          4       * Bed Number 
.PATNAM   DIM        14        14         7       * Patient Name 
XUNIQUE   DIM        8         8         21       * Unique Number
.PGNAME   DIM        25        25        29       * Patient Given Name  
.PSEX     DIM        1         1         54       * Sex
.PSAGE    FORM       3         3         55       * Age
.ADOCTA   DIM        6         6         58       * Doctor
.AFUNDH   DIM        6         6         64       * Health Fund
.AFNDTB   DIM        8         8         70       * Health Fund Table 
.ADATE    DIM        8         8         78       * Admission Date 
.ODATE    DIM        8         8         86       * Operation Date  
.ACLAIM   DIM        3         3         94       * Claim Number  
.ATYPE    DIM        3         3         97       * Admission Type 
.NUMDAY   FORM       4         3        100       * Number of Days in Hospital 
.DAYRATE  FORM       6.2       5        103       * Current room Rate
SDAYLEFT  DIM        4         4        108       * Days Left 
.DAYCASE  FORM       1         2        112       * Day Case Flag
.NOBF     FORM       1         2        114       
.TDAYNO   FORM       4         3        116        
.STRBTYP  DIM        3         3        119       * Bed Type
XAADMNO   DIM        8         8        122       * Admission Number 
XCOLNUM   FORM       3         3        130       
.PTMIPDRG DIM        9         9        133       * DRG Code 
.PTCMXLOS FORM       4         3        142       * LOS
.PRICMBS  DIM        9         9        145       * CMBS Item
.PTMICMXP DIM        1         1        154       * Case Payment Flag  
SDSTDOWN  DIM        4         4        155       * Days past Inlier 
SFNDDAY   DIM        3         3        159       
SUFNDAY   DIM        3         3        162 
.ADIAG1   DIM        50        50       165                            *I-60902
.SOUTLIER FORM       4         3        215        
.ENDDAY    DIM       4         4        218       * Inlier Days
.OUTLIERD  DIM       4         4        222       * Outlier Days 
LUMPAMNT  FORM       9.2       7        226       * Lumpsum
OVRLLCHR  FORM       9.2       7        233       * Overall charge
.CURLOS    FORM      4         4        240       * Current LOS
.XURNUM     DIM      8         8        244       * U/R Number  
.PATNAME1   DIM      14        14       252       * Patient Name
.ADNAME    DIM       14        14       266       * Doctor Name       *C-60902
.DRGDESC   DIM       30        30       280       * DRG Description 
.DRGDATE   DIM       8         8        310       * DRG Date
.HFUND     DIM       9         9        318       * Health Fund   
.PCLASS    DIM       8         8        327       * Patient Classification  
.LVDAYS    FORM      4         4        335       * Leave Days
.TGTLOS    FORM      4         4        339       * Target LOS
.XCBEND    FORM      4         4        343       * Last Day of Step Down
PDIEMLFT   FORM      4         4        347       * Days to next Rate Change
DAYTARGT   FORM      4         4        351       * Day over Target LOS 
IDAYFLAG   FORM      1         2        355       * Intended Day Case Flag
.End of Record                          357
.
. ******  CONSTANTS  ******
.
CODEA     INIT      "A "
CODEBT    INIT      "BT"
CODECL    INIT      "CL"
CODEPY    INIT      "Py"
OPNFLAG   FORM      "1"
.
PRGID     INIT      "IBAPMS88"
PRGNAM    INIT      "CASEMIX MANAGEMENT REPORT"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.        START OF PROGRAM
.******************************************************************************
ML0000    CALL      INIT0000
.
ML0500    CALL      SEL0000
          BRANCH    EXIT,ML9000
.
          BRANCH    OPTION,ML2000,ML2000            
.
ML2000    CALL      KWARD00
          BRANCH    CEXIT,ML3000,ML2000,ML0500
.
ML3000    CALL      STPROC00
          GOTO      ML0500
.
ML9000    CALL      KILLIDZ
.
ML9999    CHAIN     PGM
.
.******************************************************************************
.            INIT0000 - Display screen header and open files
.******************************************************************************
INIT0000  CALL      DISPHEAD
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          DISPLAY   *P56:24,"Opening patbfeef";
          OPEN      PATBFEE1,"patbfeef"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patmchgf";
          OPEN      PATMCHG1,"patmchgf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patcmcaf";
          OPEN      PATCMCA1,"patcmcaf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"pmsmtiaf";
          OPEN      PMSMTIA1,"pmsmtiaf"
.
          DISPLAY   *P64:24,"patdtraf";
          OPEN      PATDTRA3,"patdtraf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmmbsf";
          OPEN      PATMMBS1,"patmmbsf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"patovbaf";
          OPEN      PATOVBA1,"patovbaf"
.
          DISPLAY   *P64:24,"patsrbaf";
          OPEN      PATSRBA1,"patsrbaf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
.
          DISPLAY   *P64:24,"patcmxaf";
          OPEN      PATCMXA1,"patcmxaf"
.
          DISPLAY   *P64:24,"patchxaf";
          OPEN      PATCHXA1,"patchxaf"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR3,"patinvrf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SEVENTY9;*115,PTCNDGUP,PTCNDGUA,*239,PTCNDGAD:
                                    PTCNDGDS,PTCNDGTR,PTCNDGRG
.
          READ      CONTROLF,TEN;*217,CAUDQ
          READ      CONTROLF,TEN3;*245,CHOSTYP
          READ      CONTROLF,TEN9;*212,CFEETYP
          READ      CONTROLF,TEN5;*249,CRDRTYP,*247,CSTDOWN
          READ      CONTROLF,TWENTY;*164,PTRESDAY:
                                    *193,PTCNDCLM:
                                    *211,PTCNCNDY
          READ      CONTROLF,FIFTY9;*1,CDLSTEP
          READ      CONTROLF,SEVENTY9;*82,PTCNDSCI                    *C-195595
          READ      CONTROLF,EIGHTY8;*233,PTCNADES
.
...  used in PATCATBI CRMV0000
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEX
.
...  used in PATCATBI PTMP0000
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEZ
.
...  used in PATCATBI PT2P0000
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEY
.
...  used in PABXBILL
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,PBXFNAME
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,PBXFNAM2
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEI
.
INIT9999  RETURN 
.******************************************************************************
.                        SEL0000 - Select Option
.******************************************************************************
SEL0000   MOVE      ZERO,PHEADFL 
          DISPLAY   *P1:3,*EF:
                  *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                  *P1:5,*V2LON,ONE,*HOFF," = Ward/Bed Sequence":
                  *P1:6,*V2LON,TWO,*HOFF:
                        " = Days Left to Stepdown/Outlier Sequence":
                  *P1:8,"Select Option :";
.
SEL1000   KEYIN     *P17:8,*EL,*DV,UNDLN1,*P17:8,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          IF        @EQUAL
            MOVE      ONE,EXIT
          ENDIF
.
          IF        OPTION > 2
            DISPLAY   *P35:8,*EL,*B,*V2LON,"** Invalid Option **",*W2;
            GOTO      SEL1000
          ENDIF
.
SEL9999   RETURN
.
.******************************************************************************
.                       KWARD00 -  Keyin a ward
.******************************************************************************
KWARD00   DISPLAY   *P1:10,*EF,"Enter Ward  From :":
                        *P1:11,"            To   : ";
          MOVE      "22",CCOL
          MOVE      "10",CVERT
          CALL      KWRD0000            * Enter ward from
          BRANCH    EXIT,KWARD00
          MATCH     SP3,KEY3
          IF        @EQUAL
            DISPLAY   *P22:10,*V2LON,"Start";
          ENDIF
          MOVE      KEY3,WARDFROM
.
KWARD10   MOVE      "11",CVERT
          CALL      KWRD0000            * Enter ward to
          BRANCH    EXIT,KWARD00
.
          MATCH     SP3,KEY3
          IF        @EQUAL
            DISPLAY   *P22:11,*V2LON,"Finish";
            MOVE      "zzz",KEY3
          ENDIF
          MOVE      KEY3,WARDTO
.
          MATCH     WARDFROM,WARDTO
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,*V2LON:
                      "Ward To must be greater than Ward From. ";
            CALL      HITENTER
            GOTO      KWARD10
          ENDIF
.
          MOVE      ZERO,PSKIP
          IF        OPTION=1
.                                          * Skip to new page on change of ward
.                                          * (Option 1 only)
            CALL      KSPW0000            * Ask if a separate for each ward
          ENDIF
.
          MOVE      ONE,DAYCFLAG            * exclude intended day cases
          COMPARE   TWO,OPTION              * running option 2 ?
          GOTO      KWARD80 IF EQUAL        * yes
.
KWARD50   KEYIN     *P1:13,"Include Day Cases? (",*V2LON,*DV,ANSY,*HOFF:
                    *DV,SLASH,*V2LON,*DV,ANSN,*HOFF,"): ":
                    *P27:13,*V2LON,ANS
.
          PACK      ANS,ANS,SP1
          MATCH     SP1,ANS                * anything entered ?
          GOTO      KWARD50 IF EQUAL       * no - input is mandatory
.
          REP       UPPLOW,ANS
          MATCH     ANS,ANSN               * "No" input ?
          IF        @EQUAL
            DISPLAY   *P27:13,*V2LON,"No "
            GOTO      KWARD80              * yes - continue
          ENDIF
.
          MATCH     ANS,ANSY               * "Yes" input ?
          IF        @EQUAL
            MOVE      ZERO,DAYCFLAG        * include intended day cases
            DISPLAY   *P27:13,*V2LON,"Yes"
            GOTO      KWARD80              * yes - continue
          ENDIF
.
          GOTO      KWARD50                * invalid input
.
KWARD80   CALL      SETYDAY                * Set def Date/call KEYDATE  I-60902
.
          CALL      CONTQST                * Ok to continue ?
.
KWARD99   RETURN
.******************************************************************************
.                       STPROC00 - Process Start
.******************************************************************************
STPROC00  CALL      INDZD
          CLOCK     TIME,CTIMEIS
.
          MOVE      ONE,UNIQUE
          MOVE      SP8,DDATE
          MOVE      ZERO,DAYCASE
          MOVE      ZERO,CPAGENO
          MOVE      " ",ANS
.
          DISPLAY   *P1:24,*EL,*P23:24,*V2LON:
                    "** Processing Current Admissions **",*HOFF:
                    *P1:24,*EL,*P10:24,"Current Admission No :":
                               *P50:24,"Scanning :";
          MOVE      "2",WSTAT
          CALL      ADMFN000
.
          DISPLAY   *P1:24,*EL,*P23:24,*V2LON:
                    "** Processing On-Leave Admissions **",*HOFF:
                    *P1:24,*EL,*P10:24,"On-leave Admission No :":
                               *P50:24,"Scanning :";
          MOVE      "4",WSTAT
          CALL      ADMFN000
          CALL      REPT0000
.
STPROC99  RETURN
+
.******************************************************************************
.                    ADMFN000 - Read Admissions File
.******************************************************************************
ADMFN000  PACK      KEY9,WSTAT,SP8
          CALL      RDSMISS2
ADMFN050  CALL      RDKMISS2
          BRANCH    OVRCD,ADMFN999
.
          COMPARE   WSTAT,ASTAT
          GOTO      ADMFN999 IF NOT EQUAL
.
.         Ignore Daycare Patients
.
          MOVE      ZERO,IDAYFLAG           * not intended day case
          IF        PTCNDSCI=0
            MOVE      "P ",TCODE
            MOVE      ACLSS,ACODE
          ELSE
            PACK      TCODE,ANSU,PTCNDSCI
            LOAD      ACODE,PTCNDSCI,AUSR1,AUSR2,AUSR3,AUSR4,AUSR5
          ENDIF
.          
          MATCH     SP3,ACODE
          GOTO      ADMFN050 IF EQUAL
.
          PACK      KEY5,TCODE,ACODE
          CALL      RDCODE1                 * Read a codes file record
          BRANCH    OVRCD,ADMFN050
.          
          MATCH     "2",TCINDC1             * Intended same day patient?
          IF        @EQUAL
            BRANCH    DAYCFLAG,ADMFN050     * don't include intended day cases
            MOVE      ONE,IDAYFLAG          * intended day case
          ENDIF
.
          DISPLAY   *P62:24,AADMNO;
.
          MOVE      AWARD,XWARD 
          MOVE      ABED,XBED 
.
          UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
          PACK      WDATE,XCENT,XYEAR,XMON,XDAY
          REP       " 0",WDATE
.
          MATCH     WDATE,YTDATE            * admiss. date > report date ?
          GOTO      ADMFN050 IF LESS        * yes - ignore record
.
          MOVE      ZERO,DAYCASE
          CALL      TRANS000                * Process Billing Details
          GOTO      ADMFN050
.
ADMFN999  RETURN
+
.******************************************************************************
.  TRANS000 - Subroutine to process the DTR and Transfer files for the patient
.******************************************************************************
TRANS000  MOVE      "00000000",ODATE
          CALL      IVAR0000                * Initialize the variables
.
.         Routine to get the rate
.
          MOVE      ALACDTE,IDATEF
          MOVE      IDATEF,SIDATET
          MOVE      ZERO,ENDFLAG
          MOVE      SP10,STATYPE
          MOVE      SP10,FROMDATE
          MOVE      ONE,PROGFLAG
          MOVE      SP3,STWARD
          MOVE      SP3,STBED
          MOVE      ZERO,XCBEND             * Last Day of Stepdown
          MOVE      ZERO,XCOLNUM            * Column of current stepdown
          MOVE      ZERO,COLNUM
          MOVE      ZERO,ACDAYCS            * initialise daycase flag
          MOVE      ZERO,PTCMXLOS
.
.         Get DRG description and Target LOS
.
          PACK      DRGDESC,ADIAG1
          PACK      KEY9,PTMIPDRG,SP70
          CALL      RDPTCMC1
          IF        OVRCD=1
            MOVE      ZERO,PTCAELOS         * initialise Target LOS
          ELSE
            MOVE      PTCAELOS,TGTLOS
          ENDIF
.
          MATCH     ANSY,PTMICMXP
          GOTO      TRANS020 IF NOT EQUAL   * Not Casepayment
.
          PACK      KEY9,ACLAIM,AFUNDH
          CALL      GETCNEFF               * Get Contract Effective From
          BRANCH    EXIT,TRANS020
.
          PACK      PTMIPCMX,CMCODE,SP70
          PACK      KEY18,ACLAIM,AFUNDH,CMCODE,SP70
          MOVE      ADATE,CEFFDATE
          LOAD      CEFFDATE,CNTRCEFR,ADATE,DDATE
.
.
.         If Contract Effective From is 'For Discharges From', Discharged date
.         is blank and TCINDC19=D, default Effective date to Current date
.
          IF        CNTRCEFR=2
            PACK      DDATE,DDATE,SP70
            MATCH     SP70,DDATE
            IF        @EQUAL
              PACK      KEY5,CODECL,ACLAIM
              CALL      RDCODE1
              IF        OVRCD=0
                MATCH     "D",TCINDC19
                IF        @EQUAL
                  PACK      CEFFDATE,CCC,CYY,CMM,CDD,SP70
                  REP       " 0",CEFFDATE
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          CALL      VALCMXFN              * get the contract ID (CONTRCID)
          BRANCH    EXIT,TRANS020
.
          MOVE      PTMIPDRG,CMCODE
.
          PACK      KEY14,AFUNDH,AFNDTB,SP70
          CALL      RDFUND1
          IF        OVRCD=1
            MOVE      SP70,PTHFBCAT
          ENDIF
.
          CALL      GOUT0000                * generate start of outliers
.
.         Read transfer file
.
TRANS020  PACK      KEY30 WITH AADMNO,SP20,SP10
          CALL      RDSTRAN2
TRANS100  CALL      RDKTRAN2
          BRANCH    OVRCD OF TRANS260
.
          MATCH     TADMN,AADMNO
          GOTO      TRANS260 IF NOT EQUAL
.
          MOVE      TDATE,XDATE1            * Get Transfer date
          MOVE      TDATE,LSTDATE
          MOVE      TMOVE,STMOVE
.
.         Get yesterday ward/bed
.
          MATCH     TDATE,YTDATE
          GOTO      TRANS180 IF NOT LESS
          GOTO      TRANS190           
.
TRANS180  MOVE      TWARD,STWARD
          MOVE      TBED,STBED
.
TRANS190  MATCH     ANSY,PTMICMXP
          GOTO      TRANS200 IF NOT EQUAL        * Not a case payment
.
          CALL      PATCMSTP                     
          MATCH     ANSA,TMOVE                   * Only for Admission records
          IF        @EQUAL 
            MOVE      PTTRLSPT,LUMPAMNT
            ADD       PTTRLSRB,LUMPAMNT          * Lumpsum amount
          ENDIF
.
          GOTO      TRANS210
.
.         Not a casemix funding patient
.
TRANS200  BRANCH    CHOSTYP,TRANS210     * Public hospitals don't have stepdown
          COMPARE   ONE,CSTDOWN
          IF        @EQUAL
            CALL      STEPDOWN            * Perdiem Patients
            BRANCH    NOFEE,TRANS210
          ENDIF
.
TRANS210  COMPARE   ZERO,ENDFLAG
          GOTO      TRANS220 IF EQUAL
.
          MATCH     LSTDATE,TDATE
          GOTO      TRANS240 IF EQUAL
.
TRANS220  UNPACK    TDATE,XCENT,XYEAR,XMON,XDAY
.
          CMATCH    ANSD,TMOVE
          GOTO      TRANS300 IF EQUAL
          CMATCH    ANSL,TMOVE
          GOTO      TRANS230 IF EQUAL
.
          MATCH     STATYPE,TATYPE
          GOTO      TRANS240 IF EQUAL
          MOVE      TDATE,FROMDATE
          GOTO      TRANS240
.
.         On-leave record
.
TRANS230  MOVE      ZERO,TRATEF
          MOVE      ZERO,TDISC
          MOVE      ZERO,TALLW
          MOVE      ZERO,TRATEH
          MOVE      ZERO,DAYRATE
.
.         Save some variables for use by STEPDOWN
.
TRANS240  MOVE      TATYPE,STATYPE
          MOVE      TRBTYP,STRBTYP
          MOVE      TRBEND,STRBEND
          MOVE      PTTRAEND,SPTTRAEN
          MOVE      PTTREPSD,SPTTREPS
          MOVE      TDATE,SIDATET
.
.         Calculate Current Room Rate
.
          MOVE      TRATEF,DAYRATE
          ADD       TRATEH,DAYRATE
          MATCH     ANSY,PTMICMXP
          IF        @EQUAL
            ADD       PTTRADPT,DAYRATE
            ADD       PTTRADRB,DAYRATE
          ENDIF
.
          SUB       TDISC,DAYRATE
          SUB       TALLW,DAYRATE
.
          BRANCH    DAYCASE,TRANS100
          MATCH     TDATE,YTDATE
          GOTO      TRANS250 IF NOT LESS
          GOTO      TRANS100            
.
TRANS250  MOVE      COLNUM,XCOLNUM        * From the stepdown routine
.
          MATCH     ANSY,PTMICMXP
          IF        @EQUAL                        
            MOVE      PTTRAEND,XCBEND     * casemix patients
            MOVE      ZERO,FORM4
            MOVE      TRBEND,FORM4
            IF        FORM4<PTTRAEND
              MOVE      TRBEND,XCBEND     * use the lowest ending day
            ENDIF
          ELSE
            MOVE      CURBEND,XCBEND
          ENDIF
          GOTO      TRANS100
.
TRANS260  MOVE      YTDATE,TDATE          * Use current date
          MOVE      STATYPE,PCLASS
.
          MATCH     ANSY,PTMICMXP
          GOTO      TRANS270 IF NOT EQUAL
.
            IF        ACDAYCS = 1
              GOTO       TRANS300
            ELSE
              MOVE      YTDATE,XDATE1
              REP       " 0",XDATE1
              MOVE      XDATE1,TDATE
              MOVE      SP1,TMOVE
              LOAD      TMOVE USING ASTAT OF ANSC,ANSC,ANSC,ANSR
              MOVE      STATYPE,TATYPE
              MOVE      STRBTYP,TRBTYP
              MOVE      STRBEND,TRBEND
              MOVE      SPTTRAEN,PTTRAEND
              MOVE      SPTTREPS,PTTREPSD
              CALL      PATCMSTP
              MOVE      ONE,ENDFLAG
              GOTO      TRANS280
            ENDIF
.
TRANS270  BRANCH    CHOSTYP,TRANS300
.
.         Check if a stepdown is required
.
          COMPARE   ONE,CSTDOWN
          GOTO      TRANS300 IF NOT EQUAL
.
          MOVE      YTDATE,XDATE1
          REP       " 0",XDATE1
          MOVE      XDATE1,TDATE
.
          MOVE      SP1,TMOVE
          LOAD      TMOVE USING ASTAT OF ANSC,ANSC,ANSC,ANSR
          MOVE      STATYPE,TATYPE
          MOVE      STRBTYP,TRBTYP
          MOVE      STRBEND,TRBEND
          CALL      STEPDOWN
          MOVE      ONE,ENDFLAG
          BRANCH    NOFEE,TRANS300
.
.         Check if a stepdown was indicated = shown by a new TDATE
.
TRANS280  MATCH     TDATE,XDATE1
          GOTO      TRANS300 IF EQUAL
.
          MATCH     TDATE,LSTDATE
          GOTO      TRANS210 IF LESS
          GOTO      TRANS240
.
TRANS300  BRANCH    DAYCASE,TRANS350
          MATCH     YTDATE,TDATE
          GOTO      TRANS310 IF EQUAL   
          GOTO      TRANS310 IF LESS
          GOTO      TRANS320            
.
TRANS310  MOVE      COLNUM,XCOLNUM        * From the stepdown routine
.
          MATCH     ANSY,PTMICMXP
          IF        @EQUAL                      
            MOVE      PTTRAEND,XCBEND
            MOVE      ZERO,FORM4
            MOVE      TRBEND,FORM4
            IF        FORM4<PTTRAEND
              MOVE      TRBEND,XCBEND     * current end of day 
            ENDIF
          ELSE
            MOVE      CURBEND,XCBEND
          ENDIF
.
TRANS320  MOVE      YTDATE,TODATE         * Use current date
          REP       " 0",TODATE
.
          REP       " 0",FROMDATE
          REP       " 0",TODATE
          CALL      NHOSPDAY              * Calculate no of days since the 
          MOVE      NBRDAYS,NUMDAY        * last change of admission type
.
          MOVE      ADATE,FROMDATE        * Cal. total number of days in hosp.
          REP       " 0",FROMDATE
          CALL      NHOSPDAY
          MOVE      NBRDAYS,TDAYNO
.
          IF        ACDAYCS=0   
            MOVE      NBRDAYS,NUMDAY      * Casemix-no of days since admission
          ENDIF
.
          GOTO      TRANS360
.
TRANS350  MOVE      ONE,NUMDAY
          MOVE      ONE,TDAYNO
.
TRANS360  MOVE      STWARD,XWARD
          MOVE      STBED,XBED
          CALL      CHKWRD00             * check range of ward
          BRANCH    EXIT,TRANS999
.
          CALL      GDAY0000             * Generates stepdown/outlier days
.
.         Updating the temp file
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF NOMAST
.
          MOVE      NOFEE,NOBF
          MOVE      SP20,PATNAM
          MOVE      PSNAME,PATNAM
          PACK      KEY28 WITH XWARD,XBED,PATNAM,UNIQUE
.
          MOVE      CURRDATE,ASDATE               * Current date
          CALL      PATAGED                       * Get the current age
.
          CALL      GTRF0000             * Find MBS Item 
          CALL      GDET0000             * Get details
          CALL      RPDY0000             * Calculate overall per day
.
          MATCH     ANSY,PTMICMXP
          IF        @EQUAL            
            COMPARE   ZERO,PTCAELOS
            IF        !@EQUAL
              CALL      GINL0000         * Calculate days past inlier
            ELSE
              MOVE      "N/A",SDSTDOWN 
            ENDIF 
          ENDIF
.
          CALL      WRTEMP3              * Write to temp file 
          ADD       ONE,UNIQUE
.
TRANS999  RETURN
+
.******************************************************************************
.         GDET000 - Get Details for patient
.******************************************************************************
GDET0000  PACK      KEY8,AADMNO,SP70           * Get Patient U/R Number
          CALL      RDMISS1
          IF        OVRCD=0
            MOVE      AURNO,URNUM
          ELSE
            MOVE      SP70,URNUM  
          ENDIF
.
.         Get the patient initial given name
.
          MOVE      SP1,PNAM1
          MOVE      PGNAME,PNAM1
          MOVE      PNAM1,PATNAME1
          APPEND    DOT,PATNAME1  
          APPEND    PSNAME,PATNAME1
.
.         Get the attending doctor name
.
          MOVE      SP20,ADNAME
          MOVE      SP1,PNAM2
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,GDET0500
.
          MOVE      DGNAME,PNAM2
          MOVE      PNAM2,ADNAME
          APPEND    DOT,ADNAME
          APPEND    DSNAME,ADNAME
.
.         Get last DRG assigned date
.
GDET0500  MOVE      ADATE,DRGDATE
          PACK      KEY24,AADMNO,Z70
          CALL      RSPTCHX1
GDET1000  CALL      RPPTCHX1
          BRANCH    OVRCD,GDET3000
.
          MATCH     AADMNO,PTCHADMN
          GOTO      GDET3000 IF NOT EQUAL
.
          MOVE      PTCHDATE,DRGDATE
.
.         Get the first Operation Date
.
GDET3000  MOVE      SP70,ODATE   
          PACK      KEY11,AADMNO,SP70
          CALL      RDSMMBS1
          CALL      RDKMMBS1
          BRANCH    OVRCD,GDET4000
.
          MATCH     MMADMN,AADMNO
          GOTO      GDET4000 IF NOT EQUAL
          MOVE      MMDATE,ODATE
.
.         Get Health Fund Details
.
GDET4000  MOVE      SP70,HFUND
          MATCH     SP70,AFUNDH
          IF        !@EQUAL
            PACK      HFUND,AFUNDH,SLASH,AFNDTB
          ELSE
            PACK      KEY5,CODECL,ACLAIM
            CALL      RDCODE1
            IF        OVRCD=0       
              MOVE      TDESC,HFUND 
            ENDIF 
          ENDIF
.
GDET9999  RETURN 
+
.******************************************************************************
.         Calculate overall charge per day
.******************************************************************************
RPDY0000  MOVE      ONE,PROGFLAG
          MOVE      CDD,TDAY
          MOVE      CMM,TMON
          MOVE      CYY,TYEAR
          MOVE      CCC,TCENT
          MOVE      ZERO,NUMINVS
          MOVE      ZERO,IPASS
          MOVE      ZERO,TOTACCM
          MOVE      ZERO,TOTTHEA
          CALL      CALCTBI              * calculate amt to be invoiced
          MOVE      PTHLCOFF,FENDDAY     * Cut off inlier day
          MOVE      FENDDAY,ENDDAY
.
          MOVE      TOTACCM,OVRLLCHR
          ADD       TOTTHEA,OVRLLCHR     * overall charge per day
          ADD       LUMPAMNT,OVRLLCHR    * Lumpsum amount 
.
.         Get the Current LOS including leave days
.
          MOVE      SP70,CURLOS
          DAYSEP    ADATE,YTDATE,CURLOS
          DIV       CURLOS,OVRLLCHR          * Overall charge per day
.
          ASSIGN    (CURLOS-HOSPDAY),LVDAYS  * Number of leave days
.
RPDY9999  RETURN
+
.******************************************************************************
NOMAST    DISPLAY   *P1:24,*EL,*B,"** Adm. ",*V2LON,AADMNO,*HOFF:
                                  " has no Master file record **    ";
          CALL      HITENTER
.
          COMPARE   "2",WSTAT
          GOTO      NOMAST1 IF NOT EQUAL
          DISPLAY   *P1:24,*EL,*P10:24,"Current Admission No :";
          GOTO      NOMAST3
.
NOMAST1   COMPARE   "4",WSTAT
          GOTO      NOMAST2 IF NOT EQUAL
          DISPLAY   *P1:24,*EL,*P10:24,"On-leave Admission No :";
          GOTO      NOMAST3
.
NOMAST2   DISPLAY   *P1:24,*EL,*P10:24,"Discharged Admission No :";
.
NOMAST3   DISPLAY   *P50:24,"Scanning :";
          RETURN
+
.******************************************************************************
.             Initialise variables  
.******************************************************************************
IVAR0000  MOVE      ZERO,DAYLEFT
          MOVE      ZERO,TDAYNO
          MOVE      ZERO,DSTDOWN
          MOVE      ZERO,FNDDAY        
          MOVE      ZERO,LUMPAMNT        * Lumpsum
          MOVE      ZERO,PDIEMLFT        * Days to next Rate Change 
          MOVE      ZERO,OVRLLCHR        * Overall Charge
          MOVE      ZERO,TGTLOS          * initialise Target LOS
          MOVE      ZERO,SOUTLIER        * Start of outlier period
          MOVE      ZERO,DAYTARGT        * Days over Target LOS  
          MOVE      ZERO,IDAYFLAG
          MOVE      ZERO,FENDDAY         * Inlier Day
          MOVE      ZERO,FOUTLIER        * Outlier Day 
.
          MOVE      SP10,SUFNDAY
          MOVE      SP10,ENDDAY          * Inlier Day 
          MOVE      SP10,OUTLIERD        * Outlier Day
          MOVE      SP10,SDAYLEFT        
          MOVE      SP10,SFNDDAY
          MOVE      SP10,SDSTDOWN
          MOVE      ZERO,OVRLLCHR
.
IVAR9999  RETURN
+
.******************************************************************************
.             Generate the inlier/funded/unfunded etc...
.******************************************************************************
GDAY0000  MOVE      ADATE,FROMDATE      
          REP       " 0",FROMDATE
          MOVE      YTDATE,TODATE       
          REP       " 0",TODATE
          CALL      NHOSPDAY
          MOVE      NBRDAYS,HOSPDAY     
.
          MOVE      SP70,DDAYTARG
.
          MATCH     ANSY,PTMICMXP
          GOTO      GDAY8000 IF NOT EQUAL    * patcat
.
.         Casepayment
.
          COMPARE   ZERO,PTCAELOS
          GOTO      GDAY2000 IF EQUAL
.
          CALL      GUFN0000         * Calculate unfunded day
          CALL      GFND0000         * Calculate funded days
          CALL      GLEF0000         * Calculate no of days left to Outlier
.
.         Calculate days over target LOS
.
          MOVE      SP70,DDAYTARG                                         
          MOVE      HOSPDAY,DAYTARGT
          SUB       TGTLOS,DAYTARGT
.
          GOTO      GDAY9999
.
GDAY2000  MOVE      "N/A",SUFNDAY       * unfunded day
          MOVE      "-",SFNDDAY         * Funded day
          MOVE      "N/A",SDAYLEFT
          MOVE      "N/A",SDSTDOWN
          GOTO      GDAY9999
.
.         Patient classification - get stepdown days
.
GDAY8000  CALL      GDLT0000         * Calculate no of days before/in stepdown
.
GDAY9999  RETURN
+
.******************************************************************************
.             Generate unfunded day 
.******************************************************************************
GUFN0000  MOVE      ZERO,UFNDAY
          MOVE      "  0",SUFNDAY
.
.         If LOS <= Optimal LOS then unfunded day =0
.
          COMPARE   TDAYNO,PTCAELOS
          GOTO      GUFN9999 IF NOT LESS     * no funded day
.
.         If LOS > Optimal LOS and LOS < start of Outlier
.            Unfunded days = LOS - Optimal LOS
.
          IF        TDAYNO<SOUTLIER
            MOVE      TDAYNO,UFNDAY          * LOS < outlier
            SUB       PTCAELOS,UFNDAY
            MOVE      UFNDAY,SUFNDAY
          ELSE
.
.         If LOS >= start of outliers then
.         Unfunded Days = Outlier Commencement Period - Optimal LOS
.
            ASSIGN    (SOUTLIER-PTCAELOS),UFNDAY
            MOVE      UFNDAY,SUFNDAY
          ENDIF
.
GUFN9999  RETURN
+
.******************************************************************************
.             Generate funded day 
.******************************************************************************
GFND0000  MOVE      ZERO,FNDDAY         * Funded day
.
.         If LOS <= Optimal LOS, then funded days = LOS
.                   
          COMPARE   TDAYNO,PTCAELOS
          GOTO      GFND1000 IF LESS     * no funded day
          MOVE      PTCAELOS,FNDDAY 
          GOTO      GFND9000
.
.         If LOS > Optimal LOS, then funded days = Optimal LOS
.
GFND1000  MOVE      PTCAELOS,FNDDAY
.
GFND9000  MOVE      FNDDAY,SFNDDAY
.
GFND9999  RETURN
+
.******************************************************************************
.             Generate Day left to Outlier for casepayment
.
.         If LOS < Outlier Commencement period
.            then Days to Outlier = Outlier Commencement Period - LOS
.         Else if LOS > Outlier Commencement Period
.            then Days to Outlier = ***
.         Else if LOS < Optimal LOS
.            then Days to Outlier = N/A         
.******************************************************************************
GLEF0000  MOVE      ZERO,DAYLEFT
          MOVE      SP10,SDAYLEFT
.
          IF        TDAYNO < PTCAELOS
            MOVE      "N/A",SDAYLEFT
          ENDIF
.
.         Check of start of Outlier
.
          MOVE      OUTLIERD,FOUTLIER 
          IF        TDAYNO < FOUTLIER
            MOVE      FOUTLIER,DAYLEFT      
            SUB       TDAYNO,DAYLEFT          * Days to Outlier
            MOVE      DAYLEFT,SDAYLEFT
          ELSE
            MOVE      "----",SDAYLEFT         * Days to Outlier
          ENDIF
.
GLEF9999  RETURN
+
.******************************************************************************
.         If LOS > Outlier Commencement period
.           then number of days past inliers
.******************************************************************************
GINL0000  MOVE      ZERO,DSTDOWN
          MOVE      SP10,SDSTDOWN
.
          COMPARE   TDAYNO,SOUTLIER
          GOTO      GINL9999 IF NOT LESS
.
          MOVE      TDAYNO,DSTDOWN
          MOVE      ENDDAY,FENDDAY
          SUB       FENDDAY,DSTDOWN
.
GINL9000  MATCH     ANSY,PTMICMXP
          IF        !@EQUAL
            MOVE      "     ",SDSTDOWN
          ELSE 
            IF        DSTDOWN<0    
              MOVE      "----",SDSTDOWN
            ELSE
              MOVE      DSTDOWN,SDSTDOWN             * Days past inlier
            ENDIF
          ENDIF
.
GINL9999  RETURN
+
.******************************************************************************
.             Generate day left in stepdown for patcat
.******************************************************************************
GDLT0000  MATCH     SP9,PTMIPDRG
          IF        !@EQUAL
            CALL      GFND0000
          ENDIF
.
          MOVE      ZERO,DAYLEFT
          MOVE      SP10,PDIEMLFT
          MOVE      ZERO,DSTDOWN      * days in current stepdown
          MOVE      SP10,SDSTDOWN     * days in current stepdown
          MOVE      "N/A",SUFNDAY
.
          BRANCH    DAYCASE,GDLT9999
.
          COMPARE   NUMDAY,XCBEND
          GOTO      GDLT9999 IF LESS
.
          MOVE      XCBEND,DAYLEFT
          SUB       NUMDAY,DAYLEFT   * no of days left before next stepdown
          MOVE      DAYLEFT,PDIEMLFT
.
GDLT9999  RETURN
+
.******************************************************************************
.             PRINT REPORT
.******************************************************************************
REPT0000  DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Generating Report",*HOFF:
                     *V2LON," **",*HOFF,*P45:24,"Patient  :";
.
          CALL      IBACLOCK
.
          MOVE      "65",MAXLINES           * Maximum lines on a Page
          MOVE      SP1,FRSTFOOT
          MOVE      ZERO,DCNUM
          MOVE      ZERO,DCRATE
          MOVE      ZERO,PATNUM
          MOVE      ZERO,PATRATE
          MOVE      SP3,SAVWARD
          MOVE      SP3,SWARD
          MOVE      "99",CLNO
          MOVE      ONE,CNOUNDLN
.
          IF        OPTION=1
            PACK      KEY28,SP20,SP20
            CALL      RDSTEMP3
          ELSE
            PACK      KEY18,SP20
            CALL      RDSTEMP4
          ENDIF
.
.         Read the temp file
.
REPT1000  IF        OPTION=1
            CALL      RDKTEMP3
          ELSE
            CALL      RDKTEMP4
          ENDIF
.
          BRANCH    OVRCD,REPT8000
          DISPLAY   *P57:24,*V2LON,PATNAM;
.
          MOVE      CLNO,FORM4
.
          MATCH     SAVWARD,XWARD
          IF        @EQUAL
            ADD       SIX,FORM4             * room for next patient
          ELSE
            ADD       EIGHT,FORM4           * room for Ward Header & Patient
            IF        PSKIP = 1 & OPTION = 1
              MOVE      "99",FORM4          * Force a new page anyway
            ENDIF
          ENDIF
.                      
.                                           * Check if paging is required
          COMPARE   MAXLINES,FORM4
          GOTO      REPT1500 IF LESS        * goto check if Ward header required
.
.                                           * Force the paging
          CALL      PRNTFOOT                * Print the footers
          CALL      HEAD0000                * Print the headers 
       
REPT1500  * Print the Ward header
            PACK      KEY6,XWARD,SP3
            CALL      RDWARD1 
            IF        OVRCD = 0
              MOVE      WEFRBT,BEDTYPE
              PRINT     *1,"|",WWARD;
            ELSE
              PRINT     *1,"|";
            ENDIF
.
.         Now set up and print Patient
.
REPT2000  UNPACK    ADATE WITH XCENT,XYEAR,XMON,XDAY
          UNPACK    DRGDATE,XCENT3,XYEAR3,XMON3,XDAY3
          UNPACK    ODATE,XCENT1,XYEAR1,XMON1,XDAY1
.
          PRINT     *6,"|",XBED:                       * Bed No
                    *10,"|",XURNUM:                     * U/R No
.                   *14,"|",AADMNO:                    * Admission No
                    *19,"|",PATNAME1:                  * Patient Name
.                   *38,"|",PSEX,*42,"|",PSAGE:        * Patient Sex/Age
                    *33,"|",XDAY,SLASH,XMON,SLASH,XYEAR; * admission date
.
.         If we are including intended day cases and this admission
.         is an intended day case, then print the Current LOS as "1"
.
          IF        DAYCFLAG = 0 & IDAYFLAG = 1
            PRINT     *43,"|   ",ONE;                  * Current LOS
          ELSE
            PRINT     *43,"|",CURLOS;                  * Current LOS
          ENDIF
.          
          PRINT     *48,"|",ADNAME;                    * Clinician Name
          PRINT     *60,"|",HFUND;
.
          MATCH     ANSY,PTMICMXP
          IF        @EQUAL
            MOVE      ZERO,XCBEND
            MOVE      "C",CASEPYMT                     * Casepayment Patient
          ELSE
            MOVE      SP10,ENDDAY
            MOVE      SP10,OUTLIERD
            MOVE      "P",CASEPYMT                     * Perdiem Patient
          ENDIF
.
          PRINT     *74,"|",DRGDESC:                   * Admission/Prov DRG
                    *93,"|",CASEPYMT:
                    *97,"|":
                    *103,TGTLOS;

.       Print --- if Days over Target LOS is negative
.       and blank if stepdown

          MATCH    ANSY,PTMICMXP
          IF       !@EQUAL
            MOVE      "     ",DDAYTARG
          ELSE
            IF        DAYTARGT<0
              MOVE      "----",DDAYTARG
            ELSE
              MOVE      DAYTARGT,DDAYTARG
            ENDIF
          ENDIF
.
          PRINT     *107,"|",DDAYTARG:
                    *116,"|";
.
.         call subroutine to calculate outstanding amount
.
          CALL      INVC0000
.
.         check if patient invoice is zero, else print total outstanding
.
          COMPARE   ZERO,PATINVTL
          IF        @EQUAL
            PRINT     *127,"00.00";
          ELSE
            PRINT     *117,PATINVTL;
          ENDIF
.
          PRINT     *132,"|" 
.
.         See if days in outlier stepdown is numeric
.
          TYPE      SDSTDOWN
          IF        @EQUAL 
            MOVE      SDSTDOWN,FORM4
            IF        FORM4 > 0
              MATCH     ANSY,PTMICMXP
              IF        @EQUAL
                IF        XCOLNUM > 1
                  PACK      DIM20,SDSTDOWN
                ELSE
                  PACK      DIM20,ANSL,XCOLNUM,SLASH,SDSTDOWN
                ENDIF
              ELSE
                IF        XCOLNUM = 1
                  MOVE      SP20,DIM20
                ELSE
                  PACK      DIM20,ANSL,XCOLNUM,SLASH,SDSTDOWN
                ENDIF
              ENDIF
              SQUEEZE   DIM20
              MOVE      DIM20,DIM8
            ELSE
              MOVE      "   0",DIM8
            ENDIF
          ELSE 
            MATCH     SP70,SDSTDOWN
            IF        @EQUAL
             MOVE      "   0",DIM8     
            ENDIF
          ENDIF

.       Print --- if Days over Target LOS is negative
.       and blank if stepdown
.
.          MATCH    ANSY,PTMICMXP
.          IF       !@EQUAL
.            MOVE      "     ",DDAYTARG
.          ELSE 
.            IF        DAYTARGT<0
.              MOVE      "----",DDAYTARG
.            ELSE
.              MOVE      DAYTARGT,DDAYTARG
.            ENDIF
.          ENDIF 
.
.          PRINT     *132,"|"
.          PRINT     *1,"|",*5,"|--------------------------":
.                    *32,"------------------------------------------------":
.                    *80,"----------------------------------------------------|"
.
.          PACK      DIM4,PCLASS
.          PRINT     *1,"|":
.                    *5,"|",CASEPYMT:                   * Casepayment Y/N
.                    *9,"|",DIM4  :                     * Patient Class
.                    *14,"|",STRBTYP;                   * Bed Category
.
.         If we are including intended day cases and this admission
.         is an intended day case, then print the Current LOS as "1"
.
.          IF        DAYCFLAG = 0 & IDAYFLAG = 1
.            PRINT     *18,"|   ",ONE;                  * Current LOS
.          ELSE
.            PRINT     *18,"|",CURLOS;                  * Current LOS
.          ENDIF
.
.          PRINT     *26,"|",TGTLOS:                    * Target LOS
.                    *31,"|",LVDAYS:                    * Leave Days
.                    *37,"|",ENDDAY:                    * Inlier Days
.                    *42,"|",OUTLIERD:                  * Outlier Days
.                    *47,"|",SDSTDOWN:                  * Day Past Inlier
.                    *56,"|",SDAYLEFT:                  * Days to Outlier
.                    *64,"|",DDAYTARG:                  * Day over Target LOS
.                    *72,"|",XCBEND:                    * S/D Table
.                    *78,"|",DAYRATE:                   * Current Room Rate
.                    *88,"|",PDIEMLFT:                  * Days to Rate Change
.                    *97,"|",LUMPAMNT:                  * C/P Amount
.                    *110,"|",OVRLLCHR:                 * Overall $ per day
.                    *123,"|",XDAY3,SLASH,XMON3,SLASH,XYEAR;               
.                                                       * Last Date DRG 
.
          CALL      PRNTUNDR
          ADD       SIX,CLNO
.
          GOTO      REPT1000
.
.         End of report     
.
REPT8000  CALL      PRNTFOOT
          ADD       "1",CLNO
          COMPARE   CLNO,MAXLINES
          GOTO      REPT8500 IF LESS
          CALL      HEAD0000
.
REPT8500  MOVE      ZERO,AVPAT
          MOVE      ZERO,AVDC
.
          COMPARE   ZERO,PATNUM
          GOTO      REPT9000 IF EQUAL
.
          MOVE      PATRATE,AVPAT
          DIV       PATNUM,AVPAT
.
REPT9000  COMPARE   ZERO,DCNUM
          GOTO      REPT9500 IF EQUAL
.
          MOVE      DCRATE,AVDC
          DIV       DCNUM,AVDC
.
REPT9500  PRINT     *N,*N,"***  End of Report  ***"
          DISPLAY   *P1:24,*EL,*P25:24,*V2LON:
                    "** Report Generation Completed **";
          CALL      KILLIDZ
          CALL      ML0000
.
REPT9999  RETURN 
.
.******************************************************************************
.                    HEAD0000 - Print Heading 
.******************************************************************************
HEAD0000  IF        OPTION=1
            MOVE      "(Ward/Bed Sequence)",CPHDROPT
          ELSE
            MOVE      "(Days Left Sequence)",CPHDROPT
          ENDIF
.
          CALL      HEAD132
.
          MATCH     YTDATE,CRDATE                                      *I-60902
          IF        @EQUAL
            MOVE      CTIMEIS,DIM5                                     *I-60902
.
            PRINT     *N,*40,"From Ward : ",WARDFPRT," To ",WARDTPRT:  *I-60902
                      *N,*40,"Patients as at ",DIM5," ":               *I-60902
                         XDAY2,SLASH,XMON2,SLASH,XCENT2,XYEAR2,*N      *I-60902
          ELSE                                                         *I-60902
            PRINT     *N,*40,"From Ward : ",WARDFPRT," To ",WARDTPRT:
                      *N,*40,"Patients at Midnight ":
                         XDAY2,SLASH,XMON2,SLASH,XCENT2,XYEAR2,*N
          ENDIF                                                                
.
          CALL      PRNTUNDR
          CALL      REPH0000 
.
.          PRINT    *1,"|Bed":
.                   *5,"|U/R":
.                  *14,"|Adm":
.                  *23,"|Patient":
.                   *38,"|Sex":
.                   *42,"|Age":
.                   *46,"|Clinician":
.                   *58,"|Adm":
.                   *68,"|Description":
.                   *100,"|Health":
.                   *112,"|Adm":
.                   *122,"|Opr":
.                   *132,"|"
.
.          PRINT    *1,"|No":
.                   *5,"|":
.                   *14,"|No":
.                   *23,"|Name":
.                   *38,"|":
.                   *42,"|":
.                   *46,"|":
.                   *58,"|CMBS/DRG":
.                   *68,"|":
.                   *100,"|Fund":
.                   *112,"|Date":
.                  *122,"|Date":
.                   *132,"|"
.
.          PRINT     *1,"|",*5,"|--------------------------":
.                    *32,"------------------------------------------------":
.                    *80,"----------------------------------------------------|"
.
.          PRINT    *1,"|":
.                   *5,"|PD/":
.                   *9,"|Adm":
.                   *14,"|Bed":
.                   *18,"|Current":
.                   *26,"|TLOS":
.                   *31,"|Leave":
.                   *37,"|I":
.                   *42,"|O":
.                   *47,"|Day Past":
.                   *56,"|Days to":
.                   *64,"|Day ovr":
.                   *72,"|S/D":
.                   *78,"|Crnt Room":
.                   *88,"|Days to":
.                   *97,"|C/P":
.                   *110,"|Overall $":
.                   *123,"|Last Date":
.                   *132,"|"
.
.          PRINT    *1,"|":
.                   *5,"|CD":
.                   *9,"|Type":
.                   *14,"|Cat":
.                   *18,"|LOS":
.                   *26,"|":
.                   *31,"|Days":
.                   *37,"|Days":
.                   *42,"|Days":
.                 *47,"|Inlier":
.                  *56,"|Outlier":
.                  *64,"|Tgt LOS":
.                  *72,"|Table":
.                  *78,"|Rate":
.                  *88,"|Rate Chg":
.                  *97,"|Amount":
.                  *110,"|per pat day":
.                  *123,"|DRG":
.                  *132,"|"
.
.          CALL      PRNTUNDR
.
          MOVE      TEN4,CLNO
.
HEAD9999  RETURN
.
.******************************************************************************
.------------------------------------------------------------------------------
.         REPH0000 - report header
.------------------------------------------------------------------------------
.
REPH0000  PRINT     *1,"|Ward":
                    *6,"|Bed":
                    *10,"|U/R":
                    *19,"|Patient":
                    *33,"|Adm. Date":
                    *43,"|LOS":
                    *48,"|Att. Doc";
.
          PRINT     *60,"|Health Fund";
.
          PRINT     *74,"|Adm. Diagnosis":
                    *93,"|C/P":
                    *97,"|Expt. LOS":
                    *107,"|Daysover":
                    *116,"|Invoice Amount":
                    *132,"|"
          CALL      PRNTUNDR
.
REPH9999  RETURN
+
.------------------------------------------------------------------------------
PRNTUNDR  PRINT     *1,"*-------------------------------":
                    *33,"-----------------------------------------------":
                    *80,"----------------------------------------------------*"
          RETURN
.
.******************************************************************************
PRNTFOOT  MATCH     SP1,FRSTFOOT
          GOTO      PRNTFT99 IF EQUAL
.
          PRINT     *1,"C/P=Case Payment":
                    *20,"LOS=Length of stay"
.                   *73,"Days to Outlier=Outlier Day-LOS";
          IF        DAYCFLAG = 0
            PRINT     *106,"D/C LOS=1"
          ELSE
            PRINT     *N;
          ENDIF
.
PRNTFT99  MOVE      "99",CLNO
          MOVE      "99",FORM4
          MOVE      ANSY,FRSTFOOT
          RETURN
.
.******************************************************************************
.                  SETYDAY - Set up yesterday date
.******************************************************************************
SETYDAY   MOVE      CDD,DD
          MOVE      CMM,MM
          MOVE      CYY,YY
          MOVE      CCC,CC
          PACK      CRDATE,CCC,CYY,CMM,CDD                             *I-60902
          REP       " 0",CRDATE                                        *I-60902
          CALL      DMYCON
.
          SUB       ONE,JULDAY
.
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      YTDATE,CC,YY,MM,DD
          REP       " 0",YTDATE
.
.         Ask user for another date
.
SETY8000  UNPACK    YTDATE,CCENT,CYEAR,CMON,CDAY     * set KEYDATE vars I-60902
          DISPLAY   *P01:15,"Date 'as at'     :";                      *I-60902
          MOVE      "22",CCOL1                                         *I-60902
          MOVE      "15",CVERT                                         *I-60902
          CALL      KEYDATE                                            *I-60902
          PACK      YTDATE,CCENT,CYEAR,CMON,CDAY                       *I-60902
          REP       " 0",YTDATE                                        *I-60902
.
          MATCH     YTDATE,CRDATE                                      *I-60902
            IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Cannot Enter a future Date ";     *I-60902
            CALL      HITENTER                                         *I-60902
            GOTO      SETY8000                                         *I-60902
          ENDIF                                                        *I-60902
.
SETY9999  UNPACK    YTDATE,XCENT2,XYEAR2,XMON2,XDAY2
          RETURN
+
.******************************************************************************
.         Create an indexed file based on port
.         for accumulating Admission Type totals
.******************************************************************************
.
INDZD     MOVE      ZERO,STATUS
.
.         Check if previously exists...if so delete it
.
          CALL      KILLIDZ
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    " 357 u1-3,108-111,4-6,21-28",CMDLINE
          APPEND    " u1-3,4-6,7-20,21-28",CMDLINE           *C-60902
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          MOVE      ONE,STATUS
.
.         Open Temp Index file
.
          MOVE      ZERO,OPCND
          TRAP      NOINDZ IF IO
          OPEN	    TEMP3,FNAMEI
          OPEN	    TEMP4,FNAMEI
          TRAPCLR   IO
.
ENDC      RETURN
+
.******************************************************************************
NOINDZ    MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B,"** Temporary Index File ",*V2LON,FNAMEI:
                               *HOFF," not found **   ";
          CALL      HITENTER
          TRAPCLR   IO
          RETURN
+
.******************************************************************************
.                        KWRD0000 - Keyin a ward
.******************************************************************************
KWRD0000  MOVE      SP10,KEY3
          KEYIN     *PCCOL:CVERT,*EL,*DV,UNDLN3,*PCCOL:CVERT,*V2LON,KEY3;
.
          ENDSET    KEY3
          APPEND    SP3,KEY3
          RESET     KEY3
.
          DISPLAY   *PCCOL:CVERT,*V2LON,KEY3;
.
          CMATCH    "?",KEY3                     * Wards scan ?
          GOTO      KWRD8000 IF EQUAL            * yes
.
          MATCH     SP3,KEY3
          GOTO      KWRD8200 IF EQUAL
.
          PACK      KEY6,KEY3,SP3
          CALL      RDWARD1
          BRANCH    OVRCD,KWRD2000
.
          COMPARE   ZERO,WACTIVE                 * is Ward actvie ?
          GOTO      KWRD4000 IF EQUAL            * yes
.
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Ward is INACTIVE **    ";
          GOTO      KWRD3000
.
KWRD2000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ward does not exist on file **    ";
.
KWRD3000  CALL      HITENTER
          GOTO      KWRD0000
.
KWRD4000  DISPLAY   *P30:CVERT,WBDESC;
          MOVE      ZERO,EXIT
          GOTO      KWRD9999
.
.         Wards scan
.
KWRD8000  MOVE      CCOL,FORM2
          MOVE      CVERT,FORM2A
          CALL      GETSCR00
          CALL      PATWRDDS
          MOVE      FORM2,CCOL
          MOVE      FORM2A,CVERT
          CALL      PUTSCR00
          GOTO      KWRD0000
.
KWRD8200  MOVE      ZERO,EXIT
          GOTO      KWRD9999
.
KWRD9000  MOVE      ONE,EXIT
.
KWRD9999  RETURN
+
.******************************************************************************
.          KSPW0000 - Print Ward on a separate page ?
.******************************************************************************
KSPW0000  MOVE      ANSN,ANS
          KEYIN     *P1:20,*EL,"Would you like each Ward ":
                             "on a separate Page (":
                           *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                           *V2LON,*DV,ANSN,*HOFF,") ? ":
                    *P53:20,*RV,*DV,ANS:
                    *P53:20,*V2LON,ANS;
.
          REP       "yYnN",ANS
          CMATCH    ANSN,ANS
          IF        @EQUAL
            DISPLAY   *P53:20,*V2LON,"No "
            GOTO      KSPW9999
          ENDIF
.
          CMATCH    ANSY,ANS
          GOTO      KSPW1000 IF NOT EQUAL
          DISPLAY   *P53:20,*V2LON,"Yes"
          MOVE      ONE,PSKIP
          GOTO      KSPW9999
.
KSPW1000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Must be Y or N **    ";
          CALL      HITENTER
          GOTO      KSPW0000
.
KSPW9999  RETURN
+
.******************************************************************************
.            CHKWRD00 - Checking range of ward
.******************************************************************************
CHKWRD00  MOVE      ZERO,EXIT
          MATCH     SP3,WARDFROM
          IF        !@EQUAL
            MOVE      WARDFROM,WARDFPRT
            MATCH     WARDFROM,XWARD
            GOTO      CHKWRD90 IF LESS
          ELSE
            MOVE      "Start ",WARDFPRT
          ENDIF
.
          MATCH     "zzz",WARDTO
          IF        @EQUAL
            MOVE      "Finish",WARDTPRT
            GOTO      CHKWRD99
          ELSE
            MOVE      WARDTO,WARDTPRT
            MATCH     XWARD,WARDTO
            GOTO      CHKWRD99 IF NOT LESS
          ENDIF
.
CHKWRD90  MOVE      ONE,EXIT
.
CHKWRD99  RETURN
+
.******************************************************************************
.         Deleting an indexed file based on port
.******************************************************************************
KILLIDZ   CLOSE     TEMP3
          CLOSE     TEMP4
          MOVE      ZERO,STATUS
.
          DISPLAY   *P1:24,*EL,*P28:24,*V2LON,"** Deleting Index File **";
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          RETURN
+
.******************************************************************************
.           I/O routines for the Temp Index file
.******************************************************************************
WRTEMP3   RESET     KEY28
          MOVE      AADMNO,XAADMNO
          MOVE      URNUM,XURNUM
          MOVE      UNIQUE,XUNIQUE
          WRITE     TEMP3,KEY28;XWARD,XBED,PATNAM,XUNIQUE,PGNAME,PSEX,PSAGE:
                                   ADOCTA,AFUNDH,AFNDTB,ADATE,ODATE,ACLAIM:
                                   ATYPE,NUMDAY,DAYRATE,SDAYLEFT,DAYCASE,NOBF:
                                   TDAYNO,STRBTYP,XAADMNO,XCOLNUM,PTMIPDRG:
                                   PTCMXLOS,PRICMBS,PTMICMXP,SDSTDOWN:
                                   SFNDDAY,SUFNDAY,ADIAG1,SOUTLIER:
                                   ENDDAY,OUTLIERD,LUMPAMNT,OVRLLCHR,CURLOS:
                                   XURNUM,PATNAME1,ADNAME,DRGDESC,DRGDATE,HFUND:
                                  PCLASS,LVDAYS,TGTLOS,XCBEND,PDIEMLFT,DAYTARGT:
                                  IDAYFLAG
          RETURN
.
RDKTEMP3  MOVE      ZERO,OVRCD
          READKS    TEMP3;XWARD,XBED,PATNAM,XUNIQUE,PGNAME,PSEX,PSAGE:
                             ADOCTA,AFUNDH,AFNDTB,ADATE,ODATE,ACLAIM:
                             ATYPE,NUMDAY,DAYRATE,SDAYLEFT,DAYCASE,NOBF:
                             TDAYNO,STRBTYP,XAADMNO,XCOLNUM,PTMIPDRG:
                             PTCMXLOS,PRICMBS,PTMICMXP,SDSTDOWN:
                             SFNDDAY,SUFNDAY,ADIAG1,SOUTLIER:
                             ENDDAY,OUTLIERD,LUMPAMNT,OVRLLCHR,CURLOS,XURNUM:
                             PATNAME1,ADNAME,DRGDESC,DRGDATE,HFUND:
                             PCLASS,LVDAYS,TGTLOS,XCBEND,PDIEMLFT,DAYTARGT:
                             IDAYFLAG
          GOTO      OVERCOND IF OVER
          MOVE      XAADMNO,AADMNO
          RETURN
.
RDSTEMP3  MOVE      ZERO,OVRCD
          RESET     KEY28
          READ      TEMP3,KEY28;;
          GOTO      OVERCOND IF OVER
          RETURN
.
RDKTEMP4  MOVE      ZERO,OVRCD
          READKS    TEMP4;XWARD,XBED,PATNAM,XUNIQUE,PGNAME,PSEX,PSAGE:
                             ADOCTA,AFUNDH,AFNDTB,ADATE,ODATE,ACLAIM:
                             ATYPE,NUMDAY,DAYRATE,SDAYLEFT,DAYCASE,NOBF:
                             TDAYNO,STRBTYP,XAADMNO,XCOLNUM,PTMIPDRG:
                             PTCMXLOS,PRICMBS,PTMICMXP,SDSTDOWN:
                             SFNDDAY,SUFNDAY,ADIAG1,SOUTLIER:
                             ENDDAY,OUTLIERD,LUMPAMNT,OVRLLCHR,CURLOS,XURNUM:
                             PATNAME1,ADNAME,DRGDESC,DRGDATE,HFUND:
                             PCLASS,LVDAYS,TGTLOS,XCBEND,PDIEMLFT,DAYTARGT:
                             IDAYFLAG
          GOTO      OVERCOND IF OVER
          MOVE      XAADMNO,AADMNO
          RETURN
.
RDSTEMP4  MOVE      ZERO,OVRCD
          RESET     KEY18
          READ      TEMP4,KEY18;;
          GOTO      OVERCOND IF OVER
          RETURN
+
.******************************************************************************
.*         GTRF0000 - loop thru patdtraf to find the Primary, Secondary &     *
.*                    Tertiary MBS item.  Primary will be the mbs with the    *
.*                    highest scheduled fee                                   *
.******************************************************************************
GTRF0000  MOVE      SP10,PRICMBS            * initialise primary MBS code
          MOVE      SP10,SECCMBS            * initialise secondary MBS code
          MOVE      SP10,TERCMBS            * initialise tertiary MBS code
          MOVE      ZERO,FORM12A
          MOVE      ZERO,FORM12B
          MOVE      ZERO,FORM12C
          OPEN      PATDTRA1,"patdtraf"
          PACK      KEY22,AADMNO,SP20,SP20
          CALL      RDSDTRN1
GTRF1000  CALL      RDKDTRN1
          BRANCH    OVRCD,GTRF9999
.
          MATCH     AADMNO,TADMNO           * compare admission number
          GOTO      GTRF9999 IF NOT EQUAL
.
          COMPARE   TWO,TRECTYPE
          GOTO      GTRF1000 IF NOT EQUAL   * not a theatre item
.
          OPEN      PATITEM1,"patitemn"
          PACK      KEY9,TITEMNO
          PACK      KEY8,DTFCENT,DTFYEAR,DTFMONTH,DTFDAY
          REP       " 0",KEY8
          PACK      KEY17,KEY9,KEY8,SP70
          CALL      PATITMRD
          CLOSE     PATITEM1
          BRANCH    OVRCD,GTRF1000          * not a valid item code
.
          COMPARE   QIAMT,FORM12A
          IF        !@LESS
            MOVE      FORM12A,FORM12B
            MOVE      FORM12B,FORM12C
            MOVE      QIAMT,FORM12A
            MOVE      PRICMBS,SECCMBS
            MOVE      SECCMBS,TERCMBS
            MOVE      TITEMNO,PRICMBS
            GOTO      GTRF1000
          ENDIF
.
          COMPARE   QIAMT,FORM12B
          IF        !@LESS
            MOVE      FORM12B,FORM12C
            MOVE      QIAMT,FORM12B
            MOVE      SECCMBS,TERCMBS
            MOVE      TITEMNO,SECCMBS
            GOTO      GTRF1000
          ENDIF
.
          COMPARE   QIAMT,FORM12C
          IF        !@LESS
            MOVE      QIAMT,FORM12C
            MOVE      TITEMNO,TERCMBS
          ENDIF
.
          GOTO      GTRF1000
.
GTRF9999  RETURN
+
.*******************************************************************************
.         GOUT0000 - Generate start of outlier
.*******************************************************************************
GOUT0000  MOVE      ZERO,NOFEE
          OPEN      PATHDFA1,"pathdfaf"
          PACK      SDIM27,CONTRCID,ACLAIM,AFUNDH,PTHFBCAT,CMCODE
.
          MOVE      ZERO,FORM1
          MOVE      ZERO,FORM2
.
GOUT2000  PACK      KEY31,SDIM27,SP10
          CALL      RSPTHDFA1
GOUT3000  CALL      RKPTHDFA1
          BRANCH    OVRCD,GOUT4000
          PACK      DIM27,PTHDCNID,PTHDCLMN,PTHDHFND,PTHDTABT,PTHDCASM
          MATCH     DIM27,SDIM27
          GOTO      GOUT7000 IF EQUAL
.
GOUT4000  BRANCH    FORM2,GOUT9000
          ADD       ONE,FORM1
          BRANCH    FORM1,GOUT5000,GOUT6000
          MOVE      ONE,NOFEE
          GOTO      GOUT9000
.
GOUT5000  PACK      SDIM27,CONTRCID,ACLAIM,SP6,SP3,CMCODE
          GOTO      GOUT2000
.
GOUT6000  PACK      SDIM27,CONTRCID,PTCNDCLM,SP6,SP3,CMCODE
          GOTO      GOUT2000
.
GOUT7000  MOVE      ONE,FORM2
          COMPARE   ONE,PTHDDTYP          * Outlier ?
          GOTO      GOUT3000 IF NOT EQUAL
.
          MOVE      PTHDEDAY,FOUTLIER 
          MOVE      FOUTLIER,OUTLIERD 
.
GOUT9000  CLOSE     PATHDFA1
GOUT9999  RETURN
.-------------------------------------------------------------------------------
.         INVC0000 - calculate the invoice amount as is
.-------------------------------------------------------------------------------
.
INVC0000  MOVE      ZERO,PATINVTL       * invoice amount as is total
          MOVE      ZERO,FORM12P2
          PACK      KEY16,AADMNO,SP70
          CALL      RDPTINV3
INVC1000  CALL      RKPTINV3
          BRANCH    OVRCD,INVC9999
          MATCH     PINVADM,AADMNO
          GOTO      INVC9999 IF NOT EQUAL
.
.         Invoice should be balanced. Check it just in case (this is because
.         at one stage the centralised cash receipting module did not update
.         the PINVBLCD variable, and so occassionally PINVBLCD said the invoice
.         was balanced, even though it wasn't).
.
          MOVE      PINVAMT,FORM12P2
          ADD       PINVPATA,FORM12P2
          ADD       PINVHFDA,FORM12P2
          ADD       PINVOTHA,FORM12P2
          ADD       PINVJOUR,FORM12P2
          ADD       PTINCRTT,FORM12P2
          IF        IBCNUGST=2 | IBCNUGST=3
            ADD       PTINGSTJ,FORM12P2
          ENDIF
.
          ADD      FORM12P2,PATINVTL
.
          MOVE      ZERO,FORM12P2
          GOTO      INVC1000
.
INVC9999  RETURN
+
.*******************************************************************************
          INCLUDE   STD001IO
          INC       CALCDAYS
          INCLUDE   NHOSPDAY
          INCLUDE   STEPDOWN
          INC       PATCMSTP
.....     INC       PATDBTYP           * calculate bed days for a bed type
          INC       PATAGED
          INC       CHKCMXPT
          INC       PATWRDDS
          INC        PABXBILL
          INC       PATCATBI
.
          INC       PATMCHRD                * Miscellaneous Charges read routine
          INC        PATIVMES
          INC       CMGETTHR
          INCLUDE    PATCALFN
          INCLUDE    PATCOMN3
          INC       SAPARINT
          INC       PATGNCMX
          INC       CLPATDTR
          INC       CMXMATRX
.
. The following a subroutines mentioned in PATCATBI, but will never be
.  needed by this program. They are defined here to allow compilation
.
WEBERR00
WRITETRN
GETTHR
TAIL
INITIVAR
PEOL0000
PRLN0000
PROCMISC
PRTCLMS
NXTTRAN1
PRTINVTL
SINVT000
ISUG0000
CKTL0000
ENDPRT00
PROACC00
PRTIPROV
.
         RETURN
.
          INC       CLNZPPIC    
          INC       GETCNEFF
          INC       GETBFEES
          INC       GETTFEES
          INC       PATITMRD
          INC       VALCMXFN
          INC       TFILENAM                     * Generate Temp File Name
          INC       CLPMSIDE
          INC       COMDEPIO/INC
          INC       IBATMHIO/INC
          INC       IBAGSTIO/INC
          INC       IBAPASIO/INC
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCHXIO/INC
          INC       PMSCIDIO/INC
          INC       PMSCCDIO/INC
          INC       PATINVIO/INC
          INC       PATOVBIO/INC
          INC       PATSRBIO/INC
          INC       PATRATIO/INC
          INC       PATFINIO/INC
          INC       PATFN2IO/INC
          INC       PATDRGIO/INC
          INC       PATMCHIO/INC
          INC       PMSSINIO/INC
          INC       PMSHATIO/INC
          INC       PATIOVIO/INC
          INC       PATIBLIO/INC
          INC       PATVISIO/INC
          INC       PATVENIO/INC            * Ventilation Details
          INC       PMSVX1IO/INC
          INC       PATCMTIO/INC
          INC       PMSCMTIO/INC
          INC       OUTPREIO/INC
          INC       OPRARDIO/INC
          INC       OPRDEAIO/INC
          INC       OPRSESIO/INC
          INC       PATFMOIO/INC
          INC       PATRBLIO/INC
          INC       PATTFEIO/INC
          INC       PATDGWIO/INC
          INC       PATCTFIO/INC
          INC       PATICUIO/INC
          INC       IBAGEDIO/INC
          INC       PATINMIO/INC
          INC       PATPGRIO/INC
          INC       PATECDIO/INC
          INC       PATECOIO/INC
          INC       PATFIGIO/INC
.
          INC       PATELGIO/INC
          INC       PATASDIO/INC
          INC       PATHDFIO/INC
          INC       PATHLFIO/INC
          INC       PATHSPIO/INC
          INC       PATHTRIO/INC
          INC       PATLSDIO/INC
          INC       PATLDFIO/INC
          INC       PATAFEIO/INC
          INC       PATFN1IO/INC
          INC       PATFX1IO/INC
          INC       PATBFEIO/INC
          INC       PATCFAIO/INC
          INC       PATCMCIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATDTRIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATMMBIO/INC
          INC       PATRCAIO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       PATCMXIO/INC
          INC       PATITMIO/INC
          INC       PATRFNIO/INC
          INC       PATRFGIO/INC
          INC       PMSCNOIO/INC
          INC       PMSSGAIO/INC
          INC       PMSMTIIO/INC
          INC       PMSMPRIO/INC
          INC       PMSPBRIO/INC
          INC       PMSIDEIO/INC
          INC       MLTCFNIO/INC
          INC       NZPBFEIO/INC
          INC       NZPEXTIO/INC
          INC       NZPFIGIO/INC
          INC       NZPIBRIO/INC
          INC       NZPIVBIO/INC
          INC       NZPRBRIO/INC
          INC       NZPRDTIO/INC
          INC       NZPRVBIO/INC
          INC       NZPSPRIO/INC
          INC       RCPBNKIO/INC
          INC       RCPDTEIO/INC
          INC       NZPFINIO/INC
          INC       NZPPFEIO/INC
          INC       NZPCFNIO/INC
          INC       NZPPICIO/INC
          INC       NZPRFNIO/INC
          INC       NZPMCHIO/INC
          INC       NZPMXCIO/INC
          INC       NZPCMTIO/INC
          INC       NZPTFEIO/INC
          INC       RCPBDTIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
          INC       PMSEDGIO/INC       * Effective DRG Dates
          INC       PATDDHIO/INC
          INC       GETEFDRG
+

.******************************************************************************
.* System         : Patient Billing                                           *
.* Program        : IBABIL13.PBL                                              *
.* Name           : Small Balance Write Off                                   *
.******************************************************************************
.* Date           : 03/11/2016                                                *
.* Author         : J.Tan                                                     *
.* Function       : Ability to automatically write off small balances         *
.* Modifications  :                                                           *
.******************************************************************************
.* V12.00.03 23/05/2025  Bella Turco   TSK 0925576                            *
.*           Recompiled for UPFAPPLN                                          *
.* V12.00.02 20/05/2025  Bella Turco   TSK 0925576                            *
.*           Recompiled for UPFAPPLN                                          *
.* V12.00.01 19/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.* V11.05.05 08/05/2025  J.Tan         TSK 0925576                            *
.*           Recompiled for UPFAPPLN - Checking for invoice number            *
.* V11.05.04 05/03/2025  J.Tan   TSK 0955356                                  *
.*           Added SKEY40 for PATCALFN                                        *
.* V11.05.03 21/02/2025  J.Tan   TSK 0955356                                  *
.*           Added FININVN,FINADMN,FINURNO                                    *
.* V11.05.02 22/01/2025  J.Tan          TSK 0945920                           *
.*           Added UPFAPPLN - updating FA Completion date                     *
.* V11.05.01 27/11/2024  Thanh T        TSK 0939466                           *
.*           Added Distance in kms field                                      *
.******************************************************************************
.*       V11.01.01  18/02/2021  J.Tan          TSK 0888639                    *
.*                  Added PTDTTMNO - Theatre Team no                          *
.*       V10.14.01  20/03/2019  J.Tan            TSK 0857392                  *
.*                  Changed RCP Transaction count from DIM3 to DIM5           *
.*       V10.13.02  19/09/2018  J.Tan        TSK 0863727                      *
.*                  Added Restrictive Override code                           *
.*       V10.13.01  21/08/2017  J.Tan        TSK 0859742                      *
.*                  Added ECLIPSE new fields from aaedtr                      *
.*       V10.09.01  20/12/2016 J.Tan     TSK 302781                           *
.*                  Changed Min Amount Outstanding to Max amount outstanding  *
.******************************************************************************

          INC       STD001FD
          INC       IBACONFD/INC
          INC       IBASECFD/INC
          INC       IBAPASFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBSECFD/INC
          INC       WEBERRFD/INC                 * Web Server Error Logging
          INC       PATCONFD/INC
          INC       PATMI1FD/INC
          INC       PATCODFD/INC
          INC       PATDETFD/INC
          INC       PATDRGFD/INC
          INC       PATMA1FD/INC
          INC       PATDSCFD/INC
          INC       OUTSITFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       AAEDE1FD/INC
          INC       AAEDEBFD/INC
          INC       AAEDI1FD/INC
          INC       EMRVISFD/INC
          INC       PATHSPFD/INC
          INC       PATINVFD/INC
          INC       PATDTRFD/INC
          INC       PATFACFD/INC
          INC       PATFINFD/INC
          INC       PATFIGFD/INC
          INC       PATRFNFD/INC
          INC       NZPRFNFD/INC
          INC       PATRFGFD/INC
          INC       NZPFINFD/INC
          INC       NZPFIGFD/INC
          INC       PATFN1FD/INC
          INC       PATIJRFD/INC
          INC       PATJRNFD/INC
          INC       OUTDTRFD/INC
          INC       AAEDTRFD/INC
          INC       PATUREFD/INC
          INC       PATVISFD/INC
          INC       PMSCNOFD/INC
          INC       PMSEVTFD/INC
          INC       PMSVX1FD/INC
          INC       RCPDTEFD/INC
          INC       RCPBNKFD/INC
          INC       RCPREGFD/INC
          INC       PATSELDT/INC
.
ASATDAY   FORM      3
ASATYER   FORM      2
ASATCCC   FORM      2
BATCHNO   FORM      8
DAYSINYR  FORM      5
DIM8      DIM       8
DIM10     DIM       10
DIM12     DIM       12
.
INVDAYS   FORM      3
CNYRASAT  DIM       4
CNYRPINV  DIM       4
CURSYS    FORM      1
CURSITE   DIM       6
CODE      DIM       2
CODECL    INIT      "CL"
CMDLINE   DIM       80
FNAMET    DIM       8
FORM12    FORM      12
SAVAMNT   FORM      12.2
SAVAMT    FORM      12
FORM5DAY  FORM      5
F12P2     FORM      12.2
FORM12P2  FORM      12.2
FGSTFLAG  FORM      1
SAVKEY12  DIM       12
SKEY40    DIM       40
.
XMNDBAMT  FORM      12.2
XJRNLDEB  DIM       3               * Journal type (debit)
XMXCRAMT  FORM      12.2
XJRNLCRD  DIM       3               * Journal type (Credit)
HOSPCODE  DIM       3
XBALTYPE  FORM      1
.
TMPPAID   FORM      12.2
USERID    DIM       10
.
CODEJ     INIT      "J "
FIVE9     FORM      "59"
.
.         Temporary File Definitions
.         --------------------------
TMPFILE1  IFILE SQL, FIXED=66, KEY="U17-17,9-16,1-8"
.
.NAME     TYPE    LENGTH     PHYSICAL     START     DESCRIPTION
.----     ----    ------     --------     -----     -----------
.PINVNO   DIM       8           8           1       Invoice Number
.PINVDTE  DIM       8           8           9       Invoice date
XJRNINDC  DIM       1           1          17       Journal Type Indicator
.                                                   A=Debit,B=Credit
XJRNLCOD  DIM       2           2          18       Journal Code
XJRNLDAT  DIM       8           8          20       Journal Date
XJRNAMNT  FORM     12.2         8          28       Journal Amount
.ACLAIM   DIM       3           3          36       Claim code
.AFUNDH   DIM       6           6          39       Health Fund
.AFNDTB   DIM       8           8          45       Health Fund table
.PMVXMHOS DIM       3           3          53       Hospital ID
.WBSEUID  DIM      10          10          56       User ID
.EOR                                       66
.
PRGID     INIT      "IBABIL13"
PRGNAM    INIT      "Small Balance Write Off"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   MAIN0000                                 *
.*                                 Main Module                                *
.******************************************************************************
.
MAIN0000  CALL      INIT0000                * Initialise variables & open files
          CALL      CTMP0000                * Create tempfiles
.
MAIN1000  CALL      OPTN0000                * Choose main option
          BRANCH    EXIT,MAIN9000           * EXIT
.
          CALL      SCRN0000                * display input screen
.
          CALL      GOST0000                * keyin maximum outstanding
          CALL      GJRD0000                * keyin Journal Type (Debit)
          CALL      GCRD0000                * keyin maximum amount in credit
          CALL      GJRC0000                * input Journal Type (Credit)
          IF        IBCNMHOS=1
            CALL      GHSP0000              * keyin a hospital code
            BRANCH    EXIT,MAIN9000         * nothing/exitchar entered
          ENDIF
          CALL      GBAL0000                * All balance greater than
          CALL      KUSR0000                * keyin user id
          CALL      PATSELFN                * Type of Report
          BRANCH    EXIT,MAIN9000
.
          MOVE      "99",CLNO               * force Page header
          CALL      CONTQST                 * Continue Processing ?
          BRANCH    CEXIT,MAIN2000:         * 1 = Yes
                          MAIN1000:         * 2 = No
                          MAIN9000          * 3 = Cancel
          GOTO      MAIN1000
.
MAIN2000  CALL      LOAD0000                * load Invoices to temp file
          BRANCH    EXIT,MAIN1000
.
          CALL      PROC0000                * Process temp file
          GOTO      MAIN1000
.
MAIN9000  CALL      KILL0000                * Kill tempfile
.
MAIN9999  CHAIN     PGM
+
.******************************************************************************
.*                                  INIT0000              Called by: MAIN0000 *
.*                       Initialise Variable & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR1,"patinvrf"
          OPEN      PATINVR4,"patinvrf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patdtraf";
          OPEN      PATDTRA1,"patdtraf"
.
          DISPLAY   *P64:24,"aaedtref";
          OPEN      AAEDTRE1,"aaedtref"
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"aaede1af";
          OPEN      AAEDE1A1,"aaede1af"
.
          DISPLAY   *P64:24,"aaedi1af";
          OPEN      AAEDI1A1,"aaedi1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pmscnoaf";
          OPEN      PMSCNOA1,"pmscnoaf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmscnoaf";
          OPEN      PMSCNOA3,"pmscnoaf"
.
          DISPLAY   *P64:24,"emrvisaf";
          OPEN      EMRVISA1,"emrvisaf"
.
          OPEN      WEBSECA1,"websecaf"
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATFACT1,"patfactf"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATIJRN1,"patijrnf"
          OPEN      PATJRNL1,"patjrnlf"
          OPEN      PATFINS1,"patfinsf"
          OPEN      NZPFINA1,"nzpfinaf"
          OPEN      PMSEVTA1,"pmsevtaf"
          OPEN      PATUREA1,"patureaf"
.
          OPEN      RCPDTEA3,"rcpdteaf"
          OPEN      RCPBNKA1,"rcpbnkaf"
          OPEN      RCPREGA1,"rcpregaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS,*85,IBCNUGST
          READ      CONTROLF,TEN3;*245,CHOSTYP
          READ      CONTROLF,FIVE9;*62,CINVJRN
          READ      CONTROLF,HUND14;*249,PTCNDFUP
.
          CLOCK     TIME,CTIMEIS
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMET
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: MAIN0000 *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          DISPLAY   *P40:4,*EF:
                    *P1:6,*V2LON,"0",*HOFF," = Exit":
                    *P1:7,*V2LON,"1",*HOFF," = Report Only":
                    *P1:8,*V2LON,"2",*HOFF," = Report and Journal":
                    *P1:10,"Select Option : ";
.
OPTN1000  KEYIN     *P17:10,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN2000:             * HF extract
                           OPTN3000              * Report and Journal
          BEEP
          GOTO      OPTN1000
.
OPTN2000  DISPLAY   *P57:2,*V2LON,"- Report Only";
          GOTO      OPTN9000
.
OPTN3000  DISPLAY   *P57:2,*V2LON,"- Report and Journal";
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.***************************************************************************
.*                               GOST0000              Called by: ML0000   *
.*                     keyin Maximum outstanding                           *
.***************************************************************************
GOST0000  KEYIN     *P29:4,*EL,*V2LON,"$",XMNDBAMT:
                    *P29:4,"$",*DV,XMNDBAMT;
GOST9999  RETURN
+
.***************************************************************************
.*                               GJRD0000              Called by: ML0000   *
.*                     keyin Journal Type (Debit)                          *
.***************************************************************************
GJRD0000  DISPLAY   *P29:5,*EL;
          MOVE      FIVE,EVERT
          MOVE      TWENTY9,ECOL
          MOVE      CODEJ,CODE                   * load category
.
GJRD0500  CALL      PATCODKY                     * get start code
          BRANCH    EXIT,GJRD9800:               * nothing entered
                         GJRD9800                * exitchar entered
.
          MOVE      ACODE,XJRNLDEB               * save journal code
          MOVE      ZERO,EXIT
          GOTO      GJRD9999
.
GJRD9800  MOVE      ONE,EXIT
GJRD9999  RETURN
+
.***************************************************************************
.*                               GCRD0000              Called by: ML0000   *
.*                     keyin maximum amount in credit                      *
.***************************************************************************
GCRD0000  KEYIN     *P29:6,*EL,*V2LON,"$",XMXCRAMT:
                    *P29:6,"$",*DV,XMXCRAMT;
GCRD9999  RETURN
+
.***************************************************************************
.*                               GJRC0000              Called by: ML0000   *
.*                     keyin Journal Type (Credit)                         *
.***************************************************************************
GJRC0000  DISPLAY   *P29:7,*EL;
          MOVE      SEVEN,EVERT
          MOVE      TWENTY9,ECOL
          MOVE      CODEJ,CODE                   * load category
.
GJRC0500  CALL      PATCODKY                     * get start code
          BRANCH    EXIT,GJRC9800:               * nothing entered
                         GJRC9800                * exitchar entered
.
          MOVE      ACODE,XJRNLCRD               * save journal code
          MOVE      ZERO,EXIT
          GOTO      GJRC9999
.
GJRC9800  MOVE      ONE,EXIT
GJRC9999  RETURN
+
.*****************************************************************************
.*                                 GHSP0000        Called by: MAIN0000       *
.*               Get the user to keyin a hospital code                       *
.* Returns: EXIT   0 = Valid Hospital selected                               *
.*                 1 = nothing, exitchar or spaces input                     *
.*          HOSPCODE - 3 character hospital code                             *
.*****************************************************************************
GHSP0000  MOVE      EIGHT,EVERT
          MOVE      "29",ECOL
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          MOVE      ZERO,CKYIMODE
.
          CALL      PATHSPKY
          BRANCH    EXIT,GHSP8000:               * nothing input
                         GHSP9100:               * exitchar input
                         GHSP8000                * spaces input
.
          DISPLAY   *P34:8,PTHSNAME;
          GOTO      GHSP9000
.
GHSP8000  DISPLAY   *P34:8,*V2LON,"All"
.
GHSP9000  MOVE      KEY3,HOSPCODE
          MOVE      ZERO,EXIT
          GOTO      GHSP9999
.
GHSP9100  MOVE      ONE,EXIT
GHSP9999  RETURN
+
.***************************************************************************
.*                                GBAL0000             Called by: ML0000   *
.*                     All balance greater than                            *
.***************************************************************************
GBAL0000  DISPLAY   *P1:20,*EL:
                        "1=Less 30 Days, 2=30 Days plus, 3=60 Days plus, ":
                        " 4=90 Days plus";
          MOVE      ZERO,FORM1
          KEYIN     *P29:9,*EL,*V2LON,FORM1
          BRANCH    FORM1,GBAL1000,GBAL2000,GBAL3000,GBAL4000
          GOTO      GBAL9999
.
GBAL1000  DISPLAY   *P29:9,*V2LON,"Less than 30 Days";
          GOTO      GBAL9000
.
GBAL2000  DISPLAY   *P29:9,*V2LON,"30 Days plus";
          GOTO      GBAL9000
.
GBAL3000  DISPLAY   *P29:9,*V2LON,"60 Days plus";
          GOTO      GBAL9000
.
GBAL4000  DISPLAY   *P29:9,*V2LON,"90 Days plus";
          GOTO      GBAL9000
.
GBAL9000  MOVE      FORM1,XBALTYPE
          DISPLAY   *P1:20,*EL;
GBAL9999  RETURN
+
.******************************************************************************
.*                                  KUSR0000                                  *
.*                     Keyin user id                                          *
.******************************************************************************
KUSR0000  KEYIN     *P29:10,*EL,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.***************************************************************************
.*                                LOAD0000             Called by: ML0000   *
.*                     Load tempfile                                       *
.***************************************************************************
LOAD0000  DISPLAY   *P1:24,*EL,*P20:24,"Surname : ":
                               *P50:24,"Admission Number :";
.
          MOVE      USERID,KEY10
          CALL      RDWBSE1
          BRANCH    OVRCD,LOAD9000
.
.         Set Current date as the As at date
.
          MOVE      CCC,CC
          MOVE      CYY,YY
          MOVE      CMM,MM
          MOVE      CDD,DD
          CALL      DMYCON
          MOVE      JULDAY TO ASATDAY
          MOVE      JULYR TO ASATYER
          MOVE      JULCC,ASATCCC
.
          MOVE      FSTSYS,CURSYS
          MOVE      FSTSITE,CURSITE
          PACK      XJRNLDAT,CCC,CYY,CMM,CDD
          REP       " 0",XJRNLDAT             * Set journal date - Current date
.
LOAD0800  PACK      KEY32,CURSYS,CURSITE,SP30
          CALL      RDSINV4
LOAD1000  CALL      RDKINV4
          BRANCH    OVRCD TO LOAD8000
.
.         Ignore cancelled invoices
.
          MATCH     ZEROVISN,PINVADM
          GOTO      LOAD1000 IF EQUAL        * Cancelled Invoice
.
         MATCH       SP70,PINVCLM
         IF          !@EQUAL
           PACK        KEY5,CODECL,PINVCLM
           CALL        RDCODE1
           BRANCH      OVRCD,LOAD1000
         ENDIF
.
.         Display invoice details
.
          DISPLAY   *P31:24,*V2LON,PINALPHA,*P70:24,PINVADM
.
.         Check for a change in system
.
          COMPARE   PINVSYS,CURSYS
          GOTO      LOAD1200 IF EQUAL
.
.         Go to the next system
.
LOAD1100  ADD       ONE,CURSYS
.
.         New system. Check if we are outside the request range
.
          COMPARE   CURSYS,FEDSYS
          GOTO      LOAD8000 IF LESS
.
.         Initialise for reposition
.
          MOVE      FSTSITE,CURSITE
          GOTO      LOAD0800
.
.         Same system, check for a change in site
.
LOAD1200  MATCH     PINVSITE,CURSITE
          GOTO      LOAD1800 IF EQUAL
.
.         Go to the next site code
.
          MOVE      CURSITE,KEY6
          CALL      RDSSITA1
LOAD1600  CALL      RDKSITA1
          BRANCH    OVRCD,LOAD1100
.
.         New site. Check if we are outside the request range
.
          MATCH     OSTSITE,FEDSITE
          GOTO      LOAD1100 IF LESS
.
.         Check for a valid department
.
          MATCH     FSTDEPT,OSTSYST
          GOTO      LOAD1600 IF LESS
.
          MATCH     OSTSYST,FEDDEPT
          GOTO      LOAD1600 IF LESS
.
.         Initialise for reposition
.
          MOVE      OSTSITE,CURSITE
          GOTO      LOAD0800
.
LOAD1800  IF        IBCNUGST=1
            SUB       PTINGSTA,PINVAMT     * Exclude GST Amount
          ENDIF
.
          IF        IBCNMHOS=1
            MATCH     SP10,HOSPCODE
            IF        !@EQUAL
              PACK      KEY8,PINVADM,SP8
              CALL      RDPMVX11
              BRANCH    OVRCD,LOAD1000
              MATCH     PMVXMHOS,HOSPCODE
              GOTO      LOAD1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MATCH     PINVBLCD,XJRNLDAT
          GOTO      LOAD2000 IF LESS
.
          MOVE      PINVAMT,FORM12P2
          ADD       PINVPATA,FORM12P2
          ADD       PINVHFDA,FORM12P2
          ADD       PINVOTHA,FORM12P2
          ADD       PINVJOUR,FORM12P2
          ADD       PTINCRTT,FORM12P2
          IF        IBCNUGST=2 | IBCNUGST=3
            ADD       PTINGSTJ,FORM12P2
          ENDIF
.
          COMPARE   ZERO,FORM12P2        * invoice balanced ?
          GOTO      LOAD1000 IF EQUAL    * yes
.
.         Error msg
.
          DISPLAY   *P1:23,*EL,"Inv.No.",PINVNO:
                    " has an incorrect balanced date";
.         CALL      HITENTER
          GOTO      LOAD1000
.
LOAD2000  CALL      CPAY0000             * Check if invoice is fully paid 
          BRANCH    EXIT,LOAD1000
.
          COMPARE   ZERO,FORM12P2
          GOTO      LOAD1000 IF EQUAL
.
.         Check for Minimum and Maximum amount
.
          IF        FORM12P2<0
            COMPARE   ZERO,XMXCRAMT
            GOTO      LOAD1000 IF EQUAL
            MOVE      FORM12P2,F12P2
            MULT      "-1",F12P2
            COMPARE   F12P2,XMXCRAMT    * check against Max.amt in Credit
            GOTO      LOAD1000 IF LESS
          ELSE
            COMPARE   ZERO,XMNDBAMT
            GOTO      LOAD1000 IF EQUAL
            COMPARE   FORM12P2,XMNDBAMT * check against Max amt outstanding
            GOTO      LOAD1000 IF LESS  * ignore if greater
          ENDIF
.
          CALL      PDET0000             * Get patient details
          BRANCH    EXIT,LOAD1000
.
          CALL      CAGE0000             * Check days since invoice raised
          GOTO      LOAD1000
.
LOAD8000  MOVE      ZERO,EXIT
          GOTO      LOAD9999
.
LOAD9000  MOVE      ONE,EXIT
LOAD9999  RETURN
+
.***************************************************************************
.         Check number of days since the invoice was raised
.***************************************************************************
CAGE0000  UNPACK    PINVDTE INTO XCENT,XYEAR,XMON,XDAY
          MOVE      XCENT,CC
          MOVE      XYEAR TO YY
          MOVE      XMON TO MM
          MOVE      XDAY TO DD
          CALL      DMYCON
.
          PACK      CNYRPINV,CC,YY              * Patient Invoice date
          REP       " 0",CNYRPINV
          MOVE      JULDAY,INVDAYS
.
          PACK      CNYRASAT,ASATCCC,ASATYER,SP4
          REP       " 0",CNYRASAT
          MOVE      ASATDAY,FORM5DAY
.
          MATCH     CNYRASAT,CNYRPINV
          GOTO      CAGE2000 IF EQUAL       * same year, check day and month
          GOTO      CAGE1000 IF LESS
          GOTO      CAGE9999
.
CAGE1000  MOVE      CNYRASAT TO DAYSINYR
          MOVE      CNYRPINV,FORM4
          SUB       FORM4 FROM DAYSINYR     * calculate years difference
          MULT      "365" BY DAYSINYR       * convert to days
          ADD       DAYSINYR,FORM5DAY
.
CAGE2000  SUB       INVDAYS FROM FORM5DAY
.
          COMPARE   ZERO,FORM5DAY
          GOTO      CAGE9999 IF LESS
.
          MOVE      FORM12P2,XJRNAMNT       * the Journal Amount
.
          BRANCH    XBALTYPE,CAGE3000,CAGE4000,CAGE5000,CAGE6000
.
.         less 30 days (include 30 days)
.
CAGE3000  COMPARE   "31",FORM5DAY
          GOTO      CAGE8000 IF LESS
          GOTO      CAGE9999
.
.         30 - 60 days only (include 60 days)
.
CAGE4000  COMPARE   "31",FORM5DAY
          GOTO      CAGE9999 IF LESS
          COMPARE   "61",FORM5DAY
          GOTO      CAGE8000 IF LESS
.
          GOTO      CAGE9999
.
.         60 - 90 Days only (include 90 days)
.
CAGE5000  COMPARE   "61",FORM5DAY
          GOTO      CAGE9999 IF LESS
          COMPARE   "91",FORM5DAY
          GOTO      CAGE8000 IF LESS
.
          GOTO      CAGE9999
.
.         90 Days plus
.
CAGE6000  COMPARE   "91",FORM5DAY
          GOTO      CAGE8000 IF NOT LESS
.
          GOTO      CAGE9999
.
.         Write to tempfile
.
CAGE8000  IF        XJRNAMNT>0
            MOVE      XJRNLDEB,XJRNLCOD            * Journal type (debit)
            MOVE      "A",XJRNINDC
          ELSE
            MOVE      XJRNLCRD,XJRNLCOD            * Journal type (credit)
            MOVE      "B",XJRNINDC
          ENDIF
.
          PACK      XJRNLDAT,CCC,CYY,CMM,CDD
          REP       " 0",XJRNLDAT             * Set journal date - Current date
.
          PACK      KEY17,XJRNINDC,PINVDTE,PINVNO
          CALL      WRTEMP1
.
CAGE9999  RETURN
+
.***************************************************************************
.         Get patient details
.***************************************************************************
PDET0000  MOVE      PINVUR,KEY8
          CALL      RDMAST1              *  Get the master file details
          BRANCH    OVRCD TO PDET9000
.
          BRANCH    PINVSYS OF PDET3000,PDET2000,PDET1000,PDET4000,PDET9000
.
.         Inpatient
.
PDET1000  MOVE      PINVADM TO KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,PDET9000
.
          BRANCH    ASTAT,PDET9000,PDET8000,PDET1200,PDET8000,PDET9000
          GOTO      PDET9000
.
PDET1200  MOVE      AADMNO, KEY8
          CALL      RDDSCH1
.         BRANCH    OVRCD,PDET9000       * missing discharge record
          GOTO      PDET8000
.
.         Outpatient
.
PDET2000  MOVE      PINVADM,OBAOUTNO
          MOVE      OBAOUTNO,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,PDET9000
.
          MOVE      "out",OSTFILE
          MOVE      PVISITE,KEY6
          CALL      RDSITA1
.
          PACK      CFNAME WITH OSTFILE,FILBB1A1
          CALL      OPOTBB11
.
          MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1
          CLOSE     OUTBB1A1
          BRANCH    OVRCD,PDET9000
.
          MOVE      OBACOMP,ACLAIM            * Claim code
          MOVE      OTBBFUND,AFUNDH
          MOVE      OTBBTBLE,AFNDTB
          MATCH     SP70,AFUNDH
          IF        @EQUAL
            MOVE      PFUNDH,AFUNDH             * health fund
            MOVE      PFNDTB,AFNDTB
          ENDIF
          GOTO      PDET8000
.
.         Emergency
.
PDET3000  MOVE      PINVADM TO ADANUMB
          MOVE      SP70,ADACOMP
          MOVE      ADANUMB,KEY8
          CALL      RDDETA1
          IF        OVRCD=1
            CALL      RDEMVIS1
            IF        OVRCD=0
              MOVE      EMVICOMP,ADACOMP     * Claim code
            ENDIF
          ENDIF
          MOVE      ADACOMP,ACLAIM
          MOVE      PFUNDH,AFUNDH          * health fund
          MOVE      PFNDTB,AFNDTB
          GOTO      PDET8000
.
.         Other Financial details
.
PDET4000  MOVE      PINVCLM,ACLAIM
          MOVE      PFUNDH,AFUNDH
.
PDET8000  MOVE      ZERO,EXIT
          GOTO      PDET99999
.
PDET9000  MOVE      ONE,EXIT
PDET9999  RETURN
+
.***************************************************************************
.         Check if invoice fully paid
.***************************************************************************
CPAY0000  MOVE      ZERO,TMPPAID
          PACK      KEY22 WITH PINVADM,PINVNO,SP6
.
.         Loop over the appropriate DTRAN file
.
          BRANCH    PINVSYS OF CPAY5000,CPAY4000,CPAY3000,CPAY4000,CPAY9000
.
.         In-patient invoice
.
CPAY3000  CALL      RDSDTRN1
CPAY3100  CALL      RDKDTRN1
          BRANCH    OVRCD OF CPAY6000
.
          MATCH     TREF,PINVNO
          GOTO      CPAY6000 IF NOT EQUAL
.
.         Only cash and journals
.
          BRANCH    TRECTYPE,CPAY3100,CPAY3100,CPAY3100,CPAY3100:
                             CPAY3200,CPAY3200
          GOTO      CPAY3100
.
.         payment or journals for this report
.
CPAY3200  ADD       TPATAMTT,TMPPAID
          IF        TRECTYPE=6
            IF        IBCNUGST=2 | IBCNUGST=3
              ADD       PTDTGSTM,TMPPAID         * GST Journal
            ENDIF
          ENDIF
          GOTO      CPAY3100
.
.       Other financial invoice and Outpatient invoice
.
CPAY4000  MOVE      "out",OSTFILE
          MOVE      PVISITE,KEY6
          CALL      RDSITA1
.
          PACK      CFNAME,OSTFILE,FILDTRO1
          OPEN      OUTDTRO1,CFNAME
.
          CALL      RDSDTRO1
CPAY4100  CALL      RDKDTRO1
          BRANCH    OVRCD OF CPAY4400
.
          MATCH     OTINVNO,PINVNO
          GOTO      CPAY4400 IF NOT EQUAL
.
          BRANCH    OTRECTYP,CPAY4100,CPAY4200,CPAY4200
          GOTO      CPAY4100
.
CPAY4200  ADD         OTPATAMT,TMPPAID
          IF          OTRECTYP=3
            IF          IBCNUGST=2 | IBCNUGST=3
              ADD         OTDTGSTM,TMPPAID          * GST Journal
            ENDIF
          ENDIF
          GOTO        CPAY4100
.
CPAY4400  CLOSE     OUTDTRO1
          GOTO      CPAY6000
.
.         A + E invoice
.
CPAY5000  CALL      RDSDTRE1
CPAY5100  CALL      RDKDTRE1
          BRANCH    OVRCD OF CPAY6000
.
          MATCH     ATINVNO,PINVNO
          GOTO      CPAY6000 IF NOT EQUAL
.
          BRANCH    ATRECTYP,CPAY5100,CPAY5200,CPAY5200
          GOTO      CPAY5100
.
CPAY5200  ADD       ATPATAMT,TMPPAID
          IF        ATRECTYP=3
            IF        IBCNUGST=2 | IBCNUGST=3
              ADD       ATDTGSTM,TMPPAID    * Journal GST
            ENDIF
          ENDIF
          GOTO      CPAY5100
.
.       Add the total payments to the invoice total to get the outstanding amt
.
CPAY6000  CALL      CRDN0000              * Check credit note
          ADD       FORM12P2,PINVAMT
.
          MOVE      PINVAMT,FORM12P2
          ADD       TMPPAID,FORM12P2
          COMPARE   ZERO,FORM12P2
          GOTO      CPAY9000 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      CPAY9999
.
CPAY9000  MOVE      ONE,EXIT
CPAY9999  RETURN
+
.******************************************************************************
.*        CRDN0000 - Credit for credit note                                   *
.******************************************************************************
CRDN0000  MOVE      ZERO,FORM12P2
          PACK      KEY16,PINVNO,SP70
          CALL      RSPMCNO3
CRDN1000  CALL      RKPMCNO3
          BRANCH    OVRCD,CRDN9000
          MATCH     PMCNINVN,PINVNO         * Same invoice number?
          GOTO      CRDN9000 IF NOT EQUAL   * No
.
          MATCH     PMCNVISN,DPINVADM
          GOTO      CRDN1000 IF NOT EQUAL   * No
.
          ADD       PMCNAMNT,FORM12P2
          GOTO      CRDN10000
.
CRDN9000  COMPARE   ZERO,FORM12P2
          GOTO      CRDN9999 IF EQUAL       * No credit note
          SUB       PTINROUN,FORM12P2
CRDN9999  RETURN
+
.***************************************************************************
.*                                PROC0000             Called by: ML0000   *
.
.*                     Process tempfile                                    *
.***************************************************************************
PROC0000  CALL      PHED0000                   * Print heading
.
          MOVE      SP70,KEY17
          CALL      RSTEMP1
PROC1000  CALL      RKTEMP1
          BRANCH    OVRCD,PROC9999
.
          MOVE      PINVNO,KEY8
          CALL      RDINV1
          BRANCH    OVRCD,PROC1000
.
          MOVE      PINVUR,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PROC1000
.
          MOVE      PSNAME,PACSNAME            * set up patients name
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      DPINVADM,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,PROC1000
.
          MOVE      SP70,PTHSNAME
          CALL      RDPMVX11
          IF        OVRCD=0
            MATCH     SP70,PMVXMHOS
            IF        !@EQUAL
              MOVE      PMVXMHOS,KEY3
              CALL      RDPTHSP1
            ENDIF
          ENDIF
.
          UNPACK    PINVDTE,XCENT,XYEAR,XMON,XDAY
          PACK      DIM10,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          REP       " 0",DIM10               * Invoice date
.
          PACK      KEY10,CDD,SLASH,CMM,SLASH,CCC,CYY
          REP       " 0",KEY10               * journal date will be current date
.
          MOVE      SP70,DIM12
          IF        PVITYPE=1
            MOVE      "Emergency",DIM12
          ELSE
            IF        PVITYPE=2
              MOVE      "Outpatients",DIM12
            ELSE
              IF        PVITYPE=3
                MOVE      "Inpatients",DIM12
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      XJRNAMNT,FORM12P2
          MULT      "-1",FORM12P2
          PRINT     "<tr><td>",PINVNO,"</td>":
                      "<td>",DIM10,"</td>":
                      "<td>",FORM12P2,"</td>":
                      "<td>",XJRNLCOD,"</td>":
                      "<td>",KEY10,"</td>":
                      "<td>",PINVUR,"</td>":
                      "<td>",PACFNAME,"</td>":
                      "<td>",DPINVADM,"</td>":
                      "<td>",DIM12,"</td>":
                      "<td>",ACLAIM,"</td>":
                      "<td>",AFUNDH,"</td>";
.
          IF        IBCNMHOS=1
            PRINT     "<td>",PTHSNAME,"</td>";
          ENDIF
          PRINT       "<td>",USERID,"</td></tr>"
.
          BRANCH    OPTION,PROC1000               * Report Only
.
          PACK      KEY5,CODEJ,XJRNLCOD,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PROC1000
.
          CALL      UJNL0000                      * Update Journal Record
          BRANCH    EXIT,PROC1000
          CALL      CREC0000              * Update Un-reconciled inv.file
.
          GOTO      PROC1000
.
PROC9999  RETURN
+
.***************************************************************************
.         Update Journal
.***************************************************************************
UJNL0000  MOVE      ZERO,EXIT
          MOVE      PVIBILL,KEY8
          CALL      TRVISA1
          CALL      CHKD0000                * get the DTR record
          COMPARE   ZERO,OVRCD
          GOTO      UJNL0000 IF EQUAL
          MOVE      PVITRAN,TTRANSN1
          MOVE      TTRANSN1,ATRANNO
.
          MOVE      ZERO,TPATAMTT
          MOVE      ZERO,TPATAMTP        * Inpatient
          MOVE      ZERO,TREBATE
          MOVE      ZERO,PTDTGSTA
          MOVE      ZERO,PTDTGSTM
          MOVE      SP70,PTDTGSTC
.
          MOVE      XJRNLCOD,TTYPE
          MOVE      TDESC,TDDESC
          MOVE      XJRNAMNT,TPATAMTT
          MOVE      ZERO,TREBATE
          MOVE      "P",TTYPEDEB
.
          CALL     SETJNL00
          MOVE     "C",FINTYPE
          IF       PTCNJFEE=1
            PACK     FINCODE,ACLAIM,TTYPE,SP20
.                            Claim Code, Journal Code
          ELSE
            PACK     FINCODE,TTYPE,SP20
.                            Journal Code
          ENDIF
.
          MOVE     TPATAMTT,FINAMT
          PACK     FINDATE,CCC,CYY,CMM,CDD
          REP      " 0",FINDATE
.
          MOVE     PINVNO,FININVN
          MOVE     PINVADM,FINADMN
          MOVE     PINVUR,FINURNO
.
          MOVE     PVITYPE,FINSYS
          CALL     CEVT0000                 * Check for Events
          MATCH    "1",KEY1
          IF       @EQUAL
            MOVE     ONE,FINSYS             * Emergency system
          ENDIF
          MOVE     PINVSITE,FINSITE
          MOVE     ZERO,FGSTFLAG
          MOVE     PMVXMHOS,FINHOSP
          CALL     PATCALF
.
          MULT      SEQ,TPATAMTT
          MULT      SEQ,ATPATAMT
          MULT      SEQ,OTPATAMT
.
          BRANCH    PVITYPE OF UJNL4300,UJNL4200,UJNL4100,UJNL4150
          CALL      CEVT0000                 * Check for Events
          MATCH     "1",KEY1
          GOTO      UJNL4300 IF EQUAL        * Emergency event
          GOTO      UJNL9999
.
.         Inpatient invoice
.
UJNL4100  PACK      KEY22 WITH TADMNO,TREF,TTRANSN1,SP70
          CALL      WRDTRN1
          GOTO      UJNL5000
.
UJNL4150  MOVE      "out",OSTFILE
          GOTO       UJNL4220
.
.         Outpatient invoice
.
UJNL4200  MOVE      "out",OSTFILE
          MOVE      PINVSITE,KEY6
          CALL      RDSITA1
.
UJNL4220  MOVE      ZERO,OTSERVS
          MOVE      SP70,OTDTDSKM
          PACK      OTSPARE,SP70
.
          PACK      CFNAME,OSTFILE,FILDTRO1
          PACK      KEY22 WITH OTNUMB,OTINVNO,OTTRANS,SP70
          OPEN      OUTDTRO1,CFNAME
.
          CALL      WRDTRO1
          CLOSE     OUTDTRO1
          MOVE      OTNUMB,TADMNO
          GOTO      UJNL5000
.
.         A & E invoice
.
UJNL4300  PACK      KEY22 WITH ATNUMB,ATINVNO,ATTRANS,SP70
          CALL      WRDTRE1
          MOVE      ATNUMB,TADMNO
.
.         Update the Journal summary file
.
UJNL5000  MOVE      TTYPE,JCODE
          PACK      JDATE WITH CCC,CYY,CMM,CDD  * use current date
          REP       " 0",JDATE
.
          MOVE      TADMNO,JADMN
          MOVE      TTRANSN1,JTRNS
          MOVE      PINVNO,JINVN
          MOVE      SP70,PTJRINDC
.
          MOVE      USERID,PTJRCUID              * Created by
          PACK      PTJRCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTJRCDAT                * Date created
          CLOCK     TIME,PTJRCTIM                * Time created
          MOVE      SP70,JSPARE
.
          PACK      KEY24 WITH JCODE,JDATE,JADMN,JTRNS,SP70
          CALL      WRJRNL1
.
          MOVE      PINVNO,KEY8
          CALL      RDPTINV1
.
          ADD       TPATAMTT,PINVJOUR
.
.         Check if the invoice now balances
.
          MOVE      PINVAMT,F12P2      * Invoice amount +
          ADD       PINVPATA,F12P2     * Patient payments +
          ADD       PINVHFDA,F12P2     * H.F payments +
          ADD       PINVOTHA,F12P2     * Other payments +
          ADD       PTINCRTT,F12P2     * Credit Notes +
          ADD       PINVJOUR,F12P2     * Journals = amount outstanding
          ADD       PTINGSTJ,F12P2     * Journal GST = amount outstanding
.
          MOVE      "99999999",PINVBLCD  * Assume it doesn't balance
.
          COMPARE   ZERO,F12P2         * Does it balance ?
          GOTO      UJNL8000 IF NOT EQUAL
.
.         Invoice is now balanced. Record the date that this occured
.
          PACK      PINVBLCD,CCC,CYY,CMM,CDD
          REP       " 0",PINVBLCD
.
UJNL8000  PACK      PTINUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTINUDAT             * date updated
          CLOCK     TIME,PTINUTIM             * time updated
          MOVE      USERID,PTINUUID           * web user id
          CALL      UPINV1
.
.       If invoice is full paid or overpaid remove further Debtor Future Actions
.
          IF        F12P2<=0
            CALL      DFAC0000           * Delete Future Action from Act.Plan
            CALL      UPFAPPLN           * Update FA Completion date
          ENDIF
.
          PROC      FLAGDEBT
          COMPARE   ONE,CINVJRN
          CALL      UPDJRN00 IF EQUAL
.
.         Update the control file with the transfer date if neccessary
.
          PACK      KEY6,TFCENT,TFYEAR,TFMONTH
          REP       " 0",KEY6
          MOVE      ZERO,EXIT
          READ      CONTROLF,EIGHTY;*148,CFRDTE
          MATCH     CFRDTE,KEY6
          GOTO      UJNL9999 IF NOT LESS
          MOVE      KEY6,CFRDTE
          WRITAB    CONTROLF,EIGHTY;*148,CFRDTE
          GOTO      UJNL9999
UJNL9999  RETURN
+
.----------------------------------------------------------------------
. CHKD0000 : Check the Debtors Transaction Records
.            Set up key depending on the system (A+E, Outpatient, Inpatient)
.----------------------------------------------------------------------
CHKD0000  BRANCH    PVITYPE,CHKD1000,CHKD2000,CHKD3000,CHKD2000
.
.         A & E Dtran
.
CHKD1000  PACK      KEY22,PVIBILL,PINVNO,PVITRAN
          CALL      RDADTRE1
          GOTO      CHKD9999
.
.         Outpatient Dtran
.
CHKD2000  MOVE      "out",OSTFILE
          MOVE      PINVSITE,KEY6
          CALL      RDSITA1
.
          PACK      CFNAME,OSTFILE,FILDTRO1
          OPEN      OUTDTRO1,CFNAME
.
          PACK      KEY22,PVIBILL,PINVNO,PVITRAN
          CALL      RDADTRO1
          GOTO      CHKD9999
.
.         Inpatient Dtran
.
CHKD3000  PACK      KEY22,AADMNO,PINVNO,PVITRAN
          CALL      RDADTRN1
.
CHKD9999  RETURN
+
.***************************************************************************
.         Set journal record
.***************************************************************************
SETJNL00  BRANCH    PINVSYS OF SETJNL10,SETJNL20,SETJNL30,SETJNL20
+
.         A & E Dtran setup
.
SETJNL10  MOVE      PVIBILL,ATNUMB
          MOVE      PINVNO,ATINVNO
          MOVE      TTRANSN1,ATTRANS
          MOVE      TDDESC,ATDDESC
          MOVE      TPATAMTT,ATPATAMT
          MOVE      TPATAMTT,ATPATPOR
          MOVE      TREBATE,ATREBPOR
          MOVE      XJRNLDAT,ATDDATE
          REP       " 0",ATDDATE
          UNPACK    XJRNLDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,TFCENT
          MOVE      CYEAR,TFYEAR
          MOVE      CMON,TFMONTH
          MOVE      CDAY,TFDAY
          MOVE      PTDTGSTA,ATDTGSTA
          MOVE      PTDTGSTM,ATDTGSTM
          MOVE      PTDTGSTC,ATDTGSTC
.
          MOVE      SP10,ATITEMNO
          MOVE      TTYPE,ATTYPE
          MOVE      ZERO,ATPAYTYP
          MOVE      SP20,ATRECEPT
          MOVE      ONE,ATINVPRT
          MOVE      THREE,ATRECTYP
          MOVE      SP3,ATMISGRP
          MOVE      SP3,ATDEPTYP
          MOVE      ZERO,ATBATCHN
          MOVE      ZERO,ATSPARE1
          MOVE      ZERO,ATSPARE2
          MOVE      SP70,ATSPARE
          PACK      ATDTEFFD,CCC,CYY,CMM,CDD
          REP       " 0",ATDTEFFD
.
          MOVE      ZERO,ATDTSCHF
          MOVE      SP70,ATDTITYP
          MOVE      SP70,ATDTRBRS
          MOVE      SP70,ATDTPCOD
          PACK      ATDTACOI,SP70       * After Care Override Indicator
          PACK      ATDTDSOV,SP70       * Duplicate Service Override
          PACK      ATDTSTXT,SP70       * Service Text
          PACK      ATDTLSPN,SP70       * Location Specific Practice No(LSPN)
          PACK      ATDTMPOV,SP70       * Multi Procedure Override
          PACK      ATDTNMPT,SP70       * Number of Patients Seen
          PACK      ATDTSDCD,SP70       * Self Deem Code (Cat Sd)
          PACK      ATDTTDUR,SP70       * Time Duration (Mins)
          PACK      ATDTROVR,SP70       * Restricted Override Code (Cat Ro)
          GOTO      SETJNL99
.         Outpatient Dtran setup
.
SETJNL20  MOVE      PVIBILL,OTNUMB
          MOVE      PINVNO,OTINVNO
          MOVE      TTRANSN1,OTTRANS
          MOVE      TDDESC,OTDDESC
          MOVE      TPATAMTT,OTPATAMT
          MOVE      TPATAMTT,OTPATPOR
          MOVE      TREBATE,OTREBPOR
          MOVE      XJRNLDAT,OTDDATE
          REP       " 0",OTDDATE
          UNPACK    XJRNLDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,TFCENT
          MOVE      CYEAR,TFYEAR
          MOVE      CMON,TFMONTH
          MOVE      CDAY,TFDAY
          MOVE      PTDTGSTA,OTDTGSTA
          MOVE      PTDTGSTM,OTDTGSTM
          MOVE      PTDTGSTC,OTDTGSTC
.
          MOVE      SP10,OTITEMNO
          MOVE      TTYPE,OTTYPE
          MOVE      ZERO,OTPAYTYP
          MOVE      SP8,OTRECEPT
          MOVE      ONE,OTINVPRT
          MOVE      THREE,OTRECTYP
          MOVE      SP3,OTMISGRP
          MOVE      SP3,OTDEPTYP
          MOVE      ZERO,OTBATCHN
          MOVE      ZERO,OTNINVS
          MOVE      ZERO,OTSERVS
          PACK      OTDTEFFD,CCC,CYY,CMM,CDD
          REP       " 0",OTDTEFFD
          MOVE      SP70,OTDTPCOD
          MOVE      SP70,OTDTDSKM
          PACK      OTSPARE,SP70
          GOTO      SETJNL99
.
.         Inpatient Dtran setup
.
SETJNL30  MOVE      PVIBILL,TADMNO                              (key field)
          MOVE      PINVUR,TDCODE
          MOVE      SIX,TRECTYPE            * RECORD TYPE JOURNAL
          MOVE      ONE,TINVPRT             * set PRINT FLAG
          MOVE      ZERO,TPATAMTG
          MOVE      ZERO,TPATAMTH
          MOVE      ZERO,TPATAMTI
          MOVE      TPATAMTT,TPATAMTP
          MOVE      ZERO,TTDAY
          MOVE      ZERO,TTMONTH
          MOVE      ZERO,TTYEAR
          MOVE      ZERO,TTCENT
          MOVE      SP10,TITEMNO
          MOVE      SP3,TMISGRP
          MOVE      ZERO,TPAYTYPE
          MOVE      PINVNO,TREF             * PUT ONTO INVOICE  (key field)
          MOVE      ZERO,TNODAYS
          UNPACK    SP70,TDOCTA,TDOCTO,TSESSION,TDEPTYP,TDWARD:
                         PTDTCNTR,PTDTCPRC,PTDTCONT,PTDTSUNQ,PTDTBTCH
          MOVE      ZERO,TSERVS
          MOVE      ZERO,PTDTGSTL
          MOVE      ZERO,PTDTADPT
          MOVE      ZERO,PTDTADRB
.
          MOVE      SP20,TRECEIPT            * should key in ref
          MOVE      ZERO,TCLAIM
          MOVE      PINVCLM,TCLMTYP
          MOVE      SP70,TADMTYP
          MOVE      ZERO,TBATCHN
          MOVE      SP70,PTDTBTYP
          MOVE      ZERO,PTDTLSPT
          MOVE      ZERO,PTDTLSRB
          MOVE      ZERO,PTDTADPT
          MOVE      ZERO,PTDTADRB
          MOVE      ZERO,PTDTDTYP
          MOVE      ZERO,PTDTEPSD
          MOVE      ZERO,PTDTCCU
          PACK      PTDTDES2,SP20,SP20
          PACK      PTDTUNIQ,SP70
          MOVE      SP8,PTDTTIME
          MOVE      SP1,PTDTCRST
          PACK      TSPARE,SP30,SP30
          UNPACK    XJRNLDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,TFCENT
          MOVE      CYEAR,TFYEAR
          MOVE      CMON,TFMONTH
          MOVE      CDAY,TFDAY
          PACK      PTDTEFFD,CCC,CYY,CMM,CDD
          REP       " 0",PTDTEFFD
          MOVE      SP70,PTDTTMNO
          MOVE      SP70,PTDTPMBS
          MOVE      SP70,PTDTPCOD
.
SETJNL99  RETURN
+
.***************************************************************************
.         Subroutine to write to patient invoice with journal file
.***************************************************************************
UPDJRN00  MOVE      ZERO,BATCHNO
          PACK      KEY16,BATCHNO,PINVNO
          CALL      RDIJRN1
          COMPARE   ZERO,OVRCD
          RETURN    IF EQUAL
.
          MOVE      PINVNO,IJINVNO
          MOVE      PVIBILL,IJADMNO
          MOVE      BATCHNO,IJBATCH
          MOVE      SP20,IJSPARE
          CALL      WRIJRN1
UPDJRN99  RETURN
+
.------------------------------------------------------------
.         Delete Future Actions where tcindc1= H,B or P
.------------------------------------------------------------
DFAC0000  MATCH     "1",PTCNDFUP
          GOTO      DFAC9999 IF NOT EQUAL
.
          PACK      DIM8,CCC,CYY,CMM,CDD    * setup current date
          REP       " 0",DIM8
          PACK      KEY19,DPINVADM,DIM8,SP70
DFAC1000  CALL      RDSFACT1
DFAC2000  CALL      RDKFACT1
          BRANCH    OVRCD,DFAC9999
.
          MATCH     DPINVADM,DFADMNO
          GOTO      DFAC9999 IF NOT EQUAL
.
          MATCH     PINVNO,PTFCINVN         * same invoice number?
          GOTO      DFAC2000 IF NOT EQUAL
.
          MOVE      "FA",TCODE
          PACK      KEY5,TCODE,FACODE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,DFAC2000
.
          MATCH     "H",TCINDC1
          GOTO      DFAC4000 IF EQUAL
          MATCH     "B",TCINDC1
          GOTO      DFAC4000 IF EQUAL
          MATCH     "P",TCINDC1
          GOTO      DFAC2000 IF NOT EQUAL
.
DFAC4000  PACK      KEY19,DFADMNO,FADATE,FACODE,SP70
          CALL      DEFACT1
          GOTO      DFAC1000
.
DFAC9999  RETURN
+
.------------------------------------------------------------
.         Check for Events
.         Return KEY1=1 for Emergency 2=Outpatient
.------------------------------------------------------------
CEVT0000  MOVE      SP70,KEY1
          COMPARE   NINE,PVITYPE
          GOTO      CEVT9999 IF NOT EQUAL
.
          MATCH     " 1",PVISYST          * Event?
          GOTO      CEVT9999 IF NOT EQUAL
.
          MOVE      PVIBILL,KEY8
          CALL      RDPMEVT1
          BRANCH    OVRCD,CEVT9999
.
          PACK      KEY5,ANSE,ANSV,PMEVEVNT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CEVT9999
.
          MATCH     ANSE,TCINDC1
          IF        @EQUAL
            MOVE      "1",KEY1           * Emergency system
          ENDIF
.
          MATCH     ANSO,TCINDC1
          IF        @EQUAL
            MOVE      "2",KEY1           * Outpatient system
          ENDIF
.
CEVT9999  RETURN
+
.-------------------------------------------------------------------------------
.         Check if invoice is reconciled after input journal
.-------------------------------------------------------------------------------
CREC0000  MOVE      ZERO,F12P2
          MOVE      PINVNO,FORM12
          MOVE      FORM12,SAVKEY12
          PACK      KEY29,SAVKEY12,SP70
          CALL      RSRCDTE3
CREC1000  CALL      RKRCDTE3
          BRANCH    OVRCD,CREC2000
.
          MATCH     SAVKEY12,RCDTINVN       * Same invoice number?
          GOTO      CREC2000 IF NOT EQUAL
.
          MOVE      RCDTREGI,KEY3
          CALL      RDREGA1
          BRANCH    OVRCD,CREC1000
.
.         BRANCH    REGIBACD,CREC1200      * Patient billing
.         COMPARE   "97",REGIBACD
.         GOTO      CREC1000 IF NOT EQUAL  * NOT IP Deposits
.
CREC1200  PACK      KEY12,RCDTRECN,SP70
          CALL      RDRCBNK1
          BRANCH    OVRCD,CREC1000
          MATCH     "1",RCBNSTAT
          GOTO      CREC1000 IF EQUAL       * Receipt cancelled
.
          ADD       RCDTAMNT,F12P2       * payment
          GOTO      CREC1000
.
CREC2000  MOVE      PINVAMT,FORM12P2      * Invoice amount +
          SUB       F12P2,FORM12P2        * Payments
          ADD       PTINCRTT,FORM12P2     * Credit Notes +
          ADD       PINVJOUR,FORM12P2     * Journals = amount outstanding
          ADD       PTINGSTJ,FORM12P2     * Journal GST = amount outstanding
.
          CALL      RECN0000              * Update Unreconciled invoice
.
CREC9999  RETURN
+
.-------------------------------------------------------------------------------
.         If invoice balanced, removed record from Unreconciled invoice file
.         if invoice not balanced, write or update the Unreconciled invoice file
.-------------------------------------------------------------------------------
RECN0000  PACK      KEY8,PINVNO
          CALL      RDPTURE1
.
          CALL      IBACLOCK
          IF        FORM12P2=0
            IF        OVRCD=0
              CALL      DEPTURE1              * Delete Un-reconciled record
            ENDIF
          ELSE
            IF        OVRCD=0
              MOVE      PTINCRTT,PTURJRNL     * Credit Notes +
              ADD       PINVJOUR,PTURJRNL     * Journals = amount outstanding
              ADD       PTINGSTJ,PTURJRNL     * Journal GST = amount outstanding
              MOVE      FORM12P2,PTUROSTD     * Outstanding amount
.
.          Update PTURCDAT/PTURCTIME when updating intead of PTURUDAT/PTURUTIM
.
              PACK      PTURCDAT,CCC,CYY,CMM,CDD
              REP       " 0",PTURCDAT
              MOVE      CTIMEIS,PTURCTIM
              MOVE      USERID,PTURCUID       * WEB user id
              CALL      UPPTURE1              * Update outstanding amount
            ELSE
              CALL      SETU0000              * Setup Unreconciled inv.fields
              CALL      WRPTURE1
            ENDIF
          ENDIF
.
RECN9999  RETURN
+
.------------------------------------------------------------
.         Setup fields for the Unreconciled invoices file
.------------------------------------------------------------
SETU0000  MOVE      PINVNO,PTURINVN
          MOVE      PINVADM,PTURVISN
          MOVE      "0",PTURSTAT                * New status
          MOVE      PINVSYS,PTURSYST
          MOVE      ACLAIM,PTURCLMN
          MOVE      AFUNDH,PTURHFND
          MOVE      AFNDTB,PTURHTAB
.
          MOVE      PINVAMT,PTURINVT     * Invoice total
          MOVE      PINVPATA,PTURPAYM    * Patient payments +
          ADD       PINVHFDA,PTURPAYM    * H.F payments +
          ADD       PINVOTHA,PTURPAYM    * Other payments +
.
          MOVE      PTINCRTT,PTURJRNL    * Credit Notes +
          ADD       PINVJOUR,PTURJRNL    * Journals = amount outstanding
          ADD       PTINGSTJ,PTURJRNL    * Journal GST = amount outstanding
          MOVE      FORM12P2,PTUROSTD    * Outstanding amount
.
          CALL      IBACLOCK
          PACK      PTURCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTURCDAT
          MOVE      CTIMEIS,PTURCTIM
          MOVE      USERID,PTURCUID        * WEB user id
.
          PACK      PTURUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTURUDAT
          MOVE      CTIMEIS,PTURUTIM
          MOVE      USERID,PTURUUID        * WEB user id
.
SETU9999  RETURN
+
.***************************************************************************
.         Print heading
.***************************************************************************
PHED0000  PRINT     "<table border=1><tr><td width=10%>Invoice Number</td>":
                    "<td>Invoice Date</td>":
                    "<td>Journal Amount</td>":
                    "<td>Journal Code</td>":
                    "<td>Journal Date</td>":
                    "<td>U/R No.</td>":
                    "<td>Patient Name</td>":
                    "<td>Visit Number</td>":
                    "<td>System</td>":
                    "<td>Claim Code</td>":
                    "<td>Health Fund</td>";
.
          IF        IBCNMHOS=1
            PRINT     "<td>Hospital</td>";
          ENDIF
          PRINT     "<td>User Id</td></tr>"
.
PHED9999  RETURN
+
.***************************************************************************
.*                                SCRN0000             Called by: ML0000   *
.*                     display the input screen                            *
.***************************************************************************
SCRN0000  DISPLAY   *P1:3,*EF,*P2:4," Maximum Amount O/S":
                    *P27:4,COLON:
                    *P2:5," Journal Type (Debit)":
                    *P27:5,COLON:
                    *P2:6," Maximum Amount in Credit":
                    *P27:6,COLON:
                    *P2:7," Journal Type (Credit)":
                    *P27:7,COLON;
.
          IF        IBCNMHOS=1
            DISPLAY   *P2:8," Hospital ID":
                      *P27:8,COLON;
          ENDIF
          DISPLAY   *P2:9," Balances greater than":
                    *P27:9,COLON:
                    *P2:10," User ID":
                    *P27:10,COLON

SCRN9999  RETURN
+
.*************************************************************************
.       Create Tempfile
.*************************************************************************
CTMP0000  CALL      KILL0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMET,CMDLINE
          APPEND    " 66 ",CMDLINE
          APPEND    "U17-17,9-16,1-8",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      TMPFILE1,FNAMET
.
CTMP9999  RETURN
+
.******************************************************************************
.         Delete records from tempfile
.******************************************************************************
KILL0000  CLOSE     TMPFILE1
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.==============================================================================
.         temp file
.==============================================================================
RSTEMP1   READ      TMPFILE1,KEY17;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TMPFILE1;PINVNO,PINVDTE,XJRNINDC,XJRNLCOD,XJRNLDAT:
                             XJRNAMNT,ACLAIM,AFUNDH,PMVXMHOS,WBSEUID
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   UPDATE    TMPFILE1;PINVNO,PINVDTE,XJRNINDC,XJRNLCOD,XJRNLDAT:
                             XJRNAMNT,ACLAIM,AFUNDH,PMVXMHOS,WBSEUID
          RETURN
.
WRTEMP1   WRITE     TMPFILE1,KEY17;PINVNO,PINVDTE,XJRNINDC,XJRNLCOD:
                             XJRNLDAT,XJRNAMNT,ACLAIM,AFUNDH,PMVXMHOS,WBSEUID
          RETURN
.
+
.==============================================================================
.
          INC       STD001IO
          INC       PATCODKY
          INC       PATHSPKY
          INC       PATSELFN
          INC       TFILENAM                     * Generate Temp File Name
          INC       PATCALFN
          INC       PMSOSDTM
          INC       UPFAPPLN                     * Update FA Completion date
.
          INC       WEBSECIO/INC
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       WEBERRIO/INC                 * Web Server Error Logging
          INC       AAEDE1IO/INC
          INC       AAEDTRIO/INC
          INC       AAEDI1IO/INC
          INC       EMRVISIO/INC
          INC       IBAPASIO/INC
          INC       IBASECIO/INC
          INC       OUTBB1IO/INC
          INC       OUTDTRIO/INC
          INC       OUTSITIO/INC
          INC       PATCODIO/INC
          INC       PATDETIO/INC
          INC       PATDRGIO/INC
          INC       PATDSCIO/INC
          INC       PATDTRIO/INC
          INC       PATFACIO/INC
          INC       PATFINIO/INC
          INC       PATFIGIO/INC
          INC       PATRFNIO/INC
          INC       NZPRFNIO/INC
          INC       PATRFGIO/INC
          INC       NZPFINIO/INC
          INC       NZPFIGIO/INC
          INC       PATFN1IO/INC
          INC       PATIJRIO/INC
          INC       PATJRNIO/INC
          INC       PATHSPIO/INC
          INC       PATINVIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATUREIO/INC
          INC       PATVISIO/INC
          INC       PMSCNOIO/INC
          INC       PMSEVTIO/INC
          INC       PMSVX1IO/INC
          INC       RCPDTEIO/INC
          INC       RCPBNKIO/INC
          INC       RCPREGIO/INC
.
.
+

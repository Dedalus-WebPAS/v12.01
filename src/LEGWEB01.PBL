.----------------------------------------------------------------------
. Program       : LEGWEB01.PBL
.
. Function      : New Legacy patient visit enquiry server for R6.11 visits
.
. Modifications : 
.----------------------------------------------------------------------
. V12.00.02 03/06/2025  J.Tan          TSK 0955096
.           Added Alphanumeric visit number changes
. V12.00.01 15.05.2025  DA Sarkies     TSK 0955096                     
.           Updated for Alpha-Numeric Visit Number                     
. V11.02.01 09/02/2022  Thanh T       TSK 0905641
.           Recompiled as OUTMA1FD (PATMASTM) changes
. V11.01.01 19/02/2021  Ebon Clements  TSK 0902626
.           CrossBrowser changes                        
. V11.00.02 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V11.00.01 10/03/2020  J.Tan          TSK 0838262
.           Added Effective from and to date to MBS Item file
. V10.11.04 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.03 18/08/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. LEGWEGTM changed.
. V10.11.02 30/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
. V10.11.01 14/07/2017  Thanh T.       TSK 0821710
.           Audit Amission/outpatient visit comments. PATMISTM changed.
.----------------------------------------------------------------------
. V10.04.02 07/04/2014  Peter Vela      CAR 286258
.           Recompiled for PATMASTM
. V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.----------------------------------------------------------------------
. V9.12.01  10/09/2009  Davin                    CAR 205742
.           Recompiled for LEGCATAI - removed open and close of PATITEM1
. V9.11.01  05/03/2009  Ebon Clements CAR 191018
.           Added legacy visit comments file LEGCMNFD
. V9.10.02  23/05/2008  Mike Laming   CAR 169152
.           Recompiled for changes to LEGMISFD/IO (updated to V6.15 PATMISFD)
. V9.10.01  22/05/2008  Mike Laming   CAR 168959
.           Changes for Key Changes in "legdgwaf" for DRG Version -
.           change READ to READS and READP to get latest DRG version
. V9.08.02  19/02/2007 Peter Vela     CAR 135241  
.           Recompiled for WEBDINVS - St. John of God invoice layout
. V9.08.01  25/01/2007  Peter Vela CAR 127930
.           Recompiled for MRTMASFD
. V9.05.01  09/03/2006  Ebon Clements CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.05.B02 30/01/2006  Ebon Clements CAR 93186
.           Mods for REDIRECT length change. Recompiled for IBAWEBDF
. V9.04.02 21/11/2005  Peter Vela    CAR 68675
.           Recompiled for PATDSCTM
. V9.04.01 01/03/2005  Peter Vela    CAR 57547
.           Recompiled for PATVADFD and PATVADIO
. V9.03.02 30/06/2004 Ebon Clements              CAR 51239
.           Recompiled for LEGCATAI - removed open and close if PATFN1A1
. V9.03.01 07/06/2004 Peter Vela                 CAR 49016
.           2004 Statutory Changes - recompiled for PMSPX2TM
. V9.03.00 03/05/2004   Ebon Clements            CAR 49209
.           Created Web Server.
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
.
          INC       PATDO1FD/INC   * Doctor File
          INC       PATHLFFD/INC
          INC       PATLSDFD/INC
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC
          INC       PATVISFD/INC   * Patients Visits File
          INC       PMSVX1FD/INC   * Visit Extension File
          INC       PATCONFD/INC
          INC       PATALRFD/INC
          INC       OUTSITFD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATPR1FD/INC
          INC       PMSPX2FD/INC
          INC       PATFN1FD/INC
          INC       PATWR1FD/INC
          INC       MRTMASFD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
          INC       PATCALAG/INC
          INC       PMSPX2TD/INC
          INC       PATRE1FD/INC
          INC       PATDGWFD/INC
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC
          INC       PATMISTD/INC
          INC       PATHSPFD/INC
          INC       PATITMFD/INC
          INC       PATDADFD/INC
          INC       PATVADFD/INC
          INC       PATCMCFD/INC
          INC       PATASFFD/INC
.
          INC       PMSCCDFD/INC  * Concession Card Details
          INC       PMSHCGTD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCPTD/INC
          INC       APSMASFD/INC
          INC       PATIN1FD/INC
          INC       PATDSCFD/INC
          INC       PATICDFD/INC
          INC       PATICOFD/INC
.
. Legacy Files
.
          INC       LEGCMNFD/INC
          INC       LEGCODFD/INC
          INC       LEGDOCFD/INC
          INC       LEGDTRFD/INC
          INC       LEGDSCFD/INC
          INC       LEGINVFD/INC
          INC       LEGMISFD/INC
          INC       LEGWRDFD/INC
          INC       LEGTRNFD/INC
          INC       LEGVISFD/INC
          INC       LEGWVEFD/INC
          INC       LEGWMAFD/INC
          INC       LEGWCOFD/INC
          INC       LEGDTAFD/INC
          INC       LEGDISFD/INC
          INC       LEGDEAFD/INC
          INC       LEGDTOFD/INC
          INC       LEGBOBFD/INC
          INC       LEGBOAFD/INC
          INC       LEGECDFD/INC
          INC       LEGECOFD/INC
          INC       LEGMMBFD/INC
          INC       LEGCLIFD/INC
          INC       LEGDADFD/INC
          INC       LEGCTYFD/INC
          INC       LEGMAAFD/INC
          INC       LEGPGRFD/INC
          INC       LEGDGWFD/INC
          INC       LEGSITFD/INC
.
          INC       WEBDINVS/INC
.
.----------------------------------------------------------------------
CATD1     INIT      "D1"
CATD2     INIT      "D2"
CATD3     INIT      "D3"
CATD4     INIT      "D4"
CATD5     INIT      "D5"
CATU6     INIT      "U6"
CATU7     INIT      "U7"
.
CLAIMCOD  DIM       6
CLMDESC   DIM       20
CODTYP    FORM      2
DIM4      DIM       4
DIM18     DIM       18
DIM30     DIM       30
DISPSUR   DIM       8                          * Display Saved U/R Number
DISPMUR   DIM       8                          * Display Merged U/R Number
DISCDATE  DIM       12
CASEKEYS  DIM       28
CURRDATE  DIM       8       * Current Date
DDRGCODE  DIM       4
DISNAM70  DIM       70
.DMDCCODE  DIM       4
.DRGVER    DIM       3
DOCTCODE  DIM       8
DSCHFLAG  FORM      1
PMVXFLAG  FORM      1
BRECFLAG  FORM      1
DISPDATT  DIM       25
EPSCD001  FORM      2                            * Episode No (CGI Parameter)
EPISODNO  FORM      2                            * Episode No
FORM8     FORM      8 
FORM8P2   FORM      8.2
FILTVTYP  FORM      2
INVOICEN  DIM       8
LEGADMNO  DIM       8
HFNDESC   DIM       6
HFNTABL   DIM       8
INPATVIS  INIT      " 3"
INPSTAT1  INIT      "Pre-adm"
INPSTAT2  INIT      "Current IP"
INPSTAT3  INIT      "Discharged"
INPSTA3A  INIT      "Disc-"
INPSTAT4  INIT      "Leave"
INPSTAT5  INIT      "Cancel"
JOURFLAG  FORM      1
JOURTOT   FORM      7.2
NEXTVKEY  DIM       26
PREVVKEY  DIM       26
OUTPVIS   INIT      " 2"
EMERGVIS  INIT      " 1"
MRGEFLAG  FORM      1
MBSDESC   DIM       70
NAME70    DIM       70
NEXTPAGE  DIM       1       * Nextpage parameter CGI parameter
PREVFLAG  FORM      1
PSAGETYP  DIM       3
PSAGECON  DIM       3
ROWCOUNT  FORM      5
SHOWFLAG  FORM      1
STARTKEY  DIM       10      * Starting Key
SAVEURNO  DIM       8
SAVKEY26  DIM       26
SECOPT    DIM       1
SESSKEYS  DIM       25
SUMOUTS   FORM      12.2
SUMPAID   FORM      12.2
SUMINV    FORM      12.2
SUMJR     FORM      12.2
TRANDATE  DIM       12
TOTPAID   FORM      12.2
TOTOUTS   FORM      12.2
TOTLFLAG  FORM      1
UPDTTYPE  DIM       1       * Update Type
VISCOUNT  FORM      5
VISTDESC  DIM       10
VISTEMER  INIT      "AAE"
VISTOUTP  INIT      "OP"
VISTINPT  INIT      "IP"
VISTDAY   DIM       5
VISTLOC   DIM       7
VISTTYPE  DIM       5
VISTTIME  DIM       10
VSTATUS   DIM       35
VISITTYP  FORM      1
.
NJAN      INIT      "January"
NFEB      INIT      "February"
NMAR      INIT      "March"
NAPR      INIT      "April"
NMAY      INIT      "May"
NJUN      INIT      "June"
NJUL      INIT      "July"
NAUG      INIT      "August"
NSEP      INIT      "September"
NOCT      INIT      "October"
NNOV      INIT      "November"
NDEC      INIT      "December"
.
ZED10     INIT      "zzzzzzzzzz"
.
.----------------------------------------------------------------------
PRGID     INIT      "LEGWEB01"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Legacy V6 Visits"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      LEGPAT00        * Get Patient Details   
          GOTO      MAIN1000
.
MAIN1200  CALL      LEGINP00        * Legacy Inpatient Visit Enquiry 
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1300  CALL      LEGOUT00        * Legacy Outpatient Visit Enquiry 
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1400  CALL      LEGAAE00        * Legacy AAE Visit Enquiry 
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.------------------------------------------------------------------------------
.  Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      INTMAS00     * Open routine for PATMASTM
          OPEN      PATHLFA1,"pathlfaf"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATLSDA1,"patlsdaf"
          OPEN      PATVISA2,"patvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATRE1A1,"patre1af"
          OPEN      PATDGWA1,"patdgwaf"
          OPEN      VISMDTA1,"vismdtaf"
          OPEN      VISMTXA1,"vismtxaf"
          OPEN      PATITEM1,"patitemn"
          OPEN      PATASFA1,"patasfaf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATVADA1,"patvadaf"
          OPEN      PATIN1A1,"patin1af"
          OPEN      PMSCCDA1,"pmsccdaf"
.
          CALL      OPICD1
          CALL      OPICO1
.
          OPEN      LEGBOKA1,"legbokaf"
          OPEN      LEGBOKA2,"legbokaf"
          OPEN      LEGBOKA3,"legbokaf"
          OPEN      LEGBOKA4,"legbokaf"
          OPEN      LEGBOKA5,"legbokaf"
          OPEN      LEGBOKB1,"legbokbf"
          OPEN      LEGBOKB2,"legbokbf"
          OPEN      LEGCMNT1,"legcmntf"
          OPEN      LEGCODE1,"legcodes"
          OPEN      LEGCODE2,"legcodes"
          OPEN      LEGCODE3,"legcodes"
          OPEN      LEGCODE4,"legcodes"
          OPEN      LEGDETA1,"legdetaf"
          OPEN      LEGDETA2,"legdetaf"
          OPEN      LEGDETA3,"legdetaf"
          OPEN      LEGDETA4,"legdetaf"
          OPEN      LEGDISA1,"legdisaf"
          OPEN      LEGDOCT1,"legdoctf"
          OPEN      LEGDOCT2,"legdoctf"
          OPEN      LEGDOCT3,"legdoctf"
          OPEN      LEGDSCH1,"legdschf"
          OPEN      LEGDSCH2,"legdschf"
          OPEN      LEGDTRE1,"legdtref"
          OPEN      LEGDTRE2,"legdtref"
          OPEN      LEGDTRE3,"legdtref"
          OPEN      LEGDTRE4,"legdtref"
          OPEN      LEGDTRO1,"legdtrof"
          OPEN      LEGDTRO2,"legdtrof"
          OPEN      LEGDTRO3,"legdtrof"
          OPEN      LEGDTRO4,"legdtrof"
          OPEN      LEGDTRA1,"legdtraf"
          OPEN      LEGDTRA2,"legdtraf"
          OPEN      LEGDTRA3,"legdtraf"
          OPEN      LEGDTRA4,"legdtraf"
          OPEN      LEGDTRA5,"legdtraf"
          OPEN      LEGECDA1,"legecdaf"
          OPEN      LEGECDA2,"legecdaf"
          OPEN      LEGECDA3,"legecdaf"
          OPEN      LEGECOA1,"legecoaf"
          OPEN      LEGECOA2,"legecoaf"
          OPEN      LEGECOA3,"legecoaf"
          OPEN      LEGINVR1,"leginvrf"
          OPEN      LEGINVR2,"leginvrf"
          OPEN      LEGINVR3,"leginvrf"
          OPEN      LEGINVR4,"leginvrf"
          OPEN      LEGINVR5,"leginvrf"
          OPEN      LEGMISS1,"legmissf"
          OPEN      LEGMISS2,"legmissf"
          OPEN      LEGMISS3,"legmissf"
          OPEN      LEGMISS4,"legmissf"
          OPEN      LEGMISS5,"legmissf"
          OPEN      LEGMMBS1,"legmmbsf"
          OPEN      LEGMMBS2,"legmmbsf"
          OPEN      LEGTRAN1,"legtranf"
          OPEN      LEGTRAN2,"legtranf"
          OPEN      LEGTRAN3,"legtranf"
          OPEN      LEGTRAN4,"legtranf"
          OPEN      LEGVISA1,"legvisaf"
          OPEN      LEGVISA2,"legvisaf"
          OPEN      LEGVISA3,"legvisaf"
          OPEN      LEGWCOM1,"legwcomf"
          OPEN      LEGWMAB1,"legwmabf"
          OPEN      LEGWARD1,"legwardf"
          OPEN      LEGWARD2,"legwardf"
          OPEN      LEGWARD3,"legwardf"
          OPEN      LEGWARD4,"legwardf"
          OPEN      LEGWVET1,"legwvetf"
          OPEN      LEGCLIA1,"legcliaf"
          OPEN      LEGCLIA2,"legcliaf"
          OPEN      LEGDADA1,"legdadaf"
          OPEN      LEGMASA1,"legmasaf"
          OPEN      LEGMASA2,"legmasaf"
          OPEN      LEGMASA3,"legmasaf"
          OPEN      LEGPGRA1,"legpgraf"
          OPEN      LEGPGRA2,"legpgraf"
          OPEN      LEGDGWA1,"legdgwaf"
          OPEN      LEGDGWA2,"legdgwaf"
          OPEN      LEGDGWA3,"legdgwaf"
          OPEN      LEGCTYA1,"legctyaf"
          OPEN      LEGCTYA2,"legctyaf"
          OPEN      LEGCTYA3,"legctyaf"
          OPEN      LEGSITA1,"legsitaf"
          OPEN      LEGSITA2,"legsitaf"
.
          READ      CONTROLF,ZERO;*85,IBCNUGST
          READ      CONTROLF,TWENTY1;*44,PTCNIDES
          READ      CONTROLF,EIGHTY;*248,PTCNPEFR
          READ      CONTROLF,HUND05;*200,PTCNPJCI
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
INIT9999  RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,STARTKEY
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,SAVEURNO
          MOVE      ZERO,MRGEFLAG
          MOVE      ZERO,FILTVTYP
          MOVE      ZERO,STARTNUM
          MOVE      ZERO,PREVFLAG
          MOVE      SP70,ADMISSNO
          MOVE      SP70,LEGADMNO
          MOVE      ZERO,EPSCD001
          MOVE      SP70,NEXTVKEY
          MOVE      SP70,PREVVKEY
          MOVE      ZERO,VISITTYP
          MOVE      SP70,INVOICEN
.
CLRPAR99  RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "legadmno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LEGADMNO
            GOTO      SETPAR99
          ENDIF
.  
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF 
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtvtyp=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FILTVTYP
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "prevflag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PREVFLAG
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "epscd001=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,EPSCD001
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextvkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTVKEY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "prevvkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PREVVKEY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "visittyp=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,VISITTYP
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "invoicen=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,INVOICEN
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.------------------------------------------------------------
. Mental Health Admission
.------------------------------------------------------------
LEGVIS00  MATCH     SP70,UPDTTYPE
          GOTO      LEGVIS70 IF EQUAL
.
..          MATCH     "U",UPDTTYPE    * XXXXXXXXXXXXXXXX
..          GOTO      LEGVIS10 IF EQUAL
.
          GOTO      LEGVIS90
.

.
LEGVIS70  PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          IF        OVRCD=1
            CALL      CLPATMAS
            GOTO      LEGVIS91
          ENDIF
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
            GOTO      LEGVIS91
          ENDIF
.
          IF        PSTAT=1 | PSTAT=2
            MOVE      PURNO,SAVEURNO
            MOVE      PPSNAME,URNUMBER
            MOVE      ONE,MRGEFLAG
            GOTO      LEGVIS00
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Legacy Visits",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,LEGVIS99
          CALL      WEBBOD00   * Process Web Body
.
          IF        MRGEFLAG=1
            CALL      MRGALT00
          ENDIF
.
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      LEGVIS99
.
LEGVIS90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LEGVIS99
.
LEGVIS91  MOVE      "Can't find patient Master Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LEGVIS99
.
LEGVIS99  RETURN
.
.------------------------------------------------------------
. Alert that this is a merged U/R
.------------------------------------------------------------
MRGALT00  MATCH     SP70,URNUMBER
          GOTO      MRGALT99 IF EQUAL
.
          MATCH     SP70,SAVEURNO
          GOTO      MRGALT99 IF EQUAL
.
          MOVE      SAVEURNO,DISPSUR
          SQUEEZE   DISPSUR
          MOVE      URNUMBER,DISPMUR
          SQUEEZE   DISPMUR
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " alert('U/R Number ",DISPSUR," is merged with ":
                             DISPMUR," \n Using ",DISPMUR,"');":
                             "}":
                             "</script>"
          GOTO      MRGALT99
.
MRGALT99  RETURN
.
.------------------------------------------------------------
. Legacy Inpatient Visits
.------------------------------------------------------------
LEGINP00  MATCH     SP70,UPDTTYPE
          GOTO      LEGINP70 IF EQUAL
.
..          MATCH     "U",UPDTTYPE    * XXXXXXXXXXXXXXXX
..          GOTO      LEGINP10 IF EQUAL
.
          GOTO      LEGINP90
.

.
LEGINP70  CALL      GETPAT00      * inpatient enquiry
          BRANCH    EXIT,LEGINP91
          CLEAR     WEBTITLE
          MOVE      "Legacy Inpatient Visits",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,LEGINP99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      LEGINP99
.
LEGINP90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LEGINP99
.
LEGINP91  MOVE      "Can't find patient details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LEGINP99
.
LEGINP99  RETURN
.
.------------------------------------------------------------
. Legacy Outpatient Visits
.------------------------------------------------------------
LEGOUT00  MATCH     SP70,UPDTTYPE
          GOTO      LEGOUT70 IF EQUAL
.
..          MATCH     "U",UPDTTYPE    * XXXXXXXXXXXXXXXX
..          GOTO      LEGOUT10 IF EQUAL
.
          GOTO      LEGOUT90
.

.
LEGOUT70  CALL      SHWBOK00      * Legacy outpatient enquiry       
          BRANCH    EXIT,LEGOUT91
.
          CLEAR     WEBTITLE
          MOVE      "Legacy Outpatient Visits",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,LEGOUT99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      LEGOUT99
.
LEGOUT90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LEGOUT99
.
LEGOUT91  MOVE      "Can't find patient Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LEGOUT99
.
LEGOUT99  RETURN
.
.------------------------------------------------------------
. Legacy AAE visit enquiry
.------------------------------------------------------------
LEGAAE00  MATCH     SP70,UPDTTYPE
          GOTO      LEGAAE70 IF EQUAL
.
..          MATCH     "U",UPDTTYPE    * XXXXXXXXXXXXXXXX
..          GOTO      LEGAAE10 IF EQUAL
.
          GOTO      LEGAAE90
.
LEGAAE70  CALL      AAEPAT00      * AAE enquiry
          BRANCH    EXIT,LEGAAE91
. 
          CLEAR     WEBTITLE
          MOVE      "Legacy AAE Visits",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,LEGAAE99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      LEGAAE99
.
LEGAAE90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LEGAAE99
.
LEGAAE91  MOVE      "Can't find patient Master Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LEGAAE99
.
LEGAAE99  RETURN
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30,PROFLD40
.
          GOTO      PROFLD99 
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12
          GOTO      PROFLD99
.
PROFLD11  CALL      MASF0000    * Master File Info
          GOTO      PROFLD99
.
PROFLD12  CALL      TABL0000     * Table Displays
          GOTO      PROFLD99
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21,PROFLD22,PROFLD23,PROFLD24,PROFLD25:
                             PROFLD26,PROFLD27
          GOTO      PROFLD99
.
PROFLD21  CALL      MASF0000    * Master File Info
          GOTO      PROFLD99
.
PROFLD22  CALL      LMIS0000    * Legancy Admission File
          GOTO      PROFLD99
.
PROFLD23  CALL      PMIX0000    * PMI Extn file               
          GOTO      PROFLD99
.
PROFLD24  CALL      DRGS0000    * DRG Information               
          GOTO      PROFLD99
.
PROFLD25  CALL      LDSC0000    * Legacy Discharge Info         
          GOTO      PROFLD99
.
PROFLD26  CALL      TABL0000    * Table Displays
          GOTO      PROFLD99
.
PROFLD27  CALL      LCLF0000    * Claim Details
          GOTO      PROFLD99 
.
PROFLD30  BRANCH    FLDSETNO,PROFLD31,PROFLD32,PROFLD33
          GOTO      PROFLD99
.
PROFLD31  CALL      MASF0000    * Master File Info
          GOTO      PROFLD99
.
PROFLD32  CALL      LOBA0000    * Outpatient Files
          GOTO      PROFLD99
.
PROFLD33  CALL      TABL0000    * Table Displays
          GOTO      PROFLD99
.
PROFLD40  BRANCH    FLDSETNO,PROFLD41,PROFLD42,PROFLD43
          GOTO      PROFLD99
.
PROFLD41  CALL      MASF0000    * Master File Info
          GOTO      PROFLD99
.
PROFLD42  CALL      LAEF0000    * Legacy AAE details
          GOTO      PROFLD99
.
PROFLD43  CALL      TABL0000    * Table Displays
          GOTO      PROFLD99
.
PROFLD99  RETURN
.
.------------------------------------------------------------
. Table Outputs (Visit,etc)
.------------------------------------------------------------
TABL0000  BRANCH    FLDITMNO,TABL0100,TABL0200,TABL0300,TABL0400,TABL0500:
                             TABL0600,TABL0700,TABL0800,TABL0900,TABL1000:
                             TABL1100,TABL1200
          GOTO      TABL9999
.
TABL0100  CALL      VISITS00                 * Visit list by type
          GOTO      TABL9999
.
TABL0200  WRITE     HTMLFILE;FILTVTYP;       * Visit Type Filter
          GOTO      TABL9999
.
TABL0300  WRITE     HTMLFILE;LEGADMNO;       * Legacy Visit Number
          GOTO      TABL9999
.
TABL0400  CALL      PREVGR00                 * Previous group     
          GOTO      TABL9999
.
TABL0500  CALL      NEXTGR00                 * Next group     
          GOTO      TABL9999
.
TABL0600  CALL      OPRTAB00                 * ICD Operations Table
          GOTO      TABL9999
.
TABL0700  CALL      DISTAB00                 * ICD Disease Table
          GOTO      TABL9999
.
TABL0800  MOVE      ONE,TOTLFLAG
          CALL      PINVCI00                 * Patient Invoice list
          GOTO      TABL9999
.
TABL0900  WRITE     HTMLFILE;LPINVNO;        * invoice number
          GOTO      TABL9999
.
TABL1000  UNPACK    LPINVDTE,CCENT,CYEAR,CMON,CDAY         * invoice date
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      TABL9999
.
TABL1100  MOVE      ZERO,PRINTFLG
          UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
.
          COMPARE   ZERO,F1
          GOTO      TABL9999 IF EQUAL * skip the heading
          CALL      LCALCTAI           * Display inpat/aae/out patient invoice
          GOTO      TABL9999
.
TABL1200  PACK      KEY11,LEGADMNO,SP70
          CALL      RSLGCMN1
TABL1210  CALL      RKLGCMN1                * Legacy admission comments
          BRANCH    OVRCD,TABL9999
.
          MOVE      LEGADMNO,DIM8VISN
          MATCH     DIM8VISN,LPCADMN
          GOTO      TABL9999 IF NOT EQUAL
.
          WRITE     HTMLFILE;LPCLINE
          GOTO      TABL1210
.
TABL9999  RETURN
.
.
.-------------------------------------------------------------
. Link to Prev Group
.-------------------------------------------------------------
PREVGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       " +",ADMISSNO
          REP       " +",LEGADMNO
          REP       " +",URNUMBER
.
          WRITE     HTMLFILE;"legweb01.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&filtvtyp=",FILTVTYP:
                                 "&urnumber=",URNUMBER:
                                 "&admissno=",ADMISSNO:
                                 "&legadmno=",LEGADMNO;
.
          REP       "+ ",ADMISSNO
          REP       "+ ",LEGADMNO
          REP       "+ ",URNUMBER
.
PREVGR99  RETURN
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NEXTGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       " +",ADMISSNO
          REP       " +",LEGADMNO
          REP       " +",URNUMBER
.
          WRITE     HTMLFILE;"legweb01.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&filtvtyp=",FILTVTYP:
                                 "&urnumber=",URNUMBER:
                                 "&admissno=",ADMISSNO:
                                 "&legadmno=",LEGADMNO;
.
          REP       "+ ",ADMISSNO
          REP       "+ ",LEGADMNO
          REP       "+ ",URNUMBER
.
NEXTGR99  RETURN
.
.------------------------------------------------------------
. Display visit details according to the visit type
.------------------------------------------------------------
VISITS00  MOVE      ONE,SHOWFLAG            * Show no more visits message
.
          IF        NORECORD=0
            MOVE    "10",NORECORD           * Numb of records=0 Default to 10
          ENDIF
          MOVE      ZERO,RECNUMB            * Init Number of records Read
          MOVE      ZERO,DISNUMB            * Init number of records output
.
          COMPARE   ZERO,FILTVTYP           * Default filter to All visits
          IF        @EQUAL
            MOVE      ONE,FILTVTYP
          ENDIF
.
          COMPARE   ONE,FILTVTYP
          IF        @EQUAL
            CALL     ALLTAB00               * All visit file visits
            GOTO     VISITS99
          ENDIF
.
          COMPARE   TWO,FILTVTYP
          IF        @EQUAL
            CALL     ALLTAB00               * Inpatient Visits
            GOTO     VISITS99
          ENDIF
.
          COMPARE   THREE,FILTVTYP
          IF        @EQUAL
            CALL      ALLTAB00              * Outpatient Visits
            GOTO      VISITS99
          ENDIF
.
          COMPARE   FOUR,FILTVTYP
          IF        @EQUAL
            CALL     ALLTAB00               * Emergency Visits
            GOTO     VISITS99
          ENDIF
.
VISITS99  RETURN
.
.------------------------------------------------------------
. Output all visit file related visit in a table
.------------------------------------------------------------
ALLTAB00  MATCH     "       0",URNUMBER
          GOTO      ALLTAB99 IF EQUAL
.
          COMPARE   ZERO,FILTVTYP
          GOTO      ALLTAB99 IF EQUAL
.
          PACK      KEY24,URNUMBER,Z70
          CALL      RSLGVIS2
ALLTAB10  CALL      RPLGVIS2
          BRANCH    OVRCD,ALLTAB95
.
          MATCH     URNUMBER,LVIURNO
          GOTO      ALLTAB95 IF NOT EQUAL
.
          COMPARE   ONE,FILTVTYP            * All Visit Types
          GOTO      ALLTAB20 IF EQUAL
.
          COMPARE   TWO,FILTVTYP            * Inpatient Visit Types
          IF        @EQUAL
            COMPARE   THREE,LVITYPE
            GOTO      ALLTAB20 IF EQUAL
          ENDIF
.
          COMPARE   THREE,FILTVTYP          * Outpatient Visit Types
          IF        @EQUAL
            COMPARE   TWO,LVITYPE
            GOTO      ALLTAB20 IF EQUAL
          ENDIF
.
          COMPARE   FOUR,FILTVTYP            * Emergency Visit Types
          IF        @EQUAL
            COMPARE   ONE,LVITYPE
            GOTO      ALLTAB20 IF EQUAL
          ENDIF
.
          GOTO      ALLTAB10
.
ALLTAB20  COMPARE   THREE,LVITYPE           * Is this an inpatient visit
          GOTO      ALLTAB25 IF NOT EQUAL
.
          MOVE      LVIBILL,KEY8
          CALL      RDLGMIS1
          BRANCH    OVRCD,ALLTAB10
.
          COMPARE   THREE,LSSASTAT          * Only display discharged patients
          GOTO      ALLTAB10 IF NOT EQUAL
.
ALLTAB25  ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      ALLTAB10
          ENDIF
.
          CALL      GETVIS00             * Get Visit Details
          BRANCH    EXIT,ALLTAB10
.
ALLTAB30  UNPACK    LVIDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDATT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          MATCH     SP70,VISTTIME
          IF        !@EQUAL
            ENDSET    DISPDATT
            APPEND    " at ",DISPDATT
            APPEND    VISTTIME,DISPDATT
            RESET     DISPDATT
          ENDIF
.
          IF        (DISNUMB%2) = 0
            WRITE     HTMLFILE;"<tr class=TableRowOdd><td nowrap>"
          ELSE
            WRITE     HTMLFILE;"<tr class=TableRowEven><td nowrap>"
          ENDIF
.
          WRITE     HTMLFILE;"<img src=../images/AppointmentIcon.gif":
                             " class=ListIcon ":
                             " onclick='linkPatient(#"":
                               URNUMBER,"#",#"":
                               LVIBILL,"#",#"":
                               LVITYPE,"#",#"":
                               SECOPT,"#");'>":
                               DISPDATT,"</td>"
.
          WRITE     HTMLFILE;"<td>&nbsp;",VISTDAY,"</td>":
                             "<td>&nbsp;",VISTTYPE,"</td>":
                             "<td>&nbsp;",DOCFNAME,"</td>":
                             "<td>&nbsp;",VISTLOC,"</td>":
                             "<td>&nbsp;",CLAIMCOD,"</td>":
                             "<td>&nbsp;",VSTATUS,"</td>":
                             "<td>&nbsp;",LVIBILL,"</td>":
                             "</tr>"
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      ALLTAB10 IF NOT EQUAL
          MOVE      ZERO,SHOWFLAG
.
ALLTAB95  CALL      NOMORE00                * Show no more visit message
.
ALLTAB99  RETURN
.
.------------------------------------------------------------
. Get Visit Details
.------------------------------------------------------------
GETVIS00  MOVE      ZERO,EXIT
          MOVE      SP1,SECOPT
          MOVE      SP70,VISTDAY
          PACK      VISTTYPE,SP70
          PACK      DOCFNAME,SP70
          MOVE      SP70,VISTLOC
          MOVE      SP70,CLAIMCOD
          MOVE      SP10,VISTTIME
          MOVE      SP70,LACLAIM
          MOVE      SP70,LDDATE
          PACK      VSTATUS,SP70
          MOVE      SP70,DISCDATE
.
          UNPACK    LVIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      DAYOFWEK
          LOAD      DAYNAM3,WEEKDAY,MON,TUE,WED,THU,FRI,SAT,SUN
          RESET     DOCFNAME
.
.------ For outpatients, LVIDOCT is the clinic code, not the doctor's code ----
.
          BRANCH    LVITYPE,GETVIS05,GETVIS10,GETVIS15
          GOTO      GETVIS60
.
.       Emergency
.
GETVIS05  MOVE      LVIBILL,KEY8
          CALL      RDLGDEA1
          BRANCH    OVRCD,GETVIS60
.
          MOVE      LADACOMP,LACLAIM
          MOVE      LADADOCT,LVIDOCT
          MOVE      LADADATE,LVIDATE
          MOVE      LADATIME,VISTTIME
.
          CALL      RDLGDIS1
          IF        OVRCD=0
            MOVE      LADSDATE,LDDATE
          ENDIF
.
GETVIS08  UNPACK    LDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISCDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MATCH     SP70,DISCDATE
          GOTO      GETVIS60 IF EQUAL
.
          PACK      VSTATUS,SP70
          CLEAR     VSTATUS
          APPEND    INPSTA3A,VSTATUS
.
          MATCH     SP70,LADSDISC
          IF        !@EQUAL
            PACK      KEY5,ANSE,ANSD,LADSDISC,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TDESC,KEY10
              APPEND    KEY10,VSTATUS
            ENDIF
          ENDIF
.
          APPEND    SP1,VSTATUS
          APPEND    CPCDATE,VSTATUS
          APPEND    SP1,VSTATUS
          APPEND    LADSTIME,VSTATUS
          APPEND    SP70,VSTATUS
          RESET     VSTATUS
.
          GOTO      GETVIS60
.
.------Outpatient - Read clinic file to get either doctors name, or doctor code
.
GETVIS10  CALL      OCLI0000              * Get Outpatient details
.
          MATCH     SP6,LVIDOCT
          GOTO      GETVIS70 IF EQUAL
.
          PACK      KEY12,LOBASITE,LOBACLIN,SP70
          CALL      RDLGCLI1
          IF        OVRCD = 1
            GOTO      GETVIS12
          ENDIF
.
GETVIS11  MOVE      LOCADESC,DOCFNAME     * If no Doctor Use Clinic Name
          MATCH     SP6,LOCADOCT
          GOTO      GETVIS70 IF EQUAL
          MOVE      LOCADOCT,LVIDOCT     * Use the doctor's code from the clinic
          GOTO      GETVIS60
.
GETVIS12  CLEAR     DOCFNAME
          APPEND    "Invalid Clinic - ",DOCFNAME
          APPEND    LVIDOCT,DOCFNAME
          RESET     DOCFNAME
          GOTO      GETVIS60
.
.         Inpatient
.
GETVIS15  MOVE      LVIBILL,KEY8
          CALL      RDLGMIS1
          BRANCH    OVRCD OF GETVIS60
          LOAD      VSTATUS,LASTAT,INPSTAT1,INPSTAT2,INPSTAT3,INPSTAT4:
                                   INPSTAT5
          MOVE      LATIME,VISTTIME
.
          MOVE      SP70,LDDATE
          CALL      GETLTR00             * Get last transfer record
.
          MOVE      LVIBILL,KEY8
          CALL      RDLGDSC1
          BRANCH    OVRCD,GETVIS40
.
          UNPACK    LDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISCDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      SP20,KEY8
          IF        PTCNUADT<>1
            GOTO      GETVIS30
          ENDIF
.
          GOTO      GETVIS35
.
GETVIS30  PACK      KEY5,ANSD,ANSD,LDDEST
          CALL      RDLGCOD1
          IF        OVRCD=0
            MOVE      LTDESC,KEY8
          ENDIF
.
GETVIS35  PACK      VSTATUS,SP70
          CLEAR     VSTATUS
          APPEND    INPSTA3A,VSTATUS
.
          MATCH     SP10,KEY8
          IF        !@EQUAL
            APPEND    KEY8,VSTATUS
          ENDIF
          APPEND    SP1,VSTATUS
          APPEND    CPCDATE,VSTATUS
          APPEND    SP1,VSTATUS
          APPEND    LDTIME,VSTATUS
          APPEND    SP70,VSTATUS
          RESET     VSTATUS
.
GETVIS40  PACK      VISTLOC,LAWARD,SP70
.
GETVIS60  MATCH     SP6,LVIDOCT
          GOTO      GETVIS70 IF EQUAL
.
          MOVE      LVIDOCT,KEY6         * Read the doctor's file to get nam
          CALL      RDLGDOC1
          IF        OVRCD=1
            CLEAR     DOCFNAME
            APPEND    "Invalid Doctor - ",DOCFNAME
            APPEND    LVIDOCT,DOCFNAME
            RESET     DOCFNAME
            GOTO      GETVIS70
          ELSE
.           CALL      DOCNAM00             * format doctors name
            MOVE      LDTITL,FMTTITLE            * I CAR 13038
            MOVE      LDGNAME,FMTGNAME
            MOVE      LDSNAME,FMTSNAME
            CALL      DOCNM000                  * end I CAR 13038
          ENDIF
.
GETVIS70  MOVE      "CL",CATEGORY
          MOVE      LACLAIM,CODE
          CALL      LGETCD00
          MOVE      LACODE,CLAIMCOD
.
          LOAD      VISTTYPE,LVITYPE,VISTEMER,VISTOUTP,VISTINPT
.
          UNPACK    LVIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          PACK      VISTDAY,DAYNAM3,SP70
.
GETVIS99  RETURN
.
.------------------------------------------------------------
. Get Outpatient details
.------------------------------------------------------------
OCLI0000  PACK      KEY36,LVIURNO,LVIDATE,SP70
          CALL      RSLGBOA3
OCLI1000  CALL      RKLGBOA3
          BRANCH    OVRCD,OCLI9999
.
          MOVE      LVIURNO,KEY8
          MATCH     DLOBAURN,KEY8         * Check if same U/R
          GOTO      OCLI9999 IF NOT EQUAL
.
          MATCH     LOBADATE,LVIDATE
          GOTO      OCLI9999 IF NOT EQUAL
.
          MATCH     LOBAOUTN,LVIBILL     * Check if same Admission
          GOTO      OCLI1000 IF NOT EQUAL
.
          MOVE      LOBAOUTN,KEY8
          CALL      RDLGBOB1
.
          MOVE      LOBATIME,VISTTIME
          MOVE      LOBACOMP,LACLAIM
          IF        LOBASTAT=4
            MOVE      "Attended",VSTATUS
          ENDIF
          PACK      KEY18,LOBASITE,LOBACLIN,LOBADAY,LOBASTRT,SP70
          CALL      RDLGMAA1
          BRANCH    OVRCD,OCLI9999
          PACK      VISTLOC,LOTMALTY,SP70
.
          PACK      KEY12,LOMASITE,LOMATYP,SP70
          CALL      RDLGCTY1
          IF        OVRCD = 1
            UNPACK    SP70,LOCTSITE,LOCTCTYP,LOCTGRP,LOCTDESC,LOCTXBOK,LOCTXATT
            UNPACK    SP70,LOCTBKT,LOCTATT,LOCTACT
          ENDIF
.
OCLI9999  RETURN
.
.------------------------------------------------------------
. Get Last Transfer Record
.------------------------------------------------------------
GETLTR00  PACK      KEY30,LVIBILL,Z70
          CALL      RSLGTRN2
          CALL      RPLGTRN2
          BRANCH    OVRCD,GETLTR10
          MATCH     LVIBILL,LTADMN
          GOTO      GETLTR10 IF NOT EQUAL
          MOVE      LTADOCT,LVIDOCT          * Clinician
.
          MATCH     "D",LTMOVE
          GOTO      GETLTR99 IF NOT EQUAL
.
          MOVE      LTWARD,LAWARD
          MOVE      LTBED,LABED
          PACK      VISTLOC,LTWARD,SP70
          GOTO      GETLTR99
.
GETLTR10  MOVE      LADOCTA,LVIDOCT
          MOVE      LADTYPE,LTATYPE
.
GETLTR99  RETURN
.
.------------------------------------------------------------
. NOMORE00 - Display "No more visits" at end of visit list by type
.------------------------------------------------------------
NOMORE00  COMPARE    ONE,SHOWFLAG
          GOTO       NOMORE99 IF NOT EQUAL
.
          WRITE      HTMLFILE;"<tr bgcolor=#FF0000>":
                              "<td colspan=8 align=center>":
                              "No more visits</td>"
.
NOMORE99  RETURN
.
.------------------------------------------------------------
. Output Patient Enquiry Page
.------------------------------------------------------------
LEGPAT00  MOVE      ZERO,MASTFLAG
          MOVE      ZERO,MISSFLAG
          MOVE      ZERO,RESPFLAG
          MOVE      SP70,DOCTCODE
          CALL      IBACLOCK
          CALL      CLPATMAS
          CALL      CLPMSPX2
.
          REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
          REP       "+ ",LEGADMNO
.
          MOVE      URNUMBER,KEY8
          CALL      RDMAST1
          MOVE      OVRCD,MASTFLAG
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          IF        PSTAT=1 | PSTAT=2
            MOVE      PURNO,SAVEURNO
            MOVE      PPSNAME,URNUMBER
            MOVE      ONE,MRGEFLAG
            GOTO      LEGPAT00
          ENDIF
.
          IF        MASTFLAG=1
            MOVE      "Can't find patient master details",WEBTITLE
            CALL      WEBERR00
            GOTO      LEGPAT99
          ENDIF
.
LEGPAT05  CALL      PATNAA00                * format name without bold tags
.                                                 * read nutrition file
.    Webtplaf must be read here to get correct description for WEBTITLE
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
          PACK      KEY13,PRGID,KEY2,TEMPLATE,SP70
          CALL      RDWBTP1
          CLEAR     WEBTITLE
          APPEND    WBTPDES,WEBTITLE
          APPEND    PATFNAME,WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,LEGPAT99
.
          CALL      WEBBOD00   * Output Templated Body of HTML
          IF        MRGEFLAG=1
            CALL      MRGALT00
          ENDIF
.
LEGPAT90  CALL      WEBEND00   * Output End of Web Page
.
LEGPAT99  RETURN
.
.------------------------------------------------------------
. Legacy Patient Enquiry Page
.------------------------------------------------------------
GETPAT00  MOVE      ZERO,MASTFLAG
          MOVE      ZERO,MISSFLAG
          MOVE      ZERO,RESPFLAG
          MOVE      ZERO,DSCHFLAG
          MOVE      ZERO,PMVXFLAG
          MOVE      SP70,DOCTCODE
          CALL      IBACLOCK
          CALL      CLPATMAS
          CALL      CLPMSPX2
.
          MOVE      TEMPLATE,FORM3
          MOVE      FORM3,TEMPLATE
          REP       " 0",TEMPLATE
.
          REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
          REP       "+ ",LEGADMNO
.
          MOVE      URNUMBER,KEY8
          CALL      RDMAST1
          MOVE      OVRCD,MASTFLAG
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          IF        PSTAT=1 | PSTAT=2
            MOVE      PURNO,SAVEURNO
            MOVE      PPSNAME,URNUMBER
            MOVE      ONE,MRGEFLAG
            GOTO      GETPAT00
          ENDIF
.
          IF        MASTFLAG=1
            GOTO      GETPAT95
          ENDIF
.
          MOVE      LEGADMNO,KEY8
          CALL      RDLGMIS1
          MOVE      OVRCD,MISSFLAG
.         CALL      RDLGDAD1
          IF        OVRCD=1
            MOVE      SP70,PTDAADTS
            MOVE      SP70,PTDADCTS
          ENDIF
          CALL      RDLGVIS1
.
          MOVE      ONE,PMVXFLAG
.
          MATCH     "003",TEMPLATE
          GOTO      GETPAT20 IF EQUAL
          MATCH     "004",TEMPLATE
          GOTO      GETPAT20 IF EQUAL
          MATCH     "005",TEMPLATE
          GOTO      GETPAT20 IF EQUAL
          MATCH     "006",TEMPLATE
          GOTO      GETPAT20 IF EQUAL
          MATCH     "007",TEMPLATE
          IF        @EQUAL
            PACK      KEY8,INVOICEN,SP70
            CALL      RDLGINV1
            IF        OVRCD=1
              PACK      LPINVNO,SP70
              MOVE      ZEROVISN,LPINVADM
            ENDIF
          ENDIF
.
          GOTO      GETPAT20 IF EQUAL
.
          MOVE      LEGADMNO,KEY8
          CALL      RDLGDSC1
          MOVE      OVRCD,DSCHFLAG
          BRANCH    OVRCD,GETPAT95
.
          MOVE      LDDATE,ICDRDDTE   * Needed for RDPTICD1 & RDPTICO1 reads
          MOVE      ONE,RESPFLAG
          MOVE      ONE,BRECFLAG
.
GETPAT20  CALL      PATNAA00                * format name without bold tags
.
          PACK      KEY8,LEGADMNO,SP70
          CALL      RDLGWCO1
          IF        OVRCD=1
            CALL      LCLWCO00
          ENDIF
.
          PACK      KEY8,LEGADMNO,SP70
          CALL      RDLGWVE1
          IF        OVRCD=1
            CALL      LCLWVE00
          ENDIF
.
          PACK      KEY8,LEGADMNO,SP70
          CALL      RDLGWMA1
          IF        OVRCD=1
            CALL      LCLWMA00
          ENDIF
.
GETPAT90  MOVE      ZERO,EXIT               * no errors
          GOTO      GETPAT99
.
GETPAT95  MOVE      ONE,EXIT                * error
GETPAT99  RETURN
.
.------------------------------------------------------------------------------
. Option 1 - DRG Information and Medical Record Options
. First up read DRG File if OVRCD set flag and continue
. This is done here as not to disrupt the Main Routine
.------------------------------------------------------------------------------
DRGS0000  MOVE      SP70,KEY3
          CALL      LGDRG000       * Get Grouper Version - returns it in KEY3
          COMPARE   ZERO,EPSCD001
          IF        @EQUAL
            MOVE      ONE,EPSCD001   * Set to default to 1 if blank
          ENDIF
          PACK      KEY13,LEGADMNO,EPSCD001,KEY3,SP70
          CALL      RDLGPGR1   * Read Grouper Details
          IF        OVRCD=1
            PACK      KEY10,LEGADMNO,EPSCD001
            PACK      KEY13,KEY10,SP70
            CALL      RSLGPGR1   * Read Grouper Details
            CALL      RKLGPGR1
            IF        OVRCD=0
              PACK      DIM10,LPTPGADM,LPTPGEPN
              MATCH     KEY10,DIM10
              GOTO      DRGS0010 IF EQUAL
            ENDIF
            CALL      CLRGRP00
          ENDIF
.
.  Now start normal branch on flditmno section
.
DRGS0010  BRANCH    FLDITMNO,DRGS0100,DRGS0200,DRGS0300,DRGS0400,DRGS0500:
                             DRGS0600,DRGS0700,DRGS0800,DRGS0900,DRGS1000:
                             DRGS1100,DRGS1200,DRGS1300,DRGS1400,DRGS1500:
                             DRGS1600,DRGS1700,DRGS1800,DRGS1900,DRGS2000:
                             DRGS2100,DRGS2200,DRGS2300,DRGS2400,DRGS2500:
                             DRGS2600,DRGS2700,DRGS2800,DRGS2900
          GOTO      DRGS9999
.
DRGS0100  CALL      SELEPS00           * Selection List of Episodes
          GOTO      DRGS9999
.
DRGS0200                               * EMPTY !!!
          GOTO      DRGS9999
.
DRGS0300  MATCH     SP70,LACODDTE
          IF        @EQUAL
            WRITE     HTMLFILE;"0";    * Coding Incomplete
          ELSE
            WRITE     HTMLFILE;"1";    * Coding Complete
          ENDIF
          GOTO      DRGS9999
.
DRGS0400                               * EMPTY !!!
          GOTO      DRGS9999
.
DRGS0500  WRITE     HTMLFILE;DLPTPGAD;  * Admissno No
          GOTO      DRGS9999
.
DRGS0600  WRITE     HTMLFILE;DLPTPGEP;  * Episode No
          GOTO      DRGS9999
.
DRGS0700  WRITE     HTMLFILE;LPTPGGPV;  * Grouper Version
          GOTO      DRGS9999
.
DRGS0800  MOVE      LPTPGNDR,DDRGCODE   * National DRG
          CALL      DISDRG00            * DRG Details
          GOTO      DRGS9999
.
DRGS0900  WRITE     HTMLFILE;LPTPGNMD;  * National MDC
          GOTO      DRGS9999
.
DRGS1000  WRITE     HTMLFILE;LPTPGNWG;  * National Weight
          GOTO      DRGS9999
.
DRGS1100  WRITE     HTMLFILE;LPTPGNAL;  * National average LOS
          GOTO      DRGS9999
.
DRGS1200  MOVE      LPTPGSDR,DDRGCODE   * State DRG
          CALL      DISDRG00            * DRG Details
          GOTO      DRGS9999
.
DRGS1300  WRITE     HTMLFILE;LPTPGSMD;  * State MDC
          GOTO      DRGS9999
.
DRGS1400  WRITE     HTMLFILE;LPTPGSWG;  * State Weight
          GOTO      DRGS9999
.
DRGS1500  WRITE     HTMLFILE;LPTPGSAL;  * State average LOS
          GOTO      DRGS9999
.
DRGS1600  MOVE      LPTPGLDR,DDRGCODE   * Local DRG
          CALL      DISDRG00            * DRG Details
          GOTO      DRGS9999
.
DRGS1700  WRITE     HTMLFILE;LPTPGLMD;  * Local MDC
          GOTO      DRGS9999
.
DRGS1800  WRITE     HTMLFILE;LPTPGLWG;  * Local Weight
          GOTO      DRGS9999
.
DRGS1900  WRITE     HTMLFILE;LPTPGLAL;  * Local average LOS
          GOTO      DRGS9999
.
DRGS2000  WRITE     HTMLFILE;LPTPGPCC;  * Patient CC class level
          GOTO      DRGS9999
.
DRGS2100  WRITE     HTMLFILE;LPTPGWFI;  * WIES value fixed
          GOTO      DRGS9999
.
DRGS2200  WRITE     HTMLFILE;LPTPGWVA;  * WIES value variable
          GOTO      DRGS9999
.
DRGS2300  WRITE     HTMLFILE;LPTPGWTO;  * WIES value total
          GOTO      DRGS9999
.
DRGS2400  WRITE     HTMLFILE;LPTPGWWU;  * WIES weight used
          GOTO      DRGS9999
.
DRGS2500  WRITE     HTMLFILE;LPTPGWWD;  * WIES weight description
          GOTO      DRGS9999
.
DRGS2600  CALL      WRDDIS00      * Output Discharge Ward Description
          GOTO      DRGS9999
.
DRGS2700  WRITE     HTMLFILE;LPTPGNDR;  * National DRG
          GOTO      DRGS9999
.
DRGS2800  WRITE     HTMLFILE;LPTPGSDR;  * State DRG
          GOTO      DRGS9999
.
DRGS2900  WRITE     HTMLFILE;LPTPGLDR;  * Local DRG
          GOTO      DRGS9999
.
DRGS9999  RETURN
.
.------------------------------------------------------------------------------
.                     DRG Details
.      0.Description, 1.Code, 2.Low Trim, 3.High Trim
.      INPUT PARAMETER : DDRGCODE - DRG Code
.------------------------------------------------------------------------------
DISDRG00  UNPACK    DIM127,DIM1,KEY1,DIM127
          MOVE      KEY1,F1
.
....      MOVE      DDRGCODE,KEY4                                     *C-168959
          PACK      KEY7,DDRGCODE,ZED10
          CALL      RSLGDGW1
DISDRG05  CALL      RPLGDGW1
          BRANCH    OVRCD,DISDRG90
.
          MATCH     LPTDWDRG,DDRGCODE
          GOTO      DISDRG90 IF NOT EQUAL
.
          BRANCH    F1,DISDRG10,DISDRG20,DISDRG30
          WRITE     HTMLFILE;LPTDWDES; * Description
          GOTO      DISDRG99
.
DISDRG10  WRITE     HTMLFILE;LPTDWDRG;  * Code
          GOTO      DISDRG99
.
DISDRG20  WRITE     HTMLFILE;LPTDWLOW;  * Low Trim
          GOTO      DISDRG99
.
DISDRG30  WRITE     HTMLFILE;LPTDWHIG;  * High Trim
          GOTO      DISDRG99
.
DISDRG90  IF        F1 = ZERO
            WRITE     HTMLFILE;"DRG description not found";
          ENDIF
          IF        F1 = ONE
            WRITE     HTMLFILE;"DRG code not found";
          ENDIF
          IF        F1 > ONE
            WRITE     HTMLFILE;"DRG weightings record not found";
          ENDIF
          GOTO      DISDRG99
.
DISDRG99  RETURN
.
.------------------------------------------------------------------------------
.                 ****** WRDDIS00 ******
. Output Ward description
.------------------------------------------------------------------------------
WRDDIS00  CALL      GETLTR00
          PACK      KEY6,LAWARD,SP70
          CALL      RDLGWRD1
          BRANCH    OVRCD,WRDDIS90
.
          WRITE     HTMLFILE;LWBDESC;    * description
          GOTO      WRDDIS99
.
WRDDIS90  WRITE     HTMLFILE;"Invalid Ward Code";
.
WRDDIS99  RETURN
.
.------------------------------------------------------------------------------
.  Outputs a selection list of Episode numbers for a given Admission
.                   ******** SELEPS00 ********
.------------------------------------------------------------------------------
SELEPS00  MOVE      ZERO,EPISODNO  * Episode No - save variable
          MOVE      ZERO,LECDFLAG  * Disease Episode Flag
          MOVE      ZERO,LECOFLAG  * Operation Episode Flag
.
          PACK      KEY12,LEGADMNO,SP70   * Check Disease Coding file
          CALL      RSLGECD1
SELEPS10  CALL      RKLGECD1
          IF        OVRCD=1
            MOVE      ONE,LECDFLAG
            GOTO      SELEPS20
          ENDIF
.
          MATCH     LEGADMNO,DLPTEDAD    * Check for Admission No
          IF        !@EQUAL
            MOVE      ONE,LECDFLAG
            GOTO      SELEPS20
          ENDIF
.
          MATCH     SP70,LPTEDCOD
          IF        !@EQUAL
            IF      EPISODNO >= LPTEDEPN
              GOTO     SELEPS20
            ENDIF
            MOVE      LPTEDEPN,EPISODNO  * Save Found Episode
            COMPARE   LPTEDEPN,EPSCD001
            IF        @EQUAL
              WRITE     HTMLFILE;"<option value=#"",LPTEDEPN,"#" selected>":
                                 LPTEDEPN,"</option>"
            ELSE
              WRITE     HTMLFILE;"<option value=#"",LPTEDEPN,"#">":
                                 LPTEDEPN,"</option>"
            ENDIF
          ENDIF
.
SELEPS20  PACK      KEY12,LEGADMNO,SP70   * Check Operations Coding File
          CALL      RSLGECO1
SELEPS21  CALL      RKLGECO1
          IF        OVRCD=1
            MOVE      ONE,LECOFLAG
            GOTO      SELEPS90
          ENDIF
.
          MATCH     LEGADMNO,DLPTEOAD    * Check for Admission No
          IF        !@EQUAL
            MOVE      ONE,LECOFLAG
            GOTO      SELEPS90
          ENDIF
.
          MATCH     SP70,LPTEOCOD
          IF        !@EQUAL
            IF      EPISODNO >= LPTEOEPN
              GOTO     SELEPS90
            ENDIF
            MOVE      LPTEOEPN,EPISODNO  * Only need First Episode found
            COMPARE   LPTEOEPN,EPSCD001
            IF        @EQUAL
              WRITE     HTMLFILE;"<option value=#"",LPTEOEPN,"#" selected>":
                                 LPTEOEPN,"</option>"
            ELSE
              WRITE     HTMLFILE;"<option value=#"",LPTEOEPN,"#">":
                                 LPTEOEPN,"</option>"
            ENDIF
          ENDIF
.
SELEPS90  IF        LECDFLAG=1
            IF        LECOFLAG=1
              GOTO      SELEPS99
            ENDIF
            GOTO    SELEPS21
          ELSE
            GOTO    SELEPS10
          ENDIF
.
SELEPS99  RETURN
.
.------------------------------------------------------------------------------
.                        ****** CLRGRP00 ******
.------------------------------------------------------------------------------
CLRGRP00  MOVE      SP70,DLPTPGAD   * Admission no
          MOVE      SP70,DLPTPGEP   * Episode no
          MOVE      SP70,LPTPGGPV   * Grouper version
          MOVE      SP70,LPTPGNDR   * National DRG
          MOVE      SP70,LPTPGNMD   * National MDC
          MOVE      ZERO,LPTPGNWG   * National weight
          MOVE      ZERO,LPTPGNAL   * National average LOS
          MOVE      SP70,LPTPGSDR   * State DRG
          MOVE      SP70,LPTPGSMD   * State MDC
          MOVE      ZERO,LPTPGSWG   * State weight
          MOVE      ZERO,LPTPGSAL   * State average LOS
          MOVE      SP70,LPTPGLDR   * Local DRG
          MOVE      SP70,LPTPGLMD   * Local MDC
          MOVE      ZERO,LPTPGLWG   * Local weight
          MOVE      ZERO,LPTPGLAL   * Local average LOS
          MOVE      SP70,LPTPGPCC   * Patient CC class level
          MOVE      ZERO,LPTPGWFI   * WIES value fixed
          MOVE      ZERO,LPTPGWVA   * WIES value variable
          MOVE      ZERO,LPTPGWTO   * WIES value total
          MOVE      ZERO,LPTPGWWU   * WIES weight used
          MOVE      SP70,LPTPGWWD   * WIES weight description
          MOVE      SP70,LPTPGSPA   * Spare variable
.
CLRGRP99  RETURN
.
.------------------------------------------------------------
. Output Operation ICD9/10 Coding Table Details
.------------------------------------------------------------
OPRTAB00  WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell colspan=5 align=center>";
          CALL      ICDTYP00    * Determine Default ICD9 or 10 and Show Heading
          WRITE     HTMLFILE;"Procedure Coding";
          WRITE     HTMLFILE;"</td></tr>"
          WRITE     HTMLFILE;"<tr>":
                    "<td class=HeadingCell width=15%>Procedure Code</td>":
                    "<td class=HeadingCell>Description</td>":
                    "<td class=HeadingCell align=center>Date</td>":
                    "<td class=HeadingCell align=center>Start Time</td>":
                    "<td class=HeadingCell align=center>End Time</td></tr>"
.
          MOVE      ZERO,VISCOUNT
          COMPARE   ZERO,EPSCD001
          IF        @EQUAL
            MOVE      ONE,EPSCD001
            PACK      KEY12,LEGADMNO,EPSCD001
          ELSE
            PACK      KEY12,LEGADMNO,EPSCD001
          ENDIF
          CALL      RSLGECO1
OPRTAB10  CALL      RKLGECO1
          BRANCH    OVRCD,OPRTAB90
.
          MATCH     LEGADMNO,DLPTEOAD
          GOTO      OPRTAB90 IF NOT EQUAL
.
          COMPARE   EPSCD001,LPTEOEPN
          GOTO      OPRTAB90 IF NOT EQUAL
.
          ADD       ONE,VISCOUNT
          CALL      GETICO00
          BRANCH    OVRCD,OPRTAB10
.
OPRTAB20  CLEAR     DISNAM70
          PACK      NAME70,ODESC,SP70
          CALL      LONGNM00
          APPEND    SP70,DISNAM70
          RESET     DISNAM70
          MOVE      DISNAM70,ODESC
          STRIP     ODESC
.
          UNPACK    LPTEODAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      TRANDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          WRITE     HTMLFILE;"<tr><td>",LPTEOTYP,SP1,OPCODE,"</a></td>":
                             "<td>",ODESC,"</td>":
                             "<td align=center>",TRANDATE,"</td>":
                             "<td align=center>",LPTEOSTT,"</td>":
                             "<td align=center>",LPTEOEDT,"</td></tr>"
          GOTO      OPRTAB10
.
OPRTAB90  IF        VISCOUNT=0
            WRITE     HTMLFILE;"<tr><td align=center colspan=5>":
                               "<b>No Procedure Coding Records Found</b>":
                               "</td></tr>"
          ENDIF
.
OPRTAB99  RETURN
.
.------------------------------------------------------------
. Output Disease ICD9/10 Coding Table Details
.------------------------------------------------------------
DISTAB00  WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell colspan=3 align=center>";
.
          CALL      ICDTYP00     * Determine if ICD9 or ICD10 and Add Heading
          WRITE     HTMLFILE;"Diagnosis Coding";
.
          WRITE     HTMLFILE;"</td></tr>"
.
          WRITE     HTMLFILE;"<tr>":
                    "<td class=HeadingCell width=15%>Diagnosis Code</td>":
                    "<td class=HeadingCell>Description</td></tr>"
.
          MOVE      ZERO,VISCOUNT
          COMPARE   ZERO,EPSCD001
          IF        @EQUAL
            MOVE      ONE,EPSCD001
            PACK      KEY12,LEGADMNO,EPSCD001
          ELSE
            PACK      KEY12,LEGADMNO,EPSCD001
          ENDIF
          CALL      RSLGECD1
DISTAB30  CALL      RKLGECD1
          BRANCH    OVRCD,DISTAB90
.
          MATCH     LEGADMNO,DLPTEDAD
          GOTO      DISTAB90 IF NOT EQUAL
.
          COMPARE   EPSCD001,LPTEDEPN
          GOTO      DISTAB90 IF NOT EQUAL
.
          ADD       ONE,VISCOUNT
          CALL      GETICD00
          BRANCH    OVRCD,DISTAB30
.
DISTAB35  CLEAR     DISNAM70
          PACK      NAME70,DDESC,SP70
          CALL      LONGNM00
          APPEND    SP70,DISNAM70
          RESET     DISNAM70
          MOVE      DISNAM70,DDESC
          STRIP     DDESC
.
          WRITE     HTMLFILE;"<tr><td>",LPTEDTYP,SP1,DPCODE,"</td>":
                             "<td>",DDESC,"</td></tr>"
.
          GOTO      DISTAB30
.
DISTAB90  IF        VISCOUNT=0
            WRITE     HTMLFILE;"<tr><td align=center colspan=2>":
                               "<b>No Diagnosis Coding Records Found</td></tr>"
          ENDIF
.
DISTAB99  RETURN
.
.------------------------------------------------------------
. Get Coding Type
.------------------------------------------------------------
ICDTYP00  MOVE      ICDRDDTE,KEY8
          MATCH     SP70,KEY8
          IF        @EQUAL
            PACK      KEY8,CCC,CYY,CMM,CDD
            REP       " 0",KEY8
          ENDIF
.
          READ      CONTROLF,EIGHTY8;*59,PTCNI10D
          MATCH     PTCNI10D,KEY8
          IF        !@LESS
            MOVE       "10",CODTYP          * Using ICD10 codes
          ELSE
            MOVE       "9",CODTYP           * Using ICD9 codes
          ENDIF
.
          IF        CODTYP=9
            WRITE     HTMLFILE;"ICD9 Coding ";
          ELSE
            WRITE     HTMLFILE;"ICD10 Coding ";
          ENDIF
.
ICDTYP99  RETURN
.
.------------------------------------------------------------
. Get ICD Operation Details
.------------------------------------------------------------
GETICO00  MOVE      LPTEOCOD,KEY9
          CALL      RDPTICO1             * Read ICD Operation record
.
GETICO99  RETURN
.
.------------------------------------------------------------
. Get ICD Operation Details
.------------------------------------------------------------
GETICD00  MOVE      LPTEDCOD,KEY9
          CALL      RDPTICD1             * Read ICD Disease record
.
GETICD99  RETURN
.
.-------------------------------------------------
. Format Long Description
.-------------------------------------------------
LONGNM00  CMATCH    SP1,NAME70               * Remove Leading Blanks
          IF        @EQUAL
            BUMP      NAME70
            GOTO      LONGNM00 IF NOT EOS
            GOTO      LONGNM99               * entire name if blank
          ENDIF
          PACK      KEY70,NAME70,SP30        * reset form pointer
          MOVE      KEY70,NAME70
.
          SCAN      SP1,NAME70             * Locate the blank
          GOTO      LONGNM20 IF NOT EQUAL
          BUMP      NAME70,-1              * go back 1 to end of name
          MOVEFPTR  NAME70,CCITEM          * another handly form field
          RESET     NAME70                 * reset the form pointer
          SETLPTR   NAME70,CCITEM          * set logical length to end of name
.
.         Save this name
.
          UNPACK    NAME70,KEY1,KEY80
          REP       DOWNCASE,KEY80
          APPEND    KEY1,DISNAM70
          STRIP     KEY80
          APPEND    KEY80,DISNAM70
          APPEND    SP1,DISNAM70
.
.         Check for any more names
.
          SETLPTR   NAME70,70              * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     NAME70,CCITEM          * reset to this point
          PACK      KEY70,NAME70,SP70      * reset form pointer
          MOVE      KEY70,NAME70
          GOTO      LONGNM00
.
.         Rest of the string is one name
.
LONGNM20  UNPACK    NAME70,KEY1,KEY80
          REP       DOWNCASE,KEY80
          APPEND    KEY1,DISNAM70
          STRIP     KEY80
          APPEND    KEY80,DISNAM70
          APPEND    SP1,DISNAM70
.
LONGNM99  RETURN
.------------------------------------------------------------
. Legacy AAE Patient Enquiry Page
.------------------------------------------------------------
AAEPAT00  MOVE      ZERO,MASTFLAG
          MOVE      SP70,DOCTCODE
          CALL      IBACLOCK
          CALL      CLPATMAS
          CALL      CLPMSPX2
.
          REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
          REP       "+ ",LEGADMNO
.
          MOVE      URNUMBER,KEY8
          CALL      RDMAST1
          MOVE      OVRCD,MASTFLAG
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          IF        PSTAT=1 | PSTAT=2
            MOVE      PURNO,SAVEURNO
            MOVE      PPSNAME,URNUMBER
            MOVE      ONE,MRGEFLAG
            GOTO      AAEPAT00
          ENDIF
.
          IF        MASTFLAG=1
            GOTO      AAEPAT95
          ENDIF
.
          MOVE      LEGADMNO,KEY8
          CALL      RDLGDEA1
          BRANCH    OVRCD,AAEPAT95
.
          CALL      RDLGDIS1
          BRANCH    OVRCD,AAEPAT95
.
AAEPAT05  CALL      PATNAA00                * format name without bold tags
.
AAEPAT90  MOVE      ZERO,EXIT               * no errors
          GOTO      AAEPAT99
.
AAEPAT95  MOVE      ONE,EXIT                * error
AAEPAT99  RETURN
.
.*******************************************************************************
.                         Show Booking Details
.*******************************************************************************
SHWBOK00  MOVE      LEGADMNO,KEY8
          CALL      RDLGVIS1
          BRANCH    OVRCD,SHWBOK95
.
          COMPARE   "2",LVITYPE
          GOTO      SHWBOK95 IF NOT EQUAL
.
          CALL      OUTVIS00
          BRANCH    EXIT,SHWBOK99
.
SHWBOK30  MOVE      CASEKEYS,KEY28
          CALL      RDLGBOA1
          BRANCH    OVRCD,SHWBOK95
.
          MOVE      CASEKEYS,SESSKEYS
          CALL      SESDET00
          BRANCH    EXIT,SHWBOK95
.
          MOVE      LOBAOUTN,KEY8
          CALL      RDLGBOB1
          BRANCH    OVRCD,SHWBOK95
.
          CALL      CLPATMAS
          CALL      CLPMSPX2
.
          PACK      KEY8,LOBAURNO
          CALL      RDMAST1
          BRANCH    OVRCD,SHWBOK95
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          CALL      PATNAA00
.
          MOVE      ZERO,EXIT
          GOTO      SHWBOK99
.
SHWBOK95  MOVE      ONE,EXIT  
          GOTO      SHWBOK99
.
SHWBOK99  RETURN
.
.*******************************************************************************
.                Get Booking Details Based on Visit File Details
.*******************************************************************************
OUTVIS00  MOVE      SP70,CASEKEYS
.
          MOVE      LEGADMNO,KEY8
          CALL      RDLGBOB1
          BRANCH    OVRCD,OUTVIS90
.
          PACK      CASEKEYS,LVISITE,LVIDOCT,LVIDATE,SP70
          PACK      KEY28,LVISITE,LVIDOCT,LVIDATE,SP70
          PACK      KEY20,LVISITE,LVIDOCT,LVIDATE,SP70
          CALL      RSLGBOA1
OUTVIS10  CALL      RKLGBOA1
          BRANCH    OVRCD,OUTVIS90
.
          PACK      CASEKEYS,LOBASITE,LOBACLIN,LOBADATE,LOBASTRT,LOBASLOT,SP70
          MATCH     KEY20,CASEKEYS
          GOTO      OUTVIS90 IF NOT EQUAL
.
          MATCH     LOBAOUTN,LVIBILL         * Set up billing number
          GOTO      OUTVIS10 IF NOT EQUAL
          MOVE      ZERO,EXIT
          GOTO      OUTVIS99
.
OUTVIS90  MOVE      ONE,EXIT
          GOTO      OUTVIS99
.
OUTVIS99  RETURN
.
.*******************************************************************************
.                    Get Session Details for SESSKEYS
.*******************************************************************************
SESDET00  PACK      KEY18,LOBASITE,LOBACLIN,LOBADAY,LOBASTRT,SP70
          CALL      RDLGMAA1
          BRANCH    OVRCD,SESDET90
.
          PACK      KEY12,LOMASITE,LOMATYP,SP70
          CALL      RDLGCTY1
          BRANCH    OVRCD,SESDET90
.
          PACK      KEY12,LOBASITE,LOBACLIN,SP70
          CALL      RDLGCLI1
          BRANCH    OVRCD,SESDET99
.
          MOVE      LOCADESC,DOCFNAME
.
          MATCH     SP70,LOCADESC
          IF        @EQUAL
            MATCH     SP70,LOCADOCT
            IF        !@EQUAL
              MOVE      LOCADOCT,KEY6
              CALL      RDLGDOC1
              IF        OVRCD=1
                MOVE      LOCADOCT,DOCFNAME
              ELSE
                MOVE      LOCADOCT,DOCTCODE
                CALL      LDCNAM00
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY6,LOBASITE,SP70
          CALL      RDLGSIT1
          BRANCH    OVRCD,SESDET90
.
          MOVE      ZERO,EXIT
          GOTO      SESDET99
.
SESDET90  MOVE      ONE,EXIT
.
SESDET99  RETURN
.
.------------------------------------------------------------
. Table of Patient Invoice Details
. TOTLFLAG=1 - calculate total only and loop through the file
.              again to display screen.
.------------------------------------------------------------
PINVCI00  MATCH     SP70,URNUMBER
          GOTO      PINVCI99 IF EQUAL
.
          COMPARE   ONE,TOTLFLAG
          GOTO      PINVCI10 IF NOT EQUAL
.
          MOVE      ZERO,SUMINV
          MOVE      ZERO,SUMPAID
          MOVE      ZERO,SUMJR
          MOVE      ZERO,SUMOUTS
          GOTO      PINVCI20
.
PINVCI10  IF        NORECORD=0
            MOVE    "10",NORECORD           * Numb of records=0 Default to 10
          ENDIF
          MOVE      ZERO,RECNUMB            * Init Number of records Read
          MOVE      ZERO,ROWCOUNT           * Init number of records output
.
PINVCI20  CALL      PRPAT000                * Print Patient invoices
.
PINVCI40  BRANCH    TOTLFLAG,PINVCI90        * calculate total only
.
          IF        ROWCOUNT>0
            WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td>Total</td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td align=right><b>",SUMINV,"</b></td>":
                               "<td align=right><b>",SUMPAID,"</b></td>":
                               "<td align=right><b>",SUMJR,"</b></td>":
                               "<td align=right><b>",SUMOUTS,"</b></td></tr>"
          ENDIF
          GOTO      PINVCI99
.
PINVCI90  IF        TOTLFLAG=1
            MOVE      ZERO,TOTLFLAG
            GOTO      PINVCI00
          ENDIF
.
PINVCI99  RETURN
.
.------------------------------------------------------------
. Print Patient invoices
.------------------------------------------------------------
PRPAT000  PACK      KEY32,URNUMBER,Z70
          CALL      RSLGINV5
PRPAT100  CALL      RPLGINV5
          BRANCH    OVRCD,PRPAT999
.
          MOVE      LPINVUR,KEY8
          MATCH     KEY8,URNUMBER
          GOTO      PRPAT999 IF NOT EQUAL
.
          PACK      LEGADMNO,LEGADMNO,SP70
          MATCH     SP70,LEGADMNO
          GOTO      PRPAT150 IF EQUAL
.
          MOVE      LPINVADM,KEY8
          MATCH     KEY8,LEGADMNO
          GOTO      PRPAT100 IF NOT EQUAL
.
PRPAT150  BRANCH    TOTLFLAG,PRPAT200     * calculate total only
.
          PACK      NEXTVKEY,URNUMBER,LPINVDTE,LPINVADM,LPINVNO,SP70
          MATCH     SP70,PREVVKEY
          IF        @EQUAL
            PACK      PREVVKEY,URNUMBER,LPINVDTE,LPINVADM,LPINVNO,SP70
          ENDIF
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      PRPAT100
          ENDIF
.
          MOVE      PFUNDH,AFUNDH
          MOVE      LPINVADM,KEY8
          CALL      RDLGVIS1
          BRANCH    OVRCD,PRPAT100
.
          IF        PVITYPE=3
            MOVE      LPINVADM,KEY8
            CALL      RDLGMIS1
          ENDIF
.
          CALL      HFUND000             * Get health fund
.
PRPAT200  MOVE      LPINVPAT,TOTPAID     * Total Paid
          ADD       LPINVHFD,TOTPAID
          ADD       LPINVOTH,TOTPAID
          ADD       LPTIVPCS,TOTPAID
.
          MOVE      LPINVAMT,TOTOUTS      * Total Outstanding
          ADD       TOTPAID,TOTOUTS
          ADD       LPINVJOU,TOTOUTS
          ADD       LPINGSTJ,TOTOUTS
.
          COMPARE   ONE,TOTLFLAG
          GOTO      PRPAT300 IF NOT EQUAL
.
.         Accumulate totals
.
          ADD       LPINVAMT,SUMINV
          ADD       TOTPAID,SUMPAID
          ADD       LPINVJOU,SUMJR
          ADD       LPINGSTJ,SUMJR
          ADD       TOTOUTS,SUMOUTS
          GOTO      PRPAT100
.
PRPAT300  MOVE      "CL",KEY2
          PACK      KEY5,KEY2,LPINVCLM
          CALL      RDLGCOD1
          IF        OVRCD=0
            MOVE      LTDESC,CLMDESC
          ENDIF
.
          MOVE      SP70,VISTDESC
          LOAD      VISTDESC,LPINVSYS,VISTEMER,VISTOUTP,VISTINPT
.
.         Visit type - 1 A&E, 2 OUT, 3 INP, 4 PRIV PRAC, 5 DEBTORS
.
          MOVE      ZERO,VISITTYP
          LOAD      VISITTYP,LPINVSYS,ONE,TWO,THREE
.
          UNPACK    LVIDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          WRITE     HTMLFILE;"<tr><td align=left>",LPINVADM,"</td>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td><img src=../images/InvoiceIcon.gif ":
                             " class=ListIcon onclick='linkInvoice(#"":
                             LPINVNO,"#",#"",LPINVADM,"#",#"",VISITTYP,"#",#"":
                             URNUMBER,"#")'>":
                             LPINVNO,"</td>";
.
          UNPACK    LPINVDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;"<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td>",VISTDESC,"</td>":
                             "<td>",CLMDESC,"</td>";
.
          MATCH     "&nbsp",HFNDESC
          IF        @EQUAL
            WRITE     HTMLFILE;"<td>&nbsp;</td>";
          ELSE
            WRITE     HTMLFILE;"<td>",HFNDESC,"/",HFNTABL,"</td>";
          ENDIF
.
          MOVE      LPTIVJOU,FORM8P2
          ADD       LPINGSTJ,FORM8P2
          WRITE     HTMLFILE;"<td align=right>",LPTIVAMN,"</td>":
                             "<td align=right>",TOTPAID,"</td>":
                             "<td align=right>",FORM8P2,"</td>";
.
          WRITE     HTMLFILE;"<td align=right>",TOTOUTS,"</td></tr>"
          ADD       ONE,ROWCOUNT
.
          COMPARE   NORECORD,ROWCOUNT
          GOTO      PRPAT100 IF NOT EQUAL
.
PRPAT999  RETURN
.
.------------------------------------------------------------
. Get health fund
.------------------------------------------------------------
HFUND000  MOVE      SP70,HFNDESC
          MOVE      SP70,HFNTABL
          BRANCH    LVITYPE,HFUND100,HFUND200,HFUND300
          GOTO      HFUND900
.
HFUND100  MOVE      PFUNDH,HFNDESC          * Health Fund Set
          MOVE      PFNDTB,HFNTABL
          GOTO      HFUND900
.
HFUND200  PACK      KEY8,LPINVADM,SP70     * Read OUT - Session Booking File B
          CALL      RDLGBOB1
          BRANCH    OVRCD,HFUND900
          MOVE      LOTBBFUN,HFNDESC      * Health Fund Set
          MOVE      LOTBBTBL,HFNTABL
          GOTO      HFUND900
.
HFUND300  MOVE      LAFUNDH,HFNDESC        * Master File Var (Already read)
          MOVE      LAFNDTB,HFNTABL
          GOTO      HFUND900
.
HFUND900  MATCH     SP10,HFNDESC
          IF        @EQUAL
            MOVE      "&nbsp",HFNDESC
            MOVE      "&nbsp",HFNTABL
          ENDIF
.
HFUND999  RETURN
.
.------------------------------------------------------------
.
          INC       IBAWEBCD
          INC       WEBDATCD
.
          INC       PATDO1IO/INC   * Doctor File
          INC       PATHLFIO/INC
          INC       PATLSDIO/INC
          INC       PATMI1IO/INC
          INC       PATMA1IO/INC
          INC       PATVISIO/INC   * Patients Visits File
          INC       PMSVX1IO/INC   * Visit Extension File
          INC       PATALRIO/INC
          INC       OUTSITIO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATPR1IO/INC
          INC       PMSPX2IO/INC
          INC       PATFN1IO/INC
          INC       PATWR1IO/INC
          INC       MRTMASIO/INC
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
          INC       PATRE1IO/INC
          INC       PATDGWIO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       PATHSPIO/INC
          INC       PATITMIO/INC
          INC       PATDADIO/INC
          INC       PATVADIO/INC
          INC       PATCMCIO/INC
          INC       PATASFIO/INC
          INC       PMSHCGIO/INC
          INC       PMSHCPIO/INC
          INC       APSMASIO/INC
          INC       PATIN1IO/INC
          INC       PATDSCIO/INC
          INC       PATICDIO/INC
          INC       PATICOIO/INC
          INC       PMSCCDIO/INC            * Concession Card Details
.
. Legacy Files
.
          INC       LEGCMNIO/INC
          INC       LEGCODIO/INC
          INC       LEGDOCIO/INC
          INC       LEGDTRIO/INC
          INC       LEGDSCIO/INC
          INC       LEGINVIO/INC
          INC       LEGMISIO/INC
          INC       LEGWRDIO/INC
          INC       LEGTRNIO/INC
          INC       LEGVISIO/INC
          INC       LEGWVEIO/INC
          INC       LEGWMAIO/INC
          INC       LEGWCOIO/INC
          INC       LEGDTAIO/INC
          INC       LEGDISIO/INC
          INC       LEGDEAIO/INC
          INC       LEGDTOIO/INC
          INC       LEGBOBIO/INC
          INC       LEGBOAIO/INC
          INC       LEGECDIO/INC
          INC       LEGECOIO/INC
          INC       LEGMMBIO/INC
          INC       LEGCLIIO/INC
          INC       LEGDADIO/INC
          INC       LEGCTYIO/INC
          INC       LEGMAAIO/INC
          INC       LEGPGRIO/INC
          INC       LEGDGWIO/INC
          INC       LEGSITIO/INC
.
          INC       PATITMRD
          INC       CLNHIMAS                   * Clear nhimasaf file variables
          INC       CLPMSPX2
          INC       PATMASTM
          INC       PATDOCTM
          INC       PMSHCGTM
          INC       PMSHCPTM
          INC       PATDSCTM
          INC       PMSPX2TM
          INC       PATMSXTM
          INC       PATDOCCD
          INC       CALCAGE
          INC       NAMSTRCD
          INC       DAYOFWEK
          INC       CLPATMAS
          INC       LEGCODTM
          INC       LEGDOCTM
          INC       LEGDSCTM
          INC       LEGMISTM
          INC       LEGGTDRG
          INC       LEGMASTM
          INC       LEGCLMTM
          INC       LEGBOATM
          INC       LEGCATAI

.*****************************************************************************
.*    Program      : IBAMEH51                                                *
.*    Description  : Patients by Attending Doctor report                     *
.*                                                                           *
.*    Author       : ROD    (02/12/91)                                       *
.*    Function     : To report the patients currently being treated by       *
.*                   each doctor ; list discharged MH patients for this      *
.*                   doctor whose coding hasn't been completed yet.          *
.*    Modifications:                                                         *
.*                                                                           *
.*        V12.00.02 14/08/2025  Don Nguyen    TSK 0961623                    *
.*                  Corrected code to get the last review date as it was not *
.*                  matching on the admission number in the Legal Status     *
.*                  Review History table (mehrevaf); match on MHRVADMN at    *
.*                  LOAD2400 instead.                                        *
.*        V12.00.01 22/05/2025  Alina Bhari   TSK 0955096                    *
.*                  Added alpanumeric visit number generation                *
.*                  -LOAD1000,LOAD2400                                       *
.*        V10.04.01 28/04/2014  Steve Armstrong  CAR 261630                  *
.*                  Mods for non-port based tempfile use.                    *
.*****************************************************************************
.*        V10.02.01 25/06/2011 Steve Armstrong    CAR 240722                 *
.*                  Mods to cater for changes to PATLOCFD.                   *
.*                       - LSNAME & LGNAME extended to 40 chars.             *
.*                       - PTLCGNM2 added.                                   *
.*****************************************************************************
.*        V10.01.01 10/02/2011  Jill Parkinson CAR 233088                    *
.*                  Added pmsvx1af                                           *
.*        V10.00.01 31/03/2010  Mike Laming  CAR 217575                      *
.*                  Recompiled for changes to MEHDIAFD                       *
.*****************************************************************************
.*        V9.02.00  26/10/2002  Lina Vo    SRF  22733                        *
.*                 Ported to V9                                              *
.*****************************************************************************
.*        V5.08.01  28/08/2000  Caleb Sharman                                *
.*                 Changed Health Fund variables to be 8 chars               *
.*****************************************************************************
.*        V5.07.01 28/09/1999 Andrew Peel  SRF  646014                       *
.*                 Fixed dates on report                                     *
.*       V5.07.B02 14/01/1999 Jim Stathopoulos                               *
.*                 Date fixed for Report                                     *
.*        V5.07.00 27/10/1998  Davin                                         *
.*                 Mods for 507                                              *
.*****************************************************************************
.*        V5.05.01 22/09/1997  Steve Armstrong                               *
.*                 Mods for AXIS V to be a free text description.            *
.*                 (MEHRVDIA)                                                *
.*****************************************************************************
.*        V5.04.02  10/01/1997  Steve Armstrong   MEH Changes                *
.*                  Mods to cater for new CMH MHVISTAT values                *
.*        V5.04.01  07/11/1996  Howellsy                                     *
.*                  Added Review Time & Legal Status Time.                   *
.*        V5.01.002 23/07/92  ROD                                            *
.*                              Use latest Legal status not PTMXLS           *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
.
          INC       IBASEQFD/INC
          INC       MEHVI1FD/INC
          INC       MEHDIAFD/INC
          INC       MEHLEGFD/INC
          INC       MEHREVFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       PATLOCFD/INC
          INC       PATMI1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
CMDLINE   DIM       80
COUNTER   FORM      1
CURRDATE  DIM       8
DCDATE    DIM       10
DECEASED  DIM       8
DLRDAT    DIM       10 
DOCTNAME  DIM       50
ENDDATE   DIM       8
FNAME     DIM       8
JWKYR     FORM      2
NDAYS     FORM      2
NOPATS    FORM      4
SAVCNT    FORM      1
SAVDOCT   DIM       6
SP50      INIT      "                                                   "
STRTCENT  FORM      2
STRTDAYS  FORM      3
STRTLEAP  FORM      1
STRTYR    FORM      2
THEADER   DIM       30
ZED20     INIT      "ZZZZZZZZZZZZZZZZZZZZ"
.
. Temp file vars
MEHTMPA1  IFILE     SQL, FIXED=397, KEY="U1-6,7-7,8-39,40-47"
XXDOCT    DIM       6         1       Attending Doctor
DXXCNT    DIM       1         7       Counter  (0=Not Discharged, 1=Discharged)
XXNAME    DIM       32        8       Patients Name
XXADMN    DIM       8        40       Admission number
XXURNO    DIM       8        48       U/R Number
XXWARD    DIM       3        56       Ward
XXCOND    DIM       3        59       Condition
XXLSTAT   DIM       3        62       Legal Status
XXCDATE   DIM       8        65       Committed date
XXLREV    DIM       8        73       Last review date
XXBSTAT   DIM       1        81       Bed Status
XXDIAG    DIM       45[7]    82       Diagnosis
.              Rec length   397
XXCNT     FORM      1
.
PRGID     INIT      "IBAMEH51"
PRGNAM    INIT      "Patients By Attending Doctor"
VERSION   INIT      "V12.01.00"
.
.****************************************************************************
.* ML0000  :  Mainline code                                                 *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
ML0500    CALL      CRET0000                      * create temp file
.
ML1000    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9000                   * Exit selected
.
ML5000    CALL      LOAD0000                      * load data into temp file
.
          CALL      RPRT0000                      * Print Report
          GOTO      ML0500
.
ML9000    CALL      KILL0000                      * Delete temp file
.
ML9999    CHAIN     PGM
+
.****************************************************************************
.* INIT0000  :  Initialisation                                              *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P60:24,"mehrevaf"
          OPEN      MEHREVA1,"mehrevaf"
.
          DISPLAY   *P60:24,"mehlegaf"
          OPEN      MEHLEGA1,"mehlegaf"
.
          DISPLAY   *P60:24,"mehdiaaf"
          OPEN      MEHDIAA1,"mehdiaaf"
.
          DISPLAY   *P60:24,"mehvi1af"
          OPEN      MEHVI1A2,"mehvi1af"
.
          DISPLAY   *P60:24,"patlocaf"
          OPEN      PATLOCA1,"patlocaf"
.
          DISPLAY   *P60:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P60:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P60:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P60:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P60:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          DISPLAY   *P1:24,*EL;
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD      * get todays date
          REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.****************************************************************************
.* MENU0000  :  Main Menu                                                   *
.****************************************************************************
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Attending Doctor"
.
MENU1000  KEYIN     *P1:7,"Select Option ? ":
                    *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,OPTION
.
          IF        OPTION = 0
                      MOVE   ONE,EXIT
                      GOTO   MENU9999
          ENDIF
.
          MOVE      ZERO,EXIT
          BRANCH    OPTION,MENU9999
          BEEP
          GOTO      MENU1000
.
MENU9999  RETURN
+
.****************************************************************************
.* RPRT0000  :  Print Report                                                *
.****************************************************************************
RPRT0000  DISPLAY   *P30:24,"Printing...";
.
          CALL      IBACLOCK                      * initialise a few things
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      TEN,CLNO                      * line counter
          MOVE      ZERO,NOPATS                   * Number of patients printed
          MOVE      SP6,SAVDOCT
          MOVE      ZERO,SAVCNT
          MOVE      "CURRENTLY ADMITTED PATIENTS",THEADER
.
. Now loop thru temp file and print data
.
          PACK      KEY47,SP20,SP20
          CALL      RSTEMP1
RPRT1000  CALL      RKTEMP1
          BRANCH    OVRCD,RPRT8000                * no more records
.
          COMPARE   "55",CLNO                     * need a new page?
          GOTO      RPRT2000 IF LESS
.
. set up for new page
.
          CALL      HEAD132
          CALL      PTHD0000                      * print table headings
          MOVE      TEN,CLNO
.
RPRT2000  MATCH     XXDOCT,SAVDOCT                * same doctor?
          GOTO      RPRT2050 IF EQUAL
.
          IF        XXCNT=0
            MOVE  "CURRENTLY ADMITTED PATIENTS",THEADER
          ELSE
            MOVE  "INCOMPLETELY CODED DISCHARGES",THEADER
          ENDIF
          MOVE      XXCNT,SAVCNT                  * save print type
          MOVE      XXDOCT,SAVDOCT                 * save print type
          CALL      HEAD132
          CALL      PTHD0000
          MOVE      TEN,CLNO
          GOTO      RPRT2100
.
RPRT2050  COMPARE   XXCNT,SAVCNT                  * same print type as before?
          GOTO      RPRT2100 IF EQUAL
.
          IF        XXCNT=0
            MOVE  "CURRENTLY ADMITTED PATIENTS",THEADER
          ELSE
            MOVE  "INCOMPLETELY CODED DISCHARGES",THEADER
          ENDIF
          MOVE      XXCNT,SAVCNT                  * save print type
          CALL      HEAD132
          CALL      PTHD0000
          MOVE      TEN,CLNO
.
RPRT2100  MOVE      SP10,DLRDAT
          MATCH     SP8,XXLREV
          GOTO      RPRT3000 IF EQUAL
.
          UNPACK    XXLREV,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DLRDAT               * display variable
.
RPRT3000  MOVE      SP10,DCDATE
          MATCH     SP8,XXCDATE
          GOTO      RPRT3200 IF EQUAL
          UNPACK    XXCDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DCDATE                * display variable
.
RPRT3200  PRINT     *1,"|",XXURNO,*11,"| ",XXNAME,*45,"| ",XXWARD,"| ",XXCOND: 
                    *56,"| ",XXLSTAT," |",DCDATE,"|",DLRDAT,"|",XXDIAG[1]:
                    *132,"|"
.
          IF        XXCNT=0
            PRINT   *1,"|",*11,"|",*23,"Bed Status : ",XXBSTAT,*45,"|",*50:
                    "|",*56,"|",*62,"|",*73,"|",*84,"|",XXDIAG[2],*132,"|"
          ELSE
            MOVE    SP8,DECEASED
            MOVE    ZERO,PCEASE
            PACK    KEY8,XXURNO
            CALL    RDMAST1
.             
            IF      PCEASE=1
              MOVE     "Deceased",DECEASED
            ENDIF
            PRINT   *1,"|",DECEASED,*11,"|":
                    *45,"|",*50,"|",*56,"|",*62,"|",*73,"|",*84,"|":
                    XXDIAG[2],*132,"|"
          ENDIF
.
          MOVE      THREE,COUNTER
          WHILE     COUNTER<8
            MATCH     SP50,XXDIAG[COUNTER]
            IF        @EQUAL
              MOVE  EIGHT,COUNTER
              CALL  UND132
            ELSE
              PRINT   *1,"|",*11,"|",*45,"|",*50,"|",*56,"|",*62:
                      "|",*73,"|",*84,"|",XXDIAG[COUNTER],*132,"|"
              ADD     ONE,COUNTER
              ADD     ONE,CLNO
              IF COUNTER=8
                CALL   UND132
              ENDIF
            ENDIF
          DO
.
          ADD       TWO,CLNO                      * increment line counter
          ADD       ONE,NOPATS                    * increment no. patients
          GOTO      RPRT1000
.
. we have finished printing patient data so print end of report details
.
RPRT8000  PRINT     *1,"Number of patients = ",NOPATS,*N:
                    *N,"*** End of Report ***"
.
RPRT9999  RETURN
+
.***************************************************************************
.*  LOAD0000  :  Load data into temp file                                  *
.***************************************************************************
LOAD0000  DISPLAY   *P1:24,*EL,*P30:24,"Loading data... [        ]"
.
          PACK      KEY9,ONE,SP10
          CALL      RSMHVIS2
LOAD1000  CALL      RKMHVIS2                      * get next record
          BRANCH    OVRCD,LOAD9000                * no more records
.
. check if discharged 
.
          COMPARE   MHVISTAT,SIX                  * ignore CMH records
          GOTO      LOAD1000 IF LESS
.
          IF    MHVISTAT=6
                  MOVE   ONE,XXCNT
          ELSE
                  MOVE   ZERO,XXCNT
          ENDIF
.
. get master details
.
          PACK      KEY8,MHVIADMN
          CALL      RDMISS1
          BRANCH    OVRCD,LOAD1000                * ignore if no admission rec
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,LOAD1000                * ignore if no master rec
.
          PACK      KEY8,AURNO
          CALL      RDPMPX21
          BRANCH    OVRCD,LOAD1000                * ignore if no master rec
.
          MOVE      ADOCTA,XXDOCT
          MOVE      ADOCTA,KEY6                   * get attending doctor name
          CALL      RDDOCT1
          BRANCH    OVRCD,LOAD1000                * ignore if no doctor on file
.
          MOVE      PURNO,XXURNO
          MOVE      AADMNO,XXADMN
          STRIP     PSNAME
          PACK      XXNAME,PSNAME,COMMA,SP1,PGNAME
          MOVE      AWARD,XXWARD
          MOVE      MHVIRODT,XXCDATE
          LOAD      XXBSTAT,MHVISTAT,ANSO,ANSL,ANST,ANSH,ANSW
.
. now get latest legal status
.
          MOVE      SP3,XXLSTAT
          PACK      KEY21,AADMNO,ZED20
          CALL      RSMHLEG1
          CALL      RPMHLEG1                      * read last rec
          BRANCH    OVRCD,LOAD2400
.
          MATCH     MHLEADMN,AADMNO               * same admission no?
          GOTO      LOAD2400 IF NOT EQUAL
.
          MOVE      MHLESTAT,XXLSTAT              * legal status
.
. now get last review date
.
LOAD2400  MOVE      SP8,XXLREV
          PACK      KEY21,AADMNO,ZED20
          CALL      RSMHREV1
          CALL      RPMHREV1                      * read last rec
          BRANCH    OVRCD,LOAD2500
.
          MATCH     AADMNO,MHRVADMN               * same admission no?
          GOTO      LOAD2500 IF NOT EQUAL
.
          MOVE      MHRVDATE,XXLREV               * review date
.
. get Patient Condition code
.
LOAD2500  MOVE      SP3,LPCONDC
          PACK      KEY88,PTMASNAM,PMPXFNAM,AADMNO
          CALL      RDLOCA1
          MOVE      LPCONDC,XXCOND
.
. get latest Diagnosis
.
          MOVE      SP50,XXDIAG[1]
          MOVE      SP50,XXDIAG[2]
          MOVE      SP50,XXDIAG[3]
          MOVE      SP50,XXDIAG[4]
          MOVE      SP50,XXDIAG[5]
          MOVE      SP50,XXDIAG[6]
          MOVE      SP50,XXDIAG[7]
          PACK      KEY16,AADMNO,ZED20
          CALL      RSMHDIA1
          CALL      RPMHDIA1
          BRANCH    OVRCD,LOAD3000
.
          MATCH     AADMNO,MHDIADMN               * same admission no?
          GOTO      LOAD3000 IF NOT EQUAL
.
          MOVE      MHDIX1DA,XXDIAG[1]
          MOVE      MHDIX1DB,XXDIAG[2]
          MOVE      MHDIX1DC,XXDIAG[3]
          MOVE      MHDIX2DA,XXDIAG[4]
          MOVE      MHDIX2DB,XXDIAG[5]
          MOVE      MHDIX2DC,XXDIAG[6]
          MOVE      MHDIX3DS,XXDIAG[7]
.
LOAD3000  DISPLAY   *P47:24,MHVIADMN;
.
          PACK      KEY47,XXDOCT,XXCNT,XXNAME,XXADMN
          CALL      WRTEMP1
          GOTO      LOAD1000
.
LOAD9000  DISPLAY   *P1:24,*EL
.
LOAD9999  RETURN
+
.**************************************************************************
.*  PTHD0000  :  Print Table Heading                                      *
.**************************************************************************
PTHD0000  PACK      KEY6,XXDOCT
          CALL      RDDOCT1
          MOVE      DGNAME,PACGNAME               * format doctors name
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DOCTNAME
.
          PRINT     *1,"Doctor : ",DOCTNAME
          PRINT     *N,THEADER
          CALL      UND132
          PRINT     *1,"|",*11,"|",*45,"|",*50,"|Cond-|Legal|  Commit  |":
                    "   Last   |",*132,"|":
                    *N,*1,"| U/R No. | Patient Name",*45,"|Ward|ition|Stat.|":
                    "   Date   |  Review  | Diagnosis",*132,"|"
          CALL      UND132
.
PTHD9999  RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temp file                                         *
.*************************************************************************
KILL0000  CLOSE     MEHTMPA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CRET0000  CALL      TFILENAM                     * get new tempfile name
          MOVE      TFILNAME,FNAME
.
          CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    " 397 U1-6,7-7,8-39,40-47",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      MEHTMPA1,FNAME
.
CRET9999  RETURN
+
.***************************************************************************
.*  Temp file I/O's                                                        *
.***************************************************************************
RSTEMP1   RESET     KEY47
          READ      MEHTMPA1,KEY47;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    MEHTMPA1;XXDOCT,DXXCNT,XXNAME,XXADMN,XXURNO,XXWARD,XXCOND:
                             XXLSTAT,XXCDATE,XXLREV,XXBSTAT,XXDIAG[1]:
                             XXDIAG[2],XXDIAG[3],XXDIAG[4],XXDIAG[5]:
                             XXDIAG[6],XXDIAG[7]
          GOTO      OVERCOND IF OVER
          MOVE      DXXCNT,XXCNT
          RETURN
.
WRTEMP1   RESET     KEY47
          MOVE      XXCNT,DXXCNT
          WRITE     MEHTMPA1,KEY47;XXDOCT,DXXCNT,XXNAME,XXADMN,XXURNO,XXWARD:
                             XXCOND,XXLSTAT,XXCDATE,XXLREV,XXBSTAT,XXDIAG[1]:
                             XXDIAG[2],XXDIAG[3],XXDIAG[4],XXDIAG[5]:
                             XXDIAG[6],XXDIAG[7]
          RETURN
.
.
          INC       STD001IO
.
          INC       TFILENAM
.
          INC       IBASEQIO/INC
          INC       MEHREVIO/INC
          INC       MEHDIAIO/INC
          INC       MEHLEGIO/INC
          INC       MEHVI1IO/INC
          INC       PATDO1IO/INC
          INC       PATLOCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       WEBERRIO/INC

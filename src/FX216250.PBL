.*****************************************************************************
.* System    :   Patient Management                                          *
.* Program   :   FX216250                                                    *
.* Desc      :   Fixit to populate long description of patitemn with         *
.*               the short description.                                      *
.*****************************************************************************
.* Date      :   15/07/1997                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will loop through patitemn and check to see    *
.*               if the long description for each record is blank.  If it    *
.*               is, then the short description is loaded into the long      *
.*    Mods   :                                                               *
.*               V10.00.01 09/07/2010  Davin            CAR 225484           *
.*               Added check for null on ptitdesc                            *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATITMFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      10
.
.
. PROGRAM CONSTANTS
. -----------------
NULL      INIT      000
SP60      INIT      "                              ":
                    "                              "
.
PRGID     INIT      "FX216250"
PRGNAM    INIT      "Load Long Description - PATITMFD"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patitemn";
          OPEN      PATITEM1,"patitemn"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*      Loop though patitemn and where the long description (ptitdesc) is    *
.*      blank, load the short description (idesc) into the long description. *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,COUNTER                 * initialise counter
.
          MOVE      SP9,KEY9
          CALL      RDSITEM1                     * position at start of file
PROC0500  CALL      RDKITEM1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          CMATCH    NULL,PTITDESC
          IF        @EQUAL
            MOVE      SP70,PTITDESC
          ENDIF
          PACK      PTITDESC,PTITDESC,SP70
.
          MATCH     SP60,PTITDESC                * blank long description ?
          GOTO      PROC0500 IF NOT EQUAL        * no - ignore record
.
          ADD       ONE,COUNTER                  * increment counter
          PACK      PTITDESC,IDESC,SP30,SP30
          CALL      UPITEM1                      * update record
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:23,*EF,*V2LON,COUNTER,*HOFF," records updated.  ":
                    *P1:24,"Fixit completed.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATITMIO/INC

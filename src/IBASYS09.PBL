. ****************************************************************************
. *                     P R O G R A M  S U M M A R Y                         *
. *                     -------------  -------------                         *
. *                                                                          *
. *     PROGRAM NAME:   IBASYS09                                             *
. *                                                                          *
. *     FUNCTION:       PORT SCANNING ENQUIRY                                *
. *                                                                          *
. *     DATE WRITTEN:   15/02/88                                             *
. *                                                                          *
. *     AUTHOR:         M.HAYSE       (I.B.A.)                               *
. *                                                                          *
. *     MODIFICATIONS:  V5.07.00  10/9/98   Jim Stathopoulos                 *
. *                               507 Changes                                *
. *                     V5.01.01  27/07/90 i. chung             SRF 105451   *
. *                               Fixed display of 2nd screen of users       *
. *                                                                          *
. *                               03/09/91 Steve O'Gorman                    *
. *                               Upgraded to Release 6                      *
. *                                                                          *
. ****************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       IBAPORFD/INC
.
.
DIM3      DIM       3
DIM4      DIM       4
DIM5      DIM       5
DIM6      DIM       6
DIM8      DIM       8
DIM12     DIM       12
DIM14     DIM       14
DIM25     DIM       25
DIM30     DIM       30
.
DANSX     DIM       3
.
FIELD     FORM      2
FORM5     FORM      5
FORM6     FORM      6
FORM7     FORM      7
FORM8     FORM      8
FORM10    FORM      10
.
OUTFLAG   FORM      1
.
OPTX      DIM       1
.
.   USED IN DISP ROUTINES
.
SRDIM6    DIM       6
.
SRDIM10   DIM       10
SRDIM10A  DIM       10
SRDIM20   DIM       20
SRDIM20A  DIM       20
SRDIM30   DIM       30
SRDIM30A  DIM       30
.
TODAY     DIM       8       ***** TODAYS DATE CCYYMMDD
.
.
USERIDNT  DIM       3
.
.
PRGID     INIT      "IBASYS09"
PRGNAM    INIT      "Port Scanning Enquiry"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          DISPLAY   *ES,"IBASYS09":
                    *P56:24,"Opening ibaport1";
          OPEN      IBAPORT1,"ibaportf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
.         READ      CONTROLF,TWENTY3;*84,HUSERCD,*129,HSTART
.
          PACK      TODAY WITH CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
.       ALL FILES WILL USE TODAYS DATE AS DATE OF USUAGE
.
START     CALL      DISPHEAD
.             INPUT SYSTEM IDENTIFIER
.
KIDNT     DISPLAY   *P1:4,*EF," System Identifier :"
.
          KEYIN     *P22:4,*EL,*DV,UNDLN3:
                    *P22:4,*V2LON,USERIDNT,*P22:4,*DV,USERIDNT
.
          ENDSET    USERIDNT
          APPEND    SP3,USERIDNT
          RESET     USERIDNT
.
          MATCH     SP3,USERIDNT
          GOTO      START1 IF EQUAL
.
          MOVE      ZERO,FORM2
.
NEXT1     ADD       ONE,FORM2
          LOAD      DIM8 FROM FORM2 OF P1,P2,P3,P4,P5,P6,P7,P8,P9,P10:
                    P11,P12,P13,P14,P15,P16,P17,P18,P19,P20:
                    P21,P22,P23,P24,P25,P26,P27,P28,P29,P30:
                    P31,P32,P33,P34,P35,P36,P37,P38,P39,P40:
                    P41,P42,P43,P44,P45,P46,P47,P48,P49,P50:
                    P51,P52,P53,P54,P55
.
          UNPACK    DIM8 INTO DIM3,IBAPIDNT,DIM2
.
          MATCH     USERIDNT,IBAPIDNT
          GOTO      GOTID IF EQUAL
.
          COMPARE   "55",FORM2
          GOTO      NEXT1 IF NOT EQUAL
.
          DISPLAY   *P40:4,*B,*V2LON,"*** Invalid Identifier ***",*W3;
          GOTO      KIDNT
.
GOTID     LOAD      DIM30 FROM FORM2 OF OPT1,OPT2,OPT3,OPT4,OPT5:
                    OPT6,OPT7,OPT8,OPT9,OPT10:
                    OPT11,OPT12,OPT13,OPT14,OPT15:
                    OPT16,OPT17,OPT18,OPT19,OPT20:
                    OPT21,OPT22,OPT23,OPT24,OPT25:
                    OPT26,OPT27,OPT28,OPT29,OPT30:
                    OPT31,OPT32,OPT33,OPT34,OPT35:
                    OPT36,OPT37,OPT38,OPT39,OPT40:
                    OPT41,OPT42,OPT43,OPT44,OPT45:
                    OPT46,OPT47,OPT48,OPT49,OPT50:
                    OPT51,OPT52,OPT53,OPT54,OPT55
.
          DISPLAY   *P40:4,*V2LON,DIM30
.
START1    DISPLAY   *P1:6,*EF,*P3:6,*ULON,*V2LON,"Port",*P8:6,"   Date   ":
                    *P19:6,"  Time  ",*P29:6,"User",*P35:6,"Program ":
                    *P44:6,"Description                      "
.
          MOVE      SEVEN,CVERT
.
          MOVE      SP2,KEY2
          CALL      RDSPORT1
.
NEXTP     CALL      RDKPORT1
          BRANCH    OVRCD OF SFIELD
.
          MATCH     SP8,IBAPDATE
          GOTO      NEXTP IF EQUAL
.
          MATCH     SP3,USERIDNT
          GOTO      ALLP IF EQUAL
.
          MATCH     USERIDNT,IBAPIDNT
          GOTO      NEXTP IF NOT EQUAL
.
ALLP      DISPLAY   *P4:CVERT,IBAPPORT;
.
          UNPACK    IBAPDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      EIGHT,CCOL
.
          MOVE      ONE,CHIGHLT
          CALL      DSPDATE
.
          DISPLAY   *P19:CVERT,IBAPTIME,SP2,IBAPUSER,SP2,IBAPPROG,SP1:
                    IBAPDESC
          ADD       ONE,CVERT
.
          COMPARE   TWENTY3,CVERT
          GOTO      NEXTP IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,"(",*V2LON,ANSE,*HOFF,")xit, (":
                    *V2LON,ANSS,*HOFF,")ystem, (":
                    *V2LON,ANSR,*HOFF,")efresh, (":
                    *V2LON,ANSM,*HOFF,")ore : "
.
REPLYA    KEYIN     *P39:24,*EL,*DV,UNDLN1,*P39:24,*V2LON,ANS
          RESET     ANS
          GOTO      REPLYA1 IF EOS
          REP       UPPLOW,ANS
          DISPLAY   *P39:24,*V2LON,ANS
.
          CMATCH    ANSS,ANS
          GOTO      KIDNT IF EQUAL
.
          CMATCH    ANSR,ANS
          GOTO      START1 IF EQUAL
.
          CMATCH    ANSE,ANS
          GOTO      ENDIT IF EQUAL
.
          CMATCH    ANSM,ANS
          GOTO      REPLYA2 IF NOT EQUAL
.
REPLYA1   DISPLAY   *P1:7,*EF
          MOVE      SEVEN,CVERT
          GOTO      NEXTP
.
REPLYA2   BEEP
          GOTO      REPLYA
.
SFIELD    DISPLAY   *P1:24,"(",*V2LON,ANSE,*HOFF,")xit, (":
                    *V2LON,ANSS,*HOFF,")ystem, (":
                    *V2LON,ANSR,*HOFF,")efresh : "
.
REPLYB    KEYIN     *P31:24,*DV,UNDLN1,*P31:24,*V2LON,ANS
          RESET     ANS
          GOTO      REPLYB1 IF EOS
          REP       UPPLOW,ANS
          DISPLAY   *P31:24,*V2LON,ANS
.
          CMATCH    ANSS,ANS
          GOTO      KIDNT IF EQUAL
.
          CMATCH    ANSE,ANS
          GOTO      ENDIT IF EQUAL
.
          CMATCH    ANSR,ANS
          GOTO      START1 IF EQUAL
.
REPLYB1   BEEP
          GOTO      REPLYB
.
          INCLUDE   IBAPORIO/INC
.
          INCLUDE   STD001IO
.
ENDIT     CHAIN     PGM
          STOP

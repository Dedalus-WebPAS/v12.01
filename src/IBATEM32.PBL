.**********************************************************************
.*                                                                    *
.* Program       : IBATEM32                                           *
.*                                                                    *
.* Description   : Master listing program for IBA variable definition *
.*                 file for user defined stationery                   *
.*                                                                    *
.* Date          : 21/03/90                                           *
.*                                                                    *
.* Author        : Neeriem Dye                                        *
.*                                                                    *
.* Comments      : To add a new stationery type, modify procedure     *
.*                 TYPE0000                                           *
.*                                                                    *
.**********************************************************************
.* Mods :                                                             *
.*       V10.03.01  25/10/2012  Patrick Adair           CAR 273181    *
.*                  Recompiled for IBADSTYP                           *
.*        V9.12.01  30/09/2009  Peter Vela  CAR 205008                *
.*                  Recompiled for IBADSTYP                           *
.*        V9.10.01  29/07/2008  Peter Vela  CAR 151951                *
.*                  Recompiled for IBADSTYP                           *
.*        V9.08.01  23/03/2007  Peter Vela  CAR 128233                *
.*                  Recompiled for IBADSTYP                           *
.*        V9.05.01  22/03/2006  Peter Vela  CAR 61299                 *
.*                  Increased field no size from DIM3 to DIM5         *	
.*        V9.04.01  17/03/2005  Lina Vo         CAR 56404             *
.*                  Added IBASTP18 - Theatre Form Stationary Type     *
.**********************************************************************
.*        V9.03.01  01/03/2004  Pat Dirito      CAR 50842             *
.*                  Added IBASTP17 - Allied Health Stationary Type    *
.**********************************************************************
.*        C2.00.02  27/06/2000  Charles Handaya                       *
.*                  Added new Stationery Types (15 & 16) for Receipt  *
.*        C2.00.01  06/04/2000  Charles Handaya                       *
.*                  Added new Stationery Types (13 & 14) for Debtor   *
.*        C2.00.00  25/02/2000  Davin                                 *
.*                  Combined release 5 & 6 (all stationery types)     *
.**********************************************************************
.*        V5.07.02  09/07/1999  Nicole Harrington                     *
.*                  Added IBASTYP7 (Emergency Form)                   *
.*        V5.07.01  01/04/1999  Davin                                 *
.*                  Added IBASTYP6 (Health Fund Claim Form)           *
.*        V5.07     06/10/98  Jim Stathopoulos                        *
.*                  507 Changes                                       *
.**********************************************************************
.*                 V5.01.02  29/03/93  ROD                            *
.*                           Use new template file (Dudley)           *
.*        V5.02.01  12/04/1995                                        *
.*                  Removed the option for ibavr1af                   *
.*        V5.02.02  02/05/1995  Steve Armstrong                       *
.*                  Recompiled for IBASTPDS                           *
.*        V5.03.01  27/06/1995    Justin Coates                       *
.*                  removed use of stcntvar                           *
.*                                                                    *
.**********************************************************************
.
.$$$$$
.    IBA Variable Definition Master Listing ( IBATEM32 )
.    ---------------------------------------------------
.
.    - Initialisation
.
.            - Display Header
.            - Open Files
.               IBAVARD1
.               IBAVARD2
.
.    - Processing Options
.
.            - 0 = Exit
.            - 1 = Print by Variable Description
.                  - Get Stationery Type to Print (Or All Types)
.                  - Go to Start of Stationery Type on the File IBAVARD1
.                  - Print The Details of the Selected Stationery type(s)
.            - 2 = Print by Field Number
.                  - Get Stationery Type to Print (Or All Types)
.                  - Go to Start of Stationery Type on the File IBAVARD2
.                  - Print The Details of the Selected Stationery type(s)
.
.    - END
.$$$$$
.
          INC       STD001FD
          INC       IBAVARFD/INC
          INC       IBADSTYP/INC
.**********************************************************************
.*                           CONSTANTS                                *
.**********************************************************************
PRGID     INIT      "IBATEM32"
PRGNAM    INIT      "Variable Maintenance"
VERSION   INIT      "V12.01.00"
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
ITEMS     FORM      7
.
PRTTYPE   DIM       3
TYPENAME  DIM       30
TYPENUM   FORM      3
TYPERNGE  DIM       30
.
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
.
ML0000    CALL      INIT0000               * initialization and open files
.
ML0100    CALL      OPTN0000               * select "print by" option
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          CALL      PTYP0000               * select type to print
          CALL      PRNT0000               * print report
          GOTO      ML0100
.
ML9999    CHAIN      PGM                    * chain back to program
+
.**********************************************************************
.*        INIT0000                               Called by ML0000     *
.*        Initialization  Routine                                     *
.**********************************************************************
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P57:24,"Opening controlf";  * open variable file
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P57:24,"Opening ibavaraf";  * open variable file
          OPEN      IBAVARD1,"ibavaraf"
          OPEN      IBAVARD2,"ibavaraf"
.
          MOVE      ONE,CNOUNDLN                 * dont print line at eop
          ADD       FORTY,NSORTNUM               * POE starts at 40.
.
INIT9999 RETURN
.****************************************************************************
.*        OPTN0000                                Called by : ML0000        *
.*        get user options or exit                                          *
.*                                                                          *
.*    valid options     --->  (0) Exit                                      *
.*                            (1) Print by variable description             *
.*                            (2) Print by field number                     *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                      *P1:5,*V2LON,ONE,*HOFF," = Print by ":
                      "Variable Description":
                      *P1:6,*V2LON,TWO,*HOFF," = Print by Field Number":
                      *P1:8,"Select Option : _";
          KEYIN     *P17:8,*V2LON,COPTION
          COMPARE   ZERO,COPTION
          GOTO      OPTN9000 IF EQUAL              * exit      
          COMPARE   THREE,COPTION
          GOTO      OPTN0000 IF NOT LESS           * invalid option, re-enter
.
          MOVE      FALSE,EXIT
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT                      * set EXIT = 1 
.
OPTN9999  RETURN
.**************************************************************************
.*        PTYP0000                     Called by :   ML000                *
.*        Select Type to Print                                            *
.*                  Returns : PRTTYPE (type to print (SP3 for all))       *
.*                            TYPENAME (name of type)                     *
.**************************************************************************
PTYP0000  MOVE      TEN,CVERT
.
PTYP1000  KEYIN     *P1:CVERT,"Select Type To Print (<",*V2LON,"ENTER",*HOFF:
                    "> For All) : ___",*P42:CVERT,*V2LON,PRTTYPE,*EL;
          MOVE      PRTTYPE,TYPENUM              * convert to number
          PACK      PRTTYPE,PRTTYPE,SP3          * pad out with blanks
.
          MATCH     SP3,PRTTYPE
          GOTO      PTYP9000 IF EQUAL            * print all types
.
          MATCH     "?",PRTTYPE
          GOTO      PTYP7000 IF EQUAL            * user needs help
.
          MOVE      TYPENUM,PRTTYPE              * right justify PRTTYPE
          CALL      TYPE0000                     * get TYPENAME
          BRANCH    OVRCD,PTYP8000               * illegal type entered ?
.
          MOVE      TYPENAME,TYPERNGE
          GOTO      PTYP9999                     * legal entry
.
.--- user help for stationery type ----
.
PTYP7000  CALL      TYPEDS
          MOVE      TWENTY4,CVERT
          GOTO      PTYP1000
.
.--- illegal type entered ----
.
PTYP8000  DISPLAY   *P1:24,"Type Does Not Exist - ";
          CALL      HITENTER
          GOTO      PTYP1000
.
.--- print all types ----
.
PTYP9000  MOVE      "All",TYPERNGE
.
PTYP9999  RETURN
.**************************************************************************
.*        PRNT0000                     Called by :   ML000                *
.*        Print report                                                    *
.*                  Requires : COPTION (1=by desc, 2=by field num)        *
.**************************************************************************
PRNT0000  DISPLAY   *P50:24,"Printing :  ";
          MOVE      ZERO,ITEMS                   * clear counter
          MOVE      ZERO,CPAGENO                 * reset page no
          CLOCK     TIME,CTIMEIS                 * get time
          CALL      HEAD0000                     * print header
.
PRNT0100  CALL      FILE0000                     * go to correct file position
.
PRNT0900  CALL      NREC0000                     * get next record
.
          BRANCH    OVRCD,PRNT9000               * end of file ?
          CALL      LREC0000                     * check if legal rec
          BRANCH    OVRCD,PRNT9000               * illegal rec ?
.
          COMPARE   "55",CLNO                    * end of page ?
          CALL      HEAD0000 IF EQUAL
          CALL      WRIT0000                     * print details
          DISPLAY   *P61:24,DIBVRFNM;
          ADD       ONE,ITEMS
          ADD       ONE,CLNO 
.
          GOTO      PRNT0900                     * start new page
.
PRNT9000  CALL      LINE0000                     * print line
          PRINT     *N,*12,"Total Number Of Items Printed : ",ITEMS:
                    *N,*N,"*** END OF REPORT ****"
.
PRNT9999  RETURN
.*********************************************************************
.*        HEAD0000                             Called by : PRNT0000  *
.*        Print page header                                          *
.*********************************************************************
HEAD0000  COMPARE   ZERO,CPAGENO                 * first page ?
          CALL      LINE0000 IF NOT EQUAL
          CALL      HEAD132                      * print header
          ADD       ONE,CLNO
          PRINT     *+,*40,"Print ",TYPERNGE;
          BRANCH    COPTION,HEAD1000,HEAD2000
.
HEAD1000  PRINT     " Types By Variable Description"
          GOTO      HEAD3000 
.
HEAD2000  PRINT     " Types By Field Number"
.
HEAD3000  CALL      LINE0000                     * print line
          ADD       ONE,CLNO
          PRINT     *12,"|",*15,"Stationery Type",*53,"|",*55:
                    "Variable Description",*88,"|",*90,"Field Number":
                    *103,"|",*105,"Field Length",*118,"|"
          CALL      LINE0000                     * print line
HEAD9999  RETURN
.*********************************************************************
.*        LINE0000                     Called by : PRNT0000,HEAD0000 *
.*        Print line                                                 *
.*********************************************************************
LINE0000  ADD       ONE,CLNO
          PRINT     *12,"*---------------------------------------------------":
                    "------------------------------------------------------*"
LINE9999  RETURN
.*********************************************************************
.*        WRIT0000                    Called by : PRNT0000           *
.*        Go to first requested record on file                       *
.*********************************************************************
WRIT0000  MOVE      DIBVRTYP,TYPENUM
          CALL      TYPE0000                     * get typename
          PRINT     *12,"|",*15,DIBVRTYP,*20,TYPENAME,*53,"|",*55,IBVRDESC:
                    *88,"|",*94,DIBVRFNM,*103,"|",*109,IBVRFLEN,*118,"|"
WRIT9999  RETURN
.*********************************************************************
.*        FILE0000                    Called by : PRNT0000           *
.*        Go to first requested record on file                       *
.*                 Requires : PRRTYPE (type to start at (SP3 for all)*
.*********************************************************************
FILE0000  BRANCH    COPTION,FILE1000,FILE3000
.
.---- go to correct position in file (by variable description) ----
.
FILE1000  PACK      KEY33,PRTTYPE,SP30
          CALL      RSIBVAR1
          GOTO      FILE9999
.
.---- go to correct position in file (by field number) ----
.
FILE3000  PACK      KEY8,PRTTYPE,SP8
          CALL      RSIBVAR2
.
FILE9999  RETURN
.*********************************************************************
.*        NREC0000                    Called by : PRNT0000           *
.*        Get next record                                            *
.*********************************************************************
NREC0000  BRANCH    COPTION,NREC1000,NREC3000
.
.---- go to next record in file (by variable description) ----
.
NREC1000  CALL      RKIBVAR1
          GOTO      NREC9999
.
.---- go to next record in file (by field number) ----
.
NREC3000  CALL      RKIBVAR2
.
NREC9999  RETURN
.*********************************************************************
.*        LREC0000                             Called by : PRNT0000  *
.*        Check if current record is legal                           *
.*                 Requires : PRRTYPE (type to print (SP3 for all))  *
.*                            DIBVRTYP (type of current record)      *
.*                  Returns : OVRCD (1=illegal, 0=legal)             *
.*********************************************************************
LREC0000  MATCH     SP3,PRTTYPE                  * all types ?
          GOTO      LREC8000 IF EQUAL
.
          MATCH     DIBVRTYP,PRTTYPE             * correct type ?
          GOTO      LREC9000 IF NOT EQUAL
.
LREC8000  MOVE      ZERO,OVRCD                   * this is a legal record
          GOTO      LREC9999
.
LREC9000  MOVE      ONE,OVRCD                    * illegal record
.
LREC9999  RETURN
.**************************************************************************
.*        TYPE0000                     Called by : PRNT000                *
.*        Display chosen type                                             *
.*                  Requrements : TYPENUM  for type no. to display        *
.*                  Returns     : OVRCD (=1 if no such type, =0 if ok)    *
.*                                TYPENAME (stationery type name)         *
.**************************************************************************
TYPE0000  MOVE      SP30,TYPENAME
          MOVE      ZERO,OVRCD
          COMPARE   ONE,TYPENUM
          GOTO      TYPE1000 IF LESS
.
          COMPARE   TYPENUM,NSORTNUM
          GOTO      TYPE1000 IF LESS
.
          COMPARE   FORTY,TYPENUM
          GOTO      TYPE0500 IF NOT LESS
.
          MOVE      IBASTYP0,TYPENAME
          LOAD      TYPENAME,TYPENUM,IBASTYP1,IBASTYP2,IBASTYP3,IBASTYP4:
                                     IBASTYP5,IBASTYP6,IBASTYP7,IBASTYP8:
                                     IBASTYP9,IBASTP10,IBASTP11,IBASTP12:
                                     IBASTP13,IBASTP14,IBASTP15,IBASTP16:
                                     IBASTP17,IBASTP18
          GOTO      TYPE9999
.
TYPE0500  MOVE      TYPENUM,NSORTYPE
          SUB       FORTY,NSORTYPE
          CALL      NSMORDES
          BRANCH    OVRCD,TYPE9999
          MOVE      NSORDESC,TYPENAME
          GOTO      TYPE9999
.
TYPE1000  MOVE      ONE,OVRCD                    * illegal type
.
TYPE9999  RETURN
.**************************************************************************
.*        TYPEDS                       Called by : PTYP0000               *
.*        User help for stationery type                                   *
.**************************************************************************
TYPEDS   
          SUB        FORTY,NSORTNUM
          CALL       IBASTPDS
          ADD        FORTY,NSORTNUM
.
          RETURN
.********************************************************************** 
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
          INC       STD001IO
          INC       IBASTPDS
          INC       NSMORTYP
          INC       IBAVARIO/INC

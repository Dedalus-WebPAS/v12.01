.***************************************************************************
.* System    :   Waiting List                                              *
.* Program   :   FXWTSRCR                                                  *
.* Desc      :   Fix WL source of referral                                 *
.***************************************************************************
.* Date      :   06/09/2005                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :   This program will move the field wtwmsrcr to the correct  *
.*               user defined field                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       WATCONFD/INC
          INC       WATTR1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
BRSRCAT   DIM       2
CATW1     INIT      "W1"
CATW2     INIT      "W2"
CATW3     INIT      "W3"
CATW4     INIT      "W4"
CATX5     INIT      "X5"
CATX6     INIT      "X6"
CATX7     INIT      "X7"
CATX8     INIT      "X8"
DIM3      DIM       3
.
RECNUM    FORM      8
.
PRGID     INIT      "FXWTSRCR"
PRGNAM    INIT      "Fix source of referral       "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      WATTR1A1,"wattr1af"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,THIRTY7;*236,WTCNBRSR
          LOAD      BRSRCAT,WTCNBRSR,CATW1,CATW2,CATW3,CATW4,CATX5:
                                     CATX6,CATX7,CATX8
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Move wattr1af.wtwmsrcr to user ":
                                           "defined field ",BRSRCAT
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,RECNUM
          READ      CONTROLF,THIRTY7;*236,WTCNBRSR
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
.
          PACK      KEY19,SP70
          CALL      RDSTREA1
PROC1000  CALL      RDKTREA1
          BRANCH    OVRCD,PROC9999
.
          ADD       ONE,RECNUM
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MATCH     SP70,WTWMSRCR
          GOTO      PROC1000 IF EQUAL
.
          LOAD      DIM3,WTCNBRSR,WMUDEF1,WMUDEF2,WMUDEF3,WMUDEF4,WMUDEF5:
                                     WMUDEF6,WMUDEF7,WMUDEF8
.
          MATCH     SP3,DIM3
          IF        @EQUAL
            STORE     WTWMSRCR,WTCNBRSR,WMUDEF1,WMUDEF2,WMUDEF3,WMUDEF4,WMUDEF5:
                                     WMUDEF6,WMUDEF7,WMUDEF8
          ENDIF
          CALL      UPTREA1
          GOTO      PROC1000
.
PROC9999  CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
          RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       WATTR1IO/INC

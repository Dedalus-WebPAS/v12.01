.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   IBARSH14                                                  *
.* Desc      :   Schedule program to delete expired concession cards       *
.***************************************************************************
.* Date      :   29/11/2004                                                *
.* Author    :   Davin Sloan                                               *
.* Function  :   This program will allow the user to delete expired        *
.*           :   concession cards (by type) up to a given date             *
.*           :                                                             *
.* Mods      :                                                             *
.***************************************************************************
.*              V11.05.01 14/02/2025  Nikitha Bhaskar   TSK 0956153        *
.*                        Added ADUTCDEL to write in Audit file when a     *
.*                        concession card is deleted and added DFRS0000 to *
.*                        get deletion reason when Ind 1=D of 'cH' cat code*  
.***************************************************************************
.*              V10.06.01 12/03/2015  Steve Armstrong  CAR 314108          *
.*                        Removed definition of PTCGDATE as PATCGPFD is no *
.*                        longer in use.                                   *
.***************************************************************************
.*              V10.03.03 30/06/2013  Davin             CAR 283202         *
.*                        Recompiled for changes to hl7 messaging          *
.*              V10.03.02 01/05/2013  Steve Armstrong  CAR 284376          *
.*                        Recompiled for changes to DGCLICUP.              *
.*              V10.03.01 03/04/2013  Davin          CAR 270648            *
.*                        Recompiled for changes to HL7 messaging includes *
.***************************************************************************
.*              V10.02.01 03/08/2011 Steve Armstrong  CAR 247627           *
.*                        Added DGCLICUP trigger for delete from pmsccdaf  *
.***************************************************************************
.*               V9.04.00 29/11/2004 Davin            CAR 34707            *
.*                        Created program                                  *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
. 
          INC       IBACONFD/INC
          INC       NHIMASFD/INC
          INC       PMSCCDFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATMA1FD/INC
          INC       PATNIDFD/INC
          INC       PMSPX2FD/INC
          INC       WEBSECFD/INC
          INC       PATCCHFD/INC
.
. LOCAL VARIABLE DEFINITION
. ----
.
AUDTATRB  FORM      1
CARDTYP1  DIM       3                  * delete card type from
CARDTYP2  DIM       3                  * delete card type to
CODE      DIM       2
COUNT     FORM      12
FROMDATE  DIM       8                  * delete cards up to this expiry date
CATct     INIT      "ct"
CATcH     INIT      "cH"
DRAW      INIT      "*-------------------------------------------------------":
                    "--------------------*"
DISPDAT1  DIM       11
DISPDAT2  DIM       10
DISPSTRT  DIM       6
DISPFNSH  DIM       6
DIM20     DIM       20
DELTRESN  DIM       3                  * variable for delete reason
.
NMPNUMB   DIM       20
.
PRGID     INIT      "IBARSH14"
PRGNAM    INIT      "Delete Concess.Cards"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000                * initialisation and open files
.
ML0100    CALL      OPTN0000                * select options
          BRANCH    EXIT,ML9999             * EXIT = 1 if 0 chosen in menu
.
          BRANCH    OPTION,ML0500
.
          GOTO      ML0100
.
ML0500    CALL      CTYP0000                * keyin card type
          BRANCH    EXIT,ML0100             * nothing input
.
ML0600    CALL      FDAT0000                * keyin date
          BRANCH    EXIT,ML0500             * nothing input
.
          CALL      CONTQST                 * ok to continue
          BRANCH    CEXIT,ML5000,ML0600,ML0100    * 1=Yes, 2=No, 3=Cancel
.
ML5000    CALL      PEXP0000                * Delete/Print expired cards
          BRANCH    EXIT,ML0100
.
ML9999    CHAIN     PGM                     * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PMSCCDA2,"pmsccdaf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATCODE1,"patcodes"
          OPEN      PMSPX2A1,"pmspx2af"
.
          MOVE      ZERO,AUDTATRB
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATCCHA1,"patcchaf"
          TRAPCLR   IO
          IF        OVRCD=1
            MOVE      ONE,AUDTATRB
          ENDIF
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Delete expired concession cards";
.
OPTN0500  KEYIN     *P1:24,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:24,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.**************************************************************************
.*  FDAT0000  :  keyin From date                                          *
.**************************************************************************
FDAT0000  DISPLAY   *P1:11,*EF,"Delete to Date : "
          UNPACK    SP10,CDAY,CMON,CYEAR
          MOVE      "18",CCOL
          MOVE      TEN1,CVERT
          MOVE      ONE,CCENTRY
          MOVE      ZERO,CDEFDTE
          MOVE      CCC,CCENT
.
FDAT1000  CALL      KEYDATE
          BRANCH    CQUEST,FDAT1000
          BRANCH    OVRCD,FDAT9000                * anything input
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
          MOVE      ZERO,EXIT
.
          DISPLAY   *P1:13,"NB: All cards with expiry dates ":
                    "up to and including the above ":
                    "date,";
          DISPLAY   *P4:14," will be deleted."
          GOTO      FDAT9999
.
FDAT9000  MOVE      ONE,EXIT
.
FDAT9999  RETURN
+
.**************************************************************************
.*  CTYP0000  :  keyin card type from/to                                  *
.**************************************************************************
CTYP0000  DISPLAY   *P1:7,*EF,"Card Type From : ";
          MOVE      "18",ECOL
          MOVE      "7",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      CATct,CODE
          MOVE      ZERO,CKYIMAND           * Not Mandatory
.
          CALL      PATCODKY
          BRANCH    EXIT,CTYP2000,CTYP9500
.
          MOVE      ACODE,CARDTYP1
          DISPLAY   *P25:7,*V2LON,TDESC;
          GOTO      CTYP3000
.
CTYP2000  DISPLAY   *P18:7,*EL,*V2LON,"Start "
          MOVE      SP3,CARDTYP1
.
CTYP3000  DISPLAY   *P1:9,*EF,"Card Type To   : ";
          MOVE      "18",ECOL
          MOVE      "9",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      CATct,CODE
          MOVE      ZERO,CKYIMAND           * Not Mandatory
.
          CALL      PATCODKY
          BRANCH    EXIT,CTYP4000,CTYP9500
.
          MOVE      ACODE,CARDTYP2
          DISPLAY   *P25:9,*V2LON,TDESC;
          GOTO      CTYP5000
.
CTYP4000  DISPLAY   *P18:9,*EL,*V2LON,"Finish"
          MOVE      "~~~",CARDTYP2
.
CTYP5000  MATCH     CARDTYP1,CARDTYP2
          GOTO      CTYP9000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,"Starting card type must be before the ":
                    "ending card type.  ";
          CALL      HITENTER
          GOTO      CTYP0000
.
CTYP9000  MOVE      ZERO,EXIT
          GOTO      CTYP9999
.
CTYP9500  MOVE      ONE,EXIT
CTYP9999  RETURN
+
.*******************************************************************************
.*                                  PEXP0000                                   *
.* Print/Delete Concession Cards                         called by : MAINLINE  *
.*******************************************************************************
PEXP0000  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON:
                    "Printing Deleted Concession Cards.  Please wait ... ";
.
          MOVE      ZERO,COUNT                   * initialize counter
          MOVE      ZERO,CPAGENO                 * initialize page no.
.
          PACK      DISPSTRT,CARDTYP1,SP70
          MATCH     SP3,CARDTYP1
          IF        @EQUAL
            MOVE      "Start ",DISPSTRT
          ENDIF
          PACK      DISPFNSH,CARDTYP2,SP70
          MATCH     "~~~",CARDTYP2
          IF        @EQUAL
            MOVE      "Finish",DISPFNSH
          ENDIF
          MOVE      SP70,DISPDAT2
          MATCH     SP70,FROMDATE
          IF        !@EQUAL
            UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,DISPDAT2
          ENDIF
.
          CALL      HEAD0000                     * print headings
.
          CALL      DFRS0000                     * get deletion reason
.
          PACK      KEY19,CARDTYP1,SP70
          CALL      RSPMCCD2                     * loop through concession cards
PEXP1100  CALL      RKPMCCD2
          BRANCH    OVRCD,PEXP9000               * end-of-file
.
          MATCH     PMCDCTYP,CARDTYP2            * check card type to
          GOTO      PEXP9000 IF LESS
.
          MATCH     PMCDEXDT,FROMDATE            * Check if record has expired
          GOTO      PEXP1100 IF LESS
.
.         Get the PMI details for HL7 messages
.
          MOVE      PMCDURNO,KEY8
          CALL      RDMAST1
          CALL      RDPMPX21
.
          PACK      KEY19,PMCDCTYP,PMCDEXDT,PMCDURNO
          CALL      DEPMCCD2                     * Delete concession card
          CALL      PMIGTNID                   * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      ONE,HL7TRGID
          MOVE      SP8,HL7INCLD
          PROC      DGCLICUP                     * send PMI update message
.
          CALL      ADUTCDEL                     * write delete record to Audit
.
          CALL      RSPMCCD2                     * Positional read after delete
          ADD       ONE,COUNT
.
.         print detail line
.
          COMPARE   "55",CLNO                    * page overflow ?
          CALL      HEAD0000 IF NOT LESS
.
          PACK      DIM20,SP70
          PACK      KEY8,PMCDURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PEXP5000
.
          MOVE      PSNAME,DIM20
.
PEXP5000  MOVE      SP70,DISPDAT1
          MATCH     SP70,PMCDEXDT
          IF        !@EQUAL
            UNPACK    PMCDEXDT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,DISPDAT1
          ENDIF
.
          PRINT     *2,"| ",PMCDURNO,*13,"| ",DIM20,*36,"| ":
                    PMCDCTYP,*42,"| ",PMCDCNUM,*65,"| ":
                    DISPDAT1,"|"
.
          ADD       ONE,CLNO                     * increase current line no by 1
          GOTO      PEXP1100
.
.         End of Range/End of File
.
PEXP9000  PRINT     *2,DRAW,*N,*N,*2,COUNT:
                    " concession card(s) deleted."
          PRINT     *N,*2,"***  End of Report  ***"
.
PEXP9999  RETURN
.
.------------------------------------------------------------
. Add Update Record to Audit File (PATCCHFD)
.------------------------------------------------------------
ADUTCDEL  COMPARE   ONE,AUDTATRB
          GOTO      ADUTCD99 IF EQUAL
.
          CALL      CLPATCCH
          PACK      PTCCURNO,PMCDURNO,SP70
          PACK      PTCCCTYP,PMCDCTYP,SP70       * Card Type (cat ct)
          PACK      PTCCCNUM,PMCDCNUM,SP70       * Card Number
          PACK      PTCCEXDT,PMCDEXDT,SP70       * Card Expiry Date
          PACK      PTCCDVAC,PMCDDVAC,SP70       * DVA card colour
          MOVE      "D",PTCCUPDT                 * Update Type
          MOVE      DELTRESN,PTCCUPDR
. 
ADUTCD05  CALL      IBACLOCK
          PACK      PTCCDATE,CCC,CYY,CMM,CDD     * Date
          REP       " 0",PTCCDATE
          MOVE      CTIMEIS,PTCCTIME
          MOVE      PMCDCUID,PTCCURID            * Web UserId

ADUTCD10  PACK      KEY35,PTCCURNO,PTCCCTYP,PTCCDATE,PTCCTIME,SP70
          CALL      RDPTCCH1
          IF        OVRCD=1
            CALL      WRPTCCH1
            GOTO      ADUTCD99
          ELSE
            CALL      IBACLOCK                   * Set new date and time and
            PACK      PTCCDATE,CCC,CYY,CMM,CDD   * try write again
            REP       " 0",PTCCDATE
            CLOCK     TIME,CTIMEIS
            PACK      PTCCTIME,CTIMEIS
            REP       " 0",PTCCTIME
            GOTO      ADUTCD10
          ENDIF
          GOTO        ADUTCD99
.
ADUTCD99  RETURN

.*******************************************************************************
.*                                  DFRS0000                                   *
.*        routine headings                               called by : PEXP0000  *
.*******************************************************************************
DFRS0000  MOVE      SP70,DELTRESN
          PACK      KEY5,CATcH,SP70
          CALL      RDSCODE1
DFRS0010  CALL      RDKCODE1
          BRANCH    OVRCD,DFRS9999 
.
          MATCH     TCODE,CATcH                * check for cat code "cH"
          GOTO      DFRS9999 IF NOT EQUAL
.
          MATCH     "I",PTCOACTV               * check for inactive 
          GOTO      DFRS0010 IF EQUAL
.
          MATCH     "D",TCINDC1                * Cat code cH Ind = D
          IF         @EQUAL
            MOVE      ACODE,DELTRESN
            GOTO      DFRS9999
          ELSE
            GOTO      DFRS0010
          ENDIF
.
DFRS9999  RETURN
.*******************************************************************************
.*                                  HEAD0000                                   *
.*        routine headings                               called by : PRNT0000  *
.*******************************************************************************
HEAD0000  COMPARE   ZERO,CPAGENO
          GOTO      HEAD1000 IF EQUAL
          PRINT     *2,DRAW
.
HEAD1000  CLOCK     TIME,CTIMEIS
          CALL      HEAD80
.
          PRINT     *N,*2,"Deleted Concession Card Types from ",DISPSTRT:
                          " to ",DISPFNSH;
          PRINT     *N,*2,"(With expiry dates up to and including ",DISPDAT2,")"
          ADD       TWO,CLNO
.
          PRINT     *N,*2,DRAW:
                    *N,*2,"| U/R   ",*13,"| Surname ":
                       *36,"| Typ ",*42,"| Card Number ":
                       *65,"| Exp Date   |":
                    *N,*2,DRAW
.
          ADD       FOUR,CLNO                    * increase current line no by 4
.
HEAD9999  RETURN
.
          INC       STD001IO
.
          INC       DGCLICUP
          INC       PATCODKY
          INC       PMIGTNID
          INC       CLPATCCH
.
          INC       NHIMASIO/INC
          INC       PMSCCDIO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PATNIDIO/INC
          INC       PMSPX2IO/INC
          INC       WEBSECIO/INC
          INC       PATCCHIO/INC

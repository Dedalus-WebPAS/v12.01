.***************************************************************************
.* System    :   PRIVATE PRACTICE SYSTEM                                   *
.* Program   :   IBAPRI30                                                  *
.* Desc      :   Medicare Gap File Maintenance                             *
.***************************************************************************
.* Date      :   16/01/92                                                  *
.* Author    :   DIG                                                       *
.* Mods.     :   V6.00.01 16/12/1992  Steve Armstrong    SRF 119526        *
.*                        Fixed bug which was resetting gap value to zero  *
.***************************************************************************
.
          INC       STD001FD
.
          INC       PRIMGPFD/INC
.
.**************************************************************************
.*                            GLOBAL VARIABLES                            *
.**************************************************************************
DISPVALU  DIM       7
.
GAPVALUE  FORM      3.2
.
PRGID     INIT      "IBAPRI30"
PRGNAM    INIT      "Medicare Gap File Maintenance"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
          CALL      INIT0000                * initialization and open files
.
ML0100    CALL      OPTN0000                * select options
          BRANCH    EXIT,ML9999             * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML2000,ML3000
.
. ---- add a gap value ----
.
ML1000    CALL      ENTR0000                * enter date effective
          BRANCH    EXIT,ML0100
.
          CALL      SCRN0000                * display screen details
.
          CALL      GAPV0000                * keyin gap value
.
          CALL      SEL0000                 * select field to modify
          BRANCH    EXIT,ML1000               or ok to post
.
          CALL      WRT0000                 * write to file
.
          GOTO      ML1000
.
. ---- modify a gap value ----
.
ML2000    CALL      CLR0000                 * clear input variables
.
          CALL      ENTR0000                * enter date effective
          BRANCH    EXIT,ML0100
.
          CALL      SCRN0000                * display screen details
.
          CALL      DISP0000                * display details
.
          CALL      SEL0000                 * select field to modify
          BRANCH    EXIT,ML2000
.
          CALL      UPD0000                 * update record
.
          GOTO      ML2000
.
. ---- delete a gap value ----
.
ML3000    CALL      CLR0000                 * clear input variables
.
          CALL      ENTR0000                * enter date effective
          BRANCH    EXIT,ML0100
.
          CALL      SCRN0000                * display screen details
.
          CALL      DISP0000                * display detail
.
          CALL      CONT0000                * ok to delete ?
          BRANCH    EXIT,ML3000
.
          CALL      DEL0000                 * delete this record
.
          GOTO      ML3000
.
ML9999    CHAIN     PGM                     * chain back to program
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialization                               *
.*  The initialization routine is used to display headings and open files.  *
.****************************************************************************
INIT0000  CALL      DISPHEAD                * display heading
.
          DISPLAY   *P50:24,*EL,"Opening ":
                    *P60:24,"primgpaf";
          OPEN      PRIMGPA1,"primgpaf"
.  
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.*                                                                          *
.*    valid options     --->  (0) Exit                                      *
.*                            (1) Insert                                    *
.*                            (2) Change                                    *
.*                            (3) Delete                                    *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000  MOVE      FALSE,EXIT
.
          CALL      SELOPTN                 * select option
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9000 IF EQUAL       * exit
.
          COMPARE   FOUR,COPTION
          GOTO      OPTN9999 IF LESS        * invalid option, re-enter
.
          BEEP
.
          GOTO      OPTN0000
.
.------ exit option chosen ------
.
OPTN9000  MOVE      TRUE,EXIT    
.
OPTN9999  RETURN
+
.**************************************************************************
.*                                  CLR0000                               *
.*                            clear global variables                      *
.**************************************************************************
CLR0000   MOVE      SP8,PRMGDATE
          MOVE      ZERO,PRMGVALU
          PACK      PRMGSPAR,SP20,SP20
.
CLR9999   RETURN
+
.**************************************************************************
.*                               ENTR0000                                 *
.*                       enter date effective                             *
.**************************************************************************
ENTR0000  CALL      CLR0000                 * clear input variables
.
          MOVE      FALSE,EXIT
          DISPLAY   *P1:4,*EF:
                    *P4:4,"Date Effective : ";
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          REP       " 0",CCENT
          MOVE      FOUR,CVERT
          MOVE      TWENTY1,CCOL
.
          CALL      KEYDATE                 * keyin the date
          BRANCH    OVRCD,ENTR9000
.
          PACK      PRMGDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      PRMGDATE,KEY8
          CALL      RDPRMG1                 * read the Medicare Gap file
          BRANCH    OVRCD,ENTR8000
.
          CALL      RKPRMG1                 * get the next record from the
          BRANCH    OVRCD,ENTR1000            Medicare Gap file
.
.------ we have records on file after the date entered ------
.
ENTR0500  DISPLAY   *P1:24,*EL,*B,*V2LON,"ERROR",*HOFF," - Records exist":
                           " after the given effective date - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      ENTR0000
.
.------ the date entered exists with no subsequent records ------
.
ENTR1000  COMPARE   ONE,COPTION             * see if we are in Insert mode
          GOTO      ENTR9999 IF NOT EQUAL
.         
          DISPLAY   *P1:24,*EL,*B,"Record already exists - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      ENTR0000
.
.------ the date entered does not exist ------
.
ENTR8000  BRANCH    COPTION,ENTR8100        * skip if we are in Insert mode
.
          DISPLAY   *P1:24,*EL,*B,"Record does not exist - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      ENTR0000
.
.------ the date entered does not exist so still check to see if we ------
.------ have any subsequent records ------
.
ENTR8100  CALL      RKPRMG1
          BRANCH    OVRCD,ENTR9999
.
          GOTO      ENTR0500
.
.------ no effective date entered ------
.
ENTR9000  MOVE      TRUE,EXIT
.
ENTR9999  RETURN
+
.*************************************************************************
.*                                SCRN0000                               *
.*                      display the screen details                       *
.*************************************************************************
SCRN0000  DISPLAY   *P1:6,*V2LON,ONE:
                    *P2:6,*HOFF,". Gap Value : ";
.
SCRN9999  RETURN
+
.*************************************************************************
.*                                  GAPV0000                             *
.*                        Keyin the Medicare Gap value                   *
.*************************************************************************
GAPV0000  MOVE      ZERO,GAPVALUE
          DISPLAY   *P16:6,*EL,UNDLN6;
          KEYIN     *P16:6,*V2LON,GAPVALUE;
.
          COMPARE   ONE,GAPVALUE            * check for a valid value
          GOTO      GAPV9000 IF LESS
.
          MOVE      "$999.99",DISPVALU
          FEDIT     GAPVALUE,DISPVALU
          DISPLAY   *P16:6,*V2LON,DISPVALU;
.
          GOTO      GAPV9999
.
.------ invalid medicare gap value entered ------
.
GAPV9000  DISPLAY   *P1:24,*EL,*B,"Invalid Gap Value entered - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      GAPV0000
.
GAPV9999  RETURN
+
.**************************************************************************
.*                                   SEL0000                              *
.*                         select field to modify or post                 *
.**************************************************************************
SEL0000   MOVE      FALSE,EXIT
.
          CALL      MAINQST                 * select an item, Post, Cancel
.
          COMPARE   ZERO,CCITEM             * ok to post
          GOTO      SEL9999 IF EQUAL
.
          COMPARE   "-1",CCITEM             * cancel option
          GOTO      SEL9000 IF EQUAL
.
          COMPARE   TWO,CCITEM
          GOTO      SEL0000 IF NOT LESS     * invalid option
.
          BRANCH    CCITEM,SEL1000
.
.------ Keyin the Medicare Gap value ------
.
SEL1000   CALL      GAPV0000
.
          GOTO      SEL0000
.
.------ Cancel option chosen ------
.
SEL9000   MOVE      TRUE,EXIT
.
SEL9999   RETURN
+
.**************************************************************************
.*                              WRT0000                                   *
.*                          write a new record                            *
.**************************************************************************
WRT0000   MOVE      PRMGDATE,KEY8
          MOVE      GAPVALUE,PRMGVALU
          PACK      PRMGSPAR,SP20,SP20
.
          CALL      WRPRMG1                 * write to the Medicare Gap file
.
WRT9999   RETURN
+
.*************************************************************************
.*                                DISP0000                               *
.*                   display all data variable details                   *
.*************************************************************************
DISP0000  MOVE      "$999.99",DISPVALU
          FEDIT     PRMGVALU,DISPVALU
          MOVE      PRMGVALU,GAPVALUE
          DISPLAY   *P16:6,*V2LON,DISPVALU;
.
DISP9999  RETURN
+
.**************************************************************************
.*                              CONT0000                                  *
.*                         check if OK to Delete                          *
.**************************************************************************
CONT0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:24,*EL,"O.K. To Delete (",*V2LON,"Y",*HOFF,"/",*V2LON:
                    "N",*HOFF,") ? ";
          KEYIN     *V2LON,ANS
.
          MATCH     ANSN,ANS                * see if No entered
          GOTO      CONT9000 IF EQUAL
.
          MATCH     ANSY,ANS                * see if Yes entered
          GOTO      CONT9999 IF EQUAL
.
          BEEP                              * invalid option
          GOTO      CONT0000
.
.------ Not O.K. to Delete ------
.
CONT9000  MOVE      TRUE,EXIT
.
CONT9999  RETURN
+
.***************************************************************************
.*                               UPD0000                                   *
.*                           update record                                 *
.***************************************************************************
UPD0000   MOVE      PRMGDATE,KEY8
          CALL      RDPRMG1                 * read the Medicare Gap file
          IF        OVRCD = 1
             DISPLAY   *P1:24,*EL,"Record not found,  ";
             CALL      HITENTER
          ELSE
             MOVE      GAPVALUE,PRMGVALU
.
             CALL      UPPRMG1              * update the Medicare Gap file
          ENDIF
.
UPD9999   RETURN
+
.****************************************************************************
.*                              DEL0000                                     *
.*                         delete an existing record                        *
.****************************************************************************
DEL0000   MOVE      PRMGDATE,KEY8
          CALL      RDPRMG1                 * read the Medicare Gap file
.
          CALL      DEPRMG1                 * delete from the Medicare Gap file
.
DEL9999   RETURN
+
          INC       STD001IO
          INC       PRIMGPIO/INC
.

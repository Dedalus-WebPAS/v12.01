.------------------------------------------------------------------------------
. Program       : NHIWEB91
.
. Function      : National Health System
.                 Medical Warning Codes Table Maintenance Server
.
. Modifications :
.      V10.04.01  31.03.2014  B.G.Ackland  CAR 299363
.                 Replace META Tag Redirect with Javascript in Common RDIREC00
.                 Routine
.------------------------------------------------------------------------------
.      V9.05.B01  30/01/2006  Ebon Clements CAR 93186
.                 Mods for REDIRECT length change. Recompiled for IBAWEBDF
.      V9.03.01   13/08/2003  Sylvek Litewka  CAR 41293
.                 Removed references to WEBSCHFD and WEBSCHIO. These are now
.                 included in IBAWEBDF.PBL and IBAWEBCD.PBL.
.------------------------------------------------------------------------------
.      V9.02.00   25/02/2003  Tracey Nguyen SRF 34658
.                 Created new web server program for NHI Medical Warning Codes
.                 Table (nhimccaf) Maintenance
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
. FD Includes
.------------------------------------------------------------------------------
.
          INC       IBAWEBDF
          INC       WEBDATDF
          INC       RSHWEBDF
          INC       PATCONFD/INC
.
          INC       NHIMCCFD/INC            * NHI Medical warning code file
          INC       NHIMKCFD/INC            * Medical warning code keyword srch 

.-----------------------------------------------------------------------------
. Variable Declarations
.-----------------------------------------------------------------------------
.
. CGI Parameters
. **************
.
NEXTPAGE  FORM      1         * Nextpage parameter
UPDTTYPE  DIM       1         * Update Type (A)dd,(U)pdate,(D)elete,Blank=disp
STARTKEY  DIM       2         * Starting Key for search
.
. NHI Medical Warning Code (NHIMCCFD) parameters
.
NHIMC001  DIM       2         * NHI medical warning Coding System
NHIMC002  DIM       6         * NHI medical warning code
NHIMC003  DIM       50        * NHI medical warning description
.
. Program Variables
. *****************
.
SAVKEY8   DIM       8         
SAVKEY23  DIM       23      
+
.-----------------------------------------------------------------------------
.
PRGID     INIT      "NHIWEB91"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "NHI Medical Warning Code Table Maintenance Server"
+
.------------------------------------------------------------------------------
.
          CALL      WEBINT00                * Initialise Fifo Etc.
          CALL      INIT0000                * Open Files
.
MAIN1000  CALL      WEBRED00                * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO          * End Server Process if Option=999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00                * Output File & Write HTML Pg Header
          GOTO      MAIN1000
.
MAIN1100  CALL      NHIMCC00                * Medical Warning Codes Maintenance
          BRANCH    EXIT,MAIN1000
          CALL      NXTPG000                * next page
          GOTO      MAIN1000
. 
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
+
.------------------------------------------------------------------------------
.  Next Page URL
.------------------------------------------------------------------------------
.
NXTPG000  IF        NEXTPAGE=0
            CALL      WINCLS00              * Refresh parent and close window
          ENDIF

          IF        NEXTPAGE=1
            CALL      RSAVED00              * Redirect URL
          ENDIF
.
          IF        NEXTPAGE=2
            CALL      GOBACK00              * Go Back 1 html page
          ENDIF
.
          IF        NEXTPAGE=3
            CALL      DAH20000              * Go Back 2 html pages
          ENDIF
.
NXTPG999  RETURN
.------------------------------------------------------------------------------
.  Goes back 1 page
.------------------------------------------------------------------------------
.
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"

          CALL      CLOSHTML
.
GOBACK99  RETURN
+
.------------------------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------------------------
.
INIT0000  OPEN     NHIMCCA1,"nhimccaf"      
          OPEN     NHIMKCA1,"nhimkcaf"
          OPEN     NHIMKCA2,"nhimkcaf"
.
INIT0099  RETURN
+
.------------------------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------------------------
.
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,UPDTTYPE
          MOVE      ZERO,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      ZERO,STARTNUM
          MOVE      ZERO,NORECORD
          MOVE      SP70,STARTKEY
          MOVE      SP70,KEYWORDS
.
          MOVE      SP70,NHIMC001
          MOVE      SP70,NHIMC002
          MOVE      SP70,NHIMC003
.
CLRPAR99  RETURN
+
.------------------------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------------------------
.
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTKEY
            PACK      STARTKEY,STARTKEY,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "keywords=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,KEYWORDS
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nhimc001=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NHIMC001
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nhimc002=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NHIMC002
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nhimc003=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NHIMC003
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
+
.------------------------------------------------------------------------------
. Medical Warning Codes Maintenance
.------------------------------------------------------------------------------
.
NHIMCC00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE            * Display form-action
          GOTO      NHIMCC40 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add form-action
          GOTO      NHIMCC10 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update form-action
          GOTO      NHIMCC20 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete form-action
          GOTO      NHIMCC30 IF EQUAL
.
          GOTO      NHIMCC93
.
.
.         ************ ADD FORM-ACTION ***********
.
NHIMCC10  SQUEEZE   NHIMC001
          SQUEEZE   NHIMC002
          PACK      NHIMC001,NHIMC001,SP10
          PACK      NHIMC002,NHIMC002,SP10 
          CALL      CHKNHC00                * No, Checks mandatory fields
          BRANCH    EXIT,NHIMCC99           * Exit if error found
.
          PACK      KEY8,NHIMC001,NHIMC002,SP10
          CALL      RDNHMC1
          COMPARE   ZERO,OVRCD              * Does record already exist?
          GOTO      NHIMCC92 IF EQUAL       * Yes, display error message 
.
          CALL      NHCSAV00                * No, save input variables
          PACK      KEY8,NHIMC001,NHIMC002,SP10 
          CALL      WRNHMC1                 * Write new record
          PROC      NHIMKCUP                * Update keyword search file
.
          MOVE      ZERO,EXIT
          GOTO      NHIMCC99
.
.         ************ UPDATE FORM-ACTION **********
.
NHIMCC20  PACK      NHIMC001,NHIMC001,SP10
          PACK      NHIMC002,NHIMC002,SP10
          PACK      KEY8,NHIMC001,NHIMC002,SP10
          CALL      RDNHMC1                 * Does record exist?
          BRANCH    OVRCD,NHIMCC91          * No, Display error message
.
          CALL      CHKNHC00                * Yes, Checks mandatory fields
          BRANCH    EXIT,NHIMCC99           * Exit if error found
.
          CALL      NHCSAV00                * Saves input variables
          CALL      UPNHMC1                 * Update record 
          PROC      NHIMKCUP                * Update keyword search file
.
          MOVE      ZERO,EXIT
          GOTO      NHIMCC99
.
.         ************ DELETE FORM-ACTION **********
.
NHIMCC30  PACK      NHIMC001,NHIMC001,SP10
          PACK      NHIMC002,NHIMC002,SP10
          PACK      KEY8,NHIMC001,NHIMC002,SP10
          CALL      RDNHMC1                 * Does record exist?
          BRANCH    OVRCD,NHIMCC91          * No, Display error message
          CALL      DENHMC1                 * Yes, Delete record
          PROC      NHIMKCUP                * Update keyword search file
.
          MOVE      ZERO,EXIT
          GOTO      NHIMCC99
.
.         ************ DISPLAY FORM-ACTION **********
.
NHIMCC40  PACK      NHIMC001,NHIMC001,SP10
          PACK      NHIMC002,NHIMC002,SP10
          PACK      KEY8,NHIMC001,NHIMC002,SP10 
          CALL      RDNHMC1
          IF        OVRCD=1
            CALL      CLRNHC00              * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Medical Warning Codes Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00                * Output File & Write HTML Pg Header
          BRANCH    EXIT,NHIMCC99
.
          CALL      WEBBOD00                * Process Web Body
.
          CALL      WEBEND00                * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      NHIMCC99
.
.         ************ DISPLAY ERROR MESSAGE **********
.
NHIMCC91  MOVE      "Medical Warning Codes record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NHIMCC99
.
NHIMCC92  MOVE      "Medical Warning Codes record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NHIMCC99
.
NHIMCC93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NHIMCC99
.
NHIMCC99  RETURN
+
.------------------------------------------------------------------------------
. Initialize's file data (Medical Warning Codes Maintenance)
.------------------------------------------------------------------------------
.
CLRNHC00  MOVE      SP70,NHMCCSYS           * NHI Medical Warning Coding System
          MOVE      SP70,NHMCCOD            * NHI Medical Warning code
          MOVE      SP70,NHMCDES            * NHI Medical Warning description
          MOVE      SP70,NHMCSPA            * spare variable
.
CLRNHC99  RETURN
+
.------------------------------------------------------------------------------
. Moves cgi variables into file variables (Medical Warning Codes Maintenance)
.------------------------------------------------------------------------------
.
NHCSAV00  MOVE      NHIMC001,NHMCCSYS
          MOVE      NHIMC002,NHMCCOD
          MOVE      NHIMC003,NHMCDES
          MOVE      SP70,NHMCSPA
.
NHCSAV99  RETURN
+
.------------------------------------------------------------------------------
. Checks mandatory fields (Medical Warning Codes Maintenance)
.------------------------------------------------------------------------------
.
CHKNHC00  RESET     NHIMC001                * NHI Medical Warning Coding System
          MATCH     SP2,NHIMC001
          GOTO      CHKNHC90 IF EQUAL
.
          RESET     NHIMC002
          MATCH     SP6,NHIMC002            * NHI Medical Warning code
          GOTO      CHKNHC91 IF EQUAL
.
          RESET     NHIMC003                * NHI Medical Warning description
          MATCH     SP70,NHIMC003
          GOTO      CHKNHC92 IF EQUAL
.
          GOTO      CHKNHC98
.
CHKNHC90  MOVE      "Medical Warning Coding System is mandatory",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKNHC99
.
CHKNHC91  MOVE      "Medical Warning Code is mandatory",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKNHC99
.
CHKNHC92  MOVE      "Medical Warning Description is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKNHC99
.
CHKNHC98  MOVE      ZERO,EXIT
.
CHKNHC99  RETURN
+
.------------------------------------------------------------------------------
. Process Fields called from WEBBOD00
.------------------------------------------------------------------------------
.
PROFLD00  BRANCH    REPORTNO,PROFLD10
.
          GOTO      PROFLD99
.
. Medical Warning Codes Maintenance
.
PROFLD10  CALL      NHIC0000                * Medical Warning Codes Maintenance
          GOTO      PROFLD99
.
PROFLD99  RETURN
+
.------------------------------------------------------------------------------
. Medical Warning Codes Maintenance
.------------------------------------------------------------------------------
.
NHIC0000  BRANCH    FLDITMNO,NHIC0100,NHIC0200,NHIC0300,NHIC0400,NHIC0500:
                             NHIC0600,NHIC0700
.
          GOTO      NHIC9999
.
NHIC0100  CALL      NEXTGR00                * Link to Next Group
          GOTO      NHIC9999
.
NHIC0200  CALL      PREVGR00                * Link to Last Group
          GOTO      NHIC9999
.
NHIC0300  CALL      NHITB000                * Table of Medical Warning Codes
          GOTO      NHIC9999
.
NHIC0400  WRITE     HTMLFILE;NHMCCSYS;      * Medical Warning Coding System
          GOTO      NHIC9999
.
NHIC0500  WRITE     HTMLFILE;NHMCCOD;       * Medical Warning Code
          GOTO      NHIC9999
.
NHIC0600  WRITE     HTMLFILE;NHMCDES;       * Medical Warning Description
          GOTO      NHIC9999
.
NHIC0700  WRITE     HTMLFILE;STARTKEY;      * Startkey for search
          GOTO      NHIC9999
.
NHIC9999  RETURN
+
.------------------------------------------------------------
. Link to Next set of Medical Warning Codes
.------------------------------------------------------------
.
NEXTGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
          REP       "'~",KEYWORDS
.
          WRITE     HTMLFILE;"nhiweb91.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY:
                                 "&keywords=",KEYWORDS;
.
          REP       "~'",KEYWORDS
.
NEXTGR99  RETURN
+
.------------------------------------------------------------------------------
. Link to previous set of Medical Warning Codes
.------------------------------------------------------------------------------
.
PREVGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE    ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;"nhiweb91.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&keywords=",KEYWORDS;
.
.
PREVGR99  RETURN
+
.------------------------------------------------------------------------------
. Medical Warning Codes Table
.------------------------------------------------------------------------------
.
NHITB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      NHIHD000                * Output Table Heading
.
          IF        NORECORD=0
            MOVE      "13",NORECORD         * Set Default No Records to Display
          ENDIF
.
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          CALL      GETWRD00                * This routine Splits the KEYWORDS
                                            * field into KEYWRD01 to KEYWRD10 
                                            * (IBAWEBCD)
.
          STRIP     KEYWRD01
          PACK      KEY23,KEYWRD01,SP70
          CALL      RSNHMK2
NHITB100  CALL      RKNHMK2
          BRANCH    OVRCD,NHITB999
.
          MATCH     STARTKEY,SP2            * Search for all warning codes
          GOTO      NHITB200 IF EQUAL
.
          MATCH     STARTKEY,NHMKKTYP       * Search within startkey specified 
          GOTO      NHITB100 IF NOT EQUAL    
.
NHITB200  PACK      KEY8,NHMKKTYP,NHMKKITM
          CALL      RDNHMC1
          BRANCH    OVRCD,NHITB100
.
          CALL      SNHMKB00
          BRANCH    OVRCD,NHITB100,NHITB999
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      NHITB100
          ENDIF
.
          CALL      NHIRW000                * Med.Warning Code Table Details Row
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      NHITB100 IF NOT EQUAL
.
NHITB999  RETURN
+
.------------------------------------------------------------------------------
. Medical Warning Codes (TABLE HEADING)
.------------------------------------------------------------------------------
.
NHIHD000  WRITE     HTMLFILE;"<tr align=center>":
                             "<td class=HeadingCell colspan=4><b>":
                             "You Searched for ":
                               KEYWORDS,"</td>":
                             "</tr>":
                             "<tr>":
                               "<td class=HeadingCell width=25%>":
                               "<b> Coding System </b></td>":
                               "<td class=HeadingCell>":
                               "<b> Warning Code </b></td>":
                               "<td class=HeadingCell>":
                               "<b> Description </b></td>":
                             "</tr>"
.
NHIHD999  RETURN
+
.------------------------------------------------------------------------------
. Medical Warning Codes (TABLE ROW)
.------------------------------------------------------------------------------
.
NHIRW000  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail01":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&startkey=",STARTKEY:
                             "&nhimc001=",NHMCCSYS:
                             "&nhimc002=",NHMCCOD,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             NHMCCSYS,"</td>":
                             "<td>",NHMCCOD,"</td>":
                             "<td>",NHMCDES,"</td>":
                             "</tr>"
.
NHIRW999 RETURN
+
.------------------------------------------------------------------------------
. Check Medical Warning Record
.------------------------------------------------------------------------------
.
SNHMKB00  MATCH     KEYWRD01,NHMKKKWD
          GOTO      SNHMKB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY23,NHMKKKWD,NHMKKTYP,NHMKKITM
            CALL      SNHMKC00
            BRANCH    EXIT,SNHMKB91
            MOVE      SAVKEY23,KEY23
            CALL      RDNHMK2
          ENDIF
.
SNHMKB90  MOVE      ZERO,OVRCD
          GOTO      SNHMKB99
.
SNHMKB91  MOVE      ONE,OVRCD
          GOTO      SNHMKB99
.
SNHMKB92  MOVE      TWO,OVRCD
.
SNHMKB99  RETURN
+
.------------------------------------------------------------------------------
. Check for Matching Medical Warning Codes
.------------------------------------------------------------------------------
.
SNHMKC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY8,NHMKKTYP,NHMKKITM,SP70
.
SNHMKC10  COMPARE   KEYWRDNO,F2             * Loop thru each Key Word in Search
          GOTO      SNHMKC99 IF EQUAL       * Exit OK
.
          ADD       ONE,F2
.
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                                KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
.
          PACK      KEY23,NHMKKTYP,NHMKKITM,KEYWRDXX
          CALL      RDNHMK1                 * Check for Exact Match
          COMPARE   ZERO,OVRCD              * Match found?
          GOTO      SNHMKC10 IF EQUAL       * Yes

          CALL      RKNHMK1                 * No, Read Next Record
          BRANCH    OVRCD,SNHMKC90          * No Match So Exit No Match
.
          PACK      KEY8,NHMKKTYP,NHMKKITM,SP70
          MATCH     SAVKEY8,KEY8            * Match Key Fields
          GOTO      SNHMKC90 IF NOT EQUAL   * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     NHMKKKWD,KEYWRDXX       * Check Key Word
          GOTO      SNHMKC90 IF NOT EQUAL   * No Match So Exit
          GOTO      SNHMKC10                * Check Next Search Word
.
SNHMKC90  MOVE      ONE,EXIT
.
SNHMKC99  RETURN
+
.------------------------------------------------------------------------------
. I/O and Subroutine Includes
.------------------------------------------------------------------------------
.
          INC       IBAWEBCD
          INC       WEBDATCD
          INC       RSHWEBCD
          INC       DAYOFWEK
.
          INC       NHIMCCIO/INC            * Warning Code file
          INC       NHIMKCIO/INC            * Warning Code Keyword search
          INC       NHIMKCPR                * warning keywords records 

.------------------------------------------------------------------------------
. End Of Program
.------------------------------------------------------------------------------

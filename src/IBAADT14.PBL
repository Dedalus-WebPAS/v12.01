******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAADT14                                                 *
.* Description    :  Upload MBS Items                                         *
.******************************************************************************
.* Date           :  26/03/2020                                               *
.* Author         :  J.Tan                                                    *
.* Function       :  Upload MBS Items from ASCII file                         *
.* Modifications  :                                                           *
.*                   V11.02.01 02/02/2022 Alina.B TSK 0903886                 *
.*                   Recompiled for PATIKIPR- Made users able to be search by *
.*                                            long description                *
.*                   V11.00.04 27/01/2020 J.Tan   TSK 0838262                 *
.*                   Mod for NZ Private (Misc.group not mandatory)            *
.*                   V11.00.03 13/11/2020 J.Tan   TSK 0899391                 *
.*                   Fixed updating last record to item keyword               *
.*                   V11.00.02 11/11/2020 J.Tan   TSK 0899391                 *
.*                   Added PATIKIPR - updating item keyword                   *
.*                   V11.00.01 01/05/2020 J.Tan   TSK 0838262                 *
.*                   Mod validating effective dates                           *
.*                   V11.00.00 26/03/2020 J.Tan   TSK 0838262                 *
.*                   Created upload MBS patitemn file                         *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       IBAGSTFD/INC
          INC       PATCODFD/INC
          INC       PATITMFD/INC            * Item file
.
.         Temporary file for MBS item records which
.         are loaded from the upload file provided by the customer (PROC0000)
.
.         MBS Item Upload file
.
UITEMTXT  FILE       HL7, FIXED=1000        * Upload file for MBS item
.
.Name     Type     Size     Start
.----     ----     ----     -----
XITEMNO   DIM       9           9       ITEM NUMBER
XIALPHA   DIM       6           6       ITEM ALPHA KEY
XITMDES   DIM      40          40       ITEM DESCRIPTION
XIQIAMT   FORM     12.2         8       MBS AMOUNT
XIMSGRP   DIM       3           3       MISCELLANEOUS GROUP (CAT FI)
XITCLSS   DIM       3           3       SUGGESTED CLASS (CAT A )
XITEFDT   DIM       8           8       Effective from Date (CCYYMMDD)
XITETDT   DIM       8           8       Effective to Date (CCYYMMDD)
XIBAND1   DIM       3           3       THEATRE CLASS 1 (CAT TB)
XIBAND2   DIM       3           3       THEATRE CLASS 2 (CAT TB)

XIBAND3   DIM       3           3       THEATRE CLASS 3 (CAT TB)
XIBAND4   DIM       3           3       THEATRE CLASS 4 (CAT TB)
XIBAND5   DIM       3           3       THEATRE CLASS 5 (CAT TB)
XIBAND6   DIM       3           3       THEATRE CLASS 6 (CAT TB)
XIBAND7   DIM       3           3       THEATRE CLASS 7 (CAT TB)
XIBAND8   DIM       3           3       THEATRE CLASS 8 (CAT TB)
XIBAND9   DIM       3           3       THEATRE CLASS 9 (CAT TB)
XIBND10   DIM       3           3       THEATRE CLASS 10(CAT TB)
XIBND11   DIM       3           3       THEATRE CLASS 11(CAT TB)
XIBND12   DIM       3           3       THEATRE CLASS 12(CAT TB)

XIBND13   DIM       3           3       THEATRE CLASS 13(CAT TB)
XIBND14   DIM       3           3       THEATRE CLASS 14(CAT TB)
XIBND15   DIM       3           3       THEATRE CLASS 15(CAT TB)
XIBND16   DIM       3           3       THEATRE CLASS 16(CAT TB)
XITACTV   DIM       1           1       Active indicator
XITLINK   DIM       9           9       Linked item code (inactive
XITEXCL   FORM      1           2       Exclusion list item ?
XITIWRN   DIM       1           1       Warning to enter a Prosthetic
XITGSTA   FORM      1           2       Is GST applicable?
XITGSTC   DIM       6           6       GST Payable Code

XITDCSC   DIM       3           3       Daycase Suggested Class.
XITDESC   DIM       60          60      Long Item Description
XITTCER   DIM       3           3       Type of Certificate (CAT cr)
XITMSCH   DIM       1           1       Misc Service Charge Indicator
XITTCCD   DIM       1           1       Theatre Category Code
XITSERV   DIM       9           9       DVA Service Code
XITECEQ   DIM       11          11      Eclipse Equivalent
XITECTS   DIM       2           2       Eclipse Type of Service
XITLWRD   DIM       1           1       Eclipse Labour Ward Item
XITTACB   DIM       3           3       Theatre Average Calc. Band
.
.End of Record
.
. ----- Program Variables -----
.
ADDCOUNT  FORM      8
CFEETYP   FORM      1
CMDLINE   DIM       127
ERRFLAG   FORM      1
ERRDATE   FORM      1
ERRCOUNT  FORM      8
ERRORMSG  DIM       40
FNAMEI    DIM       150
LINECNTR  FORM      8
ITEMDESC  DIM       20
PATHNAME  DIM       150
USERID    DIM       10
UPDCOUNT  FORM      8
VALFDATE  DIM       8
VALTDATE  DIM       8
WORKDATE  DATE      8
.
XFLD0001  DIM       30
XFLD0002  DIM       30
XFLD0003  DIM       40
XFLD0004  DIM       30
XFLD0005  DIM       30
XFLD0006  DIM       30
XFLD0007  DIM       30
XFLD0008  DIM       30
XFLD0009  DIM       30
XFLD0010  DIM       30
.
XFLD0011  DIM       30
XFLD0012  DIM       30
XFLD0013  DIM       30
XFLD0014  DIM       30
XFLD0015  DIM       30
XFLD0016  DIM       30
XFLD0017  DIM       30
XFLD0018  DIM       30
XFLD0019  DIM       30
XFLD0020  DIM       30
.
XFLD0021  DIM       30
XFLD0022  DIM       30
XFLD0023  DIM       30
XFLD0024  DIM       30
XFLD0025  DIM       30
XFLD0026  DIM       30
XFLD0027  DIM       30
XFLD0028  DIM       30
XFLD0029  DIM       30
XFLD0030  DIM       30

XFLD0031  DIM       30
XFLD0032  DIM       60
XFLD0033  DIM       30
XFLD0034  DIM       30
XFLD0035  DIM       30
XFLD0036  DIM       30
XFLD0037  DIM       30
XFLD0038  DIM       30
XFLD0039  DIM       30
XFLD0040  DIM       30
.
PRGID     INIT      "IBAADT14"
PRGNAM    INIT      "Upload & Update MBS Item"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000                * Initailize variables & open files
          CALL      KOPT0000                * Keyin Upload or Update option
          BRANCH    EXIT,ML9999
.
          BRANCH    COPTION,ML1000
          GOTO      ML9999
.
ML1000    CALL      KASC0000                * Keyin ascii file
          BRANCH    EXIT,ML9900
.
ML1100    CALL      SCRD0000                * Display screen
.
          CALL      KUSR0000                * Keyin user id
          BRANCH    EXIT,ML0000
.
          CALL      KPTH0000                * Keyin pathname
          BRANCH    EXIT,ML0000
.
          CALL      SFLD0000
          BRANCH    EXIT,ML1100
.
          CALL      PRHD0000                * Print header
.
          CALL      CTOT0000                * Clear total vars
.
          CALL      PROC0000                * Updating file
.
          CALL      PTOT0000                * Print report totals
.
          GOTO      ML9999
.
ML9900    CALL      HEAD132
          PRINT     *N,"Failed to open upload file. "
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000                                   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patitemn"
          OPEN      PATITEM1,"patitemn"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"ibagstaf";
          OPEN      IBAGSTA1,"ibagstaf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN9;*212,CFEETYP
.
INIT9999  RETURN
+
.******************************************************************************
.*                                 KOPT0000                                   *
.*                            Choose Upload or Update files                   *
.******************************************************************************
KOPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COPTION
          DISPLAY   *P1:6,*EF:
                    *P1:6,*V2LON,"0",*HOFF," = Exit":
                    *P1:7,*V2LON,"1",*HOFF," = Upload MBS Items";
          DISPLAY   *P1:9,"Select Option : ";
.
KOPT1000  KEYIN     *P17:9,*V2LON,COPTION;
          BRANCH    COPTION,KOPT9999
.
          COMPARE   ZERO,COPTION
          GOTO      KOPT9000 IF EQUAL
          BEEP
          GOTO      KOPT1000
.
KOPT9000  MOVE      ONE,EXIT
KOPT9999  RETURN
+
.******************************************************************************
.*                                 KASC0000                                   *
.*                            Keyin ASCI file                                 *
.******************************************************************************
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEI WITH SP70,SP70,SP70
.
          DISPLAY   *P19:13,*EF,"MBS Item Load File";
.
          KEYIN     *P1:14,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEI
.
          PACK      FNAMEI,FNAMEI,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEI
          GOTO      KASC9000 IF EQUAL
.
          SCAN      ".txt",FNAMEI
          IF        !@EQUAL
            SQUEEZE   FNAMEI
            ENDSET    FNAMEI
            APPEND    ".txt",FNAMEI      * add ".txt" if there is a path
            APPEND    SP70,FNAMEI
            RESET     FNAMEI
          ENDIF
          RESET     FNAMEI
.
          DISPLAY   *P26:14,*V2LON,*EL,FNAMEI;
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      UITEMTXT,FNAMEI
          TRAPCLR   IO
          BRANCH    OVRCD,KASC8000
.
          CALL      EXCUS000
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
+
.******************************************************************************
.*                                 SCRD0000                                   *
.*                            Screen display                                  *
.******************************************************************************
SCRD0000  DISPLAY   *P1:11,*EF:
                    *P1:15," 3. Keyin User id      :":
                    *P1:16," 4. Keyin Path name    :";
SCRD9999  RETURN
+
.******************************************************************************
.*                                  KUSR0000                                  *
.*                     Keyin user id                                          *
.******************************************************************************
KUSR0000  KEYIN     *P26:15,*EL,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.******************************************************************************
.*                                  KPTH0000                                  *
.*                     Keyin path name                                        *
.******************************************************************************
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P26:16,*EL,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
.         PACK      PATHNAME,PATHNAME,SP70,SP70,SP70
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.******************************************************************************
.*                                  SFLD0000                                  *
.*                        Select Field                                        *
.******************************************************************************
SFLD0000  CALL      MAINQST                 * Select Item, (P)ost, (C)ancel ?
.
          COMPARE   ZERO,CCITEM
          GOTO      SFLD9999 IF EQUAL
          GOTO      SFLD1000 IF LESS
.
          MOVE      ZERO,EXIT
.
          PERFORM   CCITEM,KUSR0000:             * Keyin user id
                           KPTH0000              * Keyin pathname
          BRANCH    EXIT,SFLD9999
          GOTO      SFLD0000
.
SFLD1000  MOVE      ONE,EXIT
SFLD9999  RETURN
+
.******************************************************************************
.*                                  PRHD0000                                  *
.*                        Print the selection details                         *
.******************************************************************************
PRHD0000  CALL      HEAD132
.
          PRINT     *N,"Upload MBS Items"
.
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS:
                    *N,"User ID  : ",USERID
.
          CALL      UND132
.
          PRINT     *1,"| Line",*12,"| Error ",*54,"| Record Detail":
                    *132,"|"
          CALL      UND132
.
PRHD9999  RETURN
+
.******************************************************************************
.*                                  PROC0000                                  *
.*                        Post record                                         *
.******************************************************************************
PROC0000  MOVE      ZERO,LINECNTR
.
          OPEN      UITEMTXT,FNAMEI
.
PROC1000  READ      UITEMTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008,XFLD0009,XFLD0010:
                                 XFLD0011,XFLD0012,XFLD0013,XFLD0014,XFLD0015:
                                 XFLD0016,XFLD0017,XFLD0018,XFLD0019,XFLD0020:
                                 XFLD0021,XFLD0022,XFLD0023,XFLD0024,XFLD0025:
                                 XFLD0026,XFLD0027,XFLD0028,XFLD0029,XFLD0030:
                                 XFLD0031,XFLD0032,XFLD0033,XFLD0034,XFLD0035:
                                 XFLD0036,XFLD0037,XFLD0038,XFLD0039,XFLD0040
          GOTO      PROC9999 IF OVER
.
          PACK      XITEMNO,XFLD0001,SP70   * ITEM NUMBER
          MATCH     SP70,XITEMNO
          GOTO      PROC1000 IF EQUAL
.
          ADD       ONE,LINECNTR            * Text file line counter for error
.
          PACK      XIALPHA,XFLD0002,SP70   * ITEM ALPHA KEY
          PACK      XITMDES,XFLD0003,SP70   * ITEM DESCRIPTION
.
          MOVE      ZERO,XIQIAMT
          SQUEEZE   XFLD0004
          MOVE      XFLD0004,XIQIAMT        * MBS AMOUNT
.
          PACK      XIMSGRP,XFLD0005,SP70   * MISCELLANEOUS GROUP (CAT FI)
          PACK      XITCLSS,XFLD0006,SP70   * SUGGESTED CLASS (CAT A )
.
          UNPACK    XFLD0007,XDAY,XMON,XCENT,XYEAR
          PACK      XITEFDT,XCENT,XYEAR,XMON,XDAY,SP70 * Eff.from Date(CCYYMMDD)
.
          UNPACK    XFLD0008,XDAY,XMON,XCENT,XYEAR
          PACK      XITETDT,XCENT,XYEAR,XMON,XDAY,SP70 * Eff.to Date(CCYYMMDD)
.
          PACK      XIBAND1,XFLD0009,SP70   * THEATRE CLASS 1 (CAT TB)
          PACK      XIBAND2,XFLD0010,SP70   * THEATRE CLASS 2 (CAT TB)
          PACK      XIBAND3,XFLD0011,SP70   * THEATRE CLASS 3 (CAT TB)
          PACK      XIBAND4,XFLD0012,SP70   * THEATRE CLASS 4 (CAT TB)
          PACK      XIBAND5,XFLD0013,SP70   * THEATRE CLASS 5 (CAT TB)
          PACK      XIBAND6,XFLD0014,SP70   * THEATRE CLASS 6 (CAT TB)
          PACK      XIBAND7,XFLD0015,SP70   * THEATRE CLASS 7 (CAT TB)
          PACK      XIBAND8,XFLD0016,SP70   * THEATRE CLASS 8 (CAT TB)
          PACK      XIBAND9,XFLD0017,SP70   * THEATRE CLASS 9 (CAT TB)
          PACK      XIBND10,XFLD0018,SP70   * THEATRE CLASS 10(CAT TB)
          PACK      XIBND11,XFLD0019,SP70   * THEATRE CLASS 11(CAT TB)
          PACK      XIBND12,XFLD0020,SP70   * THEATRE CLASS 12(CAT TB)
          PACK      XIBND13,XFLD0021,SP70   * THEATRE CLASS 13(CAT TB)
          PACK      XIBND14,XFLD0022,SP70   * THEATRE CLASS 14(CAT TB)
          PACK      XIBND15,XFLD0023,SP70   * THEATRE CLASS 15(CAT TB)
          PACK      XIBND16,XFLD0024,SP70   * THEATRE CLASS 16(CAT TB)
          PACK      XITACTV,XFLD0025,SP70   * Active indicator
          PACK      XITLINK,XFLD0026,SP70   * Linked item code (inactive
.
          MOVE      ZERO,XITEXCL
          SQUEEZE   XFLD0027
          MOVE      XFLD0027,XITEXCL        * Exclusion list item ?
.
          PACK      XITIWRN,XFLD0028,SP70   * Warning to enter a Prosthetic
.
          MOVE      ZERO,XITGSTA
          SQUEEZE   XFLD0029
          MOVE      XFLD0029,XITGSTA        * Is GST applicable?
.
          PACK      XITGSTC,XFLD0030,SP70   * GST Payable Code
          PACK      XITDCSC,XFLD0031,SP70   * Daycase Suggested Class. (CAT A)
          PACK      XITDESC,XFLD0032,SP70   * Long Item Description
          PACK      XITTCER,XFLD0033,SP70   * Type of Certificate (CAT cr)
          PACK      XITMSCH,XFLD0034,SP70   * Misc Service Charge Indicator
          PACK      XITTCCD,XFLD0035,SP70   * Theatre Category Code
          PACK      XITSERV,XFLD0036,SP70   * DVA Service Code
          PACK      XITECEQ,XFLD0037,SP70   * Eclipse Equivalent
          PACK      XITECTS,XFLD0038,SP70   * Eclipse Type of Service
          PACK      XITLWRD,XFLD0039,SP70   * Eclipse Labour Ward Item
          PACK      XITTACB,XFLD0040,SP70   * Theatre Average Calc. Band
.
          PACK      ITEMDESC,XITMDES,SP70
          PACK      KEY75,XITEMNO,SP1,XIALPHA,SP1,ITEMDESC,SP1:
                          XIQIAMT,SP1,XIMSGRP,SP1,XITCLSS,SP1,XITEFDT,SP70
.
          CALL      VFLD0000                * Validation pass
          BRANCH    ERRFLAG,PROC1000        * validate fields
.
          REP       " 0",XITEFDT
          MATCH     SP70,XITETDT
          IF        !@EQUAL
            REP       " 0",XITETDT
          ENDIF
          PACK      KEY17,XITEMNO,XITEFDT,SP70
          CALL      RDITEM1
          IF        OVRCD=1
            CALL      SETF0000
            ADD       ONE,ADDCOUNT       * Total records added
            MOVE      USERID,PTITCUID
            CALL      IBACLOCK
            PACK      PTITCDAT,CCC,CYY,CMM,CDD
            REP       " 0",PTITCDAT
            MOVE      CTIMEIS,PTITCTIM
            CALL      WRITEM1             * Write if not validation pass
          ELSE
            CALL      SETF0000
            ADD       ONE,UPDCOUNT       * Total records updated
            MOVE      USERID,PTITUUID
            CALL      IBACLOCK
            PACK      PTITUDAT,CCC,CYY,CMM,CDD
            REP       " 0",PTITUDAT
            MOVE      CTIMEIS,PTITUTIM
            CALL      UPITEM1             * Update if not validation pass
          ENDIF
.
          PROC      PATIKIUP
.
          CALL      EDAT0000              * Check Eff.todate of previous record
.
          GOTO      PROC1000
.
PROC9999  RETURN
+
.******************************************************************************
.*                             EDAT0000                                       *
.*    Auto populate the previous misc items effective to date if blank        *
.******************************************************************************
EDAT0000  PACK      KEY17,XITEMNO,XITEFDT,SP70
          CALL      RDSITEM1
          CALL      RDPITEM1
          BRANCH    OVRCD,EDAT9999
.
          MATCH     XITEMNO,ITEMNO
          GOTO      EDAT9999 IF NOT EQUAL
.
          MATCH     SP70,PTITETDT                * Exit if effective to date is
          GOTO      EDAT9999 IF NOT EQUAL        * already populated
.
          MOVE      XITEFDT,WORKDATE
          SUB       ONE,WORKDATE
          MOVE      WORKDATE,PTITETDT
.
          CALL      IBACLOCK
          PACK      PTITUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTITUDAT           * date record updated
          MOVE      CTIMEIS,PTITUTIM        * time record updated
          MOVE      USERID,PTITUUID         * web user updated
.
          CALL      UPITEM1
.
EDAT9999  RETURN
+
.******************************************************************************
.*                             VFLD0000                                       *
.*                           Validate fields                                  *
.******************************************************************************
VFLD0000  MOVE      ZERO,ERRFLAG
          MOVE      ZERO,ERRDATE
.
          IF        CFEETYP=5
            MATCH     SP70,XITDESC            * Long description
          ELSE
            MATCH     SP70,XITMDES
          ENDIF
          IF        @EQUAL
            MOVE      "Blank Item Description",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MATCH     SP70,XITEFDT
          IF        !@EQUAL
            UNPACK    XITEFDT,CCENT,CYEAR,CMON,CDAY
            MOVE      ONE,F1
            CALL      VLDT0000             * validate date
          ELSE
            MOVE      "Blank Effective From Date",ERRORMSG
            CALL      PERR0000
            MOVE      ONE,ERRDATE
          ENDIF
.
          COMPARE   FIVE,CFEETYP
          GOTO      VFLD1000 IF EQUAL        * NZ Private
.
          MATCH     SP70,XIMSGRP
          IF        @EQUAL
            MOVE      "Blank Misc.Group",ERRORMSG
            CALL      PERR0000
          ELSE
            PACK      KEY5,ANSF,ANSI,XIMSGRP,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Misc.Group",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
VFLD1000  MATCH     SP70,XITCLSS
          IF        !@EQUAL
            PACK      KEY5,ANSA,SP1,XITCLSS,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Suggested Classification",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XITETDT
          IF        !@EQUAL
            UNPACK    XITETDT,CCENT,CYEAR,CMON,CDAY
            MOVE      ZERO,F1
            CALL      VLDT0000             * validate date
          ENDIF
.
          COMPARE   ZERO,ERRDATE
          GOTO      VFLD2000 IF NOT EQUAL
.
          MATCH     SP70,XITETDT
          IF        !@EQUAL
            MATCH     XITEFDT,XITETDT
            IF        @LESS
              MOVE      "Effective To Date is Less than Eff.From Date",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          PACK      VALFDATE,XITEFDT,SP70
          PACK      VALTDATE,XITETDT,SP70
          PACK      KEY17,XITEMNO,XITEFDT,SP70
          CALL      VIDT0000               * Validate effective dates
          IF        EXIT=1
            MOVE      "Invalid Effective From/To Date(Overlap)",ERRORMSG
            CALL      PERR0000
          ENDIF
.
VFLD2000  MATCH     SP70,XIBAND1
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSB,XIBAND1,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Theatre Class 1",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XIBAND2
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSB,XIBAND2,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Theatre Class 2",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XIBAND3
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSB,XIBAND3,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Theatre Class 3",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XIBAND4
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSB,XIBAND4,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Theatre Class 4",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XIBAND5
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSB,XIBAND5,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Theatre Class 5",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XIBAND6
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSB,XIBAND6,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Theatre Class 6",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XIBAND7
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSB,XIBAND7,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Theatre Class 7",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XIBAND8
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSB,XIBAND8,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Theatre Class 8",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XIBAND9
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSB,XIBAND9,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Theatre Class 9",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XIBND10
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSB,XIBND10,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Theatre Class 10",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XIBND11
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSB,XIBND11,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Theatre Class 11",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XIBND12
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSB,XIBND12,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Theatre Class 12",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XIBND13
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSB,XIBND13,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Theatre Class 13",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XIBND14
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSB,XIBND14,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Theatre Class 14",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XIBND15
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSB,XIBND15,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Theatre Class 15",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XIBND16
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSB,XIBND16,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Theatre Class 16",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XITACTV
          IF        !@EQUAL
            PACK      KEY1,XITACTV,SP70
            REP       "I A ",KEY1
            MATCH     SP70,KEY1
            IF        !@EQUAL
              MOVE      "Invalid Active Indicator",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XITLINK
          IF        !@EQUAL
            MATCH     "I",XITACTV
            IF        !@EQUAL
              MOVE      "Linked Item Code for Inactive Item Only",ERRORMSG
              CALL      PERR0000
            ELSE
              PACK      KEY17,XITLINK,SP70
              CALL      PATITMRD
              IF        OVRCD=1
                MOVE      "Invalid Linked Item Code",ERRORMSG
                CALL      PERR0000
              ENDIF
            ENDIF
          ENDIF
.
          IF        XITEXCL>2
            MOVE      "Invalid Exclusion list Item",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MATCH     SP70,XITIWRN
          IF        !@EQUAL
            PACK      KEY1,XITIWRN,SP70
            REP       "1 2 ",KEY1
            MATCH     SP70,KEY1
            IF        !@EQUAL
              MOVE      "Invalid Warning to enter a Prosthetic",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          IF        XITGSTA>2
            MOVE      "Invalid value for GST applicable",ERRORMSG
            CALL      PERR0000
          ENDIF
.
.         If GST Applicable is yes, GST Code is mandatory
.
          IF        XITGSTA=1
            MATCH     SP70,XITGSTC
            IF        @EQUAL
              MOVE      "GST Code can not be blank",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
          MATCH     SP70,XITGSTC
          IF        !@EQUAL
            PACK      KEY6,XITGSTC,SP70
            CALL      RDIBGS1
            IF        OVRCD=1
              MOVE      "Invalid GST Code",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XITDCSC
          IF        !@EQUAL
            PACK      KEY5,ANSA,SP1,XITDCSC,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Daycase Suggested Class.",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XITTCER
          IF        !@EQUAL
            MOVE      "cr",KEY2
            PACK      KEY5,KEY2,XITTCER,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Type of Certificate",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XITMSCH
          IF        !@EQUAL
            PACK      KEY1,XITMSCH,SP70
            REP       "0 1 ",KEY1
            MATCH     SP70,KEY1
            IF        !@EQUAL
              MOVE      "Invalid Misc.Service Charge Indicator",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XITTCCD
          IF        !@EQUAL
            PACK      KEY1,XITTCCD,SP70
            REP       "0 1 ",KEY1
            MATCH     SP70,KEY1
            IF        !@EQUAL
              MOVE      "Invalid Theatre Category Code",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MOVE      ZERO,F2
          MOVE      XITECTS,F2
          IF        F2<0 |  F2 > 16
            MOVE      "Invalid Eclipse Type of Service",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MATCH     SP70,XITLWRD
          IF        !@EQUAL
            PACK      KEY1,XITLWRD,SP70
            REP       "0 1 ",KEY1
            MATCH     SP70,KEY1
            IF        !@EQUAL
              MOVE      "Invalid Eclipse Labour Ward Item",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XITTACB
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSB,XITTACB,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Theatre Average Calc.Band",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
VFLD9999  RETURN
+
.******************************************************************************
.*                                  VLDT0000                                  *
.*                             Validate date                                  *
.******************************************************************************
VLDT0000  MOVE        ZERO,FORM2
          MOVE        CDAY,FORM2
          IF          FORM2 < 1 | FORM2 > 31
            GOTO        VLDT8000
          ENDIF
.
          MOVE        ZERO,FORM2
          MOVE        CMON,FORM2
          IF          FORM2 < 1 | FORM2 >12
            GOTO        VLDT8000
          ENDIF
.
          MOVE        ZERO,FORM2
          MOVE        SP70,KEY4
          PACK        KEY4,CCENT,CYEAR,SP70
          MOVE        KEY4,FORM4
          IF          FORM4 < 1
            GOTO        VLDT8000
          ENDIF
          GOTO        VLDT9999
.
VLDT8000  IF          F1=1
            MOVE        "Invalid format of From Date",ERRORMSG
          ELSE
            MOVE        "Invalid format of To Date",ERRORMSG
          ENDIF
          MOVE        ONE,ERRDATE
          CALL        PERR0000
.
VLDT9999  RETURN
+
.******************************************************************************
.*                                  SETF0000                                  *
.*                             Set fields                                     *
.******************************************************************************
SETF0000  PACK      ITEMNO,XITEMNO,SP70     * ITEM NUMBER
          PACK      IALPHA,XIALPHA,SP70     * ITEM ALPHA KEY
          PACK      IDESC,XITMDES,SP70      * ITEM DESCRIPTION
          MOVE      XIQIAMT,QIAMT           * MBS AMOUNT
          PACK      IMISGRP,XIMSGRP,SP70    * MISCELLANEOUS GROUP (CAT FI)
          PACK      ICLSS,XITCLSS,SP70      * SUGGESTED CLASS (CAT A )
          PACK      PTITEFDT,XITEFDT,SP70   * Effective from Date (CCYYMMDD)
          PACK      PTITETDT,XITETDT,SP70   * Effective to Date (CCYYMMDD)
          PACK      IBAND1,XIBAND1,SP70     * THEATRE CLASS 1 (CAT TB)
          PACK      IBAND2,XIBAND2,SP70     * THEATRE CLASS 2 (CAT TB)
          PACK      IBAND3,XIBAND3,SP70     * THEATRE CLASS 3 (CAT TB)
          PACK      IBAND4,XIBAND4,SP70     * THEATRE CLASS 4 (CAT TB)
          PACK      IBAND5,XIBAND5,SP70     * THEATRE CLASS 5 (CAT TB)
          PACK      IBAND6,XIBAND6,SP70     * THEATRE CLASS 6 (CAT TB)
          PACK      IBAND7,XIBAND7,SP70     * THEATRE CLASS 7 (CAT TB)
          PACK      IBAND8,XIBAND8,SP70     * THEATRE CLASS 8 (CAT TB)
          PACK      IBAND9,XIBAND9,SP70     * THEATRE CLASS 9 (CAT TB)
          PACK      IBAND10,XIBND10,SP70    * THEATRE CLASS 10(CAT TB)
          PACK      IBAND11,XIBND11,SP70    * THEATRE CLASS 11(CAT TB)
          PACK      IBAND12,XIBND12,SP70    * THEATRE CLASS 12(CAT TB)
          PACK      IBAND13,XIBND13,SP70    * THEATRE CLASS 13(CAT TB)
          PACK      IBAND14,XIBND14,SP70    * THEATRE CLASS 14(CAT TB)
          PACK      IBAND15,XIBND15,SP70    * THEATRE CLASS 15(CAT TB)
          PACK      IBAND16,XIBND16,SP70    * THEATRE CLASS 16(CAT TB)
          PACK      PTITACTV,XITACTV,SP70   * Active indicator
          PACK      PTITLINK,XITLINK,SP70   * Linked item code (inactive
          MOVE      XITEXCL,PTITEXCL        * Exclusion list item ?
          PACK      PTITIWRN,XITIWRN,SP70   * Warning to enter a Prosthetic
          MOVE      XITGSTA,PTITGSTA        *  Is GST applicable?
          PACK      PTITGSTC,XITGSTC,SP70   * GST Payable Code
          PACK      PTITDCSC,XITDCSC,SP70   * Daycase Suggested Class.
          PACK      PTITDESC,XITDESC,SP70   * Long Item Description
          PACK      PTITTCER,XITTCER,SP70   * Type of Certificate (CAT cr)
          PACK      PTITMSCH,XITMSCH,SP70   * Misc Service Charge Indicator
          PACK      PTITTCCD,XITTCCD,SP70   * Theatre Category Code
          PACK      PTITSERV,XITSERV,SP70   * DVA Service Code
          PACK      PTITECEQ,XITECEQ,SP70   * Eclipse Equivalent
          PACK      PTITECTS,XITECTS,SP70   * Eclipse Type of Service
          PACK      PTITLWRD,XITLWRD,SP70   * Eclipse Labour Ward Item
          PACK      PTITTACB,XITTACB,SP70   * Theatre Average Calc. Band
.
          UNPACK    SP70,PTITCDAT,PTITCTIM,PTITCUID
          UNPACK    SP70,PTITUDAT,PTITUTIM,PTITUUID
          MOVE      SP70,PTITSPAR
SETF9999  RETURN
+
.******************************************************************************

.         Execute us1 command before processing
.******************************************************************************
EXCUS000  CLEAR     CMDLINE
          APPEND    "ibaadt14.us1 ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
EXCUS999  RETURN
+
.******************************************************************************
. Print error line
.******************************************************************************
PERR0000  ADD       ONE,ERRCOUNT            * Total error count
.
          PRINT     *1,"|",LINECNTR,*12,"|",ERRORMSG,*54,"|":
                    KEY75,*132,"|"
          PACK      KEY75,SP70,SP20
          MOVE      ONE,ERRFLAG
.
PERR9999  RETURN
+
.******************************************************************************
. Clear total vars
.******************************************************************************
CTOT0000  MOVE      ZERO,ERRCOUNT
          MOVE      ZERO,ADDCOUNT
          MOVE      ZERO,UPDCOUNT
.
CTOT999   RETURN
+
.******************************************************************************
. Print total line
.******************************************************************************
PTOT0000  CALL      UND132
.
          PRINT     *1,"| Total Errors  : ",ERRCOUNT,*132,"|"
          PRINT     *1,"| Total Added   : ",ADDCOUNT,*132,"|"
          PRINT     *1,"| Total Updated : ",UPDCOUNT,*132,"|"
.
          CALL      UND132
.
PTOT9999  RETURN
.
          INC       STD001IO
          INC       PATITMRD
          INC       PATITMVL
          INC       PATIKIPR
.
          INC       IBAGSTIO/INC
          INC       PATCODIO/INC
          INC       PATITMIO/INC            * MBS item file
.

.******************************************************************************
.* System         : Waiting Lists                                             *
.* Program        : IBAWAT55.PBL                                              *
.* Name           : ACT HDP Waiting List Extraction                           *
.******************************************************************************
.* Date           : 13/08/2004                                                *
.* Author         : Jill Habasque                                             *
.* Function       : Extract waiting lists details via a date range & load the *
.*                  details into a text file.                                 *
.* Modifications  :                                                           *
.******************************************************************************
.* V12.00.01 22/05/2025  Alina Bhari   TSK 0955096                            *
.*           Added alpanumeric visit number generation -GRSD0000              *
.* V11.02.01 10/02/2022  Thanh T       TSK 0889899                            *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V11.01.01 22/02/2021  Tracey Nguyen  TSK 0888639                    *
.*                  FD changed - patmmbsf.PATMMBS1 from KEY10 to KEY11        *
.******************************************************************************
.*        V10.03.01 18/11/2011  Mike Laming     CAR 240184                    *
.*                  Changes to IBAPOSTF Post Code table - added State to Keys *
.*        V9.11.01  01/05/2009  Jill Habasque CAR 195865                      *
.*                  July 2009 mods - added indigenous status and int los      *
.*        V9.09.03  12/12/2007  Jill Habasque CAR 140056                      *
.*                  Added WAT prefix to filename                              *
.*        V9.09.02  14/09/2007  Jill Habasque CAR 150060                      *
.*                  Mods to ignore the first priority change if changed twice *
.*                  in the one day                                            *
.*        V9.09.01  09/08/2007  Jill Habasque CAR 147456                      *
.*                  Commented out match for DTLURGIN to spaces in MOVE0000    *
.*        V9.08.02  15/05/2007  Jill Habasque CAR 140045                      *
.*                  Moved call to CINR0000                                    *
.*        V9.08.01  01/05/2007  Jill Habasque CAR 139417                      *
.*                  2007 Changes                                              *
.*        V9.06.02  19/06/2006  Jill Habasque CAR 108920                      *
.*                  2006 Changes                                              *
.*        V9.06.01  17/08/2005  Jill Habasque CAR 62827                       *
.*                  Mods for WTCENSUS calculation                             *
.*        V9.04.08  19/07/2005  Jill Habasque CAR 62827                       *
.*                  Mods for WTREM calculation                                *
.*        V9.04.07  15/07/2005  Jill Habasque CAR 62827                       *
.*                  Fixed hospital ID                                         *
.*        V9.04.06  20/04/2005  Jill Habasque CAR 58325                       *
.*                  Changed calculation for DAYSLCAT                          *
.*        V9.04.05  19/04/2005  Jill Habasque CAR 58325                       *
.*                  Added check for WMSTAT in GRSD0000                        *
.*        V9.04.04  18/04/2005  Jill Habasque CAR 58453                       *
.*                  Added hospital ID, fixed DAYSLCAT calculation             *
.*        V9.04.03  29/03/2005  Guomin Fu     CAR 58453                       *
.*                  Changed to read ibapostf for SSLA                         *
.*        V9.04.02  17/03/2005  Jill Habasque CAR 58453                       *
.*                  Fixed CLINURG for patients without any changes            *
.*        V9.04.01  01/03/2005  Peter Vela    CAR 57547                       *
.*                  Recompiled for PATVADFD and PATVADIO                      *
.*        V9.04.00  13/08/2004  Jill Habasque                                 *
.*                  Created extract                                           *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
.
          INC       BOKRC1FD/INC            * Booking file
          INC       IBAPOSFD/INC            * Postcode file
          INC       PATCALAG/INC            * calcage   
          INC       PATCODFD/INC            * Codes file
          INC       PATCONFD/INC            * Patient control file
          INC       PATDRGFD/INC            * Period Date Range
          INC       PATDSCFD/INC            * Discharge file
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC            * Master file
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       PATMI1FD/INC            * Admission file
          INC       PATVADFD/INC            * Transfer source file
          INC       PATCT1FD/INC
          INC       PATMMBFD/INC
          INC       WATCATFD/INC            * Code changes file
          INC       WATCONFD/INC
          INC       WATPROFD/INC            * Procedure file
          INC       WATRTDFD/INC            * Referral Destination file
          INC       WATSUSFD/INC            * Procedure Suspension file
          INC       WATTR1FD/INC            * Treatment file
          INC       WATTX1FD/INC            * Treatment Extention file
.
EXTRACTF  FILE      ASCII, FIXED=194
.
.Name     Type    Length    Start  Description
.-------  ----    ------    -----  -----------
.HOSPID    DIM          2        1  Hospital ID
.PMI       DIM          8        3  Unique ACT number (not required)
.PIN       DIM          8       11  Patient ID number
.LISTID    DIM          8       19  Unique listing ID
.SEX       DIM          1       27  Sex
.DOB       DIM          8       28  Date of Birth
.POSTCODE  DIM          4       36  Post Code
SSLA      DIM          5       40  State/Statistical Local Area of Residence
.WAITCAT   DIM          1       45  Waiting List Category
.LISTDATE  DIM          8       46  Date on List
.CENSDATE  DIM          8       54  Census Date
.LISTSTAT  DIM          1       62  Listing Status
.DAYSNRC   FORM         4       63  Days not ready for care
.DAYSLCAT  FORM         4       67  Days waiting at a lower urgency 
.WTCENSUS  FORM         4       71  Waiting time at census
.WTREM     FORM         4       75  Waiting time at removal
.CLINURG   DIM          1       79  Previous Clinical urgency
.CLURGR    DIM          1       80  Current Clinical urgency
.CATRDATE  DIM          8       81  Category Reassignment Date
.OVERDUE   DIM          1       89  Overdue flag
.SURGSPEC  DIM          2       90  Surgical Specialty
.INDPROC   DIM          2       92  Indicator Procedure
.PPROC     DIM          8       94  Planned Procedure
.SADMDATE  DIM          8      102  Scheduled Admission Date
.PROCDATE  DIM          8      110  Date of Procedure
.REMVDATE  DIM          8      118  Removal Date
.RREASON   DIM          1      126  Removal Reason
.XRESCHD   FORM         2      127  No of times rescheduled (removed 01/07/2006)
.ESDOCTOR  DIM          5      127  Listing Physician
.EPIS      DIM          8      132  APC episode number
.LOCALITY  DIM         50      140  Locality
.CLINUNIT  DIM          3      190  Clinical Unit
.IHCP      DIM          1      193  Inter hospital contracted patient
.
.End of Record                192
.
EXTRACTB  FILE      ASCII, FIXED=190    * layout after 01/07/2007
EXTRACTC  FILE      ASCII, FIXED=192    * layout after 01/07/2009
.
.Name     Type    Length    Start  Description
.-------  ----    ------    -----  -----------
HOSPID    DIM          2        1  Hospital ID
PMI       DIM          8        3  Patient ID number
PIN       DIM          8       11  Patient ID number
LISTID    DIM          8       19  Unique listing ID
SEX       DIM          1       27  Sex
DOB       DIM          8       28  Date of Birth
POSTCODE  DIM          4       36  Post Code
STATEID   DIM          1       40  State ID (after 01/07/2007)
WAITCAT   DIM          1       41  Waiting List Category
LISTDATE  DIM          8       42  Date on List
CENSDATE  DIM          8       50  Census Date
LISTSTAT  DIM          1       58  Listing Status
DAYSNRC   FORM         4       59  Days not ready for care
DAYSLCAT  FORM         4       63  Days waiting at a lower urgency
WTCENSUS  FORM         4       67  Waiting time at census
WTREM     FORM         4       71  Waiting time at removal
CLINURG   DIM          1       75  Previous Clinical urgency
CLURGR    DIM          1       76  Current Clinical urgency
CATRDATE  DIM          8       77  Category Reassignment Date
OVERDUE   DIM          1       85  Overdue flag
SURGSPEC  DIM          2       86  Surgical Specialty
INDPROC   DIM          2       88  Indicator Procedure
PPROC     DIM          8       90  Planned Procedure
SADMDATE  DIM          8       98  Scheduled Admission Date
PROCDATE  DIM          8      106  Date of Procedure
REMVDATE  DIM          8      114  Removal Date
RREASON   DIM          1      122  Removal Reason
ESDOCTOR  DIM          5      123  Listing Physician
EPIS      DIM          8      128  APC episode number
LOCALITY  DIM         50      136  Locality
CLINUNIT  DIM          3      186  Clinical Unit
IHCP      DIM          1      189  Inter hospital contracted patient
INDIGEN   DIM          1      190  Indigenous Status
ILOHS     DIM          1      191  Intended length of hospital stay
.
.End of Record                192
. ----- Program Variables -----
BJDAY     FORM      3
CDYSDAYS  FORM      6                       * For CALCDAYS
CDYSFDTE  DIM       8                       * For CALCDAYS
CDYSTDTE  DIM       8                       * For CALCDAYS
CDYSYEAR  FORM      2                       * For CALCDAYS
CJDAY     FORM      3
CMDLINE   DIM       127
CPTDATE   DIM       8
CURDTE    DIM       8
DATOLIST  DIM       10
DSPCNT    FORM      5
DTLURGIN  DIM       8
DIM4      DIM       4
DIM4A     DIM       4
ENDDTE    DIM       8
ERRMSG    DIM       98
FCMON     FORM      2
FCYEAR    FORM      2
FILENAME  DIM       8
IBCNMHOS  FORM      1
NEWLAYT   FORM      1
NRFCDAYS  FORM      3
PSAGECON  DIM       3
PSAGETYP  DIM       1
RECCNT    FORM      4
SAVINDC3  DIM       1
STRTDTE   DIM       8
TNRFCLCU  FORM      4
TOTALDAY  FORM      4
.
MTHNAM3   DIM       3
JAN       INIT      "Jan"
FEB       INIT      "Feb"
MAR       INIT      "Mar"
APR       INIT      "Apr"
MAY       INIT      "May"
JUN       INIT      "Jun"
JUL       INIT      "Jul"
AUG       INIT      "Aug"
SEP       INIT      "Sep"
OCT       INIT      "Oct"
NOV       INIT      "Nov"
DEC       INIT      "Dec"
.
PRGID     INIT      "IBAWAT55"
PRGNAM    INIT      "ACT HDP Waiting List Extraction"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.****************************************************************************** 
.
ML0000    MOVE      ZERO,NEWLAYT
          CALL      INIT0000                * Initialise variables & open files 
.
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    OPTION,ML2000,ML2500
          BRANCH    EXIT,ML9999
.
ML2000    CALL      DATE0000                * Keyin the census date
          BRANCH    EXIT,ML9999
          GOTO      ML3000
.
ML2500    CALL      IDAT0000                * Keyin the end date for YTD data
          BRANCH    EXIT,ML9999
          GOTO      ML3000
.
ML3000    MATCH     "20090701",ENDDTE
          IF        !@LESS
            MOVE      ONE,NEWLAYT
          ENDIF
          CALL      HOSP0000
          BRANCH    EXIT,ML9999
          CALL      FILE0000                * Keyin the extract filename
          BRANCH    EXIT,ML9999
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML4000,ML3000,ML1000
.
ML4000    DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Loading : ";
          CALL      PROC0000
.
          CALL      MVEXT000                * Move Extract for Web
          GOTO      ML1000
.
ML9999    CHAIN     PGM
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                       Initialise Variable & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
.
          DISPLAY   *P64:24,"ibapostf";
          OPEN      IBAPOST1,"ibapostf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patct1cf";
          OPEN      PATCT1C1,"patct1cf"
.
          DISPLAY   *P64:24,"patmmbsf";
          OPEN      PATMMBS1,"patmmbsf"
.
          DISPLAY   *P64:24,"watcataf";
          OPEN      WATCATA1,"watcataf"
          OPEN      WATCATA2,"watcataf"
.
          DISPLAY   *P64:24,"watproaf";
          OPEN      WATPROA1,"watproaf"
.
          DISPLAY   *P64:24,"watsusaf";
          OPEN      WATSUSA1,"watsusaf"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A3,"wattr1af"
.
          DISPLAY   *P64:24,"wattx1af";
          OPEN      WATTX1A1,"wattx1af"
.
          CLOCK     TIME,CTIMEIS
          PACK      CURDTE,CCC,CYY,CMM,CDD
          REP       " 0",CURDTE
          READ      CONTROLF,TEN;*54,CFILENO
          READ      CONTROLF,FIFTY9;*220,PTCNUADT
          READ      CONTROLF,NINTY8;*81,PTCNHCOD
.
          IF        PTCNUADT = 1
            OPEN      WATRTDA1,"watrtdaf"
            OPEN      PATVADA1,"patvadaf"
          ENDIF
.
INIT9000  MOVE      ZERO,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Census":
                    *P1:6,*V2LON,"2",*HOFF," = Year to Date":
                    *P1:8,"Select Option : ";
OPTN1000  KEYIN     *P17:8,*V2LON,*DV,UNDLN2,*P17:8,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  DATE0000              Called by: ML0000   *
.*                            Keyin The Date Range                            *
.******************************************************************************
DATE0000  MOVE      "21",CCOL
          MOVE      "9",CVERT
          DISPLAY   *P1:CVERT,"Starting Date     : ",*EF;
          ADD       ONE,CVERT
          DISPLAY   *P1:CVERT,"Ending Date       : ";
.
          MOVE      ONE,CCANLDTE            * Blank cancel default
          MOVE      ZERO,CHIGHLT            * Use highlight
.
. ----- Enter starting date -----
.
DATE1000  SUB       ONE,CVERT
DATE2000  MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY     * No defaults
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9500          * Nothing entered
          BRANCH    CQUEST,DATE2000
.
          PACK      STRTDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDTE
.
          MATCH     STRTDTE,CURDTE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Date must not be in the future.  ";
            CALL      HITENTER
            GOTO      DATE2000
          ENDIF
.
. ----- Enter ending date -----
.
          ADD       ONE,CVERT
DATE3000  UNPACK    STRTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE1000          * Nothing entered
          BRANCH    CQUEST,DATE3000
.
          PACK      ENDDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDTE
.
          MATCH     ENDDTE,CURDTE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Date must not be in the future.  ";
            CALL      HITENTER
            GOTO      DATE3000
          ENDIF
.
          MATCH     STRTDTE,ENDDTE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must be after start date.  ";
            CALL      HITENTER
            GOTO      DATE3000
          ENDIF
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.******************************************************************************
.*                                  IDAT0000                                  *
.*        input ending date                              called by : MAINLINE *
.******************************************************************************
IDAT0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:11,*EF,"Ending date : ";
.
.         set up input requirements for KEYDATE
.
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT
          MOVE      "15",CCOL
          MOVE      "11",CVERT
.
.         input ending date
.
IDAT1000  UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    OVRCD,IDAT9500
.
          PACK      ENDDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDTE
.
          CALL     CSFY0000       * Get start of financial year
          GOTO     IDAT9999
.
IDAT9500  MOVE      ONE,EXIT
IDAT9999  RETURN
.
.*******************************************************************************
.*                              CSFY0000                  CALLED BY : ML1000   *
.*                                                                    ML2000   *
.* Calculate the Start of financial year from the end of current month/quarter *
.*******************************************************************************
CSFY0000  UNPACK    ENDDTE,CCENT,CYEAR,CMON,CDAY
.
          MOVE      CMON,FCMON
          COMPARE   FCMON,SIX                * Month > June
          GOTO      CSFY1000 IF LESS         * Yes
.
          MOVE      CYEAR,FCYEAR             * Mth 1-6:CYEAR is the previous yr
          SUB       ONE,FCYEAR
          MOVE      FCYEAR,CYEAR
.
CSFY1000  PACK      STRTDTE,CCENT,CYEAR,ZERO,SEVEN,ZERO,ONE * Start of fin. Yr
          REP       " 0",STRTDTE
.
CSFY9999  RETURN
+
.******************************************************************************
.*                                  HOSP0000              Called by: ML0000   *
.*                         Keyin The Hospital ID                              *
.******************************************************************************
HOSP0000  DISPLAY   *P1:14,*EL,"Hospital ID: "
          MOVE       "15",ECOL
          MOVE       "14",EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ONE,CKYIMAND           * Mandatory for Multi hospital
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,HOSP9500,HOSP9500
          MOVE       PTHSHDPC,HOSPID
          CLOSE      PATHSPA1
          DISPLAY   *P15:14,*V2LON,PTHSNAME
          GOTO      HOSP9999
.
HOSP9500  MOVE       ONE,EXIT
HOSP9999  RETURN
+
.******************************************************************************
.*                                  FILE0000              Called by: ML0000   *
.*                         Keyin The Extract Filename                         *
.******************************************************************************
FILE0000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
FILE1000  MOVE      "WAT",KEY3
          UNPACK    ENDDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      CPTDATE,ENDDTE
          CALL      GPERD
          PACK      FILENAME,KEY3,MTHNAM3,CYEAR,SP70
          REPLACE   " 0",FILENAME
.
          DISPLAY   *P1:14,*EL,"Extract Filename":
                    *PCCOL:14,*EL,*V2LON,*+,FILENAME,".txt";
.
          IF        NEWLAYT=1
            PREP      EXTRACTC,FILENAME
          ELSE
            PREP      EXTRACTB,FILENAME
          ENDIF
.
FILE9999  RETURN
+
.******************************************************************************
.*                                  PROC0000              Called by: ML0000   *
.*                              Run the extract                               *
.******************************************************************************
PROC0000  CALL      HEAD0000
          PACK      KEY27,SP70
          CALL      RDSTREA3                * Position on & read a treatment
PROC1000  CALL      RDKTREA3                  file record
          BRANCH    OVRCD,PROC9999
.
          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
          CALL      RDWTTX11
          BRANCH    OVRCD,PROC1000
.
          ADD       ONE,DSPCNT              * Increment the record counter
          IF        DSPCNT%10=1
            DISPLAY   *P12:24,*V2LON,WMSTAT,SP2,WMURNO;
          ENDIF
.
          CALL      VPTD0000                * Validate patient details
          BRANCH    EXIT,PROC1000
.
          CALL      CLEAR000
          CALL      MOVE0000
          CALL      WRITE000
          GOTO      PROC1000
.
PROC9999  RETURN
+
.******************************************************************************
.*                                  VPTD0000              Called by: GTRE0000 *
.*                          Validate Patient Details                          *
.******************************************************************************
VPTD0000  MOVE      ZERO,EXIT
          MATCH     WMDATE1,ENDDTE
          GOTO      VPTD9500 IF LESS        * End date < on list date
.
          PACK      KEY8,WMURNO,SP70
          CALL      RDMAST1
          IF        OVRCD=1
            MOVE     "Missing master file record",ERRMSG
            CALL      PRNT0000          * Print error message
            GOTO      VPTD9500
          ENDIF
          CALL      RDPMPX21
.
          IF        WMSTAT=2 | WMSTAT=3 | WMSTAT=4 | WMSTAT=5
            PACK      KEY8,WMBOOK
            CALL      RDBKREC1              * Read a booking file record
            IF        OVRCD=1
              MOVE      "Missing Booking file record",ERRMSG
              CALL      PRNT0000          * Print error message
              GOTO      VPTD9500
            ENDIF
          ENDIF
.
          IF        WMSTAT=3 | WMSTAT=4 | WMSTAT=5
            MOVE      WMBOOK,AADMNO
            PACK      KEY8,AADMNO
            CALL      RDMISS1               * Read an admin file record
            IF        OVRCD=1
              MOVE      "Missing Admission file record",ERRMSG
              CALL      PRNT0000            * Print error message
              GOTO      VPTD9500
            ENDIF
          ENDIF
.
          IF        WMSTAT=5
            MOVE      WMBOOK,DADMNO
            PACK      KEY8,DADMNO
            CALL      RDDSCH1               * Read a disch file record
            IF        OVRCD=1
              MOVE      "Missing Discharge file record",ERRMSG
              CALL      PRNT0000            * Print error message
              GOTO      VPTD9500
            ENDIF
          ENDIF
.
          IF        WMSTAT=3
            COMPARE   ONE,ASTAT
            IF        !@EQUAL
             MOVE     "Waiting list status not equal to admission status",ERRMSG
             CALL      PRNT0000          * Print error message
             GOTO      VPTD9500
            ENDIF
          ENDIF
.
          IF        WMSTAT=4
            MATCH     STRTDTE,ADATE
            GOTO      VPTD9500 IF LESS      * Admin date < start date
.
            COMPARE   TWO,ASTAT
            IF        !@EQUAL
             MOVE     "Waiting list status not equal to admission status",ERRMSG
             CALL      PRNT0000          * Print error message
             GOTO      VPTD9500
            ENDIF
          ENDIF
.
          IF        WMSTAT=5
            MATCH     STRTDTE,ADATE
            GOTO      VPTD9500 IF LESS      * Admin date < start date
.
            COMPARE   THREE,ASTAT
            IF        !@EQUAL
             MOVE     "Waiting list status not equal to admission status",ERRMSG
             CALL      PRNT0000          * Print error message
             GOTO      VPTD9500
            ENDIF
          ENDIF
.
          IF        WMSTAT=6
            MATCH     STRTDTE,WMDATE4
            GOTO      VPTD9500 IF LESS      * Removal date < start date
          ENDIF
          GOTO      VPTD9000
.
VPTD9000  MOVE      ZERO,EXIT
          GOTO      VPTD9999
.
VPTD9500  MOVE      ONE,EXIT                * Ignore flag - yes
          GOTO      VPTD9999
.
VPTD9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: ML0000   *
.*                             Print Report Header                 & PRNT0000 *
.******************************************************************************
HEAD0000  CALL      HEAD132                 * Print report header
          PRINT     *N;
          UNPACK    STRTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *35,"Starting Date : ",CPCDATE
          UNPACK    ENDDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *35,"Ending Date   : ",CPCDATE
.
          CALL      UND132

          PRINT     *1,"| U/R No |Proc Code|Date On List":
                    "|Error Message",*132,"|"
          CALL      UND132
          ADD       "2",CLNO
.
HEAD9000  MOVE      ZERO,EXIT
HEAD9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: VALD0000 *
.*                             Print Error Message                            *
.******************************************************************************
PRNT0000  COMPARE   CLNO,FIFTY8
          CALL      HEAD0000 IF LESS        * Print report header
.
          PACK      ERRMSG,ERRMSG,SP70
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATOLIST
          PRINT     *1,"|",WMURNO,"|",WMCODE:
                    "| ",DATOLIST," |",ERRMSG,"|"
          ADD       ONE,CLNO
          PACK      ERRMSG,SP70
.
PRNT9000  MOVE      ZERO,EXIT
PRNT9999  RETURN
+
.******************************************************************************
. Move Extract to Web Directory
.******************************************************************************
MVEXT000  CLEAR     CMDLINE
          APPEND    "ibawat55.us1 ",CMDLINE
          APPEND    HOSPID,CMDLINE
          APPEND    SP1,CMDLINE
          APPEND    FILENAME,CMDLINE
          IF        OPTION=1
            APPEND    " cens",CMDLINE
          ENDIF
          IF        OPTION=2
            APPEND    " ytd",CMDLINE
          ENDIF
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
MVEXT999  RETURN
+
.******************************************************************************
.*                                  MOVE0000              Called by: PROC0000 *
.*                         Move in the extract values                         *
.******************************************************************************
MOVE0000  PACK      PIN,WMURNO,SP70
          PACK      PMI,WMURNO,SP70
          PACK      LISTID,SP1,SP1,SP1,WTWMUNIQ,SP70
.
          MOVE      PSEX,SEX
          REP       "M1F2I3R3U9",SEX      
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DOB,CDAY,CMON,CCENT,CYEAR,SP70
.
          PACK      POSTCODE,PPOST,SP70
          MATCH     "9988",POSTCODE
          IF        @EQUAL
            PACK      POSTCODE,SP70
          ENDIF
          REP       " 0",POSTCODE
.
          PACK      STATEID,ZERO,SP70       * check Address Line 2 for Suburb
          PACK      KEY56,PPOST,PADD2,SP10,PADD4,SP70                 *C-240184
          CALL      RDIBPOS1
          COMPARE   ONE,OVRCD
          GOTO      MOVE0500 IF NOT EQUAL
.                                           * check for Postcode/Suburb
          PACK      KEY56,PPOST,PSUBRB,SP10,PADD4,SP70                *C-240184
          CALL      RDIBPOS1
          COMPARE   ONE,OVRCD
          GOTO      MOVE0500 IF NOT EQUAL
.
          PACK      KEY56,PPOST,PSUBRB,SP70                           *I-240184
          CALL      RSIBPOS1
          CALL      RKIBPOS1
          IF        OVRCD <> 1
            MATCH     PPOST,IBPOPCOD
            IF        @EQUAL
              MATCH     PSUBRB,IBPOSUBR
              GOTO      MOVE0500 IF EQUAL
            ENDIF
          ENDIF
          GOTO      MOVE1000                * Postcode/Suburb not found
.
.
MOVE0500  UNPACK    IBPOASGC,DIM1,DIM4,DIM4A
          PACK      SSLA,DIM1,DIM4A
          PACK      STATEID,SSLA,SP70
.
          MOVE      PMPXABRG,ACODE
          MATCH     SP3,ACODE
          GOTO      MOVE1000 IF EQUAL
.
          PACK      KEY5,ANSV,ANSA,ACODE   * Check for a valid code
          CALL      RDCODE1
          BRANCH    OVRCD,MOVE1000
.
          MATCH     SP4,THCSCOD
          GOTO      MOVE1000 IF EQUAL
          MOVE      THCSCOD,INDIGEN
.
MOVE1000  REP       " 0",SSLA
.
          MOVE      "1",WAITCAT
          PACK      KEY5,ANSW,ANSU,WMUNIT,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            PACK      WAITCAT,TCINDC1,SP70
            UNPACK    THCSCOD,DIM1,DIM1,CLINUNIT
          ENDIF
.
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          PACK      LISTDATE,CDAY,CMON,CCENT,CYEAR
          REP       " 0",LISTDATE
.
          UNPACK    ENDDTE,CCENT,CYEAR,CMON,CDAY
          PACK      CENSDATE,CDAY,CMON,CCENT,CYEAR
          REP       " 0",CENSDATE
.
 
          PACK      KEY5,ANSV,ANSL,WTTXPLST,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            PACK     LISTSTAT,TCINDC2,SP70
          ENDIF
.
          CALL      DNRC0000
.
          CALL      GTPC0000
          REP       " 0",CATRDATE
          REP       " 0",SADMDATE
          REP       " 0",PROCDATE
          REP       " 0",REMVDATE
.
          CALL      CINR0000
          CALL      LOWU0000
          DAYSEP    WMDATE1,ENDDTE,WTCENSUS
          SUB       TNRFCLCU,WTCENSUS
          SUB       DAYSLCAT,WTCENSUS
...         MATCH     DTLURGIN,SP70
...         IF        @EQUAL
            SUB       DAYSNRC,WTCENSUS
...         ENDIF
.
MOVE1100  IF        WMSTAT=4|WMSTAT=5
            PACK      KEY8,WMBOOK,SP70
            CALL      RDMISS1             
            MATCH     ADATE,ENDDTE
            GOTO      MOVE1150 IF LESS
            DAYSEP    WMDATE1,ADATE,WTREM
            SUB       TNRFCLCU,WTREM
            SUB       DAYSLCAT,WTREM
            MOVE      ZERO,WTCENSUS 
...         MATCH     DTLURGIN,SP70
...         IF        @EQUAL
              SUB       DAYSNRC,WTREM
...         ENDIF
          ENDIF
.
MOVE1150  IF        WMSTAT=6
            MATCH     WMDATE4,ENDDTE
            GOTO      MOVE1160 IF LESS
            DAYSEP    WMDATE1,WMDATE4,WTREM
            SUB       TNRFCLCU,WTREM
            SUB       DAYSLCAT,WTREM
            MOVE      ZERO,WTCENSUS 
...         MATCH     DTLURGIN,SP70
...         IF        @EQUAL
              SUB       DAYSNRC,WTREM
...         ENDIF
          ENDIF
.
MOVE1160  CALL      OVER0000
.
          MATCH     SP3,WMUNIT
          IF        !@EQUAL
            PACK      KEY5,ANSW,ANSU,WMUNIT,SP70
            CALL      RDCODE1               * Read a codes file record
            IF        OVRCD<>1
              MOVE      THCSCOD,SURGSPEC    * Surgical speciality
              REP       " 0",SURGSPEC
            ENDIF
          ENDIF
.
          MATCH     SP9,WMCODE
          IF        !@EQUAL
            PACK      KEY9,WMCODE
            CALL      RDPROA1               * Read a procedure file record
            IF        OVRCD<>1
              MOVE      WTWPDRGC,INDPROC    * Principal prescribed procedure
              REP       " 0",INDPROC
              PACK      PPROC,WTWPHDPE,SP70
            ELSE
              MOVE      "Invalid Procedure Code",ERRMSG
              CALL      PRNT0000              * Print error message
            ENDIF
          ENDIF
.
          CALL      GBKD0000
          CALL      GRSD0000
.
          PACK      ILOHS,SP70
          PACK      KEY8,WMBOOK,SP70
          CALL      RDPMVX11
          IF        OVRCD<>1
            MATCH     SP70,PMVXIDUS
            IF        !@EQUAL
              PACK      KEY5,ANSV,ANSI,PMVXIDUS,SP70
              CALL      RDCODE1
              IF        OVRCD<>1
                PACK      ILOHS,TCINDC1,SP70
              ENDIF
            ENDIF
          ENDIF
.
          PACK      ESDOCTOR,WMDOCTOR,SP70
          PACK      LOCALITY,PSUBRB,SP70
.
MOVE9999  RETURN
+
.******************************************************************************
.*                                  OVER0000              Called by: PROC0000 *
.*         Calculate the total number of not ready for care days              *
.******************************************************************************
OVER0000  MOVE      "2",OVERDUE
          MOVE      ZERO,TOTALDAY
          IF        WMSTAT<4
            MOVE      WTCENSUS,TOTALDAY
          ELSE
            MOVE      WTREM,TOTALDAY
          ENDIF
.
          MATCH     "1",CLURGR
          GOTO      OVER1000 IF NOT EQUAL
.
          COMPARE   "30",TOTALDAY
          IF        @LESS|@EQUAL
            MOVE      "2",OVERDUE
          ELSE
            MOVE      "1",OVERDUE
          ENDIF
          GOTO      OVER9999
.
OVER1000  MATCH     "2",CLURGR
          GOTO      OVER2000 IF NOT EQUAL
.
          COMPARE   "90",TOTALDAY
          IF        @LESS|@EQUAL
            MOVE      "2",OVERDUE
          ELSE
            MOVE      "1",OVERDUE
          ENDIF
          GOTO      OVER9999
.
OVER2000  MATCH     "3",CLURGR
          GOTO      OVER9999 IF NOT EQUAL
.
          COMPARE   "365",TOTALDAY
          IF        @LESS|@EQUAL
            MOVE      "2",OVERDUE
          ELSE
            MOVE      "1",OVERDUE
          ENDIF
          GOTO      OVER9999
.
OVER9999  RETURN
+
.******************************************************************************
.*                                  DNRC0000              Called by: PROC0000 *
.*         Calculate the total number of not ready for care days              *
.******************************************************************************
DNRC0000  MOVE      ZERO,DAYSNRC
.
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP70
          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
          CALL      RSWTSUS1
DNRC1000  CALL      RKWTSUS1
          BRANCH    OVRCD,DNRC9000
.
          MATCH     WMURNO,WTSUURNO
          GOTO      DNRC9000 IF NOT EQUAL   * Different U/R no
.
          MATCH     WMCODE,WTSUCODE
          GOTO      DNRC9000 IF NOT EQUAL   * Different procedure code
.
          COMPARE   WMCNT,WTSUCNT
          GOTO      DNRC9000 IF NOT EQUAL
.
          MATCH     WTSUFDAT,ENDDTE
          GOTO      DNRC1000 IF LESS        * End date < suspension start
.
          PACK      TCINDC2,SP2
          MATCH     SP3,WTSULSTS
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSL,WTSULSTS
            CALL      RDCODE1               * Read a codes file record
          ENDIF
.
          MATCH     "2",TCINDC2             * not ready for care
          GOTO      DNRC1000 IF NOT EQUAL
.
          MOVE      WTSUFDAT,CDYSFDTE
          MOVE      WTSUTDAT,CDYSTDTE
.
          MATCH     SP8,WTSUTDAT
          IF        @EQUAL
            MOVE      ENDDTE,CDYSTDTE
          ELSE
            MATCH     ENDDTE,WTSUTDAT
            IF        !@LESS
              MOVE      ENDDTE,CDYSTDTE
            ENDIF
          ENDIF
.
          CALL      CALCDAYS              * Calculate diff between dates
          SUB       ONE,CDYSDAYS
          ADD       CDYSDAYS,DAYSNRC      * Incre the days not ready for care
.
          MATCH     SP70,WTSUTDAT
          IF        @EQUAL
            ADD       ONE,DAYSNRC         * still NRFC at end date, so add 1
          ELSE
            MATCH     ENDDTE,WTSUTDAT
            IF        !@LESS
              ADD       ONE,DAYSNRC       * still NRFC at end date, so add 1
            ENDIF
          ENDIF
.
          GOTO      DNRC1000
.
DNRC9000  MOVE      ZERO,EXIT
DNRC9999  RETURN
+
.******************************************************************************
.*                                  GTPC0000              Called by: PROC0000 *
.*                           Get Category TP Changes                          *
.******************************************************************************
GTPC0000  MOVE      ZERO,RECCNT
          MOVE      SP70,DTLURGIN
.
          PACK      KEY29,WMURNO,WMCODE,WMCNT,ANST,ANSP,SP20
          CALL      RSWTCAT2                * Position on & read code changes
GTPC1000  CALL      RKWTCAT2                  file record
          BRANCH    OVRCD,GTPC3000
.
          MATCH     WMURNO,WTCAURNO
          GOTO      GTPC3000 IF NOT EQUAL   * Different U/R no
.
          MATCH     WMCODE,WTCAPROC
          GOTO      GTPC3000 IF NOT EQUAL   * Different procedure code
.
          COMPARE   WMCNT,WTCAPCNT
          GOTO      GTPC3000 IF NOT EQUAL   * Different procedure count
.
          MATCH     "TP",WTCACATF
          GOTO      GTPC3000 IF NOT EQUAL   * Different category
.
          MATCH     WTCAEDAT,ENDDTE
          GOTO      GTPC3000 IF LESS        * End date < effective date
.
          MOVE      SP1,TCINDC3
          MATCH     SP3,WTCACODF
          IF        !@EQUAL
.....       PACK      KEY5,ANST,ANSP,WTCACODF,SP70
.....       CALL      RDCODE1               * Read a codes file record
          ENDIF
.
....      MOVE      TCINDC3,SAVINDC3        * Save the 3rd indicator
.
          MOVE      SP1,TCINDC3
          MATCH     SP3,WTCACODT
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSP,WTCACODT,SP70
            CALL      RDCODE1               * Read a codes file record
          ENDIF
          MATCH     SP70,WTCACODF
          IF        @EQUAL
            MOVE      TCINDC3,SAVINDC3        * Save the 3rd indicator
          ENDIF
.
GTPC2000  ADD       ONE,RECCNT
          IF        RECCNT=1
            MOVE      TCINDC3,CLINURG
            MOVE      TCINDC3,CLURGR
            MATCH     SP3,WTCACODF
            GOTO      GTPC1000 IF EQUAL     * Blank 1st from change code
          ENDIF
.
          MATCH     SAVINDC3,TCINDC3
          GOTO      GTPC1000 IF EQUAL
.
          MOVE      TCINDC3,CLURGR
          MATCH     WTCAEDAT,WMDATE1
          IF        @EQUAL
            MOVE      SP8,WTCAEDAT
            MOVE      TCINDC3,CLURGR        * Clinical urgency cat last code
            GOTO      GTPC1000
          ENDIF
.
          MATCH     SP8,WTCAEDAT
          GOTO      GTPC2500 IF EQUAL
.
          UNPACK    WTCAEDAT,CCENT,CYEAR,CMON,CDAY
          PACK      CATRDATE,CDAY,CMON,CCENT,CYEAR
          REP       " 0",CATRDATE     * Clinical urgency cat last date
.
          MATCH     WTCACODF,WTCACODT
          IF        @LESS
            PACK      DTLURGIN,CCENT,CYEAR,CMON,CDAY
            REP       " 0",DTLURGIN     * Date of last Clinical Urgency
          ENDIF
.                                           * Increase
GTPC2500  MOVE      SAVINDC3,CLINURG
          MOVE      TCINDC3,CLURGR        * Clinical urgency cat last code
.
          GOTO      GTPC1000
.
GTPC3000  MATCH     LISTDATE,CATRDATE
          IF        @EQUAL
            MOVE      SP8,CATRDATE
            MOVE      CLINURG,CLURGR
            MOVE      SP8,CLINURG
          ENDIF
.
GTPC9000  MOVE      ZERO,EXIT
GTPC9999  RETURN
+
.******************************************************************************
.*                                  LOWU0000              Called by: PROC0000 *
.*         Calculate the number of days at a lower urgency                    *
.******************************************************************************
LOWU0000  MOVE      ZERO,NRFCDAYS
.
          MATCH     DTLURGIN,SP70
          IF        @EQUAL
            GOTO      LOWU9000
          ENDIF
.
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP70
          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
          CALL      RSWTSUS1
LOWU1000  CALL      RKWTSUS1
          BRANCH    OVRCD,LOWU3000
.
          MATCH     WMURNO,WTSUURNO
          GOTO      LOWU3000 IF NOT EQUAL   * Different U/R no
.
          MATCH     WMCODE,WTSUCODE
          GOTO      LOWU3000 IF NOT EQUAL   * Different procedure code
.
          COMPARE   WMCNT,WTSUCNT
          GOTO      LOWU3000 IF NOT EQUAL
.
          MATCH     WTSUFDAT,DTLURGIN
          GOTO      LOWU1000 IF LESS        * End date < suspension start
.
          MOVE      WTSUFDAT,CDYSFDTE
          MOVE      WTSUTDAT,CDYSTDTE
.
          MATCH     SP70,WTSUTDAT
          IF        @EQUAL
            MOVE      DTLURGIN,CDYSTDTE
          ELSE
            MATCH     DTLURGIN,WTSUTDAT
            IF        !@LESS
              MOVE      DTLURGIN,CDYSTDTE
            ENDIF
          ENDIF
.
          PACK      TCINDC1,SP2
          MATCH     SP3,WTSULSTS
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSL,WTSULSTS
            CALL      RDCODE1               * Read a codes file record
          ENDIF
.
          MATCH     "2",TCINDC2             * not ready for care
          GOTO      LOWU2000 IF EQUAL
.
LOWU2000  MATCH     WTSUFDAT,DTLURGIN
          IF        @LESS
            UNPACK    DTLURGIN,CDAY,CMON,CCENT,CYEAR
            PACK      CDYSFDTE,CCENT,CYEAR,CMON,CDAY
          ENDIF
.
          MATCH     SP8,WTSUTDAT
          IF        @EQUAL
            MOVE      DTLURGIN,CDYSTDTE
          ELSE
            MATCH     DTLURGIN,WTSUTDAT
            IF        !@LESS
              MOVE      DTLURGIN,CDYSTDTE
            ENDIF
          ENDIF
.
          CALL      CALCDAYS              * Calculate diff between dates
          SUB       ONE,CDYSDAYS
          ADD       CDYSDAYS,NRFCDAYS     * Incre the days not ready for care
.
          MATCH     SP70,WTSUTDAT
          IF        @EQUAL
            ADD       ONE,NRFCDAYS        * still NRFC at end date, so add 1
          ELSE
            MATCH     DTLURGIN,WTSUTDAT
            IF        !@LESS
              ADD       ONE,NRFCDAYS      * still NRFC at end date, so add 1
            ENDIF
          ENDIF
.
          GOTO      LOWU1000
.
LOWU3000  MATCH     WMDATE1,DTLURGIN
          IF        @LESS
            GOTO      LOWU9999
          ENDIF
          DAYSEP    WMDATE1,DTLURGIN,DAYSLCAT
          SUB       NRFCDAYS,DAYSLCAT
.
LOWU9000  MOVE      ZERO,EXIT
LOWU9999  RETURN
+
.******************************************************************************
.*                                  CINR0000              Called by: GTRE0000 *
.*         Calculate the number of days since clinical urgency increase       *
.******************************************************************************
CINR0000  MOVE      ZERO,NRFCDAYS
          MOVE      ZERO,TNRFCLCU
.
          MATCH     DTLURGIN,SP70
          IF        @EQUAL
            GOTO      CINR9000
          ENDIF
.
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP70
          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
          CALL      RSWTSUS1
CINR1000  CALL      RKWTSUS1
          BRANCH    OVRCD,CINR3000
.
          MATCH     WMURNO,WTSUURNO
          GOTO      CINR3000 IF NOT EQUAL   * Different U/R no
.
          MATCH     WMCODE,WTSUCODE
          GOTO      CINR3000 IF NOT EQUAL   * Different procedure code
.
          COMPARE   WMCNT,WTSUCNT
          GOTO      CINR3000 IF NOT EQUAL
.
          MATCH     WTSUFDAT,ENDDTE
          GOTO      CINR1000 IF LESS        * End date < suspension start
.
          MATCH     SP70,WTSUTDAT
          GOTO      CINR1500 IF EQUAL
.
          MATCH     DTLURGIN,WTSUTDAT
          GOTO      CINR1000 IF LESS        * End date < suspension start
.
CINR1500  PACK      TCINDC1,SP2
          MATCH     SP3,WTSULSTS
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSL,WTSULSTS
            CALL      RDCODE1               * Read a codes file record
          ENDIF
.
CINR2000  MOVE      WTSUFDAT,CDYSFDTE
          MOVE      WTSUTDAT,CDYSTDTE
.
          MATCH     DTLURGIN,WTSUFDAT
          IF        @LESS
            PACK      CDYSFDTE,DTLURGIN
          ENDIF
.
          MATCH     SP8,WTSUTDAT
          IF        @EQUAL
            MOVE      ENDDTE,CDYSTDTE
          ELSE
            MATCH     ENDDTE,WTSUTDAT
            IF        !@LESS
              MOVE      ENDDTE,CDYSTDTE
            ENDIF
          ENDIF
.
          CALL      CALCDAYS              * Calculate diff between dates
          SUB       ONE,CDYSDAYS
          ADD       CDYSDAYS,NRFCDAYS     * Incre the days not ready for care
.
          MATCH     SP70,WTSUTDAT
          IF        @EQUAL
            ADD       ONE,NRFCDAYS        * still NRFC at end date, so add 1
          ELSE
            MATCH     ENDDTE,WTSUTDAT
            IF        !@LESS
              ADD       ONE,NRFCDAYS      * still NRFC at end date, so add 1
            ENDIF
          ENDIF
.
          GOTO      CINR1000
.
CINR3000  MOVE      NRFCDAYS,TNRFCLCU
.
CINR9000  MOVE      ZERO,EXIT
CINR9999  RETURN
+
.******************************************************************************
.*                                  GBKD0000              Called by: PROC0000 *
.*                             Get Booking Details                            *
.******************************************************************************
GBKD0000  PACK      KEY16,WMURNO,SP20
          CALL      RSBKREC6                * Position on & read a booking
GBKD1000  CALL      RKBKREC6                  file record
          BRANCH    OVRCD,GBKD3000
.
          MATCH     WMURNO,BKREURNO
          GOTO      GBKD3000 IF NOT EQUAL   * Different U/R no
.
          MATCH     WMCODE,BKREPROC
          GOTO      GBKD1000 IF NOT EQUAL   * Different procedure code
.
          COMPARE   WMCNT,BKRECNT
          GOTO      GBKD1000 IF NOT EQUAL   * Different procedure count
.
          MATCH     BKREBKDT,ENDDTE
          GOTO      GBKD1000 IF LESS        * End date < date booking made
.
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          IF        WMSTAT<>1
            COMPARE   TWO,BKRESTAT
            IF        !@EQUAL
              PACK      SADMDATE,CDAY,CMON,CCENT,CYEAR
              REP       " 0",SADMDATE        * Date scheduled for admin
            ENDIF
          ENDIF
.
          MATCH     SP3,BKRECANC
          IF        @EQUAL
            GOTO      GBKD1000              * Blank cancellation code, get next
          ENDIF
.
          GOTO      GBKD1000
.
GBKD3000  PACK      KEY8,WMBOOK
          CALL      RDBKREC1                * Read a booking file record
.
GBKD9000  MOVE      ZERO,EXIT
GBKD9999  RETURN
+
.******************************************************************************
.*                                  GRSD0000              Called by: PROC0000 *
.*                         Get Removed Status Defaults                        *
.******************************************************************************
GRSD0000  IF        WMSTAT<4
            GOTO      GRSD9000
          ENDIF
.
          IF        WMSTAT=4 | WMSTAT=5
            MATCH     ADATE,ENDDTE
            GOTO      GRSD9000 IF LESS      * End date < admin date
          ENDIF
.
          MOVE      " 9",IHCP
          IF        WMSTAT=6
            MATCH     WMDATE4,ENDDTE
            GOTO      GRSD9000 IF LESS      * End date < removal date
            PACK      KEY5,ANSW,ANSR,WMREMOVE
            CALL      RDCODE1               * Read a codes file record
            BRANCH    OVRCD,GRSD9000
            MOVE      TCINDC1,RREASON 
            MATCH     "1",RREASON
            IF        @EQUAL
              PACK      IHCP,TCINDC2
              REP       " 9",IHCP
            ENDIF
            MATCH     "2",RREASON
            IF        @EQUAL
              PACK      IHCP,TCINDC2
              REP       " 9",IHCP
            ENDIF
            UNPACK    WMDATE4,CCENT,CYEAR,CMON,CDAY
            PACK      REMVDATE,CDAY,CMON,CCENT,CYEAR
            REP       " 0",REMVDATE          * Date of removal - default to
          ENDIF
.
          MOVE      WMBOOK,AADMNO
          PACK      KEY8,AADMNO
          CALL      RDMISS1                 * Read an admin file record
          IF        OVRCD<>1
            MOVE      "1",RREASON 
            UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
            PACK      REMVDATE,CDAY,CMON,CCENT,CYEAR
            REP       " 0",REMVDATE          * Date of removal - default to
            PACK      EPIS,AADMNO,SP70
            MATCH     SP70,ACLSSFT
            IF        !@EQUAL
              PACK      KEY5,ANSA,ANSC,ACLSSFT,SP70
              CALL      RDCODE1
              IF        OVRCD<>1
                UNPACK    THCSCOD,CLINUNIT
              ENDIF
            ENDIF
          ENDIF                               admin date
.
          PACK      KEY11,WMBOOK,SP70
          CALL      RDSMMBS1
          CALL      RDKMMBS1
          BRANCH    OVRCD,GRSD9000
.
          MATCH     WMBOOK,MMADMN
          GOTO      GRSD9000 IF NOT EQUAL
.
          UNPACK    MMDATE,CCENT,CYEAR,CMON,CDAY
          PACK      PROCDATE,CDAY,CMON,CCENT,CYEAR
.
GRSD9000  MOVE      ZERO,EXIT
GRSD9999  RETURN
+
.******************************************************************************
.*                                  CLEAR000              Called by: ML0000   *
.*                         Clear the extract variables                        *
.******************************************************************************
CLEAR000  UNPACK    SP70,PIN      
          UNPACK    SP70,PMI   
          UNPACK    SP70,LISTID   
          UNPACK    SP70,SEX      
          UNPACK    SP70,DOB      
          UNPACK    SP70,POSTCODE 
          UNPACK    SP70,SSLA     
          UNPACK    SP70,WAITCAT  
          UNPACK    SP70,LISTDATE 
          UNPACK    SP70,CENSDATE 
          UNPACK    SP70,LISTSTAT 
          MOVE      ZERO,DAYSNRC  
          MOVE      ZERO,DAYSLCAT 
          MOVE      ZERO,WTCENSUS 
          MOVE      ZERO,WTREM    
          UNPACK    SP70,CLINURG  
          UNPACK    SP70,CLURGR   
          UNPACK    SP70,CATRDATE 
          UNPACK    SP70,OVERDUE  
          UNPACK    SP70,SURGSPEC 
          UNPACK    SP70,INDPROC  
          UNPACK    SP70,PPROC    
          UNPACK    SP70,SADMDATE 
          UNPACK    SP70,PROCDATE 
          UNPACK    SP70,REMVDATE 
          UNPACK    SP70,RREASON  
          UNPACK    SP70,ESDOCTOR 
          UNPACK    SP70,EPIS     
          UNPACK    SP70,LOCALITY 
          UNPACK    SP70,CLINUNIT 
          UNPACK    SP70,IHCP  
          UNPACK    SP70,ILOHS 
          UNPACK    SP70,INDIGEN
.
CLEAR999  RETURN
.******************************************************************************
.*                                  WRITE000              Called by: ML0000   *
.*                         Write the extract variables                        *
.******************************************************************************
WRITE000  IF        NEWLAYT=1
            WRITE     EXTRACTC,SEQ;HOSPID,PMI,PIN,LISTID,SEX:
                                 DOB,POSTCODE,STATEID,WAITCAT,LISTDATE:
                                 CENSDATE,LISTSTAT,DAYSNRC,DAYSLCAT,WTCENSUS:
                                 WTREM,CLINURG,CLURGR,CATRDATE,OVERDUE:
                                 SURGSPEC,INDPROC,PPROC,SADMDATE,PROCDATE:
                                 REMVDATE,RREASON,ESDOCTOR,EPIS:
                                 LOCALITY,CLINUNIT,IHCP,INDIGEN,ILOHS
          ELSE
            WRITE     EXTRACTB,SEQ;HOSPID,PMI,PIN,LISTID,SEX:
                                 DOB,POSTCODE,STATEID,WAITCAT,LISTDATE:
                                 CENSDATE,LISTSTAT,DAYSNRC,DAYSLCAT,WTCENSUS:
                                 WTREM,CLINURG,CLURGR,CATRDATE,OVERDUE:
                                 SURGSPEC,INDPROC,PPROC,SADMDATE,PROCDATE:
                                 REMVDATE,RREASON,ESDOCTOR,EPIS:
                                 LOCALITY,CLINUNIT,IHCP
           ENDIF
.
WRITE999  RETURN
.==============================================================================
.
          INC       STD001IO
          INC       CALCDAYS                * Calculate diff between dates
          INC       CALCAGE 
          INC       PATCOMN3
.
          INC       BOKRC1IO/INC            * Booking file
          INC       IBAPOSIO/INC            * Postcode file
          INC       PATDSCIO/INC            * Discharge file
          INC       PATCODIO/INC            * Codes file
          INC       PATDRGIO/INC            * Date Range
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC            * Master file
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATMI1IO/INC            * Admission file
          INC       PATVADIO/INC            * Transfer source file
          INC       PATCT1IO/INC
          INC       PATMMBIO/INC
          INC       WATCATIO/INC            * Code changes file
          INC       WATPROIO/INC            * Procedure file
          INC       WATRTDIO/INC            * Referral Destination file
          INC       WATSUSIO/INC            * Procedure Suspension file
          INC       WATTR1IO/INC            * Treatment file
          INC       WATTX1IO/INC            * Treatment Extention file
          INC       PATHSPKY
+
.


.******************************************************************************
.* System    :  OUTPATIENTS                                                   *
.* Program   :  IBAOUT31                                                      *
.* Desc      :  Update Monthly Statistics                                     *
.******************************************************************************
.* Date      :  09/05/1989                                                    *
.* Author    :  Eric Phan                                                     *
.* Function  :  This program recreates statistics for various outpatient files*
.*              for a nominated month/year.  Note that existing figures are   *
.*              overwritten with the newly calculated ones.                   *
.* Mods.     :                                                                *
.*        V12.00.01 28/05/2025  PJ Le Febour      TSK 0955096 AlphaNum visitnm*
.*                  replace  SAVADMN FORM 8 to SAVADMN DIM 8                  *
.*                           COMPARE   AADMNO,TADMN to  MATCH     AADMNO,TADMN*
.*                          COMPARE OBAOUTNO,PVIBILL to MATCH OBAOUTNO,PVIBILL*
.******************************************************************************
.*        V10.12.01 20/03/2018  Richa Phogat      TSK 0261630                 *
.*                  Removed multiple instances of PORT                        *
.******************************************************************************
.*        V10.02.01 27/06/2011  Steve Armstrong   CAR 245347                  *
.*                  Mods for changes to OUTLOCIO call routines                *
.******************************************************************************
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.02.02  28/11/2002  Lina Vo                                       *
.*                  Remove site specific open of outsitaf                     *
.*        V9.02.01  24/10/2001  B.G.Ackland                                   *
.*                  Remove pi on Temp File                                    *
.*        V9.02.00  10/10/2001  Dean Cramer                                   *
.*                  Modified for web access.                                  *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*           V5.07.02  13/08/1999  Steve Downing                              *
.*                     Redundant code removed, mods to use DAYSEP             *
.*           V5.07.01  22/07/1999  Steve Downing                              *
.*                     Update century when calculating end period if year     *
.*                     reaches "00", Proceed option accepts upper & lower case*
.*           V5.07.00  30/10/1998  Jim Stathopoulos                           *
.*                     507 Changes                                            *
.******************************************************************************
.*              V5.10  09/05/89 ETP Rewrote this vers. from IBAOUT31 V5.06    *
.*                     excluding MHMS stats & adding outoccaf,outstbaf, site  *
.*                     range & dept type.                                     *
.*              V5.11  01/06/89 ETP Fixed attendance file stats generation    *
.*              V5.12  03/06/89 S.Barcham          SRF 100751                 *
.*                     Split OUTBOK & OUTMAS includes.                        *
.*              V5.13  05/06/89 Graeme Williams                               *
.*                     Fix up zero-fill of temporary file names               *
.*              V5.14  05/06/89 S.Barcham          SRF 100809                 *
.*                     Recompiled for locking of PATIOMAS                     *
.*              V5.15  07/06/89 E.Phan                                        *
.*                     Changed attendance routine for non-booked attendees    *
.*                     Temporarily uses PROC6000 & PROC7000 replacing PROC4000*
.*                     & PROC5000 till time is available for rationalising the*
.*                     reports they're used for.                              *
.*              V5.16  09/06/89 Graeme Williams    SRF 101009                 *
.*                     Put Inpatient Statistics update back in                *
.*              V5.17  20/06/89 Graeme Williams    SRF 101112                 *
.*                     Change RAOTLOC1 to RDOTLOC1 to try to fix I*F UPOTLOC1 *
.*                     Also changed most of the RDA's to full reads           *
.*              V5.18  24/06/89 S.Barcham                                     *
.*                     Compiled for OUTBB1FD                                  *
.*              V5.19  26/06/89 K.Peak             SRF 101020                 *
.*                     Fixed location file problem                            *
.*              V5.20  03/07/89 Graeme Williams    SRF 101251                 *
.*                     Fix up Occassions of Service update                    *
.*                     Initialise files before updates                        *
.*              V5.21  03/07/89 DIG                                           *
.*                     Changed to get source of referral from OUTSITAF file   *
.*              V5.22  07/07/89 Graeme Williams    SRF 101410                 *
.*                     Fix I*D bug with location file                         *
.*              V5.23  12/07/89 E.Phan             SRF 101532                 *
.*                     Modified to use only 3 options. Removed warning.       *
.*                                                                            *
.*              V5.01.01  14/07/89 S. O'Gorman                                *
.*                        Converted for N.Z.                                  *
.*                        21/07/89 Graeme Williams                            *
.*                        Allow for 13x4 week periods. Also improved slightly *
.*                        the efficiency of some of the read loops.  The loop *
.*                        over the Discharge file was always starting from the*
.*                        beginning instead of starting at the from date.     *
.*                        The loop over the BOKA file would always read all   *
.*                        non-attendances, instead of just looking at the     *
.*                        period in question.                                 *
.*              V5.01.02  03/10/89 K.Peak          SRF 102513                 *
.*                        Month/Year to Period/Year                           *
.*              V5.01.03  07/02/90 J.Tan           SRF 103500                 *
.*                        Fixed number of days in the periods                 *
.*              V5.01.04  19/02/90 J.Tan           SRF 103975                 *
.*                        Include the TODATE for no of days                   *
.*              V5.01.05  01/05/90 S. O'Gorman     Balancing Wellington       *
.*                        Checked "R" record in the tran file for a change of *
.*                        ward from "L" record and if so make the old ward    *
.*                        have a transfer out and the new ward have a transfer*
.*                        in                                                  *
.*              V5.01.06  11/05/90 Glen Hobby                                 *
.*                        Added check on system parameter PTCNNEWS to find out*
.*                        if we are using the new stats updates               *
.*              V5.01.07  21/06/90 J.Tan                                      *
.*                        Mods to calculate the separation bed days           *
.*              V5.01.08  11/07/90 Paul Duncan                                *
.*                        Recompiled for OUTBOA & OUTBOB audits               *
.*              V5.01.09  04/09/90 J.Tan           SRF 757                    *
.*                        Mods to include attendances with no catchment code  *
.*              V5.01.121 25/10/90 D.Di.Paola.     SRF 106407                 *
.*                        Added code to generate statistics for clinic open   *
.*                        sessions statistics file .....                      *
.*                        Used for Outpatient clinic statistics.              *
.*              V5.01.122 29/11/90 D.Di.Paola.     SRF 106407                 *
.*                        Fixed bug in extraction of bookings.  Now uses      *
.*                        fromdate and todate instead of period, as a range.. *
.*              V5.01.123 14/12/90 D.Di.Paola      SRF 107295                 *
.*                        Fixed clinic open sessions to exclude attendances   *
.*                        with extended slots....                             *
.*              V5.01.124 25/02/91 i chung         SRF 106812                 *
.*                        Fixed only to display Update Outpatient Statistics  *
.*                        option if system parameter PTCNNEWS is turned on    *
.*              V5.01.125 07/02/92 Justin Coates  (Southland changes)         *
.*                        Allow for Non-booked attendees                      *
.*              V5.01.126 07/02/92 i chung         SRF 115638                 *
.*                        Fixed non-booked attendees variable not incrementing*
.*                        correctly                                           *
.*              V5.01.127 14/07/93  WHIRLPOOL        SRF 123030               *
.*                        Dudley postcode changes                             *
.*        V5.01.128 25/03/1994  Glenn Berry      Taranaki Mods (3.3.5)        *
.*                  Write OTATDSCH to OUTATTFD                                *
.*        V5.01.129 22/04/1994  Glenn Berry                                   *
.*                  Fix temp site file                                        *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       PATCOMM/INC
.
          INC       OUTATTFD/INC        * Attendance file
          INC       OUTBOAFD/INC        * Bookings file
          INC       OUTBB1FD/INC        * Bookings file
          INC       OUTCTYFD/INC        * Clinic type file
          INC       OUTLOCFD/INC        * Location file
          INC       OUTOCCFD/INC        * Occasions of service file
          INC       OUTSESFD/INC        * Clinic open sessions file
          INC       OUTSITFD/INC        * Site file
          INC       OUTSTBFD/INC        * A&E referral statistics file
          INC       OUTUSEFD/INC        * used session file
.
          INC       PATCT1FD/INC        * Catchment file
          INC       PATCODFD/INC        * Codes file
          INC       PATCONFD/INC        * Inpatient control file
          INC       PATCSTFD/INC        * Catchment summary file
          INC       PATDRGFD/INC        * Period Date Range file
          INC       PATDSCFD/INC        * Discharge file
          INC       PATMA1FD/INC        * Master file
          INC       PATMI1FD/INC        * Admissions file
          INC       PMSVX1FD/INC        * Admissions file
          INC       PATMSTFD/INC        * Inpatient statistics summary file
          INC       PATTRNFD/INC        * Bed transfer file
          INC       PATVISFD/INC        * Visit file
          INC       PATWR1FD/INC        * Ward/bed file
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.====================================================
. TEMPORARY FILE DESCRIPTIONS 
.====================================================
.
.********************************************************************** 
.-- Temporary File A  - Site file prefix 
TMPFILA1  IFILE     SQL, FIXED=4, KEY="U1-3"
TMPAFNAM  DIM       8
.
. NAME    TYPE    LENGTH   PHYSICAL  START LOC.  DESCRIPTION
. ----    ----    ------   --------  ----------  -----------
.OSTFILE  DIM       3         3           1       File Prefix
.
.End of record                            4
.********************************************************************** 
.
.      
.
PATTEMP1  IFILE     SQL, FIXED=24, KEY="U1-23"
.
.  ----- Tempfile consists of following vars ------
.        -----------------------------------
.          Type     Length    Physical   Start
.          ----     ------    --------   -----
.SMSWARD   DIM        3          3         1
.SMSDCLS   DIM        3          3         4
.SMSWCLS   DIM        3          3         7
.SMSDATE   DIM        6          6        10
DSAVADMN   DIM        8          8        16
.End of Record                            24
.
BRDFLG    FORM      1
.
CALDAYS   FORM      3
CHGWRD    FORM      1
CMDLINE   DIM       50
CODEA     INIT      "A "      * Admission Type
CODED     INIT      "D "      * Discharge Status
CODECV    INIT      "CV"      * Visit Type        
CODEO     INIT      "O "      * Outpatient Department
CODES     INIT      "S "      * Source of Referral (for inpatients only)
CODEP     INIT      "P "      * Admission Class
.
DAYCASE   FORM      1
DIM6      DIM       6         * for matching months
DIM15     DIM       15
DIM15A    DIM       15
DIM2A1    DIM       2
DIM2B1    DIM       2
DIM2A2    DIM       2
DIM2B2    DIM       2
DSHFLG    FORM      1
DSCOUNT   FORM      3
DSTRDESC  DIM       30        * for displaying Starting Site desc after "?"
DSTRSITE  DIM       6         * for displaying Starting Site after "?"
.
FILEFLG   FORM      1
FIRST     FORM      1
FIRSTREC  FORM      1
FMONTH    DIM       6
FROMDATE  DIM       8
FORM2A1   FORM      2
FORM2B1   FORM      2
FORM2A2   FORM      2
FORM2B2   FORM      2
FORM4B    FORM      4
FORM7A    FORM      7         * Storage for attendees
FORM7B    FORM      7         * Storage for non-attendees
FORM8B    FORM      8
.
HOAAESCD  DIM       3         * Outpat Ref Source - controlf,31;*98
HMAAESCD  DIM       3         * Mammog Ref Source - controlf,54;*98
HRAAESCD  DIM       3         * Radiol Ref Source - controlf,57;*98
.
INDIC     FORM      1
.
JDAYS     FORM      3
JYEAR     FORM      2
.
JAN       FORM      "31"
FEB       FORM      "28"
MAR       FORM      "31"
APR       FORM      "30"
MAY       FORM      "31"
JUN       FORM      "30"
JUL       FORM      "31"
AUG       FORM      "31"
SEP       FORM      "30"
OCT       FORM      "31"
NOV       FORM      "30"
DEC       FORM      "31"
.
.
LYR       FORM      1
LEVFLG    FORM      1
.
NBRDAYS   FORM      4
NMONTH    DIM       6
.
OTDATE    DIM       8
OYEAR     FORM      "365"
OUT       INIT      "out"     * for outsitaf
OUTTM1    DIM       8         * Temp file for site
OUTTM1XX  IFILE     SQL, FIXED=10, KEY="U1-9"
OUTTM2    DIM       8         * Temp file for outattaf - same format
OUTTM2XX  IFILE     SQL, FIXED=48, KEY="U1-24"
OUTTM3    DIM       8         * Temp file for outsesaf 
OUTTM3XX  IFILE     SQL, FIXED=93, KEY="U1-25"
TEMPTM1   DIM       8
TEMPTM2   DIM       8
TEMPTM3   DIM       8   
.
PATNUM    FORM      1
PUBFLG    FORM      1
ROW       FORM      2         * horizontal display position for "Select Option"
.
SKEY30    DIM       30
SAVADMN   DIM       6
SAVCATCH  DIM       4         * Save variable for catchment code
SAVGROUP  DIM       3
SAVCLS    DIM       3
SAVCTYP   DIM       6
SAVPREF   DIM       3         * Save variable for file prefix
SAVWRD    DIM       3
SAVDATE   DIM       8
SELDAT    FORM      3
SMSWARD   DIM       3
SMSDCLS   DIM       3
SMSWCLS   DIM       3
SMSDATE   DIM       8
.
TEMPF2    FORM      2
TEMPF3    FORM      3
TMPNEW    FORM      3
TMPREV    FORM      3
TMPSPC    FORM      3
TODATE    DIM       8
TTODATE   DIM       8
.
VCURDATE  DIM       6         * Current period - CCYYMM
VFRDATE   DIM       8         * From Date for current period
VTODATE   DIM       8         * To Date for current period
VSOURCE   DIM       3         * Code for A&E source referrals - Category S
VSTRSITE  DIM       6         * Starting Site
VENDSITE  DIM       6         * Ending Site
VOUTDEPT  FORM      6         * Outpat.Dept. - TCFORM6 from CODEO,OSTSYST
VTYPEN    INIT      "N"       * Visit Type = new 
VTYPER    INIT      "R"       * Visit Type = Revisit
VTYPEV    INIT      "V"       * Visit Type = special visit
.
WDATE1    DIM       8
WDATE2    DIM       8
WSTAT     FORM      1
.
YDATE     DIM       4
.
PRGID     INIT      "IBAOUT31"
PRGNAM    INIT      "Update Monthly Statistics"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
ML0000    CALL      INIT0000          * Initialization and open files
          CALL      SITE0000          * Set up alternative Site file
.
ML0100    CALL      OPTN0000          * select options
          BRANCH    EXIT,ML9900       * EXIT=1 if "0" chosen in menu
          BRANCH    PTCNNEWS,ML0200
          BRANCH    OPTION,ML1000,ML2000,ML3000
          GOTO      ML0100
.
ML0200    BRANCH    OPTION,ML1000
          GOTO      ML0100
.
. ----- Update Outpatient Statistics -----
.
ML1000    CALL      PROC2000          * Process Attendance Statistics
          COMPARE   ONE,FIRSTREC      * If set, no records found
          CALL      ML9200 IF EQUAL
          CALL      PROC3000          * Process Location Statistics
          COMPARE   ONE,FIRSTREC      * If set, no records found
          CALL      ML9300 IF EQUAL
          CALL      PROC6000          * Process Occasions of Service Statistics
          COMPARE   ONE,FIRSTREC      * If set, no records found
          CALL      ML9400 IF EQUAL
          CALL      PROC7000          * Process Referral Source Statistics
          COMPARE   ONE,FIRSTREC      * If set, no records found
          CALL      ML9500 IF EQUAL
          GOTO      ML0100
.
. ----- Update Inpatient Statistics -----
.
ML2000    CALL      IPRO0000          * Process Inpatient Statistics
          GOTO      ML0100
.
. ----- Update All Statistics -----
.
ML3000    CALL      PROC2000         * Process Attendance & clinic session stats
          COMPARE   ONE,FIRSTREC      * If set, no records found
          CALL      ML9200 IF EQUAL
          CALL      PROC3000          * Process Location Statistics
          COMPARE   ONE,FIRSTREC      * If set, no records found
          CALL      ML9300 IF EQUAL
          CALL      PROC6000          * Process Occasions of Service Statistics
          COMPARE   ONE,FIRSTREC      * If set, no records found
          CALL      ML9400 IF EQUAL
          CALL      PROC7000          * Process Referral Source Statistics
          COMPARE   ONE,FIRSTREC      * If set, no records found
          CALL      ML9500 IF EQUAL
          CALL      IPRO0000          * Process Inpatient Statistics
          GOTO      ML0100
.
. ------  No records found in processing pass  ------
.
ML9200    MATCH     "IBARSH",PGM
          GOTO      ML9600 IF EQUAL
. 
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "*** No Attendance records found *** ";
          CALL      HITENTER
          RETURN
.
ML9300    MATCH     "IBARSH",PGM
          GOTO      ML9600 IF EQUAL
. 
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "*** No Location records found *** ";
          CALL      HITENTER
          RETURN
.
ML9400    MATCH     "IBARSH",PGM
          GOTO      ML9600 IF EQUAL
. 
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "*** No Occasions of Service records found *** ";
          CALL      HITENTER
          RETURN
.
ML9500    MATCH     "IBARSH",PGM
          GOTO      ML9600 IF EQUAL
. 
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "*** No Referral Source records found *** ";
          CALL      HITENTER
          RETURN
.
ML9600    RETURN
.
. ------  The end  ------
.
ML9900    CHAIN     PGM               * chain back to appropriate menu
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialization                               *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,THIRTY1;*98,HOAAESCD  * Outpatients A&E source code
          READ      CONTROLF,FIFTY4;*98,HMAAESCD   * Mammography A&E source code
          READ      CONTROLF,FIFTY7;*98,HRAAESCD   * Radiology A&E source code
          READ      CONTROLF,FIFTY9;*27,CPERMTH,*173,PTCNNEWS     
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          CALL      TFILENAM
          MOVE      TFILNAME,TMPAFNAM
.
          CALL      TFILENAM
          MOVE      TFILNAME,TEMPTM1
.
          CALL      TFILENAM
          MOVE      TFILNAME,TEMPTM2
.
          CALL      TFILENAM
          MOVE      TFILNAME,TEMPTM3
.
INIT9999  RETURN 
+
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.****************************************************************************
OPTN0000  MOVE      ONE,EXIT            * Exit condition defaults to true
          MOVE      ZERO,FORM1          * For redisplay after 2nd "?" option
          HLINE     *G37,2,53,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Update Outpatient Statistics";
.
          BRANCH    PTCNNEWS,OPTN0040            * using new stats update ?
          DISPLAY   *P1:6,*V2LON,"2",*HOFF," = Update Inpatient Statistics":
                    *P1:7,*V2LON,"3",*HOFF," = Update All Statistics";
          MOVE      NINE,ROW
          GOTO      OPTN0050
.
OPTN0040  MOVE      SEVEN,ROW
.
OPTN0050  DISPLAY   *P1:ROW,"Select Option :";
.
OPTN0060  KEYIN     *P17:ROW,*EL,*DV,UNDLN1,*P17:ROW,*V2LON,OPTION;
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL
.
OPTN0070  BRANCH    PTCNNEWS,OPTN0080
          BRANCH    OPTION,OPTN0100,OPTN0200,OPTN0300
          GOTO      OPTN0090
.
OPTN0080  BRANCH    OPTION,OPTN0100
.
OPTN0090  BEEP
          GOTO      OPTN0060
.
OPTN0100  DISPLAY   *P53:2,*V2LON,"- Outpatient Statistics ";
          GOTO      OPTN1000
.
OPTN0200  DISPLAY   *P53:2,*V2LON,"- Inpatient Statistics ";
          GOTO      OPTN8000
.
OPTN0300  DISPLAY   *P53:2,*V2LON,"- All Statistics ";
          GOTO      OPTN1000
.
.----------------------------------------------------------------------------
.         Keyin range of sites
.----------------------------------------------------------------------------
OPTN1000  BRANCH    FORM1,OPTN1400
          KEYIN     *P1:12,*EF,"Starting Site : ",*V2LON,VSTRSITE;
          ENDSET    VSTRSITE
          APPEND    SP6,VSTRSITE
          RESET     VSTRSITE
          MATCH     SP6,VSTRSITE
          GOTO      OPTN1100 IF NOT EQUAL
.
          MOVE      "Start ",DSTRSITE
          MOVE      SP30,DSTRDESC
          DISPLAY   *P17:12,*V2LON,DSTRSITE;
          GOTO      OPTN1400
.
OPTN1100  CMATCH    QUEST,VSTRSITE
          GOTO      OPTN1200 IF NOT EQUAL
          CALL      OUTDSSIT
          HLINE     *G37,2,53,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Update Outpatient Statistics";
.
          BRANCH    PTCNNEWS,OPTN1110
          DISPLAY   *P1:6,*V2LON,"2",*HOFF," = Update Inpatient Statistics":
                    *P1:7,*V2LON,"3",*HOFF," = Update All Statistics":
                    *P1:9,"Select Option : ",*V2LON,OPTION;
          GOTO      OPTN0070
.
OPTN1110  DISPLAY   *P1:7,"Select Option : ",*V2LON,OPTION;
          GOTO      OPTN0070
.
OPTN1200  PACK      KEY6,VSTRSITE
          CALL      RDSITA1
          BRANCH    OVRCD,OPTN1300
          MOVE      VSTRSITE,DSTRSITE
          MOVE      OSTDESC,DSTRDESC
          DISPLAY   *P30:12,DSTRDESC;
          GOTO      OPTN1400
.
OPTN1300  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Site **    ";
          CALL      HITENTER
          GOTO      OPTN1000
.
OPTN1400  MOVE      ONE,FORM1           * After "?" redisplay come here again
          KEYIN     *P1:13,*EF,"Ending   Site : ",*V2LON,VENDSITE;
          ENDSET    VENDSITE
          APPEND    SP6,VENDSITE
          RESET     VENDSITE
          MATCH     SP6,VENDSITE
          GOTO      OPTN1500 IF NOT EQUAL
.
          DISPLAY   *P17:13,*V2LON,"Finish";
          MOVE      "ZZZZZZ",VENDSITE
          GOTO      OPTN2000
.
OPTN1500  CMATCH    QUEST,VENDSITE
          GOTO      OPTN1600 IF NOT EQUAL
          CALL      OUTDSSIT
          HLINE     *G37,2,53,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Update Outpatient Statistics";
.
          BRANCH    PTCNNEWS,OPTN1510
          DISPLAY   *P1:6,*V2LON,"2",*HOFF," = Update Inpatient Statistics":
                    *P1:7,*V2LON,"3",*HOFF," = Update All Statistics":
                    *P1:9,"Select Option : ",*V2LON,OPTION;
          GOTO      OPTN1520
.
OPTN1510  DISPLAY   *P1:7,"Select Option : ",*V2LON,OPTION;
.
OPTN1520  DISPLAY   *P1:12,"Starting Site : ",*V2LON,DSTRSITE,*HOFF:
                                             *P30:12,DSTRDESC;
          GOTO      OPTN0070
.
OPTN1600  PACK      KEY6,VENDSITE
          CALL      RDSITA1
          BRANCH    OVRCD,OPTN1700
          DISPLAY   *P30:13,OSTDESC;
          GOTO      OPTN2000
.
OPTN1700  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Site **    ";
          CALL      HITENTER
          GOTO      OPTN1400
.
.         Check for valid Sites range
.
OPTN2000  MATCH     VSTRSITE,VENDSITE
          GOTO      OPTN3000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON,"Ending Site must be > or equal":
                                         " to Starting Site.  ";
          CALL      HITENTER
          GOTO      OPTN1400
.
.----------------------------------------------------------------------------
.         Set up A+E source code
.----------------------------------------------------------------------------
OPTN3000  MOVE      HOAAESCD,VSOURCE
.
.----------------------------------------------------------------------------
.          Keyin dates
.----------------------------------------------------------------------------
OPTN8000  DISPLAY   *P1:16,*EF,"Generate statistics for ",CPERMTH," : ";
          UNPACK    SP4,CMON,CYEAR
          MOVE      CCC,CCENT
          MOVE      ZERO,CHIGHLT
          MOVE      "34",CCOL
          MOVE      TEN6,CVERT
          MOVE      "55",ECOL
          MOVE      TEN6,EVERT
          CALL      KEYPER                   * Keyin the new period
          BRANCH    OVRCD,OPTN0000
.
.         Validate the date entered
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,OPTN8800           * Invalid date
.
          PACK      VCURDATE,DRGYR,DRGNUM    * Current period CCYYMM
          MOVE      DRGFDTE,VFRDATE          * From date for period
          MOVE      DRGTDTE,VTODATE          * To date for period
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          UNPACK    DRGTDTE,XCENT,XYEAR,XMON,XDAY
          DISPLAY   *P46:16,CDAY,"/",CMON,"/",CCENT,CYEAR:
                    *P46:17,XDAY,"/",XMON,"/",XCENT,XYEAR;
.
.         Add one to the VTODATE, including the TODATE
.
.         UNPACK    DRGTDTE,XCENT,XYEAR,XMON,XDAY
          MOVE      XCENT,CC
          MOVE      XYEAR,YY
          MOVE      XMON,MM
          MOVE      XDAY,DD
          CALL      DMYCON
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      TTODATE,CC,YY,MM,DD
          REP       " 0",TTODATE
.
.         Calculate the dates difference (number of days in the period)
.
OPTN8090  DAYSEP    DRGFDTE,TTODATE,SELDAT
          GOTO      OPTN9000
.
.         Invalid date
.
OPTN8800  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Date **    ";
          CALL      HITENTER
          GOTO      OPTN8000
.
.----------------------------------------------------------------------------
.         Ok to proceed ?
.----------------------------------------------------------------------------
OPTN9000  MOVE      SP1,ANS
          KEYIN     *P1:24,*EL,"Ok to proceed (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,"/",*V2LON,"C",*HOFF,") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          GOTO      OPTN9990 IF EQUAL
          MATCH     ANSN,ANS
          GOTO      OPTN9500 IF EQUAL
          MATCH     ANSC,ANS
          GOTO      OPTN0000 IF EQUAL
          BEEP
          GOTO      OPTN9000
.
OPTN9500  MOVE      ZERO,FORM1
          GOTO      OPTN0070
.
OPTN9990  MOVE      ZERO,EXIT           * Exit condition is false
.
OPTN9999  RETURN
+
.**************************************************************************
.*                              PROC2000                                  *
.*                        Process Attendance File                         *
.**************************************************************************
.*  Status : Not tested with extensive data                               *
.*  Notes  : Because referral source is not part of an index, temp files  *
.*           are used for initial collation of data which is then updated *
.*           to the attendance stats file. This ensures that initial file *
.*           is "clean" & legitimate data is not wiped out accidentally.  *
.*           Pass 1 - Read through Site file to create temp data files.   *
.*           Pass 2 - Process bookings file & update stats to temp files. *
.*                  - also for open sessions file for status 4 and 5.     *
.*           Pass 3 - Process non-booked attendees from outbb1af.         *
.*           Pass 4 - Update temp file data to attendance stats files.    *
.*           Pass 5 - Process bookings file & update status to tempfile   *
.*                    for open sessions file for status 1 = booked.....   *
.*           Pass 6 - Update temp file data to Clinic sessions stats file *
.*           Pass 7 - Process Discharges                                  *
.**************************************************************************
PROC2000  CALL      PROC2100                 * Pass 1
          CALL      PROC2200                 * Pass 2
          CALL      PROC2400                 * Pass 3
          CALL      PROC2800                 * Pass 4
          CALL      PROC2900                 * Pass 5
          CALL      PROC8000                 * Pass 6
          CALL      PDSC0000                 * Processs Discharges (Pass 7)
          RETURN
+
.------------------------------------------------------------------------------
.         PASS 1 - Create various temp files
.------------------------------------------------------------------------------
PROC2100  DISPLAY   *P1:18,*EF,*V2LON,"*** Processing Attendance ":
                    "File ***",*HOFF," - Pass 1",*P1:20,*EL,"Creating ";
          PACK      KEY6,VSTRSITE            * Starting Site
          CALL      RDSITA1                  * Positioning read
          COMPARE   ZERO,OVRCD               * Got record ?
          GOTO      PROC2120 IF EQUAL        * Yes - skip readks
.
PROC2110  CALL      RDKSITA1                 * Read next site record
          BRANCH    OVRCD,PROC2190           * No more sites - start pass 2
.
PROC2120  MATCH     VSTRSITE,OSTSITE         * If site read is before Starting
          GOTO      PROC2190 IF LESS         * Site then no more records
          MATCH     OSTSITE,VENDSITE         * If site read is past Ending Site
          GOTO      PROC2190 IF LESS         * then finished as well
.
          CLEAR     OUTTM2
.         APPEND    OSTFILE,OUTTM2
          APPEND    TEMPTM2,OUTTM2
          RESET     OUTTM2
.
          DISPLAY   *P10:20,*EL,*V2LON,OUTTM2;
.
          CLOSE     OUTTM2XX
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    OUTTM2,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    OUTTM2,CMDLINE
          APPEND    " 48 u1-24",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      OUTTM2XX,OUTTM2
.
.------- create tempfile for clinic sessions open file for stats ----
.
          CLEAR     OUTTM3
.         APPEND    OSTFILE,OUTTM3
          APPEND    TEMPTM3,OUTTM3
          RESET     OUTTM3
.
          DISPLAY   *P10:20,*EL,*V2LON,OUTTM3;
.
          CLOSE     OUTTM3XX
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    OUTTM3,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    OUTTM3,CMDLINE
          APPEND    " 93 u1-25",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      OUTTM3XX,OUTTM3
          GOTO      PROC2110                 * Get next site
.
PROC2190  RETURN                             * Finished Pass 1
+
.------------------------------------------------------------------------------
.         PASS 2 - Collate data into temp files
.------------------------------------------------------------------------------
PROC2200  DISPLAY   *P1:18,*EF,*V2LON,"*** Processing Attendance ":
                                      "File ***",*HOFF," - Pass 2":
                    *P1:20,"Site        :":
                    *P1:21,"Clinic ID   :":
                    *P1:22,"Clinic Type :":
                    *P40:20,"Ref. Source :":
                    *P40:21,"Date        :";
.
          PACK      KEY6,VSTRSITE            * Starting Site
          CALL      RDSITA1                  * Positioning read
          COMPARE   ZERO,OVRCD               * Got record ?
          GOTO      PROC2220 IF EQUAL        * Yes - skip readks
.
PROC2210  CALL      RDKSITA1                 * Read next site record
          BRANCH    OVRCD,PROC2399           * No more sites
.
PROC2220  MATCH     VSTRSITE,OSTSITE         * If site read is before Starting
          GOTO      PROC2399 IF LESS         * Site then no more records - exit
          MATCH     OSTSITE,VENDSITE         * If site read is past Ending Site
          GOTO      PROC2399 IF LESS         * then exit as well
.
          CLOSE     OUTTM2XX                 * Close files in case
          CLOSE     OUTTM3XX                 * Close files in case
          CLOSE     OUTCTYA1                 * previously opened.
          CLOSE     OUTBOKA2
          CLOSE     OUTBB1A1
.
          CLEAR     CFNAME
.         APPEND    OSTFILE,CFNAME
          APPEND    TEMPTM2,CFNAME
          RESET     CFNAME
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          OPEN      OUTTM2XX,CFNAME          * Open attendance stats file
.
.--------- open clinic sessions file ----------
.
          CLEAR     CFNAME
.         APPEND    OSTFILE,CFNAME
          APPEND    TEMPTM3,CFNAME
          RESET     CFNAME
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          OPEN      OUTTM3XX,CFNAME          * Open Clinic sessions file
.
          PACK      CFNAME,OSTFILE,FILCTYA1
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          OPEN      OUTCTYA1,CFNAME          * Open clinic type file
.
          PACK      CFNAME,OSTFILE,FILBOKA2
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          OPEN      OUTBOKA2,CFNAME          * Open bookings file
.
          PACK      CFNAME,OSTFILE,FILBB1A1
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          CALL       OPOTBB11
.
          PACK      CFNAME,OSTFILE,FILUSEA1
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          OPEN      OUTUSEA1,CFNAME          * Open used sessions file
.
          PACK      KEY12,OSTSITE,SP6        * All records for current site
          CALL      RDSCTYA1                 * Positioning read
.
PROC2230  CALL      RDKCTYA1                 * Read clinic type record
          BRANCH    OVRCD,PROC2210           * No more records - get next site
.
          MATCH     OSTSITE,OCTSITE          * Same site still ?
          GOTO      PROC2210 IF NOT EQUAL    * No - get next site
.
          MOVE      FOUR,OBASTAT             * Start with attendees
          GOTO      PROC2235                 * Start loop with this status
.
.         Get the next status (will always be status 5 if the current status
.         is 4, otherwise skip to the next clinic type)
.
PROC2233  ADD       ONE,OBASTAT              * Get the next status
          BRANCH    OBASTAT,PROC2230,PROC2230,PROC2230,PROC2235,PROC2235
          GOTO      PROC2230                 * No more attd/nonattd - get next
.
.         Start processing the current status
.
PROC2235  PACK      KEY35,OCTSITE,OCTCTYP,OBASTAT,VFRDATE,SP30
          CALL      RDSBOKA2
.
PROC2240  MOVE      ZERO,EXIT                * Clear exit flag for every rec.
          CALL      RDKBOKA2
          BRANCH    OVRCD,PROC2230           * No more records - get next ctype
.
          MATCH     OCTSITE,OBASITE
          GOTO      PROC2230 IF NOT EQUAL    * Different site - get next ctype
.
          MATCH     OCTCTYP,OBACTYP
          GOTO      PROC2230 IF NOT EQUAL    * Different clinic type - get next
.
          BRANCH    OBASTAT,PROC2230,PROC2230,PROC2230,PROC2250,PROC2250
          GOTO      PROC2230                 * No more attd/nonattd - get next
.
PROC2250  MATCH     VFRDATE,OBADATE          * Booking before from date ?
          GOTO      PROC2235 IF LESS         * Yes. Skip forward to from date
.
          MATCH     OBADATE,VTODATE          * Booking after to date ?
          GOTO      PROC2233 IF LESS         * Yes. Skip to next status
.
.-------- clinic sessions file ----
.
          COMPARE   ZERO,OBAEXSL             * clinic sessions DOES NOT include
          GOTO      PROC2255 IF NOT EQUAL    * extended slots as data....
.
          CALL      VTYP0000                 * process clinic session stats
.
PROC2255  MOVE      SP3,OBASRCR              * Clear variable to be read
          PACK      KEY8,OBAOUTNO            * For this U/R ...
          CALL      RDBOKB1                  * get source of referral
          BRANCH    OVRCD,PROC2240           * No source of ref - ignore record
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P15:20,*V2LON,OBASITE:
                    *P15:21,OBACLIN:
                    *P15:22,OBACTYP:
                    *P54:20,OBASRCR:
                    *P54:21,CPCDATE:
                    *P72:24,OBAOUTNO;
.
.         Update record
.
          MOVE      OCTSITE,OATSITE          * Site
          MOVE      OCTGRP,OATGRUP           * Group code
          MOVE      OCTCTYP,OATCTYP          * Clinic type
          MOVE      OBASRCR,OATREFR          * Source of referral
          MOVE      VCURDATE,OATDATE         * Month of statistics
.
          MOVE      ZERO,OATNREF             * clear attendee
          MOVE      ZERO,OATNNON             * clear non-attendee
          MOVE      ZERO,OATNBKD             * clear non-booked att.
          PACK      KEY24,OATSITE,OATGRUP,OATCTYP,OATREFR,OATDATE
          CALL      RDTM2XX
.
          COMPARE   FIVE,OBASTAT             * Non-Attendee ?
          GOTO      PROC2265 IF NOT EQUAL    * no
          ADD       ONE,OATNNON              * yes - One more Non-Attendee
          GOTO      PROC2270                 * Update record
.
PROC2265  MATCH     SP1,OBBBOOK              * Booked Attendee ?
          GOTO      PROC2240 IF NOT EQUAL    * no - ignore
          ADD       ONE,OATNREF              * yes - One more Attendee
.
.         update or write
.
PROC2270  IF        OVRCD = ONE 
            CALL      WRTM2XX                  * Write with current figures
          ELSE
            CALL      UPTM2XX                  * Update with current figures
          ENDIF
.
PROC2310  MOVE      ZERO,FIRSTREC            * Found a record
          GOTO      PROC2240                 * Get next record
.
PROC2399  CLOSE     OUTTM2XX                 * Close temp attendance file
          CLOSE     OUTTM3XX                 * Close temp open sessions file
          CLOSE     OUTCTYA1                 * Close clinic type file
          CLOSE     OUTBOKA2                 * Close bookings file
          CLOSE     OUTBB1A1                 * Close bookings extension file
          RETURN                             * Finished Pass 2
+
.------------------------------------------------------------------------------
.         PASS 3 - Process non-booked attendees
.------------------------------------------------------------------------------
PROC2400  DISPLAY   *P1:18,*EF,*V2LON,"*** Processing Attendance ":
                    "File ***",*HOFF," - Pass 3",*P1:20,"Site        :":
                    *P1:21,"Clinic ID   :",*P1:22,"Clinic Type :":
                    *P40:20,"Ref. Source :",*P40:21,"Date        :";
.
          OPEN      PATVISA1,"patvisaf"      * Use visit file
.
          PACK      KEY6,VSTRSITE            * Starting Site
          CALL      RDSITA1                  * Positioning read
          COMPARE   ZERO,OVRCD               * Got record ?
          GOTO      PROC2420 IF EQUAL        * Yes - skip readks
.
PROC2410  CALL      RDKSITA1                 * Read next site record
          BRANCH    OVRCD,PROC2599           * No more sites
.
PROC2420  MATCH     VSTRSITE,OSTSITE         * If site read is before Starting
          GOTO      PROC2599 IF LESS         * Site then no more records - exit
          MATCH     OSTSITE,VENDSITE         * If site read is past Ending Site
          GOTO      PROC2599 IF LESS         * then exit as well
.
          CLOSE     OUTTM2XX                 * Close files in case
          CLOSE     OUTBB1A2                 * previously opened.
          CLOSE     OUTCTYA1
.
          CLEAR     CFNAME
.         APPEND    OSTFILE,CFNAME
          APPEND    TEMPTM2,CFNAME
          RESET     CFNAME
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          OPEN      OUTTM2XX,CFNAME          * Open attendance stats file
.
          PACK      CFNAME,OSTFILE,FILCTYA1
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          OPEN      OUTCTYA1,CFNAME          * Open clinic type file
.
          PACK      CFNAME,OSTFILE,FILBB1A1
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          CALL       OPOTBB12
.
          PACK      KEY16,VFRDATE,SP20       * All records for current month
          CALL      RDSBOKB2                 * Positioning read
.
PROC2430  CALL      RDKBOKB2                 * Read bookings extension record
          BRANCH    OVRCD,PROC2410           * No more records - get next site
.
          MATCH     OBADATE,VTODATE          * Still in the same period ?
          GOTO      PROC2410 IF LESS         * No - get next site
.
          DISPLAY   *P72:24,*V2LON,OBAOUTNO;
.
          MATCH     SP1,OBBBOOK              * Booked Attendee ?
          GOTO      PROC2430 IF EQUAL        * yes - ignore record
.
          PACK      KEY8,OBAOUTNO            * Outpatient No. is the key
          CALL      RDVISA1                  * for access to the visits file
          BRANCH    OVRCD,PROC2430           * No visit found - ignore record
.
          MATCH     PVISITE,OSTSITE          * Double check - same site ?
          GOTO      PROC2430 IF NOT EQUAL    * No - ignore record
          MATCH     PVIDATE,OBADATE          * Double check - same date ?
          GOTO      PROC2430 IF NOT EQUAL    * No - ignore record
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P15:20,*V2LON,PVISITE:
                    *P15:21,PVIDOCT:
                    *P15:22,OBBCTYP:
                    *P54:20,OBASRCR:
                    *P54:21,CPCDATE;
.
          UNPACK    SP30,OCTGRP,OCTCTYP
          PACK      KEY12,PVISITE,OBBCTYP
          CALL      RDCTYA1
.
.         Update record
.
          MOVE      PVISITE,OATSITE          * Site
          MOVE      OCTGRP,OATGRUP           * Group code
          MOVE      OCTCTYP,OATCTYP          * Clinic type
          MOVE      OBASRCR,OATREFR          * Source of referral
          MOVE      VCURDATE,OATDATE         * Month of statistics
.
          PACK      KEY24,OATSITE,OATGRUP,OATCTYP,OATREFR,OATDATE
          CALL      RDTM2XX
          BRANCH    OVRCD,PROC2480           * Record doesn't exist - write
.
          ADD       ONE,OATNBKD              * One more Non-booked Attendee
          CALL      UPTM2XX                  * Update with current figures
          GOTO      PROC2510                 *
.
PROC2480  MOVE      ONE,OATNBKD              * One Non-booked Attendee
          MOVE      ZERO,OATNREF             * Clear Attendees field
          MOVE      ZERO,OATNNON             * Clear Non-attendees field
          CALL      WRTM2XX                  * Write new record
.
PROC2510  MOVE      ZERO,FIRSTREC            * Found a record
          GOTO      PROC2430                 * Get next record
.
PROC2599  CLOSE     OUTTM2XX                 * Close temp attendance file
          CLOSE     OUTBB1A1                 * Close bookings extension file
          CLOSE     OUTCTYA1                 * Close clinic type file
          CLOSE     PATVISA1                 * Close visits file
          RETURN                             * Finished Pass 3
+
.------------------------------------------------------------------------------
.         Pass 4 - Update figures from temp file to attendance file
.------------------------------------------------------------------------------
PROC2800  DISPLAY   *P1:18,*EF,*V2LON,"*** Processing Attendance ":
                                      "File ***",*HOFF," - Pass 4":
                    *P1:20,"Site        :":
                    *P1:21,"Group Code  :":
                    *P1:22,"Clinic Type :":
                    *P40:20,"Ref. Source :":
                    *P40:21,"Date        :";
          PACK      KEY6,VSTRSITE            * Starting Site
          CALL      RDSITA1                  * Positioning read
          COMPARE   ZERO,OVRCD               * Got record ?
          GOTO      PROC2820 IF EQUAL        * Yes - skip readks
.
PROC2810  CALL      RDKSITA1                 * Read next site record
          BRANCH    OVRCD,PROC2899           * No more sites
.
PROC2820  MATCH     VSTRSITE,OSTSITE         * If site read is before Starting
          GOTO      PROC2899 IF LESS         * Site then no more records - exit
          MATCH     OSTSITE,VENDSITE         * If site read is past Ending Site
          GOTO      PROC2899 IF LESS         * then exit as well
.
          CLOSE     OUTTM2XX                 * Close files in case
.
          CLEAR     CFNAME
.         APPEND    OSTFILE,CFNAME
          APPEND    TEMPTM2,CFNAME
          RESET     CFNAME
          OPEN      OUTTM2XX,CFNAME          * Open temp attendance file
.
          PACK      CFNAME,OSTFILE,FILATTA1
          OPEN      OUTATTA1,CFNAME          * Open attendance file
.
          CALL      CLR2000                  * Clear attendance data for site
          PACK      KEY24,SP30
          CALL      RDSTM2XX
.
PROC2830  CALL      RDKTM2XX
          BRANCH    OVRCD,PROC2810           * No more records - read next site
.
          PACK      KEY24,OATSITE,OATGRUP,OATCTYP,OATREFR,OATDATE
          CALL      RDAATTA1
.
          UNPACK    OATDATE,CCENT,CYEAR,CMON
          DISPLAY   *P15:20,*V2LON,OATSITE,*P15:21,OATGRUP,*P15:22,OATCTYP:
                    *P54:20,OATREFR,*P54:21,*EL,CMON,SLASH,CCENT,CYEAR;
.
          BRANCH    OVRCD,PROC2840
          CALL      UPATTA1                  * Update figures from OUTTM2XX
          GOTO      PROC2830
.
PROC2840  MOVE      ZERO,OTATDSCH            * No discharges yet
          CALL      WRATTA1                  * Write new figures from OUTTM2XX
          GOTO      PROC2830
.
PROC2899  CLOSE     OUTTM2XX                 * Close temp attendance file
          RETURN
+
.------------------------------------------------------------------------------
.         PASS 5 - Collate bookings data into tempfile - Clinic Sessions
.                - N.B. - this is only for bookings
.------------------------------------------------------------------------------
PROC2900  DISPLAY   *P1:18,*EF,*V2LON,"*** Processing Clinic Sessions ":  
                                      "File ***",*HOFF," - Pass 5":
                    *P1:20,"Site        :":
                    *P1:21,"Clinic ID   :":
                    *P1:22,"Date        :":
                    *P40:20,"Visit Type  :";
.
          PACK      KEY6,VSTRSITE            * Starting Site
          CALL      RDSITA1                  * Positioning read
          COMPARE   ZERO,OVRCD               * Got record ?
          GOTO      PROC2920 IF EQUAL        * Yes - skip readks
.
PROC2910  CALL      RDKSITA1                 * Read next site record
          BRANCH    OVRCD,PROC2999           * No more sites
.
PROC2920  MATCH     VSTRSITE,OSTSITE         * If site read is before Starting
          GOTO      PROC2999 IF LESS         * Site then no more records - exit
          MATCH     OSTSITE,VENDSITE         * If site read is past Ending Site
          GOTO      PROC2999 IF LESS         * then exit as well
.
          CLOSE     OUTTM3XX                 * Close files in case
          CLOSE     OUTCTYA1                 * previously opened.
          CLOSE     OUTBOKA2
          CLOSE     OUTBB1A1
.
.-------- open relevant files -----
.
          CLEAR     CFNAME
.         APPEND    OSTFILE,CFNAME
          APPEND    TEMPTM3,CFNAME
          RESET     CFNAME
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          OPEN      OUTTM3XX,CFNAME          * Open clinic session stats file
.
          PACK      CFNAME,OSTFILE,FILCTYA1
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          OPEN      OUTCTYA1,CFNAME          * Open clinic type file
.
          PACK      CFNAME,OSTFILE,FILBOKA2
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          OPEN      OUTBOKA2,CFNAME          * Open bookings file
.
          PACK      CFNAME,OSTFILE,FILBB1A1
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          CALL       OPOTBB11
.
.--------- read clinic type file -------
.
          PACK      KEY12,OSTSITE,SP6        * All records for current site
          CALL      RDSCTYA1                 * Positioning read
.
PROC2930  CALL      RDKCTYA1                 * Read clinic type record
          BRANCH    OVRCD,PROC2910           * No more records - get next site
.
          MATCH     OSTSITE,OCTSITE          * Same site still ?
          GOTO      PROC2910 IF NOT EQUAL    * No - get next site
.
.-------- get only reocrds with slot status of 1 = Booked -------
.
          PACK      KEY35,OCTSITE,OCTCTYP,ONE,VFRDATE,SP30
          CALL      RDSBOKA2
.
PROC2940  MOVE      ZERO,EXIT                * Clear exit flag for every rec.
          CALL      RDKBOKA2
          BRANCH    OVRCD,PROC2930           * No more records - get next ctype
.
          MATCH     OCTSITE,OBASITE
          GOTO      PROC2930 IF NOT EQUAL    * Different site - get next ctype
.
          MATCH     OCTCTYP,OBACTYP
          GOTO      PROC2930 IF NOT EQUAL    * Different clinic type - get next
.
          BRANCH    OBASTAT,PROC2950,PROC2930,PROC2930,PROC2930,PROC2930
          GOTO      PROC2930                 * No more Booked - get next
.
PROC2950  MATCH     VFRDATE,OBADATE          * Booking before fromdate ?
          GOTO      PROC2940 IF LESS         * Yes, read next rec.
.
          MATCH     OBADATE,VTODATE          * Booking after todate ?
          GOTO      PROC2940 IF LESS         * yes, read next rec.
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          DISPLAY   *P15:20,*V2LON,OBASITE:
                    *P15:21,OBACLIN:
                    *P15:22,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                    *P54:20,OBAVISIT;
.
.------- get visit type totals, total bookings  and write to tempfile -----
.
          MOVE      ZERO,OSESLOTB             * clear total variables
          MOVE      ZERO,TMPNEW               * clear temp. variables
          MOVE      ZERO,TMPREV
          MOVE      ZERO,TMPSPC
.
.-------- read patient codes file -------
.
          PACK      KEY5,CODECV,OBAVISIT
          CALL      RDCODE1
          MATCH     VTYPEN,TCINDC1             * "N" = New visit
          GOTO      PROC2960 IF EQUAL
          MATCH     VTYPER,TCINDC1             * "R" = Re-visit
          GOTO      PROC2970 IF EQUAL
          ADD       ONE,TMPSPC                 * default to "S" = special visit
          GOTO      PROC2980
.
.-------- increment subtotals for bookings -----
.
PROC2960  ADD       ONE,TMPNEW                    * add 1 to total for new
          GOTO      PROC2980
PROC2970  ADD       ONE,TMPREV                    * add 1 to revisit total
.
.-------- move variables to file variables for temp file --------
.
PROC2980  MOVE      OBASITE,OSESITE                * site 	
          MOVE      OBACLIN,OSECLIN                * clinic
          MOVE      OBADATE,OSEDATE                * date
          MOVE      OBASTRT,OSESTRT                * start time
          MOVE      OBADAY,DOSEDAY                 * day indicator
.
.------- clear tempfile variables ------
.
          MOVE      ZERO,OSESLOTB 
          MOVE      ZERO,OSEBNEW
          MOVE      ZERO,OSEBRV 
          MOVE      ZERO,OSEBSPC
          MOVE      ZERO,OSEANEW
          MOVE      ZERO,OSEARV  
          MOVE      ZERO,OSEASPC 
.
.-------- call actual hours routine to calculate actual hours used ------
.
          CALL      AHRS0000                       * get actual hours
.
.-------- read tempfile to see if record already exists ------
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          CALL      RDTM3XX                        * full read
          BRANCH    OVRCD,PROC2990                 * not on file, so write
.
.-------- record exists therefore update -----
.
          ADD       ONE,OSESLOTB                   * add one to slot total
          ADD       TMPNEW,OSEBNEW                 * add temp counter to var.
          ADD       TMPREV,OSEBRV                  * add temp counter to var.
          ADD       TMPSPC,OSEBSPC                 * add temp counter to var.
          CALL      UPTM3XX                        * update tempfile
          GOTO      PROC2995 
.
.-------- record not on file therefore write new one ----
.
PROC2990  MOVE      ONE,OSESLOTB                   * slot total equals 1.
          MOVE      TMPNEW,OSEBNEW                 * move temp counter to var.
          MOVE      TMPREV,OSEBRV                  * move temp counter to var.
          MOVE      TMPSPC,OSEBSPC                 * move temp counter to var.
          CALL      WRTM3XX                 * write to clinic session stats file
.
PROC2995  MOVE      ZERO,FIRSTREC           * Found a record, set flag
          GOTO      PROC2940                * Get next record
.
.-------- close appropriate files --------
.
PROC2999  CLOSE     OUTTM3XX                * Close temp clinic session file
          CLOSE     OUTCTYA1                * Close clinic type file
          CLOSE     OUTBOKA2                * Close bookings file
          CLOSE     OUTBB1A1                * Close bookings extension file
          RETURN
+
.------------------------------------------------------------------------------
.         Temp file I/O - same as OUTIOATA.INC
.------------------------------------------------------------------------------
RDSTM2XX  RESET     KEY24
          READ      OUTTM2XX,KEY24;;
          RETURN  
.
RDTM2XX   MOVE      ZERO,OVRCD
          RESET     KEY24
          READ      OUTTM2XX,KEY24;OATSITE,OATGRUP,OATCTYP,OATREFR,OATDATE:
                                   OATNREF,OATNNON,OATNBKD,OATSPAR
          GOTO      OVERCOND IF OVER
          RETURN  
.
RDKTM2XX  MOVE      ZERO,OVRCD
          READKS    OUTTM2XX;OATSITE,OATGRUP,OATCTYP,OATREFR,OATDATE:
                             OATNREF,OATNNON,OATNBKD,OATSPAR
          GOTO      OVERCOND IF OVER
          RETURN  
.
WRTM2XX   RESET     KEY24
          WRITE     OUTTM2XX,KEY24;KEY24,OATNREF,OATNNON,OATNBKD,OATSPAR
          RETURN  
.
UPTM2XX   UPDATE    OUTTM2XX;OATSITE,OATGRUP,OATCTYP,OATREFR,OATDATE:
                             OATNREF,OATNNON,OATNBKD,OATSPAR
          RETURN  
+
.------------------------------------------------------------------------------
.         Temp file I/O - same as OUTIOSEA.INC
.------------------------------------------------------------------------------
RDATM3XX  MOVE      ZERO,OVRCD
          RESET     KEY25
          READ      OUTTM3XX,KEY25;ANS
          GOTO      OVERCOND IF OVER
          RETURN  
.
RDSTM3XX  RESET     KEY25
          READ      OUTTM3XX,KEY25;;
          RETURN  
.
RDTM3XX   MOVE      ZERO,OVRCD
          RESET     KEY25
          READ      OUTTM3XX,KEY25;OSESITE,OSECLIN,OSEDATE,OSESTRT,DOSEDAY:
                                   OSESLOTB,OSETIMEU,OSEBNEW,OSEBRV:
                                   OSEBSPC,OSEANEW,OSEARV,OSEASPC,OSESPARE
          GOTO      OVERCOND IF OVER
          RETURN  
.
RDKTM3XX  MOVE      ZERO,OVRCD
          READKS    OUTTM3XX;OSESITE,OSECLIN,OSEDATE,OSESTRT,DOSEDAY:
                             OSESLOTB,OSETIMEU,OSEBNEW,OSEBRV:
                             OSEBSPC,OSEANEW,OSEARV,OSEASPC,OSESPARE
          GOTO      OVERCOND IF OVER
          RETURN  
.
WRTM3XX   RESET     KEY25
          WRITE     OUTTM3XX,KEY25;KEY25,DOSEDAY,OSESLOTB,OSETIMEU,OSEBNEW:
                                  OSEBRV,OSEBSPC,OSEANEW,OSEARV,OSEASPC,OSESPARE
          RETURN  
.
UPTM3XX   UPDATE    OUTTM3XX;OSESITE,OSECLIN,OSEDATE,OSESTRT,DOSEDAY:
                             OSESLOTB,OSETIMEU,OSEBNEW,OSEBRV:
                             OSEBSPC,OSEANEW,OSEARV,OSEASPC,OSESPARE
          RETURN  
+
.**********************************************************************
.*                        VTYP0000                                    *
.*        Get the visit type to determine particular totals           *
.*        this for attendees and non-attendees                        *
.**********************************************************************
VTYP0000  MOVE      ZERO,TMPNEW                    * clear temp visit counters
          MOVE      ZERO,TMPREV
          MOVE      ZERO,TMPSPC
.
.------- get visit type totals -----
.
          PACK      KEY5,CODECV,OBAVISIT
          CALL      RDCODE1
          MATCH     VTYPEN,TCINDC1                 * "N" = New visit
          GOTO      VTYP1000 IF EQUAL
          MATCH     VTYPER,TCINDC1                 * "R" = Re-visit
          GOTO      VTYP2000 IF EQUAL
          GOTO      VTYP3000                       * "S" = special visit 
.
.------- increment subtotals for attendances including non-attendees -----
.
VTYP1000  ADD       ONE,TMPNEW                     * add 1 to total for new
          GOTO      VTYP4000
VTYP2000  ADD       ONE,TMPREV                     * add 1 to revisit total
          GOTO      VTYP4000
VTYP3000  ADD       ONE,TMPSPC                     * add 1 to special total
.
.-------- move variables to file variables for temp file --------
.
VTYP4000  MOVE      OBASITE,OSESITE
          MOVE      OBACLIN,OSECLIN
          MOVE      OBADATE,OSEDATE            
          MOVE      OBASTRT,OSESTRT
          MOVE      OBADAY,DOSEDAY
.
.-------- clear tempfile total variables ----- 
.
          MOVE      ZERO,OSESLOTB
          MOVE      ZERO,OSEBNEW
          MOVE      ZERO,OSEBRV 
          MOVE      ZERO,OSEBSPC
          MOVE      ZERO,OSEANEW
          MOVE      ZERO,OSEARV  
          MOVE      ZERO,OSEASPC 
.
.-------- read outpatients - used sessions file to get actual hours used ----
.
          CALL      AHRS0000                    * get actual hours used
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          CALL      RDTM3XX                     * full read
          BRANCH    OVRCD,VTYP5000
.
.-------- record exists therefore update -----
.
          ADD       TMPNEW,OSEBNEW              * add temp counters to vars.
          ADD       TMPREV,OSEBRV 
          ADD       TMPSPC,OSEBSPC
.
.-------- check visit status, if not attended then must be non-attended -----
.-------- therefore slot counter is incremented but not attendee totals -----
.
          COMPARE   FOUR,OBASTAT                * if visit NOT = "4" Attended
          GOTO      VTYP4800 IF NOT EQUAL       * then don't add attended total.
          ADD       TMPNEW,OSEANEW              * add temp counters to vars.
          ADD       TMPREV,OSEARV 
          ADD       TMPSPC,OSEASPC
.
VTYP4800  ADD       ONE,OSESLOTB                * increment slots booked
          CALL      UPTM3XX                     * update temp file
          GOTO      VTYP9999 
.
.-------- record not on file therefore write new one ----
.-------- check visit status, if not attended then must be non-attended -----
.-------- therefore slot counter is incremented but not attendee totals -----
.
VTYP5000  COMPARE   FOUR,OBASTAT                * if visit NOT = "4" Attended
          GOTO      VTYP6000 IF NOT EQUAL       * then don't add attended total.
.
          MOVE      TMPNEW,OSEANEW              * move temp counters to vars.
          MOVE      TMPREV,OSEARV 
          MOVE      TMPSPC,OSEASPC
.
.------- increment slot totals ------
.
VTYP6000  MOVE      TMPNEW,OSEBNEW              * add temp counters to vars.
          MOVE      TMPREV,OSEBRV 
          MOVE      TMPSPC,OSEBSPC
.
          ADD       ONE,OSESLOTB                * increment slots booked
          CALL      WRTM3XX                 * write to clinic session stats file
.
VTYP9999  RETURN
+
.**************************************************************************
.*                             AHRS0000                                   *
.*              Calculate difference b/w actual hours                     *
.*         N.B. Result is to get number of hours actually used.           *
.**************************************************************************
AHRS0000  MOVE      SP5,OSETIMEU  
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          CALL      RDUSEA1                       * read used sessions file
          BRANCH    OVRCD,AHRS9999
.
.-------- unpack variables ------
.
          UNPACK    OUSACST,DIM2A1,ANS,DIM2A2       * HH:MM 
          UNPACK    OUSACEND,DIM2B1,ANS,DIM2B2      * HH:MM
          MOVE      DIM2A2,FORM2A2                  * MM to form
          MOVE      DIM2A1,FORM2A1                  * HH to form
          MOVE      DIM2B1,FORM2B1                  * HH to form
          MOVE      DIM2B2,TEMPF3                   * MM to form
          SUB       FORM2A2,TEMPF3              * subtract start MM from end MM
.
          COMPARE   ZERO,TEMPF3
          GOTO      AHRS2000 IF LESS            * IF less or = to zero cont.
.
AHRS1000  MOVE      FORM2B1,TEMPF2              * move end hour to working var.
          SUB       FORM2A1,TEMPF2              * sub. start hour from end hour.
          GOTO      AHRS3000
.
AHRS2000  ADD       "60",TEMPF3                 * if less or = to zero
          SUB       ONE,FORM2B1                 * sub. 1 from end hour 
          GOTO      AHRS1000
.
AHRS3000  MOVE      TEMPF2,DIM2B1               * move result hour to dim
          MOVE      TEMPF3,FORM2B2          
          MOVE      FORM2B2,DIM2B2              * move result minutes to dim
.
          PACK      OSETIMEU,DIM2B1,COLON,DIM2B2    * pack time
          REPLACE   " 0",OSETIMEU                   * replace spaces with zero
AHRS9999  RETURN
+
.**************************************************************************
.*                              PROC3000                                  *
.*                       Process Location File                            *
.*               Status : Not tested with extensive data                  *
.**************************************************************************
PROC3000  MOVE      ONE,FIRSTREC             * 1st record not read yet
          MOVE      SP3,SAVCATCH             * Initialise catchment code
          MOVE      ZERO,FORM7A              * Clear # of attendees field
          DISPLAY   *P1:18,*EF:
                    *V2LON,"*** Processing Location File ***",*HOFF:
                    *P1:20,"Site        :":
                    *P1:21,"Clinic ID   :":
                    *P1:22,"Clinic Type :":
                    *P1:23,"Catchment   :":
                    *P1:24,"Date        :";
.
          OPEN      PATCT1C1,"patct1cf"                * Open catchment file
          OPEN      PATMA1A1,"patma1af"      * Open PMI file
          OPEN      PATMX1A1,"patmx1af"
.
          PACK      KEY6,VSTRSITE            * Starting Site
          CALL      RDSITA1                  * Positioning read
          COMPARE   ZERO,OVRCD               * Got record ?
          GOTO      PROC3110 IF EQUAL        * Yes - skip readks
.
PROC3100  CALL      RDKSITA1                 * Read next site record
          BRANCH    OVRCD,PROC3999           * No more sites
.
PROC3110  MATCH     VSTRSITE,OSTSITE         * If site read is before Starting
          GOTO      PROC3999 IF LESS         * Site then no more records - exit
          MATCH     OSTSITE,VENDSITE         * If site read is past Ending Site
          GOTO      PROC3999 IF LESS         * then exit as well
.
          CLOSE     OUTLOCA1                 * Close files in case
          CLOSE     OUTCTYA1                 * previously opened.
          CLOSE     OUTBOKA2
.
          PACK      CFNAME,OSTFILE,FILLOCA1
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          OPEN      OUTLOCA1,CFNAME          * Open location stats file
.
          PACK      CFNAME,OSTFILE,FILCTYA1
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          OPEN      OUTCTYA1,CFNAME          * Open clinic type file
.
          PACK      CFNAME,OSTFILE,FILBOKA2
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          OPEN      OUTBOKA2,CFNAME          * Open bookings file
.
          CALL      CLR3000                  * Clear location data for site
.
          PACK      KEY12,OSTSITE,SP6        * All records for current site
          CALL      RDSCTYA1                 * Positioning read
.
PROC3200  CALL      RDKCTYA1                 * Read clinic type record
          BRANCH    OVRCD,PROC3100           * No more records - get next site
.
          MATCH     OSTSITE,OCTSITE          * Same site still ?
          GOTO      PROC3100 IF NOT EQUAL    * No - get next site
.
.         Attendees only
.
PROC3250  PACK      KEY35,OCTSITE,OCTCTYP,FOUR,VFRDATE,SP30
          CALL      RDSBOKA2                 * Positioning read
.
PROC3300  MOVE      ZERO,EXIT                * Clear exit flag for every rec.
          CALL      RDKBOKA2
          BRANCH    OVRCD,PROC3800           * No more records - update
.
          MATCH     OCTSITE,OBASITE
          GOTO      PROC3800 IF NOT EQUAL    * Different site - update
.
          MATCH     OCTCTYP,OBACTYP
          GOTO      PROC3800 IF NOT EQUAL    * Different clinic type - update
.
          COMPARE   FOUR,OBASTAT
          GOTO      PROC3800 IF NOT EQUAL    * No more attds - update
.
          MATCH     OBADATE,VTODATE          * Still in the same period ?
          GOTO      PROC3800 IF LESS         * No - update
.
          MOVE      OBAURNO,PURNO            * Use U/R Number
          MOVE      PURNO,KEY8               * Use U/R Number
          CALL      RDMAST1                  * to get postcode
          BRANCH    OVRCD,PROC3300           * No U/R record - ignore record
.
          MOVE      SP8,CTCCODE              * Initialize before read
          PACK      KEY8,PPOST               * Use postcode to
.
          CALL      RDCATC1                  * get catchment code
.         BRANCH    OVRCD,PROC3300           * No catchment code - ignore record
.
          COMPARE   ZERO,FORM7A              * 1st time for this clinic type ?
          GOTO      PROC3390 IF EQUAL        * Yes.
          MATCH     SAVCATCH,CTCCODE         * Same catchment code ?
          GOTO      PROC3500 IF NOT EQUAL    * No - update record
.
PROC3390  MOVE      CTCCODE,SAVCATCH
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P15:20,*V2LON,OBASITE:
                    *P15:21,OBACLIN:
                    *P15:22,OBACTYP:
                    *P15:23,CTCCODE:
                    *P15:24,CPCDATE:
                    *P72:24,OBAOUTNO;
.
          ADD       ONE,FORM7A               * One more attendee
          GOTO      PROC3300                 * Get another record
.
.         Update record
.
PROC3500  MOVE      OCTSITE,LOASITE          * Site
          MOVE      OCTGRP,LOAGRUP           * Group code
          MOVE      OCTCTYP,LOATYPE          * Clinic type
          MOVE      SAVCATCH,LOACACH         * Catchment code
          MOVE      VCURDATE,LOADATE         * Month of statistics
          MOVE      FORM7A,LOANUMB           * # of Attendees
          PACK      KEY24,LOASITE,LOAGRUP,LOATYPE,LOACACH,LOADATE
          CALL      RDOTLOC1
          BRANCH    OVRCD,PROC3600           * Record doesn't exist - write
.
          ADD       FORM7A,LOANUMB           * # of Attendees
          CALL      UPOTLOC1                 * Update with current figures
          GOTO      PROC3700                 *
.
PROC3600  CALL      WROTLOC1                 * Write new record
.
PROC3700  MOVE      ZERO,FIRSTREC            * Found a record
          MOVE      ZERO,FORM7A              * Clear # of attendees field
          MOVE      SP3,SAVCATCH             * Clear referral source field
          BRANCH    EXIT,PROC3200            * Get next clinic type
          GOTO      PROC3390                 * Add up for this record
.
PROC3800  MOVE      ONE,EXIT                 * After update, get next type
          COMPARE   ZERO,FORM7A              * Have we found anything
          GOTO      PROC3500 IF NOT EQUAL    * Yes. Process last code
          GOTO      PROC3200                 * No. Get next clinic type
.
PROC3999  CLOSE     PATCT1C1                * Close catchment file
          CLOSE     PATMA1A1                 * Close PMI file
          CLOSE     PATMX1A1

          CLOSE     OUTLOCA1                 * Close location file
          CLOSE     OUTCTYA1                 * Close clinic type file
          CLOSE     OUTBOKA2                 * Close bookings file
          RETURN
+
.**************************************************************************
.*                              PROC6000                                  *
.*                  Process Occasions of Service File                     *
.*              Status : Not tested with extensive data                   *
.**************************************************************************
PROC6000  MOVE      ONE,FIRSTREC             * 1st record not read yet
          DISPLAY   *P1:18,*EF:
                    *V2LON,"*** Processing Occasions of Service File ***",*HOFF:
                    *P1:20,"Site        :",*P1:21,"Clinic ID   :":
                    *P1:22,"Clinic Type :",*P1:23,"Date        :";
.
          PACK      KEY6,VSTRSITE            * Starting Site
          CALL      RDSITA1                  * Positioning read
          COMPARE   ZERO,OVRCD               * Got record ?
          GOTO      PROC6110 IF EQUAL        * Yes - skip readks
.
PROC6100  CALL      RDKSITA1                 * Read next site record
          BRANCH    OVRCD,PROC6999           * No more sites
.
PROC6110  MATCH     VSTRSITE,OSTSITE         * If site read is before Starting
          GOTO      PROC6999 IF LESS         * Site then no more records - exit
          MATCH     OSTSITE,VENDSITE         * If site read is past Ending Site
          GOTO      PROC6999 IF LESS         * then exit as well
.
          CLOSE     OUTOCCA1                 * Close files in case
          CLOSE     OUTATTA1                 * previously opened.
.
          PACK      CFNAME,OSTFILE,FILATTA1
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          OPEN      OUTATTA1,CFNAME          * Open attendance file
.
          PACK      CFNAME,OSTFILE,FILOCCA1
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          OPEN      OUTOCCA1,CFNAME          * Open occasions of serv file
.
          CALL      CLR6000                  * Clear occ of serv data for site
.
          MOVE      SP3,SAVGROUP
          MOVE      ZERO,EXIT
          PACK      KEY24,OSTSITE,SP6        * All records for current site
          CALL      RDSATTA1                 * Positioning read
.
PROC6200  CALL      RDKATTA1                 * Read attendance record
          BRANCH    OVRCD,PROC6800           * No more records - update & exit
.
          MATCH     OSTSITE,OATSITE          * Same site still ?
          GOTO      PROC6800 IF NOT EQUAL    * No - update & exit
.
          MATCH     VCURDATE,OATDATE         * Same month ?
          GOTO      PROC6200 IF NOT EQUAL    * No. Try next attendance record
.
          MATCH     SP3,SAVGROUP             * 1st time round ?
          GOTO      PROC6300 IF EQUAL        * Yes - collate figures
.
          MATCH     OATGRUP,SAVGROUP         * Same group ?
          GOTO      PROC6500 IF NOT EQUAL    * No - update, save & keep looping
.
          MATCH     OATCTYP,SAVCTYP          * Same clinic type ?
          GOTO      PROC6500 IF NOT EQUAL    * No - update, save & keep looping
.
PROC6300  UNPACK    OATDATE,CCENT,CYEAR,CMON
          DISPLAY   *P15:20,*V2LON,OATSITE,*P15:21,OATGRUP:
                    *P15:22,OATCTYP,*P15:23,CMON,SLASH,CCENT,CYEAR;
          MOVE      OATGRUP,SAVGROUP
          MOVE      OATCTYP,SAVCTYP
          ADD       OATNREF,FORM7A           * Add normal attendees
          ADD       OATNBKD,FORM7A           * Add non-booked attendees
          GOTO      PROC6200                 * Get another record
.
.         Update record
.
PROC6500  MOVE      OSTSITE,OCCSITE          * Site
          MOVE      SAVGROUP,OCCGRUP         * Group code
          MOVE      SAVCTYP,OCCTYPE          * Clinic type
          MOVE      VCURDATE,OCCDATE         * Month of statistics
          MOVE      FORM7A,OCCNUMB           * # of Attendees
.
          MATCH     SP3,SAVGROUP             * Do we have a valid group ?
          GOTO      PROC6700 IF EQUAL        * No. Ignore this data
.
          PACK      KEY21,OCCSITE,OCCGRUP,OCCTYPE,OCCDATE
          CALL      RDOCCA1
          BRANCH    OVRCD,PROC6600           * Record doesn't exist - write
          MOVE      FORM7A,OCCNUMB           * # of Attendees
          CALL      UPOCCA1                  * Update with current figures
          GOTO      PROC6700                 *
.
PROC6600  CALL      WROCCA1                  * Write new record
.
PROC6700  MOVE      ZERO,FIRSTREC            * Found a record
          MOVE      ZERO,FORM7A              * Clear # of attendees field
          BRANCH    EXIT,PROC6100            * No more records - get next site
          GOTO      PROC6300                 * Add up current clinic & reloop
.
PROC6800  MOVE      ONE,EXIT                 * Exit after update
          GOTO      PROC6500
.
PROC6999  CLOSE     OUTOCCA1                 * Close occasions of serv file
          CLOSE     OUTATTA1                 * Close attendance file
          RETURN
+
.**************************************************************************
.*                              PROC7000                                  *
.*                      Process Referral Source File                      *
.*                Status : Not tested with extensive data                 *
.*                         (Why not?? - Get a Clue)                       *
.**************************************************************************
PROC7000  MOVE      ONE,FIRSTREC             * 1st record not read yet
          DISPLAY   *P1:18,*EF:
                    *V2LON,"*** Processing Referral Source File ***",*HOFF:
                    *P1:20,"Site        :",*P1:21,"Clinic ID   :":
                    *P1:22,"Clinic Type :",*P1:23,"Date        :";
.
          PACK      KEY6,VSTRSITE            * Starting Site
          CALL      RDSITA1                  * Positioning read
          COMPARE   ZERO,OVRCD               * Got record ?
          GOTO      PROC7110 IF EQUAL        * Yes - skip readks
.
PROC7100  CALL      RDKSITA1                 * Read next site record
          BRANCH    OVRCD,PROC7999           * No more sites
.
PROC7110  MATCH     VSTRSITE,OSTSITE         * If site read is before Starting
          GOTO      PROC7999 IF LESS         * Site then no more records - exit
          MATCH     OSTSITE,VENDSITE         * If site read is past Ending Site
          GOTO      PROC7999 IF LESS         * then exit as well
.
          CLOSE     OUTSTBA1                 * Close files in case
          CLOSE     OUTATTA1                 * previously opened.
.
          PACK      CFNAME,OSTFILE,FILATTA1
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          OPEN      OUTATTA1,CFNAME          * Open attendance file
.
          PACK      CFNAME,OSTFILE,FILSTBA1
          DISPLAY   *P71:24,*V2LON,"[",CFNAME,"]";
          OPEN      OUTSTBA1,CFNAME          * Open referral source file
.
          CALL      CLR7000                  * Clear ref. source data for site
.
          PACK      KEY24,OSTSITE,SP6        * All records for current site
          CALL      RDSATTA1                 * Positioning read
.
PROC7200  CALL      RDKATTA1                 * Read attendance record
          BRANCH    OVRCD,PROC7500           * No more records - update & exit
.
          MATCH     OSTSITE,OATSITE          * Same site still ?
          GOTO      PROC7500 IF NOT EQUAL    * No - update & exit
.
          MATCH     VCURDATE,OATDATE         * Same month ?
          GOTO      PROC7200 IF NOT EQUAL    * No - ignore record
.
          MATCH     VSOURCE,OATREFR          * Correct referral source ?
          GOTO      PROC7200 IF NOT EQUAL    * No - ignore record
.
          UNPACK    OATDATE,CCENT,CYEAR,CMON
          DISPLAY   *P15:20,*V2LON,OATSITE,*P15:21,OATGRUP:
                    *P15:22,OATCTYP,*P15:23,CMON,SLASH,CCENT,CYEAR;
          ADD       OATNREF,FORM7A           * Add normal attendees
          ADD       OATNBKD,FORM7A           * Add non-booked attendees
          GOTO      PROC7200                 * Get another record
.
.         Update record
.
PROC7500  COMPARE   ZERO,FORM7A              * Anything at all ?
          GOTO      PROC7100 IF EQUAL        * No - don't write record
          MOVE      ZERO,FIRSTREC            * Found a record
.
          MOVE      OSTSITE,OSBSITE          * Site
          MOVE      OSTCATG,OSBCATA            * Category - source of referral
          MOVE      VSOURCE,OSBCODE
          MOVE      VCURDATE,OSBDATE         * Month of statistics
          MOVE      FORM7A,OSBNUMB           * # of Attendees
.
          PACK      KEY17,OSBSITE,OSBCATA,OSBCODE,OSBDATE
          CALL      RDSTBA1
          BRANCH    OVRCD,PROC7600           * Record doesn't exist - write
          MOVE      FORM7A,OSBNUMB           * # of Attendees
          CALL      UPSTBA1                  * Update with current figures
          GOTO      PROC7100                 * Get next site
.
PROC7600  CALL      WRSTBA1                  * Write new record
          GOTO      PROC7100                 * Get next site
.
PROC7999  CLOSE     OUTOCCA1                 * Close occasions of serv file
          CLOSE     OUTATTA1                 * Close attendance file
          RETURN
+
.------------------------------------------------------------------------------
.         Pass 6 - Update figures from temp file to Clinic Sessions Stats file
. Notes: This sub-routine is cactus
.------------------------------------------------------------------------------
PROC8000  DISPLAY   *P1:18,*EF,*V2LON,"*** Processing Clinic Sessions ":
                                      "File ***",*HOFF," - Pass 6":
                    *P1:20,"Site        :":
                    *P1:21,"Clinic Id   :":
                    *P1:22,"Date        :":
                    *P40:20,"Total Slots :";
.
          PACK      KEY6,VSTRSITE            * Starting Site
          CALL      RDSITA1                  * Positioning read
          COMPARE   ZERO,OVRCD               * Got record ?
          GOTO      PROC8820 IF EQUAL        * Yes - skip readks
.
PROC8810  CALL      RDKSITA1                 * Read next site record
          BRANCH    OVRCD,PROC8899           * No more sites
.
PROC8820  MATCH     VSTRSITE,OSTSITE         * If site read is before Starting
          GOTO      PROC8899 IF LESS         * Site then no more records - exit
          MATCH     OSTSITE,VENDSITE         * If site read is past Ending Site
          GOTO      PROC8899 IF LESS         * then exit as well
.
          CLOSE     OUTTM3XX                 * Close files in case
          CLOSE     OUTSESA1                 * previously opened.
.
          CLEAR     CFNAME
.         APPEND    OSTFILE,CFNAME
          APPEND    TEMPTM3,CFNAME
          RESET     CFNAME
          OPEN      OUTTM3XX,CFNAME          * Open temp attendance file
.
          PACK      CFNAME,OSTFILE,FILSESA1
          OPEN      OUTSESA1,CFNAME          * Open attendance file
.
          CALL      CLR4000                  * Clear attendance data for site
.
          PACK      KEY25,SP30
          CALL      RDSTM3XX
PROC8830  CALL      RDKTM3XX
          BRANCH    OVRCD,PROC8810           * No more records - read next site
.
          PACK      KEY25,OSESITE,OSECLIN,OSEDATE,OSESTRT,DOSEDAY
          CALL      RDSESA1
.
          MOVE      DOSEDAY,OSEDAY
          UNPACK    OSEDATE,CCENT,CYEAR,CMON
          DISPLAY   *P15:20,*V2LON,OSESITE,*P15:21,OSECLIN:
                    *P15:22,CMON,SLASH,CCENT,CYEAR:
                    *P54:20,OSESLOTB;
.
          BRANCH    OVRCD,PROC8840           * record not on file
.
          CALL      RDTM3XX                  * full read on tempfile
          CALL      UPSESA1                  * Update figures from OUTTM3XX
          GOTO      PROC8830
.
PROC8840  CALL      WRSESA1                  * Write new figures from OUTTM3XX
          GOTO      PROC8830
.
PROC8899  CLOSE     OUTTM3XX                 * Close temp clinic sessions file
          CLOSE     OUTSESA1                 * Close Clinic Sessions file
          RETURN
+
.------------------------------------------------------------------------------
.       Processing Monthly inpatient statistics
.
IPRO0000  OPEN      PATMSTS1,"patmstsf"
          OPEN      PATCSTS1,"patcstsf"
          OPEN      PATCT1C1,"patct1cf"            
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
.       Check if the selected month has been updated
.
          BRANCH    PTCNNEWS,CONPROC             * using new stats update ?
          PACK      KEY15 WITH SP10,SP5          * no
          CALL      RDSMSTS1
TRYNXT    CALL      RDKMSTS1
          BRANCH    OVRCD OF CONPROC
.
          MATCH     MSDATE,VCURDATE
          GOTO      TRYNXT IF NOT EQUAL
.
.        Initialize the file for the month
.
TRYN20    DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** Initializing the Monthly Statistics File **";
          GOTO      TRYN30
.
TRYN25    CALL      RDKMSTS1
          BRANCH    OVRCD,CLRCSTS
.
          MATCH     MSDATE,VCURDATE
          GOTO      TRYN25 IF NOT EQUAL
.
TRYN30    CALL      INTMST1
          CALL      UPMSTS1
          GOTO      TRYN25
.
.         Initialize the catchment suumary records before we write over it
.
CLRCSTS   BRANCH    PTCNNEWS,CONPROC             * using new stats update ?
          MOVE      SP10,KEY9                    * no
          CALL      RDSCSTS1
RDNXT     CALL      RDKCSTS1
          BRANCH    OVRCD OF CONPROC
.
          MATCH     CCCYYMM,VCURDATE
          GOTO      RDNXT IF NOT EQUAL
.
BLREC     CALL      INTREC
          CALL      UPCSTS1
          GOTO      RDNXT
.
CONPROC   DISPLAY   *P1:24,*EL,*V2LON,*P10:24:
                    "** Processing Monthly Inpatient Statistics **";
.
.       Create tempfile to calculate no of patients
.
          CALL      IDEXINT
          CALL      PROCC            * processing
.
          CALL      CALCPAT          * Calculate the no of patient in the month
          CALL      KILLIDZ          * kill the tempfile
.
FINMST    CLOSE     PATMSTS1
          CLOSE     PATCSTS1
          CLOSE     PATCT1C1
          CLOSE     PATMA1A1
          CLOSE     PATMX1A1
.
FINM10    RETURN
+
.         Initialize the catchment summary record
.
INTREC    MOVE      ZERO,CCADMN
          MOVE      ZERO,CCABDAY
          MOVE      ZERO,CCSAMED
          MOVE      ZERO,CCSMDAY
          MOVE      ZERO,CCBOARD
          MOVE      ZERO,CCBRDAY
          RETURN
+
.------------------------------------------------------------------------------
.       Processing for each month figures
.
PROCC     MOVE      VFRDATE,FROMDATE   * From date for inpatient stats
          UNPACK    VTODATE,XCENT,XYEAR,XMON,XDAY
          MOVE      XDAY,DD
          MOVE      XMON,MM
          MOVE      XYEAR,YY
          MOVE      XCENT,CC
          CALL      DMYCON
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
.
          CALL      JULCON
          PACK      TODATE,CC,YY,MM,DD    * To date for inpatient statistics
          REP       " 0",TODATE
.
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "*** Processing Current Admission ***",*W2,*P1:24,*EL:
                    *HOFF,*P20:24,"Current Admission No :",*P55:24,"Scanning :";
          MOVE      TWO,WSTAT
          CALL      ADMFN
.
          DISPLAY   *P1:24,*EL,*P22:24,*V2LON:
                    "*** Processing On Leave Admission ***",*W2,*P1:24,*EL:
                    *HOFF,*P20:24,"On Leave Admission No :",*P55:24,"Scanning :"
          MOVE      FOUR,WSTAT
          CALL      ADMFN
.
          DISPLAY   *P1:24,*EL,*P22:24,*V2LON,"*** Processing Discharge ":
                    "Admission ***",*W2,*P1:24,*EL,*HOFF:
                    *P20:24,"Discharged Admission No :",*P55:24,"Scanning :";
.
          PACK      KEY16,FROMDATE,SP8
          CALL      RDSDSCH2
NEXT3     CALL      RDKDSCH2
          COMPARE   ONE,OVRCD
          RETURN    IF EQUAL
          DISPLAY   *P72:24,DADMNO;
.
.       Check if they admitted after the selected month
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF NEXT3
.
.       Ignore if the patient admitted after the selected month
.
          MATCH     TODATE,ADATE             * check with the admission date
          GOTO      NEXT3 IF NOT LESS
          DISPLAY   *P47:24,*V2LON,DADMNO;
          CALL      GENPROC
          GOTO      NEXT3
.
.         READ ADMISSION FILE
.
ADMFN     PACK      KEY9 WITH WSTAT,SP6
          MOVE      SP8,DDATE
          CALL      RDSMISS2
NEXT5     CALL      RDKMISS2
          BRANCH    OVRCD OF CONT1A
.
          DISPLAY   *P72:24,AADMNO;
          COMPARE   WSTAT,ASTAT
          GOTO      CONT1 IF NOT EQUAL
.
.       Check if they admitted after the selected month
.
DSPA      MATCH     TODATE,ADATE       * check with the admission date
          GOTO      NEXT5 IF NOT LESS
          DISPLAY   *P47:24,*V2LON,AADMNO;
          CALL      GENPROC
          GOTO      NEXT5
.
.       Make sure we have actually finished
.
CONT1     CALL      RDKMISS2
          BRANCH    OVRCD OF CONT1A
.
          COMPARE   WSTAT,ASTAT
          GOTO      DSPA IF EQUAL
CONT1A    RETURN
+
.       Generate according to the option
.
GENPROC   CALL      MCSTRN             * Generate Monthly Catchment Summary
          CALL      MSTTRN             * Generate Monthly Inpatient Statistics
          RETURN
+
.------------------------------------------------------------------------------
.       Monthly Catchment Summary Option
.       Loop through the TRANS file to get the number of beddays
.       for each patient
.
MCSTRN    CALL      NHOSPDAY
.
.       Read the master file to get the postcode
.
          MOVE      SP4,PPOST
          MOVE      AURNO,KEY8
          CALL      RDMAST1
.
          MOVE      SP3,CCCODE
.
          MOVE      PPOST,KEY8
          CALL      RDCATC1
          BRANCH    OVRCD OF CHKCLS
          MOVE      CTCCODE,CCCODE
.
.       Check the admission class
.
CHKCLS    MOVE      ONE,INDIC          * default Inpatient
          MATCH     SP3,ACLSS
          GOTO      CHKREC IF EQUAL
.
          PACK      KEY5 WITH CODEP,ACLSS
          CALL      RDCODE1
          BRANCH    OVRCD OF CHKREC
          MOVE      TCINDC1,INDIC
.
          COMPARE   ZERO,INDIC
          GOTO      CHKREC IF NOT EQUAL
          MOVE      ONE,INDIC            * As default
.
.       Check if the record already exists
.
CHKREC    PACK      CCCYYMM WITH VCURDATE
          REP       " 0",CCCYYMM
.
          BRANCH    PTCNNEWS,NOEXIST             * using new stats update ?
          PACK      KEY9 WITH CCCODE,CCCYYMM     * no
          CALL      RDCSTS1
          BRANCH    OVRCD OF NOEXIST
          CALL      CALREC
          CALL      UPCSTS1
          RETURN
+
.       New record
.
NOEXIST   CALL      INTCAT
          CALL      CALREC
.
          BRANCH    PTCNNEWS,NOEXIST10           * using new stats update ?
          CALL      WRCSTS1                      * no
.
NOEXIST10 RETURN
+
.       Get the appropriate fields
.
CALREC    MATCH     FROMDATE,ADATE
          GOTO      CALR10 IF LESS
.
.         Update the number of admissions
.
          MOVE      CCADMN,FORM8B
          LOAD      FORM8B USING INDIC OF CCADMN,CCSAMED,CCBOARD
          ADD       ONE,FORM8B
          STORE     FORM8B USING INDIC OF CCADMN,CCSAMED,CCBOARD
.
.         Update the number of bed days
.
CALR10    MOVE      CCABDAY,FORM8B
          LOAD      FORM8B USING INDIC OF CCABDAY,CCSMDAY,CCBRDAY
          ADD       NBRDAYS,FORM8B
          STORE     FORM8B USING INDIC OF CCABDAY,CCSMDAY,CCBRDAY
          RETURN
+
.       Initialize the record
.
INTCAT    MOVE      ZERO,CCADMN
          MOVE      ZERO,CCABDAY
          MOVE      ZERO,CCSAMED
          MOVE      ZERO,CCSMDAY
          MOVE      ZERO,CCBOARD
          MOVE      ZERO,CCBRDAY
          PACK      CCSPARE WITH SP30,SP30
          RETURN
+
.------------------------------------------------------------------------------
.         Monthly Inpatient Statistics file option
.
MSTTRN    MOVE      ZERO,LEVFLG           * on leave flag
          MOVE      ZERO,BRDFLG           * boarder flag
          MOVE      ZERO,CHGWRD           * change ward flag
.
.       Check for a day case
.
          MOVE      ZERO,DAYCASE
          COMPARE   THREE,ASTAT
          GOTO      NDCREC IF NOT EQUAL
.
.       Check for daycase
.
          MATCH     ADATE,DDATE
          GOTO      NDCREC IF NOT EQUAL
.
.       Check if it is in the selected month
.
          MATCH     FROMDATE,ADATE
          GOTO      NDCREC IF LESS
          MOVE      ONE,DAYCASE
.
.       Setup the key
.
NDCREC    MOVE      SP3,MSDCLS
          MOVE      VCURDATE,MSDATE
          MOVE      SP1,STMOVE
          MOVE      SP3,SAVWRD
          MOVE      SP3,SAVCLS
          MOVE      SP10,SAVDATE
.
          PACK      KEY30 WITH AADMNO,SP30
          CALL      RDSTRAN2
NXTTRN    CALL      RDKTRAN2
          BRANCH    OVRCD OF CHLST
.
          MATCH     AADMNO,TADMN
          GOTO      CHLST IF NOT EQUAL
.
          MOVE      TMOVE,STMOVE
          MOVE      TDATE,OTDATE
          CMATCH    ANSA,TMOVE
          GOTO      RECADM IF EQUAL
          CMATCH    ANSR,TMOVE
          GOTO      RECRET IF EQUAL
          CMATCH    ANSL,TMOVE
          GOTO      RECLEV IF EQUAL
          CMATCH    ANSD,TMOVE
          GOTO      RECCHG IF NOT EQUAL
.
.       Discharge record
.
          MATCH     FROMDATE,TDATE
          RETURN    IF LESS
.
          MATCH     TODATE,TDATE
          GOTO      CHL05 IF LESS
          MOVE      TODATE,TDATE
          GOTO      CHL05
.
.       Admission record
.
RECADM    CALL      CHKADM
          GOTO      RECR05
.
.       Return record
.
RECRET    MOVE      ONE,LEVFLG
.
RECR05    MATCH     TODATE,TDATE
          GOTO      RECR20 IF NOT LESS
.
          MATCH     FROMDATE,TDATE
          GOTO      RECR10 IF NOT LESS
          MOVE      FROMDATE,STDATE
          MOVE      ZERO,LEVFLG
          GOTO      SLAST
.
RECR10    COMPARE   ZERO,LEVFLG
          GOTO      RECR15 IF EQUAL
.
          MATCH     FROMDATE,STDATE
          GOTO      RECR12 IF LESS
.
.       Calculate number of bed days on leave
.
          MATCH     SAVWRD,TWARD
          GOTO      RECR11 IF EQUAL
          CALL      TRFR0000
.
RECR11    MOVE      STDATE,WDATE2
          MOVE      TDATE,WDATE1
          CALL      UPMSREC
          MOVE      ZERO,LEVFLG
          GOTO      RECR15
.
.       Patient start on leave from the previous month
.
RECR12    MATCH     SAVWRD,TWARD
          GOTO      RECR13 IF EQUAL
          CALL      TRFR0000
.
RECR13    MOVE      FROMDATE,WDATE2
          MOVE      TDATE,WDATE1
          CALL      UPMSREC
          MOVE      ZERO,LEVFLG
.
RECR15    MOVE      TDATE,STDATE
          GOTO      SLAST
.
RECR20    COMPARE   ZERO,LEVFLG
          RETURN    IF EQUAL
.
.       Get bed days on leave until end of month
.
          MOVE      TODATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      UPMSREC
          MOVE      ZERO,LEVFLG
          RETURN
+
.       On Leave record
.
RECLEV    MATCH     FROMDATE,TDATE
          GOTO      SLAST IF LESS
.
          MATCH     TODATE,TDATE
          GOTO      RECL10 IF LESS
          MOVE      TODATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      UPMSREC
          RETURN
+
.
RECL10    MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      UPMSREC
          MOVE      TDATE,STDATE        * Date start on leave
          GOTO      SLAST
.
.       Change record
.
RECCHG    MATCH     TWARD,SAVWRD        * No change - Ignore record
          GOTO      NXTTRN IF EQUAL
.
          MATCH     FROMDATE,TDATE
          GOTO      RECCH0 IF LESS
.
          MATCH     TODATE,TDATE
          GOTO      RECCH0 IF NOT LESS
.
.       Ward is changed during the month
.
          MOVE      ONE,CHGWRD
.
RECCH0    MATCH     FROMDATE,TDATE
          GOTO      SLAST IF LESS
.
          MATCH     TODATE,TDATE
          GOTO      RECCH1 IF LESS
.
          MOVE      TODATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      UPMSREC
          RETURN
+
.
RECCH1    MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      UPMSREC
          MOVE      TDATE,STDATE
.
.       Save the ward
.
SLAST     MOVE      SP3,WCLASS
          OPEN      PATWR1A1,"patwr1af"
          PACK      KEY6 WITH TWARD,SP3
          CALL      RDWARD1
          CLOSE     PATWR1A1
.
          MOVE      TDATE,SAVDATE
          MOVE      TWARD,SAVWRD
          MOVE      WCLASS,SAVCLS
          GOTO      NXTTRN
.
.       Check the last record
.
CHLST     MOVE      SAVWRD,TWARD
          MOVE      SAVCLS,WCLASS        * Restore the last ward
.
CHL05     MATCH     ANSL,STMOVE
          GOTO      CHL10 IF NOT EQUAL
.
          MOVE      ONE,LEVFLG
          MOVE      TODATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      UPMSREC               * calculate beddays on leave
          MOVE      ZERO,LEVFLG
          RETURN
+
.
CHL10     MATCH     SP1,STMOVE
          RETURN    IF EQUAL
+
.       Last record
.
          MATCH     ANSD,STMOVE
          GOTO      RECCUR IF NOT EQUAL
.
.       Patient is discharged
.
          MOVE      TDATE,WDATE1
          GOTO      RECC10
.
RECCUR    MOVE      TODATE,WDATE1
.
RECC10    MOVE      STDATE,WDATE2
          CALL      UPMSREC
          RETURN
+
.       Update beddays for the Monthly inpatient statistics file
.
UPMSREC   BRANCH    PTCNNEWS,GOTMST              * using new stats update ?
          PACK      KEY15 WITH SAVWRD,MSDCLS,SAVCLS,MSDATE   * no
          CALL      RDMSTS1
          COMPARE   ZERO,OVRCD
          GOTO      GOTMST IF EQUAL
.
          CALL      INTMST
          CALL      WRMSTS1
          CALL      RDMSTS1
.
GOTMST    MOVE      ZERO,NBRDAYS
          MOVE      ZERO,DSHFLG
          COMPARE   ZERO,DAYCASE
          GOTO      CALBD IF EQUAL
.
.       A day case only have one bedday
.       if this is not the last Trans record, no need to update the beddays
.       only update the transaction of ward if necessary
.
          CMATCH    ANSD,STMOVE
          GOTO      UPM20 IF NOT EQUAL
          MOVE      ONE,DSHFLG
          MOVE      ONE,NBRDAYS
          GOTO      UPM15
.
.       Calculate no of bed days for non-daycase patient
.
CALBD     MATCH     TODATE,WDATE1
          GOTO      UPM10 IF LESS
.
.       Use the end of the month as the TODATE
.
          MOVE      TODATE,WDATE1
.
UPM10     DAYSEP    WDATE2,WDATE1,NBRDAYS
          BRANCH    LEVFLG OF LEVBD
.
UPM15     BRANCH    BRDFLG OF UPBRD
.
.       Beddays for patient
.
          ADD       NBRDAYS,MSNBDAY
          GOTO      UPM20
.
.       Boarder
.
UPBRD     ADD       NBRDAYS,MSBBDAY
          GOTO      UPM20
.
.       Beddays on leave
.
LEVBD     ADD       NBRDAYS,MSLVBD
.
UPM20     COMPARE   ZERO,CHGWRD
          GOTO      UPM25 IF EQUAL
          ADD       ONE,MSTROUT
.
UPM25     BRANCH    PTCNNEWS,UPM28               * using new stats update ?
.
.         Check if the original transaction date is within the period
.
UPM26     MATCH     FROMDATE,OTDATE              * Ignore transaction not within
          GOTO      UPM27 IF LESS                * the period
.
          MATCH     TODATE,OTDATE
          GOTO      UPM27 IF NOT LESS
.
          MATCH     ANSR,STMOVE                  * Ignore the onleave bed days
          GOTO      UPM27 IF EQUAL
.
          PACK      SKEY30,TADMN,TDATE,TTIME,TWARD,TBED   * Save the key
.
          CMATCH    ANSD,STMOVE              * Calculate Separation bed days 
          GOTO      UPM27A IF EQUAL          * for patient discharged in periods
.
.         Check if the next transaction move is a discharged record
.
          CALL      RDKTRAN2
          BRANCH    OVRCD OF UPM27A
.
          MATCH     TADMN,AADMNO
          GOTO      UPM27A IF NOT EQUAL
.
          MATCH     TODATE,TDATE
          GOTO      UPM27A IF LESS
.
          CMATCH    ANSD,TMOVE             * Don't include bed days if patient
          GOTO      UPM27B IF EQUAL        * discharged in the next transaction
.
.         Calculate the separation bed days
. 
UPM27A    MOVE      SAVDATE,WDATE2          * Calculate the full period of 
          MOVE      OTDATE,WDATE1           * bed days
          MOVE      ZERO,NBRDAYS
          DAYSEP    SAVDATE,OTDATE,NBRDAYS
          ADD       NBRDAYS,MSSEPBD        * Separation bed days
.
.         Reposition the transaction record
.
UPM27B    MOVE      SKEY30,KEY30
          CALL      RDTRAN2
.
UPM27     CALL      UPMSTS1
.
.       Check if we need to update/write  the current record
.       - If the patient is discharged then we need to update/write a record
.         for the number of discharges/death
.       - If the patient changed ward then we need to update/write a record
.         that the patient was transfering to
.
UPM28     CMATCH    ANSD,STMOVE
          GOTO      UPM30 IF NOT EQUAL
.
          MATCH     SP8,DDATE
          GOTO      UPM30 IF EQUAL
.
.       Check the discharged date with the selected month
.
          MATCH     TODATE,DDATE
          GOTO      UPM30 IF NOT LESS
          MOVE      ONE,DSHFLG
          GOTO      UPM40
.
.       Check if Ward is changed
.
UPM30     COMPARE   ZERO,CHGWRD
          GOTO      CHKTEMP IF EQUAL
.
.       Create/update the file using the current ward
.
UPM40     MOVE      SP3,WCLASS
          OPEN      PATWR1A1,"patwr1af"
          PACK      KEY6 WITH TWARD,SP3
          CALL      RDWARD1
          CLOSE     PATWR1A1
.
          BRANCH    PTCNNEWS,UPM60               * using new stats update ?
          PACK      KEY15 WITH TWARD,MSDCLS,WCLASS,MSDATE    * no
          CALL      RDMSTS1
          COMPARE   ZERO,OVRCD
          GOTO      UPM60 IF EQUAL
          CALL      INTMST
          CALL      WRMSTS1
          CALL      RDMSTS1
.
UPM60     BRANCH    DSHFLG OF UPM65
          ADD       ONE,MSTRIN          * transfer in
          GOTO      UPM80
.
.       Patient is discharged
.
UPM65     ADD       ONE,MSDSCH
.
.       Check if patient is death or not
.
          PACK      KEY5 WITH CODED,DSTAT
          CALL      RDCODE1
          BRANCH    OVRCD OF UPM80
.
          CMATCH    "D",TCINDC1
          GOTO      UPM80 IF NOT EQUAL
          ADD       ONE,MSDEATH
.
UPM80     BRANCH    PTCNNEWS,UPM90               * using new stats update ?
          CALL      UPMSTS1                      * no
.
UPM90     MOVE      ZERO,CHGWRD
          MOVE      ZERO,DSHFLG
.
.       Check if patient is already in the tempfile
.       (total no of patient in the month)
.
CHKTEMP   MOVE      MSWARD,SMSWARD
          MOVE      MSDCLS,SMSDCLS
          MOVE      MSWCLS,SMSWCLS
          MOVE      MSDATE,SMSDATE
          MOVE      AADMNO,SAVADMN
.
          PACK      KEY21 WITH MSWARD,MSDCLS,MSWCLS,MSDATE,SAVADMN
          CALL      RDTEMP1
          COMPARE   ZERO,OVRCD
          RETURN    IF EQUAL
.
          CALL      WRTEMP1
          RETURN
+
.********************************************************************** 
.*                            TRFR0000                                *
.*   Write a transfer to stats file if "R" record has a ward change   *
.********************************************************************** 
TRFR0000  OPEN      PATWR1A1,"patwr1af"
          MOVE      SP3,WCLASS
          PACK      KEY6,WWARD,SP3
          CALL      RDWARD1
          MOVE      WCLASS,SAVCLS
.
          BRANCH    PTCNNEWS,TRFR2000            * using new stats update ?
          PACK      KEY15 WITH SAVWRD,MSDCLS,SAVCLS,MSDATE   * no
          CALL      RDMSTS1
          COMPARE   ZERO,OVRCD
          GOTO      TRFR2000 IF EQUAL
.
          MOVE      ZERO,SELDAT
          CALL      INTMST
          CALL      WRMSTS1
.
. -----   Add one to the Transfers out for the old ward   ------
.
TRFR2000 ADD       ONE,MSTROUT
.
          BRANCH    PTCNNEWS,TRFR3000            * using new stats update ?
          CALL      UPMSTS1                      * no
.
TRFR3000  MOVE      SP3,WCLASS
          PACK      KEY6,TWARD,SP3
          CALL      RDWARD1
          MOVE      WCLASS,MSWCLS
          CLOSE     PATWR1A1
.
          BRANCH    PTCNNEWS,TRFR4000            * using new stats update ?
          PACK      KEY15 WITH TWARD,MSDCLS,MSWCLS,MSDATE    * no
          CALL      RDMSTS1
          COMPARE   ZERO,OVRCD
          GOTO      TRFR4000 IF EQUAL
.
.       A new record
.
          CALL      INTMST
          CALL      WRMSTS1
          CALL      RDMSTS1
.
. -----   Add one to the Transfers in for the new ward   ------
.
TRFR4000  ADD       ONE,MSTRIN
.
          BRANCH    PTCNNEWS,TRFR9999            * using new stats update ?
          CALL      UPMSTS1                      * no
.
TRFR9999  RETURN
+
.       Check the first TRANS record (Admission record)
.
CHKADM    MOVE      SP3,WCLASS
          OPEN      PATWR1A1,"patwr1af"
          PACK      KEY6 WITH TWARD,SP3
          CALL      RDWARD1
          CLOSE     PATWR1A1
.
          MOVE      TWARD,MSWARD
          MOVE      WCLASS,MSWCLS
          MOVE      TWARD,SAVWRD
          MOVE      TDATE,SAVDATE
          MOVE      WCLASS,SAVCLS
.
.         Check if IBAPMS31 is to update PATMSTFD - indicated by PTCNNEWS=1
.
          BRANCH    PTCNNEWS,CHKTYP              * using new stats update ?
          PACK      KEY15 WITH MSWARD,MSDCLS,MSWCLS,MSDATE   * no
          CALL      RDMSTS1
          COMPARE   ZERO,OVRCD
          GOTO      CHKTYP IF EQUAL
.
.       A new record
.
          CALL      INTMST
          CALL      WRMSTS1
          CALL      RDMSTS1
.
.       Check if private or public (admission type)
.
CHKTYP    MOVE      ONE,PUBFLG                * public flag (default)
          MOVE      ZERO,FORM1
          MOVE      ZERO,BRDFLG               * boarder flag
.
          MATCH     SP3,TATYPE
          GOTO      CHKBRD IF EQUAL
.
          PACK      KEY5 WITH CODEA,TATYPE
          CALL      RDCODE1
          BRANCH    OVRCD OF CHKBRD
          MOVE      TCINDC1,FORM1
          BRANCH    FORM1 OF CHKBRD,CHKT10
          GOTO      CHKBRD
.
CHKT10    MOVE      ZERO,PUBFLG               * private
.
.       Check if boarder or not boarder (admission class)
.
CHKBRD    MOVE      ZERO,BRDFLG                * default not a boarder
          MATCH     SP3,ACLSS
          GOTO      NOBOARD IF EQUAL
.
          PACK      KEY5 WITH CODEP,ACLSS
          MOVE      ZERO,FORM1
          CALL      RDCODE1
          BRANCH    OVRCD OF NOBOARD
.
          MOVE      TCINDC1,FORM1
          COMPARE   THREE,FORM1
          GOTO      NOBOARD IF NOT EQUAL
.
.       Boarder
.
          MOVE      ONE,BRDFLG
.
          BRANCH    PUBFLG OF PBBOARD
          ADD       ONE,MSBPRAD               * Private Boarder patient
          GOTO      CHKSRC
.
PBBOARD   ADD       ONE,MSBPBAD               * Public Boarder patient
          GOTO      CHKSRC
.
.       Not a Boarder
.
NOBOARD   BRANCH    PUBFLG OF NOPBBR
          ADD       ONE,MSNPRAD               * Private not a Boarder patient
          GOTO      CHKSRC
.
NOPBBR    ADD       ONE,MSNPBAD               * Public not a Boarder patient
.
.       Check for A & E (admission source)
.
CHKSRC    PACK      KEY5 WITH CODES,ASRCE
          CALL      RDCODE1
          BRANCH    OVRCD OF UPMADM
.
          CMATCH    ANSA,TCINDC1
          GOTO      UPMADM IF NOT EQUAL
          ADD       ONE,MSAEADM                * A & E admission
.
.       Check if the patient admited in the selected month
.
UPMADM    MATCH     FROMDATE,ADATE
          RETURN    IF LESS
.
          BRANCH    PTCNNEWS,UPMADM10            * using new stats update ?
          CALL      UPMSTS1                      * no
.
UPMADM10  RETURN
+
.       Initialize the Monthly Inpatient Statistics record
.
INTMST    MOVE      SP3,MSWARD
          MOVE      SP3,MSDCLS
          MOVE      SP3,MSWCLS
          MOVE      SP6,MSDATE
.
INTMST1   MOVE      SELDAT,MSDYMTH      * always same no of days in the month
          MOVE      ZERO,MSNPRAD
          MOVE      ZERO,MSNPBAD
          MOVE      ZERO,MSTRIN
          MOVE      ZERO,MSTROUT
          MOVE      ZERO,MSDSCH
          MOVE      ZERO,MSNBDAY
          MOVE      ZERO,MSBBDAY
          MOVE      ZERO,MSDEATH
          MOVE      ZERO,MSBPRAD
          MOVE      ZERO,MSBPBAD
          MOVE      ZERO,MSLVBD
          MOVE      ZERO,MSAEADM
          MOVE      ZERO,MSOBSTH
          MOVE      ZERO,MSOUTTH
          MOVE      ZERO,MSNOPAT
          MOVE      ZERO,MSSEPBD
          PACK      MSSPARE WITH SP20,SP20,SP10
          RETURN
+
.------------------------------------------------------------------------------
.       Calculate number of patients in the month from the tempfile
.
CALCPAT   PACK      KEY23 WITH SP20,SP10
          MOVE      SP20,DIM15
          MOVE      SP20,DIM15A
          MOVE      ZERO,FIRST
          MOVE      ZERO,PATNUM
.
          CALL      RDSTEMP1
NXTTEMP   CALL      RDKTEMP1
          BRANCH    OVRCD OF NXTT20
.
          PACK      DIM15 WITH SMSWARD,SMSDCLS,SMSWCLS,SMSDATE
          MATCH     DIM15,DIM15A
          GOTO      NXTT10 IF EQUAL
.
.       Check if we need to update the previous record
.
          COMPARE   ZERO,FIRST
          GOTO      NXTT10 IF EQUAL
          CALL      CPATMST                 * Update/write to Statistics file
          MOVE      ZERO,PATNUM
.
.       Accumulate no of patients
.
NXTT10    ADD       ONE,PATNUM
          MOVE      DIM15,DIM15A
          MOVE      ONE,FIRST
          GOTO      NXTTEMP
.
.       Last record
.
NXTT20    COMPARE   ZERO,FIRST             * Check if there is any record
          RETURN    IF EQUAL
.
          CALL      CPATMST
          RETURN
+
.       Check if the record exists  then write to Monthly statistics file
.
CPATMST   MOVE      DIM15A,KEY15
.
          BRANCH    PTCNNEWS,CPATM10             * using new stats update ?
          CALL      RDMSTS1                      * no
          BRANCH    OVRCD OF CPATM10
          MOVE      PATNUM,MSNOPAT
          CALL      UPMSTS1
          RETURN
+
.       Create a new record
.
CPATM10   CALL      INTMST1
          ADD       PATNUM,MSNOPAT
.
          BRANCH    PTCNNEWS,CPATM20             * using new stats update ?
          CALL      WRMSTS1                      * no
.
CPATM20   RETURN
+
.------------------------------------------------------------------------------
.         Set up alternative site file to avoid duplication
.         of file opens & reads for same file (e.g. outbb1af)
.------------------------------------------------------------------------------
SITE0000  CLEAR     OUTTM1                   * Set up temp filename
          APPEND    TEMPTM1,OUTTM1
          RESET     OUTTM1
.
          CLOSE     OUTTM1XX                 * Close logical file
          PACK      CMDLINE,SP30,SP30        * Remove current tempfile
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    OUTTM1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP30,SP30        * Create tempfile
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    OUTTM1,CMDLINE
          APPEND    " 10 u1-9",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      OUTTM1XX,OUTTM1
.
          PACK      KEY6,SP6
          CALL      RDSSITA1
SITE1000  CALL      RDKSITA1
          BRANCH    OVRCD,SITE9000
          PACK      KEY9,OSTFILE,OSTSITE
          CALL      RDATM1XX
          BRANCH    OVRCD,SITE2000
          GOTO      SITE1000
.
SITE2000  CALL      WRTM1XX
          GOTO      SITE1000
.
SITE9000  RETURN
+
.         Temp Site file I/Os
.
RDATM1XX  MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      OUTTM1XX,KEY9;ANS
          GOTO      OVERCOND IF OVER
          RETURN  
.
RDSTM1XX  RESET     KEY9
          READ      OUTTM1XX,KEY9;;
          RETURN  
.
RDKTM1XX  MOVE      ZERO,OVRCD
          READKS    OUTTM1XX;OSTFILE,OSTSITE
          GOTO      OVERCOND IF OVER
          RETURN  
.
WRTM1XX   RESET     KEY9
          WRITE     OUTTM1XX,KEY9;KEY9
          RETURN  
+
.       Empty the interim file and reindex
.
IDEXINT   CALL      KILLIDZ
          EXECUTE   "isbuild pattempf 24 u1-23",TASKID
          OPEN      PATTEMP1,"pattempf"
          RETURN
+
.       Deleting an indexed file based on port
.
KILLIDZ   EXECUTE   "iserase pattempf",TASKID
          RETURN
+
.          I/O Routine for tempfile (used for inpatient statistics)
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY23
          MOVE      SAVADMN,DSAVADMN
          READ      PATTEMP1,KEY23;SMSWARD,SMSDCLS,SMSWCLS,SMSDATE,DSAVADMN
          GOTO      OVERCOND IF OVER
          MOVE      DSAVADMN,SAVADMN
          RETURN
.
RDSTEMP1  RESET     KEY23
          READ      PATTEMP1,KEY23;;
          RETURN
.
RDKTEMP1  MOVE      ZERO,OVRCD
          MOVE      SAVADMN,DSAVADMN
          READKS    PATTEMP1;SMSWARD,SMSDCLS,SMSWCLS,SMSDATE,DSAVADMN
          GOTO      OVERCOND IF OVER
          MOVE      DSAVADMN,SAVADMN
          RETURN
.
WRTEMP1   RESET     KEY23
          MOVE      ZERO,OVRCD
          READ      PATTEMP1,KEY23;ANS
          GOTO      OVERCOND IF NOT OVER
.
          MOVE      SAVADMN,DSAVADMN
          WRITE     PATTEMP1,KEY23;KEY23
          RETURN
+
.------------------------------------------------------------------------------
.         Clear the attendance file for the current site
.------------------------------------------------------------------------------
CLR2000   PACK      KEY24,OSTSITE,SP20
          CALL      RDSATTA1
CLR2100   CALL      RDKATTA1
          BRANCH    OVRCD,CLR2999
.
          MATCH     OATSITE,OSTSITE
          GOTO      CLR2999 IF NOT EQUAL
.
          MATCH     OATDATE,VCURDATE
          GOTO      CLR2100 IF NOT EQUAL
.
.         Clear this record
.
          MOVE      ZERO,OATNREF
          MOVE      ZERO,OATNNON
          MOVE      ZERO,OATNBKD
          CALL      UPATTA1
          GOTO      CLR2100
.
CLR2999   RETURN
+
.------------------------------------------------------------------------------
.         Clear the location file for the current site
.------------------------------------------------------------------------------
CLR3000   PACK      KEY24,OSTSITE,SP20
          CALL      RSOTLOC1
CLR3100   CALL      RKOTLOC1
          BRANCH    OVRCD,CLR3999
.
          MATCH     LOASITE,OSTSITE
          GOTO      CLR3999 IF NOT EQUAL
.
          MATCH     LOADATE,VCURDATE
          GOTO      CLR3100 IF NOT EQUAL
.
.         Clear this record
.
          MOVE      ZERO,LOANUMB
          CALL      UPOTLOC1
          GOTO      CLR3100
.
CLR3999   RETURN
+
.------------------------------------------------------------------------------
.         Clear the open sessions file for the current site
.------------------------------------------------------------------------------
CLR4000   PACK      KEY25,OSTSITE,SP20
          CALL      RDSSESA1
CLR4100   CALL      RDKSESA1
          BRANCH    OVRCD,CLR4999
.
          MATCH     OSESITE,OSTSITE
          GOTO      CLR4999 IF NOT EQUAL
          MOVE      OSEDATE,DIM6                * move only ccyymm for testing
. 
          MATCH     DIM6,VCURDATE
          GOTO      CLR4100 IF NOT EQUAL
.
.         Clear this record
.
          MOVE      SP1,DOSEDAY
          MOVE      ZERO,OSEANEW
          MOVE      ZERO,OSEARV 
          MOVE      ZERO,OSEASPC 
          MOVE      ZERO,OSEBNEW
          MOVE      ZERO,OSEBRV 
          MOVE      ZERO,OSEBSPC 
          MOVE      SP5,OSETIMEU 
          CALL      UPSESA1                     * update file
          GOTO      CLR4100
.
CLR4999   RETURN
+
.------------------------------------------------------------------------------
.         Clear the occassions of service file for the current site
.------------------------------------------------------------------------------
CLR6000   PACK      KEY24,OSTSITE,SP20
          CALL      RDSOCCA1
CLR6100   CALL      RDKOCCA1
          BRANCH    OVRCD,CLR6999
.
          MATCH     OCCSITE,OSTSITE
          GOTO      CLR6999 IF NOT EQUAL
.
          MATCH     OCCDATE,VCURDATE
          GOTO      CLR6100 IF NOT EQUAL
.
.         Clear this record
.
          MOVE      ZERO,OCCNUMB
          CALL      UPOCCA1
          GOTO      CLR6100
.
CLR6999   RETURN
+
.------------------------------------------------------------------------------
.         Clear the Referral Source file for the current site
.------------------------------------------------------------------------------
CLR7000   PACK      KEY17,OSTSITE,SP20
          CALL      RDSSTBA1
CLR7100   CALL      RDKSTBA1
          BRANCH    OVRCD,CLR7999
.
          MATCH     OSBSITE,OSTSITE
          GOTO      CLR7999 IF NOT EQUAL
.
          MATCH     OSBDATE,VCURDATE
          GOTO      CLR7100 IF NOT EQUAL
.
.         Clear this record
.
          MOVE      ZERO,OSBNUMB
          CALL      UPSTBA1
          GOTO      CLR7100
.
CLR7999   RETURN
+
.
.*********************************************************************
. PDSC0000 : Process Discharges  (Pass 7)
.*********************************************************************
PDSC0000  CALL      CRTP0000                 * Create temp site file
          CALL      LSIT0000                 * Load the temp site file
          CALL      DDSC0000                 * delete discharge stats
.                                             on outattaf for this period
.
          DISPLAY   *P1:18,*EF,*V2LON,"*** Processing Discharges ":
                                      "***",*HOFF," - Pass 7":
                    *P1:20,"Site        :":
                    *P1:21,"Date        :":
                    *P1:22,"Outpatient  :":
                    *P40:20,"No. of Discharges :";
.
          MOVE      ZERO,DSCOUNT
.
          OPEN      PATVISA1,"patvisaf"      * Use visit file
.
          PACK      KEY3,SP3
          CALL      RSTMFLA1                 * Positioning read at start
.
PDSC1000  CALL      RKTMFLA1                 * Read next site record
          BRANCH    OVRCD,PDSC9100           * No more sites
.
PDSC1500  DISPLAY   *P15:20,*V2LON,OBASITE
.
          PACK      CFNAME,OSTFILE,FILBOKA3
          OPEN      OUTBOKA3,CFNAME          * Open bookings file
.
          PACK      CFNAME,OSTFILE,FILBB1A2
          CALL      OPOTBB12
.
          PACK      CFNAME,OSTFILE,FILATTA1
          OPEN      OUTATTA1,CFNAME          * Open attendance file
.
          PACK      KEY16,VFRDATE,SP20       * Position on bokb
          CALL      RDBOKB2             
.
PDSC2000  CALL      RDKBOKB2                 * Read bokb
          BRANCH    OVRCD,PDSC1000           * No more records - get next site
.
          MATCH     OBADATE,VTODATE          * past end of period? 
          GOTO      PDSC1000 IF LESS         * yes, get next site
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P15:21,*V2LON,CPCDATE:
                    *P15:22,OBAOUTNO;
.
          PACK      KEY8,OBAOUTNO,SP10  
          CALL      RDVISA1                  * read visit file for U/R     
          BRANCH    OVRCD,PDSC2000           * not there
.
          PACK      KEY36,PVIURNO,SP20,SP20
          CALL      RDSBOKA3
.
PDSC3000  CALL      RDKBOKA3
          BRANCH    OVRCD,PDSC2000           * EOF, get next bokb
.
          MATCH     OBAURNO,PVIURNO          * same U/R?
          GOTO      PDSC2000 IF NOT EQUAL    * no, get next bokb
.
          MATCH     OBAOUTNO,PVIBILL         * same outpatient number?
          GOTO      PDSC3000 IF NOT EQUAL    * no, get next visit
.
          MATCH     SP3,OBADISCH             * Discharged?
          GOTO      PDSC2000 IF EQUAL        * no, get next bokb
.
          ADD       ONE,DSCOUNT
          DISPLAY   *P58:20,DSCOUNT
.
.         Set up variables
.
          MOVE      OBASITE,OATSITE          * Site
          MOVE      OBACTYP,OATCTYP          * Clinic type
          MOVE      OBASRCR,OATREFR          * Source of referral
          MOVE      VCURDATE,OATDATE         * Month of statistics
.
.         Get Group Type
.
          PACK      CFNAME,OSTFILE,FILCTYA1
          OPEN      OUTCTYA1,CFNAME   
.
          PACK      KEY12,OBASITE,OBACTYP,SP20
          CALL      RDCTYA1
          BRANCH    OVRCD,PDSC2000           * Clinic Type not there!
.
          MOVE      OCTGRP,OATGRUP           * Group Type
.
.         update or write
.
          PACK      KEY24,OATSITE,OATGRUP,OATCTYP,OATREFR,OATDATE
          CALL      RDATTA1
          IF        OVRCD = ONE 
            MOVE      ONE,OTATDSCH
            CALL      WRATTA1                * write new
          ELSE
            ADD       ONE,OTATDSCH
            CALL      UPATTA1                * Update existing
          ENDIF
.
          GOTO      PDSC2000                 * Get next record
.
PDSC9100  CALL      KILL0000                 * delete temp site file
.
PDSC9999  RETURN
+
.
.*********************************************************************
. DDSC0000 : Delete Discharges for this period
.*********************************************************************
DDSC0000 DISPLAY   *P1:18,*EF,*V2LON,"*** Processing Discharges ":
                                      "***",*HOFF," - Pass 7":
                    *P1:19,"Deleting old stats... ":
                    *P1:20,"Site        :"
.
          PACK      KEY3,SP3
          CALL      RSTMFLA1  
DDSC1000  CALL      RKTMFLA1                 * Read next site record
          BRANCH    OVRCD,DDSC9999           * No more sites
.
DDSC1500  PACK      CFNAME,OSTFILE,FILATTA1
          OPEN      OUTATTA1,CFNAME          * Open attendance file
.
          PACK      KEY24,VSTRSITE,SP20,SP5  * Starting Site
          CALL      RDSATTA1                 * Positioning read
DDSC3000  CALL      RDKATTA1                 * Read next
          BRANCH    OVRCD,DDSC1000           * get next site
.
          DISPLAY   *P15:20,*V2LON,OATSITE
.
          MATCH     OATSITE,VENDSITE         * past end site?
          GOTO      DDSC1000 IF LESS         * yes, get next site
.
          MATCH     OATDATE,VCURDATE         * same period?
          GOTO      DDSC1000 IF NOT EQUAL    * no, get next site
.
          MOVE      ZERO,OTATDSCH
          PACK      KEY24,OATSITE,OATGRUP,OATCTYP,OATREFR,OATDATE,SP20,SP5
          CALL      UPATTA1                  * Update existing
.
          GOTO      DDSC3000
.
DDSC9999  RETURN
+
.
.*********************************************************************
. LSIT0000 : Load temp site file
.            This routine loads a temp file with file prefixes
.            for all site codes within site code range
.*********************************************************************
LSIT0000  DISPLAY   *P1:18,*EF,*V2LON,"Building temp site file..."
.
          PACK      KEY6,VSTRSITE            * Starting Site
          CALL      RDSSITA1                 * Positioning read
          COMPARE   ZERO,OVRCD               * Got record ?
          GOTO      LSIT1500 IF EQUAL        * Yes - skip readks
.
LSIT1000  CALL      RDKSITA1                 * Read next site record
          BRANCH    OVRCD,LSIT9999           * No more sites
.
LSIT1500  MATCH     VSTRSITE,OSTSITE         * If site read is before Starting
          GOTO      LSIT9999 IF LESS           Site then no more records - exit
          MATCH     OSTSITE,VENDSITE         * If site read is past Ending Site
          GOTO      LSIT9999 IF LESS           then exit as well
.
          PACK      KEY3,OSTFILE
          CALL      RDTMFLA1                 * Write record to temp file
          IF        OVRCD=1
            PACK      KEY3,OSTFILE
            CALL      WRTMFLA1               * Write record to temp file
          ENDIF
          GOTO      LSIT1000
.
LSIT9999  RETURN
+
.
.******************************************************************************
.* CRTP0000   : Create temporary file
.******************************************************************************
CRTP0000  CALL      KILL0000                * make sure old file is not there
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPAFNAM,CMDLINE
          APPEND    " 4 U1-3",CMDLINE
.
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TMPFILA1,TMPAFNAM 
.
CRTP9999  RETURN
+
.
.******************************************************************************
.* KILL0000   : Remove temporary file
.******************************************************************************
KILL0000  CLOSE     TMPFILA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TMPAFNAM,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.
.===============================
.  TEMPORARY FILE IO ROUTINES 
.===============================
.
RSTMFLA1   READ      TMPFILA1,KEY3;;
           RETURN
.
RATMFLA1   MOVE      ZERO,OVRCD
           READ      TMPFILA1,KEY3;ANS
           GOTO      OVERCOND IF OVER 
           RETURN
.
RDTMFLA1   MOVE      ZERO,OVRCD
           READ      TMPFILA1,KEY3;OSTFILE
           GOTO      OVERCOND IF OVER 
           RETURN
.
RPTMFLA1   MOVE      ZERO,OVRCD
           READKP    TMPFILA1;OSTFILE
           GOTO      OVERCOND IF OVER 
           RETURN
.
RKTMFLA1   MOVE      ZERO,OVRCD
           READKS    TMPFILA1;OSTFILE
           GOTO      OVERCOND IF OVER 
           RETURN
.
WRTMFLA1   WRITE     TMPFILA1,KEY3;OSTFILE
           RETURN
.
UPTMFLA1   UPDATE    TMPFILA1;OSTFILE
           RETURN
.
DETMFLA1   RESET     KEY3            
           DELETE    TMPFILA1,KEY3
           RETURN
.
.==============================================================================
.
          INC       STD001IO
          INC       NHOSPDAY
          INC       PATCOMN3
.
          INC       OUTDSSIT
.
          INC       OUTATTIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTCTYIO/INC
          INC       OUTLOCIO/INC
          INC       OUTOCCIO/INC
          INC       OUTSESIO/INC
          INC       OUTSITIO/INC
          INC       OUTSTBIO/INC
          INC       OUTUSEIO/INC
.
          INC       PATDRGIO/INC
          INC       PATCT1IO/INC
          INC       PATCODIO/INC
          INC       PATCSTIO/INC
          INC       PATDSCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATMSTIO/INC
          INC       PATTRNIO/INC
          INC       PATVISIO/INC
          INC       PATWR1IO/INC
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
+

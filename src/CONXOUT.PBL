.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   CONXOUT                                                   *
.* Desc      :   Delete Conversion data for Outpatients and Allied Health  *
.***************************************************************************
.* Date      :   05/06/2001                                                *
.* Author    :   Mike Laming                                               *
.* Function  :   This program will read the conversion files convahrf.txt  *
.*               and convoutp.txt to remove Allied Health Referrals data & *
.*               Outpatients data from ALLREFAF and PMSEVTAF               *
.* Mods      :                                                             *
.*               V9.05.B01 20/02/2006  Ebon Clements     CAR 76341         *
.*               Added referral and encounter file audit                   *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLCONFD/INC                 * Allied Heath Control File
          INC       PATMA1FD/INC                 * Patient Master file
          INC       PATVISFD/INC                 * Visit Details
          INC       PMSVX1FD/INC                 * Visit Extension 
          INC       PMSEVTFD/INC                 * Patient Event file
          INC       PATDO1FD/INC                 * Patient Doctor Table
          INC       PMSHCPFD/INC                 * Patient Healthcare Profess
          INC       PATCODFD/INC                 * Patient Codes Table
          INC       ALLREFFD/INC                 * Allied Health Referral 
          INC       OUTCTYFD/INC                 * Outpatients Clinic Type
          INC       OUTCLIFD/INC                 * Outpatients Clinic ID
          INC       OUTSITFD/INC                 * Outpatients Site Table
.
.
.Layout of convahrf.txt
.---------------------
AHRFPD   FILE      ASCII, FIXED=131                                    *C-90202
.
.Name     Type    Length  Pos  Description
.----     ----    ------  ---  -----------
XEVISN    DIM       8      1    Visit Number
XEREFN    DIM       8      9    Referral Number
XEURNO    DIM       8      17   U/R Number
XERDAT    DIM       8      25   Date of Referral (CCYYMMDD)    (Date of Letter)
XESRCE    DIM       3      33   Source of Referral  (Categ LQ) (Referral Source)
XERHCP    DIM       6      36   Referring HCP (pmshcpaf)       (Referring Doc)
XERNAM    DIM       30     42   Refer's Name                   (Ref. Person)
XEHCP     DIM       6      72   Responsible HCP (patdo1af)     (Responsible Doc)
XEUNIT    DIM       3      78   Unit/Specialty   (Category AC) (Unit)
XEPRTY    DIM       3      81   Priority (Category TP)         (Priority)
XECOMP    DIM       3      84   Compensation Cod (Category CL) (Claim Code)
XECTYP    DIM       6      87   Clinic Type                    (Clinic Type)
XEDIA1    DIM       9      93   Diagnosis Code 1               |
XEDIA2    DIM       9      102  Diagnosis Code 2               | were XEPROn
XEDIA3    DIM       9      111  Diagnosis Code 3               |
XELINK    DIM       8      120  Linked Visit Number
XEEDEP    DIM       3      128  Department Code                (Department)
.        End of record     131
.
.
.Layout of convoutp.txt
.---------------------
OUTPATD   FILE      ASCII, FIXED=135
.
.Name     Type    Length  Pos  Description
.----     ----    ------  ---  -----------
XVURNO    DIM       8      1    U/R Number
XVEVNT    DIM       3      9    Event Type (Category EV) 
XVADTE    DIM       8      12   Admission/Attendance Date (ccyymmdd
XVATIM    DIM       5      20   Admission/Attendance Time (hh:mm:ss) (to 8 byte)
XVBTYP    DIM       3      25   Booking Type (Cat S)       (Source of Referral)
XVDDTE    DIM       8      28   Discharge/Depart Date (ccyymmdd)
XVDTIM    DIM       5      36   Discharge/Depart Time (hh:mm:ss)  (Convert to 8)
XVACON    DIM       6      41   Attending Consultant (pmshcpaf)  (Doc.Seen Code)
XNANAM    DIM       25     47   Doctor's Name   (When needed to add to PATDO1FD)
XVATYP    DIM       3      72   Appointment Type (Cat CV)
XVASTT    DIM       1      75   Attendance Status
XVCCOD    DIM       3      76   Claim Code  (Cat CL)
XVCTYP    DIM       6      79   Clinic Type (outctyaf)
XVCLID    DIM       6      85   Clinic Id   (outcliaf)
XNCNAM    DIM       30     91   Clinic Name (when needed to add to OUTCLIFD)
XVVISN    DIM       8      121  Visit Number
XNRCNC    DIM       3      129  Reason for cancellation (Category CB)  to where?
XVUNIT    DIM       3      132  Unit (Department)       (Category AC)
.        End of record     135
.
TEMP      FILE      ASCII,FIXED=256
.
ANERROR   FORM      1
BYPAHRF   FORM      1
BYPOUTPT  FORM      1
COUNT     FORM      6
COUNTIN   FORM      6
COUNTADE  FORM      6
COUNTEDE  FORM      6
COUNTVDE  FORM      6
COUNT1    FORM      3
DATAERR   FORM      1                                                  *I-90204
DISPVAR   FORM      6
NEWVIS    FORM      1
WHICHPAS  FORM      1
.
CH12      DIM       12
CURDTE    DIM       8
KEY127    DIM       127
LINNO     DIM       3
PRTPT1    DIM       35
PRTPT2    DIM       92
SECS      INIT      ":00" 
.                    ....*....1....*....2....*....3....*....4..
SP127     INIT      "                                          ":
                    "                                          ":
                    "                                           "
WRKDATE   DIM       10
WDD       DIM       2
WMM       DIM       2
WCC       DIM       2
WYY       DIM       2
.
DASH      INIT      "-"
LALL      INIT      "ALL   "
LOUT      INIT      "OUT   "
LCL       INIT      "CL"
LTP       INIT      "TP"
.
PRGID     INIT      "CONXOUT"
PRGNAM    INIT      "Remove Allied Health Refs & Outpatients"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and check files
          BRANCH    EXIT,ML9999            * if conversion files don't exist
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      OPEN0000               * Open file
          CALL      PROC0000               * process the details
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and check files. *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      AHRFPD,"convahrf"
          MOVE      OVRCD,BYPAHRF
          TRAPCLR   IO
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OUTPATD,"convoutp"
          MOVE      OVRCD,BYPOUTPT
          TRAPCLR   IO
.
          MOVE      ZERO,EXIT
.                                           * If no conversion files exist
          IF        BYPAHRF=1 & BYPOUTPT=1
            DISPLAY   *P1:23,*EL,*B,"Neither convoutp.txt nor convahrf.txt ":
                      " exist - Process Terminated":  
                      *P1:24,"Hit <",*V2LON,"ENTER",*HOFF,"> to continue ";
            KEYIN     ANS;
            MOVE      ONE,EXIT
          ENDIF 
.
          PACK      CURDTE,CCC,CYY,CMM,CDD  * Set up the current date for use
          REPLACE   " 0",CURDTE
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.****************************************************************************

OPTN0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:4,"This program removes Outpatient and/or Allied ":
                    "Health Referral data ":
                    *P20:5,"using the convoutp & convahrf.txt files";
          KEYIN     *P1:24,*EL,"Ok to Continue (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          GOTO      OPTN9000 IF EQUAL
.
          MATCH     ANSN,ANS
          GOTO      OPTN9500 IF EQUAL
.
          BEEP
          GOTO      OPTN0000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.**************************************************************************
.*                               OPEN0000              Called by: ML0000  *
.*                              Open Files                                *
.**************************************************************************
.
OPEN0000  MOVE      ZERO,OVRCD                   * 
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.                                                * Visit Details
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
.                                                * Doctor Table 
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.                                                * Referring Doctor Table 
.         DISPLAY   *P64:24,"pmshcpaf";
.         OPEN      PMSHCPA1,"pmshcpaf"
.                                                * Codes Table
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.                                                * Clinic Type
          DISPLAY   *P64:24,"outctyaf";
          OPEN      OUTCTYA1,"outctyaf"
.                                                * Clinic Id
          DISPLAY   *P64:24,"outcliaf";
          OPEN      OUTCLIA1,"outcliaf"
.                                                * Site     
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          COMPARE   ONE,BYPAHRF
          GOTO      OPEN1000 IF EQUAL
.                                                * Allied Health Referral
          DISPLAY   *P64:24,"allrefaf";
          OPEN      ALLREFA1,"allrefaf"
.
OPEN1000  COMPARE   ONE,BYPOUTPT
          GOTO      OPEN9999 IF EQUAL
.                                                * Patient Events
          DISPLAY   *P64:24,"pmsevtaf";
          OPEN      PMSEVTA1,"pmsevtaf"
.
OPEN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                     Process the conversion files                       *
.**************************************************************************
.
.
PROC0000  CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CPAGENO
          MOVE      "100",CLNO              * Force 1st page Heading
          MOVE      ONE,CNOUNDLN
.
          CALL      HEAD0000
          MOVE      ZERO,COUNTIN
          MOVE      ZERO,COUNTADE
          MOVE      ZERO,COUNTVDE
.
          COMPARE   ONE,BYPAHRF
          GOTO      PROC5000 IF EQUAL
.
          PRINT     *1,"|",*37,"|",*50,"'convahrf.txt'",*132,"|"
.
          MOVE      ONE,COUNTIN
          MOVE      ONE,COUNT
          DISPLAY   *P1:14,"* Processing ",*V2LON,"convahrf.txt ",*HOFF," *":
                    *P1:16,"Started : ",*V2LON,CTIMEIS,*HOFF:
                    *P1:18,"U/R Number : ":
                    *P1:19,"S          : ":
                    *P1:20,"S          : ":
                    *P1:21,*EL,"Records    : ";
.
          MOVE      ZERO,COUNTIN
.
PROC1000  MOVE      ZERO,OVRCD
          MOVE      ZERO,DATAERR                                       *I-90204
          TRAP      FERR0000 IF FORMAT                                 *I-90204
.
          READ      AHRFPD,SEQ;XEVISN,XEREFN,XEURNO,XERDAT:            *I-90202
                               XESRCE,XERHCP,XERNAM,XEHCP:             *I-90202
                               XEUNIT,XEPRTY,XECOMP,XECTYP:            *I-90202
                               XEDIA1,XEDIA2,XEDIA3,XELINK,XEEDEP      *I-90202
.
          TRAPCLR   FORMAT                                             *I-90204
.
          GOTO      PROC4000 IF OVER        * Check for EOF
          BRANCH    DATAERR,PROC1000        * Get next record          *I-90204
.
          ADD       ONE,COUNTIN
          ADD       ONE,COUNT
          ASSIGN    COUNT % 100,DISPVAR
          IF        DISPVAR = 0
            DISPLAY   *P14:18,*V2LON,XEURNO:
                      *P14:19,*V2LON,XEVISN:
.                     *P14:20,*V2LON,XRGNAM:
                      *P14:21,COUNT;
          ENDIF
.
          MOVE      ONE,WHICHPAS
.
          CALL      UPAA0000                * Add to Allied Health Referral
          GOTO      PROC1000
.
PROC4000  PRINT     *1,"|",*37,"|",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTIN:
                    " 'convahrf.txt' records read",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTADE:
                    " Allied Health Referrals deleted",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTVDE:
                    " Visit records deleted",*132,"|"
          ADD       FOUR,CLNO
.
          MOVE      ZERO,COUNTIN
          MOVE      ZERO,COUNTVDE
.
PROC5000  COMPARE   ONE,BYPOUTPT
          GOTO      PROC9999 IF EQUAL
.
          PRINT     *1,"|",*37,"|",*132,"|":
                    *N,*1,"|",*37,"|",*50,"'convoutp.txt'",*132,"|"
          ADD       TWO,CLNO
          MOVE      ZERO,COUNTEDE
.
          MOVE      ONE,COUNT
          DISPLAY   *P1:14,"* Processing ",*V2LON,"convoutp.txt ",*HOFF," *":
                    *P1:16,"Started : ",*V2LON,CTIMEIS,*HOFF:
                    *P1:18,"U/R Number : ":
                    *P1:19,"A          : ":
                    *P1:20,"A          : ":
                    *P1:21,*EL,"Records    : ";
.
PROC6000  MOVE      ZERO,OVRCD
          MOVE      ZERO,DATAERR                                       *I-90204
          TRAP      FERR2000 IF FORMAT                                 *I-90204
.
          READ      OUTPATD,SEQ;XVURNO,XVEVNT,XVADTE,XVATIM,XVBTYP,XVDDTE:
                                XVDTIM,XVACON,XNANAM,XVATYP,XVASTT,XVCCOD:
                                XVCTYP,XVCLID,XNCNAM,XVVISN,XNRCNC,XVUNIT
.
          TRAPCLR   FORMAT                                             *I-90204
.
          GOTO      PROC9000 IF OVER        * Check for EOF
          BRANCH    DATAERR,PROC6000        * Get next record          *I-90204
.
          ADD       ONE,COUNTIN
          ADD       ONE,COUNT
          ASSIGN    COUNT % 100,DISPVAR
          IF        DISPVAR = 0
            DISPLAY   *P14:18,*V2LON,XVURNO:
.                     *P14:19,*V2LON,XLCATG:
.                     *P14:20,*V2LON,XLCODE:
                      *P14:21,COUNT;
          ENDIF
.
          MOVE      TWO,WHICHPAS
.
          CALL      UPBB0000                * Add to Events & Visit Tables
          GOTO      PROC6000
.
PROC9000  PRINT     *1,"|",*37,"|",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTIN:
                    " 'convoutp.txt' records read",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTEDE:
                    " Patient Events deleted",*132,"|":
                    *N,*1,"|",*37,"|",*50,COUNTVDE:
                    " Visit records deleted",*132,"|"
.
PROC9999  MOVE      ZERO,OVRCD
          CLOCK     TIME,CTIMEIS
          CALL      UND132
          PRINT     *N,*N,"**** End of Report ****";
.
          DISPLAY   *P20:21,*V2LON,COUNT;
          KEYIN     *P40:16,"Finished : ",*V2LON,*DV,CTIMEIS:
                    *P1:24,*EL,*HOFF,"No more records.  Tap ",*V2LON,"<ENTER>":
                    *HOFF," to exit ... ",*EOFF,ANS;
          RETURN
+
.**************************************************************************
.*                               UPAA0000              Called by: PROC0000*
.*              Write Allied Health Ref data to allrefaf and patvisaf     *
.**************************************************************************
.
UPAA0000  MOVE      XEVISN,KEY8                                    
          CALL      RDALREF1                * Check if this record exists
.
          IF        OVRCD = 0
            CALL      DEALREF1              * Delete AHR record
            ADD       ONE,COUNTADE
          ENDIF
.
. ------- Check for an existing Visit Details & Extension -------
.
          MOVE      XEVISN,PVIBILL
          MOVE      PVIBILL,KEY8
          CALL      RDVISA1                 * read the patient visit file
.                                           
          IF        OVRCD = 0
            CALL      DEVISA1               * Delete Visit Record
            ADD       ONE,COUNTVDE
            CALL      DEPMVX11
          ENDIF
.
UPAA9999  RETURN
+
.**************************************************************************
.*                               UPBB0000              Called by: PROC0000*
.*              Delete Outpatients record from PMSEVTAF and PATVISAF      *
.**************************************************************************
.
UPBB0000  MOVE      XVVISN,KEY8     
          CALL      RDPMEVT1                * Read the Event Record
 
          IF        OVRCD = 0
            CALL      DEPMEVT1
            ADD       ONE,COUNTEDE
          ENDIF
.
. ------- Check for an existing Visit Details & Extension -------
.
          MOVE      XVVISN,PVIBILL
          MOVE      PVIBILL,KEY8
          CALL      RDVISA1                 * read the patient visit file
.
          IF        OVRCD = 0
            CALL      DEVISA1               * Delete Visit Record
            ADD       ONE,COUNTVDE
            CALL      DEPMVX11
          ENDIF
.
UPBB9999  RETURN
+
.******************************************************************************
.*                                     FERR0000                               *
.*            Print TRAP data errors in "convoutp.txt" & "convahrf.txt"        *
.******************************************************************************
.
FERR0000  MOVE      ONE,DATAERR
          PRINT     *1,"|",XEURNO," Visit ",XEVISN,*37,"| ":
                    "Data error occurred in this record - Record No. :":
                    COUNTIN,"   - Record bypassed",*132,"|"
          READ      AHRFPD,SEQ;XEVISN       * Bypass rest of record
          GOTO      FERR9999
.
FERR2000  MOVE      ONE,DATAERR
          PRINT     *1,"|",XVURNO," Event ",XVEVNT,*37,"| ":
                    "Data error occurred in this record - Record No. :":
                    COUNTIN,"   - Record bypassed",*132,"|"
          READ      OUTPATD,SEQ;XVURNO      * Bypass rest of record
.
FERR9999  RETURN
+
.******************************************************************************
.*                                     PRNT0000                               *
.*                                   Print A Line                             *
.******************************************************************************
PRNT0000  ADD       ONE,CLNO
.
          IF        CLNO>=58
            CALL      HEAD0000              * print the page header
          ENDIF
.
          PRINT     *1,"|",PRTPT1,*37,"|",PRTPT2,*132,"|"
.
PRNT9999  RETURN
.
.******************************************************************************
.*                                    HEAD0000                                *
.*                  Routine to print the error report page header             *
.******************************************************************************
HEAD0000  CALL      HEAD132                 * print the page header
.
          CALL      UND132
          PRINT     *1,"|    U/R No.    Date      Referral  | Error Message":
                    *132,"|"
          CALL      UND132
.       
          MOVE      ONE,CLNO
.
HEAD9999  RETURN
.
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATMA1IO/INC                 * patient master file
          INC       PATVISIO/INC                 * Visit Details
          INC       PMSVX1IO/INC                 * Visit Extension
          INC       PMSEVTIO/INC                 * Patient Event file
          INC       PATDO1IO/INC                 * Patient Doctor Table
          INC       PMSHCPIO/INC                 * Patient Healthcare Profess
          INC       PATCODIO/INC                 * Patient Codes Table
          INC       ALLREFIO/INC                 * Allied Health Referral 
          INC       OUTCTYIO/INC                 * Outpatients Clinic Type
          INC       OUTCLIIO/INC                 * Outpatients Clinic Id
          INC       OUTSITIO/INC                 * Outpatients Site Table :q

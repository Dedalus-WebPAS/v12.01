.*****************************************************************************
.* System    :   Allied Health                                               *
.* Program   :   FX301523                                                    *
.* Desc      :   Fix invalid hospital codes in allrefaf                      *
.*****************************************************************************
.* Date      :   28/05/2014                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will loop through allrefaf looking for         *
.*               hospital codes of "~~~" and if found, update them with      *
.*               the corresponding pmvxmhos field if populated with a valid  *
.*               code.                                                       *
.* Mods      :                                                               *
.*        V10.05.00 10/10/2014  Ebon Clements    CAR 301523                  *
.*                  Ported program to V10.04                                 *
.*        V10.03.00 02/06/2014  Steve Armstrong  CAR 301523                  *
.*                  Program created.                                         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLCONFD/INC
          INC       ALLREFFD/INC
          INC       PATHSPFD/INC
          INC       PMSVX1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      8
.
.
. PROGRAM CONSTANTS
. -----------------
TILDA3    INIT      "~~~"
.
.
.
PRGID     INIT      "FX301523"
PRGNAM    INIT      "Fixit for allrefaf hospital codes"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"allrefaf";
          OPEN      ALLREFA1,"allrefaf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*        Check allrefaf for hospital codes of "~~~" and replace with the    *
.*        corresponding pmvxmhos value if populated and valid.               *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,COUNTER                 * initialise update count
          DISPLAY   *P1:24,*EL,"Processing:";
.
          MOVE      SP8,KEY8
          CALL      RSALREF1                     * position at start of file
PROC0500  CALL      RKALREF1                     * read next record
          BRANCH    OVRCD,PROC9000
.
          DISPLAY   *P13:24,*EL,*V2LON,ALREVISN;
.
          MATCH     TILDA3,ALREHOSN              * tilda filled hospital field ?
          GOTO      PROC0500 IF NOT EQUAL        * no - ignore record
.
          MOVE      ALREVISN,KEY8
          CALL      RDPMVX11                     * visit extension found ?
          BRANCH    OVRCD,PROC0500               * no - ignore record
.
          MATCH     SP3,PMVXMHOS                 * hospital code blank ?
          GOTO      PROC0500 IF EQUAL            * yes - ignore record
.
          MOVE      PMVXMHOS,KEY3
          CALL      RDPTHSP1                     * valid hospital code ?
          BRANCH    OVRCD,PROC0500               * no - ignore record
.
          MOVE      PMVXMHOS,ALREHOSN            * yes - load hospital code
          CALL      UPALREF1
          ADD       ONE,COUNTER                  * increment counter
          GOTO      PROC0500                     * get next referral record
.
PROC9000  DISPLAY   *P1:23,*EF,"Fixit completed.":
                    *P1:24,*V2LON,COUNTER,*HOFF," records updated.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       ALLREFIO/INC
          INC       PATHSPIO/INC
          INC       PMSVX1IO/INC

.******************************************************************************
.* System     : WAITING LIST                                                  *
.* Program    : IBAWAT27                                                      *
.* Description: Short Notice Report                                           *
.* Function   : List of unscheduled patients who are available on short notice*
.******************************************************************************
.* Author     : N.S.                                                          *
.* Date       : 31/10/1988                                                    *
.* Comments   :                                                               *
.* Mods.      :                                                               *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*        V10.05.01 05/01/2015  Ebon Clements CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V9.04.01  01/03/2005  Peter Vela    CAR 57547                       *
.*                  Recompiled for PATVADFD and PATVADIO                      *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V9.00.01  05/07/2001  Dean Cramer                                   *
.*                  Fix ALL option for Doctors                                *
.*        V5.08.02  01/09/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01  30/12/1999  Steve Armstrong                              *
.*                   Fixed global recompile bugs                              *
.******************************************************************************
.*        V5.07.01  16/08/1999 J.Tan                                          *
.*                  Added transfer destination description                    *
.*                  22/10/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
.*        V5.04.02  25/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.*        V5.04.01  18/12/1996  Howellsy         SRF 642436                   *
.*                  Use WATCNTCN to derive the number of cancellations        *
.******************************************************************************
.*              V5.01     03/06/89 S.Barcham         SRF 100809               *
.*                        Recompiled for locking in PATIOMAS                  *
.*              V5.02     10/06/89 M.Ohleiter        SRF 101030               *
.*                        Fixed displays                                      *
.*              V5.03     15/10/89 M.Ohleiter        SRF 100926               *
.*                        Included '?' on doctor, unit and short notice codes *
.*              V5.01.01  19/06/89 M.Ohleiter                                 *
.*                        Changes for new release 5.01                        *
.*                        09/07/89 J.Clark                                    *
.*                        Added Key26 variable added to PATIODOC by SOG       *
.*              V5.01.10  01/08/90 Justin "I Pooch Male Dogs" Coates          *
.*                        Standardize format of code                          *
.*                        Modify according to new specs                       *
.*                        27/08/90 Bill Dew                                   *
.*                        Fix Over due calculation                            *
.*              V5.01.121 19/10/90 DIG / i chung (13/11/90)                   *
.*                        Recompile for WATCATIO.INC - incorrect Keys size    *
.*            V5.01.122 27/11/90 Glen Hobby                                   *
.*                      Recompiled for changes to                             *
.*                      PATMX1FD                                              *
.*            V5.01.123   10/06/92 Steve Armstrong        SRF 115060          *
.*                        Removed debug from code                             *
.*        V5.01.124 02.03.93 Neeriem "I Hate Support" Dye (I.B.A.)            *
.*                  Reformat print for new address lengths                    *
.*            V5.01.125   13/12/93  Rob Leonard  SRF 440557                   *
.*                        Bug fix printing of public/private indicator        * 
.*        V5.01.126 26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD                * common variables
          INC       IBASEQFD/INC
          INC       PATCOMM/INC             * for 'gperd'
.
          INC       PATCODFD/INC            * codes file
          INC       PATCONFD/INC            * patient control file
          INC       PATDO1FD/INC            * doctors file
          INC       PATDRGFD/INC            * date range file for gperd
          INC       PATMA1FD/INC            * master file
          INC       TFILEVAR/INC
.
          INC       WATCATFD/INC            * WL category change file
          INC       WATCONFD/INC            * WL control file
          INC       WATRTDFD/INC            * Transfer destination file
          INC       WATTR1FD/INC            * treatment file
          INC       WATWTAFD/INC            * WL movement analysis file
          INC       WEBERRFD/INC
.
. ------ program variables ------
.
COL       FORM      2
CMDLINE   DIM       50                      * line for execute command
CODE      DIM       2                       * for patdscod
CURDAYS   FORM      3                       * day of current year
CURYR     FORM      2                       * current year
CURCC     FORM      2                       * current year
CURLEAP   FORM      1                       * leap year indicator
.
DATE10    DIM       10                      * wmdate1 (dd/mm/ccyy)
DATE20    DIM       10                      * wmdate4 (dd/mm/ccyy)
DATE40    DIM       10                      * d.o.b.  (dd/mm/ccyy)
DAYSODUE  FORM      3                       * days been overdue
DESCNOTC  DIM       30                      * description for short notice     
DESCOPT   DIM       30                      * option description in heading
DESCUNIT  DIM       20                      * descr unit (default = ALL)
DIM3      DIM       3
DIM3A     DIM       3                       * shortened stats variables
DIM3B     DIM       3                       * shortened stats variables
DIM3C     DIM       3                       * shortened stats variables
DOCNAM    DIM       30                      * descr doctor (default = ALL)
DOCTOR    DIM       6                       * key in doctor ( default is ALL )
.
FNAME1    DIM       8                       * file name
FROMDATE  DIM       8                       * FROMdate ccyymmdd
HOSP3     FORM      3
LIMIT     FORM      5                       * appropriate limit overdue
NOTICE    DIM       3                       * short notice code keyed in
SRTKEY    DIM       30                      * which key is being used
OTH3      FORM      3
PAT3      FORM      3
QUPER     FORM      1                       * ? performed flag
TODATE    DIM       8                       * TOdate ccyymmdd
UNIT      DIM       3                       * key in unit ( default is ALL )
VERT      FORM      2
.
WAITDAYS  FORM      3                       * days been waiting
WLDAYS    FORM      3                       * day of waiting list year
WLYR      FORM      2                       * waiting list year
WLCC      FORM      2
WLLEAP    FORM      1                       * waiting list leap year indicator
WMDESC31  DIM       31                      * procedure to write to file
.
.
KEY21OR   DIM       21                      * values for book. canc original
KEY21NE   DIM       21                      * values for book. canc read in 
.
. ------ print variables ------
.
CNTPATCL  FORM      4                       * prog count
CNTPATTL  FORM      5                       * total count
.
DIM14     DIM       14                      * doct part one for print
DIM26     DIM       26                      * unit part two for print
DIM31     DIM       31                      * doct part two for print
DOCCODE   DIM        6
.
NEEDNP    FORM      "48"                    * when need a new page b4 table
PRINTLIM  FORM      "55"                    * when need a new page
PREVTYPE  DIM       8                       * used when new clin etc needed
.
ADDRESS   DIM       50                      * print address
PRCANC    DIM       16                      * print cancellation reason
PRCLIN    DIM       23                      * print unit/clinic
PRDESC    DIM       30                      * print procedure description
PRDOCT    DIM       30                      * print doctor
PRFROM    DIM       42                      * print from date
PRTO      DIM       42                      * print to date
PNAME     DIM       50                      * print name
PRPRTY    DIM       11                      * print priority description
PRPUBPRI  DIM       9                       * public or private
PRMETCOU  DIM       13                      * metropolition or country
PRSEX     DIM       8                       * print sex male  /female  *C-49016
PRSHORT   DIM       45                      * short notice indicator
PRSNDESC  DIM       23                      * short notice description
PRUNDO1   DIM       45                      * current unit/doct
PRUNDO2   DIM       45                      * unit/doct code entered
.
. ------ program constants ------
.
ALL       INIT      "All"
CATA      INIT      "A "       * category A  (patient classific'n)
CATBC     INIT      "BC"       * category BC (booking cancellation)
CATTP     INIT      "TP"       * category TP (priority)
CATWS     INIT      "WS"       * catagory WS (short notice)
CATWU     INIT      "WU"       * catagory WU (unit)
CATW2     INIT      "W2"       * catagory W2 (user defined 2)
DASH      INIT      " - "
DESCOPT1  INIT      "Unit/Clinic Code Sequence"
DESCOPT2  INIT      "Doctor Code Sequence     "
FN1       INIT      "watemp"                * temp file prefix
PREFDOCT  INIT      "Doctor Code : "
PREFSHRT  INIT      "Short Notice Code : "
PREFUNIT  INIT      "Unit/Clinic Code : "
PREFFROM  INIT      "From Date 0n List : "
PREFTO    INIT      "To   Date 0n List : "
SORT1     INIT      "U59-61,1-8,9-17,18-19"
SORT2     INIT      "U68-73,1-8,9-17,18-19"
WATEMP    IFILE     SQL, FIXED=316, KEY=SRTKEY
Z8        INIT      "zzzzzzzz"
.
.
XWTWMHOS  FORM      3
XWTWMPAT  FORM      3
XWTWMOTH  FORM      3
.
PRGID     INIT      "IBAWAT27"
PRGNAM    INIT      "Short Notice Report"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                  MAINLINE                                              *
.*        Controlling Logic                                               *
.**************************************************************************
ML0000
          CALL      INIT0000                * open files     
.
ML0100    CALL      OPTN0000                * select options
          BRANCH    EXIT,ML9999             * exit chosen
.
. ---- By Unit or Doctor sequence -----
.
.
ML1000    LOAD      DESCOPT,MOPTION,DESCOPT1,DESCOPT2
          LOAD      CPHDROPT,MOPTION,DESCOPT1,DESCOPT2
          DISPLAY   *P50:2,*V2LON,"- ",*+,DESCOPT,SP1
.
          CALL      KDAT0000                * keyin from/to date
          BRANCH    EXIT,ML0100             * no from date entered
.
          CALL      CODE0000                * keyin unit code
          CALL      KSNC0000                * keyin short notice code
.
          CALL      CONTQST                 * ok to continue ?
          BRANCH    CEXIT,ML2000,ML1000,ML0100
.                          Yes    No     Cancel
.
ML2000    CALL      TEMP0000                * create tempfile
.
. ------ produce the report ------
.
          MOVE      ZERO,EXIT
          CALL      PROC0000                * read wattrefd
          BRANCH    EXIT,ML0100             * empty tempfile
.
. ------ print the report ------
.
          CALL      PRTT0000                * print
          GOTO      ML0100
.
ML9999    CHAIN     PGM
          STOP
+
.***************************************************************************
.*                  INIT0000                                               *
.*        Initialisation , opening files                                   *
.***************************************************************************
INIT0000  CALL      DISPHEAD
          CALL      IBACLOCK                * get current date
.
          DISPLAY   *P56:24,"Opening wattr1af";
          OPEN      WATTR1A1,"wattr1af"     * WL treatment file
          OPEN      WATTR1A2,"wattr1af"
          OPEN      WATTR1A3,"wattr1af"
          DISPLAY   *P64:24,"watcataf";
          OPEN      WATCATA1,"watcataf"     * WL category change file
          OPEN      WATCATA2,"watcataf"     * WL category change file
          DISPLAY   *P64:24,"watwtaaf";
          OPEN      WATWTAA1,"watwtaaf"     * WL movement analysis file
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"     * master file
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"     * master extension file
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"     * doctors file
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"     * codes file
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"     * control file
.
          READ      CONTROLF,THIRTY7;*173,WTCNURDY,WTCNSUDY,WTCNRODY
          READ      CONTROLF,FIFTY9;*220,PTCNUADT
          MOVE      ONE,CNEWDS
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,FNAME1
.
INIT9999  RETURN
+
.**************************************************************************
.*                  OPTN0000                     Called by : ML0000       *
.*        Display the options to the user                                 *
.**************************************************************************
OPTN0000  HLINE     *G37,2,50,80            * clear subheading
          CALL       SCRO0000               * display main optionscreen
          DISPLAY   *P1:8,"Select Option : ",*EL;
.
OPTN1000  KEYIN     *P17:8,*DV,UNDLN1:
                    *P17:8,*V2LON,MOPTION:
                    *P17:8,*DV,MOPTION
.
          COMPARE   ZERO,MOPTION            * was exit selected ??
          GOTO      OPTN9000 IF EQUAL       * yes
.
          MOVE      FALSE,EXIT              * set exit flag
          BRANCH    MOPTION,OPTN9999,OPTN9999
.
          BEEP
          GOTO      OPTN1000                * invalid response
.
OPTN9000  MOVE      TRUE,EXIT               * exit chosen
.
OPTN9999  RETURN
+
.******************************************************************************
.*                  SCRO0000                     Called by : OPTN0000         *
.*          Main option screen                                                *
.******************************************************************************
SCRO0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = ",DESCOPT1:
                    *P1:6,*V2LON,TWO,*HOFF," = ",DESCOPT2
SCRO9999  RETURN
+
.******************************************************************************
.*                  KDAT0000                     Called bu : ML0000           *
.*        Keyin from / to date                                                *
.*        fromdate < date on list = wmdate1 < todate                          *
.******************************************************************************
KDAT0000  DISPLAY   *P1:9,*EF:
                    *P1:10,"From Date on List : ":
                    *P1:11,"To   Date on List : "
.
. ------ enter the FROM date ------
.
KDAT1000  UNPACK    SP6,CYEAR,CMON,CDAY     * no default
          MOVE      "21",CCOL               * column for input
          MOVE      TEN,CVERT               * row for input
          MOVE      CCC,CCENT               * default to current cent
.
          CALL      KEYDATE                 * enter the date
          BRANCH    OVRCD,KDAT9000          * nothing entered
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE           * zero fill
          CALL      PACDATE                 * format the date
          PACK      PRFROM,PREFFROM,CPCDATE * set up display print value
.
. ------ enter the TO date ------
.
KDAT2000  UNPACK    SP6 INTO CYEAR,CMON,CDAY
          MOVE      "21",CCOL               * column for input
          MOVE      TEN1,CVERT              * row for input
          MOVE      CCC,CCENT               * default to current century
.
          CALL      KEYDATE                 * enter date
          BRANCH    OVRCD,KDAT1000          * nothing entered, reenter FROM
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
          CALL      PACDATE                 * format the date
.
. ------ see if to date is after from date ------
.
          MATCH     FROMDATE,TODATE
          GOTO      KDAT8000 IF LESS
.
          PACK      PRTO,PREFTO,CPCDATE     * set up print value
          MOVE      FALSE,EXIT              * set exit status
          GOTO      KDAT9999
.
. ------ to date is before from date ------
.
KDAT8000  DISPLAY   *P1:24,*B,"TO date must be after FROM date.  ",*EL;
          CALL      HITENTER
          GOTO      KDAT2000
.
KDAT9000  MOVE      TRUE,EXIT
.
KDAT9999  RETURN
+
.**************************************************************************
.*                  CODE0000                     Called by : ML0000       *
.*        Keyin unit (option1) or doctor (option 2)                       *
.*        Returns : UNIT          the unit entered or,                    *
.*                  DOCTOR        the doctor code entered, or             *
.**************************************************************************
CODE0000  BRANCH    MOPTION,CODE1000,CODE2000
.
CODE1000  CALL      KUCC0000                * enter the unit/clinic code
          GOTO      CODE9999
.
CODE2000  CALL      KDOC0000                * enter the doctors code
          GOTO      CODE9999
.
CODE9999  RETURN
+
.*********************************************************************
.*                  KUCC0000                    Called by : UNCL0000 *
.*        Keyin the Unit/Clinic Code                                 *
.*        Returns : UNIT          the unit/clinic code               *
.*                  DESCUNIT      the unit/clinic description        *
.*********************************************************************
KUCC0000  MOVE      ONE,QUPER               * ? not performed
          MOVE      TEN3,CVERT              * row for input
.
          DISPLAY   *P1:13,"Unit/Clinic Code : "
.
KUCC1000  KEYIN     *P20:CVERT,*DV,UNDLN3:
                    *P20:CVERT,*V2LON,UNIT:
                    *P20:CVERT,*DV,UNIT
.
          ENDSET    UNIT
          APPEND    SP3,UNIT
          RESET     UNIT
.
          MATCH     SP3,UNIT                * was anything entered ?
          GOTO      KUCC4000 IF EQUAL       * no
.
          MATCH     QUEST,UNIT              * was ? entered ?
          GOTO      KUCC2000 IF NOT EQUAL   * no
.
. ------ ? entered ------
.
          PACK      CODE,CATWU              * set up category for codes file
          CALL      PATCODDS                * ? routine
          MOVE      ZERO,QUPER              * set ? as performed
          MOVE      "24",CVERT              * input on line 24
          PACK      UNIT,SP3                * clear the code
          DISPLAY   *P1:24,"Unit/Clinic Code : ",*EL
          GOTO      KUCC1000
.
. ------ validate what was entered ------
.
KUCC2000  PACK      KEY5,CATWU,UNIT         * key = cat and value entered
          CALL      RDCODE1                 * read codes file
          BRANCH    OVRCD,KUCC3000          * invalid entry
.
          MOVE      TDESC,DESCUNIT          * set up description
          CLEAR     PRUNDO2                 * clear the heading variable
          APPEND    "Unit/Clinic Code  : ",PRUNDO2
          APPEND    UNIT,PRUNDO2
          APPEND    DASH,PRUNDO2
          APPEND    DESCUNIT,PRUNDO2
          RESET     PRUNDO2
          GOTO      KUCC5000
.
. ------ ERROR: invalid code entered ------
.
KUCC3000  DISPLAY   *P1:24,*B,"Invalid Unit/Clinic Code entered.  ",*EL;
          CALL      HITENTER
          PACK      UNIT,SP3                * clear the code
.
          BRANCH    QUPER,KUCC1000          * ? not entered
.
          DISPLAY   *P1:24,"Unit/Clinic Code : ",*EL
          GOTO      KUCC1000
.
. ------ only enter hit ------
.
KUCC4000  BRANCH    QUPER,KUCC4500
          CALL      RDIS0000                * redisplay the screen
          GOTO      KUCC0000
.
KUCC4500  PACK      UNIT,ALL,SP1            * set up default value
          PACK      DESCUNIT,SP20           * clear description
          CLEAR     PRUNDO2                 * clear the heading variable
          APPEND    "Unit/Clinic Code  : ",PRUNDO2
          APPEND    UNIT,PRUNDO2
          APPEND    SP30,PRUNDO2
          RESET     PRUNDO2
.
. ------ valid entry ------
.
KUCC5000  BRANCH    QUPER,KUCC8000          * ? not entered
.
          CALL      RDIS0000                * redisplay the screen
          DISPLAY   *P1:13,"Unit/Clinic Code : "
.
KUCC8000  DISPLAY   *P20:13,*V2LON,UNIT,*P40:13,*HOFF,DESCUNIT
.
KUCC9999  RETURN
+
.*********************************************************************
.*                  KDOC0000                    Called by : CODE0000 *
.*        Keyin the Doctors Code                                     *
.*        Returns : DOCTOR        the doctor code                    *
.*                  DOCNAM        the doctor description             *
.*********************************************************************
KDOC0000  MOVE      TEN3,CVERT              * row for input
.
          DISPLAY   *P1:13,"Doctor Code : "
.
          MOVE      "15",ECOL
          MOVE      TEN3,EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          MATCH     SP6,DCODE
          GOTO      KDOC7000 IF EQUAL 
.
          BRANCH    EXIT,KDOC0000,KDOC0000
.
          MOVE      DCODE,DOCTOR
.
. ------ validate what was entered ------
.
          PACK      KEY6,DOCTOR             * key = cat and value entered
          CALL      FDOC0000                * format docs name
          PACK      DOCNAM,PACFNAME         * store the name
          CLEAR     PRUNDO2                 * clear the heading variable
          APPEND    "Doctor Code       : ",PRUNDO2
          APPEND    DOCTOR,PRUNDO2
          APPEND    DASH,PRUNDO2
          APPEND    DOCNAM,PRUNDO2
          RESET     PRUNDO2
          GOTO      KDOC8000
.
KDOC7000  MOVE      ALL,DOCTOR
          MOVE      SP70,DOCNAM 
.
KDOC8000  DISPLAY   *P15:13,*V2LON,DOCTOR,*P40:13,*HOFF,DOCNAM
          GOTO      KDOC9999
.
KDOC9999  RETURN
+
.*********************************************************************
.*                  KSNC0000                    Called by : ML0000   *
.*        Keyin the Short Notice Code                                *
.*        Returns : NOTICE        the short notice code              *
.*                  DESCNOTC      the chort notice description       *
.*********************************************************************
KSNC0000  MOVE      ONE,QUPER               * ? not performed
          MOVE      TEN5,CVERT              * row for input
.
          DISPLAY   *P1:15,"Short Notice Code : "
.
KSNC1000  KEYIN     *P21:CVERT,*DV,UNDLN3:
                    *P21:CVERT,*V2LON,NOTICE:
                    *P21:CVERT,*DV,NOTICE
.
          ENDSET    NOTICE
          APPEND    SP3,NOTICE
          RESET     NOTICE
.
          MATCH     SP3,NOTICE              * was anything entered ?
          GOTO      KSNC4000 IF EQUAL       * no
.
          MATCH     QUEST,NOTICE            * was ? entered ?
          GOTO      KSNC2000 IF NOT EQUAL   * no
.
. ------ ? entered ------
.
          PACK      CODE,CATWS              * set up category for codes file
          CALL      PATCODDS                * ? routine
          MOVE      ZERO,QUPER              * set ? as performed
          MOVE      "24",CVERT              * input on line 24
          PACK      NOTICE,SP3                * clear the code
          DISPLAY   *P1:24,"Short Notice Code : ",*EL
          GOTO      KSNC1000
.
. ------ validate what was entered ------
.
KSNC2000  PACK      KEY5,CATWS,NOTICE       * key = cat and value entered
          CALL      RDCODE1                 * read codes file
          BRANCH    OVRCD,KSNC3000          * invalid entry
.
          MOVE      TDESC,DESCNOTC          * set up description
          PACK      PRSHORT,PREFSHRT,NOTICE,DASH,TDESC * set up description
          RESET     PRSHORT
          GOTO      KSNC5000
.
. ------ ERROR: invalid code entered ------
.
KSNC3000  DISPLAY   *P1:24,*B,"Invalid Short Notice Code entered.  ",*EL;
          CALL      HITENTER
          PACK      NOTICE,SP3                * clear the code
.
          BRANCH    QUPER,KSNC1000          * ? not entered
.
          DISPLAY   *P1:24,"Short Notice Code : ",*EL
          GOTO      KSNC1000
.
. ------ only enter hit ------
.
KSNC4000  PACK      NOTICE,ALL,SP1            * set up default value
          PACK      DESCNOTC,SP20           * clear description
          PACK      PRSHORT,PREFSHRT,ALL,SP30
          RESET     PRSHORT
.
. ------ valid entry ------
.
KSNC5000  BRANCH    QUPER,KSNC8000          * ? not entered
.
          CALL      RDIS0000                * redisplay the screen
.
          BRANCH    MOPTION,KSNC5100,KSNC5200
.
KSNC5100  DISPLAY   *P1:13,"Unit/Clinic Code : ",*V2LON,UNIT,*HOFF,*P40:13,DESCUNIT,*EL
          GOTO      KSNC7000
.
KSNC5200  DISPLAY   *P1:13,"Doctor Code : ",*V2LON,DOCTOR,*HOFF,*P40:13,DOCNAM,*EL
.
KSNC7000  DISPLAY   *P1:15,"Short Notice Code : "
.
KSNC8000  DISPLAY   *P21:15,*V2LON,NOTICE,*P40:15,*HOFF,DESCNOTC,*EL
.
KSNC9999  RETURN
+
.**************************************************************************
.*                  RDIS0000                                              *
.*        Redisplay after "?" option                                      *
.**************************************************************************
RDIS0000  CALL       SCRO0000
          DISPLAY    *P1:8,*HOFF,"Select Option : ",*V2LON,MOPTION,*HOFF:
                     *P1:10,"From Date on List : ":
                     *P1:11,"To   Date on List : "
.
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P21:10,*V2LON,CPCDATE
.
          UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P21:11,*V2LON,CPCDATE
.
RDIS9999  RETURN
+
.****************************************************************************
.*                  PROC0000                     Called by : ML0000         *
.*        Read WATTR1FD only patients with treatment status =1 (wmstat)     *
.*        and on short notice                                               *
.*        Option 1 (=By unit )                                              *
.*        Option 2 (=By doctor)                                             *
.****************************************************************************
PROC0000  PACK      KEY27,FROMDATE,SP20     * key for positional read
          CALL      RDSTREA3                * positional read
          DISPLAY   *P1:24,"Obtaining the valid entries now.",*EL
.
. ------ next read on treatment file ------
.
PROC4000  CALL      RDKTREA3                * next read
          BRANCH    OVRCD TO PROC9999       * at end of search
.
          DISPLAY   *P70:24,WMURNO,*EL
.
          COMPARE   ONE,WMSTAT              * is the status one ??
          GOTO      PROC4000 IF NOT EQUAL   * status is not 1, read next record
.
          MATCH     SP3,WMNOTICE            * is there as short notice code ??
          GOTO      PROC4000 IF EQUAL       * no, so dont print patient
.
          MATCH     WMDATE1,TODATE          * is date before to date ??
          GOTO      PROC9999 IF LESS        * no, date3 > todate therefore done
.
          PACK      KEY8,WMURNO             * UR number as key
          CALL      RDMAST1                 * read master file
          BRANCH    OVRCD,PROC4000          * invalid UR so ignore
.
. ------ decide which option we are using for extra checks ------
.
          BRANCH    MOPTION,PROC4100,PROC4200
.
. ------ check on unit/clinic code ------
.
PROC4100  MATCH     ALL,UNIT
          GOTO      PROC4300 IF EQUAL       * print all units
.
          MATCH     UNIT,WMUNIT
          GOTO      PROC4000 IF NOT EQUAL   * unit not right
.
          GOTO      PROC4300                * a valid unit
.
. ------ check on doctors code ------
.
PROC4200  MATCH     ALL,DOCTOR 
          GOTO      PROC4300 IF EQUAL       * print all doctors      
.
          MATCH     DOCTOR,WMDOCTOR
          GOTO      PROC4000 IF NOT EQUAL   * doctor not right
.
          GOTO      PROC4300                * a valid doctors code
.
. ------ check on short notice code ------
.
PROC4300  MATCH     ALL,NOTICE              * are we using all codes ??
          GOTO      PROC4700 IF EQUAL       * yes, print all codes
.
          MATCH     NOTICE,WMNOTICE         * is it the correct code ??
          GOTO      PROC4000 IF NOT EQUAL   * code not correct
.
. ------ continue to obtain data ------
.
PROC4700  CALL      WATCNTCN                * obtain booking canc. stats
          MOVE      XWTWMHOS,WTWTHOSP
          MOVE      XWTWMPAT,WTWTPAT
          MOVE      XWTWMOTH,WTWTOTH
.
. ------ write the temp file record ------
.
PROC5000  BRANCH    MOPTION,PROC5005,PROC5010
.
. ------ write using unit/clinic code key ------
.
PROC5005  PACK      KEY22,WMUNIT,WMURNO,WMCODE,DWMCNT
          RESET     KEY22
          MOVE      WMDESC,WMDESC31         * shorten desc
          MOVE      WTWTHOSP,HOSP3          * make a form3
          MOVE      HOSP3,DIM3A             * make a dim3
          MOVE      WTWTPAT,PAT3
          MOVE      PAT3,DIM3B              * make a dim3
          MOVE      WTWTOTH,OTH3
          MOVE      OTH3,DIM3C              * make a dim3
          WRITE     WATEMP,KEY22;WMURNO,WMCODE,DWMCNT,WMDESC31,WMDATE1,WMUNIT:
                                 WMNOTICE,WMPTY,WMDOCTOR,WMSTAY,WMTIME,WMREMOVE:
                                 WMDATE4,WMUDEF2,WTWMDEDA,DIM3A,DIM3B,DIM3C:
                                 WMPCAT
.
          DISPLAY   *P70:24,*V2LON,WMURNO,*EL
          GOTO      PROC4000
.
. ------ write using doctor code key ------
.
PROC5010  PACK      KEY25,WMDOCTOR,WMURNO,WMCODE,DWMCNT
          RESET     KEY25
          MOVE      WMDESC,WMDESC31         * shorten desc
          MOVE      WTWTHOSP,HOSP3          * make a form3
          MOVE      HOSP3,DIM3A             * make a dim3
          MOVE      WTWTPAT,PAT3            * make a form3
          MOVE      PAT3,DIM3B              * make a dim3
          MOVE      WTWTOTH,OTH3            * make a form3
          MOVE      OTH3,DIM3C              * make a dim3
          WRITE     WATEMP,KEY25;WMURNO,WMCODE,DWMCNT,WMDESC31,WMDATE1,WMUNIT:
                                 WMNOTICE,WMPTY,WMDOCTOR,WMSTAY,WMTIME,WMREMOVE:
                                 WMDATE4,WMUDEF2,WTWMDEDA,DIM3A,DIM3B,DIM3C:
                                 WMPCAT
.
          DISPLAY   *P70:24,*V2LON,WMURNO,*EL
          GOTO      PROC4000
.
PROC9999  RETURN
+
.****************************************************************************
.*                  PRTT0000                                                 *
.*        Produce the report                                                *
.****************************************************************************
PRTT0000  MOVE      ZERO,CNTPATTL          * clear total count
          MOVE      ZERO,CNTPATCL          * clear progressive count
          MOVE      ZERO,CPAGENO           * initialize page counter
          CALL      IBACLOCK               * get current date and time
          MOVE      "SHORT NOTICE REPORT -",PRGNAM  * set up report name
          DISPLAY   *P1:24,"Producing the report now.",*EL
.
. ------ do the initial read on the temp file ------
.
PRTT1000  BRANCH    MOPTION,PRTT1100,PRTT1200
.
. ------ initial read on unit/clinic index ------
.
PRTT1100  PACK      KEY22,SP30
          READ      WATEMP,KEY22;;          * positional read
          READKS    WATEMP;WMURNO,WMCODE,DWMCNT,WMDESC31,WMDATE1,WMUNIT:
                           WMNOTICE,WMPTY,WMDOCTOR,WMSTAY,WMTIME,WMREMOVE:
                           WMDATE4,WMUDEF2,WTWMDEDA,DIM3A,DIM3B,DIM3C:
                           WMPCAT
          GOTO      PRTT7000 IF OVER
          MOVE      DWMCNT,WMCNT
          MOVE      DIM3A,WTWTHOSP
          MOVE      DIM3B,WTWTPAT
          MOVE      DIM3C,WTWTOTH
.
. ------ set up the first units name ------
.
          PACK      KEY5,CATWU,WMUNIT       * unit as the key
          CALL      RDCODE1                 * read codes file
          PACK      PRUNDO1,PREFUNIT,WMUNIT,DASH,TDESC
          PACK      PREVTYPE,WMUNIT,SP3     * set up previous unit
          GOTO      PRTT2000
.
. ------ initial read on doctor code index ------
.
PRTT1200  PACK      KEY25,SP20,SP5
          READ      WATEMP,KEY25;;          * positional read
          READKS    WATEMP;WMURNO,WMCODE,DWMCNT,WMDESC31,WMDATE1,WMUNIT:
                           WMNOTICE,WMPTY,WMDOCTOR,WMSTAY,WMTIME,WMREMOVE:
                           WMDATE4,WMUDEF2,WTWMDEDA,DIM3A,DIM3B,DIM3C:
                           WMPCAT
          GOTO      PRTT7000 IF OVER
          MOVE      DWMCNT,WMCNT
          MOVE      DIM3A,WTWTHOSP
          MOVE      DIM3B,WTWTPAT
          MOVE      DIM3C,WTWTOTH
.
. ------ set up the first docts name ------
.
          PACK      KEY6,WMDOCTOR           * doct as the key
          CALL      FDOC0000                * format docs name
          PACK      PRUNDO1,PREFDOCT,WMDOCTOR,DASH,PACFNAME
          PACK      PREVTYPE,WMDOCTOR       * set up previous doct
          GOTO      PRTT2000
.
. ------ have the first units data so now print the first page heading ------
.
PRTT2000  CALL       NEWP0000               * print a new page
          GOTO       PRTT5500               * print the information
.
. ------------------------------------
. ------ next read on temp file ------
. ------------------------------------
.
PRTT4000  READKS    WATEMP;WMURNO,WMCODE,DWMCNT,WMDESC31,WMDATE1,WMUNIT:
                           WMNOTICE,WMPTY,WMDOCTOR,WMSTAY,WMTIME,WMREMOVE:
                           WMDATE4,WMUDEF2,WTWMDEDA,DIM3A,DIM3B,DIM3C:
                           WMPCAT
          GOTO      PRTT9000 IF OVER
          MOVE      DWMCNT,WMCNT
          MOVE      DIM3A,WTWTHOSP
          MOVE      DIM3B,WTWTPAT
          MOVE      DIM3C,WTWTOTH
.
. ------ see if it is a different clinic ------
.
PRTT5400  CALL      DIFF0000                * determine if new clinic
.
. ------ obtain the current information to display ------
.
PRTT5500  CALL      FINF0000                * get the info
.
. ------ print the line but first see if it will fit on page ------
.
          COMPARE   CVERT,PRINTLIM          * is new page needed ??
          GOTO      PRTT6000 IF NOT LESS    * no
.
PRTT5600  CALL      NEWP0000                * print new page heading
.
PRTT6000  CALL      PPAT0000                * print the line
          GOTO      PRTT4000                * read next record
.
. ------ there is no data obtained for this report ------
.
PRTT7000  CALL      NEWP0000                * print a page heading
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"There are no patients for the entered values."
          GOTO      PRTT9050
.
. --------------------------------
. ------ report is finished ------
. --------------------------------
.
PRTT9000  CALL      DONE0000                * print end of report
.
PRTT9050  PRINT     *N,*N,*N,"***  END OF REPORT ****"
.
. ----- Kill indexed tempfile -----
.
          CLOSE     WATEMP                  * close the file
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          MOVE      "SHORT NOTICE REPORT  ",PRGNAM  * get back name
.
PRTT9999  RETURN
+
.*********************************************************************
.*                  FDOC0000                    Called by : lots     *
.*        Obtain the doctors name and format it                      *
.*        Paras   : KEY6          contains the doctors code          *
.*        Returns : PACFNAME      doctors name (65 chars)            *
.*********************************************************************
FDOC0000  PACK      PACFNAME,SP30,SP30,SP5  * clear the variable
          CALL      RDDOCT1                 * read doct file
          BRANCH    OVRCD,FDOC9999          * invalid code
          MOVE      DTITL,PACTITLE          * title
          MOVE      DGNAME,PACGNAME         * given name
          MOVE      DSNAME,PACSNAME         * surname
          MOVE      TWO,PACFRMT             * format as Surname, Title Given
          CALL      PACNAME                 * format the name
.
FDOC9999  RETURN
+
.*********************************************************************
.*                  FINF0000                    Called by : PROC0000 *
.*        Obtain and format all info on patient                      *
.*********************************************************************
FINF0000  PACK      KEY8,WMURNO             * UR number as key
          CALL      RDMAST1                 * read master file
          MOVE      PTITL,PACTITLE          * title
          MOVE      PGNAME,PACGNAME         * given name
          MOVE      PSNAME,PACSNAME         * surname
          MOVE      TWO,PACFRMT             * format as Surname, Title Given
          CALL      PACNAME                 * format the name
          PACK      PNAME,PACFNAME          * store the name
          RESET     PNAME
.
          CALL      ADDR0000                * format the address
          MOVE      WMDESC31,PRDESC         * set up procedure description
.
. ------ format the date of birth ------
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATE40
.
. ------ format the priority ------
.
          MOVE      SP20,TDESC         * clear tdesc
          PACK      KEY5,CATTP,WMPTY   * key of priority
          CALL      RDCODE1            * read codes file
          MOVE      TDESC,PRPRTY       * set up priority
.
. ------ format the doctors name ------
.
          PACK      KEY6,WMDOCTOR      * docs name as key
          CALL      FDOC0000           * get docs name
          MOVE      PACFNAME,PRDOCT    * set up docs name
.
. ------ format the unit/clinic ------
.
          PACK      KEY5,CATWU,WMUNIT  * key of unit
          CALL      RDCODE1            * read codes file
          PACK      PRCLIN,WMUNIT,SP1,TDESC
.
. ------ format the short notice value ------
.
          PACK      KEY5,CATWS,WMNOTICE     * key of short notice
          CALL      RDCODE1            * read codes file
          PACK      PRSNDESC,WMNOTICE,SP1,TDESC 
.
. ------ format the date on list ------
.
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATE10
.
. ------ determine if public or private ------
.
          MOVE      "(Unknown)",PRPUBPRI    * default to error
          PACK      KEY5,CATA,WMPCAT        * pat. cat as key
          CALL      RDCODE1                 * read codes file
          BRANCH    OVRCD,FINF2000          * invalid pat cat
.
          MATCH     "2",TCINDC1
          IF        @EQUAL
            MOVE      "(Private)",PRPUBPRI    * set as private
          ENDIF
.
          MATCH     "1",TCINDC1
          IF        @EQUAL
            MOVE      "(Public) ",PRPUBPRI    * set as public
          ENDIF
.
. ------ determine if metro or country ------
.
FINF2000  MOVE      SP20,PRMETCOU           * default if read fails
          MATCH     SP3,WMUDEF2             * is there a value ??
          GOTO      FINF3000 IF EQUAL       * no there isnt
.
          PACK      KEY5,CATW2,WMUDEF2      * UD field as key
          CALL      RDCODE1                 * read codes file
          BRANCH    OVRCD,FINF3000          * not on file
          MOVE      TDESC,PRMETCOU          * set up variable
.
. ------ determine if patient is overdue ------
.
FINF3000
          CALL      CDOL0000
          MOVE      ZERO,DAYSODUE           * clear variable
          PACK      KEY5,CATTP,WMPTY        * key of priority
          CALL      RDCODE1                 * read codes file
          BRANCH    OVRCD,FINF9999          * invalid
.
. ------ calculate days overdue ------
.
          REP       "R1S2U3",TCINDC1        * make rout = 1,semi = 2, urg = 3
          MOVE      TCINDC1,FORM1           * set up as a form
          LOAD      LIMIT,FORM1,WTCNRODY,WTCNSUDY,WTCNURDY * load app. day limit
.
.FINFQQ1   DISPLAY   *P1:3,"FINF> limit ",LIMIT," waitdays ",WAITDAYS;
.FINFQQK1  KEYIN     ANS
          COMPARE   LIMIT,WAITDAYS          * is days waiting less than limit ?
          GOTO      FINF9999 IF LESS        * yes, so not overdue
.
          MOVE      WAITDAYS,DAYSODUE       * get days waiting
          SUB       LIMIT,DAYSODUE          * get days overdue
.
FINF9999  RETURN
.**********************************************************************
.*                             ADDR0000                               *
.*                     Pack Patients Address                          *
.**********************************************************************
.
ADDR0000  PACK      ADDRESS,SP70
          CLEAR     ADDRESS
.
          MATCH     SP70,PADD1
          IF        !@EQUAL
            STRIP     PADD1
            APPEND    PADD1,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD2
          IF        !@EQUAL
            STRIP     PADD2
            APPEND    PADD2,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PSUBRB
          IF        !@EQUAL
            STRIP     PSUBRB
            APPEND    PSUBRB,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD4
          IF        !@EQUAL
            STRIP     PADD4
            APPEND    PADD4,ADDRESS
            APPEND    " ",ADDRESS
          ENDIF
.
          MATCH     SP70,PPOST
          IF        !@EQUAL
            SQUEEZE   PPOST
            APPEND    PPOST,ADDRESS
          ENDIF
.
          RESET     ADDRESS
.
          RETURN
+
.********************************************************************
.*                  CDOL0000                   Called by : FINF0000 *
.*        Determines how many days a patient has been on the list   *
.*        Paras   : WMDATE1       the waiting list date             *
.*        Returns : CURDAYS       current julian day                *
.*                  CURYR         current julian year               *
.*                  CURLEAP       if curr. is a leap year = 1       *
.*                  WLDAYS        WL julian day                     *
.*                  WLYR          WL julian year                    *
.*                  WLLEAP        WL leap year                      *
.*                  WAITDAYS      days between wait and current     *
.********************************************************************
CDOL0000  
.
.------ turn the current date into julian date ------
.
          CALL      IBACLOCK
          MOVE      CDD,DD                  * set up current day
          MOVE      CMM,MM                  *                 month
          MOVE      CYY,YY                  *                 year
          MOVE      CCC,CC
.
          CALL      DMYCON                  * get julian day
          MOVE      JULDAY,CURDAYS          * get current julian day
          MOVE      JULYR,CURYR             * get current julian year
          MOVE      JULCC,CURCC
          MOVE      LEAPYR,CURLEAP          * get leap year flag
.
.------ turn date on waiting list into julian days ------
.
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY  * WL date
          MOVE      CCENT,CC
          MOVE      CYEAR,YY                * set up year
          MOVE      CMON,MM                 * set up month
          MOVE      CDAY,DD                 * set up day
.
          CALL      DMYCON
          MOVE      JULDAY,WLDAYS           * WL julian day
          MOVE      JULYR,WLYR              * WL julian year
          MOVE      JULCC,WLCC
          MOVE      LEAPYR,WLLEAP           * WL leap year
.
          MOVE      ZERO,WAITDAYS           * clear the day difference
.
.------ if the current and waiting years are the same, dont worry about 
.------ allowing for current date being in the next year
.
          COMPARE   WLYR,CURYR              * check for the same year
          GOTO      CDOL2000 IF EQUAL       * same year - just sub dates
.
.------ current  year is in a year after the waiting year ------
.------ idea is to add 365/366 to the current  julian day ------
.------ to make it eg, the 369th day of the previous year ------
.------ for the 4th of january                            ------
.
          MOVE      CURYR,WAITDAYS          * holds keyed in year
          SUB       WLYR,WAITDAYS           * # years b/w curr and wlist year
          MULT      "365",WAITDAYS          * # days b/w curr and wlist year
          ADD       WLLEAP,WAITDAYS         * # days b/w curr and wlist year
          
CDOL2000  ADD       CURDAYS,WAITDAYS        * day number of current year
          SUB       WLDAYS,WAITDAYS         * difference in full days
.
CDOL9999  RETURN
+
.*********************************************************************
.*                  DIFF0000                    Called by : PRTT0000 *
.*        see if a new clinic/doct      has been found               *
.*********************************************************************
DIFF0000  BRANCH    MOPTION,DIFF1000,DIFF2000
.
DIFF1000  MATCH     PREVTYPE,WMUNIT         * is previous same as current ??
          GOTO      DIFF9999 IF EQUAL       * yes, dont need a new heading
.
. ---------------------------------------------------------------
. ------ have a new clinic type (only applies when ALL)    ------
. ------ do underlines for last clinic, give patient count ------
. ------ set up new clinic name, do new table heading      ------
. ------ update the previous clinic with the new one       ------
. ---------------------------------------------------------------
.
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*"
          UNPACK    PRUNDO1,DIM19,DIM26     * get the two parts
          PRINT     *N,"Total Patients Printed for ",DIM26," is ",CNTPATCL
          ADD       THREE,CVERT             * increment line counter
          MOVE      ZERO,CNTPATCL           * clear patient count
.
. ------ get the new units details ------
.
          PACK      KEY5,CATWU,WMUNIT       * unit as the key
          CALL      RDCODE1                 * read codes file
          PACK      PRUNDO1,PREFUNIT,WMUNIT,DASH,TDESC * store new unit
          RESET     PRUNDO1
          PACK      PREVTYPE,WMUNIT,SP5     * update previous unit
          GOTO      DIFF8000
.
. ------ check on doctors codes ------
.
DIFF2000  MATCH     PREVTYPE,WMDOCTOR       * is previous same as current ??
          GOTO      DIFF9999 IF EQUAL       * yes, dont need a new heading
.
. ---------------------------------------------------------------
. ------ have a new doctor code (only applies when ALL)    ------
. ------ do underlines for last doctor, give patient count ------
. ------ set up new doctor name, do new table heading      ------
. ------ update the previous doctor with the new one       ------
. ---------------------------------------------------------------
.
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*"
          UNPACK    PRUNDO1,DIM14,DIM31     * get the two parts
          PRINT     *N,"Total Patients Printed for ",DIM31," is ",CNTPATCL
          ADD       THREE,CVERT             * increment line counter
          MOVE      ZERO,CNTPATCL           * clear patient count
.
. ------ get the new docts details ------
.
          PACK      KEY6,WMDOCTOR           * doct as the key
          CALL      FDOC0000                * get docs name
          PACK      PRUNDO1,PREFDOCT,WMDOCTOR,DASH,PACFNAME
          RESET     PRUNDO1
          PACK      PREVTYPE,WMDOCTOR,SP2   * update previous doct
          GOTO      DIFF8000
.
. ------ see if there is enough room on the page for new table heading ------
.
DIFF8000  COMPARE   CVERT,NEEDNP            * will table fit on page ??
          GOTO      DIFF8500 IF NOT LESS    * yes, print new table heading only
.
          CALL      NEWP0000                * print new page and table heading
          GOTO      DIFF9999
.
DIFF8500  CALL      PTBH0000                * print new table
.
DIFF9999  RETURN
+
.*********************************************************************
.*                  NEWP0000                    Called by : PRTT0000 *
.*        Prints the new page heading for a report          DORP0000 *
.*        Para's  : CPHRDOPT      sub heading                        *
.*********************************************************************
NEWP0000  MOVE      ZERO,CNOUNDLN           * underlines at end of page
.
NEWP0500  CALL      HEAD132                 * do the new heading
.
          PRINT     *1,PRFROM,*40,PRUNDO2:  * from date
                    *N,PRTO,*40,PRSHORT     * to date
.
. ------ print the table heading ------
.
          MOVE      SEVEN,CVERT             * set line counter
          CALL      PTBH0000                * print a table heading
.
NEWP9999  RETURN
+
.*********************************************************************
.*                  PPAT0000                    Called by : PRTT0000 *
.*        Prints a single data line of the report           DORP0000 *
.*        ie. the information about a patients procedure             *
.*        Updates : CNTPATCL      count of pats                      *
.*                  CNTPATTL      count of total patients            *
.*                  CVERT         current row number                 *
.*********************************************************************
PPAT0000  PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,"|",WMURNO,*12,PNAME,*62,"|",PRDESC: 
                    *93,"|Added   : ",DATE10,*132,"|"
.
. -------------------------------------------------------------------
. ------ print line two about patient                          ------
. ------ if by clinic code, put doctors code value on line two ------
. ------ if by doctor code, put clinic code  value on line two ------
. -------------------------------------------------------------------
.
PPAT2500  BRANCH    MOPTION,PPAT2550        * by unit/clinic code
.
. ------ by doctors code so unit/clinc desc goes on line two ------
.
          PRINT     *1,"|",*12,ADDRESS,*62,"|Unit : ",PRCLIN;
          GOTO      PPAT2600
.
PPAT2550  PRINT     *1,"|",*12,ADDRESS,*62,"|",PRDOCT;
.
PPAT2600  PRINT     *93,"|Priority: ";
.
. ------ determine if a staged or deferred admission ------
.
          PACK      KEY5,CATTP,WMPTY        * key of priority
          CALL      RDCODE1                 * read codes file
          BRANCH    OVRCD,PPAT3100          * not on file
.
          MATCH     ANST,TCINDC2            * is it a staged admission ??
          GOTO      PPAT2700 IF EQUAL       * yes
.
          MATCH     ANSD,TCINDC2            * is it a deferred admission
          GOTO      PPAT3100 IF NOT EQUAL   * no
.
          PRINT     "Deferred Admiss. ";
          GOTO      PPAT2900
.
PPAT2700  PRINT     "Staged Admission ";
.
PPAT2900  UNPACK    WTWMDEDA,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *121,CPCDATE,*132,"|"
          GOTO      PPAT5000
.
PPAT3100  PRINT     PRPRTY,*117,"O/Due:",DAYSODUE," Days",*132,"|"
.
. ---------------------------------------------------------------------
. ------ print line three about patient                          ------
. ---------------------------------------------------------------------
.
PPAT5000  PRINT     *1,"|",*12,"Home ",PTELEP,SP1,"Bus ",PTELEB:
                    *62,"|S/N  : ",PRSNDESC;
.
PPAT5100  PRINT     *93,"|";
.
. ------ see if there is a booking cancellation reason ------
.
          MATCH     SP3,WMREMOVE            * is there a booking canc. reason ??
          GOTO      PPAT5500 IF EQUAL       * no, dont display anything
.
          MOVE      WTWTHOSP,HOSP3          * get as form 3
          MOVE      WTWTPAT,PAT3
          MOVE      WTWTOTH,OTH3
.
. ------ display the stats on booking cancellations ------
.
          PRINT     "Cancel  : Hos =",HOSP3," Pat =",PAT3:
                    " Oth =",OTH3;
.
PPAT5500  PRINT     *132,"|"
.
. -------------------------------------------
. ------ print line four about patient ------
. -------------------------------------------
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,PRSEX
.* ******************************************   End of Changes         *C-49016
.
PPAT7900  PRINT     *1,"| ",PRPUBPRI,*12,"Sex: ",PRSEX,*24,"DOB: ",DATE40:
                    *41,"M/C: ",PRMETCOU:
                    *62,"|","Stay : ",WMSTAY:
                    SP2,"Duration : ",WMTIME,*93,"|";
.
. ------ determine if a there is a latest booking cancellation ------
.
          MATCH     SP3,WMREMOVE            * is there a booking canc ??
          GOTO      PPAT8300 IF EQUAL       * no.
.
          UNPACK    WMDATE4,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * format the cancellation date
          MOVE      CPCDATE,DATE20          * set up the date
          MOVE      SP20,TDESC              * default if read fails
          PACK      KEY5,CATBC,WMREMOVE     * key of booking canc reason
          CALL      RDCODE1                 * read codes file
          MOVE      TDESC,PRCANC            * set up reason
.
          PACK      KEY19,WMURNO,WMCODE,DWMCNT
          PROC      DTRNDEST               * to get the transfer destination
          MOVE      TDESC,PRCANC
.
          PRINT     "Latest  : ",DATE20," ",PRCANC;
.
PPAT8300  PRINT     *132,"|"
.
          DISPLAY   *P70:24,WMURNO          * display UR number to show working
.
. ------ increment the counters ------
.
PPAT9000  ADD       FIVE,CVERT              * add to line counter
          ADD       ONE,CNTPATCL            * add to patient count for clinic
          ADD       ONE,CNTPATTL            * add to total pat. count
.
PPAT9999  RETURN
+
.*********************************************************************
.*                  PTBH0000                    Called by : NEWP0000 *
.*        Prints a new table heading                        PRTT0000 *
.*        Needed at a new page or for a new unit/clinic     DORP0000 *
.*        Paras/Returns : CVERT      current row number              *
.*********************************************************************
PTBH0000  PRINT     *N,PRUNDO1:
                    *N,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*1,"|",*3,"U/R No ",*12,"Personal Details ":
                    *62,"|Medical Details",*93,"|Waiting List Details":
                    *132,"|"
.
          ADD       FOUR,CVERT              * add to line count
.
PTBH9999  RETURN
+
.*********************************************************************
.*                  DONE0000                    Called by : PRTT0000 *
.*        Finish off the report                                      *
.*********************************************************************
DONE0000  BRANCH    MOPTION,DONE1000,DONE2000
.
. ------ finish for unit/clinic ------
.
DONE1000  UNPACK    PRUNDO1,DIM19,DIM26     * get the two parts
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"Total Patients Printed for ",DIM26," :  ",CNTPATCL:
                    *N,*N,"Grand Total Patients Printed ",SP20,SP4," : ":
                    CNTPATTL
          GOTO      DONE9999
.
. ------ finish for doctor code ------
.
DONE2000  UNPACK    PRUNDO1,DIM14,DIM31     * get the two parts
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"Total Patients Printed for ",DIM31," :  ",CNTPATCL:
                    *N,*N,"Grand Total Patients Printed ",SP20,SP9," : ":
                    CNTPATTL
          GOTO      DONE9999
.
DONE9999  RETURN
+
.*****************************************************************************
.*                  TEMP0000                                                 *
.*        Open tempfile for Unit   sequence :                                *
.*                          Doctor sequence :                                *
.*        Kill indexed temp files, and create new indexed tempfiles          *
.*****************************************************************************
TEMP0000  
.
. ----- Kill indexed tempfile -----   
.
          CLOSE     WATEMP                  * close temp file
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
. ----- Create indexed tempfile -----
.
          DISPLAY   *P1:24,*V2LON,*BLINKON,"Please wait.",*EL
          LOAD      SRTKEY,MOPTION,SORT1,SORT2  * which key to use
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME1,CMDLINE
          APPEND    " 316 ",CMDLINE
          APPEND    SRTKEY,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      WATEMP,FNAME1           * open temp file
          DISPLAY   *P1:24,*EL
.
TEMP9999  RETURN
.
. ------ I/O includes needed ------
.
          INC       STD001IO                * common routines
          INC       PATCOMN3                * for gperd
          INC       PATCODKY                * ? routine on codes file
          INC       PATDOCKY                * ? routine on doctors file
          INC       TRANDEST                * transfer destination
          INC       TFILENAM
          INC       SEXDSCIO
.
          INC       IBASEQIO/INC
          INC       PATDRGIO/INC            * date range file for gperd
          INC       PATCODIO/INC            * codes file
          INC       PATDO1IO/INC            * doctors file
          INC       PATMA1IO/INC            * master file
.
          INC       WATCATIO/INC            * WL category change file
          INC       WATTR1IO/INC            * WL treatment file
          INC       WATRTDIO/INC            * Transfer destination file
          INC       WATWTAIO/INC            * WL movement analysis file
          INC       WATCNTCN                * WL Cancellations             
          INC       WEBERRIO/INC

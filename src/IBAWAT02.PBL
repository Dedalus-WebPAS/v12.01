.***********************************************************************
.* System   : WAITING LIST                                             *
.* Program  : IBAWAT02                                                 *
.* Desc     : Remove Procedure from patient                            *
.***********************************************************************
.* Author   : N.S                                                      *
.* Date     : 20/10/1988                                               *
.* Comments :                                                          *
.* Mod/ns   :                                                          *
.*                                                                     *
.***********************************************************************
.* V12.00.01 30/05/2025 Jacob Jackson     TSK 0955096                  *
.*           Alphanumeric changes                                      *
.* V11.02.01 09/02/2022  Thanh T          TSK 0889899                  *
.*           Added WMDESC2 - Procedure Description 2                   *
.*                 WMDESC3 - Procedure Description 3                   *
.***********************************************************************
.* V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*           Added SEXDSCIO include                                    *
.* V9.04.01  01/03/2005  Peter Vela    CAR 57547                       *
.            Recompiled for PATVADFD and PATVADIO                      *
.* V9.03.03  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *     
.*           - Add Sex Description routine SEXDSC00 with Code "R" - "Intersex
.* V9.03.02  13/08/2003  Sandra Barcham    42274                       *
.*           Defined WATHI002 with tilda's to put current date into    *
.*           WTHIEDAT to stop I * D                                    *
.* V9.03.01  12/08/2003  Sandra Barcham    42268                       *
.*           Added open of wathisa1 to fix 00E7B3 I * C                *
.***********************************************************************
.* V9.02.01  27/02/2003  Jill Habasque SRF 34660                       *
.*           Recompiled for WLHISSUB                                   *
.* V5.08.08  01/09/2000  Tonii                                         *
.*           Mods to accept Unknown and Indeterminate as valid patient *
.*           sex description                                           *
.* V5.08.07  16/08/2000  Caleb Sharman                                 *
.*           Changed Health Fund variables to be 8 chars               *
.* V5.08.06  27/07/2000  Jill Habasque                                 *
.*           Recompiled for WLHISSUB                                   *
.* V5.08.05  17/07/2000  Jill Habasque                                 *
.*           Recompiled for WLHISSUB                                   *
.* V5.08.04  03/07/2000  Jill Habasque                                 *
.*           Recompiled for WATAUTR1                                   *
.* V5.08.03  29/06/2000  Jill Habasque                                 *
.*           Recompiled for WATHISFD                                   *
.* V5.08.02  08/06/2000  Tonii Tang                                    *
.*           Moved "20" into Booking Status Code when removing a       *
.*           procedure, and a new record is written to the history file*
.* V5.08.01  11/05/2000  J.Tan                                         *
.*           Recompiled for changing record length of WATTR1           *
.* V5.07.03  15/10/1999  Steve Armstrong                               *
.*           Removed reference to CSTRTYR (not used)                   *
.* V5.07.02  16/08/1999 J.Tan                                          *
.*           Mods to keyin transfer destination description            *
.* V5.07.01  20/07/1999  Andrew Peel  SRF 645866                       *
.*            Fixed wait list removal codes appearing on next screen   *
.* V5.07.B02 12/01/1999  Jill Habasque                                 *
.*           Changed to display centuries                              *
.            22/10/1998  Glenn Berry      5.07 changes                 *
.***********************************************************************
.* V5.01.121 25/09/90 J.Tan             SRF 106164                     *
.*           Recompile for rate change audit                           *
.* V5.01.122 19/10/90 DIG / i chung (12/11/90)                         *
.*           Recompile for WATCATIO.INC - incorrect Keys size          *
.* V5.01.123 13/11/90 i chung           SRF 106963                     *
.*           Changed category for removal reasons from "BC" to "WR"    *
.* V5.01.124 27/11/90 Glen Hobby                                       *
.*           Recompiled for changes to                                 *
.*           PATMX1FD                                                  *
.* V5.01.125 19/12/90 D.Di.Paola    SRF 107397                         *
.*           Recompiled for changes to PATURAFD                        *
.* V5.01.126 25/01/91 Justin Coates                                    *
.*           Added BOKRC1FD/IO                                         *
.* V5.01.127 25/02/1993    Justin Coates  (DUDLEY)                     *
.*           when cancel procedure, delete all suspensions             *
.* V5.01.128 16/03/93   ROD  IS GAY (SAN FRANCISCO)                    *
.*           check security for a procedure                            *
.* V5.01.129 26/05/93   WHIRLPOOL                                      *
.*           Added stupid invalid security message                     *
.* V5.01.130 26/08/93   ROD                                            *
.*           Recompiled for NZHIS                                      *
.* V5.01.131 26/05/1994 Ian Rutt                                       *
.*           Fixed Global Recompile Bugs                               * 
.* V5.01.132 07/09/1994 Paul Howells     SRF441066                     *
.*           Delete from Contracting files                             * 
.***********************************************************************
.
          INC       STD001FD
          INC       NHIMASFD/INC                 * Include for NZHIS vars
          INC       BOKRC1FD/INC
          INC       OUTPREFD/INC
          INC       PATAM1FD/INC
          INC       PATCALAG/INC
          INC       PATCODFD/INC
          INC       PATCOMM/INC
          INC       PATCONFD/INC
          INC       PATDHEAD/INC
          INC       PATDO1FD/INC
          INC       PATDRGFD/INC
          INC       PATDSCFD/INC
          INC       PATINVFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATPR1FD/INC
          INC       PATRCAUD/INC
          INC       PATGSRFD/INC
          INC       PATURAFD/INC
          INC       PATVISFD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       WATCATFD/INC
          INC       WATCONFD/INC                   * control file
          INC       WATRTDFD/INC
          INC       WATSEAFD/INC
          INC       WATSEIFD/INC
          INC       WATSUSFD/INC
          INC       WATTR1FD/INC                   * waiting list master file
          INC       WATHISFD/INC
          INC       WATPROFD/INC
          INC       WATNBRFD/INC
          INC       WEBSECFD/INC
          INC       NZHISIFD/INC                 * Include for NZHIS vars
.
.******************************************************************************
.                            VARIABLE DECLARATION
AUDIFLAG  FORM      1
ADDWLFLG  FORM      1
.
CHGT      FORM      1           * changes to procedure fields
CODEWU    INIT      "WU"        * category WU (unit)
CODETP    INIT      "TP"        * category TP (priority)
CODEWR    INIT      "WR"        * category WR (removal reason)
CCOUNT    FORM      1           * counter in loop rdcode1 (WMUDEF..)
COUNT     FORM      2           * counter in display wattrefd entries
CHOICE    FORM      2           * option for UR no to be displayed
COUNTER   FORM      3
DATETMP   DIM       8
DDESCWU   DIM       20          * screen display variable for codes
DESCOPT   DIM       20          * option display on screen
DOCNAME   DIM       32          * doctor title initial.surname for display
DESCWR    DIM       20          * description for removal reason (cata WR)
DESCWU    DIM       20          * description for unit (cata WU)
DIM5      DIM       5
DIM8      DIM       8           * store wmurno
DIM10     DIM       10          * store patient first name for display
DSEX      DIM       8
FIELD     FORM      2           * user def var
FORM3     FORM      3
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
NAMEPAT   DIM       50          * patient name Surname, Title Given
PATNAME   DIM       31          * patient title first name surname display
PATDOB    DIM       10          * patient DOB for display (dd/mm/ccyy)
SCNDGNAM  DIM       40
SDESC     DIM       30          * display of procedure description
SELECT    FORM      2           * option for modify procedure screen
TODAY     DIM       10
WATHI002  INIT      "~~~~~~~~"
WMDAT10   DIM       10          * wmdate1 dd/mm/ccyy
WMDAT20   DIM       10          * wmdate2 dd/mm/ccyy
WMDAT30   DIM       10          * wmdate3 dd/mm/ccyy
WMDAT40   DIM       10          * wmdate4 dd/mm/ccyy
.
BJDAY     FORM      3
CJDAY     FORM      3
CODE      DIM       2
SEC1      FORM      "00001"
SECCOUNT  FORM      3           * counter for invalid security
COL       FORM      2
VERT      FORM      2
.
DIM6      DIM       6
DIM18     DIM       18
DIM20     DIM       20
.
SP14      INIT      "              "
.
Z20        INIT    "ZZZZZZZZZZZZZZZZZZZZ"
.
DIM13     DIM       13
DIMC01    DIM       19
DIMC02    DIM       19
DIMC03    DIM       19
DIMC04    DIM       19
DIMC05    DIM       19
DIMC06    DIM       19
DIMC07    DIM       19
DIMC08    DIM       19
DIMC09    DIM       19
DIMC10    DIM       19
DIMC11    DIM       19
DIMC12    DIM       19
DIMC13    DIM       19
DIMC14    DIM       19
DIMC15    DIM       19
DIMC16    DIM       19
NMPNUMB   DIM       20
SAVKEY    DIM       19          * for deleting audits
.
STAT0     DIM       1
STAT1     INIT      "Unscheduled  "
STAT2     INIT      "Scheduled    "
STAT3     INIT      "Pre-admitted "
STAT4     INIT      "Admitted     "
STAT5     INIT      "Discharged   "
STAT6     INIT      "Removed      "
.
URDIM     DIM       8           * keyed in U/R number
DIM81     DIM       8
SURDIM    DIM       8           * save var for urdim  
WMDATE    DIM       8
NUM59     FORM      "59"
.
PRGID     INIT      "IBAWAT02"
PRGNAM    DIM       16          * it's value is set up in INIT0000
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
ML0000    CALL      INIT0000               * open files     
.
. ---- remove a procedure from a patient ----.
.
ML1000    CALL      CLRP0000               * clear vars patient master
          CALL      CLRW0000               * clear vars waiting master
          CALL      CLRM0000               * clear DIMC.. variables
.
          CALL      MODW0000               * displ all recs for one UR
          BRANCH    EXIT,ML9000,ML9000
.
          CALL      DOCT0000               * get doctor name for display
          CALL      UNIT0000               * get unit description for displ
          CALL      SCRT0000               * procedure screen
          CALL      DIST0000               * display procedure variables
          CALL      AUDB0000           * write a B audit
.
          CALL      KEYT2000               * keyin reason removal
          BRANCH    EXIT,ML1000            * return hit or exit char
.
          CALL      KEYT3000               * keyin removal date
.
          CALL      SELT0000               * select field to update (1)
          BRANCH    EXIT,ML1000
.
          CALL      REMW0000               * remove patient from list
          CALL      RSUS0000               * remove suspensions
.         CALL      DELC0000               * delete from contracting files
          GOTO      ML1000
ML9000
.
ML9999    CHAIN     PGM
          STOP
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialization                               *
.****************************************************************************
INIT0000  DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,THIRTY7;*122,WTCNTPUC,*185,WTCNTPDS
          READ      CONTROLF,TWENTY1;*64,PTCNNPDS
.
. ------  set up either TREATMENT or PROCEDURE in program name ------
.
          CLEAR     PRGNAM
          APPEND    "Remove ",PRGNAM
          APPEND    WTCNTPUC,PRGNAM
          RESET     PRGNAM
.
          MOVE      SP20,DESCOPT
          CALL      DISPHEAD
          PACK      KEY60,PRGNAM,DESCOPT
          CALL      DISPLIN2
.
. ---- open files ----
.
          READ      CONTROLF,ZERO;*2,CDD,CMM,CYY,CCC
          READ      CONTROLF,THIRTY7;*44,HWAUDB,*211,WTCNWLSC
          READ      CONTROLF,FIFTY9;*220,PTCNUADT
.
          BRANCH    HWAUDB,INIT1111 
          DISPLAY   *P56:24,"Opening wataudwm";
          OPEN      WATAUTR1,"watautr1"
INIT1111
          DISPLAY   *P56:24,"Opening ibacodef";
          OPEN      IBACODE1,"ibacodef"
.
          IF        WTCNWLSC=1
            DISPLAY   *P64:24,"watseaaf";
            OPEN      WATSEAA1,"watseaaf"
            DISPLAY   *P64:24,"watseiaf";
            OPEN      WATSEIA2,"watseiaf"
          ENDIF
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN1,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
          DISPLAY   *P64:24,"patpraxf";
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTR1A2,"wattr1af"
          OPEN      WATTR1A3,"wattr1af"
.
          DISPLAY   *P64:24,"watcataf";
          OPEN      WATCATA1,"watcataf"
.
          DISPLAY   *P64:24,"wathisaf";
          OPEN      WATHISA1,"wathisaf"
.
          DISPLAY   *P64:24,"watsusaf";
          OPEN      WATSUSA1,"watsusaf"
          DISPLAY   *P1:24,*EL
.
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD    * save current date
          REPLACE   " 0",TODAY
.
          PACK      KEY5 WITH CODEWU,SP5          * pack codes key 
          CALL      RDCODE1                       * read codes file
          BRANCH    OVRCD OF INIT9999             * disp blank if no code
          MOVE      TDESC,DDESCWU                 * save display var.
.
INIT9999  RETURN
.******************************************************************************
.*               CLRP0000                                                     *
.*       Subroutine clear patient master variables                            *
.******************************************************************************
.
CLRP0000  MOVE      SP8,PURNO
          MOVE      SP30,PTITL
          MOVE      SP30,PSNAME
          MOVE      SP30,PGNAME
          MOVE      SP30,PPSNAME
          MOVE      SP30,PADD1
          MOVE      SP30,PADD2
          MOVE      SP30,PSUBRB
          MOVE      SP30,PPOST
          MOVE      SP30,PTELEP
          MOVE      SP30,PSEX
          MOVE      SP30,PBDATE
          MOVE      ZERO,PSAGE
          MOVE      SP30,PCONT
          MOVE      SP30,PREG
          MOVE      SP30,PTYPE
          MOVE      SP30,POCCUP
          MOVE      SP30,PMEDI
          MOVE      SP30,PPENNO
          MOVE      ZERO,PYRRES
          PACK      PATNAME WITH SP30,SP30
          PACK      PATDOB WITH SP10
          PACK      DOCNAME WITH SP30,SP30
.
          RETURN
.
.******************************************************************************
.*               CLRW0000                                                     *
.*       Subroutine clear waiting-list variables                              *
.******************************************************************************
.
CLRW0000  MOVE      SP8,WMURNO
          MOVE      SP30,WMCODE
          MOVE      ZERO,WMCNT
          PACK      WMDESC WITH SP30,SP30,SP10
          MOVE      ZERO,WMTIME
          PACK      WMREASON WITH SP30,SP30
          MOVE      ZERO,WMSTAT
          MOVE      SP30,WMDATE1
          MOVE      SP30,WMDATE2
          MOVE      SP30,WMDATE3
          MOVE      SP30,WMDATE4
          MOVE      SP30,WMREMOVE
          MOVE      SP30,WMUNIT
          MOVE      SP20,DESCWU
          MOVE      ZERO,WMDOCTOR
          PACK      DOCNAME WITH SP30,SP10
          MOVE      ZERO,WMSTAY
          MOVE      SP30,WMPTY
          MOVE      SP30,WMUDEF1
          MOVE      SP30,WMUDEF2
          MOVE      SP30,WMUDEF3
          MOVE      SP30,WMUDEF4
          MOVE      ZEROVISN,WMBOOK
          MOVE      SP70,WTWMRADT
          PACK      WMDESC2,SP70,SP70
          PACK      WMDESC3,SP70,SP70
          PACK      WTWMSPAR,SP30,SP30
          PACK      WMDAT10 WITH SP20
          PACK      WMDAT20 WITH SP20
          PACK      WMDAT30 WITH SP20
          MOVE      SP10,WTWMBSCD
. 
          RETURN
.
.****************************************************************************
.*               CLRM0000                                                   *
.*          Clear MODW0000 variables                                        *
.****************************************************************************
.
CLRM0000    MOVE      SP20,DIM19
            MOVE      SP20,DIMC01
            MOVE      SP20,DIMC02
            MOVE      SP20,DIMC03
            MOVE      SP20,DIMC04
            MOVE      SP20,DIMC05
            MOVE      SP20,DIMC06
            MOVE      SP20,DIMC07
            MOVE      SP20,DIMC08
            MOVE      SP20,DIMC09
            MOVE      SP20,DIMC10
            MOVE      SP20,DIMC11
            MOVE      SP20,DIMC12
            MOVE      SP20,DIMC13
            MOVE      SP20,DIMC14
            MOVE      SP20,DIMC15
            MOVE      SP20,DIMC16
            RETURN
.
.****************************************************************************
.*               MODW0000                                                   *
.*          Display wanted UR's and WATTR1FD info on screen                 *
.    Modified 27/08/1990 Bill Dew to make it better - well you failed buddy
.****************************************************************************
MODW0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:4,*EF,"U/R Number : "
.
          MOVE      SP8,DIM81
          MOVE      FOUR,CVERT
          MOVE      TEN4,CCOL
          MOVE      TWO,SRCHOPT
.
          CALL      KURN
          MOVE      CPPURNO,DIM81
          BRANCH    EXIT,MODW9999           * nothing entered
          BRANCH    OVRCD,MODW0050          * illegal UR number
.
          MOVE      PURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,MODW6666
          MOVE      KEY8,DIM8
.
          CALL      WHED0000                     * PMI Heading
.
          GOTO      MODW0120
.
MODW0050  DISPLAY   *P1:24,*EL,*B,"U/R not on file.  ";
          CALL      HITENTER
          GOTO      MODW0000
.
.-----------------------------------------------------------------------------
.           Display all records in WATTR1FD with this UR number
.-----------------------------------------------------------------------------
.
MODW0120  DISPLAY   *V2LON:
                    *ULON,*P1:7,"Lne":
                    *P5:7,WTCNTPDS:
                    *P15:7,"Description               ":
                    *P42:7,"Status       ":
                    *P56:7,"On List   ":
                    *P67:7,"Review    ":
                    *P78:7,"Pty"
          MOVE      EIGHT,VERT
          MOVE      ZERO,SECCOUNT
          MOVE      ZERO,COUNT         * zero records on screen 
          MOVE      ZERO,COUNTER       * zero the total record counter
          PACK      KEY19,DIM81,SP20         * display one UR
          CALL      RDSTREA1
.
MODW0400  CALL      RDKTREA1
          BRANCH    OVRCD TO MODW6666
.
          MATCH     DIM81,WMURNO       * one single UR
          GOTO      MODW6666 IF NOT EQUAL
.           
          COMPARE   ONE,WMSTAT         * only display status = 1
          GOTO      MODW0400 IF NOT EQUAL
.
. check if user has security access to this record
.
          COMPARE   ONE,WTCNWLSC                  * using security?
          GOTO      MODW1000 IF NOT EQUAL
.
          PACK      KEY4,PASSCODE
          CALL      RDWTSEA1
          COMPARE   ZERO,OVRCD                    * ALL Access
          GOTO      MODW1000 IF EQUAL
          PACK      KEY13,PASSCODE,WMDOCTOR,WMUNIT
          CALL      RDWTSEI2
          IF        OVRCD = 1
            ADD       ONE,SECCOUNT
            GOTO      MODW0400              * invalid security
          ENDIF
.          
MODW1000  ADD       ONE,COUNT
          ADD       ONE,COUNTER        * record counter
.
          COMPARE   TEN7,COUNT
          GOTO      MODW4444 IF NOT EQUAL
.
MODW3000  KEYIN     *P1:24,*EL,"(",*V2LON,*DV,ANSF,*HOFF,")urther search, ":
                    "(",*V2LON,*DV,ANSS,*HOFF,")elect Line : ",*V2LON,ANS
          REPLACE   UPPLOW,ANS
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
          MATCH     SP1,ANS
          GOTO      MODW0000 IF EQUAL
          MATCH     ANSF,ANS
          GOTO      MODW4000 IF EQUAL
          MATCH     ANSS,ANS
          GOTO      MODW5000 IF EQUAL
          BEEP
          GOTO      MODW3000
MODW4000
          MOVE      ONE,COUNT          * pre-emptive read
          MOVE      SEVEN,VERT
          DISPLAY   *P1:VERT,*EF
MODW4444
          MOVE      WMDESC,SDESC
          CALL      SDAT0000           * set date var
          LOAD      DIM13,WMSTAT,STAT1,STAT2,STAT3,STAT4,STAT5,STAT6
          DISPLAY   *P1:VERT,*V2LON,COUNT,*HOFF,*P5:VERT,WMCODE:
                    *P15:VERT,SDESC,*P42:VERT,DIM13,*P56:VERT,WMDAT10:
                    *P67:VERT,WMDAT20,*P78:VERT,WMPTY
          ADD       ONE,VERT
.
          PACK      DIM19,WMURNO,WMCODE,WMCNT
          STORE     DIM19,COUNT,DIMC01,DIMC02,DIMC03,DIMC04,DIMC05,DIMC06:
                    DIMC07,DIMC08,DIMC09,DIMC10,DIMC11,DIMC12,DIMC13,DIMC14:
                    DIMC15,DIMC16
          GOTO      MODW0400
.
MODW5000
          KEYIN     *P1:24,*EL,"Select Line : __",*V2LON,*P15:24,CHOICE
          COMPARE   ZERO,CHOICE
          GOTO      MODW0000 IF EQUAL  * get another UR number
          COMPARE   CHOICE,COUNT
          GOTO      MODW5000 IF LESS
          MOVE      SP20,KEY19
          LOAD      KEY19 USING CHOICE OF DIMC01,DIMC02,DIMC03,DIMC04:
                                          DIMC05,DIMC06,DIMC07,DIMC08:[
                                          DIMC09,DIMC10,DIMC11,DIMC12:
                                          DIMC13,DIMC14,DIMC15,DIMC16
          RESET     KEY19
          CALL      RDTREA1
          CALL      SDAT0000           * set date vars
.
          MATCH     SP8,WMDATE3
          GOTO      MODW9999 IF EQUAL
          UNPACK    WMDATE3 INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      WMDAT30 WITH CPCDATE,SP10
          REP       " 0",WMDAT30
          GOTO      MODW9999
MODW6666
          COMPARE   ZERO,COUNTER
          GOTO      MODW5000 IF NOT EQUAL
          IF        SECCOUNT = 0
            DISPLAY   *P1:24,*EL,*B,"No procedures to remove.  ";
          ELSE
            DISPLAY   *P1:24,*EL,*B,"Invalid Security.  ";
          ENDIF
          CALL      HITENTER
          GOTO      MODW0000
.
MODW9999  RETURN
+
.****************************************************************************
.*               PDOB0000                                                   *
.*          Get patient DOB for display and sex-description                 *
.****************************************************************************
PDOB0000  UNPACK    KEY19,DIM8,WMCODE,DIM2
.
PDOB1000  MOVE      DIM8,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PDOB2000
          UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
          PACK      PATDOB,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
PDOB2000  MOVE      SP8,DSEX
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DSEX
.* ******************************************   End of Changes         *C-49016
.
PDOB4000  BRANCH    OVRCD,PDOB9999                      * from read above
          CALL      PATN0000
.
PDOB9999  RETURN
+
.****************************************************************************
.*               PATN0000                                                   *
.*          Concatenate patient name                                        *
.****************************************************************************
PATN0000  MOVE      PGNAME,PACGNAME           * set up pacname variables
          MOVE      PSNAME,PACSNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                   * format name
          MOVE      PACFNAME,PATNAME          * move to display variable
          RETURN
+
.*********************************************************************
.*        WHED0000                                                   *
.*        Display Waiting Lists Heading                              *
.********************************************************************
.
WHED0000  CALL      PDOB1000                       * get DOB
          IF        PTCNNPDS=0
            DISPLAY   *P40:4,*EF,"Name   : ":
                      *P40:5,"D.O.B. : ":
                      *P49:4,*V2LON,PATNAME:
                      *P49:5,PATDOB;
            MOVE      SIX,CVERT
          ELSE
            DISPLAY   *P28:4,*EF,"Name : ":
                      *P28:5,"Sex  : ":
                      *P42:5,"Born : ":
                      *P60:5,"Age : ":
                      *P28:6,"Priv : ":
                      *P55:6,"Bus : ":
                      *P35:4,*V2LON,PATNAME:
                      *P35:5,DSEX:
                      *P49:5,PATDOB:
                      *P66:5,PSAGE:
                      *P35:6,PTELEP:
                      *P61:6,PTELEB;
            MOVE      SEVEN,CVERT
          ENDIF
.
          RETURN
.******************************************************************************
SDAT0000
          UNPACK    SP20,WMDAT10,WMDAT20
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      WMDAT10,CPCDATE,SP10
.
          UNPACK    WMDATE2,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      WMDAT20,CPCDATE,SP10
SDAT9999  RETURN
.******************************************************************************
.*               SCRT0000                                                     *
.*          main screen for procedure removal                                 *
.******************************************************************************
.
SCRT0000  DISPLAY   *P1:7,*EF:
                    *P1:8,"    ",WTCNTPDS," Code        : ":
                    *P1:9,"    ",WTCNTPDS," Description : ":
                    *P1:10,"    ",WTCNTPDS," Reason      : ":
                    *P1:11,"    ",WTCNTPDS," Status      : ":
                    *P1:12,"    Date on List          : ":
                    *P1:13,"    Last Review Date      : ":
                    *P1:14,"    ",DDESCWU,*P26:14," : ":
                    *P1:15,"    Doctor                : ":
                    *P1:17,*V2LON," 1",*HOFF,". Removal Reason        : ":
                    *P1:18,*V2LON," 2",*HOFF,". Date of Removal       : "
          RETURN
.******************************************************************************
.*               DIST0000                                                     *
.*          display variables of main menu (Procedure removal menu)           *
.******************************************************************************
DIST0000  LOAD      KEY13,WMSTAT,STAT1,STAT2,STAT3,STAT4,STAT5,STAT6
          DISPLAY   *V2LON,*P30:8,WMCODE:
                    *P30:9,WMDESC:
                    *P30:10,WMREASON:
                    *P30:11,*V2LON,WMSTAT,*HOFF,*P42:11,KEY13:
                    *V2LON,*P30:12,WMDAT10:
                    *P30:13,WMDAT20:
                    *P30:14,WMUNIT,*HOFF,*P42:14,DESCWU:
                    *V2LON,*P30:15,WMDOCTOR,*HOFF,*P42:15,DOCNAME
          RETURN
.****************************************************************************
AUDB0000
          MOVE      TWO,AUDIFLAG       * write a B audit
          CALL      AUDWMTRE
AUDB9999  RETURN
.****************************************************************************
.                                 SELT0000
.*          Select the field to update in procedure screen                  *
.    Modified 28/06/1990 Bill Dew
.    SELT was changed to delete the B audit immeadiately after cancel.
.****************************************************************************
SELT0000
          MOVE      ZERO,CHGT
SELT1000
          CALL      MAINQST
          COMPARE   ZERO,CCITEM
          GOTO      SELT6000 IF EQUAL
          COMPARE   "-1",CCITEM
          GOTO      SELT7000 IF EQUAL
          GOTO      SELT1000 IF LESS
.
          MOVE      ONE,CHGT         * 1= procedure items changed    
          BRANCH    CCITEM TO SELT2000,SELT3000
          BEEP
          GOTO      SELT1000
SELT2000  
          CALL     KEYT2000          * removal reason
          BRANCH   EXIT,SELT7000     * cancel
          GOTO     SELT1000
SELT3000
          CALL     KEYT3000          * removal date
          GOTO     SELT1000
SELT6000
          MOVE     ZERO,EXIT
          GOTO     SELT9999
SELT7000
          MOVE      FIVE,AUDIFLAG
          CALL      AUDWMTRE           * delete the B audit due to cancel
          MOVE      ONE,EXIT
SELT9999  RETURN
.****************************************************************************
.*                  KEYT2000                                                *
.*          Keyin removal reason (category WR in PATCODES)  - mandatory     *
.****************************************************************************
KEYT2000  MOVE      DWMSTAT,FORM1
          BRANCH    FORM1,KEYT2005             * test for status = unsched
          GOTO      KEYTSTAT                     * message rtne
.
KEYT2005  MOVE      FALSE,EXIT
          MOVE      SP3,WMREMOVE
          KEYIN     *P30:17,*DV,UNDLN3,*EL:
                    *P30:17,*V2LON,WMREMOVE:
                    *P30:17,*DV,WMREMOVE,*HOFF
.
          ENDSET    WMREMOVE
          APPEND    SP3,WMREMOVE
          RESET     WMREMOVE
.
          MATCH     SP3,WMREMOVE
          GOTO      KEYT2300 IF EQUAL            * mandatory     
.
          CMATCH    EXITCHAR,WMREMOVE
          GOTO      KEYT2900 IF EQUAL
.
          MATCH     QUEST,WMREMOVE               * ? option
          GOTO      KEYT2100 IF EQUAL 
.
. ------ validate what was entered ------
.
.         PACK      KEY5,CODEBC,WMREMOVE    * code must exist in PATCODES
          PACK      KEY5,CODEWR,WMREMOVE    * changed for SRF 106963
          CALL      RDCODE1
          BRANCH    OVRCD TO KEYT2400
          MOVE      TDESC,DESCWR
          DISPLAY   *P42:17,DESCWR
          MOVE      FALSE,EXIT 
          GOTO      KEYT2500                     * exit routine
.
. ------ ? routine ------
.
KEYT2100  MOVE      ONE,CNEWDS         * new ? routine
.         MOVE      CODEBC,CODE
          MOVE      CODEWR,CODE        * changed for SRF 106963
          CALL      PATCODDS                     * display WR codes
.
KEYT2150  DISPLAY   *P1:24,"Removal Reason : ",*EL;
.
KEYT2200  KEYIN     *P18:24,*EL,*DV,UNDLN3:
                    *P18:24,*V2LON,WMREMOVE:
                    *P18:24,*DV,WMREMOVE
.                    
          ENDSET    WMREMOVE
          APPEND    SP3,WMREMOVE
          RESET     WMREMOVE
.
          MATCH     SP3,WMREMOVE
          GOTO      KEYT2350 IF EQUAL            * mandatory     
.
          CMATCH    EXITCHAR,WMREMOVE
          GOTO      KEYT2900 IF EQUAL
.
          MATCH     QUEST,WMREMOVE               * ? option
          GOTO      KEYT2100 IF EQUAL 
.
.         PACK      KEY5,CODEBC,WMREMOVE         * code must exist in PATCODES
          PACK      KEY5,CODEWR,WMREMOVE         * changed for SRF 106963
          CALL      RDCODE1
          BRANCH    OVRCD TO KEYT2250
          MOVE      TDESC,DESCWR
          MOVE      FALSE,EXIT
          DISPLAY   *P1:4,*EL,"U/R Number : ",*V2LON,CPPURNO;
          CALL      WHED0000                     * PMI heading
          CALL      SCRT0000                     * get procedure screen back
          CALL      DIST0000                     * procedure vars back screen
          DISPLAY   *P30:17,*V2LON,WMREMOVE:
                    *P42:17,*HOFF,DESCWR
          MATCH     SP8,WMDATE4
          GOTO      KEYT2500 IF EQUAL
          UNPACK    WMDATE4 INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      WMDAT40,CPCDATE,SP10
          REP       " 0",WMDAT40
          DISPLAY   *P30:18,*EL,*V2LON,WMDAT40,*HOFF
          GOTO      KEYT2500
.
. ------ invalid code entered at line 24 ------
.
KEYT2250  DISPLAY   *P1:24,*EL,*B,"Invalid Removal Code.  ";
          CALL      HITENTER
          GOTO      KEYT2150
.
. ------ only enter hit at line 16 ------
.
KEYT2300  DISPLAY   *P1:24,*B,"Removal Reason is mandatory.  ",*EL;
          CALL      HITENTER
          GOTO      KEYT2005
.
. ------ only enter hit at line 24 ------
.
KEYT2350  DISPLAY   *P1:24,*B,"Removal Reason is mandatory.  ",*EL;
          CALL      HITENTER
          GOTO      KEYT2150
.
. ------ invalid code entered at line 16 ------
.
KEYT2400  DISPLAY   *P1:24,*EL,*B,"Invalid Removal Code.  ";
          CALL      HITENTER
          GOTO      KEYT2000
.
KEYT2500  MOVE      "42",COL
          MOVE      "17",VERT
          PROC      KTRNDEST                * Keyin transfer destination
.
          MOVE      FALSE,EXIT 
          GOTO      KEYT2999
.
KEYT2900  MOVE      TRUE,EXIT               * exitchar entered
.
KEYT2999  RETURN
+
KEYTSTAT  DISPLAY   *P1:24,*B,"Note : Can only remove if ",WTCNTPDS:
                    " Status = Unscheduled.  ";
          CALL      HITENTER
          GOTO      KEYT2999
+
.*****************************************************************************
.*                  KEYT3000                                                 *
.*          Display date of change (wmdate4)         * not mandatory         *
.*****************************************************************************
KEYT3000  MOVE      THIRTY,CCOL
          MOVE      TEN8,CVERT
          MOVE      ZERO,CHIGHLT
          MOVE      ONE,CDEFDTE             * hit enter once
          MOVE      CDD,CDAY
          MOVE      CMM,CMON
          MOVE      CYY,CYEAR
          MOVE      CCC,CCENT
          REP       " 0",CDAY
          REP       " 0",CMON
          CALL      KEYDATE               * keyin date of removal
.
          PACK      WMDATE4 WITH CCENT,CYEAR,CMON,CDAY    * keyin date
          REP       " 0",WMDATE4
.
          MATCH     WMDATE4,TODAY     * match date of removal to current date
          GOTO      KEYT3400 IF LESS  * removal date cannot be < current date
           
          MATCH     WMDATE1,WMDATE4   * match date on list to removal date
          GOTO      KEYT3500 IF LESS  * removal date cannot be < date on list
          GOTO      KEYT3999
.
.---------- invalid removal date -----
.
KEYT3400  DISPLAY   *P1:24,*B,"Removal date must not be in the future.  ",*EL;
          CALL      HITENTER
          GOTO      KEYT3000
.
KEYT3500  DISPLAY   *P1:24,*B,"Removal date must be after Date on List.  ",*EL;
          CALL      HITENTER
          GOTO      KEYT3000
.
KEYT3999  RETURN
.
NPERD     UNPACK    CPTDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
.
            KEYIN     *P1:24,*EL,*V2LON,*B:
                      "** Error: ",*DV,CPCDATE," not found in Period file. ":
                      "Hit <ENTER> to retry ",*EOFF,ANS,*P1:24,*EL
            GOTO      KEYT3000
.
.*****************************************************************************
.*               REMW0000                                                    *
.*          Remove this procedure --> status changes from 1 to 6             *
.*****************************************************************************
REMW0000  CALL     SAVHIS00
          MOVE     SIX,WMSTAT
          MOVE     TWENTY,WTWMBSCD 	         * Booking Status Code
          MOVE     ONE,NBRFLAG
          CALL     WRTHIS00                  * Write a new record
.
          PACK     KEY19,WMURNO,WMCODE,WMCNT
          CALL     RDATREA1            * validate record still exist
          BRANCH   OVRCD OF REMW9000
.
          CALL     UPTREA1
.
          COMPARE  ONE,PTCNUADT
          GOTO     REMW4000 IF NOT EQUAL
.
          OPEN     WATRTDA1,"watrtdaf"
          MOVE     WTRTTDES,DIM5       * Save the transfer destination
          PACK     KEY19,WMURNO,WMCODE,WMCNT
          MATCH    SP5,WTRTTDES        * Transfer destination = spaces ?
          GOTO     REMW1000 IF NOT EQUAL
.
          CALL     RDWTRTD1
          BRANCH   OVRCD,REMW3900
          MATCH    SP5,WTRTREFR        * Check for referring hospital
          GOTO     REMW2000 IF NOT EQUAL
          CALL     DEWTRTD1
          GOTO     REMW3900
.
REMW1000  CALL     RDWTRTD1            * check if already exist
          BRANCH   OVRCD,REMW3000      * not exist, write a new record
.
REMW2000  MOVE     DIM5,WTRTTDES       * Restore the new transfer dest.
          CALL     UPWTRTD1            * update with the destination code
          GOTO     REMW3900
.
REMW3000  UNPACK   KEY19,WTRTURNO,WTRTPROC,WTRTPCNT
          MOVE     SP10,WTRTREFR       * referring hospital
          MOVE     DIM5,WTRTTDES       * transfer destination
          CALL     WRWTRTD1
.
REMW3900  CLOSE    WATRTDA1
.
REMW4000  MOVE     THREE,AUDIFLAG      * write a C audit
          CALL     AUDWMTRE
.
          MOVE     WMURNO,WTCAURNO
          MOVE     WMCODE,WTCAPROC
          MOVE     WMCNT,WTCAPCNT
.         MOVE     CODEBC,WTCACATF
          MOVE     CODEWR,WTCACATF     * changed for SRF 106963
          MOVE     WMDATE4,WTCAEDAT
          MOVE     WMREMOVE,WTCACODT
          MOVE     SP3,WTCACODF
.         PACK     KEY29,WTCAEDAT,CODEBC,WMURNO,WMCODE,WMCNT
          PACK     KEY29,WTCAEDAT,CODEWR,WMURNO,WMCODE,WMCNT
          CALL     RAWTCAT1            * check if record already exist
          BRANCH   OVRCD,REMW8888
          CALL     UPWTCAT1            * overwrite with new record
          GOTO     REMW9999
.
REMW8888  CALL     WRWTCAT1            * write cancellation record
          GOTO     REMW9999
.
REMW9000  DISPLAY  *P1:24,*EL,*B,"** Missing Treatment record : [",*V2LON:
                   KEY19,"] ** ",*W3;
REMW9999  RETURN
+
.*********************************************************************
.         remove all suspensions
.*********************************************************************
RSUS0000  PACK      KEY21,WMURNO,WMCODE,WMCNT,SP30
          CALL      RSWTSUS1
          CALL      RKWTSUS1
          BRANCH    OVRCD,RSUS9999
          PACK      KEY19,WTSUURNO,WTSUCODE,WTSUCNT,SP30
          MATCH     KEY19,KEY21
          GOTO      RSUS9999 IF NOT EQUAL
          PACK      KEY21,WTSUURNO,WTSUCODE,WTSUCNT,WTSUSCNT
          CALL      DEWTSUS1
          GOTO      RSUS0000
          
RSUS9999  RETURN
+
.*********************************************************************
.         Delete from contracting files
.*********************************************************************
DELC0000  PACK      KEY19,WMURNO,WMCODE,WMCNT
.         PROC      CONDELWL
.
DELC9999  RETURN
.****************************************************************************
.*               DOCT0000                                                   *
.*          Concatenate doctor name                                         *
.****************************************************************************
DOCT0000    MOVE      WMDOCTOR,DCODE
            MOVE      DCODE,KEY6
            CALL      RDDOCT1
            BRANCH    OVRCD TO DOCT9999
            MOVE      DGNAME,PACGNAME
            MOVE      DSNAME,PACSNAME
            MOVE      DTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,DOCNAME
DOCT9999    RETURN
.
.****************************************************************************
.*               UNIT0000                                                   *
.*          Get unit description from patcodes file                         *
.****************************************************************************
.
UNIT0000    PACK      DESCWU WITH SP20
            MATCH     SP3,WMUNIT
            GOTO      UNIT9999 IF EQUAL
            PACK      KEY5 WITH CODEWU,WMUNIT
            CALL      RDCODE1
            BRANCH    OVRCD TO UNIT9999
            MOVE      TDESC,DESCWU
UNIT9999    RETURN
.
GETSVAR     RETURN
.
REDISPS     DISPLAY   *P1:3,*EF:
                      *P1:4,"U/R No.   : ",*V2LON,DIM81:
                      *ULON,*P1:6,"Cnt",*P5:6,WTCNTPDS:
                      *P14:6," Description                      ":
                      *P49:6,"Status",*P56:6," On List  ":
                      *P67:6,"  Review  ",*P78:6,"Pty"
            RETURN
.
.****************************************************************************
.                        AUDIT ROUTINES for WAT01
.                 Created by Bill Dew on the 31/05/1990
.
.    AUDIFLAG : 1   Write a A audit to file
.               2   Write a B audit to file
.               3   Write a C audit to file
.               4   Write a D audit to file
.               5   Delete  B audit to file
.****************************************************************************
.                                 AUDWMTRE
.    Audits for patient waiting list procedure details.
.****************************************************************************
AUDWMTRE
.         DISPLAY   *P1:3,*EL,"HWAUDB ",HWAUDB
          BRANCH    HWAUDB,WMTRE999
.
          COMPARE   FIVE,AUDIFLAG      * test to delete B audit
          GOTO      WMTRE600 IF EQUAL
.
          COMPARE   THREE,AUDIFLAG     * is it an after change audit
          GOTO      WMTRE100 IF EQUAL  * yes, so dont update time
.
          CALL      IBACLOCK           * get current date
          PACK      WTWMAUDD,CCC,CYY,CMM,CDD
          REP       " 0",WTWMAUDD
          CLOCK     TIME,WTWMAUDT      * clock current time
.
WMTRE100  MOVE      ONE,WTWMAUDS       * set print status
          MOVE      PASSCODE,WTWMAUDO  * get user ID
          MOVE      PORT,WTWMAUDP      * get port number
.
          BRANCH    AUDIFLAG,WMTRE200,WMTRE200,WMTRE200,WMTRE200,WMTRE600
          DISPLAY   *P60:24,*V2LON,*BLINKON,"Audit flag not set"
          GOTO      WMTRE999
WMTRE200
          MOVE      AUDIFLAG,WTWMAUDR
          REPLACE   "1A2B3C4D",WTWMAUDR
          PACK      KEY19,WTWMAUDD,WTWMAUDT,WTWMAUDP,WTWMAUDR  
          CALL      AWTREA1            * write to audit file
          GOTO      WMTRE999
WMTRE600                               * Delete B audit
          PACK      KEY19,WTWMAUDD,WTWMAUDT,WTWMAUDP,ANSB
          CALL      ADTREA1            * delete audit file
WMTRE999  RETURN
.****************************************************************************
.
          INC       STD001IO
          INC       BOKRC1IO/INC
          INC       CALCAGE
          INC       TRANDEST
          INC       OUTPREIO/INC
          INC       PATCOMN1
          INC       PATCOMN3
          INC       PATDRGIO/INC
          INC       PATCODKY
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATINVIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATPR1IO/INC
          INC       PATGSRIO/INC
          INC       PATVISIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       SEXDSCIO
          INC       PATSNDX
          INC       PATSNX2
          INC       WATCATIO/INC
          INC       WATTR1IO/INC
          INC       WATHISIO/INC
          INC       WATNBRIO/INC
          INC       WATPROIO/INC
          INC       WATRTDIO/INC
          INC       WATSUSIO/INC
          INC       WATSEAIO/INC
          INC       WATSEIIO/INC
          INC       WEBSECIO/INC
          INC       NZCOMPIO/INC
.         INC       CONDELWL
          INC       WLHISSUB
AGECHK
CLPATMAS  RETURN
...........................................................................

.*****************************************************************************
.*    Program      : FXPATDO1                                                *
.*    Description  : Fix-it for Address Lines in PATDOC1af and PMSHCPaf      *
.*                                                                           *
.*    Author       : Mike Laming   CAR 125736                                *
.*    Date         : 06/06/2007                                              *
.*    Modifications:                                                         *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
          INC       PATDO1FD/INC
          INC       PMSHCPFD/INC
.
.
CMDLINE   DIM       100
NEWLINNO  DIM       2
COUNT     FORM      8
RECNUM    FORM      8
DONE      FORM      1
OLDSPAR   DIM       100                           * decreasing to 100
.
PRGNAM    INIT      "DOCTOR ADDRESS FIX"
PRGID     INIT      "FXPATDO1"
VERSION   INIT      "V12.01.00"
.*****************************************************************************
.
. Start of Program
.
          CALL      INIT0000                      * init and open files
.
MAIN1000  CALL      OPTN0000
          BRANCH    EXIT,MAIN9999
          BRANCH    OPTION,MAIN2000,MAIN3000,MAIN4000
          GOTO      MAIN1000
.
.
MAIN2000  CALL      BKUP0000                * back up file/s
          GOTO      MAIN1000
.
.                                           * process Doctors File
MAIN3000  CALL      CHEK0000                * check file has not been processed
          BRANCH    EXIT,MAIN1000
.
          CALL      READ0000
          GOTO      MAIN1000
.
.                                           * process HCP File
MAIN4000  CALL      CHEK0000                * check DOC file has been processed
          BRANCH    EXIT,MAIN1000
.
          CALL      RHCP0000
          GOTO      MAIN1000
.
.
MAIN9999  CHAIN     PGM
+
.******************************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.******************************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
.
.
INIT9999  RETURN
.
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Back-up Doctor & HCP Files (":
                          "CISAM only)":
                    *P1:6,*V2LON,"2",*HOFF," = Re-align Addresses on Doctor":
                          " file":
                    *P1:7,*V2LON,"3",*HOFF," = Copy Doctor File Address to ":
                          "HCP Address":
                    *P1:9,"Select Option :";
.         
OPTN1000  KEYIN     *P17:9,*DV,UNDLN2,*P17:9,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000,OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                          CHEK0000                                          *
.*          Check that Address Line 4 has never been populated                *
.******************************************************************************
CHEK0000  MOVE      ZERO,EXIT  
          MOVE      ZERO,OVRCD
          MOVE      ZERO,COUNT
          OPEN      PATDO1A1,"patdo1af"
.
          PACK      KEY6,SP10
          CALL      RDSDOCT1
CHEK1000  CALL      RDKDOCT1
          BRANCH    OVRCD,CHEK9000
.
          MATCH     SP35,DSADD4
          GOTO      CHEK1000 IF EQUAL
.
          ADD       ONE,COUNT
          GOTO      CHEK1000
.
.
CHEK9000  CLOSE     PATDO1A1
          BRANCH    OPTION,CHEK9999,CHEK9100,CHEK9200
.
CHEK9100  COMPARE   ZERO,COUNT                   * Option 1.
          GOTO      CHEK9999 IF EQUAL
.
          MOVE      ONE,EXIT
          IF        COUNT < 10
            DISPLAY   *P1:23,"A few records have data in Address line 4 - ";
          ELSE
            DISPLAY   *P1:23,"Address fix-it has already been processed - ";
          ENDIF
          DISPLAY   *P45:23,"Program will not continue ",*P1:24;
          CALL      HITENTER
          GOTO      CHEK9999 
.
CHEK9200  COMPARE   ZERO,COUNT                   * Option 2.
          GOTO      CHEK9999 IF NOT EQUAL
.
          DISPLAY   *P1:23,"Doctor file has not been processed - ";
          DISPLAY   *P45:23,"Program will not continue ",*P1:24;
          CALL      HITENTER
          GOTO      CHEK9999
.
CHEK9999  RETURN
.
.******************************************************************************
.*                          BKUP0000                                          *
.*          Check if backup exists and create if required                     *
.******************************************************************************
BKUP0000  MOVE      ZERO,OVRCD
.
. check if back-up file already exists
.
          TRAP      OVERCOND IF IO
          OPEN      PATDO1A1,"savdo1af"
          TRAPCLR   IO
          BRANCH    OVRCD,BKUP2000
.
. File found. Tell users and check if they want to proceed
.
BKUP1000  KEYIN     *P1:15,*B,*EF,*V2LON,"Doctor file back-up already exists.":
                    *HOFF," Do you want to over-write this (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,"/",*V2LON,"C":
                    *HOFF,") ? ",*V2LON,ANS
          REP       "1 2 3 ",ANS
          REP       "y1Y1n2N2c3C3",ANS
          MOVE      ANS,FORM1
          BRANCH    FORM1,BKUP2000,BKUP3000,BKUP9000
          GOTO      BKUP1000
.
BKUP2000  DISPLAY   *P1:15,*B,*EF
          EXECUTE   "cp `ldat patdo1af.dat` savdo1af.dat",TASKID
          EXECUTE   "cp `ldat patdo1af.idx` savdo1af.idx",TASKID
.
.
BKUP3000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PMSHCPA1,"savhcpaf"
          TRAPCLR   IO
          BRANCH    OVRCD,BKUP5000
.
. File found. Tell users and check if they want to proceed
.
BKUP4000  KEYIN     *P1:15,*B,*EF,*V2LON,"HCP file back-up already exists.":
                    *HOFF," Do you want to over-write this (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,"/",*V2LON,"C":
                    *HOFF,") ? ",*V2LON,ANS
          REP       "1 2 3 ",ANS
          REP       "y1Y1n2N2c3C3",ANS
          MOVE      ANS,FORM1
          BRANCH    FORM1,BKUP5000,BKUP8000,BKUP9000
          GOTO      BKUP1000
.
BKUP5000  DISPLAY   *P1:15,*B,*EF
          EXECUTE   "cp `ldat pmshcpaf.dat` savhcpaf.dat",TASKID
          EXECUTE   "cp `ldat pmshcpaf.idx` savhcpaf.idx",TASKID
.
BKUP8000  DISPLAY   *P1:23,*B,*EF,*V2LON,"Back-up/s are complete. ":
                    *P1:24,*B
          CALL      HITENTER
.
BKUP9000  MOVE      ZERO,OVRCD
.
BKUP9999  RETURN
+
.******************************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.******************************************************************************
READ0000  MOVE      ZERO,EXIT
          PACK      KEY6,SP30
          OPEN      PATDO1A1,"patdo1af"
          CALL      RDSDOCT1                      * position at start
READ1000  CALL      RDKDOCT1                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
          MATCH     SP35,DSADD4
          IF        @EQUAL
            MOVE      DSADD3,DSADD4
            MOVE      DSADD2,DSADD3
            PACK      DSADD2,SP35
          ENDIF
          MATCH     SP35,DHADD4
          IF        @EQUAL
            MOVE      DHADD3,DHADD4
            MOVE      DHADD2,DHADD3
            PACK      DHADD2,SP35
          ENDIF
.
          CALL      UPDOCT1                       * update file
.
          IF        (RECNUM%50) = 0
            PACK      KEY8,DCODE,SP10
            DISPLAY   *P10:14,*V2LON,RECNUM,*P20:14,KEY8
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     PATDO1A1                      * close new file
.
          CALL      IBACLOCK
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:18,"Ended   : ",CDATE,SP1,CTIMEIS
.
READ7000  DISPLAY   *P1:23,*B,*EF,*V2LON,"Doctor File changes are complete. ":
                    *P1:24,*B
          CALL      HITENTER
.
READ9000  DISPLAY   *P1:23,*EF
.
READ9999  RETURN
.
.******************************************************************************
.*                               RHCP0000                                     *
.*             loop thru Doctors file and update Addresses in HCP file        * 
.******************************************************************************
RHCP0000  MOVE      ZERO,EXIT
          MOVE      ZERO,RECNUM
          PACK      KEY6,SP30
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PMSHCPA1,"pmshcpaf"
          CALL      RDSDOCT1                      * position at start
RHCP1000  CALL      RDKDOCT1                      * read next record
          BRANCH    OVRCD,RHCP5000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          PACK      KEY10,DCODE,SP10
          CALL      RDPMHCP1
          BRANCH    OVRCD,RHCP1000
.
          MOVE      ZERO,DONE
.                                                 * check all address lines
RHCP2000  MATCH     SP70,PMHCADR1                 * Surgery address
          IF        !@EQUAL
            MATCH     SP70,PMHCADR2
            IF        !@EQUAL
              MATCH     SP70,PMHCADR3
              IF        !@EQUAL
                MATCH     SP70,PMHCADR4
                IF        !@EQUAL
                  GOTO      RHCP2100      * all lines in use - bypass
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          PACK      PMHCADR1,DSADD1,SP70
          PACK      PMHCADR2,DSADD2,SP70
          PACK      PMHCADR3,DSADD3,SP70
          PACK      PMHCADR4,DSADD4,SP70
          MOVE      ONE,DONE
.
RHCP2100  MATCH     SP70,PMHCHAD1                 * Home address
          IF        !@EQUAL
            MATCH     SP70,PMHCHAD2
            IF        !@EQUAL
              MATCH     SP70,PMHCHAD3
              IF        !@EQUAL
                MATCH     SP70,PMHCHAD4
                IF        !@EQUAL
                  GOTO      RHCP2500      * all lines in use - bypass
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          PACK      PMHCHAD1,DHADD1,SP70
          PACK      PMHCHAD2,DHADD2,SP70
          PACK      PMHCHAD3,DHADD3,SP70
          PACK      PMHCHAD4,DHADD4,SP70
          MOVE      ONE,DONE
.
RHCP2500  BRANCH    DONE,RHCP3000
          GOTO      RHCP1000
.
RHCP3000  CALL      UPPMHCP1 
.
          IF        (RECNUM%50) = 0
            PACK      KEY10,PMHCHCPC,SP30
            DISPLAY   *P10:14,*V2LON,RECNUM,*P20:14,KEY8 
          ENDIF
.
          GOTO      RHCP1000                      * get next record
.
RHCP5000  CLOSE     PMSHCPA1                      * close new file
          CLOSE     PATDO1A1                      * close old file
          DISPLAY   *P10:14,*V2LON,RECNUM,*P20:14,KEY8
.
          CALL      IBACLOCK
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:18,"Ended   : ",CDATE,SP1,CTIMEIS
.
RHCP7000  DISPLAY   *P1:23,*B,*EF,*V2LON,"HCP File processing is complete.":
                    *P1:24,*B;
          CALL      HITENTER   
.
RHCP9000  DISPLAY   *P1:23,*EF
.
RHCP9999  RETURN
.
.******************************************************************************
.******************************************************************************
.
          INC       PATDO1IO/INC
          INC       PMSHCPIO/INC
          INC       STD001IO

. ************************************************************************
. *                    P R O G R A M  S U M M A R Y                      *
. *                    - - - - - - -  - - - - - - -                      *
. *                                                                      *
. *     PROGRAM NAME:    IBARSH01                                        *
. *                                                                      *
. *     FUNCTION:        Report Scheduler Initialisation                 *
. *                             FOR ALL SYSTEMS                          *
. *                      To enable any existing Unicare Report to        *
. *                      be executed by the Report Scheduler.            *
. *                                                                      *
. *     AUTHOR:          Pat Dirito                                      *
. *                                                                      *
. *     MODIFICATIONS:                                                   *
. *     V11.02.02  05/04/2022  Ebon Clements  Task 0899288               *
. *                Use scheduler records OP site for IBAOUT71            *
. *     V11.02.01  23/03/2022  Jill Parkinson Task 0917341               *
. *                Added REPTNAME to pass through to reports             *
. *                                                                      *
. *                    V11.00.01  05/03/2020 Jill Parkinson 0879613      *
. *                               Added patcodes                         *
. *                    V10.02.01  19.04.2011 Jill Parkinson CAR 239574   *
. *                               Mods to call OPNPRINT instead of WEBOPNPR*
. *                    V10.00.02  19.07.2010 Jill Parkinson CAR 225573   *
. *                               Added write to ibaspolf                *
. *                    V10.00.01  16.12.2009 Jill Parkinson CAR 218988   *
. *                               Fixed V9.12.01 changes to save port    *
. *                    V9.12.02   16.12.2009 Jill Parkinson CAR 209483   *
. *                               Removed previous changes               *
. *                    V9.12.01   16.12.2009 Jill Parkinson CAR 209483   *
. *                               Mods to use ports 01-99                *
. *                    V9.02.01   10.10.2001 B.G.Ackland                 *
. *                               Wait on Lock to be Released for Report *
. *                    V9.00.B02  29.06.2001 B.G.Ackland                 *
. *                               Change to WEBSCHFD to for TRU64 Cluster*
. *                    V9.00.B01  15.06.2001  Ebon Clements              *
. *                               Set up outpatient site from websec     *
. *                               file for outpatient reports            *
. *                    C2.00.02   15.09.2000  Sandra Barcham             *
. *                               Include RSHCONFD into WEBCONFD         *
. *                    C2.00.01   12/04/2000  B.G.Ackland & Pat Dirito   *
. *                               Created Program                        *
. *                                                                      *
. ************************************************************************
          INC       STD002FD
          INC       IBASECFD/INC
          INC       CHECKDAT/INC
          INC       OUTSITFD/INC    * Web Security File             
          INC       PATCODFD/INC
          INC       WEBCONFD/INC    * Web Security File             
          INC       WEBERRFD/INC    * Web Error File             
          INC       WEBSECFD/INC    * Web Security File             
          INC       WEBSCHFD/INC    * Report Server Schedule Table
.
HUNDRED2  FORM      "102"
CMDLINE   DIM       127
VAR       DIM       127
LOCKCNT   FORM      2
REPTSCHD  DIM       8
PROGNAME  DIM       8
FLAGALL   FORM      1
SAVEPORT  DIM       2
.------------------------------------------------------------------------
PRGNAM    INIT      "Report Initialisation"
PRGID     INIT      "IBARSH01"
VERSION   INIT      "V12.01.00"
.------------------------------------------------------------------------
MAIN0000  CALL      SETX0000   * Initialise Common
          CALL      INIT0000   * Initialise
.
          KEYIN     "Schedule No",REPTSCHD
.
.        * Read Report Schedule Table
.
          MOVE      REPTSCHD,KEY8
          CALL      RDWBSC1           
          BRANCH    OVRCD,MAIN9100
.
          MOVE      WBSCRPID,PROGNAME  * Determine Program ID to Execute
.
.        * Determine Pass Code based on Web User ID (Read WEBSECFD)
.
          MOVE      WBSCUSER,KEY10
          CALL      RDWBSE1            * Read Web Security File 
          BRANCH    OVRCD,MAIN9200
.
          MATCH     "IBAOUT71",WBSCRPID     * OP Letters
          IF        @EQUAL
            MATCH     SP70,WBSCSITE
            IF        !@EQUAL
              PACK      KEY6,WBSCSITE,SP70  * use OP site from scheduler
              GOTO      MAIN0500
            ENDIF
          ENDIF
.
          MOVE      WBSESIT,KEY6
MAIN0500  CALL      RDSITA1
          IF        OVRCD=0
            MOVE      OSTSITE,IDDD   ***** COMMON
            MOVE      OSTFILE,UID    ***** COMMON SITE FILE PREFIX
          ELSE
            MOVE      SP70,IDDD   ***** COMMON
            MOVE      SP70,UID    ***** COMMON SITE FILE PREFIX
          ENDIF
.
          MOVE      WBSEPCD,PASSCODE   * Passcode Set
.
.        * Update Report Schedule Table with Process ID & Status
.
          MOVE      ZERO,LOCKCNT
MAIN1000  MOVE      REPTSCHD,KEY8
          CALL      RLWBSC1          * Readlock Record 
          BRANCH    OVRCD,MAIN9100,MAIN9400
.
          CLOCK     PID,WBSCACTV     * Clock Process ID 
          MOVE      ZERO,WBSCSTAT    * Set to 0-current execution
          CALL      UPWBSC1          * Update Record 
          CALL      UUWBSC1          * Unlock Record 
.
          MOVE      PASSCODE,KEY4
          CALL      RDPASS1
          BRANCH    OVRCD,MAIN9300
.
          MOVE      WBSCDSRP,REPTNAME
          MOVE      "IBARSH99",PGM
          PACK      CCMENU,REPTSCHD,SP70
.
          MOVE      ONE,PPAPER       * Default of plain paper report
          MOVE      SP8,SPLFILE
          MOVE      PPAPER,CPPAPER
          MOVE      PSECLEV,CPROGLEV
.
          PACK      SAVEPORT,PORT,SP70
.....     CALL      WEBOPNPR
          CALL      OPNPRINT
          PACK      PORT,SAVEPORT,SP70
          MOVE      PORT,PORTNO
.
          MATCH     SP70,PROGNAME
          GOTO      MAIN2000 IF EQUAL
.
          MOVE      THREE,CPRTFLG       * No Printer Selection in Programs
          TRAP      MAIN9500 IF CFAIL
          CHAIN     PROGNAME
.
          GOTO      MAIN9999
.
MAIN2000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUNDRED2;*1,RSCNCCP,RSCNSLP,RSCNRCD,RSCNROD
.
          STRIP     WBSCUNIX
          MOVELPTR  WBSCUNIX,F3
          SFORMAT   VAR,127  
          SFORMAT   VAR,F3   
          MOVE      WBSCUNIX,VAR
.
          CLEAR     CMDLINE
          APPEND    VAR,CMDLINE
          APPEND    SP1,CMDLINE
.
          STRIP     RSCNRCD
          MOVELPTR  RSCNRCD,F3
          SFORMAT   VAR,127  
          SFORMAT   VAR,F3   
          MOVE      RSCNRCD,VAR
          APPEND    VAR,CMDLINE
          APPEND    REPTSCHD,CMDLINE
          APPEND    ".cfg ",CMDLINE
          APPEND    "$TBSPOOL/",CMDLINE
          APPEND    SPLFILE,CMDLINE
          APPEND    ".prt ",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          CHAIN     PGM
.
          GOTO      MAIN9999
.
MAIN9100  DISPLAY   *R,"Invalid Schedule No. ",KEY4
          GOTO      MAIN9999
.
MAIN9200  DISPLAY   *R,"Invalid Web User ID ",KEY4
          GOTO      MAIN9999
.
MAIN9300  DISPLAY   *R,"Invalid Passcode ",KEY4
          GOTO      MAIN9999
.
MAIN9400  DISPLAY   *W2
          ADD       ONE,LOCKCNT
          IF        LOCKCNT<20
            GOTO      MAIN1000
          ENDIF
          DISPLAY   *R,"Report Record Locked ",KEY8
          GOTO      MAIN9999
.
MAIN9500  NORETURN
          TRAPCLR   CFAIL
          DISPLAY   *R,"Program not Available.",PROGNAME
          GOTO      MAIN9999
.
MAIN9999  SHUTDOWN  " "
.----------------------------------------------------------------------  
. Initialization 
.----------------------------------------------------------------------
INIT0000  OPEN      IHAPASS1,"ihapassf"
          OPEN      WEBERRA1,"weberraf"
          OPEN      WEBSCHA1,"webschaf"
          OPEN      WEBSECA1,"websecaf"
          OPEN      OUTSITA1,"outsitaf"
          OPEN      PATCODE1,"patcodes"
.
INIT9999  RETURN
.
.**********************************************************************
.*                            WEBOPNPR                                *
.*                        Open a Print File                           *
.**********************************************************************
.WEBOPNPR  COMPARE     ZERO,CPRTFLG
.          GOTO        WEBOP999 IF NOT EQUAL
.          MATCH       SP8,SPLFILE
..          GOTO        WEBOP999 IF NOT EQUAL
.          OPEN        IBAPORT1,"ibaportf"
.
.          MOVE        ZERO,PORTNO
.WEBOP010  MOVE        ZERO,FLAGALL
.          ADD         ONE,PORTNO
.          MOVE        PORTNO,PORT
.          REPLACE     " 0",PORT
.
.
.         Generate a unique file name
.
.          MOVE        PORT,KEY2
.          MOVE        PORT,CPORTNO
.          CALL        RDPRNT1
.
.          COMPARE     ZERO,OVRCD
.          GOTO        WEBOP100 IF EQUAL
.
.
.          CALL        WRITE000              * Write a Print File Record
.          GOTO        WEBOPNPR
.
.         Pack up unique file name, and increment spool id (for next file)
.
.WEBOP100  PACK        SPLFILE WITH FILEHDR,PORT,CSPOOLID
.          COMPARE     "999",CSPOOLID
.          GOTO        WEBOP200 IF EQUAL
.
.          ADD         ONE,CSPOOLID
.          GOTO        WEBOP300
.
.WEBOP200  COMPARE     ZERO,FLAGALL
.          IF          !@EQUAL
.            MATCH       "99",PORT
.            CALL        PRTERR00 IF EQUAL
.            GOTO        WEBOP010
.          ENDIF
.
.          MOVE        ONE,FLAGALL
.          MOVE        ZERO,CSPOOLID
.
.WEBOP300  RESET       SPLFILE
.          REP         " 0",SPLFILE
.
.         Check if this filename is currently spooled
.          OPEN        IBASPOL1,"ibaspolf"
.          PACK        KEY8,SPLFILE
.          CALL        RDSPOL1
.          IF          OVRCD = 1
.            PACK        CSPDESC,PROGNAME,SP30
.            PACK        CSPDATE,CCC,CYY,CMM,CDD
.            REP         " 0",CSPDATE
.            CLOCK       TIME,CSPTIME
.            MOVE        PASSCODE,CSPOPER
.            MOVE        SPLFILE,CSPNAME
.            MOVE        SPLFILE,KEY8
.            MOVE        CPROGLEV,CSPLEVE
.            MOVE        CDEPCDE,CSPDEPT
.            CALL        WRSPOL1
.          ELSE
.            GOTO        WEBOP100             * currently spooled, don't use
.          ENDIF
.          CLOSE       IBASPOL1
.
.         Open spool file and update next id.
.
.          SPLOPEN     SPLFILE
.          CALL        UPPRNT1
.
.          MOVE        PRGID,IBAPPROG           * Current program being accessed
.          CALL        WRPORT1
..
.WEBOP999  RETURN
.
.------------------------------------------------------------
. Log Error to Web Error Log
.------------------------------------------------------------
PRTERR00  CLOCK     TIME,CTIMEIS
          MOVE      "IBARSH01",PRGID
          PACK      KEY24,PRGID,CCC,CYY,CMM,CDD,CTIMEIS,SP70
          REP       " 0",KEY24
          CALL      RDWBER1
          IF        OVRCD=0
            GOTO      PRTERR99
          ENDIF
          UNPACK    KEY24,WBERPID,WBERDAT,WBERTIM
          MOVE      "Too many spool files.",WBERERR
          PACK      WBERUID,SP70
          PACK      WBERREP,SP70
          PACK      WBERTMP,SP70
          PACK      WBERURN,SP70
          PACK      WBERVIS,SP70
          MOVE      "1",WBERCNT
          MOVE      SP70,WBERSPA
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRWBER1
          TRAPCLR   IO
.
PRTERR99  RETURN
+
.**********************************************************************
.*                          I/O Includes                              *
.**********************************************************************
.
          INC       STD002IO
.
          INC       WEBERRIO/INC    * Web Error File             
          INC       WEBSECIO/INC    * Web Security File             
          INC       WEBSCHIO/INC    * Report Server Schedule Table
          INC       OUTSITIO/INC    * Web Security File             
          INC       PATCODIO/INC
.

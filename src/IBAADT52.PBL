.******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAADT52                                                 *
.* Description    :  Mapping HAC to ICD10 Upload                              *
.******************************************************************************
.* Date           :  16/04/2019                                               *
.* Author         :  J.Tan                                                    *
.* Function       :  Upload program to Map HAC to ICD10 from ASCII file       *
.* Modifications  :                                                           *
.*----------------------------------------------------------------------------*
.* V12.01.01 11/12/2025  J.Tan         TSK 0956147                            *
.*           Mod for HAF (HAC Flags)                                          *
.* V12.00.01 13/08/2025  J.Tan         TSK 0964813                            *
.*           Added ICD10 Ed13                                                 *
.* V11.03.01 15/02/2023  Bella Turco   TSK 0929362                            *
.*           Added ICD10 Ed12                                                 *
.* V10.14.00 16/04/2019  J.Tan         TSK 0857392                            *
.*           Created new program                                              *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PMSVSCFD/INC
          INC       PMSVMTFD/INC
          INC       PATCODFD/INC
          INC       PATICDFD/INC
          INC       PATCONFD/INC
.
.         Mapping HAC or HAF to ICD10 Upload file
.
HACICTXT  FILE       HL7, FIXED=1000  * Upload file for mapping HAC/HAF to ICD10
.
.Name     Type     Size     Start
.----     ----     ----     -----
XICDCD    DIM         9     ICD10 Disease Code
XHACCD    DIM         3     HAC or HAF Group
.
. ----- Program Variables -----
.
CRETURN   INIT      015                * carriage return character
CMDLINE   DIM       100
ERRFLAG   FORM      1
ERRNUMB   FORM      6
FNAMEI    DIM       150
FORM12P2  FORM      12.2
HACFLAG   DIM       3
HACCODE   INIT      "HAC"
HAFCODE   INIT      "HAF"
ICD10EDT  DIM        8
RECNUMB   FORM       6
RECWRDT   FORM       6
RECUPDT   FORM       6
USERID    DIM       10
PATHNAME  DIM       100
TIMEIS    DIM       8
.
XFLD0001  DIM       30
XFLD0002  DIM       30
.
PRGID     INIT      "IBAADT52"
PRGNAM    INIT      "Mapping HAC to ICD10 Upload Program"
VERSION   INIT      "V12.01.01"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000              * Initailize variables & open files
          CALL      KOPT0000              * Keyin Upload or Update option
          BRANCH    EXIT,ML9999
.
          CALL      KASC0000              * Keyin ascii file
          BRANCH    EXIT,ML9900
.
          CALL      KUSR0000              * Keyin user id
          BRANCH    EXIT,ML0000
.
          CALL      KPTH0000              * Keyin pathname
          BRANCH    EXIT,ML0000
.
          CALL      KEDT00000             * Keyin edition
          BRANCH    EXIT,ML0000
.
          CALL      PRHD0000              * Print header
.
          CALL      CTOT0000                * Clear total vars
.
          CALL      POST0000              * Updating file
.
          CALL      PTOT0000                * Print report totals
.
          GOTO      ML9999
.
ML9900    CALL      HEAD132
          PRINT     *N,"Failed to open upload file. "
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000                                   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,"Opening pmsvmtaf";
          OPEN      PMSVMTA1,"pmsvmtaf"
          OPEN      PMSVSCA1,"pmsvscaf"
          OPEN      PATCODE1,"patcodes"
          CALL      OPICD1              * Open ICD10 V1-4 Dis file
.
INIT9999  RETURN
+
.******************************************************************************
.*                                 KOPT0000                                   *
.*                            Upload Mapping HAC to ICD10                     *
.******************************************************************************
KOPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COPTION
          DISPLAY   *P1:6,*EF:
                    *P1:6,*V2LON,"0",*HOFF," = Exit":
                    *P1:7,*V2LON,"1",*HOFF," = Mapping HAC to ICD10":
                    *P1:8,*V2LON,"2",*HOFF," = ":
                         "Mapping HAF (HAC Flags) to ICD10":
                    *P1:10,"Select Option : ";
.
KOPT1000  KEYIN     *P17:10,*V2LON,COPTION;
          BRANCH    COPTION,KOPT9999,KOPT9999
.
          COMPARE   ZERO,COPTION
          GOTO      KOPT9000 IF EQUAL
          BEEP
          GOTO      KOPT1000
.
KOPT9000  MOVE      ONE,EXIT
KOPT9999  RETURN
+
.******************************************************************************
.*                                 KASC0000                                   *
.*                            Keyin ASCI file                                 *
.******************************************************************************
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEI WITH SP70,SP70,SP70
.
          DISPLAY   *P19:13,*EF,"Mapping HAC to ICD10 Load File";
.
          KEYIN     *P1:14,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEI
.
          PACK      FNAMEI,FNAMEI,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEI
          GOTO      KASC9000 IF EQUAL
.
          SCAN      ".txt",FNAMEI
          IF        !@EQUAL
            SQUEEZE   FNAMEI
            ENDSET    FNAMEI
            APPEND    ".txt",FNAMEI      * add ".txt" if there is a path
            APPEND    SP70,FNAMEI
            RESET     FNAMEI
          ENDIF
          RESET     FNAMEI
.
          DISPLAY   *P26:14,*V2LON,*EL,FNAMEI;
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      HACICTXT,FNAMEI
          TRAPCLR   IO
          BRANCH    OVRCD,KASC8000
.
          CALL      EXCUS000
.
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
+
.******************************************************************************
.*                                  KUSR0000                                  *
.*                     Keyin user id                                          *
.******************************************************************************
KUSR0000  KEYIN     *P1:16,*EL,"Keyin Username: ",*P26:16,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.******************************************************************************
.*                                  KPTH0000                                  *
.*                     Keyin path name                                        *
.******************************************************************************
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P1:17,*EL,"Keyin Pathname: ",*P26:17,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
.         PACK      PATHNAME,PATHNAME,SP70,SP70,SP70
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.******************************************************************************
.*                                  KEDT0000                                  *
.*                     Keyin ICD10 Edition                                    *
.******************************************************************************
KEDT0000  DISPLAY   *P1:24,*EL,"Edition 10, 11, 12 or 13";
.
          PACK      KEY2,SP70
          KEYIN     *P1:19,*EL,"Keyin ICD10 Edition: ":
                    *P26:19,*V2LON,*JR,KEY2;
          ENDSET    KEY2
          APPEND    SP70,KEY2
          RESET     KEY2
.
          MATCH     SP70,KEY2
          GOTO      KEDT9500 IF EQUAL
.
          MATCH     "10",KEY2
          IF        @EQUAL
            MOVE      "pat10dxf",ICD10EDT
          ELSE
            MATCH     "11",KEY2
            IF        @EQUAL
              MOVE      "patd11af",ICD10EDT
            ELSE
              MATCH     "12",KEY2
              IF        @EQUAL
                MOVE      "patd12af",ICD10EDT
              ELSE
                MATCH     "13",KEY2
                IF        @EQUAL
                  MOVE      "patd13af",ICD10EDT
                ELSE
                  DISPLAY   *P1:24,*EL,"Invalid Edition number"
                  CALL      HITENTER
                  GOTO      KEDT0000
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      KEDT9999
.
KEDT9500  MOVE      ONE,EXIT
KEDT9999  RETURN
+
.******************************************************************************
. Clear total vars
.******************************************************************************
CTOT0000  MOVE      ZERO,RECWRDT
          MOVE      ZERO,RECUPDT
          MOVE      ZERO,ERRNUMB
CTOT9999  RETURN
+
.******************************************************************************
.*                                  POST0000                                  *
.*                        Post record                                         *
.******************************************************************************
POST0000  MATCH     "patd11af",ICD10EDT
          IF        @EQUAL
            OPEN      PATD11A1,"patd11af"
          ELSE
            MATCH     "pat10dxf",ICD10EDT
            IF        @EQUAL
              OPEN      PATI1XD1,"pat10dxf"
            ELSE
              MATCH     "patd12af",ICD10EDT
              IF        @EQUAL
                OPEN      PATD12A1,"patd12af"
              ELSE
                MATCH     "patd13af",ICD10EDT
                IF        @EQUAL
                  OPEN      PATD13A1,"patd13af"
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
.         COPTION 1=HAC, 2=HAF
.
          MOVE      HACCODE,HACFLAG
          LOAD      HACFLAG,COPTION,HACCODE,HAFCODE
.
          MOVE      ZERO,RECNUMB
          OPEN      HACICTXT,FNAMEI
POST1000  READ      HACICTXT,SEQ;XFLD0001,XFLD0002
          GOTO      POST9000 IF OVER
.
          UNPACK    SP70,XICDCD,XHACCD
          PACK      XICDCD,XFLD0001,SP70
          PACK      XHACCD,XFLD0002,SP70
.
          PACK      KEY12,XICDCD,XHACCD,SP70
          MATCH     SP70,KEY12
          GOTO      POST1000 IF EQUAL
.
          ADD       ONE,RECNUMB
.
          CALL      VALC0000              * Validating
          BRANCH    ERRFLAG,POST1000
.
          CALL      UICD0000              * Update Mapping HAC to ICD10 Record
          GOTO      POST1000
.
POST9000  CLOSE     HACICTXT
.
POST9999  RETURN
+
.******************************************************************************
.  Print total line
.******************************************************************************
PTOT0000  PRINT     *1,"----------------------------------":
                    "--------------------------------------"
.
          PRINT     *N,*1,"Number of Records in txt file : ",RECNUMB:
                    *N,*1,"Number of Records Added       : ",RECWRDT:
                    *N,*1,"Number of Records Updated     : ",RECUPDT:
                    *N,*1,"Number of Errors occured      : ",ERRNUMB
PTOT9999  RETURN
+
.******************************************************************************
.                             VALC0000
.                         Validation
.******************************************************************************
VALC0000  MOVE      ZERO,ERRFLAG
          MATCH     SP70,XICDCD
          GOTO      VALC8000 IF EQUAL
.
          MOVE      XICDCD,KEY9
          CALL      RDPTICDD
          BRANCH    OVRCD,VALC1000             * ICD10 code exactly match ?
          GOTO      VALC2000                   * Yes, ICD Code is verified
.
VALC1000  UNPACK    KEY9,KEY3,KEY6
          PACK      KEY9,KEY3,SP6
          MATCH     XICDCD,KEY9                * ICD code group supplied ?
          GOTO      VALC8200 IF NOT EQUAL      * No, Invalid ICD10 code
.
          CALL      RSPTICDD                   * ICD group, Positional read
          CALL      RKPTICDD                   * First ICD code for group
          BRANCH    OVRCD,VALC8300             * No, Invalid ICD10 group
.
          UNPACK    DPCODE,KEY3,KEY6
          PACK      KEY9,KEY3,SP6              * Group code from first ICD
          MATCH     XICDCD,KEY9                * Does group code match ?
          GOTO      VALC8300 IF NOT EQUAL      * No, Invalid ICD10 group
.
VALC2000  MATCH     SP70,XHACCD
          GOTO      VALC8400 IF EQUAL
.
          MOVE      "Ha",KEY2
          PACK      KEY5,KEY2,XHACCD
          CALL      RDCODE1
          BRANCH    OVRCD,VALC8600
.
          MATCH     "A",PTCOACTV
          GOTO      VALC8600 IF NOT EQUAL       * Not active
.
          PACK      KEY33,HACFLAG,XHACCD,SP70
          CALL      RDPMVSC1
          BRANCH    OVRCD,VALC8500
.
          MATCH     "1",PMVCACTV
          GOTO      VALC8600 IF NOT EQUAL       * Not active
.
          MOVE      ZERO,EXIT
          GOTO      VALC99999
.
VALC8000  CALL      PREC0000                  * Print record details
          PRINT     *40,"ICD10 Disease code must not be blank"
          GOTO      VALC9000
.
VALC8200  CALL      PREC0000                  * Print record details
          PRINT     *40,"Invalid ICD10 Disease Code"
          GOTO      VALC9000
.
VALC8300  CALL      PREC0000                  * Print record details
          PRINT     *40,"Invalid ICD Disease Code Group"
          GOTO      VALC9000
.
VALC8400  CALL      PREC0000                  * Print record details
          IF        COPTION=2
            PRINT     *40,"HAF Group Code must not be blank"
          ELSE
            PRINT     *40,"HAC Group Code must not be blank"
          ENDIF
          GOTO      VALC9000
.
VALC8500  CALL      PREC0000                  * Print record details
          IF        COPTION=2
            PRINT     *40,"Invalid HAF Group Code"
          ELSE
            PRINT     *40,"Invalid HAC Group Code"
          ENDIF
          GOTO      VALC9000
.
VALC8600  CALL      PREC0000                  * Print record details
          IF        COPTION=2
            PRINT     *40,"Invalid or Inactive HAF Group Code"
          ELSE
            PRINT     *40,"Invalid or Inactive HAC Group Code"
          ENDIF
          GOTO      VALC9000
.
VALC9000  MOVE      ONE,EXIT
          ADD       ONE,ERRNUMB
VALC9999  RETURN
+
.----------------------------------------------------------------------
. Subroutine to post mapping HAC or HAF to ICD10 
.----------------------------------------------------------------------
UICD0000  PACK      KEY33,HACFLAG,XHACCD,SP70
          CALL      RDPMVSC1
          BRANCH    OVRCD,UICD9999
.
          PACK      PMVMTIDX,XICDCD,SP70
          PACK      KEY41,ICD10EDT,PMVMTIDX,HACFLAG,SP70
          CALL      RDPMVMT1
          IF        OVRCD=1
            UNPACK    KEY41,PMVMTNAM,PMVMTIDX,PMVMMVSI
            MOVE      XHACCD,PMVMMCOD
            PACK      PMVMMDES,PMVCPTRM,SP70
            MOVE      SP70,PMVMSPAR
            CALL      WRPMVMT1
            ADD       ONE,RECWRDT
          ELSE
            MOVE      XHACCD,PMVMMCOD
            PACK      PMVMMDES,PMVCPTRM,SP70
            CALL      UPPMVMT1
            ADD       ONE,RECUPDT
          ENDIF
UICD9999  RETURN
+
.******************************************************************************
.         Print record
.******************************************************************************
PREC0000  PRINT     *1,RECNUMB,*12,XICDCD,*27,XHACCD;
          ADD       ONE,ERRFLAG
PREC9999  RETURN
+
.******************************************************************************
.*                                  PRHD0000                                  *
.*                        Print the selection details                         *
.******************************************************************************
PRHD0000  CALL      HEAD132
          MOVE      "HAC to ICD10             ",KEY25
          IF        COPTION=2
            MOVE      "HAF (HAC Flags) to ICD10 ",KEY25
          ENDIF
.
          PRINT     *N,"Date               : ",CDATE:
                    *N,"Time               : ",CTIMEIS:
                    *N,"User ID            : ",USERID:
                    *N,"Load Option        : ",KEY25
.
          MATCH     "patd11af",ICD10EDT
          IF        @EQUAL
            PRINT      "ICD10 Edition      : 11"
          ELSE
            MATCH     "pat10dxf",ICD10EDT
            IF        @EQUAL
              PRINT      "ICD10 Edition      : 10"
            ELSE
              MATCH     "patd12af",ICD10EDT
              IF        @EQUAL
                PRINT      "ICD10 Edition      : 12"
              ELSE
                MATCH     "patd13af",ICD10EDT
                IF        @EQUAL
                  PRINT      "ICD10 Edition      : 13"
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          PRINT     "Mapping Load file   : ",PATHNAME;
          IF        COPTION=2
            PRINT     *N,*N,*1," Line No.",*12,"ICD10 Code",*27,"HAF Code";
          ELSE
            PRINT     *N,*N,*1," Line No.",*12,"ICD10 Code",*27,"HAC Code";
          ENDIF
          PRINT     *40,"Error Description":
                    *N,*1,"----------------------------------":
                    "--------------------------------------"
PRHD9999  RETURN
+
.******************************************************************************
.         Execute us1 command before processing
.******************************************************************************
EXCUS000  CLEAR     CMDLINE
          APPEND    "ibaadt52.us1 ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
EXCUS999  RETURN
+
.******************************************************************************
.         I/O for ICD
.******************************************************************************
RDPTICDD  MATCH     "patd11af",ICD10EDT
          IF        @EQUAL
            MOVE      "11",ICDRDVER         * Yes, set version ind to ICD10 Ed11
            CALL      RDPD11A1              * Read ICD10 Ed11 file
          ELSE
            MATCH     "pat10dxf",ICD10EDT
            IF        @EQUAL
              MOVE      "10",ICDRDVER       * Yes, set version ind to ICD10 Ed10
              CALL      RDPT1XD1            * Read ICD10 Ed10 file
            ELSE
              MATCH     "patd12af",ICD10EDT
              IF        @EQUAL
                MOVE      "12",ICDRDVER     * Yes, set version ind to ICD10 Ed12
                CALL      RDPD12A1            * Read ICD10 Ed12 file
              ELSE
                MATCH     "patd13af",ICD10EDT
                IF        @EQUAL
                  MOVE      "13",ICDRDVER   * Yes, set version ind to ICD10 Ed13
                  CALL      RDPD13A1          * Read ICD10 Ed13 file
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          RETURN
.
RSPTICDD  MATCH     "patd11af",ICD10EDT
          IF        @EQUAL
            MOVE      "11",ICDRDVER         * Yes, set version ind to ICD10 Ed11
            CALL      RSPD11A1              * Read ICD10 Ed11 file
          ELSE
            MATCH     "pat10dxf",ICD10EDT
            IF        @EQUAL
              MOVE      "10",ICDRDVER       * Yes, set version ind to ICD10 Ed10
              CALL      RSPT1XD1            * Read ICD10 Ed10 file
            ELSE
              MATCH     "patd12af",ICD10EDT
              IF        @EQUAL
                MOVE      "12",ICDRDVER     * Yes, set version ind to ICD10 Ed12
                CALL      RSPD12A1          * Read ICD10 Ed12 file
              ELSE
                MATCH     "patd13af",ICD10EDT
                IF        @EQUAL
                  MOVE      "13",ICDRDVER   * Yes, set version ind to ICD10 Ed13
                  CALL      RSPD13A1        * Read ICD10 Ed13 file
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          RETURN
.
RKPTICDD  MATCH     "patd11af",ICD10EDT
          IF        @EQUAL
            MOVE      "11",ICDRDVER         * Yes, set version ind to ICD10 Ed11
            CALL      RKPD11A1              * Read ICD10 Ed11 file
          ELSE
            MATCH     "pat10dxf",ICD10EDT
            IF        @EQUAL
              MOVE      "10",ICDRDVER       * Yes, set version ind to ICD10 Ed10
              CALL      RKPT1XD1            * Read ICD10 Ed10 file
            ELSE
              MATCH     "patd12af",ICD10EDT
              IF        @EQUAL
                MOVE      "12",ICDRDVER     * Yes, set version ind to ICD10 Ed12
                CALL      RKPD12A1          * Read ICD10 Ed12 file
              ELSE
                MATCH     "patd13af",ICD10EDT
                IF        @EQUAL
                  MOVE      "13",ICDRDVER   * Yes, set version ind to ICD10 Ed13
                  CALL      RKPD13A1        * Read ICD10 Ed13 file
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          RETURN
+
.******************************************************************************
          INC       STD001IO
.
          INC       PMSVSCIO/INC
          INC       PMSVMTIO/INC
          INC       PATCODIO/INC
          INC       PATICDIO/INC
.
.
.
.

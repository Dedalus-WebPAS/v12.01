.*****************************************************************************
.*              P R O G R A M  S U M M A R Y                                 *
.*              -------------  -------------                                 *
.*                                                                           *
.*     PROGRAM NAME:     IBAHOS14                                            *
.*                                                                           *
.*     FUNCTION:         This program will find print all discharged patients*
.*                       for a selected Claim code and range of Discharge    *
.*                       dates with NO invoice raised                        *
.*                                                                           *
.*     AUTHOR:           Jeni Tan                                            *
.*                                                                           *
.*     DATE WRITTEN:     16/02/2015                                          *
.*                                                                           *
.*     MODIFICATIONS:                                                        *
.*                                                                           *
.******************************************************************************
.
          INC       STD001FD 
.
          INC       IBAPASFD/INC
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC

          INC       PATCODFD/INC
          INC       PATDSCFD/INC
. 
          INC       PATVISFD/INC
          INC       IBACONFD/INC
          INC       PATHSPFD/INC
          INC       PMSVX1FD/INC
.
+
CODE      DIM       2
FROMDATE  DIM       10
.
KCLMCODE  DIM       3
TODATE    DIM       10
ECOL1     FORM      2
ENDDATE   DIM       8
STRTDATE  DIM       8
.
TIMEIS    DIM       8
XNOPAT    FORM      8
.
PRGID     INIT      "IBAHOS14"
PRGNAM    INIT      "Billable Patient with No Invoice raised"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
MAIN1000  CALL      DSCR0000                      * Display screen
.
          CALL      KCOD0000                      * keyin claim code
          BRANCH    EXIT,MAIN1200,MAIN9999
.
MAIN1200  CALL      KDSC0000                      * keyin range of disc.dates
          BRANCH    EXIT,MAIN1000
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN2000,MAIN1000,MAIN9999 * Yes, no, cancel
.
MAIN2000  CALL      PROC0000                      * processing
.
MAIN9999  CHAIN     PGM
          STOP
+
.******************************************************************************
.*                          INIT0000                                          *
.*  Display heading and check that the files needed exist&open them           *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.         OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN 	    PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
INIT9999  RETURN
+
.******************************************************************************
.         Display Screen
.******************************************************************************
DSCR0000  DISPLAY   *P1:5,*EF,"Claim Code              : ":
                        *P1:7,"Starting Discharge Date : ":
                        *P1:8,"Ending   Discharge Date : "
.
DSCR9999  RETURN
+
.******************************************************************************
.       Keyin Range of Discharge dates
.******************************************************************************
KDSC0000  MOVE      SP70,STRTDATE
          MOVE      SP70,ENDDATE
          UNPACK    SP70,FROMDATE,TODATE
.
          MOVE      "7",CVERT
          MOVE      "27",CCOL
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin the starting date
          BRANCH    OVRCD,KDSC9000
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          CALL      PACDATE                 * Pack the starting date
          MOVE      CPCDATE,FROMDATE
.
KDSC2000  MOVE      "8",CVERT
          MOVE      "27",CCOL
          CALL      KEYDATE                 * Keyin the ending date
          BRANCH    OVRCD,KDSC0000
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE                 * Pack the starting date
.
. ----- Check That The Ending Date Is Greater Than The Starting Date -----
.
          MATCH     STRTDATE,ENDDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Ending date must be greater than the ":
                      "starting date. ";
            CALL      HITENTER
            GOTO      KDSC2000
          ENDIF
          MOVE      CPCDATE,TODATE
          MOVE      ZERO,EXIT
          GOTO      KDSC9999
.
KDSC9000  MOVE      ONE,EXIT
KDSC9999  RETURN
+
.******************************************************************************
.         Keyin Claim code
.******************************************************************************
KCOD0000  MOVE      SP70,KCLMCODE
          MOVE      "5",EVERT
          MOVE      "27",ECOL               * keyin position
          MOVE      "32",ECOL1              * Display description position
          MOVE      SP3,ACODE
          MOVE      "CL",CODE
.
          CALL      KEYINCOD
          BRANCH    EXIT,KCOD9000:          * Nothing entered
                         KCOD9999           * Exitchar entered
.
          DISPLAY   *PECOL1:EVERT,*V2LON,TDESC;
          MOVE      ACODE,KCLMCODE
          GOTO      KCOD9999
.
KCOD9000  DISPLAY   *PECOL1:EVERT,*V2LON,"All";
KCOD9999  RETURN
+
.******************************************************************************
.       Get the discharge patients
.******************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Processing Discharges **";
.
          MOVE      "60",CLNO
          PACK      KEY16,STRTDATE,SP70       * position on start date
          CALL      RDSDSCH2
PROC1000  CALL      RDKDSCH2
          BRANCH    OVRCD,PROC8000
.
          MATCH     DDATE,ENDDATE    
          GOTO      PROC8000 IF LESS          * out of range
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,PROC1000
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PROC1000
.
          DISPLAY   *P66:24,AADMNO;
.
          MATCH     SP70,KCLMCODE
          IF        !@EQUAL
            MATCH     ACLAIM,KCLMCODE         * Check claim code
            GOTO      PROC1000 IF NOT EQUAL
          ENDIF
.
          COMPARE   ZERO,AINVPRT
          GOTO      PROC1000 IF NOT EQUAL   * Invoice printed
.
          CALL      PLIN0000                * Print data
          GOTO      PROC1000
.
PROC8000  CALL      ENDP0000                * Check End of Report
.
PROC9999  RETURN
+
.******************************************************************************
.         Print patient details
.******************************************************************************
PLIN0000  COMPARE   "50",CLNO
          CALL      PHED0000 IF NOT LESS
.
          UNPACK    PSNAME,KEY20
          PRINT     *1,"| ",AADMNO,*13,"| ",AURNO,*26,"| ",KEY20;
.
          UNPACK    PGNAME,KEY20
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Pack the starting date
          PRINT     *51,"| ",KEY20,*76,"| ",CPCDATE;
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Pack the starting date
          PRINT     *91,"| ",CPCDATE,*108,"| ",ACLAIM,*132,"|"
.
          ADD       ONE,CLNO
          ADD       ONE,XNOPAT
.
PLIN9999  RETURN
+
.******************************************************************************
.         Heading
.******************************************************************************
PHED0000  CALL      IBACLOCK
          MOVE      ZERO,CNOUNDLN
          CALL      HEAD132
          MOVE      THREE,CLNO
.
          PRINT     *40,"Discharge Date from : ",FROMDATE," To: ",TODATE;
.
          MATCH     SP70,KCLMCODE
          IF        @EQUAL
            PRINT     *N,*40,"Claim Code          : All":
                      *N
          ELSE
            PRINT     *N,*40,"Claim Code          : ",KCLMCODE,*72,TDESC:
                      *N
          ENDIF
          ADD       FIVE,CLNO
.
          CALL      UND132
          PRINT     *1,"| Admission",*13,"| U/R Number ",*26,"| Surname    ":
                    *51,"| Given Name ",*76,"| Adm.Date",*91,"| Disc.Date":
                    *108,"| Claim",*132,"|"
          ADD       ONE,CLNO
          CALL      UND132
.
PHED9999  RETURN
+
.******************************************************************************
.         End of Report
.******************************************************************************
ENDP0000  COMPARE   ZERO,XNOPAT
          GOTO      ENDP1000 IF NOT EQUAL
.
          CALL      NDAT0000               * No data
          CALL      PHED0000               * print headings
          GOTO      ENDP8000
.
ENDP1000  CALL      UND132
          PRINT     *N,*2,"Number of Patients : ",XNOPAT
.
          DISPLAY   *P1:24,*EL,*P22:24,*V2LON:
                    "***  Report Generation Completed  ***",*W2;
.
ENDP8000  PRINT     *N," ***  End of Report  ***"
.
ENDP9999  RETURN
+
.**********************************************************************
.*                              NDAT0000                              *
.*                           No Data Available                        *
.**********************************************************************
NDAT0000  DISPLAY   *P1:24,*EL,*P25:24,*B,*V2LON:
                    "** No available data on file **",*W2;
          RETURN
+
.******************************************************************************
         INC       IBAPASIO/INC
         INC       PATMI1IO/INC
         INC       PATMA1IO/INC
         INC       PATCODIO/INC
         INC       PATDSCIO/INC
.
         INC       PATVISIO/INC
         INC       PMSVX1IO/INC
.
         INC       PATDSCOD
         INC       KEYINCOD
         INC       STD001IO
.

.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:    IBAPMS25                                              *
.*                                                                            *
.*     FUNCTION:        WARD SUMMARY REPORT                                   *
.*                                                                            *
.*     DATE WRITTEN:    28/03/1990                                            *
.*                                                                            *
.*     AUTHOR:          DIG (Adapted from IBAHOS65..... which didn't work)    *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*        V12.00.01 15.05.2025  DA Sarkies     TSK 0955096                    *
.*                  Updated for Alpha-Numeric Visit Number                    *
.*        V11.02.01 25/07/2022  PJ Le Febour   TSK 0921398                    *
.*                  RFS when running report over multiple years               *
.*                  the start date is not appearing as what the User input    *
.*        V10.06.01 26/06/2015  Peter Vela     CAR 307720                     *
.*                  Added new line to Hospital name display                   *
.*        V10.05.01 22/09/2014  J.Tan          CAR 294962                     *
.*                  Added input Hospital id for multi hospital                *
.*        V10.04.01 15/04/2014  J.Tan          CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*                      V5.07.01  22/02/1999 Jim Stathopoulos                 *
.*                                Fixed another Glenn Berry error             *
.*                      V5.07.B02 27/01/1999 Jim Stathopoulos                 *
.*                                Report Modifications                        *
.                   09/11/1998  Glenn Berry      5.07 changes
.******************************************************************************
.*                      V5.01.02  11/02/91 i chung        SRF 107699          *
.*                                Modify from single MONTH/PERIOD report to a *
.*                                date range report                           *
.*                      V5.01.03  19/03/91 J.Tan          SRF 108111          *
.*                                Fixed no. of onleave and return for patients*
.*                                who go onleave and return on the same day   *
.*                      V5.01.04  23/09/91 J.Tan          SRF 110792          *
.*                                Fixed to check ward changes for return rec. *
.*                      V5.02.01  03/03/95 Paul Howells   SRF 440889          *
.*                                Standardise & Define Specialty as Drs Spec. *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
          INC       PATCOMM/INC
.
          INC       PATHSPFD/INC
          INC       PATCODFD/INC
          INC       PATDAYFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
          INC       PATWR1FD/INC
+
.
.           Variables Definition
.
CENT      DIM       2
CENT1     DIM       2
CMDLINE   DIM       50
CODE      DIM       2
CODEDT    INIT      "DT"
CPERMTH   DIM       6
CURDATE   DIM       8
.
DAY       DIM       2
DAY1      DIM       2
DESCALL   INIT      "All Patients         "
DESCSPEC  INIT      "By Doctor's Specialty"
DIM8      DIM       8
DOCTYPE   DIM       3
DUMMY2    DIM       2
DJAN      INIT      "JANUARY"
DFEB      INIT      "FEBUARY"
DMAR      INIT      "MARCH"
DAPR      INIT      "APRIL"
DMAY      INIT      "MAY"
DJUN      INIT      "JUNE"
DJUL      INIT      "JULY"
DAUG      INIT      "AUGUST"
DSEP      INIT      "SEPTEMBER"
DOCT      INIT      "OCTOBER"
DNOV      INIT      "NOVEMBER"
DDEC      INIT      "DECEMBER"
.
ENDFLAG   FORM      1
FDTEFORM  FORM      8
FNAMEI    DIM       8
FNAMER    DIM       8
FNAMEU    DIM       8
FRDATE    DIM       8
FROMDATE  DIM       8
.
GNDFLG    FORM      1
HOSPID    DIM       3
HOSPNAME  DIM       50
INCFLG    FORM      1
IBCNMHOS  FORM      1
.
LASTDAY   DIM       8
LBRAC     INIT      "("
RBRAC     INIT      ")"
.
MON       DIM       2
MON1      DIM       2
NEGONE    FORM      "-1"
.
OPTNDESC  DIM       25
.
PAGENO    FORM      3
PATFNAME  DIM       8
PERDTE    DIM       4
PLNFLAG   FORM      1
.
SDAYMON   DIM       4
SDAYMONO  DIM       4
SP14      INIT      "              "
SPECFLAG  FORM      1
STATUS    FORM      1
STFORM    FORM      8
SYEAR     DIM       4
SYEARO    DIM       4
.
TDAYMON   DIM       4
TDTEFORM  FORM      8
TIMEIS    DIM       8
TMPADMN   DIM       8
TO        INIT      " To "
TODATE    DIM       8
TODAY     DIM       8
TOFORM    FORM      8
TYEAR     DIM       4
.
.VERT      FORM      2
WSTAT     FORM      1
YEAR1     DIM       2
.
.           Save variables
.
SELMTH    DIM       8
SLEVDAY   FORM      8          * number of on-leave bed days
SLEVNUM   FORM      8          * number of on-leave patient
SPATADM   FORM      8          * number of admissions
SPATDAY   FORM      8          * number of bed days
SPATDSH   FORM      8          * number of discharges
SPATIN    FORM      8          * number of transfer IN
SPATOUT   FORM      8          * number of transfer OUT
SRETNUM   FORM      8          * number of return patient
.
.           Grand total variables
.
GRTADM    FORM      8
GRTDSH    FORM      8
GRTIN     FORM      8
GRTLDAY   FORM      8
GRTLEV    FORM      8
GRTOUT    FORM      8
GRTPDAY   FORM      8
GRTRET    FORM      8
.
.           Sub-Total variables
.
SP12      INIT      "            "
.
TEN59     FORM      "59"
TOTADM    FORM      8
TOTDSH    FORM      8
TOTIN     FORM      8
TOTLDAY   FORM      8
TOTLEV    FORM      8
TOTOUT    FORM      8
TOTPDAY   FORM      8
TOTRET    FORM      8
.
.           Temporary files definition
.
PATMP1XX  IFILE     SQL, FIXED=9, KEY="u1-8"
PSTROL    FILE      ASCII, FIXED=256
PWARD     IFILE     SQL, FIXED=52, KEY="U1-11"
.
.-----------------------------------------------------------
.Name     Type    Length      Physical   Start
.----     ----    ------      --------   -----
XWARD     DIM       3             3         1
TEMPDAT   DIM       8             8         4
PATADM    FORM      8             5        12
PATIN     FORM      8             5        17
PATDSH    FORM      8             5        22
PATOUT    FORM      8             5        27
PATDAY    FORM      8             5        32
LEVNUM    FORM      8             5        37
LEVDAY    FORM      8             5        42
RETNUM    FORM      8             5        47
.
.End of Record                             52
.-----------------------------------------------------------
.
PRGID     INIT      "IBAPMS25"
PRGNAM    INIT      "Ward Summary Report"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
.          DISPLAY   *P64:24,"patdrgaf";
.          OPEN      PATDRGA1,"patdrgaf"
.          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,PATFNAME
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEI
+
.
.         START OF PROGRAM
.
START     MOVE      ZERO,CPAGENO
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit ":
                    *P1:5,*V2LON,"1",*HOFF," = All Patients":
                    *P1:6,*V2LON,"2",*HOFF," = By Doctor's Specialty":
                    *P1:8,"Select Option :";
.
START1    KEYIN     *P17:8,*EF,*DV,UNDLN1,*P17:8,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      ENDIT IF EQUAL
.
OPTN0000  BRANCH    OPTION,ALLPAT,BYSPEC
          BEEP
          GOTO      START1
.
ALLPAT    MOVE      ONE,SPECFLAG
          GOTO      DATE0000
.
. ----    Specialty is Also Known as Attending Doctor Type   ----
.
BYSPEC    MOVE      ZERO,SPECFLAG
.
          DISPLAY   *P1:12,*EF,"Doctor's Specialty : ",UNDLN3;
.
          KEYIN     *P22:12,*V2LON,DOCTYPE
          DISPLAY   *P22:12,*V2LON,DOCTYPE
.
          ENDSET    DOCTYPE
          APPEND    SP3,DOCTYPE
          RESET     DOCTYPE
.
          MATCH     SP3,DOCTYPE
          GOTO      START1 IF EQUAL
.
          MATCH     QUEST,DOCTYPE
          GOTO      BYSPEC1 IF NOT EQUAL
.
          MOVE      CODEDT,CODE
          CALL      PATCODDS
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit ":
                    *P1:5,*V2LON,"1",*HOFF," = All Patients":
                    *P1:6,*V2LON,"2",*HOFF," = By Doctor's Specialty":
                    *P1:8,"Select Option : ",*V2LON,OPTION,*HOFF;
          GOTO      BYSPEC
.
BYSPEC1   PACK      KEY5,CODEDT,DOCTYPE
          CALL      RDCODE1
          COMPARE   ZERO,OVRCD
          GOTO      BYSPEC2 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Doctor's Specialty not on file - ";
          CALL      HITENTER
          GOTO      BYSPEC
.
BYSPEC2   DISPLAY   *P33:12,TDESC
.
. ----    Keyin date range    ----
.
DATE0000  DISPLAY   *P1:14,*EF,"Generate report from "
.
          MOVE      TEN4,CVERT
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          MOVE      0,SYEARO
.
.         "From Date"
.
KFDATE    DISPLAY   *P22:14,*EL;
.
KFDATE1   UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      "22",CCOL
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    CQUEST,KFDATE1
          BRANCH    OVRCD,START1
.
          PACK      SYEAR,CCENT,CYEAR
          REP       " 0",SYEAR
          PACK      SDAYMON,CMON,CDAY
          REP       " 0",SDAYMON
          PACK      FROMDATE,CCENT,CYEAR,SDAYMON
          REP       " 0",FROMDATE
.
          MATCH     FROMDATE,CURDATE
          GOTO      KTDATE IF NOT LESS
.
          DISPLAY   *P1:24,*B,*EL,*V2LON,"From Date",*HOFF," must be less than":
                                         " or equal to today's date - ";
          CALL      HITENTER
          GOTO      KFDATE1
.
.         "To Date"
.
KTDATE    DISPLAY   *P33:14,*EL,"to";
.
KTDATE1   UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      "36",CCOL
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    CQUEST,KTDATE1
          BRANCH    OVRCD,KFDATE
.
          PACK      TYEAR,CCENT,CYEAR
          REP       " 0",TYEAR
          PACK      TDAYMON,CMON,CDAY
          REP       " 0",TDAYMON
          PACK      TODATE,CCENT,CYEAR,TDAYMON
          REP       " 0",TODATE
.
          MATCH     TODATE,CURDATE
          GOTO      CHKDATE IF NOT LESS
.
          DISPLAY   *P1:24,*B,*EL,*V2LON,"To Date",*HOFF," must be less than ":
                                         "or equal to today's date - ";
          CALL      HITENTER
          GOTO      KTDATE1
.
. ----    check for valid date range entered    ----
.
CHKDATE   MATCH     FROMDATE,TODATE
          GOTO      KEYHOSP IF NOT LESS
.
          DISPLAY   *P1:24,*B,*EL,*V2LON,"To Date",*HOFF," must be greater ":
                                         "than or equal to From Date - ";
          CALL      HITENTER
          GOTO      KTDATE1
.
KEYHOSP   MOVE       SP10,HOSPID
          IF         IBCNMHOS=1
            CALL       KHOS0000        * Hospital id
            BRANCH     EXIT,OPTN0000
          ENDIF
.
. ----    check if Ok to continue processing or not    ----
.
REKEY     KEYIN     *P1:24,*EL,"Ok to continue (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,"/",*V2LON,"C",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     "C" TO ANS
          GOTO      START1 IF EQUAL
.
          MATCH     "N" TO ANS
          GOTO      OPTN0000 IF EQUAL
.
          MATCH     "Y" TO ANS
          GOTO      CONTPRO IF EQUAL
          BEEP
          GOTO      REKEY
.
.         set last day of calculation equivalent to the end date of date range
.
CONTPRO    MOVE      TODATE,LASTDAY
.
.
. ----    Create the Indexed Temp File    ----
.
          DISPLAY   *P1:24,*EL,*P16:24,*V2LON,"** Creating the Temp Index ":
                    "File - ",*BLINKON,"Please wait",*HOFF,*V2LON," **";
.
.       Kill the temp index file if it already exists
.
          CALL      KILL0000
.
.       Build new temp index file
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    PATFNAME,CMDLINE
          APPEND    " 9 u1-8",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      PATMP1XX,PATFNAME
.
. ----    Start Processing    ----
.
          DISPLAY   *P1:24,*EL,*P15:24,*V2LON,"** Initializing Ward Summary ":
                    "File - ",*BLINKON,"Please wait ",*HOFF,*V2LON," **";
.
          CALL      INDZD
          CLOCK     TIME,TIMEIS
.
          CALL      DAYA0000         * Write the patdayaf data to the temp file
.
          CALL      INTVAR           * Initialize the variables
.
          CALL      INTREC           * Initialize the record fields
.
          MOVE      ZERO,GNDFLG
.
          DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Processing Admissions":
                         *HOFF,*V2LON," **",*HOFF:
                    *P34:24,"Admission No :",*P61:24,"Scanning :";
.
.------ position on the temp file ------
.
          MOVE      SP8,KEY8
          CALL      RSTMPR1
.
.------ read through the temp index file and process each admission ------
.
NEXTADM   CALL      RKTMPR1
          BRANCH    OVRCD,REPORT
.
          MOVE      TMPADMN,KEY8
          CALL      RDMISS1              * read the admissions file
          BRANCH    OVRCD,NEXTADM
.
          IF         IBCNMHOS=1
            MATCH     SP70,HOSPID
            IF        !@EQUAL
              MATCH     PMVXMHOS,HOSPID  * Check hospital id
              GOTO      NEXTADM IF NOT EQUAL
            ENDIF
          ENDIF
.
          BRANCH    SPECFLAG,CHCKDATE    * skip if not checking specialty
.
          PACK      KEY6,ADOCTA
          CALL      RDDOCT1              * read the doctors file
          BRANCH    OVRCD,NEXTADM
.
          MATCH     DOCTYPE,DRTYPE       * match doctor types
          GOTO      NEXTADM IF NOT EQUAL
.
.       check the admission date
.
CHCKDATE  MATCH     ADATE,TODATE         * make sure admission is in date range
          GOTO      NEXTADM IF LESS
.
          DISPLAY   *P72:24,AADMNO;
.
          BRANCH    ASTAT,NEXTADM,CONTADM,CONTDIS,CONTADM,NEXTADM
.
CONTDIS   MOVE      AADMNO,KEY8
          CALL      RDDSCH1              * read discharge file
          BRANCH    OVRCD,NEXTADM
.
          MATCH     FROMDATE,DDATE       * make sure discharge is in date range
          GOTO      NEXTADM IF LESS        
.
.       we have a valid admission so extract details and write to summary file
.
CONTADM   DISPLAY   *P49:24,*V2LON,AADMNO;
          CALL      TRANS
          GOTO      NEXTADM
.
.**********************************************************************
.*                            DAYA0000                                *
.*     Process patdayaf Data writing to temp indexed file patmp1xx    *
.**********************************************************************
.
DAYA0000  DISPLAY   *P1:24,*EL,*P14:24,*V2LON,"** Initializing the Temp Index":
                    " File - ",*BLINKON,"Please wait",*HOFF,*V2LON," **";
.
.------ close previous day file and set up the name of the next one ------
.
DAYA1000  CLOSE     PATDAYA1
          PACK      CFNAME,FILDAYA1,SYEAR
.
          TRAP      DAYA4900 IF IO        * trap if file does not exist
          OPEN      PATDAYA1,CFNAME
          TRAPCLR   IO
.
          PACK      KEY12,SDAYMON,SP10    * position on daily patient list file
          CALL      RSPTDAY1
.
.------ read through the patient daily list file ------
.
DAYA2000  CALL      RKPTDAY1
          BRANCH    OVRCD,DAYA5000
.
          MATCH     TYEAR,SYEAR            * see if the current file is the one
          GOTO      DAYA3000 IF NOT EQUAL    for the last year in the date range
.
          MATCH     PTDYDATE,TDAYMON       * if so, make sure the month and day
          GOTO      DAYA9999 IF LESS         are still in range
.
.------ Only need to write to the temporary file if the admission number -----
.------ is not already there ------
.
DAYA3000  MOVE      PTDYADMN,KEY8
          CALL      RDTMPR1                * read the temp file
          BRANCH    OVRCD,DAYA4000
          GOTO      DAYA2000
.
.------ write a new record to the temp file ------
.
DAYA4000  CALL      WRTMPR1
          GOTO      DAYA2000
.
.------ need to pop the return address off the stack if I/O error trapped -----
.
DAYA4900  NORETURN
.
.------ either file does not exist or we have reached the end of the ------
.------ current file ------
.
DAYA5000  MATCH     TYEAR,SYEAR             * test if we have reached the end 
          GOTO      DAYA9999 IF NOT LESS      year in the date range
.
          MOVE      SYEAR,SYEARO            * save Orig year input
          MOVE      SDAYMON,SDAYMONO        * save Orig day month input

          MOVE      SYEAR,FORM4             * if not, increment the start year
          ADD       ONE,FORM4                 to the next year and process
          MOVE      FORM4,SYEAR               the file for this year
          REP       " 0",SYEAR
          MOVE      "0101",SDAYMON
          GOTO      DAYA1000
.
DAYA9999  RETURN
+
.**********************************************************************
.
. ----    Loop through the TRANS file    ----
.
TRANS     MOVE      SP8,STDATE
          MOVE      SP3,STWARD
          MOVE      SP1,STMOVE
.
          PACK      KEY30 WITH AADMNO,SP30
          CALL      RDSTRAN2
.
NXTTRN    CALL      RDKTRAN2
          COMPARE   ONE,OVRCD
          GOTO      CHKLAST IF EQUAL
.
          MATCH     TADMN,AADMNO
          GOTO      CHKLAST IF NOT EQUAL
.
.       Check if TRANS date is in the selected period
.
          MATCH     FROMDATE,TDATE
          GOTO      KEEPREC IF LESS
.
          MATCH     TDATE,TODATE
          GOTO      CHKLAST IF LESS
.
          MATCH     SP8,STDATE
          GOTO      SAMEPAT IF NOT EQUAL
.
.       Check with the first TMOVE of the patient
.
          CMATCH    "A",TMOVE
          GOTO      NXT10 IF EQUAL
.
          CMATCH    "R",TMOVE
          GOTO      NXT03 IF EQUAL
.
.       If TMOVE does not start with Admission
.       or Return record than we need to update the bed days from the
.       start of period if necessary
.
          MOVE      TDATE,TEMPDAT                    * to date
.
.       Start from the first of the period
.
          MOVE      FROMDATE,STDATE
.
          MOVE      STWARD,XWARD
          CALL      UPBDAY
          GOTO      NXT10
.
.       If it starts with a return record, then we need to
.       add beddays for on-leave
.
NXT03     MOVE      FROMDATE,STDATE                   * from date
.
          MOVE      TDATE,TEMPDAT                     * to date
          MOVE      STWARD,XWARD
          MOVE      ZERO,INCFLG            * update bed days not include the
          CALL      UPBLEV                 * the current trans date
          GOTO      NXT10
.
. ----    Check if the same transfer date    ----
.
SAMEPAT   MATCH     TDATE,STDATE
          GOTO      NXT05 IF NOT EQUAL
.
.       Check if the patient discharged the same day after changes made
.
          CMATCH    "D",TMOVE
          GOTO      SAME10 IF NOT EQUAL
.
.       Remove the bed days as the patient discharge on the same day
.
          MOVE      TDATE,TEMPDAT
          MOVE      STWARD,XWARD
          MOVE      NEGONE,SPATDAY
          CALL      CHKREC
.
.       Add one to number of discharged patient
.
          MOVE      ONE,SPATDSH
          CALL      CHKREC
          RETURN
.
.       Check if the patient on leave on the same day after changes made
.
SAME10    CMATCH    "L",TMOVE
          GOTO      SAME20 IF NOT EQUAL
.
.       Remove bed days
.
          MOVE      TDATE,TEMPDAT
          MOVE      STWARD,XWARD
          MOVE      NEGONE,SPATDAY
          CALL      CHKREC
.
.       Add one to patient on leave
.
          MOVE      ONE,SLEVNUM
          MOVE      ONE,SLEVDAY
          CALL      CHKREC
          GOTO      SAME60
.
.       Check if the patient return on the same day after on leave
.
SAME20    CMATCH    "R",TMOVE
          GOTO      SAME30 IF NOT EQUAL
.
.       Remove bed days for on leave (the patient cancel going on leave)
.
          MOVE      STWARD,XWARD
          MOVE      TDATE,TEMPDAT
          MOVE      NEGONE,SLEVDAY
.
.       Check if the patient return to different ward
.
          MATCH     TWARD,STWARD
          GOTO      SAME22 IF EQUAL
          MOVE      ONE,SPATOUT            * Transfer out
.
SAME22    CALL      CHKREC
.
.       Add one to bed days
.
          MOVE      TWARD,XWARD
          MOVE      TDATE,TEMPDAT
          MOVE      ONE,SRETNUM
          MOVE      ONE,SPATDAY
.
.       Check if the patient return to different ward
.
          MATCH     TWARD,STWARD
          GOTO      SAME24 IF EQUAL
          MOVE      ONE,SPATIN            * transfer in
.
SAME24    CALL      CHKREC
          GOTO      SAME60
.
.       Check if the patient made changes on the same day
.
SAME30    CMATCH    "C",TMOVE
          GOTO      SAME60 IF NOT EQUAL
.
.       Check if ward changed
.
          MATCH     TWARD,STWARD
          GOTO      SAME60 IF EQUAL
.
.       Add one to Patient trans OUT and trans IN
.
          MOVE      STWARD,XWARD
          MOVE      TDATE,TEMPDAT
          MOVE      ONE,SPATOUT
          MOVE      NEGONE,SPATDAY
          CALL      CHKREC
.
          MOVE      TWARD,XWARD
          MOVE      TDATE,TEMPDAT
          MOVE      ONE,SPATIN
          MOVE      ONE,SPATDAY
          CALL      CHKREC
.
SAME60    MOVE      TWARD,STWARD
          MOVE      TMOVE,STMOVE
          GOTO      NXTTRN
.
.       Update the number of bed days up to the next trans date
.       Ignore if the patient on leave
.
NXT05     CMATCH    "L",STMOVE
          GOTO      NXT08 IF EQUAL
.
          MOVE      TDATE,TEMPDAT
          MOVE      STWARD,XWARD
          CALL      INCSTDTE               * Increment STDATE
          CALL      UPBDAY
          GOTO      NXT10
.
.    Update the number of bed days on leave  up to the date before they return
.
NXT08     MOVE      TDATE,TEMPDAT
          MOVE      STWARD,XWARD
          CALL      INCSTDTE               * Increment STDATE
          MOVE      ZERO,INCFLG            * update bed days not include the
          CALL      UPBLEV                 * current trans date
.
NXT10     CMATCH    "A",TMOVE
          GOTO      CHSTAT1 IF NOT EQUAL
.
.       Patient is admitted
.
          MOVE      TDATE,TEMPDAT
          MOVE      TWARD,XWARD
          MOVE      ONE,SPATADM
          MOVE      ONE,SPATDAY
          CALL      CHKREC
          CALL      SAVREC
          GOTO      NXTTRN
.
CHSTAT1   CMATCH    "D",TMOVE
          GOTO      CHSTAT2 IF NOT EQUAL
.
.       Discharged
.
          MOVE      TDATE,TEMPDAT
          MOVE      TWARD,XWARD
          MOVE      ONE,SPATDSH
          CALL      CHKREC
          RETURN
.
CHSTAT2   CMATCH    "C",TMOVE
          GOTO      CHSTAT3 IF NOT EQUAL
.
.       Check if WARD has been changed
.
          MATCH     STWARD,TWARD
          GOTO      NXT20 IF NOT EQUAL
.
.       Update the number of bed days for the current trans date
.
          MOVE      TDATE,TEMPDAT
          MOVE      TWARD,XWARD
          MOVE      ONE,SPATDAY
          CALL      CHKREC
          CALL      SAVREC
          GOTO      NXTTRN
.
.       Update tempfile for the TRANS IN  and TRANS OUT
.
NXT20     MOVE      TDATE,TEMPDAT
          MOVE      STWARD,XWARD
          MOVE      ONE,SPATOUT
          CALL      CHKREC
.
          MOVE      TDATE,TEMPDAT
          MOVE      TWARD,XWARD
          MOVE      ONE,SPATIN
          MOVE      ONE,SPATDAY
          CALL      CHKREC
          CALL      SAVREC
          GOTO      NXTTRN
.
CHSTAT3   CMATCH    "L",TMOVE
          GOTO      CHSTAT4 IF NOT EQUAL
.
.       On Leave
.
          MOVE      TDATE,TEMPDAT
          MOVE      TWARD,XWARD
          MOVE      ONE,SLEVNUM
          MOVE      ONE,SLEVDAY
          CALL      CHKREC
          CALL      SAVREC
          GOTO      NXTTRN
.
CHSTAT4   CMATCH    "R",TMOVE
          GOTO      NXTTRN IF NOT EQUAL
.
.       Return
.
          MATCH     TWARD,STWARD
          GOTO      NXT40 IF EQUAL
.
          MOVE      STDATE,TEMPDAT
          MOVE      STWARD,XWARD
          MOVE      ONE,SPATOUT           * Transfer out
          CALL      CHKREC
.
NXT40     MOVE      TDATE,TEMPDAT
          MOVE      TWARD,XWARD
          MOVE      ONE,SRETNUM
          MOVE      ONE,SPATDAY
.
.       Check if the patient return to different ward
.
          MATCH     TWARD,STWARD
          GOTO      NXT50 IF EQUAL
          MOVE      ONE,SPATIN            * Transfer in
.
NXT50     CALL      CHKREC
          CALL      SAVREC
          GOTO      NXTTRN
.
.       Add number of bed days up to the next transfer date
.
UPBDAY    MOVE      TEMPDAT,TOFORM                    * to date
          MOVE      STDATE,STFORM                     * from date
.
UPBD10    COMPARE   TOFORM,STFORM
          GOTO      INTVAR IF NOT LESS
.
.       Set up the next trans date
.
          MOVE      STFORM,TEMPDAT
          MOVE      ONE,SPATDAY
.
          CALL      CHKREC
.
          MOVE      STFORM,DIM8
          UNPACK    DIM8,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
.
          CALL      DMYCON                 * convert to julian form
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
.
          CALL      JULCON                 * convert back to CC,YY,MM,DD
.
          PACK      DIM8,CC,YY,MM,DD
          REP       " 0",DIM8
          MOVE      DIM8,STFORM
          GOTO      UPBD10
.
.       Add number of bed days on leave up to the next transfer date
.
UPBLEV    MOVE      TEMPDAT,TOFORM                    * to date
          MOVE      STDATE,STFORM                     * from date
.
UPBL10    COMPARE   TOFORM,STFORM
          GOTO      UPBL20 IF NOT LESS
.
.       Set up the next trans date
.
          MOVE      STFORM,TEMPDAT
          MOVE      ONE,SLEVDAY
.
          CALL      CHKREC
.
          MOVE      STFORM,DIM8
          UNPACK    DIM8,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
.
          CALL      DMYCON                     * convert to julian form
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
.
          CALL      JULCON                     * convert back to CC,YY,MM,DD
.
          PACK      DIM8,CC,YY,MM,DD
          REP       " 0",DIM8
          MOVE      DIM8,STFORM
          GOTO      UPBL10
.
.       Check if it includes the current transfer date
.
UPBL20    COMPARE   ZERO,INCFLG
          GOTO      INTVAR IF EQUAL
.
.       Include  the current trans date
.
          MOVE      STDATE,TEMPDAT
          MOVE      ONE,SLEVDAY
          CALL      CHKREC
          GOTO      INTVAR
.
.       Save the record which is not on the current month
.       Incase to check for on-leave patient
.
KEEPREC   MATCH     SP8,STDATE
          GOTO      CHKLAST IF NOT EQUAL
.
          MOVE      TWARD,STWARD
          MOVE      TMOVE,STMOVE
          GOTO      NXTTRN
.
.       Check with the last record
.
CHKLAST   CMATCH    "L",STMOVE
          GOTO      LAST10 IF EQUAL
.
          CMATCH    "D",STMOVE
          RETURN    IF EQUAL
.
.       Add the number of bed days for the rest of the month
.
          MOVE      LASTDAY,TEMPDAT
.
.       Check if the last date has been updated
.
          MATCH     TEMPDAT,STDATE
          RETURN    IF EQUAL
.
.       Check if start of date is less than the start of the period
.       (ie if the patient Admitted from last period and still Admitted up to
.       this period)
.
          MOVE      FROMDATE,XDATE
.
          MATCH     XDATE,STDATE
          GOTO      LAST03 IF NOT LESS
          GOTO      LAST04
.
LAST03    CALL      INCSTDTE               * Increment STDATE
          GOTO      LAST05
.
.       Use the start of the selected period
.
LAST04    MOVE      FROMDATE,STDATE
.
LAST05    MOVE      STWARD,XWARD
          CALL      UPBDAY
.
.       Update the last date of the period as UPBDAY not update the last date
.
          MOVE      STFORM,TEMPDAT
          MOVE      ONE,SPATDAY
          CALL      CHKREC
          RETURN
.
.       Check if we need to add up number of days on leave
.
LAST10    MOVE      LASTDAY,TEMPDAT
.
.       Check if the last date has been updated
.
          MATCH     TEMPDAT,STDATE
          RETURN    IF EQUAL
.
.       Check if start of date is less than the start of the period
.       (ie if the patient on-leave from last period and still on-leave up to
.       this period)
.
          MOVE      FROMDATE,XDATE
.
          MATCH     XDATE,STDATE
          GOTO      LAST15 IF NOT LESS
          GOTO      LAST17
.
LAST15    CALL      INCSTDTE               * Increment STDATE
          GOTO      LAST20
.
.       Use the start of the selected period
.
LAST17    MOVE      FROMDATE,STDATE
.
LAST20    MOVE      ONE,INCFLG       * Flag to include the current trans date
          MOVE      STWARD,XWARD
          CALL      UPBLEV
          RETURN
.
.       Increment STDATE
.
INCSTDTE  UNPACK    STDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
.
          CALL      DMYCON           * convert to julian date
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
.
          CALL      JULCON           * convert back to CC,YY,MM,DD 
.
          PACK      STDATE,CC,YY,MM,DD
          REP       " 0",STDATE
          RETURN
.
.       Check if the record is already exist in the tempfile
.
CHKREC    REP       " 0",TEMPDAT
          PACK      KEY11 WITH XWARD,TEMPDAT
          CALL      RDWRD
          BRANCH    OVRCD OF NEWREC
.
.       Update the record
.
          ADD       SPATADM,PATADM
          ADD       SPATIN,PATIN
          ADD       SPATDSH,PATDSH
          ADD       SPATOUT,PATOUT
          ADD       SPATDAY,PATDAY
          ADD       SLEVNUM,LEVNUM
          ADD       SLEVDAY,LEVDAY
          ADD       SRETNUM,RETNUM
          CALL      UPWRD
          GOTO      INTVAR
.
.       Write a new record to tempfile
.
NEWREC    CALL      INTREC
          MOVE      SPATADM,PATADM
          MOVE      SPATIN,PATIN
          MOVE      SPATDSH,PATDSH
          MOVE      SPATOUT,PATOUT
          MOVE      SPATDAY,PATDAY
          MOVE      SLEVNUM,LEVNUM
          MOVE      SLEVDAY,LEVDAY
          MOVE      SRETNUM,RETNUM
          CALL      WRWRD
.
.       Initialize the variables
.
INTVAR    MOVE      ZERO,SPATADM
          MOVE      ZERO,SPATIN
          MOVE      ZERO,SPATDSH
          MOVE      ZERO,SPATOUT
          MOVE      ZERO,SPATDAY
          MOVE      ZERO,SLEVNUM
          MOVE      ZERO,SLEVDAY
          MOVE      ZERO,SRETNUM
          RETURN
+
.
.       Save the record the variables
.
SAVREC    MOVE      TWARD,STWARD
          MOVE      TDATE,STDATE
          MOVE      TMOVE,STMOVE
          RETURN
+
.
.       Initialize the record
.
INTREC    MOVE      ZERO,PATADM
          MOVE      ZERO,PATIN
          MOVE      ZERO,PATDSH
          MOVE      ZERO,PATOUT
          MOVE      ZERO,PATDAY
          MOVE      ZERO,LEVNUM
          MOVE      ZERO,LEVDAY
          MOVE      ZERO,RETNUM
          RETURN
+
.
.      Create an index file based on port for accumulating Admission Type totals
.
INDZD     MOVE      ZERO,STATUS
.
.       Check if previously exists..if so delete it
.
          CALL      KILLIDZ
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    " 52 u1-11 ",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      PWARD,FNAMEI
          RETURN
+
.
.       Deleting an index temp file
.
KILL0000  CLEAR     CMDLINE
          CLOSE     PATMP1XX
          APPEND    "iserase ",CMDLINE
          APPEND    PATFNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
KILLIDZ   CLOSE     PWARD
          MOVE      ZERO,STATUS
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
. ----    Generating the report    ----
.
REPORT    DISPLAY   *P1:24,*EL,*P9:24,*V2LON,"** ",*BLINKON,"Generating Report":
                    *HOFF,*V2LON," **",*HOFF,*P50:24,"Ward : ";
.
          CALL      INGRTOT
.
.        Print report from the tempfile
.
          MOVE      ZERO,PLNFLAG
          MOVE      SP3,STWARD
          MOVE      SP20,KEY11
          CALL      RDSWRD
NXTWRD    CALL      RDKWRD
          BRANCH    OVRCD OF ENDREP
.
          DISPLAY   *P57:24,*V2LON,XWARD;
.
          MATCH     STWARD,XWARD
          GOTO      SAMEWRD IF EQUAL
.
.        Print the total of each ward
.
          MATCH     SP3,STWARD
          CALL      PRTTOT IF NOT EQUAL
.
.        A separate page for each ward
.
          CALL      INTOTAL
          CALL      HEAD
          MOVE      XWARD,STWARD
.
SAMEWRD   UNPACK    TEMPDAT,XCENT,XYEAR,XMON,XDAY
          MOVE      XDAY,IDAY
          MOVE      XMON,IMON
.
          COMPARE   ZERO,CPAGENO
          GOTO      SWARD1 IF EQUAL
.
          COMPARE   "56",CLNO
          CALL      HEAD IF NOT LESS
.
          PRINT     *1,"|  ",IDAY,SLASH,IMON,*10,"|",PATADM,*22,"|",PATIN:
                    *35,"|",PATDSH,*48,"|",PATOUT,*61,"|",PATDAY:
                    *74,"|",LEVNUM,*87,"|",LEVDAY,*100,"|",RETNUM,*113,"|"
          ADD       ONE,CLNO
.
.        Add up for sub-total
.
SWARD1    ADD       PATADM,TOTADM
          ADD       PATIN,TOTIN
          ADD       PATDSH,TOTDSH
          ADD       PATOUT,TOTOUT
          ADD       PATDAY,TOTPDAY
          ADD       LEVNUM,TOTLEV
          ADD       LEVDAY,TOTLDAY
          ADD       RETNUM,TOTRET
.
.        Add up for grand total
.
          ADD       PATADM,GRTADM
          ADD       PATIN,GRTIN
          ADD       PATDSH,GRTDSH
          ADD       PATOUT,GRTOUT
          ADD       LEVNUM,GRTLEV
          ADD       LEVDAY,GRTLDAY
          ADD       RETNUM,GRTRET
          GOTO      NXTWRD
.
.        Print the sub-total
.
PRTTOT    COMPARE   "57",CLNO
          CALL      HEAD IF NOT LESS
.
          CALL      PRTLN
          PRINT     *1,"| TOTAL",*10,"|",TOTADM,*22,"|",TOTIN:
                    *35,"|",TOTDSH,*48,"|",TOTOUT,*61,"|",TOTPDAY:
                    *74,"|",TOTLEV,*87,"|",TOTLDAY,*100,"|",TOTRET,*113,"|"
          CALL      PRTLN
          ADD       TOTPDAY,GRTPDAY
          MOVE      ONE,PLNFLAG
          RETURN
+
.
.        Print the grand-total
.
PRTGND    MOVE      ONE,GNDFLG
          CALL      HEAD
          PRINT     *1,"| GRAND",*10,"|",*22,"|":
                    *35,"|",*48,"|",*61,"|":
                    *74,"|",*87,"|",*100,"|",*113,"|":
                    *N,*1,"| TOTAL",*10,"|",GRTADM,*22,"|",GRTIN:
                    *35,"|",GRTDSH,*48,"|",GRTOUT,*61,"|",GRTPDAY:
                    *74,"|",GRTLEV,*87,"|",GRTLDAY,*100,"|",GRTRET,*113,"|"
          CALL      PRTLN
          RETURN
+
.
.        Initialize the totals
.
INGRTOT   MOVE      ZERO,GRTADM
          MOVE      ZERO,GRTIN
          MOVE      ZERO,GRTDSH
          MOVE      ZERO,GRTOUT
          MOVE      ZERO,GRTPDAY
          MOVE      ZERO,GRTLEV
          MOVE      ZERO,GRTLDAY
          MOVE      ZERO,GRTRET
.
INTOTAL   MOVE      ZERO,TOTADM
          MOVE      ZERO,TOTIN
          MOVE      ZERO,TOTDSH
          MOVE      ZERO,TOTOUT
          MOVE      ZERO,TOTPDAY
          MOVE      ZERO,TOTLEV
          MOVE      ZERO,TOTLDAY
          MOVE      ZERO,TOTRET
          RETURN
+
.
.        Print total for each ward
.
ENDREP    COMPARE   ZERO,CPAGENO
          GOTO      ENDRP1 IF EQUAL
.
          CALL      PRTTOT
.
.         Print the grand total
.
ENDRP1    CALL      PRTGND
          PRINT     *N,*N,"Note : Bed Days does not include day cases":
                       *N,"       No of Return and On-leave include patients":
                          " who go on leave and return on the same day":
                    *N,*N,*N,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Report Generation Completed **",*W2;
          CALL      KILL0000
          CALL      KILLIDZ
          GOTO      START
.
. ----    Headings for output    ----
.
HEAD      PACK      KEY6 WITH XWARD,SP3       * get ward description
          CALL      RDWARD1
.
          IF        PLNFLAG=0
            COMPARE   ZERO,CPAGENO            * First page ?
            CALL      PRTLN IF NOT EQUAL      * No. End previous page
          ENDIF
.
          ADD       ONE,CPAGENO
.
          LOAD      OPTNDESC,OPTION,DESCALL,DESCSPEC
.
          CLOCK     TIME,CTIMEIS
          CALL      IBACLOCK
          PRINT     *F,PRGID,*40,CNAME,*116,"DATE : ",CDATE:
                    *N,VERSION,*40,PRGNAM," - ",OPTNDESC:
                    *116,"TIME :   ",CTIMEIS:
                    *N,*116,"PAGE :        ",CPAGENO
.
.       **********************************************
.
          MATCH     SYEAR,SYEARO
          IF        !@EQUAL
            MOVE      SYEARO,SYEAR
            MOVE      SDAYMONO,SDAYMON
          ENDIF

          UNPACK    SYEAR,CCENT,CYEAR
          UNPACK    SDAYMON,CMON,CDAY
          UNPACK    TYEAR,XCENT,XYEAR
          UNPACK    TDAYMON,XMON,XDAY
.
          PRINT     *40,"From  ",CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                        "  to  ",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR;
          MOVE     FOUR,CLNO
.
          IF       IBCNMHOS =1
            PRINT    *N,*40,"Hospital : ",HOSPID,"    ",HOSPNAME
            ADD      ONE,CLNO
          ENDIF
.
          BRANCH    OPTION,SKIPSPEC,DOSPEC
          GOTO      SKIPSPEC
.
DOSPEC    PRINT     *N,*N,*40,"Doctor's Specialty : ",DOCTYPE,SP2,TDESC;
          ADD       TWO,CLNO
.
SKIPSPEC  BRANCH    GNDFLG OF DOGTOTL
          PRINT     *N,*N,*40,"WARD  :  ",XWARD,"  ",WBDESC
          ADD       THREE,CLNO
          GOTO      SKIPWRD
.
DOGTOTL   PRINT     *N,*N,*40,"GRAND TOTAL FOR ALL WARDS"
.
SKIPWRD   PRINT     *N;
          CALL      PRTLN
          PRINT     *1,"|",*10,"|","       ---------------- Patient ----":
                    "---------------------":
                    *74,"|  -----  On-Leave  -----":
                    *100,"|   Return",*113,"|":
                    *N,*1,"| Day No.",*10,"| Admissions",*22,"|  Transfer":
                    *35,"| Discharges",*48,"|  Transfer",*61,"|  Bed Days":
                    *74,"|   Number",*87,"|  Bed Days":
                    *100,"|   Number",*113,"|":
                    *N,*1,"|",*10,"|",*22,"|     In",*35,"|":
                    *48,"|    Out",*61,"|",*74,"|",*87,"|",*100,"|",*113,"|"
          CALL      PRTLN
          ADD       FIVE,CLNO
          MOVE      ZERO,PLNFLAG
          RETURN
+
.
PRTLN     PRINT     *1,"*----------------------------":
                    *30,"--------------------------------------------------":
                    *80,"---------------------------------*"
          RETURN
+
.        ======================================================================
.        KHOS0000: Keyin hospital ID
.        ======================================================================
KHOS0000 DISPLAY    *P1:16,*EL,"Hospital Id : ";
         MOVE       TEN5,ECOL
         MOVE       "16",EVERT
         MOVE       SP3,CKYIDEF3
         MOVE       ZERO,CKYIMAND           * Not Mandatory
         OPEN       PATHSPA1,"pathspaf"
.
         CALL       PATHSPKY
         BRANCH     EXIT,KHOS1000,KHOS2000
         MOVE       PTHSHOSP,HOSPID
         MOVE       PTHSNAME,HOSPNAME
         MOVE       ZERO,EXIT
         GOTO       KHOS9999
.
KHOS1000 MOVE       "All Hospitals",HOSPNAME
         MOVE       SP10,PTHSHOSP
         MOVE       ZERO,EXIT
         GOTO       KHOS9999
.
KHOS2000 MOVE       ONE,EXIT
KHOS9999 RETURN
+
.
. ----    I/O Routines for the Ward Summary temp file    ----
.
WRWRD     RESET     KEY11
          WRITE     PWARD,KEY11;XWARD,TEMPDAT,PATADM,PATIN,PATDSH:
                    PATOUT,PATDAY,LEVNUM,LEVDAY,RETNUM
          RETURN
.
RDWRD     MOVE      ZERO,OVRCD
          RESET     KEY11
          READ      PWARD,KEY11;XWARD,TEMPDAT,PATADM,PATIN,PATDSH:
                    PATOUT,PATDAY,LEVNUM,LEVDAY,RETNUM
          GOTO      OVERCOND IF OVER
          RETURN
.
RDKWRD    MOVE      ZERO,OVRCD
          READKS    PWARD;XWARD,TEMPDAT,PATADM,PATIN,PATDSH:
                    PATOUT,PATDAY,LEVNUM,LEVDAY,RETNUM
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSWRD    MOVE      ZERO,OVRCD
          RESET     KEY11
          READ      PWARD,KEY11;;
          GOTO      OVERCOND IF OVER
          RETURN
.
UPWRD     UPDATE    PWARD;XWARD,TEMPDAT,PATADM,PATIN,PATDSH:
                    PATOUT,PATDAY,LEVNUM,LEVDAY,RETNUM
          RETURN
+
.
.-----    I/O routines for temp index file    -----
.
RATMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;;
          RETURN
.
RDTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READKS    PATMP1XX;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READKP    PATMP1XX;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          WRITE     PATMP1XX,KEY8;KEY8
          RETURN
.
UPTMPR1   UPDATE    PATMP1XX;TMPADMN
          RETURN
.
DETMPR1   RESET     KEY8
          DELETE    PATMP1XX,KEY8
          RETURN
+
.
ENDIT     CHAIN     PGM
          STOP
.
.==============================================================================
.
          INC       STD001IO
          INC       PATCODKY
          INC       PATHSPKY
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       PATHSPIO/INC
          INC       PATDAYIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC

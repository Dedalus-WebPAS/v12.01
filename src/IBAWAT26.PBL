.******************************************************************************
.* System   : WAITING LIST                                                    *
.* Program  : IBAWAT26           (N.Z. Only)                                  *
.* Desc     : Summary of Surgical Waiting Lists                               *
.******************************************************************************
.* Date     :  13/02/90                                                       *
.* Author   :  K.Peak                                                         *
.* Version  :  V5.01.01                                                       *
.* Mods.    :                                                                 *
.*             V10.05.01 05/01/2015  Ebon Clements     CAR 261630             *
.*                       Removed port number from temp file name              *
.*             V5.07.B01 23/11/1998  Nicole Harrington                        *
.*                       507 changes                                          *
.*             V5.01.02  09/03/90 Steve O'Gorman      SRF 104238              *
.*                       Added Changes for 'Cheryl at Wel'                    *
.*             V5.01.03  20/06/90 Steve O'Gorman      SRF 104919              *
.*                       Modify 'Booked Admission' to 'Forward Calling'       *
.*                       Added Footnote for Forward Calling                   *
.*                       Made Foward Calling Bookings + Pre-Admissions        *
.*                       Stop Average Time Being 99999.99 if 0 pats           *
.*                       Made Total Average the Average of all Totals         *
.*             V5.01.04  03/07/90 Steve O'Gorman      SRF 104919              *
.*                       Modify 1/2 the above crap again                      *
.*             V5.01.10  11/07/90 Bill Dew                                    *
.*                       Make changes for wellington requiring a new "Pending *
.*                       status".  Split Number of patients on waiting list to*
.*                       Number of day cases and inpatients.                  *
.*             V5.01.11  06/11/90 i chung             SRF 106920              *
.*                       Changed option headings on report as per SRF 106919  *
.*                       Correct Key 3 of wattr1af in WAT90 from 26 to 27     *
.******************************************************************************
.
          INC       STD001FD
          INC       PATCOMM/INC
.
          INC       IBASEQFD/INC
          INC       PATCODFD/INC     * codes file
          INC       PATCONFD/INC     * control file
          INC       PATDRGFD/INC     * period date range file
          INC       TFILEVAR/INC
          INC       WATCONFD/INC     * w/list control file
          INC       WATUDSFD/INC     * w/list unit/doctor summary file
          INC       WATUNSFD/INC     * w/list unit summary file
          INC       WEBERRFD/INC
+
.**********************************************************************
.*                         VARIABLES                                  *
.**********************************************************************
.
ANATMP    DIM       8       * temporary file index 1
AVTIME    FORM      8.1     * average time on waiting list
.
CLNDESC   DIM       20      * clinic/unit description
CMDLINE   DIM       50
CODEWU    INIT      "WU"    * unit code
CSTPER    DIM       6       * ccyymm
CURPER    DIM       6       * current period
.
DRNG1     FORM      2       * month range 1 + 1
DRNG2     FORM      2       * month range 2 + 1
DRNG3     FORM      2       * month range 3 + 1
DRNG4     FORM      2       * month range 4 + 1
DRNG5     FORM      2       * month range 5 + 1
.
ENDDATE   DIM       10      * enddate
ERNG1     FORM      3       * day range 1 + 1
ERNG2     FORM      3       * day range 2 + 1
ERNG3     FORM      3       * day range 3 + 1
ERNG4     FORM      3       * day range 4 + 1
ERNG5     FORM      3       * day range 5 + 1
.
FADMDTE   DIM       8       * ddmmyy
FORM8     FORM       8
.
GAVTIME   FORM       8.1    * Average time on waiting list
GBOOKED   FORM       8      * No. on list who are booked but not attd g/total
GDAYCAS   FORM       8      * total number of day case patients
GINPAT    FORM       8      * total number of inpatients
GPATS     FORM       8      * Number of patients on waiting list g/total
GPATRG1   FORM       8      * Number on list < WTCNMRG1 g/total
GPATRG2   FORM       8      * Number on list >= WTCNMRG1 <= WTCNMRG2 g/total
GPATRG3   FORM       8      * Number on list > WTCNMRG2 <= WTCNMRG3 g/total
GPATRG4   FORM       8      * Number on list > WTCNMRG3 <= WTCNMRG4 g/total
GPATRG5   FORM       8      * Number on list > WTCNMRG4 <= WTCNMRG5 g/total
GPATRG6   FORM       8      * Number on list > WTCNMRG5 g/total
.
.
MRNG1     FORM      2.1      * middle point of range 1
MRNG2     FORM      2.1      * middle point of range 2
MRNG3     FORM      2.1      * middle point of range 3
MRNG4     FORM      2.1      * middle point of range 4
MRNG5     FORM      2.1      * middle point of range 5
MRNG6     FORM      2.1      * middle point of range 6
.
PATRG1    FORM      8.1      * Number on list < WTCNMRG1         
PATRG2    FORM      8.1      * Number on list >= WTCNMRG1 <= WTCNMRG2
PATRG3    FORM      8.1      * Number on list > WTCNMRG2 <= WTCNMRG3
PATRG4    FORM      8.1      * Number on list > WTCNMRG3 <= WTCNMRG4
PATRG5    FORM      8.1      * Number on list > WTCNMRG4 <= WTCNMRG5
PATRG6    FORM      8.1      * Number on list > WTCNMRG5
PDATE     DIM       7        * period for printing
PFDATE    DIM       10       * print from date
PTDATE    DIM       10       * print to date
.
RNG1      FORM      2        * month range 1 ** actual ranges
RNG2      FORM      2        * month range 2
RNG3      FORM      2        * month range 3
RNG4      FORM      2        * month range 4
RNG5      FORM      2        * month range 5
.
SRNG1     FORM      3        * month range 1  ** used in initial calc'ns only
SRNG2     FORM      3        * month range 2
SRNG3     FORM      3        * month range 3
SRNG4     FORM      3        * month range 4
SRNG5     FORM      3        * month range 5
STRDATE   DIM       10       * start date
.
TADMDTE   DIM       8        * ddmmyy
TODATE    DIM       8        * todate 
TOTPATS   FORM      8        * Total number of patients on waiting list
.
ANATMPXX  IFILE SQL, FIXED=55, KEY="u1-3"
.
.----------------------------------------------------------------------
.         Temporary File Variables
.
.Name     Type  Length  Physical  Start   Description
.----     ----  ------  --------  -----   -----------
XUNIT     DIM        3         3      1   Unit Code (cat WU)
XNDAYCAS  FORM       8         5      4   Number of day case patients
XNINPAT   FORM       8         5      9   Number of inpatients
XNPATS    FORM       8         5     14   Number of patients on waiting list
XNPATRG1  FORM       8         5     19   Number on list < WTCNMRG1         
XNPATRG2  FORM       8         5     24   Number on list >= WTCNMRG1 <= WTCNMRG2
XNPATRG3  FORM       8         5     29   Number on list > WTCNMRG2 <= WTCNMRG3
XNPATRG4  FORM       8         5     34   Number on list > WTCNMRG3 <= WTCNMRG4
XNPATRG5  FORM       8         5     39   Number on list > WTCNMRG4 <= WTCNMRG5
XNPATRG6  FORM       8         5     44   Number on list > WTCNMRG5
XNBOOKED  FORM       8         5     49   Number on list who are booked but 
.                                         not attended
.
.End of record                       55
.
PRGID     INIT      "IBAWAT26"
PRGNAM    INIT      "Summary Of Surgical Waiting Lists"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                         MAINLINE                                   *
.**********************************************************************
ML0000    
.MLQQ      SPLOPEN   "data"
          CALL      INIT0000        * initialisation
.
ML1000    CALL      OPTN0000         * select option
          BRANCH    OPTION,ML1500,ML1500
.
ML9000    CALL      KILL0000        * remove temporary file 
.MLQQQ     SPLCLOSE  "data"
          CHAIN     PGM
.
ML1500    CALL      CLR0000         * clear variables
.
          CALL      DAT0000         * keyin period
          BRANCH    EXIT,ML1000
          CALL      CONTQST         * ok to continue y/n ?
          BRANCH    CEXIT,ML2000,ML1500,ML1000
.
ML2000    CALL      CREA0000        * create temporary file 
          CALL      DSPS0000        * display screen       
.
          CALL      WATN0000        * process watunsfd data
          BRANCH    OPTION,ML2222,ML3333
ML2222    CALL      WATS0000        * process watudsfd data
ML3333    CALL      PRNT0000        * print detail data  
          CALL      END0000         * end of report
          GOTO      ML1000
+
.**********************************************************************
.*                         INIT0000                                   *
.*                      Initialisation                                *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P64:24,"watudsaf";
          OPEN      WATUDSA1,"watudsaf"
.
          DISPLAY   *P64:24,"watunsaf";
          OPEN      WATUNSA1,"watunsaf"
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,ANATMP
.
          READ      CONTROLF,THIRTY7;*158,WTCNMRG1,WTCNMRG2,WTCNMRG3,WTCNMRG4:
                                          WTCNMRG5
          READ      CONTROLF,FIFTY9;*27,CPERMTH
.
.         To print the headings in month ranges when the control file ranges
.         are in days, we must divide each range by 30 
.
          MOVE      WTCNMRG1,SRNG1
          DIV       THIRTY,SRNG1
          MOVE      SRNG1,RNG1
.
          MOVE      WTCNMRG2,SRNG2
          DIV       THIRTY,SRNG2
          MOVE      SRNG2,RNG2
.
          MOVE      WTCNMRG3,SRNG3
          DIV       THIRTY,SRNG3
          MOVE      SRNG3,RNG3
.
          MOVE      WTCNMRG4,SRNG4
          DIV       THIRTY,SRNG4
          MOVE      SRNG4,RNG4
.
          MOVE      WTCNMRG5,SRNG5
          DIV       THIRTY,SRNG5
          MOVE      SRNG5,RNG5
.
.         Find the middle point of each range. As range 6 is xx months plus
.         you need to add six to the start of the period to get the mid point
.
          MOVE      RNG1,MRNG1
          DIV       TWO,MRNG1
.
          MOVE      RNG2,MRNG2
          ADD       RNG1,MRNG2
          DIV       TWO,MRNG2
.
          MOVE      RNG3,MRNG3
          ADD       RNG2,MRNG3
          DIV       TWO,MRNG3
.
          MOVE      RNG4,MRNG4
          ADD       RNG3,MRNG4
          DIV       TWO,MRNG4
.
          MOVE      RNG5,MRNG5
          ADD       RNG4,MRNG5
          DIV       TWO,MRNG5
.
          MOVE      RNG5,MRNG6
          ADD       SIX,MRNG6
.
          MOVE      RNG1,DRNG1
          ADD       ONE,DRNG1
          MOVE      RNG2,DRNG2
          ADD       ONE,DRNG2
          MOVE      RNG3,DRNG3
          ADD       ONE,DRNG3
          MOVE      RNG4,DRNG4
          ADD       ONE,DRNG4
          MOVE      RNG5,DRNG5
          ADD       ONE,DRNG5
.
          RETURN
+
.**********************************************************************
.*                         CLR0000                                    *
.*                     Clear Variables                                *
.**********************************************************************
.
CLR0000   MOVE      SP3,XUNIT      * Unit Code (cat WU)
          MOVE      ZERO,XNPATS    * Number of patients on waiting list
          MOVE      ZERO,XNPATRG1  * Number on list < WTCNMRG1
          MOVE      ZERO,XNPATRG2  * Number on list >= WTCNMRG1 <= WTCNMRG2
          MOVE      ZERO,XNPATRG3  * Number on list > WTCNMRG2 <= WTCNMRG3 
          MOVE      ZERO,XNPATRG4  * Number on list > WTCNMRG3 <= WTCNMRG4
          MOVE      ZERO,XNPATRG5  * Number on list > WTCNMRG4 <= WTCNMRG5
          MOVE      ZERO,XNPATRG6  * Number on list > WTCNMRG5
          MOVE      ZERO,XNBOOKED  * Number on list who are booked but not attnd
          MOVE      ZERO,AVTIME    * Average time on waiting list
.
          MOVE      ZERO,XNDAYCAS      * zero number of day cases
          MOVE      ZERO,XNINPAT       * zero number of inpatients
          MOVE      ZERO,GPATRG1 
          MOVE      ZERO,GPATRG2
          MOVE      ZERO,GPATRG3 
          MOVE      ZERO,GPATRG4 
          MOVE      ZERO,GPATRG5
          MOVE      ZERO,GPATRG6
          MOVE      ZERO,GBOOKED
          MOVE      ZERO,GAVTIME    
          MOVE      ZERO,GPATS     * grand totals for days cases and inpatients
          MOVE      ZERO,GDAYCAS   * grand total for day cases
          MOVE      ZERO,GINPAT    * grand total for inpatients
          RETURN
.**********************************************************************
.*                         OPTN0000                                   *
.*                      Main Option Screen                            *
.**********************************************************************
OPTN0000  HLINE     *G37,2,57,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Non-Pending Patients Only":
                    *P1:6,*V2LON,"2",*HOFF," = Pending Patients Only" 
OPTN1000
          KEYIN     *P1:8,"Select Option : _",*P17:8,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
          BRANCH    OPTION,OPTN9000,OPTN9000
          BEEP
          GOTO      OPTN1000
OPTN9000
OPTN9999  RETURN
.**********************************************************************
.*                         DAT0000                                    *
.*                       Keyin Period                                 *
.**********************************************************************
DAT0000   
          DISPLAY   *P1:11,*EF,"Enter the ",CPERMTH," of the report : "
.
          PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
          CALL      GPERD
          BRANCH    EXIT,DAT2000
.
          PACK      CURPER,DRGYR,DRGNUM
.
.         Find previous period
.
          PACK      KEY6,CURPER
          CALL      RDDRGA1
          CALL      RDPDRGA1
          PACK      CSTPER,DRGCYR,DRGCNUM
.
          UNPACK    CSTPER,CCENT,CYEAR,CMON
          MOVE      ZERO,CHIGHLT         * 0 = highlight 1 = no highlight
          MOVE      "34",CCOL            * column for keyin
          MOVE      TEN1,CVERT           * row for keyin
          CALL      KEYPER
          BRANCH    OVRCD,DAT0000        * no date keyed in
.
.         Validate the date entered
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,DAT3000
          PACK      PDATE,CMON,SLASH,CCENT,CYEAR
.
.         Pack appropriate start dates
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,STRDATE
          PACK      FADMDTE,CCENT,CYEAR,CMON,CDAY
.
.         Pack appropriate end dates
.
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,ENDDATE
          PACK      TADMDTE,CCENT,CYEAR,CMON,CDAY
.
          PACK      CSTPER,DRGYR,DRGNUM
.
          DISPLAY   *P43:11,*EL,DRGLDSC:
                    *P43:12,*EL,STRDATE," to ",ENDDATE
          GOTO      DAT8000
.
.         No current period on file
.
DAT2000   DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Error: Current date not found in Period file.  ";
          CALL      HITENTER
          GOTO      DAT9000
.
.         Invalid date entered
.
DAT3000   KEYIN     *P1:24,*EL,*B,*V2LON:
                    "** Invalid Date **",*EOFF,ANS,*P1:24;
          GOTO      DAT0000
.
DAT8000   MOVE      FALSE,EXIT
          GOTO      DAT9999
.
DAT9000   MOVE      TRUE,EXIT
DAT9999   RETURN
+
.**********************************************************************
.*                         DSPS0000                                   *
.*                       Display Screen                               *
.**********************************************************************
.
DSPS0000  MOVE      ZERO,CPAGENO   * page counter
          MOVE      "60",CLNO      * line counter
          CLOCK     TIME,CTIMEIS   * find time report started
.
          DISPLAY   *P1:24,*EL,*P20:24:
                    "Unit : ",*P48:24,"Scanning : "
          RETURN
+
.**********************************************************************
.*                         WATN0000                                   *
.*                   Process WATUNSFD Data                            *
.*   Modified 11/07/1990 Bill Dew                                     *
.*   Wellington changes for "Pending status" In Option 1 waiting list *
.*   report will exclude all records with pending status.  Meanwhile  *
.*   Option 2 will accept only records with pending priority.         *
.**********************************************************************
WATN0000  
.         Loop through the waiting list unit summary file and put the data 
.         found into the temporary file
.
          PACK      KEY18,CSTPER,SP20
          CALL      RDSUNSA1
WATN1000 
          CALL      RDKUNSA1
.WATNQQ    DISPLAY   *P1:3,"WATN>[",WTUNDATE,WTUNUNIT,WTUNDOCT,WTUNPRTY,"]":
.                    *P1:23,WTUNDYCS,WTUNINPT,WTUNRNG1,WTUNRNG2,WTUNRNG3:
.                    WTUNRNG4,WTUNRNG5,WTUNRNG6;
.WATNQQK1  KEYIN     ANS
          BRANCH    OVRCD,WATN9999
.
          DISPLAY   *P59:24,*EL,WTUNUNIT;
.
          MATCH     WTUNDATE,CSTPER   * test reached end of report range
          GOTO      WATN9999 IF NOT EQUAL
.
          PACK      KEY5,ANST,ANSP,WTUNPRTY,SP5
          CALL      RDCODE1            * get 2nd indicator of priority code
          BRANCH    OVRCD,WATN1000
.
.WATNQQQ   DISPLAY   *P40:3,"code [",WTUNPRTY,"] 2nd ind [",TCINDC2,"]";
.WATNQQK   KEYIN     ANS,*P40:3,*EL,*P1:23,*EL
          BRANCH    OPTION,WATN2222,WATN3333
WATN2222
          MATCH     ANSP,TCINDC2       * test for pending status
          GOTO      WATN1000 IF EQUAL
          GOTO      WATN4444
WATN3333
          MATCH     ANSP,TCINDC2       * test for pending status
          GOTO      WATN1000 IF NOT EQUAL
WATN4444
          CALL      CUDN0000           * write data to temp files
          GOTO      WATN1000
.
WATN9999  RETURN
.**********************************************************************
.*                         CUDN0000                                   *
.*                Write watunsaf data to Temporary File               *
.**********************************************************************
.
CUDN0000  PACK       KEY5,CODEWU,WTUNUNIT
          CALL       RDCODE1
.
.         Check whether the unit is a surgical unit, as we only 
.         want surgical units
.
          MATCH      ANSS,TCINDC1
          GOTO       CUDN9999 IF NOT EQUAL
.
          DISPLAY    *P27:24,*V2LON,WTUNUNIT;
.
          PACK      KEY3,WTUNUNIT
          CALL      RDTMP1
          BRANCH    OVRCD,CUDN1000
.
          ADD       WTUNRNG1,XNPATRG1
          ADD       WTUNRNG2,XNPATRG2
          ADD       WTUNRNG3,XNPATRG3
          ADD       WTUNRNG4,XNPATRG4
          ADD       WTUNRNG5,XNPATRG5
          ADD       WTUNRNG6,XNPATRG6
.
          ADD       WTUNDYCS,XNDAYCAS
          ADD       WTUNINPT,XNINPAT
          ADD       WTUNDYCS,XNPATS
          ADD       WTUNINPT,XNPATS
.
          CALL      UPTMP1
          GOTO      CUDN9999
.
CUDN1000  MOVE      WTUNUNIT,XUNIT
.
          MOVE      WTUNRNG1,XNPATRG1
          MOVE      WTUNRNG2,XNPATRG2
          MOVE      WTUNRNG3,XNPATRG3
          MOVE      WTUNRNG4,XNPATRG4
          MOVE      WTUNRNG5,XNPATRG5
          MOVE      WTUNRNG6,XNPATRG6
.
          MOVE      WTUNDYCS,XNDAYCAS
          MOVE      WTUNINPT,XNINPAT
          MOVE      WTUNDYCS,XNPATS
          ADD       WTUNINPT,XNPATS
          MOVE      ZERO,XNBOOKED
.
          CALL      WRTMP1
.
CUDN9999  RETURN
+
.**********************************************************************
.*                         WATS0000                                   *
.*                   Process WATUDSFD Data                            *
.*   Doctor summary file now has a priority variable for identifying  *
.*   pending status.  Test for pending priority in watudsaf.          *
.**********************************************************************
WATS0000  
.         Loop through the unit/doctor summary file and put the data 
.         found into the temporary file
.
          PACK       KEY15,CSTPER,SP20
          CALL       RDSUDSA1
WATS1000  CALL       RDKUDSA1
          BRANCH     OVRCD,WATS9999
.
          DISPLAY    *P59:24,*EL,WTUDUNIT;
.
          MATCH     WTUDDATE,CSTPER    * test same period
          GOTO      WATS9999 IF NOT EQUAL
.
          PACK      KEY5,ANST,ANSP,WTUNPRTY,SP5
          CALL      RDCODE1            * get 2nd indicator of priority code
          BRANCH    OVRCD,WATS1000
          BRANCH    OPTION,WATS2222,WATS3333
WATS2222
          MATCH     ANSP,TCINDC2       * test for pending status
          GOTO      WATS1000 IF EQUAL
          GOTO      WATS4444
WATS3333
          MATCH     ANSP,TCINDC2       * test for pending status
          GOTO      WATS1000 IF NOT EQUAL
WATS4444
          PACK      KEY5,CODEWU,WTUDUNIT    * test code is surgical unit
          CALL      RDCODE1
          MATCH     ANSS,TCINDC1
          GOTO      WATS1000 IF NOT EQUAL
.
          CALL      CUDS0000           * write data to temp files
          GOTO      WATS1000
.
WATS9999  RETURN
.**********************************************************************
.*                         CUDS0000                                   *
.*                Write watudsaf data to Temporary File               *
.**********************************************************************
CUDS0000  
          DISPLAY   *P27:24,*V2LON,WTUDUNIT;
.
CUDS5500  PACK      KEY3,WTUDUNIT
          CALL      RDTMP1
          BRANCH    OVRCD,CUDS6000
.
          ADD       WTUDBOOK,XNBOOKED
.
          CALL      UPTMP1
          GOTO      CUDS9999
.
CUDS6000  MOVE      WTUDUNIT,XUNIT
          MOVE      WTUDBOOK,XNBOOKED
.
          MOVE      ZERO,XNPATRG1
          MOVE      ZERO,XNPATRG2
          MOVE      ZERO,XNPATRG3
          MOVE      ZERO,XNPATRG4
          MOVE      ZERO,XNPATRG5
          MOVE      ZERO,XNPATRG6
          MOVE      ZERO,XNDAYCAS
          MOVE      ZERO,XNINPAT
          MOVE      ZERO,XNPATS
.
          CALL      WRTMP1
.
CUDS9999  RETURN
+
.**********************************************************************
.*                         END0000                                    *
.*                      End of Report                                 *
.**********************************************************************
.
END0000   COMPARE   ZERO,CPAGENO
          GOTO      END1000 IF NOT EQUAL
.
.         No data selected
.
          DISPLAY   *P1:24,*EL,*P28:24,*V2LON:
                    "** No Record Selected **",*W;
          CALL      HEAD0000
          PRINT     *N,*N,"***  End of Report  ***"
          GOTO      END9999
.
END1000   COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
.         Print Totals (Grand Totals)
.
          MOVE      GPATRG1,PATRG1
          MULT      MRNG1,PATRG1
          ADD       PATRG1,GAVTIME
.
          MOVE      GPATRG2,PATRG2
          MULT      MRNG2,PATRG2
          ADD       PATRG2,GAVTIME
.
          MOVE      GPATRG3,PATRG3
          MULT      MRNG3,PATRG3
          ADD       PATRG3,GAVTIME
.
          MOVE      GPATRG4,PATRG4
          MULT      MRNG4,PATRG4
          ADD       PATRG4,GAVTIME
.
          MOVE      GPATRG5,PATRG5
          MULT      MRNG5,PATRG5
          ADD       PATRG5,GAVTIME
.
          MOVE      GPATRG6,PATRG6
          MULT      MRNG6,PATRG6
          ADD       PATRG6,GAVTIME
.
          DIV       GPATS,GAVTIME
.
          PRINT     *N,*15,"Totals",*23,GDAYCAS,*33,GINPAT,*43,GPATS:
                    *53,GPATRG1,*63,GPATRG2,*73,GPATRG3:
                    *83,GPATRG4,*93,GPATRG5,*103,GPATRG6:
                    *113,GBOOKED,*123,GAVTIME
          ADD       ONE,CLNO
.
          CALL      PGE0000
.
          MOVE      WTCNMRG1,ERNG1  *** set up the day range variables
          ADD       ONE,ERNG1
          MOVE      WTCNMRG2,ERNG2
          ADD       ONE,ERNG2
          MOVE      WTCNMRG3,ERNG3
          ADD       ONE,ERNG3
          MOVE      WTCNMRG4,ERNG4
          ADD       ONE,ERNG4
          MOVE      WTCNMRG5,ERNG5
          ADD       ONE,ERNG5
.
          COMPARE   "50",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *N,*N,"Note: This report uses 30 day months.":
                    *N,"      The actual day ranges are : ":
                    "Less than ",RNG1," Month =   0 to ",WTCNMRG1," Days":
                    *75,"Average Time    = The sum of the number of ":
                                          "patients in":
                    *N,*36,RNG1," to ",RNG2," Months   = ",ERNG1," to ":
                    WTCNMRG2," Days":
                    *93,"each date range multiplied by the mid-":
                    *N,*36,DRNG2," to ",RNG3," Months   = ",ERNG2," to ":
                    WTCNMRG3," Days":
                    *93,"point of that range, in months, divided":
                    *N,*36,DRNG3," to ",RNG4," Months   = ",ERNG3," to ":
                    WTCNMRG4," Days":
                    *93,"by the total number of patients on the":
                    *N,*36,DRNG4," to ",RNG5," Months   = ",ERNG4," to ":
                    WTCNMRG5," Days":
                    *93,"list.":
                    *N,*37,"Over ",RNG5," Months   = Over   ",WTCNMRG5:
                           " Days":
                    *75,"Forward Calling = Total number on list who are ":
                        "booked ":
                    *N,*93,"or pre-admitted as at the end date":
                    *N,*93,"of the period":
                    *N,*N,"***  End of Report  ***"
.
          DISPLAY   *P1:23,*EF,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W;
END9999   RETURN
.**********************************************************************
.*                         PRNT0000                                   *
.*                        Print Data                                  *
.**********************************************************************
.
PRNT0000  DISPLAY   *P1:24,*EL,*P29:24,*V2LON:
                    "*** Printing Report ***",*W:
                    *P1:24,*EL,*P20:24,*HOFF,"Unit : "
.
          MOVE      SP3,KEY3
          CALL      RDSTMP1
.
PRNT1000  CALL      RDKTMP1
          BRANCH    OVRCD,PRNT9999
.
          MOVE      ZERO,AVTIME
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          DISPLAY   *P27:24,*V2LON,XUNIT;
.
          MOVE      "UNKNOWN CLINIC      ",CLNDESC     * clinic description
          PACK      KEY5,CODEWU,XUNIT
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT2000
          MOVE      TDESC,CLNDESC
.
.         To find the average waiting time on waiting list, you need to 
.         multiple the number of patients in each range by the middle point 
.         of that range
.
PRNT2000  MOVE      XNPATRG1,PATRG1
          MULT      MRNG1,PATRG1
          ADD       PATRG1,AVTIME
.
          MOVE      XNPATRG2,PATRG2
          MULT      MRNG2,PATRG2
          ADD       PATRG2,AVTIME
.
          MOVE      XNPATRG3,PATRG3
          MULT      MRNG3,PATRG3
          ADD       PATRG3,AVTIME
.
          MOVE      XNPATRG4,PATRG4
          MULT      MRNG4,PATRG4
          ADD       PATRG4,AVTIME
.
          MOVE      XNPATRG5,PATRG5
          MULT      MRNG5,PATRG5
          ADD       PATRG5,AVTIME
.
          MOVE      XNPATRG6,PATRG6
          MULT      MRNG6,PATRG6
          ADD       PATRG6,AVTIME
.
.  -----  Make Average Time on W.L. to be Time / No. Patients on W.L.  -----
.
          COMPARE   ZERO,XNPATS
          GOTO      PRNT3000 IF NOT EQUAL
          MOVE      ZERO,AVTIME
          GOTO      PRNT4000
.
PRNT3000  DIV       XNPATS,AVTIME
.
PRNT4000  
          MOVE      XNDAYCAS,FORM8
          ADD       XNINPAT,FORM8
          PRINT     *N,CLNDESC,*23,XNDAYCAS,*33,XNINPAT,*43,FORM8:
                    *53,XNPATRG1,*63,XNPATRG2,*73,XNPATRG3:
                    *83,XNPATRG4,*93,XNPATRG5,*103,XNPATRG6:
                    *113,XNBOOKED,*123,AVTIME
          ADD       TWO,CLNO
.
          ADD       XNPATRG1,GPATRG1
          ADD       XNPATRG2,GPATRG2
          ADD       XNPATRG3,GPATRG3
          ADD       XNPATRG4,GPATRG4
          ADD       XNPATRG5,GPATRG5
          ADD       XNPATRG6,GPATRG6
          ADD       XNBOOKED,GBOOKED
          ADD       AVTIME,GAVTIME
          ADD       XNDAYCAS,GDAYCAS
          ADD       XNINPAT,GINPAT
          ADD       XNPATS,GPATS        * add totals to grand totals
          GOTO      PRNT1000
PRNT9999  RETURN
.**********************************************************************
.*                         HEAD0000                                   *
.*                      Print Headings                                *
.**********************************************************************
HEAD0000  COMPARE   ZERO,CPAGENO
          CALL      PGE0000 IF NOT EQUAL
.
          MOVE      ONE,CNOUNDLN
          CALL      HEAD132
.
          PRINT     *40,"Period : ",PDATE,*59,DRGLDSC:
                    *N,*59,STRDATE," to ",ENDDATE,*N
.
          BRANCH    OPTION,HEAD2222,HEAD3333
HEAD2222
          PRINT     *40,"NON-PENDING PATIENTS ONLY",*N
          GOTO      HEAD4444
HEAD3333
          PRINT     *40,"PENDING PATIENTS ONLY",*N
HEAD4444
          CALL      PGE0000
.
          PRINT     *25,"Number of patients on the   --- ":
                    "Number of Patients who have been on a Waiting List ---":
                    *114,"No. of    Average"
.
          PRINT     "Clinic Speciality",*25,"----- Waiting List -----":
                    *56,"Under   Between   Between   Between   Between":
                    *105,"Over",*114,"Forward",*124,"Time on"
.
          PRINT     *26,"Day",*57,RNG1,*64,RNG1," & ",RNG2,*74,DRNG2," & ",RNG3:
                    *84,DRNG3," & ",RNG4,*94,DRNG4," & ",RNG5:
                    *106,RNG5,*114,"Calling",*124,"Waiting"
.
          PRINT   *24,"Patient",*33,"Inpatient    Total":
                  *56,"Month    Months    Months    Months    Months    Months":
                  *122,"List (Mnth)"
.
          CALL      PGE0000
          MOVE      TEN4,CLNO
.
HEAD9999  RETURN
.**********************************************************************
.*                         PGE0000                                    *
.*                        Print Line                                  *
.**********************************************************************
.
PGE0000   PRINT     *1,"---------------------------------------------------":
                    "---------------------------------------------------":
                    "------------------------------"
          ADD       ONE,CLNO
          RETURN
+
.**********************************************************************
.*                         CREA0000                                   *
.*                  Create Temporary File                             *
.**********************************************************************
CREA0000  CALL      KILL0000
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    ANATMP,CMDLINE
          APPEND    " 55 u1-3",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      ANATMPXX,ANATMP
          RETURN
.**********************************************************************
.*                         KILL0000                                   *
.*                  Remove Temporary File                             *
.**********************************************************************
.
KILL0000  CLOSE     ANATMPXX
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    ANATMP,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.**********************************************************************
.*              I/O For Temporary File                                *
.**********************************************************************
.
.         Temporary File index 1
.
RDSTMP1   MOVE      ZERO,OVRCD
          RESET     KEY3
          READ      ANATMPXX,KEY3;;
          RETURN
.
RDKTMP1   MOVE      ZERO,OVRCD
          READKS    ANATMPXX;XUNIT,XNDAYCAS,XNINPAT,XNPATS:
                    XNPATRG1,XNPATRG2,XNPATRG3,XNPATRG4,XNPATRG5,XNPATRG6:
                    XNBOOKED
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTMP1    MOVE      ZERO,OVRCD
          RESET     KEY3
          READ      ANATMPXX,KEY3;XUNIT,XNDAYCAS,XNINPAT,XNPATS:
                    XNPATRG1,XNPATRG2,XNPATRG3,XNPATRG4,XNPATRG5,XNPATRG6:
                    XNBOOKED
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMP1    UPDATE    ANATMPXX;XUNIT,XNDAYCAS,XNINPAT,XNPATS:
                    XNPATRG1,XNPATRG2,XNPATRG3,XNPATRG4,XNPATRG5,XNPATRG6:
                    XNBOOKED
          RETURN
.
WRTMP1    RESET     KEY3
          WRITE     ANATMPXX,KEY3;KEY3,XNDAYCAS,XNINPAT,XNPATS:
                    XNPATRG1,XNPATRG2,XNPATRG3,XNPATRG4,XNPATRG5,XNPATRG6:
                    XNBOOKED
          RETURN

.**********************************************************************
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
          INCLUDE   STD001IO
          INCLUDE   PATCOMN3
          INC       TFILENAM
.
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
          INCLUDE   PATDRGIO/INC       * period date range file
          INC       PATCODIO/INC       * patient codes file
          INCLUDE   WATUDSIO/INC       * w/list unit/doctor summary file
          INCLUDE   WATUNSIO/INC       * w/list unit summary file

. ***************************************************************************
. *              P R O G R A M  S U M M A R Y                               *
. *              -------------  -------------                               *
. *                                                                         *
. *     PROGRAM NAME:     IBAPMS73                                          *
. *                                                                         *
. *     FUNCTION:         PRS2 EXCLUSION/INCLUSION REPORT                   *
. *                                                                         *
. *     DATE WRITTEN:     27/03/2003                                        *
. *                                                                         *
. *     AUTHOR:           Lina Vo       (I.B.A)    CAR35150                 *
. *                                                                         *
. *     MODIFICATIONS:                                                      *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
. *       V9.03.01  10/02/2004  Davin         CAR 22654                     *
. *                 Added extract status 4 (include & delete)               *
. *       V9.02.00  27/03/2003 Created                                      *
. *                                                                         *
. ***************************************************************************
+
          INC       STD001FD
.
          INC       PATCODFD/INC            * Codes file
          INC       PATCONFD/INC            * Control file
          INC       PATMA1FD/INC            * Master file
          INC       PATMI1FD/INC            * Admission file
          INC       PMSVX1FD/INC            * Admission file
          INC       PATPEIFD/INC            * PRS Exclusion/Inclusion file
          INC       PATPR1FD/INC
          INC       PMSPX2FD/INC
.
.
. ----- Program Variables -----
.
ADMISSNO  DIM       8
.
CDYSDAYS  FORM      6                       * CALCDAYS no of days
CDYSFDTE  DIM       8                       * CALCDAYS from date (ccyymmdd)
CDYSTDTE  DIM       8                       * CALCDAYS to date (ccyymmdd)
CDYSYEAR  FORM      2                       * CALCDAYS year
CURDTE    DIM       8                       * Current date (ccyymmdd)
CURFLAG   FORM      1
.
DSPSTDTE  DIM       10                      * Display start date (dd/mm/ccyy)
.
EXTSTAT   DIM       20
.
REASON    DIM       20
.
PSAGETYP  DIM       3
PSAGECON  DIM       3
.
STATUS    DIM       12
STRTDTE   DIM       8                       * Start date (ccyymmdd)
.
TOTRDCNT  FORM      4                       * Total record counter
.
URNUMBER  DIM       8
. ----- Program Constants -----
.
ASTAT1    INIT      "Pre-admitted   "
ASTAT2    INIT      "Admitted       "
ASTAT3    INIT      "Discharged     "
ASTAT4    INIT      "On-leave       "
ASTAT5    INIT      "Cancelled      "
.
EXTSTAT1  INIT      "Excluded            "
EXTSTAT2  INIT      "Included            "
EXTSTAT3  INIT      "Deleted             "
EXTSTAT4  INIT      "Included and Deleted"
.
PRGID     INIT      "IBAPMS73"
PRGNAM    INIT      "PRS Exclusion/Inclusion Report"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    EXIT,ML9999
.
ML2000    CALL      DATE0000                * Keyin the start date 
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * Ok to continue (Y/N/C) ?
          BRANCH    CEXIT,ML4000,ML1000,ML1000
          GOTO      ML1000
.
ML4000    CALL      PROC0000                * Process the files
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                   INIT0000             Called by: ML0000   *
.*                        Initialise Variable & Open Files                    *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patpeiaf";
          OPEN      PATPEIO1,"patpeiaf"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patpr1af";
          OPEN      PATPR1A1,"patpr1af"
.
          READ      CONTROLF,TWENTY1;*205,PTCNUKEY
.
          MOVE      ONE,CNEWDS              * ? keyin on display
          MOVE      ONE,CNOUNDLN            * Dont print report header
          CLOCK     TIME,CTIMEIS
          PACK      CURDTE,CCC,CYY,CMM,CDD
          REP       " 0",CURDTE 
.
INIT9000  MOVE      ZERO,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Choose Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          MOVE      ZERO,CPAGENO            * Reset the page no
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Date Range":
                    *P1:7,"Select Option : ";
OPTN1000  KEYIN     *P17:7,*V2LON,*DV,UNDLN2,*P17:7,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  DATE0000              Called by: ML0000   *
.*                            Keyin The Date Range                            *
.******************************************************************************
DATE0000  MOVE      "17",CCOL
          DISPLAY   *P1:9,"Starting Date : ",*EF;
.
          MOVE      ONE,CCANLDTE            * Blank cancel default
.
. ----- Keyin start date -----
.
DATE1000  MOVE      "9",CVERT
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9500          * Nothing entered
          BRANCH    CQUEST,DATE1000
.
          PACK      STRTDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDTE
.
          CALL      PACDATE                 * Format the start date
          MOVE      CPCDATE,DSPSTDTE
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,DSPSTDTE;
.
          MATCH     STRTDTE,CURDTE 
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Start date must not be in the future.  ";
            CALL      HITENTER 
            GOTO      DATE1000
          ENDIF
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.******************************************************************************
.*                                  PROC0000              Called by: ML0000   *
.*                              Process The Files                             *
.******************************************************************************
PROC0000  MOVE      ZERO,TOTRDCNT           * Reset the total record counter
          MOVE      ZERO,CPAGENO            * Reset the page counter
          DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Printing : ";
          CALL      HEAD0000                * Print report header
          CALL      SHED0000                * Print sub report header
.
. ----- Loop thru the prs2 file -----
.
          READ      CONTROLF,EIGHTY8;*124,CHCSST,*132,CHCSED
          MATCH     STRTDTE,CHCSST
          IF        @EQUAL
            MOVE      ONE,CURFLAG
          ENDIF
.
          PACK      KEY16,STRTDTE,SP70
          CALL      RSPTPEI1
PROC1000  CALL      RKPTPEI1
          BRANCH    OVRCD,PROC8000
.
          MATCH     PTPESTDT,STRTDTE 
          GOTO      PROC8000 IF NOT EQUAL
.
          MOVE      SP70,STATUS
          MOVE      SP70,REASON
          MOVE      SP70,EXTSTAT
.
          PACK      KEY5,ANSR,ANSN,PTPERESN,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      TDESC,REASON
          ENDIF
.
          LOAD      EXTSTAT,PTPESTAT,EXTSTAT1,EXTSTAT2,EXTSTAT3,EXTSTAT4
.
          MOVE      PTPEADMN,ADMISSNO
          PACK      KEY8,PTPEADMN,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,PROC2000
.
          LOAD      STATUS,ASTAT,ASTAT1,ASTAT2,ASTAT3,ASTAT4,ASTAT5
.
PROC2000  MOVE      AURNO,URNUMBER          * URNUMBER used in GETPAT
          CALL      GETPAT00                * Get Patient Details
.
          CALL      PRNT0000                * Print a report line
.
          GOTO      PROC1000
.
PROC8000  CALL      UND132                  * Print underline
          PRINT     *1,"*** End of Report ***"
.
PROC9000  MOVE      ZERO,EXIT
PROC9999  RETURN
+
.------------------------------------------------------------
. Get Patient
.------------------------------------------------------------
GETPAT00  MATCH     "       0",URNUMBER
          IF        !@EQUAL
            MOVE      URNUMBER,KEY8
            CALL      RDMAST1
            MOVE      PURNO,KEY8
            CALL      RDPMPX21
            IF        OVRCD=1
              CALL      CLPMSPX2
            ENDIF
          ELSE
            CALL      CLPMSPX2
            MOVE      ADMISSNO,KEY8
            CALL      RDPRAM1
          ENDIF
.
          PACK      KEY50,SP70
.**       MOVE      PGNAME,ANS
.**       PACK      KEY50,ANS,DOT,PSNAME,SP30 * Patient name
          SQUEEZE   PSNAME
          PACK      KEY50,PSNAME,COMMA,PGNAME,SP70
.
GETPAT99  RETURN
.******************************************************************************
.*                                  HEAD0000              Called by: PROC0000 *
.*                             Print Report Header        PRNT0000 & TAIL0000 *
.******************************************************************************
HEAD0000  CALL      HEAD132                 * Print report header
          PRINT     *40,"Extract Start Date : ",DSPSTDTE,*N 
          ADD       "1",CLNO
.
HEAD9000  MOVE      ZERO,EXIT
HEAD9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: PROC0000 *
.*                             Print A Report Line                            *
.******************************************************************************
PRNT0000  IF        CLNO>=58
            CALL      UND132                * Print underline
            CALL      HEAD0000              * Print report header
            CALL      SHED0000              * Print report sub header
          ENDIF
.
          PRINT     *1,"|",PTPEADMN,*10,"|",KEY50,*61,"|",STATUS:
                    *74,"|",ATYPE,*82,"|",ACARE:
                    *90,"|",REASON,*111,"|",EXTSTAT:
                    *132,"|"
.
          ADD       ONE,CLNO
          ADD       "1",TOTRDCNT            * Increment the total record counter
.
PRNT9000  MOVE      ZERO,EXIT
PRNT9999  RETURN
+
.******************************************************************************
.*                                  SHED0000              Called by: PRNT0000 *
.*                           Print Report Sub Header                          *
.******************************************************************************
SHED0000  CALL      UND132                  * Print underline
            PRINT     *1,"| Visit",*10,"|",*61,"|":
                      *74,"| Adm",*82,"| Care",*90,"|":
                      *111,"| Extract",*132,"|",*N:
                      *1,"| Number",*10,"| Patient",*61,"| Status":
                      *74,"| Type",*82,"| Type",*90,"| Reason":
                      *111,"| Status",*132,"|"
          CALL      UND132                  * Print underline
          ADD       "4",CLNO
.
SHED9000  MOVE      ZERO,EXIT
SHED9999  RETURN
+
.******************************************************************************
.*                                  TAIL0000              Called by: PRNT0000 *
.*                              Print Report Tail                             *
.******************************************************************************
TAIL0000  CALL      UND132                  * Print underline
          ADD       "1",CLNO
.
          IF        CLNO>=51
            CALL      HEAD0000              * Print report header
          ENDIF
.
TAIL9000  MOVE      ZERO,EXIT
TAIL9999  RETURN
+
.==============================================================================
.
          INC       STD001IO
          INC       CALCDAYS                * Calculate no of days
.
          INC       PATCODIO/INC            * Codes file
          INC       PATMA1IO/INC            * Master file
          INC       PATMI1IO/INC            * Admission file
          INC       PMSVX1IO/INC            * Admission file
          INC       PATPEIIO/INC            * PRS Exclusion/Inclusion file
          INC       PATPR1IO/INC
          INC       PMSPX2IO/INC
.
          INC       CLPMSPX2
+
.


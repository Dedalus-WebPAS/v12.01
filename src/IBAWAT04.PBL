.******************************************************************************
.*  System       :  Waiting List                                              *
.*  Program      :  IBAWAT04                                                  *
.*  Function     :  Operator error correction.                                *
.*                  take removed patient from waiting list                    *
.******************************************************************************
.*  Date         :  30/11/93                                                  *
.*  Author       :  Rob Leonard                                               *
.*  Modifictions :                                                            *
.*                                                                            *
.******************************************************************************
.* V12.00.01 29.05.2025  DA Sarkies    TSK 0944096                            *
.*           Recompiled as WATTR1FD changes                                   *
.* V11.02.01 10/02/2022  Thanh T       TSK 0889899                            *
.*           Recompiled as WATTR1FD changes                                   *
.* V10.03.01 14/10/2013  Ania P           CAR278232                           *
.*           Recompiled                                                       *
.* V10.02.01 07/07/2011  Steve Armstrong  CAR 240722                          *
.*           Added RDPMPX21 prior to calling PMIHEAD                          *
.******************************************************************************
.* V9.03.04  25/06/2004 Peter Vela         50402                              *
.*           Recompiled                                                       *
.* V9.03.03  29/10/2003 Sandra Barcham     43783                              *
.*           Recompiled for NZHISIIO and NZHISIUP                             *
.* V9.03.02  20/08/2003  Sandra Barcham    42660                              *
.*           Fix 01092C I * C Open patvisaf                                   *
.* V9.03.01  13/08/2003  Sandra Barcham    42274                              *
.*           Defined WATHI002 with tilda's to put current date into           *
.*           WTHIEDAT to stop I * D                                           *
.******************************************************************************
.* V9.02.02  27/02/2003  Jill Habasque SRF 34660                              *
.*           Added WTCNBRSR                                                   *
.* V9.02.01  13/02/2003  Lina Vo        SRF22709                              *
.*           Removed open of outpreaf - not used                              *
.******************************************************************************
.*        V5.08.05  21/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.04  27/07/2000  Jill Habasque                                 *
.*                  Recompiled for WLHISSUB                                   *
.*        V5.08.03  17/07/2000  Jill Habasque                                 *
.*                  Recompiled for WLHISSUB                                   *
.*        V5.08.02  29/06/2000  Jill Habasque                                 *
.*                  Recompiled for WATHISFD                                   *
.*        V5.08.01  09/06/2000  Tonii Tang                                    *
.*                  Changed so when W/L status changes to "1", the booking    *
.*                  status code is set to "05" (deferred booking) and a new   *
.*                  record is written to the history file                     *
.******************************************************************************
.*        V5.04.02  16/06/1997  Steve Armstrong                               *
.*                  Fixed bugs in global recompile                            *
.*        V5.04.01  03/10/1996  Howellsy          EOC & ACC                   *
.*                  Generate Booking No when placed on the Waiting List       *
.******************************************************************************
.*        V5.01.03  24/03/1994    Matt Surridge                               *
.*                  Recompile for NZHISIIO and DONORDET.                      *
.*        V5.01.04  26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.02.01  30.11.1994    B.G.Ackland                                 *
.*                  Remove Date of Death Link to NHI/MWS                      *
.*                  Recompile for Latest NHI/MWS Changes                      *
.******************************************************************************
.
. ------------------  FD INCLUDES  -------------------------
.
          INC       STD001FD
.
          INC       BOKRC1FD/INC       * booking file
          INC       NZHISIFD/INC
          INC       OUTPREFD/INC       * OP pre-attendance file
          INC       PATALRFD/INC
          INC       PATCALAG/INC
          INC       PATCODFD/INC
          INC       PATCOMM/INC        * variables for surname search
          INC       PATCONFD/INC       * control file
          INC       PATDHEAD/INC
          INC       PATDNRFD/INC
          INC       PATMA1FD/INC       * patient master file
          INC       PATDSCFD/INC       * admission file
          INC       PATMI1FD/INC       * admission file
          INC       NHIMASFD/INC
          INC       PATNIDFD/INC
          INC       PATPR1FD/INC       * pre-admission file
          INC       PATSDNFD/INC
          INC       PATSMWFD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       PATGSRFD/INC       * surname file
          INC       PATVISFD/INC
          INC       PMIVARS/INC
          INC       WATTR1FD/INC       * waiting list patient procedures
          INC       WATHISFD/INC	      * waiting list history file
          INC       WATNBRFD/INC	      * waiting list history file
          INC       WATPROFD/INC	      * waiting list history file
          INC       WEBSECFD/INC
          INC       SCRPSTFD/INC
.
. --------------------  VARIABLES LIST  -------------------
.
ADDWLFLG  FORM      1
BJDAY     FORM       3
CHG       FORM       1
CJDAY     FORM       3
CMDLINE   DIM       80
CODE      DIM        2
COUNT     FORM       2                 * record count
DIM35     DIM       35
DOPTION   DIM        2
DY        DIM        2
FOPTION   FORM       2
FORM3     FORM       3
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
ITEMSEL   FORM       1
KEYARRAY  DIM       20[10]             * Stores first record of each screen
MODE1     DIM        1
MRCRDS    FORM       1                 * more records flag
MTH       DIM        2                 * month
RMDATE    DIM       10                 * display date
SCNDGNAM  DIM       40
SCOUNT    FORM       2                 * screen count
STATUS    FORM       1
SVERT     FORM       2                 * screen verticle position
WATTKEY   DIM       20
WKEY1     DIM       20
WKEY2     DIM       20
WKEY3     DIM       20 
WKEY4     DIM       20
WKEY5     DIM       20
WKEY6     DIM       20
WKEY7     DIM       20
WKEY8     DIM       20
WKEY9     DIM       20
WKEY10    DIM       20
WKEY11    DIM       20
WKEY12    DIM       20
WKEY13    DIM       20
WKEY14    DIM       20
WKEY15    DIM       20
WATHI002  INIT      "~~~~~~~~"
WTCNBRSR  FORM      1
YR        DIM        4                 * century and year
.
.
.--------------  CONSTANTS  ----------------

FIFTEEN   FORM      "15"
SP11      INIT      "           "
THIRTEEN  FORM      "13"
WR        INIT      "WR"
Z15       INIT      "zzzzzzzzzzzzzzz"
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
.
PRGID     INIT      "IBAWAT04"
PRGNAM    INIT      "Operator Error Correction"
VERSION   INIT      "V12.01.00"
.
.*****************************************************************************
.*                              ML0000                                       *
.*                             Mainline                                      *
.*****************************************************************************
ML0000    CALL      INIT0000           * initialize and open files
.
ML1000    CALL      CLRV0000           * clear variables
.
          CALL      OPTN0000           * get option
          BRANCH    EXIT,ML9999
.
ML1500    CALL      GURN0000           * get U/R Number
          BRANCH    EXIT,ML1000        * Enter pressed (No U/R entered)
.
ML2000    CALL      DSWL0000           * display relevant W/L records
          BRANCH    EXIT,ML1500
          GOTO      ML1000 
.
ML9999    CHAIN     PGM
          STOP
.*****************************************************************************
.*                              INIT0000                                     *
.*                        Initialisation section                             *
.*****************************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening "
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P58:24,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
          DISPLAY   *P58:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P58:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P58:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
          DISPLAY   *P58:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          DISPLAY   *P58:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
          DISPLAY   *P58:24,"patpraxf"
          DISPLAY   *P58:24,"wattr1af"
          OPEN      WATTR1A2,"wattr1af"
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P58:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
.
          READ      CONTROLF,TEN9;*75,CCNOTES
          READ      CONTROLF,FIFTY9;*132,CALRDESC,*231,PTCNRGNS
          DISPLAY   *P58:24,"patgsrnf"
          OPEN      PATGSRN1,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
INIT9999  RETURN
.**************************************************************************
.*                              CLRV0000                                  *
.*                          clear variables                               *
.**************************************************************************
CLRV0000  PACK      WKEY1,SP20
          PACK      WKEY2,SP20
          PACK      WKEY3,SP20
          PACK      WKEY4,SP20
          PACK      WKEY5,SP20
          PACK      WKEY6,SP20
          PACK      WKEY7,SP20
          PACK      WKEY8,SP20
          PACK      WKEY9,SP20
          PACK      WKEY10,SP20
          PACK      WKEY11,SP20
          PACK      WKEY12,SP20
          PACK      WKEY13,SP20
          PACK      WKEY14,SP20
          PACK      WKEY15,SP20
          MOVE      ZERO,SVERT
          MOVE      ZERO,EXIT
.
CLRV9999  RETURN
.**************************************************************************
.*                              OPTN0000                                  *
.*                      Get option to perform                             *
.**************************************************************************
OPTN0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:4,*EF,*V2LON,"0",*HOFF," = ","Exit":
                    *P1:5,*V2LON,"1",*HOFF," = ","Unremove Procedure"
          KEYIN     *P1:7,"Select Option : ",*V2LON,OPTION
.
          IF        OPTION < ZERO | OPTION > ONE
            GOTO      OPTN0000         * Invalid option
          ENDIF
          IF        OPTION=ZERO
            MOVE      TRUE,EXIT
          ENDIF
.        
OPTN9999  RETURN
.******************************************************************************
.*                                GURN0000                                    *
.*                              Get U/R Number                                *
.******************************************************************************
GURN0000  DISPLAY   *P1:4,*EF,"U/R Number: ";
          MOVE      FOUR,CVERT
          MOVE      THIRTEEN,CCOL
          CALL      KURN
.
          BRANCH    EXIT,GURN9999      * no U/R entered
          BRANCH    OVRCD,GURN9000     * entered U/R not found
          GOTO      GURN9999           * entered U/R found
.
GURN9000  DISPLAY   *P1:24,"U/R does not exist.  ";
          CALL      HITENTER
          GOTO      GURN0000
.         
GURN9999  RETURN
.****************************************************************************
.*                              DSWL0000                                    *
.*                  Display relevant waiting list details                   *
.*  Only those records that are Removed from the waiting list are wanted.   *
.****************************************************************************
DSWL0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COUNT         * record count
          MOVE      ONE,SCOUNT         * screen count
.
          PACK      KEY20,SIX,PURNO,SP11
          CALL      RDSTREA2           * read W/L patient procedures file
          CALL      RDKTREA2           * read in record
          BRANCH    OVRCD,DSWL8000
.
          MOVE      PURNO,KEY8
          CALL      RDMAST1            * read master file
          CALL      RDPMPX21
          CALL      PMIHEAD
          CALL      DSHD0000           * display header
          MOVE      EIGHT,SVERT        * screen vert. position
          CALL      RDPTREA2           * position before first matched record
.
DSWL1000  CALL      RDKTREA2
          BRANCH    OVRCD,DSWL9000
          MATCH     WMURNO,PURNO       * record matches U/R ? 
          GOTO      DSWL9000 IF NOT EQUAL
.
          MOVE      DWMSTAT,STATUS
          COMPARE   STATUS,SIX         * Removed from W/L status ?
          GOTO      DSWL1000 IF NOT EQUAL
.
          PACK      WATTKEY,DWMSTAT,WMURNO,WMCODE,DWMCNT
          ADD       ONE,COUNT
.
          IF        COUNT=ONE
            MOVE      WATTKEY,KEYARRAY[SCOUNT]  * store first record
            DISPLAY   *P1:8,*EF                 * of each screen
          ENDIF
.
          STORE     WATTKEY,COUNT,WKEY1,WKEY2,WKEY3,WKEY4,WKEY5,WKEY6,WKEY7:
                    WKEY8,WKEY9,WKEY10,WKEY11,WKEY12,WKEY13,WKEY14,WKEY15
.
          CALL      DSRC0000           * display record details line
          COMPARE   TWENTY3,SVERT      * check line number
          GOTO      DSWL1000 IF LESS   * get next record
.
          CALL      CHSN0000           * check screen layout
          BRANCH    EXIT,DSWL9999      * exit selected ?
          BRANCH    ITEMSEL,DSWL7000   * item selected ?
          GOTO      DSWL1000
. 
DSWL7000  CALL      REMW0000           * UNDO removed patient from W/L 
          IF        FOPTION=ONE
            MOVE      WKEY2,WKEY1      * first record on screen selected
          ENDIF
          DISPLAY   *P1:8,*EF          * start new updated display screen
          MOVE      EIGHT,SVERT
          MOVE      ZERO,COUNT
          MOVE      WKEY1,KEY20
          CALL      RDTREA2            * read first record of screen
          CALL      RDPTREA2           * position before first record
          GOTO      DSWL1000
.
DSWL8000  MOVE      ONE,EXIT
          DISPLAY   *P1:24,"No removed waiting list records exist for U/R ":
                    PURNO,". ";
          CALL      HITENTER
          GOTO      DSWL9999
.
DSWL9000  IF        COUNT=0
            GOTO      DSWL8000         * no records found with status=6
          ENDIF
          CALL      CHSN0000           * check screen layout
          BRANCH    EXIT,DSWL9999      * exit selected ?
          BRANCH    ITEMSEL,DSWL7000   * item selected ?
          GOTO      DSWL1000
.
DSWL9999  RETURN
.******************************************************************************
.*                                DSHD0000                                    *
.*                             Display Header                                 *
.******************************************************************************
DSHD0000  DISPLAY   *P1:7,*V2LON,*ULON,"Item",*P6:7,"Code    ",*P15:7:
                    "Procedure Description              ",*P51:7:
                    "Remvl Date",*P62:7,"Removal Reason      "
.
          ADD       ONE,SVERT
.
DSHD9999  RETURN
.****************************************************************************
.*                            DSRC0000                                      *
.*                      Display record details line                         *
.****************************************************************************
DSRC0000  UNPACK    WMDATE4,YR,MTH,DY
          PACK      RMDATE,DY,SLASH,MTH,SLASH,YR
          MOVE      WMDESC,DIM35       * truncate for display reasons
.
          PACK      TDESC,SP20
          PACK      KEY5,WR,WMREMOVE
          CALL      RDCODE1            * read codes table file
.
.
DSRC1000  DISPLAY   *V2LON,*P1:SVERT,COUNT,*HOFF,*P6:SVERT,WMCODE,*P15:SVERT:
                    DIM35,*P51:SVERT,RMDATE,*P62:SVERT,WMREMOVE,*P66:SVERT:
                    TDESC
.
          ADD       ONE,SVERT
.
DSRC9999  RETURN                   
.******************************************************************************
.*                                CHSN0000                                    *
.*                          Check screen layout                               *
.*  Offer option to Select Item, (P)revious Screen, (N)ext Screen, (E)xit ?   *
.******************************************************************************
CHSN0000  MOVE      FALSE,ITEMSEL      * item selected flag
          MOVE      FALSE,EXIT
          CALL      MRRC0000           * check if more records
.
          IF        SCOUNT=ONE & COUNT=FIFTEEN & MRCRDS=TRUE
            GOTO      CHSN8000         * End of first screen
          ENDIF  
.
          BRANCH    SCOUNT,CHSN9000    * Only one screen of data
.
          IF        SCOUNT>ONE & COUNT=FIFTEEN & MRCRDS=TRUE
            GOTO      CHSN7000         * End of second or greater screen
          ENDIF  
.
          IF        SCOUNT>ONE & COUNT<=15
            GOTO      CHSN6000         * Partial screen greater than one
          ENDIF
.
.
.         -- Select Item, (P)revious Screen, (E)xit ? --
. 
CHSN6000  KEYIN     *P1:24,*EL,"Select Item, (",*V2LON,"P",*HOFF:
                    ")revious Screen, (",*V2LON,"E",*HOFF,")xit ? ":
                    *V2LON,DOPTION
          REP       UPPLOW,DOPTION
.
          MATCH     "E",DOPTION
          GOTO      CHSN9900 IF EQUAL  * exit selected ?
.
          MATCH     "P",DOPTION
          GOTO      CHSN9600 IF EQUAL  * previous selected ?
.
          TYPE      DOPTION
          GOTO      CHSN6000 IF NOT EQUAL
.
          MOVE      DOPTION,FOPTION     * move option to form field
          IF        FOPTION > 0  &  FOPTION <= COUNT
            GOTO      CHSN9800         * item selected is in range
          ENDIF
          GOTO      CHSN6000 
.
. 
.   ----- Select Item, (P)revious Screen, (N)ext Screen, (E)xit ? -----
.
CHSN7000  KEYIN     *P1:24,*EL,"Select Item, (",*V2LON,"P",*HOFF:
                    ")revious Screen, (",*V2LON,"N",*HOFF,")ext Screen, (":
                    *V2LON,"E",*HOFF,")xit ? ",*V2LON,DOPTION
          REP       UPPLOW,DOPTION
.
          MATCH     "E",DOPTION
          GOTO      CHSN9900 IF EQUAL  * exit selected ?
.
          MATCH     "N",DOPTION
          GOTO      CHSN9700 IF EQUAL  * next selected ?
.
          MATCH     "P",DOPTION
          GOTO      CHSN9600 IF EQUAL  * previous selected ?
.
          TYPE      DOPTION
          GOTO      CHSN7000 IF NOT EQUAL
.
          MOVE      DOPTION,FOPTION     * move option to form field
          IF        FOPTION > 0  &  FOPTION <= COUNT
            GOTO      CHSN9800         * item selected is in range
          ENDIF
          GOTO      CHSN7000 
.
. 
.         --------- Select Item, (N)ext Screen, (E)xit ? ------------
.
CHSN8000  KEYIN     *P1:24,*EL,"Select Item, (",*V2LON,"N",*HOFF,")ext":
                    " Screen, (",*V2LON,"E",*HOFF,")xit ? ",*V2LON,DOPTION
          REP       UPPLOW,DOPTION
.
          MATCH     "E",DOPTION
          GOTO      CHSN9900 IF EQUAL  * exit selected ?
.
          MATCH     "N",DOPTION
          GOTO      CHSN9700 IF EQUAL  * next selected ?
.
          TYPE      DOPTION
          GOTO      CHSN8000 IF NOT EQUAL
.
          MOVE      DOPTION,FOPTION     * move option to form field
          IF        FOPTION > 0  &  FOPTION <= COUNT
            GOTO      CHSN9800         * item selected is in range
          ENDIF
          GOTO      CHSN8000 
.
.
.         --------- Select Item, (E)xit ? ------------
.
CHSN9000  KEYIN     *P1:24,*EL,"Select Item, (",*V2LON,"E",*HOFF,")xit ? ":
                    *V2LON,DOPTION
          REP       UPPLOW,DOPTION
.
          MATCH     "E",DOPTION
          GOTO      CHSN9900 IF EQUAL  * exit selected ?
.
          TYPE      DOPTION
          GOTO      CHSN9000 IF NOT EQUAL
.
          MOVE      DOPTION,FOPTION    * move option to form field
          IF        FOPTION > 0  &  FOPTION <= COUNT
            GOTO      CHSN9800         * item selected is in range
          ENDIF
          GOTO      CHSN9000
.
.    For Previous, the first record of the previous screen is read in,
.    then the file pointer is set prior to this. The file is then read
.    from this position.
.
CHSN9600  SUB       ONE,SCOUNT         * Previous selected
          MOVE      EIGHT,SVERT
          MOVE      ZERO,COUNT
          MOVE      KEYARRAY[SCOUNT],KEY20
          CALL      RDTREA2
          CALL      RDPTREA2
          GOTO      CHSN9999
.
CHSN9700  ADD       ONE,SCOUNT         * Next selected 
          MOVE      ZERO,COUNT 
          MOVE      EIGHT,SVERT
          GOTO      CHSN9999
.
CHSN9800  MOVE      TRUE,ITEMSEL       * valid Item selected
          CALL      POSTQST            * Post (Y/N/C) ? 
          BRANCH    CEXIT,CHSN9999,CHSN0000,CHSN9900
.
CHSN9900  MOVE      TRUE,EXIT          * Exit selected
.
CHSN9999  RETURN
.******************************************************************************
.*                                REMW0000                                    *
.*                     undo removed patient from waiting list                 *
.******************************************************************************
REMW0000  LOAD      KEY20,FOPTION,WKEY1,WKEY2,WKEY3,WKEY4,WKEY5,WKEY6:
                    WKEY7,WKEY8,WKEY9,WKEY10,WKEY11,WKEY12,WKEY13,WKEY14:
                    WKEY15
          CALL      RDTREA2          * read record to change
          CALL      SAVHIS00
          MOVE      ONE,WMSTAT       * change status to 1 (Unscheduled)
          MOVE      "05",WTWMBSCD    * booking status changed to "05"(deferred)
          MOVE      ONE,NBRFLAG
          CALL      WRTHIS00         * write new record to history file
          CALL      GENBOKNO         * generate booking number
          MOVE      SP3,WMREMOVE
          MOVE      SP8,WMDATE4 
          CALL      UPTREA2            * update record
          DISPLAY   *P1:24,*EL,"Record updated.  ";
          CALL      HITENTER
.
REMW9999  RETURN
.******************************************************************************
.*                                GETSVAR                                     *
.******************************************************************************
GETSVAR   RETURN
.
.******************************************************************************
.*                                MRRC0000                                    *
.*          Check if any more records after current display screen            *
.******************************************************************************
MRRC0000  MOVE      FALSE,MRCRDS       * more records after current screen flag
          CALL      RDKTREA2           * read in next record
          BRANCH    OVRCD,MRRC9999
.
          MATCH     WMURNO,PURNO       * record matches U/R ? 
          GOTO      DSWL99999 IF NOT EQUAL
.
          MOVE      DWMSTAT,STATUS
          COMPARE   STATUS,SIX         * Removed from W/L status ?
          GOTO      DSWL9999 IF NOT EQUAL
.
          MOVE      TRUE,MRCRDS
.
MRRC9999  CALL      RDPTREA2           * reset file pointer
          RETURN
.
OPEN0000
CLOS0000  RETURN
.******************************************************************************
.
. ------------------  I/O Section  ------------------------
.
          INC       STD001IO
.
          INC       AGECHK
          INC       BOKRC1IO/INC
          INC       CALCAGE
          INC       CLPATMAS
          INC       NZHISIIO
          INC       NZIBSRCH
          INC       OUTPREIO/INC
          INC       PATALERT
          INC       PATALRIO/INC
          INC       PATCOMN1
          INC       PATDNRIO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       PATPR1IO/INC
          INC       PATGSRIO/INC
          INC       NHIMASIO/INC
          INC       PATNIDIO/INC
          INC       PATSDNIO/INC
          INC       PATSMWIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATVISIO/INC
          INC       PATSNDX/PBL
          INC       PATSNX2/PBL
          INC       PMIHEAD/PBL
          INC       WATTR1IO/INC
          INC       WATHISIO/INC
          INC       WATNBRIO/INC
          INC       WATPROIO/INC
          INC       WEBSECIO/INC
          INC       GENBOKNO           * generate booking number
          INC       GENANVIS
          INC       WLHISSUB          

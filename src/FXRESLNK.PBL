.*******************************************************************************
.*                                                                             *
.*    Title        : FXRESLNK.PBL                                              *
.*                                                                             *
.*    Description  : Create New Yearly tables from reslnkaf.dat                *
.*                                                                             *
.*    Modification : V9.03.00 30/01/2004 Peter Vela     CAR 38290              *
.*                            Created Program                                  *
.*                                                                             *
.*******************************************************************************.
          INC       STD001FD
          INC       RESLNKFD/INC
.
CMDLINE   DIM       100
DIM4      DIM       4
DIM4A     DIM       4
RECNUM    FORM      8
NEWFILE   DIM       60
.
PRGNAM    INIT      "Create Yearly tables from reslnkaf"
PRGID     INIT      "FXRESLNK"
VERSION   INIT      "V12.01.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999
.
MAIN1000  CALL      READ0000
.
MAIN9999  CHAIN     PGM
+
.*******************************************************************************
.*                                 INIT0000                                    *
.*                 Display Heading and Initialise Record Count                 *
.*******************************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
.
INIT9999  RETURN
+
.*******************************************************************************
.*                                 READ0000                                    *
.*******************************************************************************
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          OPEN      RESLNKA1,"reslnkaf"
          PACK      KEY29,SP70
          CALL      RSRELN1
          GOTO      READ1050
.
READ1000  OPEN      RESLNKA1,"reslnkaf"
          CALL      RDRELN1
READ1050  CALL      RKRELN1
          BRANCH    OVRCD,READ9000
.
          PACK      KEY29,RELNLTY,RELNLKY,RELNRDT,RELNRTM,RELNRUN,SP70
.
          ADD       ONE,RECNUM
          IF        RECNUM%1000=0
            CLOCK     TIME,CTIMEIS
            DISPLAY   *P1:23,"Records ",RECNUM," at ",CTIMEIS
          ENDIF
.
          UNPACK    RELNRDT,DIM4,DIM4A
          CLEAR     NEWFILE
          APPEND    "reln",NEWFILE
          APPEND    DIM4,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,NEWFILE
          TRAPCLR   IO
.
          COMPARE   ZERO,OVRCD
          GOTO      READ2000 IF EQUAL
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " 110 UC1-2,3-12,13-20,21-25,26-29",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC1-2,3-12,30-30,13-20,21-25,26-29",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC13-20,21-25,26-29,1-2,3-12",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC49-64,1-2,3-12,13-20,21-25,26-29",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      RESLNKA1,NEWFILE
.
READ2000  CALL      RARELN1
          COMPARE   ONE,OVRCD
          IF        @EQUAL
            CALL      WRRELN1
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ9000  DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
          CALL      HITENTER
.
READ9999  RETURN
+
          INC       RESLNKIO/INC
          INC       STD001IO

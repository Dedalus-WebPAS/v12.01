.-------------------------------------------------------------------------------
. Program       : OPRWEB02
.
. Function      : Surgeon Information - Patient Search
.
. Modifications :
.-------------------------------------------------------------------------------
. V12.00.02 25/06/2025  Bella Turco    TSK 0961829
.           Added missing CALCAGE routine call to CMBLIS15 and CMBLIA30
. V12.00.01 30.05.2025  DA Sarkies      TSK 0955096
.            Updated for Alpha-Numeric Visit Number
. V11.04.02 04/06/2024  Sunny          TSK 0946065
.           Added MHVEXIST to check if MH Visit exists
. V11.04.01 02/05/2023  Nikitha B        TSK 0946063
.           Modified to pack WBSEDOC and then WBSEHOSP for RSOPSES2 at PATSRH10.
. V11.03.03 21/03/2023  Ebon Clements    TSK 0909393
.           Added hospital to oprsesaf(1-6) and oprdetfa(1&5) indexes
. V11.03.02 24/11/2022 J.Tan  Task 0926814
.           Fixed updating Theatre incomplete (CMBLIA00)
. V11.03.01 15/11/2022 J.Tan  Task 0926814
.           Recompiled for OPTHCMPL - checking for Abandoned case
. V11.02.04 11/08.2022  Alina Bhari    TSK 0922975
.           Added keep alive to CMBS item confirmation - CMBLIS00,CMBLIS10
.           CMBLIS18,CMBLIS30,CMBLIA65,CMBLIA10
. V11.02.03 03.03.2022  DA Sarkies     TSK 0911558
.           Added DIM6 variable to hold the contents of VALDCODE
. V11.02.02 03.03.2022  DA Sarkies     TSK 0911558
.           Added remote script to check whether a theatre is unavailable at a
.           specific time on a specific date
. V11.02.01 01/02/2022  Ebon Clements  TSK 0888071
.           Added output of clinical documents exits indicators. Admission 
.           paperwork, Medical history and consent DSPADM00 - OBSV0000 
. V11.01.02 07/01/2022  Thanh T        TSK 0011735
.           Added CMBCU000, RSTHIC00, TLST1800, TLST1900, TLST2000, CMBLIA00
.           and MBSDSC00 for MBS Confirmation View
. V11.01.01 27/04/2021  Thanh T        TSK 0895165
.           Recompiled for OPRARDFD changes
.-------------------------------------------------------------------------------
.      V11.00.06 09/12/2020  Thanh T        TSK 0872840
.                Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
.      V11.00.05 05/08/2020  Ebon Clements  Task 0870994
.                Only show discharged visits that where admitted on the
.                proposed discharge date - STAVIS00
.      V11.00.04 04/08/2020  Ebon Clements  Task 0870994
.                Show all discharges regardless of admission date STAVIS00
.      V11.00.03 26/06/2020  Ebon Clements  Task 0870994
.                Added SRCHTYPE to stistical admission visit link search 
.                STAVIS00
.      V11.00.02 17/06/2020  Jill Parkinson Task 0871656
.                Added output of adiag2 to default from admission link   
.      V11.00.01 01/06/2020  Ebon Clements  TSK 0870994
.                Added statistical admission visit link search - STAVIS00
.      V10.13.01 14/12/2018  Richa Phogat   TSK 0867588  
.                Added code under CMBLIS00 to display colums 
.                Surgeon/ Anaesthetist
.                when menu option surganae=1
.                Added SURNINDC, ANESINDC, SURGNAME, ANESNAME & SURGMENU
.      V10.11.04 11/12/2017  Don Nguyen     TSK 0849557
.                Modified CCMLST00 to output Item Number and Quantity
.      V10.11.03 23/11/2017  Don Nguyen     TSK 0846231
.                Added Report 6 (REMOTE00) - Remote Scripting
.                Added Report 5 (CCMI0000) - Cancelled CMBS & Misc Items
.      V10.11.02 16/10/2017  Thanh T.       TSK 0846191
.                Recomplied for OPTHETAT
.      V10.11.01 22/09/2017  Peter Vela     TSK 0308822
.                Added CHKVIO00 for Day Oncology
.-------------------------------------------------------------------------------
.      V10.10.01 15/05/2017  Ebon Clements  TSK 0815569
.                Added transfer source to visit search - CHKVIS00
.-------------------------------------------------------------------------------
.      V10.06.02 27/08/2015  Davin          CAR 316639
.                Added ATYPDESC and TCINDC15 to admission display row (DSPADM00)
.      V10.06.01 22/04/2015  Don Nguyen     CAR 299117
.                Modified THTC0000() to verify theatre charges complete for all
.                theatre records associated with patient visit using OPTHCMPL.
.                Write theatre audit record (OPTHETAT) for OPTHCMPL.
.                Changed output of TheatreConfirm(); passed theatre unique ID.
.                Modified CMBLIS00 to verify OPDAACPD instead of PMVXATCP.
.      V10.05.02 15/12/2014  Peter Vela     CAR 307518
.                Changed Care Type display in DSPADM00 to DIM2
.      V10.05.01 06/11/2014  Don Nguyen     CAR 304059
.                Recompiled for changes to OPRSESIO - added Default Team 
.                Completed fields.
.-------------------------------------------------------------------------------
.      V10.04.02 31.03.2014  B.G.Ackland  CAR 299363
.                Replace META Tag Redirect with Javascript in Common RDIREC00
.                Routine
.      V10.04.01 20/02/2014  Peter Vela       CAR 218689
.                Added Theatre Requests indicator to ADMLST00
.-------------------------------------------------------------------------------
.      V10.03.03 10/09/2013  Steve Armstrong  CAR 291089
.                Recompiled for changes to PMSQVIFD.
.      V10.03.02 18/07/2013  Steve Armstrong  CAR 268961
.                Recompiled for changes to PMSQVIFD.
.      V10.03.01 23/11/2011  Ebon Clements   CAR 255738
.                Show ward beds in short description order
.-------------------------------------------------------------------------------
.      V10.02.02 27/10/2011  Steve Armstrong CAR 251323
.                Recompiled for PMSQVIFD changes
.      V10.02.01 28/06/2011 Peter McMullen CAR 240725   
.                Change ward selection list to sort by name, not code 
.-------------------------------------------------------------------------------
.      V10.01.01 06/01/2011 Jill Parkinson CAR 233088   
.                Changed to use PMVXFNDM instead of AFNDMM
.-------------------------------------------------------------------------------
.      V10.00.03 08/10/2010  Mike Laming     CAR 217171
.                Corrected EndTime in CMBS Confirmation list in routine CMBLIS00
.      V10.00.02 18/08/2010 J.Tan            CAR 222031
.                Mods to set PMMIDUPD and PMMITUPD
.      V10.00.01 19/03/2010  Steve Armstrong   CAR 135505
.                Added HL7TRGID & HL7INCLD setting for all broadcast messages
.-------------------------------------------------------------------------------
.      V9.12.04  13/01/2010  Jill Parkinson CAR 212711
.                Changed CHKVIS00 to do current admissions first     
.      V9.12.03  07/12/2009  Peter McMullen CAR 210130
.                replace DOCNAM00 calls with DOCNM000                
.      V9.12.02  05/11/2009  Ebon Clements CAR 207604
.                Added call to RCVT0000 - determine time in recovery
.      V9.12.01  30/10/2009  Ebon Clements    CAR 207744
.                Added surgeny end time to CMBS item list - CMBLIS00
.      V9.11.05  08/04/2009  Peter Vela       CAR 183976
.                Added PMVXPILC to DSPADM00 
.      V9.11.04  06/03/2009  Peter Vela       CAR 183976
.                Added ASTAT to DSPADM00
.      V9.11.03  11/12/2008  Ebon Clements    CAR 174080
.                Added post op ward/bed to CHKVIS00
.      V9.11.02  07/11/2008  Steve Armstrong  CAR 183001
.                Recompiled for changes to DGCLIITM.
.      V9.11.01  31/10/2008  Davin             CAR 182311
.                Added code to get pmi details before triggering hl7 item
.                message in dgcliitm (cmbc0000 & thtc0000)
.------------------------------------------------------------
.      V9.10.02  14/08/2008  Jill Habasque     CAR 175549
.                Mods to show all preadmissions (not just those within 7 days)
.      V9.10.01  13/05/2008  Ebon Clements     CAR 161800
.                Added AMBS to DSPADM00
.------------------------------------------------------------
.      V9.09.05  27/02/2008  Steve Armstrong   CAR 160815
.                Recompiled for changes to PMSQITFD.
.      V9.09.04  07/12/2007  Steve Armstrong   CAR 155887
.                Added HL7 message trigger for items (DGCLIITM)
.      V9.09.03  24/09/2007  Jill Habasque CAR 150322
.                Added report option 4 - CMBS Confirmation details
.      V9.09.02  19/06/2007  Jill Habasque CAR 142530
.                Mods to go to oprweb0111001 in  PATSRH20
.      V9.09.01  13/06/2007  Jill Habasque CAR 142489
.                Added check for WBSEDOC to OPSECLIN in PATSRH00
.------------------------------------------------------------
.      V9.08.02  02/05/2007  Jill Habasque CAR 125135
.                Remove unplanned visit to theatre PMVXUVTH and added call to
.                PTHV0000
.      V9.08.01  30/11/2006  Peter Vela    CAR 126014
.                Added unplanned visit to theatre PMVXUVTH to DSPADM00 call to
.                UpdateParent()
.------------------------------------------------------------
.      V9.05.01  09/03/2006  Ebon Clements CAR 78533
.                Mods for PATCODTM - Hospital specific category codes
.------------------------------------------------------------
.      V9.03.04  07/06/2004 Peter Vela         CAR 49016
.                2004 Statutory Changes - recompiled for PMSPX2TM
.      V9.03.03  10/10/2003  Sandra Barcham        43422
.                Recompiled for CLBOKRX1
.      V9.03.02  01/10/2003  Jill Habasque CAR 43619
.                Fixed default of admitting point and expected ward
.      V9.03.01  01/08/2003  Jill Habasque CAR 41815
.                Added report option 3 - booking admission list
.------------------------------------------------------------
.      V9.02.08  13/03/2003  Tracey Nguyen CAR/SRF 34880
.                Call PATFLD00 to display the correct colour for the patient 
.                folder.
.                Note: A call to PATFLD00 returns KEY3 which contains the
.                      colour code for the folder.
.      V9.02.07  07/02/2003 Tonii CAR 35992
.                Recompiled for PATMISTM - inactive codes in patfundf
.      V9.02.06  09/10/2002  Peter Vela    SRF 17693
.                Recompiled for PATMISTM - Admitting Point
.      V9.02.05  28/06/2002  Ebon Clements SRF 19105
.                Recompiled for sigpipe tran
.      V9.02.04  14/05/2002  J.Tan
.                Recompiled for PATMISTM - added casemix code
.      V9.02.03  24.01.2002 Ashwin
.                Change Booking No. to Visit No
.      V9.02.02  13.12.2001 Ashwin
.                Removing the validation of web security doctor
.      V9.02.01  03.10.2001 Jill Habasque
.                Commented out replace of apostrophies with space.
.------------------------------------------------------------------------------
.                 V9.01.03  19.08.2001 HPS
.                           Modified for patient booking search.
.                 V9.01.02  14.08.2001 J.Tan
.                           Recompiled for PATMASTM
.                 V9.01.01  14.08.2001 J.Tan
.                           Recompiled for PATMASTM
.------------------------------------------------------------------------------
.                 V9.00.01  24/05/2001 Jill Habasque
.                           Mods to display booked patients for all doctors
.                           if WBSEDOC is blank
.------------------------------------------------------------------------------
.                 V5.09.02  15/01/2001 Bronko Sosic 
.                           Recompiled for PATMASTM      
.------------------------------------------------------------------------------
.                 V5.08.04  02/10/2000  Bronko Sosic 
.                           Fixed images that were not displayed from tag
.                 V5.08.03  16/08/2000  Caleb Sharman
.                           Changed Health Fund variables to be 8 chars
.                 V5.08.02  07.06.2000 B.G.Ackland
.                           Recompiled for PATMASTM      
.                 V5.08.01  25/05/2000 Pat Dirito   
.                           Recompiled for PATMASTM      
.------------------------------------------------------------------------------
.                 V5.07.04  04/04/2000 Bronko Sosic
.                           Changed bfcolor from server
.                 V5.07.03  17/03/2000 Pat Dirito      
.                           Fixed table display for Theatre Search
.                 V5.07.02  02/02/2000 Steven Watkins
.                           Recompiled for PATMASTM / PATMISTM
.                 V5.07.01  01.11.1999 B.G.Ackland                             
.                           Ported to 5.07
.                           20.12.1999 K.C.Nelson                              
.                           Recompiled for IBAWEBDF/IBAWEBCD/PATWRDTM
.------------------------------------------------------------------------------
.                 V6.06.03  28.10.1999 Pat Dirito                              
.                           Recompiled for WEBDATCD & DAYOFWEK
.                 V6.06.02  27/08/1999  Nicole Harrington
.                           Changed HTML writes to use classes
.------------------------------------------------------------
.                 V6.05.03  22.03.1999 B.G.Ackland
.                           Fix to look at booking if admission blank
.
.                 V6.05.02  18.01.1999 B.G.Ackland
.                           Fix Next Page Rep + in Ward Code
.
.                 V6.05.01  23.03.1999 B.G.Ackland
.                           New Table Format with Surgeon & Anaesthetist
.
.                 V6.04.03  23.03.1998 B.G.Ackland
.                           New Template Body Processing   
.
.------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF    
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
          INC       MEHVI1FD/INC
          INC       OBSPCOFD/INC
          INC       OPRARDFD/INC
          INC       OPRCONFD/INC
          INC       OPRDEAFD/INC
          INC       OPRDEBFD/INC
          INC       OPRDECFD/INC
          INC       OPROPRFD/INC
          INC       OPRSESFD/INC
          INC       OPRSRGFD/INC
          INC       OPRUAVFD/INC   * Operating Room - Unavailable Theatre
          INC       NHIMASFD/INC
          INC       PATCOMM/INC
          INC       PATCMCFD/INC
          INC       PATDADFD/INC
          INC       PATDO1FD/INC
          INC       PATELGFD/INC
          INC       PATHSPFD/INC
          INC       PATITMFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATDSCFD/INC
          INC       PATNIDFD/INC
          INC       PATNURFD/INC
          INC       PATONLFD/INC
          INC       PATPR1FD/INC
          INC       PATVISFD/INC
.
          INC       PMSCCDFD/INC   * Concession Card Details
          INC       PMSVX1FD/INC
          INC       PMSMTIFD/INC
          INC       PATWR1FD/INC
          INC       PATCALAG/INC
          INC       PATRGMFD/INC
          INC       MRTLOCFD/INC
          INC       OPRTSMFD/INC
.
          INC       PATCONFD/INC
          INC       PATDHEAD/INC
          INC       SCRMASFD/INC
          INC       PMSBRQFD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PMSPX2FD/INC
          INC       PMSPX2TD/INC            * PMI Details
          INC       PMSCMBFD/INC       * Cancelled CMBS and Miscellaneous Items
.
DIM1A     DIM       1
DIM11     DIM       11
DIM6      DIM       6
DFLTWARD  DIM       3
FORM8     FORM      8
HL7ITMFL  DIM       1
TABLEFMT  FORM      1
PSRHNAME  DIM       30
DOCEXIST  DIM       3
MHVEXIST  FORM      1
DOCTCODE  DIM       10
WARDCODE  DIM       3
LOCNCODE  DIM       4
SURGTYPE  DIM       3                       * Surgery Type - Day/General
.
ATYPDESC  DIM       20
CAREDESC  DIM       20
CLAIMDSC  DIM       20
CASECNT   FORM      3
CMBSSTAT  DIM       1        * CMBS Status
HTMLLNK2  DIM       127
HTMLLNK3  DIM       127
HTMLLNK4  DIM       127
LOOPCNTR  FORM      3          * Keep alive loop counter
THISCAS   FORM      3
LSTCASE   FORM      3
NODAYS    FORM      5
NEXTPAGE  DIM       1        * Nextpage parameter CGI parameter
PATFOLDR  DIM       3
UPDATETY  DIM       1        * Update Type
THEATRES  DIM       1        * Theatre Status
.
ACTION    DIM       1
ADMNDATE  DIM       11
TAGMATCH  DIM       14
MTICHECK  DIM       1 
PTFOLDER  DIM       20
PFOLDER1  INIT      "PatientFolder"
PFOLDER2  INIT      ".gif"
PMMTIKEY  DIM       14
PMMTDATE  DIM       12
PTHVFLAG  FORM      1
VALDCODE  DIM       70
VALDDATE  DIM       8
VALDFTIM  DIM       5
VALDTTIM  DIM       5
VISCHECK  DIM       1 
VISITKEY  DIM       8 
OLDEXPD   FORM      4
ONCLCLSS  INIT      "CareColour"
DISPCLSS  DIM       11
.
MDBDESCS  DIM       400
SPMICCON  DIM       1
MBITMCNT    FORM      3
.
PREAD     INIT      "Preadmission  "
ADMIT     INIT      "Current IP    "
LEAVE     INIT      "On leave      "
DISCH     INIT      "Discharged    "
.
. Details for New Booking
.-------------------------
OPEREXPD  FORM      4       * Expected Duration (min)
OPERANAE  DIM       3       * Anaesthetic Code (Cat. OA )
OPERTYPE  DIM       3       * Operation Type   (Cat. OY )
OPERUSR1  DIM       3       * User Defined 1 (Category Y6)
OPERUSR2  DIM       3       *              2 (Category Y7)
OPERUSR3  DIM       3       *              3 (Category Y8)
OPERUSR4  DIM       3       *              4 (Category Y9)
OPERPRV1  DIM       9       * Provisional item # 1 ( MBS or ICD9)
OPERPRV2  DIM       9       * Provisional item # 2 ( MBS or ICD9)
OPERPRV3  DIM       9       * Provisional item # 3 ( MBS or ICD9)
OPERDES1  DIM       55      * Operation description line 1
OPERDES2  DIM       55      *                            2
OPERDES3  DIM       55      *                            3
OPERCOMM  DIM       40      * Comments
OPERCANC  DIM       3       * Cancellation Code (Category SC)
OPERTHET  DIM       6       * Operating Room Code
OPERKNOW  DIM       3       * Known Infection (Category OK)
OPERPREF  DIM       9       * Doctors Operation Preference
.
PATTITLE  DIM       4       * Title
PATSNAME  DIM       20      * Surname
PATGNAME  DIM       25      * Given Name
PATADDR1  DIM       25      * Address Line 1
PATADDR2  DIM       25      * Address Line 2
PATADDR3  DIM       15      * Address Line 3
PATPOSTC  DIM       4       * Post Code
PATBUSPH  DIM       12      * Phone Business
PATHOMPH  DIM       12      * Phone Private
PATGENDE  DIM       1       * Gender M or F
PATMSTAT  DIM       3       * Marital Status
PATBIRDT  DIM       11      * Date of Birth DD MMM YYYY
PATCOBIR  DIM       3       * Country of Birth
PATRELIG  DIM       3       * Religon
PATRSTAT  DIM       3       * Resident Status
PATOCCUP  DIM       14      * Occupation
PATMEDIC  DIM       10      * Medicare Number
PATPENSI  DIM       15      * Pension Number
PATPENEX  DIM       4       * Pension Expiry
PATHFUND  DIM       6       * Health Fund
PATPAYOR  DIM       9       * Payor (Health Fund 6, Claim Code 3)
PATADMDT  DIM       11      * Date of Admission DD MMM YYYY
PATCLAIM  DIM       3       * Claim Code
.
SRCHTYPE  DIM       1
SURFNAME  DIM       50
STATUS    DIM       14
ANAFNAME  DIM       50
PATFNAM1  DIM       60
SURFNAM1  DIM       60
ANAFNAM1  DIM       60
SESHEAD1  DIM       80
FORMNAME  DIM       8
ELEMNAME  DIM       8
ELEMVALU  DIM       80
ENDDTIME  DIM       5
OUTVALUE  DIM       80
SESSDATE  DIM       10    Session Date
SESSFILL  FORM      3.1   Fill Percentage for Session
FILWIDTH  DIM       10
FILHIGHT  DIM       12
FILHIGH   FORM      3 
STARTIME  DIM       5
CENDTME   DIM       5
SURNINDC  FORM      1        * Surgeon Indicator
ANESINDC  FORM      1        * Anaesthetist Indicator
SURGNAME  DIM       50       * Surgeon Name
ANESNAME  DIM       50       * Anaesthetist Name
SURGMENU  DIM       1        * Surgeon Type column; 1=Show
.
SAVKEY20  DIM       20
LASTCASE  DIM       3
MVTOCASE  DIM       3
CASEKEYS  DIM       26
SESSKEYS  DIM       22
NEWSESSI  DIM       19
NMPNUMB   DIM       20
PATCANTH  DIM       3   * Reason for Theatre Cancellation
CANADMIS  DIM       1   * Cancel Admission 
PATCANAD  DIM       3   * Reason for Admission Cancellation
CODEOA    INIT      "OA"
CODEBC    INIT      "BC"
CODESC    INIT      "SC"
CODESP    INIT      "SP"
CODEST    INIT      "ST"
STYPDES   DIM       25
.
SAVANAE   DIM       3
SAVASTA   DIM       1
SAVCANC   DIM       3
SAVCASE   DIM       3
SAVCLIN   DIM       6
SAVCOMM   DIM       40
SAVDATE   DIM       8
SAVDES1   DIM       55
SAVDES2   DIM       55
SAVDES3   DIM       55
SAVEXPS   DIM       5
SAVKEY19  DIM       19
SAVKEY30  DIM       30
SAVKNOW   DIM       3
SAVPOS1   DIM       5
SAVPOS2   DIM       5
SAVPOS3   DIM       5
SAVPOS4   DIM       5
SAVPRE1   DIM       5
SAVPRE2   DIM       5
SAVPRE3   DIM       5
SAVPRE4   DIM       5
SAVPRE5   DIM       5
SAVPRV1   DIM       9
SAVPRV2   DIM       9
SAVPRV3   DIM       9
SAVSTAT   FORM      1
SAVTHET   DIM       6
SAVTIME   DIM       5
SAVTRAN   DIM       3
SAVTYPE   DIM       3
SAVTDUR   FORM      4
SAVUNIQ   DIM       10
SAVUSR1   DIM       3
SAVUSR2   DIM       3
SAVUSR3   DIM       3
SAVUSR4   DIM       3
SAVWAIT   DIM       1
DIM3      DIM       3
UPDATEBK  DIM       1
THREQIND  DIM       1
.
DESCRICM  DIM       20       * Reason Incomplete description
OPTHVISN  DIM       8        * Visit number used by OPTHCMPL proc call
OPTHRICM  DIM       3        * Reason Incomplete used by OPTHCMPL proc call
THETUNIQ  DIM       10       * Theatre Unique ID
PROSREQD  DIM       16
.
REGIME01  DIM       10       * Regime Code 1 work variable
REGIME02  DIM       10       * Regime Code 2 work variable
REGIME03  DIM       10       * Regime Code 3 work variable
.
TADTTYPE  DIM       3        * Theatre Audit type
OLDUNIQ   DIM       10       * used by OPTHETAT
.
CMRECCNT  FORM      3        * Record Counter
CMRECSRC  DIM       50       * Record Source (description)
CMRECSTA  DIM       20       * Record Status (description)
CMRECTYP  DIM       20       * Record Type; 1 = CMBS Item, 2 = Misc Item
DELRCELL  DIM       400      * HTML for Delete Record table cell
DISBLSTR  DIM       20       * Disabled string
FIELDNAM  DIM       8        * Field name
FILTSRCE  DIM       1        * Filter Source
FILTSTAT  DIM       1        * Filter Status
PATNCELL  DIM       400      * HTML for patient table cell
.
REMOTENO  FORM      2        * Remote Scripting number
UPDATKEY  DIM       14       * Update Key (KEY14)
UPDATTYP  DIM       1        * Update Type:
.                                1=Theatre, 2=Coding, 3=Billing, 4=Manual Delete
UPDATVAL  DIM       1        * Update Value:
.                                0=Clear, 1=Updated
UPDATTXT  DIM       50       * Update Text (Deletion Comments)
YESSADMP  DIM       1       * Adm Paperwork Received
YESSCONO  DIM       1       * Consent Obtained
YESSMEDH  DIM       1       * Medical History Received
.
RECTYPE1  INIT      "CMBS Item"
RECTYPE2  INIT      "Miscellaneous Item"
RECSRCE1  INIT      "Invoice Raised, manual intervention required"
RECSRCE2  INIT      "No Invoice Raised, item removed by system"
RECSTAT0  INIT      "New Entry"
RECSTAT1  INIT      "Record Deleted"
RECSTAT2  INIT      "Record Removed"
.
CATLWI    INIT      "wi"
CATGY_Rt  INIT      "Rt"    
.
.-----------------------------------------------------------------------
PRGID     INIT      "OPRWEB02"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Surgeon Information - Patient Search"
.-----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400,MAIN1500:
                             MAIN1600
.
          MOVE      "INVALID OPTION",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      PATSRH00   * Search for a Patient
          GOTO      MAIN1000
.
MAIN1200  CALL      WRDLST00   * Theatre List by Admission Ward
          GOTO      MAIN1000
.
MAIN1300  CALL      ADMLST00   * List of valid admissions for booking
          GOTO      MAIN1000
.
MAIN1400  CALL      CCON0000   * CMBS Confirmation
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1500  CALL      CCMI0000   * Cancelled CMBS & Miscellaneous Items
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1600  CALL      REMOTE00   * Remote Scripting
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      PATVISA1,"patvisaf"
          OPEN      PMSMTIA1,"pmsmtiaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PATPX1A1,"patpx1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A8,"patmi1af"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDADA1,"patdadaf"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATELGA1,"patelgaf"
          OPEN      PATITEM1,"patitemn"
          OPEN      PATRGMA1,"patrgmaf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A7,"patwr1af" 
          OPEN      PATWR1A8,"patwr1af" 
          OPEN      PATMI1A2,"patmi1af" 
          OPEN      BOKRC1A1,"bokrc1af" 
          OPEN      BOKRC1A6,"bokrc1af" 
          OPEN      BOKRX1A1,"bokrx1af" 
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATCODE1,"patcodes" 
          OPEN      PATCODE2,"patcodes" 
          OPEN      OBSPCOA1,"obspcoaf"
          OPEN      OPROPRA1,"opropraf"
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRSESA2,"oprsesaf"
          OPEN      OPRDETA1,"oprdetaf"
          OPEN      OPRDETA2,"oprdetaf"
          OPEN      OPRDETA3,"oprdetaf"
          OPEN      OPRDETC1,"oprdetcf"
          OPEN      OPRARDA1,"oprardaf"
          OPEN      OPRSRGA1,"oprsrgaf"
          OPEN      OPRTSMA1,"oprtsmaf"
          OPEN      OPRUAVA1,"opruavaf"
          OPEN      PMSCCDA1,"pmsccdaf"
.
          READ      CONTROLF,FORTY;*43,OPCNAUDA,OPCNAUDB,*46,OPCNAUDD:
                                   *61,OPCNAUDS,OPCNAUDT
          READ      CONTROLF,FORTY2;*28,OPCNFULL,*235,OPCNFTIM,*237,OPCNTTIM
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
          OPEN      MEHVI1A1,"mehvi1af"
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PMSCMBA1,"pmscmbaf"
          IF        OVRCD=0
            OPEN      PMSCMBA2,"pmscmbaf"
          ENDIF
          TRAPCLR   IO
.
INIT9999  RETURN
+
.------------------------------------------------------------
. Format Patient Name <b>SURNAME</b>Title Given Names
.------------------------------------------------------------
PATNAM00  CLEAR     DISPNAME
...       REP       "' ",PSNAME
          REP       "#" ",PSNAME
...       REP       "' ",PGNAME
          REP       "#" ",PGNAME
          MOVE      SP70,PATFNAME
          REP       UPPLOW,PSNAME
          APPEND    "<b>",DISPNAME
          APPEND    PSNAME,DISPNAME
          APPEND    "</b>",DISPNAME
          PACK      NAME35,PTITL,SP70
          CALL      NAMSTR00
          PACK      NAME35,PGNAME,SP70
          CALL      NAMSTR00
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,PATFNAME
          STRIP     PATFNAME
PATNAM99  RETURN
+
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      "000",TEMPLATE
          MOVE      SP70,PSRHNAME
          MOVE      ZERO,VIEWTYPE
          MOVE      SP70,WARDCODE
          MOVE      SP70,FRSTDATE
          MOVE      SP70,LASTDATE
          MOVE      SP70,VYEARMTH
          MOVE      SP70,UPDATEBK
          MOVE      SP70,URNUMBER
          MOVE      SP70,ADMISSNO
          MOVE      SP70,SESSKEYS
          MOVE      SP70,UPDATETY
          MOVE      SP70,NEXTPAGE
          MOVE      SP70,THEATRES
          MOVE      SP70,CMBSSTAT
          MOVE      SP70,DOCTCODE
          MOVE      SP70,SURGTYPE
          MOVE      SP70,PMMTIKEY
          MOVE      SP70,MTICHECK
          MOVE      SP70,VISCHECK
          MOVE      SP70,VISITKEY
          MOVE      SP70,THREQIND
          MOVE      SP70,THETUNIQ
          MOVE      SP70,DESCRICM
          MOVE      SP70,FILTSRCE
          MOVE      SP70,FILTSTAT
          MOVE      ZERO,REMOTENO
          MOVE      SP70,SURGMENU
          MOVE      SP70,UPDATKEY
          MOVE      SP70,UPDATTYP
          MOVE      SP70,UPDATVAL
          MOVE      SP70,UPDATTXT
          MOVE      SP70,VALDCODE
          MOVE      SP70,VALDDATE
          MOVE      SP70,VALDFTIM
          MOVE      SP70,VALDTTIM
          MOVE      SP70,SRCHTYPE
.
CLRPAR99  RETURN
+
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "wardcode=",QRYNAME
          IF        @EQUAL
            PACK      WARDCODE,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "viewtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VIEWTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "vyearmth=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VYEARMTH
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "frstdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FRSTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "lastdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LASTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "psrhname=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PSRHNAME
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updatebk=",QRYNAME
          IF        @EQUAL
            PACK      QRYDATA,QRYDATA,SP70
            MOVE      QRYDATA,UPDATEBK
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "sesskeys=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SESSKEYS
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updatety=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDATETY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "theatres=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,THEATRES
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "cmbsstat=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,CMBSSTAT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "doctcode=",QRYNAME
          IF        @EQUAL
            PACK      DOCTCODE,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "surgtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SURGTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmmtikey=",QRYNAME
          IF        @EQUAL
            PACK      PMMTIKEY,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "mticheck=",QRYNAME
          IF        @EQUAL
            PACK      MTICHECK,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "visitkey=",QRYNAME
          IF        @EQUAL
            PACK      VISITKEY,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "vischeck=",QRYNAME
          IF        @EQUAL
            PACK      VISCHECK,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "threqind=",QRYNAME
          IF        @EQUAL
            PACK      THREQIND,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "thetuniq=",QRYNAME
          IF        @EQUAL
            PACK      THETUNIQ,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtsrce=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FILTSRCE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtstat=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FILTSTAT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "remoteno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REMOTENO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updatkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDATKEY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updattyp=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDATTYP
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updatval=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDATVAL
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updattxt=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDATTXT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "surgmenu=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SURGMENU
            GOTO      SETPAR99
          ENDIF
.
.         Set code sent from template
          MATCH     "valdcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VALDCODE
            GOTO      SETPAR99
          ENDIF
.
.         Set and rearrange date sent from template
          MATCH     "valddate=",QRYNAME
          IF        @EQUAL
            MATCH     SP70,QRYDATA
            GOTO      SETPAR99 IF EQUAL
            GOTO      SETPAR99 IF EOS
.
            UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
            CALL      SETMTH00            * Set CMON from MTHNAM3
            PACK      VALDDATE,CCENT,CYEAR,CMON,CDAY
            REP       " 0",VALDDATE
            GOTO      SETPAR99
          ENDIF
.
.         Set from time
          MATCH     "valdftim=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VALDFTIM
            GOTO      SETPAR99
          ENDIF
.
.         Set too time
          MATCH     "valdttim=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VALDTTIM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchtype=",QRYNAME
          IF        @EQUAL
            PACK      SRCHTYPE,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
+
.------------------------------------------------------------
. Search for Patient on Doctors Theatre List
.------------------------------------------------------------
PATSRH00  MOVE      WBSEDOC,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,PATSRH05
          MOVE      DTITL,FMTTITLE
          MOVE      DGNAME,FMTGNAME
          MOVE      DSNAME,FMTSNAME
          CALL      DOCNM000
.
PATSRH05  MATCH     SP70,FRSTDATE
          IF        @EQUAL
            CALL      IBACLOCK
            PACK      FRSTDATE,CCC,CYY,CMM,ZERO,ONE
            REP       " 0",FRSTDATE
            PACK      LASTDATE,Z70
          ENDIF
.
          UNPACK    FRSTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          STRIP     MTHNAM
          CLEAR     WEBTITLE
          APPEND    "Theatre List Patient Search from ",WEBTITLE
          APPEND    MTHNAM,WEBTITLE
          APPEND    SP1,WEBTITLE
          APPEND    CCENT,WEBTITLE
          APPEND    CYEAR,WEBTITLE
          APPEND    "  Name of ",WEBTITLE
          APPEND    PSRHNAME,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBHED00
          BRANCH    EXIT,PATSRH99
.
          REP       UPPLOW,PSRHNAME
          STRIP     PSRHNAME

          WRITE     HTMLFILE;"<table border=1 width=#"100%#" ":
                             "cellspacing=0 cellpadding=1 bgcolor=ffffCC>":
                             "<tr><td colspan=7 class=MaintenanceHeading ":
                             "align=center>Search for Patient Surname of ":
                             PSRHNAME,"</td></tr>":
                             "<tr><td class=HeadingCell>Patient</td>":
                             "<td class=HeadingCell>Theatre Session</td>":
                             "<td class=HeadingCell align=center>Time</td>":
                             "<td class=HeadingCell align=center>Duration</td>":
                             "<td class=HeadingCell>Visit No</td>":
                             "<td class=HeadingCell>Operation</td>":
                             "<td class=HeadingCell>Surgeon</td></tr>"
          MOVE      ZERO,F3
          PACK      KEY22,WBSEDOC,WBSEHOSP,FRSTDATE,SP70
          CALL      RSOPSES2
PATSRH10  CALL      RKOPSES2
          BRANCH    OVRCD,PATSRH90
.
          MATCH     FRSTDATE,OPSEDATE
          GOTO      PATSRH10 IF LESS
.
          MATCH     OPSEDATE,LASTDATE
          GOTO      PATSRH90 IF LESS
.
          MATCH     WBSEDOC,OPSECLIN
          GOTO      PATSRH90 IF NOT EQUAL
.
          MATCH     WBSEHOSP,OPSEHOSP
          GOTO      PATSRH90 IF NOT EQUAL
.
          PACK      SESSKEYS,OPSEHOSP,OPSEDATE,OPSETIME,OPSECLIN,SP70
          PACK      KEY22,SESSKEYS,SP70
          PACK      KEY26,KEY22,SP20
          CALL      RSOPDEA1
PATSRH20  CALL      RKOPDEA1                * next read on details file
          BRANCH    OVRCD,PATSRH10          * end of session
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN
          MATCH     SESSKEYS,KEY22
          GOTO      PATSRH10 IF NOT EQUAL   * different sessio
          COMPARE   THREE,OPDASTAT
          GOTO      PATSRH20 IF EQUAL       * cancelled
.
          CALL      CASDET00    * Get Display Details for Case
.
          REP       UPPLOW,PSNAME
          MATCH     PSRHNAME,PSNAME
          GOTO      PATSRH20 IF NOT EQUAL
          PACK      CASEKEYS,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,OPDASTAT:
                    OPDACASE
          REP       " +",CASEKEYS
          CALL      PATNAM00
          MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      OPSECLIN,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
            MOVE      DOCFNAME,SURFNAME
          ELSE
            MOVE      "&nbsp;",SURFNAME
          ENDIF
.
          CALL      PATFLD00          * Returns KEY3 for folder colour code
          PACK      PTFOLDER,PFOLDER1,KEY3,PFOLDER2,SP20
.
          UNPACK    OPSEDATE,CCENT,CYEAR,CMON,CDAY
          CALL      DAYOFWEK
          LOAD      DAYNAM,WEEKDAY,MON,TUE,WED,THU,FRI,SAT,SUN
          MOVE      CMON,F2
          LOAD      MTHNAM,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          MATCH     SP70,UPDATEBK    
          IF        !@EQUAL
            MOVE      "005",KEY3
          ENDIF
.
          WRITE     HTMLFILE;"<TR valign=top>":
                             "<td nowrap>":
                             "<A HREF=oprweb01.pbl?reportno=11":
                             "&template=001":
                             "&casekeys=",CASEKEYS,">":
                             "<IMG SRC=../images/",PTFOLDER," border=0 ":
                             "align=absmiddle></a>&nbsp":
                             "  ",PATFNAME,"</TD>":
                             "<TD>":
                             DAYNAM,SP1,CDAY,SP1,MTHNAM,SP1,CCENT,CYEAR,"</TD>":
                             "<TD ALIGN=CENTER>",OPDAEXPS,"</TD>":
                             "<TD ALIGN=CENTER>",OPDAEXPD," min</TD>":
                             "<TD>",OPDAADMN,"</TD>":
                             "<TD>",OPDADES1," ",OPDADES2:
                             " ",OPDADES3,"&nbsp</TD>":
                             "<TD>",SURFNAME,"</TD></TR>"
          GOTO      PATSRH20
.
PATSRH90  WRITE     HTMLFILE;"</TABLE>"
          CALL      WEBEND00
          GOTO      PATSRH99
.
PATSRH95  MOVE      "INVALID SECURITY",WEBTITLE
          CALL      WEBERR00
.
PATSRH99  RETURN
.------------------------------------------------------------
. Get Case Details
.------------------------------------------------------------
CASDET00  MOVE      OPDAADMN,KEY8
          CALL      GURN0000
          BRANCH    EXIT,CASDET99
.
          MATCH     ZEROUR,AURNO
          GOTO      CASDET10 IF NOT EQUAL   * have a UR number
.
.         on pre-admission file
.
          MOVE      OPDAADMN,KEY8           * visit number
          CALL      RDPRAM1
          BRANCH    OVRCD,CASDET10          * invalid visit number
.
          MOVE      ZERO,PURNO
          GOTO      CASDET99                * valid patient
.
.         on master file
.
CASDET10  MOVE      AURNO,KEY8     
          CALL      RDMAST1
          BRANCH    OVRCD,CASDET99          * invalid UR number
          CALL      RDPMPX21
.
CASDET99  RETURN
.******************************************************************************
.                   GURN0000
.    Given the admission number in KEY8/KEY6 this gets the UR number from
.    either the admission or booking master file.
.    Returns : AURNO
.    Routine by Bill Dew 14/03/90
.*******************************************************************************
GURN0000  CALL      RDMISS1                 * read admission file
          BRANCH    OVRCD,GURN5555
.         GOTO      GURN9000
.
GURN5555  CALL      RDBKREC1                * read booking records file
          BRANCH    OVRCD,GURN9990
          MOVE      BKREURNO,AURNO
.
GURN9000  MOVE      ZERO,EXIT               * a valid admission number
          GOTO      GURN9999
.
GURN9990  MOVE      ONE,EXIT                * an invalid admission number
.
GURN9999  RETURN
.------------------------------------------------------------
. Process Templated Fields
.------------------------------------------------------------
PROFLD00  BRANCH      REPORTNO,PROFLD99,PROFLD20,PROFLD30,PROFLD40,PROFLD50
          GOTO        PROFLD99
.
. Ward Theatre List
.
PROFLD20  BRANCH  FLDSETNO,PROFLD21,PROFLD22
.
PROFLD21  CALL    WLST0000
          GOTO    PROFLD99
.
PROFLD22  CALL    WRDF0000
          GOTO    PROFLD99
.
PROFLD30  BRANCH  FLDSETNO,PROFLD31
.
PROFLD31  CALL    VISL0000
          GOTO    PROFLD99
.
PROFLD40  BRANCH  FLDSETNO,PROFLD41
.
PROFLD41  CALL    TLST0000
          GOTO    PROFLD99
.
PROFLD50  BRANCH  FLDSETNO,PROFLD51
.
PROFLD51  CALL    CMFP0000
          GOTO    PROFLD99
.
PROFLD99  RETURN
.------------------------------------------------------------
. Ward Theatre List
.------------------------------------------------------------
WRDLST00  MATCH     SP70,WARDCODE
          IF        @EQUAL
            MOVE      WBSEWRD,WARDCODE
          ENDIF
          PACK      KEY6,WARDCODE,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,WRDLST90
.
          CALL      IBACLOCK
.
          CALL      CURPER00
          CALL      CALCDT00   * Calculate Next and Last Period Dates
.
          CLEAR     WEBTITLE
          APPEND    WBDESC,WEBTITLE
          APPEND    "(Theatre List)",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBHED00
          BRANCH    EXIT,WRDLST99
.
          CALL      WEBBOD00
.
          CALL      WEBEND00
.
          GOTO      WRDLST99
.
WRDLST90  MOVE      "INVALID WARDCODE SPECIFIED",WEBTITLE
          CALL      WEBERR00
.
WRDLST99  RETURN
.------------------------------------------------------------
. Admission List    
.------------------------------------------------------------
ADMLST00  MOVE      "Booking Admission List",WEBTITLE
.
          MATCH     "1",THREQIND
          IF        !@EQUAL
            PACK      KEY22,SESSKEYS,SP70
            CALL      RDOPSES1
            BRANCH    OVRCD,ADMLST90
          ENDIF
.
          CALL      WEBHED00
          BRANCH    EXIT,ADMLST99
.
          CALL      WEBBOD00
          CALL      WEBEND00
          GOTO      ADMLST99
.
ADMLST90  MOVE      "Invalid session selected.",WEBTITLE
          CALL      WEBERR00
          GOTO      ADMLST99
.
ADMLST99  RETURN
.------------------------------------------------------------
. CMBS Confirmations
.------------------------------------------------------------
CCON0000  MOVE      ZERO,EXIT
          RESET     UPDATETY
          MATCH     SP70,UPDATETY
          GOTO      CCON8000 IF EQUAL
.
          MATCH     "C",UPDATETY            * CMBS Confirmation
          GOTO      CCON1000 IF EQUAL
.
          MATCH     "U",UPDATETY            * CMBS Confirmation - Unqiue Id
          GOTO      CCON2000 IF EQUAL
.
          MATCH     "T",UPDATETY            * Theatre Confirmation
          GOTO      CCON3000 IF EQUAL
.
          GOTO      CCON9000                
.
CCON1000  CALL      CMBC0000                * Confirm CMBS
          MOVE      ONE,EXIT
          GOTO      CCON9999
.
CCON2000  CALL      CMBCU000                * Confirm CMBS - Unqilue Id
          MOVE      ONE,EXIT
          GOTO      CCON9999
.
CCON3000  CALL      THTC0000                * Theatre Confirmation
          MOVE      ONE,EXIT
          GOTO      CCON9999
.
CCON8000  CALL      CURPER00
          CALL      CALCDT00
          MATCH     SP70,URNUMBER
          IF        !@EQUAL
            PACK      KEY8,URNUMBER,SP70
            CALL      RDMAST1
            BRANCH    OVRCD,CCON9100
            CALL      RDPMPX21
            BRANCH    OVRCD,CCON9100
          ENDIF

          MATCH     "2",SURGMENU
          IF        @EQUAL
            MOVE      "MBS Confirmation Details",WEBTITLE
          ELSE
            MOVE      "CMBS Confirmation Details",WEBTITLE
          ENDIF
          CALL      WEBHED00
          BRANCH    EXIT,CCON9999
.
          CALL      WEBBOD00
          CALL      WEBEND00
          MOVE      ONE,EXIT
          GOTO      CCON9999
.
CCON9000  MOVE      "Invalid update type.",WEBTITLE
          CALL      WEBERR00
          GOTO      CCON9999
.
CCON9100  MOVE      "Invalid UR number",WEBTITLE
          CALL      WEBERR00
          GOTO      CCON9999
.
CCON9999  RETURN
+
.------------------------------------------------------------
. CMBS Confirmation
.------------------------------------------------------------
CMBC0000  CALL      XMLHED00
          BRANCH    EXIT,CMBC9999
.
          PACK      KEY14,PMMTIKEY,SP70
          CALL      RDPMMTI1
          BRANCH    OVRCD,CMBC9000
.
          PACK      PMMIDUPD,CCC,CYY,CMM,CDD
          REP       " 0",PMMIDUPD
          CALL      IBACLOCK
          MOVE      CTIMEIS,PMMITUPD
          MOVE      WBSEUID,PMMIWUSR
          PACK      PMMICCON,MTICHECK,SP70
          CALL      UPPMMTI1
.
.         Send an appropriate HL7 message depending on whether the item
.         confirmation field was ticked or unticked.
.>>>>
          PACK      KEY8,PMMIVISN                * get admn details & aurno
          CALL      RDMISS1
          BRANCH    OVRCD,CMBC8000

          PACK      KEY8,AURNO
          CALL      RDMAST1                      * get PMI details
          BRANCH    OVRCD,CMBC8000
.
          MOVE      PURNO,KEY8                 
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.>>>>
          CALL      PMIGTNID                * get national id for HL7 write
          MOVE      NMPNUMB,PTNINMPI
          MATCH     "1",MTICHECK
          IF        @EQUAL
            MOVE      ANSA,HL7ITMFL         * ticked item
          ELSE
            MOVE      ANSD,HL7ITMFL         * unticked item
          ENDIF
          MOVE      ONE,HL7TRGID
          MOVE      SP8,HL7INCLD
          PROC      DGCLIITM                * send HL7 message
.
CMBC8000  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             "Update Complete":
                             "</RETURN_VALUE>"
          GOTO      CMBC9500
.
CMBC9000  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Record":
                             "</RETURN_VALUE>"
.
CMBC9500  CALL      XMLEND00
.
CMBC9999  RETURN
.
.------------------------------------------------------------
. CMBS Confirmation - Unique Id
.------------------------------------------------------------
CMBCU000  CALL      XMLHED00
          BRANCH    EXIT,CMBCU999
.
          PACK      KEY14,VISITKEY,SP70
          CALL      RSPMMTI1
CMBCU200  CALL      RKPMMTI1
          BRANCH    OVRCD,CMBCU500
.
          MATCH     VISITKEY,PMMIVISN
          GOTO      CMBCU500 IF NOT EQUAL
.
          MATCH     SP70,THETUNIQ
          IF        !@EQUAL
            MATCH     THETUNIQ,PMMIUNIQ
            GOTO      CMBCU200 IF NOT EQUAL
          ENDIF
.
          MATCH     "2",PMMIRTYP
          GOTO      CMBCU200 IF NOT EQUAL
.
          MATCH     SP70,CMBSSTAT
          GOTO      CMBCU300 IF EQUAL
.
          MATCH     "1",CMBSSTAT
          IF        @EQUAL
            MATCH     "1",PMMICCON
            GOTO      CMBCU200 IF NOT EQUAL
          ENDIF
.
          MATCH     "2",CMBSSTAT
          IF        @EQUAL
            MATCH     "1",PMMICCON
            GOTO      CMBCU200 IF EQUAL
          ENDIF
.
CMBCU300  PACK      PMMIDUPD,CCC,CYY,CMM,CDD
          REP       " 0",PMMIDUPD
          CALL      IBACLOCK
          MOVE      CTIMEIS,PMMITUPD
          MOVE      WBSEUID,PMMIWUSR
          PACK      PMMICCON,MTICHECK,SP70
          CALL      UPPMMTI1
.
          GOTO      CMBCU200
.
.         Send an appropriate HL7 message depending on whether the item
.         confirmation field was ticked or unticked.
.>>>>
.
CMBCU500  PACK      KEY8,VISITKEY                * get admn details & aurno
          CALL      RDMISS1
          BRANCH    OVRCD,CMBCU800

          PACK      KEY8,AURNO
          CALL      RDMAST1                      * get PMI details
          BRANCH    OVRCD,CMBCU800
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.>>>>
          CALL      PMIGTNID                * get national id for HL7 write
          MOVE      NMPNUMB,PTNINMPI
          MATCH     "1",MTICHECK
          IF        @EQUAL
            MOVE      ANSA,HL7ITMFL         * ticked item
          ELSE
            MOVE      ANSD,HL7ITMFL         * unticked item
          ENDIF
          MOVE      ONE,HL7TRGID
          MOVE      SP8,HL7INCLD
          PROC      DGCLIITM                * send HL7 message
.
CMBCU800  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             "Update Complete":
                             "</RETURN_VALUE>"
          GOTO      CMBCU950
.
CMBCU950  CALL      XMLEND00
.
CMBCU999  RETURN
.
.------------------------------------------------------------
. Theatre Complete Confirmation
.------------------------------------------------------------
THTC0000  CALL      XMLHED00
          BRANCH    EXIT,THTC9999
.
          PACK      KEY8,VISITKEY,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,THTC9000
.
          PACK      PMVXATCP,VISCHECK,SP70
          CALL      UPPMVX11
.
          MATCH     SP70,THETUNIQ           * No theatre record ID passed in?
          GOTO      THTC0500 IF EQUAL       * Skip theatre record update
.
          PACK      KEY10,THETUNIQ,SP70
          CALL      RDOPDEA3                * Get the theatre record
          IF        OVRCD=0
            MOVE      VISCHECK,OPDAACPD
            CALL      UPOPDEA3              * Update charges complete flag
          ENDIF
.
THTC0500  MOVE      VISITKEY,OPTHVISN
          PROC      OPTHCMPL                * Validate theatre charges complete
          IF        EXIT=1
            MOVE      ONE,PMVXATCP          * Update charges posted for visit
            MOVE      SP70,PMVXTICM         * Clear reason incomplete
            MOVE      "018",TADTTYPE        * Theatre complete audit record
          ELSE
            MOVE      ZERO,PMVXATCP         * Theatre charges incomplete
            MOVE      OPTHRICM,PMVXTICM     * Update reason
            MOVE      "019",TADTTYPE        * Theatre incomplete audit record
          ENDIF
.
          CALL      UPPMVX11                * Update theatre complete flag
                                            * for the patient visit based on
                                            * charges complete flag of all other
                                            * theatre records
.
          CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
.
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
          PROC      OPTHETAT                * write audit
.
          MATCH     "1",VISCHECK            * Theatre not confirmed, exit
          GOTO      THTC8000 IF NOT EQUAL
.
          PACK      KEY8,VISITKEY           * Get admn details & aurno
          CALL      RDMISS1
          BRANCH    OVRCD,THTC8000

          PACK      KEY8,AURNO
          CALL      RDMAST1                 * get PMI details
          BRANCH    OVRCD,THTC8000
.
          MOVE      PURNO,KEY8  
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
. **** if theatre is confirmed, all existing mbs item will be confirmed ***
.
          PACK      KEY14,VISITKEY,SP70
          CALL      RSPMMTI1
THTC1000  CALL      RKPMMTI1
          BRANCH    OVRCD,THTC8000
.
          MATCH     VISITKEY,PMMIVISN
          GOTO      THTC8000 IF NOT EQUAL
.
          MATCH     "1",PMMICCON
          GOTO      THTC1000 IF EQUAL
.
          PACK      PMMIDUPD,CCC,CYY,CMM,CDD
          REP       " 0",PMMIDUPD
          CALL      IBACLOCK
          MOVE      CTIMEIS,PMMITUPD
          MOVE      WBSEUID,PMMIWUSR
          MOVE      "1",PMMICCON
          CALL      UPPMMTI1
.
.         This item is being confirmed, so send an appropriate HL7 message.
.
          CALL      PMIGTNID                * get national id for HL7 write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      ANSA,HL7ITMFL           * set flag for add record
          MOVE      TWO,HL7TRGID
          MOVE      SP8,HL7INCLD
          PROC      DGCLIITM                * send HL7 message
.
          GOTO      THTC1000
.
THTC8000  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             "Update Complete":
                             "</RETURN_VALUE>"
          GOTO      THTC9500
.
THTC9000  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Record":
                             "</RETURN_VALUE>"
THTC9500  CALL      XMLEND00
THTC9999  RETURN
+
.------------------------------------------------------------
. Update Reason Theatre Incomplete
.------------------------------------------------------------
RSTHIC00  CALL      XMLHED00
          BRANCH    EXIT,RSTHIC99
.
RSTHIC20  PACK      KEY10,THETUNIQ,SP70
          CALL      RDOPDEA3                * Get the theatre record
          BRANCH    OVRCD,RSTHIC90
.
          MOVE      UPDATTXT,OPDARICM
.
          CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
.
          CALL      UPOPDEA3              
.
.         Update Reason incomplete for patient visit extension record
.
          PACK      KEY8,OPDAADMN,SP70
          CALL      RDPMVX11
          IF        OVRCD=0
            MATCH     "1",OPDAACPD               * all charges completed
            IF        !@EQUAL
              PACK      PMVXTICM,OPDARICM,SP70   * update with new reason code
              CALL      UPPMVX11
            ENDIF
          ENDIF
.
RSTHIC80  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             "Update Complete":
                             "</RETURN_VALUE>"
          GOTO      RSTHIC95
.
RSTHIC90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Record Key: ",THETUNIQ:
                             "</RETURN_VALUE>"
.
RSTHIC95  CALL      XMLEND00
.
RSTHIC99  RETURN
+
.******************************************************************************
.           Output Next Page Based on CGI Parameter nextpage                  *
.******************************************************************************
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40,NXTPAG50:
                       NXTPAG60,NXTPAG70
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00      * Redirect Parent Content Page
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      PREDIR00      * Redirect Content Page
          GOTO      NXTPAG99
.
NXTPAG50  CALL      TOPDIR00      * Top Content Redirect Content Page
          GOTO      NXTPAG99
.
NXTPAG60  CALL      RELOAD00
          GOTO      NXTPAG99
.
NXTPAG70  CALL      WINEXT00
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.******************************************************************************
.            Redirect Content URL                                             *
.******************************************************************************
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
.                             " alert(#"",WEBTITLE,"#");":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
.
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
.------------------------------------------------------------
. Redirect Content URL
.------------------------------------------------------------
TOPDIR00  CALL      OPENHTML
          BRANCH    EXIT,TOPDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " top.content.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      TOPDIR99
.
TOPDIR90  DISPLAY   "*** Fifo Not Found ***"
.
TOPDIR99  RETURN
.******************************************************************************
.               Goes back 1 page                                              *
.******************************************************************************
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      OPENHTML
GOBACK99  RETURN
.*******************************************************************************
.                         Goes back 1 page
.*******************************************************************************
RELOAD00  CALL      OPENHTML
          BRANCH    EXIT,RELOAD99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
.                            "    alert(#"",WEBTITLE,"#");":
                             "    history.go(-1);":
.                            "    location.reload();":
                             "}":
                             "</script>"
          CALL      CLOSHTML
RELOAD99  RETURN
.------------------------------------------------------------
. Ward Admissions
.------------------------------------------------------------
VISL0000  BRANCH    FLDITMNO,VISL0100,VISL0200,VISL0300,VISL0400,VISL0500:
                             VISL0600
          GOTO      VISL9999
.
VISL0100  CALL      NEXT0000
          GOTO      VISL9999
.
VISL0200  CALL      PREV0000
          GOTO      VISL9999
.
VISL0300  CALL      CHKVIS00
          GOTO      VISL9999
.
VISL0400  CALL      CHKVIO00
          GOTO      VISL9999
.
VISL0500  CALL      STAVIS00
          GOTO      VISL9999
.
VISL0600  MATCH     VALDDATE,SP70
          GOTO      VISL9999 IF EQUAL
.
          UNPACK    VALDDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      VISL9999
.
VISL9999  RETURN
.------------------------------------------------------------
. Ward Admissions
.------------------------------------------------------------
WLST0000  BRANCH    FLDITMNO,WLST0100,WLST0200,WLST0300,WLST0400,WLST0500:
                             WLST0600,WLST0700,WLST0800,WLST0900,WLST1000:
                             WLST1100,WLST1200,WLST1300
          GOTO      WLST9999
.
WLST0100  CALL      NEXT0000
          GOTO      WLST9999
.
WLST0200  CALL      PREV0000
          GOTO      WLST9999
.
WLST0300  CALL      CURSTD00
          GOTO      WLST9999
.
WLST0400  CALL      CUREND00
          GOTO      WLST9999
.
WLST0500  CALL      CURYR000
          GOTO      WLST9999
.
WLST0600  CALL      CURMON00
          GOTO      WLST9999
.
WLST0700  CALL      SELV0000
          GOTO      WLST9999
.
WLST0800  MOVE      ONE,TABLEFMT
          CALL      WTABA000        * Table of Patient due in Theatre
          GOTO      WLST9999
.
WLST0900  WRITE     HTMLFILE;TEMPLATE;
          GOTO      WLST9999
.
WLST1000  WRITE     HTMLFILE;REPORTNO;
          GOTO      WLST9999
.
WLST1100  WRITE     HTMLFILE;VIEWTYPE;
          GOTO      WLST9999
.
WLST1200  CALL      WSEL0000        * Ward Select Options
          GOTO      WLST9999
.
WLST1300  MOVE      TWO,TABLEFMT
          CALL      WTABA000        * Table of Patient due in Theatre
          GOTO      WLST9999
.
WLST9999  RETURN
.------------------------------------------------------------
. Theatre Confirmation fields
.------------------------------------------------------------
TLST0000  BRANCH    FLDITMNO,TLST0100,TLST0200,TLST0300,TLST0400,TLST0500:
                             TLST0600,TLST0700,TLST0800,TLST0900,TLST1000:
                             TLST1100,TLST1200,TLST1300,TLST1400,TLST1500:
                             TLST1600,TLST1700,TLST1800,TLST1900,TLST2000
          GOTO      TLST9999
.
TLST0100  CALL      CMBLIS00
          GOTO      TLST9999
.
TLST0200  WRITE     HTMLFILE;VIEWTYPE;
          GOTO      TLST9999
.
TLST0300  CALL      PREV0000
          GOTO      TLST9999
.
TLST0400  CALL      NEXT0000
          GOTO      TLST9999
.
TLST0500  CALL      SELV0000
          GOTO      TLST9999
.
TLST0600  CALL      CURSTD00
          GOTO      TLST9999
.
TLST0700  CALL      CUREND00
          GOTO      TLST9999
.
TLST0800  CALL      CURYR000
          GOTO      TLST9999
.
TLST0900  CALL      CURMON00
          GOTO      TLST9999
.
TLST1000  WRITE     HTMLFILE;THEATRES;
          GOTO      TLST9999
.
TLST1100  WRITE     HTMLFILE;CMBSSTAT;
          GOTO      TLST9999
.
TLST1200  CALL      DOCDET00
          GOTO      TLST9999
.
TLST1300  CALL      MTIL0000       * list of items on pmsmtiaf
          GOTO      TLST9999
.
TLST1400  WRITE     HTMLFILE;ADMISSNO;
          GOTO      TLST9999
.
TLST1500  WRITE     HTMLFILE;URNUMBER;
          GOTO      TLST9999
.
TLST1600  CALL      PATNAM00
          WRITE     HTMLFILE;PATFNAME;
          GOTO      TLST9999
.
TLST1700  WRITE     HTMLFILE;SURGMENU;
          GOTO      TLST9999
.
TLST1800  MOVE      "YD",CATEGORY
          MOVE      SURGTYPE,CODE
          CALL      CODITM00
          GOTO      TLST9999
.
TLST1900  WRITE     HTMLFILE;THETUNIQ;
          GOTO      TLST9999
.
TLST2000  PACK      KEY10,THETUNIQ,SP70
          CALL      RDOPDEA3                * Get the theatre record
          IF        OVRCD=0
            MOVE      "Rt",CATEGORY
            MOVE      OPDARICM,CODE
            CALL      CODITM00
          ENDIF 
          GOTO      TLST9999
.
TLST9999  RETURN
+
.------------------------------------------------------------
. Displays Items from pmsmtiaf
.------------------------------------------------------------
MTIL0000  MATCH     SP70,ADMISSNO
          GOTO      MTIL9999 IF EQUAL
.
          PACK      KEY14,ADMISSNO,SP70
          CALL      RSPMMTI1
MTIL1000  CALL      RKPMMTI1
          BRANCH    OVRCD,MTIL9999
.
          MATCH     ADMISSNO,PMMIVISN
          GOTO      MTIL9999 IF NOT EQUAL
.
          MATCH     "2",PMMIRTYP
          GOTO      MTIL1500 IF EQUAL
.
          MATCH     "3",PMMIRTYP
          GOTO      MTIL1000 IF NOT EQUAL
.
MTIL1500  UNPACK    PMMITDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      PMMTDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          WRITE     HTMLFILE;"<tr><td>":
                             PMMTDATE,"</td><td>":
                             "&nbsp",PMMIITEM,"</td><td>":
                             "&nbsp",PMMIDESC,"</td></tr>"
          GOTO      MTIL1000
.
MTIL9999  RETURN
+
.------------------------------------------------------------
. Displays Theatre Bookings for a Ward
.------------------------------------------------------------
WTABA000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      WHEDA000
.
          UNPACK    FRSTDATE,KEY8
          PACK      KEY26,WBSEHOSP,FRSTDATE,SP70
          CALL      RSOPDEA1
WTABA100  CALL      RKOPDEA1
          BRANCH    OVRCD,WTABA9000
.
          MATCH     OPDAHOSP,WBSEHOSP
          GOTO      WTABA900 IF NOT EQUAL
.

          MATCH     OPDADATE,LASTDATE
          GOTO      WTABA900 IF LESS
.
          MOVE      OPDAADMN,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,WTABA200
          MATCH     SP70,AWARD
          GOTO      WTABA200 IF EQUAL
          MATCH     WARDCODE,AWARD
          GOTO      WTABA300 IF EQUAL
          GOTO      WTABA100
.
WTABA200  MOVE      OPDAADMN,KEY8
          CALL      RDBKREC1                * read booking records file
          BRANCH    OVRCD,WTABA100
          MATCH     WARDCODE,BKREWARD
          GOTO      WTABA100 IF NOT EQUAL
.
WTABA300  CALL      WROWA000
.
          GOTO      WTABA100
WTABA900
WTABA999  RETURN
.------------------------------------------------------------
.  Output Heading of Theatre Cases by Ward
.------------------------------------------------------------
WHEDA000  BRANCH    TABLEFMT,WHEDA100,WHEDA200
WHEDA100  WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell>Patient</TD>":
                             "<TD class=HeadingCell>Theatre</TD>":
                             "<TD class=HeadingCell ALIGN=CENTER>Time</TD>":
                             "<TD class=HeadingCell ALIGN=CENTER>Duration</TD>":
                             "<TD class=HeadingCell>Surgeon<br>":
                             "Anaesthetist</TD>":
                             "<TD class=HeadingCell>Operation</TD></TR>"
          GOTO       WHEDA999
WHEDA200
WHEDA999  RETURN
.------------------------------------------------------------
.  Output Row of Theatre Cases by Ward
.------------------------------------------------------------
WROWA000  CALL      CASDET00    * Get Display Details for Case
          PACK      CASEKEYS,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,OPDASTAT:
                    OPDACASE
          REP       " +",CASEKEYS
          CALL      SESDET00
          BRANCH    TABLEFMT,WROWA100,WROWA200
.
WROWA100  CALL      PATNAM00
          WRITE     HTMLFILE;"<TR valign=top>":
                             "<td nowrap>":
                             "<A HREF=",LINKPRG,".pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&casekeys=",CASEKEYS,">":
                             "<IMG SRC=../images/patdet.gif border=0 ":
                             "align=absmiddle></a>&nbsp":
                             "  ",PATFNAME,"</TD>":
                             "<TD>",OPRMDESC,"</TD>":
                             "<TD ALIGN=CENTER>",OPDAEXPS,"</TD>":
                             "<TD ALIGN=CENTER>",OPDAEXPD," min</TD>":
                             "<TD>",SURFNAME,"<br>",ANAFNAME,"</TD>":
                             "<TD>",OPDADES1," ",OPDADES2:
                             " ",OPDADES3,"&nbsp</TD><TR>"
          GOTO      WROWA999
.
WROWA200  PACK      PATFNAM1,PSNAME,COMMA,PGNAME,COMMA,PTITL
          REP       "#" ",PATFNAM1
          REP       "#" ",OPDADES1
          REP       "#" ",OPDADES2
          REP       "#" ",OPDADES3
          WRITE     HTMLFILE;"t.addRow(#"",LINKPRG,".pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&casekeys=",CASEKEYS,"#",#"":
                             PATFNAM1,"#",#"":
                             OPRMDESC,"#",#"":
                             OPDAEXPS,"#",#"":
                             OPDAEXPD,"#",#"":
                             SURFNAM1,"#",#"":
                             ANAFNAM1,"#",#"":
                             OPDADES1," ",OPDADES2," ",OPDADES3,"#");"
WROWA999  RETURN
.------------------------------------------------------------
. Check for Admissions
.------------------------------------------------------------
CHKVIS00  MOVE      ZERO,NODAYS
          MATCH     SP70,URNUMBER
          GOTO      CHKVIS99 IF EQUAL
.
. --- check for current admissions before session date ---
.
CHKVIS10  PACK      KEY17,URNUMBER,TWO,SP70
          CALL      RSPTMI18
          CALL      RKPTMI18
          BRANCH    OVRCD,CHKVIS15
.
          MATCH     URNUMBER,AURNO
          GOTO      CHKVIS15 IF NOT EQUAL
.
          COMPARE   TWO,ASTAT
          GOTO      CHKVIS15 IF NOT EQUAL
.
          CALL      DSPADM00
.
.
. --- check for preadmissions  ---
.
          PACK      KEY17,URNUMBER,ONE,Z70
          CALL      RSPTMI18
CHKVIS15  CALL      RPPTMI18
          BRANCH    OVRCD,CHKVIS20
.
          MATCH     URNUMBER,AURNO
          GOTO      CHKVIS20 IF NOT EQUAL
.
          COMPARE   ONE,ASTAT
          GOTO      CHKVIS20 IF NOT EQUAL
.
.....     MATCH     ADATE,OPSEDATE
.....     GOTO      CHKVIS20 IF LESS
.
.....     DAYSEP    ADATE,OPSEDATE,NODAYS
.....     COMPARE   EIGHT,NODAYS
.....     GOTO      CHKVIS20 IF NOT LESS
.
          CALL      DSPADM00
          GOTO      CHKVIS15
.
. --- check for on leave admissions before session date ---
.
CHKVIS20  PACK      KEY17,URNUMBER,FOUR,SP70
          CALL      RSPTMI18
          CALL      RKPTMI18
          BRANCH    OVRCD,CHKVIS25
.
          MATCH     URNUMBER,AURNO
          GOTO      CHKVIS25 IF NOT EQUAL
.
          COMPARE   FOUR,ASTAT
          GOTO      CHKVIS25 IF NOT EQUAL
.
          CALL      DSPADM00
.
. --- check for discharged admissions within session date ---
.
CHKVIS25  PACK      KEY17,URNUMBER,THREE,Z70
          CALL      RSPTMI18
CHKVIS30  CALL      RPPTMI18
          BRANCH    OVRCD,CHKVIS99
.
          MATCH     URNUMBER,AURNO
          GOTO      CHKVIS99 IF NOT EQUAL
.
          COMPARE   THREE,ASTAT
          GOTO      CHKVIS99 IF NOT EQUAL
.
          MATCH     ADATE,OPSEDATE
          GOTO      CHKVIS30 IF LESS
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDDSCH1
          BRANCH    OVRCD,CHKVIS30
.
          MATCH     OPSEDATE,DDATE
          GOTO      CHKVIS99 IF LESS
.
          CALL      DSPADM00
.
CHKVIS99  RETURN
+
.------------------------------------------------------------
. Display admission row  
.------------------------------------------------------------
DSPADM00  LOAD      STATUS,ASTAT,PREAD,ADMIT,DISCH,LEAVE
.
          IF        ASTAT=1
            MOVE      PTMIXWRD,AWARD
            MOVE      PTMIEBED,ABED
          ENDIF
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDBKREC1
          IF        OVRCD=1
            CALL      CLBOK000
          ENDIF
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDBKRX11
          IF        OVRCD=1
            CALL      CLBOKRX1
          ENDIF
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDPTDAD1
          IF        OVRCD=1
            UNPACK    SP70,PTDAADTS,PTDADCTS
          ENDIF
.
          CALL      CLPMSVX1
          PACK      KEY8,AADMNO,SP70
          CALL      RDPMVX11
          IF        OVRCD=0
            MATCH     SP70,BKRXADWD
            IF        @EQUAL
              MOVE      PMVXAPWD,BKRXADWD
            ENDIF
          ENDIF
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      ADMNDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      SP70,DOCFNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
          ENDIF
.
          MOVE      "CL",CATEGORY
          MOVE      ACLAIM,CODE
          CALL      GETCOD00
          MOVE      TDESC,CLAIMDSC
.
          MOVE      "CC",CATEGORY
          MOVE      ACARE,CODE
          CALL      GETCOD00
          MOVE      TDESC,CAREDESC
          MOVE      THCSCOD,DIM2
.
          MOVE      "A ",CATEGORY
          MOVE      ATYPE,CODE
          CALL      GETCOD00
          MOVE      TDESC,ATYPDESC
          MOVE      TCINDC15,DIM1
.
          CALL      PTHV0000         * check for previous theatre booking
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDPTELG1          * check for eligibility information
          IF        OVRCD=1            
            CALL      CLPATELG
          ENDIF
.
          CALL      OBSV0000          * Check for clinical documents
.
          MOVE      ZERO,MHVEXIST                   * Check MH Visit
          PACK      KEY8,AADMNO,SP70
          CALL      RDMHVIS1
          IF        OVRCD=0
            MOVE      ONE,MHVEXIST
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td>":
                             "<img src=../images/AppointmentIcon.gif ":
                             " class=ListIcon onclick='updateParent(#"":
                             AADMNO,"#",#"",ADMNDATE,"#",#"":
                             ATIME,"#",#"",ACLAIM,"#",#"":
                             ACARE,"#",#"",DIM2,"#",#"":
                             ASTAY,"#",#"",BKRXADWD,"#",#"":
                             AWARD,"#",#"",ASRCE,"#",#"":
                             ACLSS,"#",#"",ATYPE,"#",#"",DIM1,"#",#"":
                             PTHVFLAG,"#",#"",AMBS,"#",#"":   
                             ABED,"#",#"",PMVXPOWD,"#",#"":   
                             PMVXPOBD,"#",#"",ASTAT,"#",#"":
                             PMVXIDUS,"#",#"",PMVXUDIN,"#",#"":
                             ADIAG1,"#",#"",PTELSPEA,"#",#"":
                             PTELEADM,"#",#"",AFUNDH,"#",#"":
                             AFNDTB,"#",#"",PMVXFNDM,"#",#"":
                             PMVXPOEC,"#",#"",PMVXPILC,"#",#"":
                             PTDAADTS,"#",#"":
                             ADIAG2,"#",#"",DOCEXIST,"#",#"":
                             MHVEXIST,"#");'>";
.
          WRITE     HTMLFILE;ADMNDATE,SP1,ATIME,"</td><td>":
                             STATUS,"</td><td>":
                             "&nbsp",DOCFNAME,"</td><td>":
                             "&nbsp",CAREDESC,"</td><td>":
                             "&nbsp",CLAIMDSC,"</td><td>":
                             "&nbsp",ATYPDESC,"</td></tr>"
.
DSPADM99  RETURN
.------------------------------------------------------------
. Check clinical document type cat wi ind 2 to see if there
. are any linked documents for this visit
. cat wi ind 2 = S = PMVXADMP - Adm Paperwork Received
.                C = PMVXCONO - Consent Obtained
.                M = PMVXMEDH - Medical History Received
.------------------------------------------------------------
OBSV0000  MOVE      ZERO,YESSADMP           * No Adm Paperwork Received
          MOVE      ZERO,YESSCONO           * No Consent Obtained
          MOVE      ZERO,YESSMEDH           * No Medical History Received
          MOVE      "000",DOCEXIST          * No docs exist
.
          PACK      KEY20,URNUMBER,DAADMNO,SP70
          CALL      RSOBPC1
OBSV1000  CALL      RKOBPC1                 * Check correspondence records
          BRANCH    OVRCD,OBSV8000
.
          MATCH     URNUMBER,OBPCURNO       * Correct U/R
          GOTO      OBSV8000 IF NOT EQUAL
.
          MATCH     DAADMNO,OBPCCVIS       * Correct U/R
          GOTO      OBSV8000 IF NOT EQUAL
.
          MATCH     "1",OBPCMDEL            * Deleted ?
          GOTO      OBSV1000 IF EQUAL
.
          MATCH     SP70,OBPCCTYP           * Blank type of Correspondence
          GOTO      OBSV1000 IF EQUAL
.
          PACK      KEY5,CATLWI,OBPCCTYP,SP70   * Type of Correspondence cat wi
          CALL      RDCODE1
          BRANCH    OVRCD,OBSV1000
.
          MATCH     "S",TCINDC2
          IF        @EQUAL
            MOVE      ONE,YESSADMP           * Yes Adm Paperwork Received
          ENDIF
.
          MATCH     "C",TCINDC2
          IF        @EQUAL
            MOVE      ONE,YESSCONO           * Yes Consent Obtained
          ENDIF
.
          MATCH     "M",TCINDC2
          IF        @EQUAL
            MOVE      ONE,YESSMEDH           * Yes Medical History Received
          ENDIF
.
          PACK      KEY3,YESSADMP,YESSCONO,YESSMEDH,SP70
.
          MATCH     "111",KEY3              * All 3 set to Yes so exit loop
          GOTO      OBSV1000 IF NOT EQUAL
.
OBSV8000  PACK      DOCEXIST,YESSADMP,YESSCONO,YESSMEDH,SP70
.
OBSV9999  RETURN
.------------------------------------------------------------
. Display booking row for statistical admission search
.------------------------------------------------------------
DSPBOK00  CALL      CLPATMIS
          CALL      CLPMSVX1
          MOVE      "Booking",STATUS
.
          PACK      KEY8,BKREBOOK,SP70
          CALL      RDBKRX11
          IF        OVRCD=1
            CALL      CLBOKRX1
          ENDIF
.
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      ADMNDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      SP70,DOCFNAME
          MOVE      BKREADOC,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
          ENDIF
.
          MOVE      "CL",CATEGORY
          MOVE      BKRECLMT,CODE
          CALL      GETCOD00
          MOVE      TDESC,CLAIMDSC
.
          MOVE      "CC",CATEGORY
          MOVE      BKREEATY,CODE
          CALL      GETCOD00
          MOVE      TDESC,CAREDESC
.
          MOVE      "A ",CATEGORY
          MOVE      BKREATYP,CODE
          CALL      GETCOD00
          MOVE      TDESC,ATYPDESC
.
          WRITE     HTMLFILE;"<tr><td>":
                             "<img src=../images/AppointmentIcon.gif ":
                             " class=ListIcon onclick='updateParent(#"":
                             ANSB,ANSO,ANSK,"#",#"",BKREBOOK,"#");'> ";
.
          WRITE     HTMLFILE;ADMNDATE,SP1,BKREATIM,"</td><td>":
                             STATUS,"</td><td>":
                             "&nbsp",DOCFNAME,"</td><td>":
                             "&nbsp",CAREDESC,"</td><td>":
                             "&nbsp",CLAIMDSC,"</td><td>":
                             "&nbsp",ATYPDESC,"</td></tr>"
.
DSPBOK99  RETURN
+
.------------------------------------------------------------
. Display admission row for statistical admission search
.------------------------------------------------------------
DSPSAD00  LOAD      STATUS,ASTAT,PREAD,ADMIT,DISCH
.
          IF        ASTAT=1
            MOVE      PTMIXWRD,AWARD
            MOVE      PTMIEBED,ABED
          ENDIF
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDBKREC1
          IF        OVRCD=1
            CALL      CLBOK000
          ENDIF
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDBKRX11
          IF        OVRCD=1
            CALL      CLBOKRX1
          ENDIF
.
          CALL      CLPMSVX1
          PACK      KEY8,AADMNO,SP70
          CALL      RDPMVX11
          IF        OVRCD=0
            MATCH     SP70,BKRXADWD
            IF        @EQUAL
              MOVE      PMVXAPWD,BKRXADWD
            ENDIF
          ENDIF
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      ADMNDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      SP70,DOCFNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
          ENDIF
.
          MOVE      "CL",CATEGORY
          MOVE      ACLAIM,CODE
          CALL      GETCOD00
          MOVE      TDESC,CLAIMDSC
.
          MOVE      "CC",CATEGORY
          MOVE      ACARE,CODE
          CALL      GETCOD00
          MOVE      TDESC,CAREDESC
.
          MOVE      "A ",CATEGORY
          MOVE      ATYPE,CODE
          CALL      GETCOD00
          MOVE      TDESC,ATYPDESC
.
          IF        ASTAT=1
            MOVE      "PRE",DIM3
          ENDIF
.
          IF        ASTAT=2
            MOVE      "CUR",DIM3
          ENDIF
.
          IF        ASTAT=3
            MOVE      "DIS",DIM3
          ENDIF

.
          WRITE     HTMLFILE;"<tr><td>":
                             "<img src=../images/AppointmentIcon.gif ":
                             " class=ListIcon onclick='updateParent(#"":
                             DIM3,"#",#"",AADMNO,"#");'> ";
.
          WRITE     HTMLFILE;ADMNDATE,SP1,ATIME,"</td><td>":
                             STATUS,"</td><td>":
                             "&nbsp",DOCFNAME,"</td><td>":
                             "&nbsp",CAREDESC,"</td><td>":
                             "&nbsp",CLAIMDSC,"</td><td>":
                             "&nbsp",ATYPDESC,"</td></tr>"
.
DSPSAD99  RETURN
.------------------------------------------------------------
. Get Details for Session
.------------------------------------------------------------
SESDET00  PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,OPDASTAT,SP70
          CALL      RDOPSES1
.
          MOVE      OPSETHET,KEY6
          CALL      RDOPOPR1
          IF        OVRCD=0
            CLEAR     DISPNAME
            MOVE      OPRMDESC,NAME35
            CALL      NAMSTR00
            RESET     DISPNAME
            PACK      OPRMDESC,DISPNAME,SP70
          ELSE
            MOVE      "&nbsp;",OPRMDESC
          ENDIF
.
          MOVE      OPSECLIN,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
            MOVE      DOCFNAME,SURFNAME
.            PACK      SURFNAM1,DSNAME,COMMA,DGNAME,COMMA,DTITL
          ELSE
            MOVE      "&nbsp;",SURFNAME
            MOVE      "&nbsp;",SURFNAM1
          ENDIF
.
          MOVE      OPSEANAE,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
            MOVE      DOCFNAME,ANAFNAME
.            PACK      ANAFNAM1,DSNAME,COMMA,DGNAME,COMMA,DTITL
          ELSE
            MOVE      "&nbsp;",ANAFNAME
            MOVE      "&nbsp;",ANAFNAM1
          ENDIF
          RETURN
.          MOVE      "&nbsp;",STYPDES
.          PACK      KEY5,CODEST,OPSETYPE
.          CALL      RDCODE1
.          IF        OVRCD=0
.            MOVE      TDESC,STYPDES
.          ENDIF
.          RETURN
.------------------------------------------------------------
. Ward selection
.------------------------------------------------------------
WSEL0000  PACK      KEY29,SP70
          IF        IBCNMHOS = 1
            PACK      KEY29,SP3,WBSEHOSP,SP70
          ENDIF
          CALL      RDSWARD7
WSEL0100  CALL      RDKWARD7
          BRANCH    OVRCD,WSEL9999
.
.                         if we have a bed number we have passed all the ward
.                         master records, so exit
          MATCH     SP70,WBED
          GOTO      WSEL9999 IF NOT EQUAL
.
          COMPARE   WACTIVE,ZERO                 *List only active wards
          GOTO      WSEL0100 IF NOT EQUAL
.
          IF        IBCNMHOS = 1
            MATCH      SP3,WBSEHOSP
            IF        !@EQUAL
              MATCH     WBSEHOSP,PTWDHOSN
              GOTO      WSEL9999 IF NOT EQUAL
            ENDIF
          ENDIF
.
.                       write the select option.
          WRITE     HTMLFILE;"<option value=#"",WWARD,"#"";
          MATCH     DFLTWARD,WWARD
          IF        @EQUAL
            WRITE     HTMLFILE;" selected";
          ENDIF
          WRITE     HTMLFILE;">",WBDESC;
.
          IF        IBCNMHOS = 1
            MATCH      SP3,WBSEHOSP
            IF        @EQUAL
              WRITE     HTMLFILE;" - ",PTWDHOSN;
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"</option>"
          GOTO      WSEL0100
.
WSEL9999  RETURN
.
.------------------------------------------------------------
. Next Day, Week, Month
.------------------------------------------------------------
NEXT0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          REP       " +",WARDCODE
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
          WRITE     HTMLFILE;"oprweb02.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&wardcode=",WARDCODE:
                             "&frstdate=",NXTFDATE:
                             "&lastdate=",NXTLDATE:
                             "&viewtype=",VIEWTYPE:
                             "&surgmenu=",SURGMENU;
          REP       "+ ",WARDCODE
.
NEXT9999  RETURN
.------------------------------------------------------------
. Previous Day, Week, Month
.------------------------------------------------------------
PREV0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          REP       " +",WARDCODE
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
          WRITE     HTMLFILE;"oprweb02.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&wardcode=",WARDCODE:
                             "&frstdate=",PREFDATE:
                             "&lastdate=",PRELDATE:
                             "&viewtype=",VIEWTYPE:
                             "&surgmenu=",SURGMENU;
          REP       "+ ",WARDCODE
.
PREV9999  RETURN
.*****************************************************************************
.*        Previous visit to theatre indicator                                *
.*****************************************************************************
PTHV0000  MOVE      ZERO,PTHVFLAG
          PACK      KEY31,AADMNO,SP70
          CALL      RSOPDEA2
PTHV1000  CALL      RKOPDEA2
          BRANCH    OVRCD,PTHV9999
.
          MATCH     DAADMNO,OPDAADMN
          GOTO      PTHV9999 IF NOT EQUAL
.
          MATCH     "3",DOPDASTA
          GOTO      PTHV1000 IF EQUAL
.
PTHV2000  MOVE      ONE,PTHVFLAG
.
PTHV9999  RETURN
+
.******************************************************************************
.*                       CMBS Confirmation List                               *
.******************************************************************************
CMBLIS00  MOVE      ZERO,LOOPCNTR         * Keep alive loop counter
          MATCH     "2",SURGMENU
          IF        @EQUAL 
            CALL      CMBLIA00
            GOTO      CMBLIS99 
          ENDIF
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          PACK      KEY26,WBSEHOSP,FRSTDATE,SP70
          CALL      RSOPDEA1
CMBLIS10  CALL      RKOPDEA1
          BRANCH    OVRCD,CMBLIS99
.
          MATCH     WBSEHOSP,OPDAHOSP
          GOTO      CMBLIS99 IF NOT EQUAL
.
.------------------------------------------------------
. Loop counter to prevent timeout internal server error
.------------------------------------------------------
          ADD       ONE,LOOPCNTR
.
          IF        (LOOPCNTR > 100)
            WRITE     HTMLFILE;"//-- ",LOOPCNTR,"--"
            MOVE      ZERO,LOOPCNTR
          ENDIF

          MATCH     OPDADATE,LASTDATE
          GOTO      CMBLIS99 IF LESS
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
          CALL      RDOPSES1
          BRANCH    OVRCD,CMBLIS10
.
          COMPARE   THREE,OPDASTAT
          GOTO      CMBLIS10 IF EQUAL
.
          MATCH     SP70,DOCTCODE
          IF        !@EQUAL
            MATCH     OPDACLIN,DOCTCODE
            GOTO      CMBLIS10 IF NOT EQUAL
          ENDIF
.
          PACK      DESCRICM,SP70      * Clear reason theatre charges incomplete
          MATCH     "1",OPDAACPD
          GOTO      CMBLIS11 IF EQUAL
.
          MATCH     SP70,OPDARICM
          GOTO      CMBLIS11 IF EQUAL
.
          PACK      KEY5,CATGY_Rt,OPDARICM,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      TDESC,DESCRICM
          ENDIF

CMBLIS11  PACK      KEY10,OPDAUNIQ,SP70
          CALL      RDOPARD1
          IF        OVRCD=1
            CALL      CLOPRARD
          ENDIF
.
          PACK      KEY11,OPDAUNIQ,ONE,SP70
          CALL      RDOPSRG1
          IF        OVRCD=1
            CALL      CLOPRSRG
          ENDIF
.
          CALL      RCVT0000              * determine time in recovery
.
          MOVE      SP70,ENDDTIME
          LOAD      ENDDTIME,OPCNTTIM,OPARTCAL,OPARATIM,OPARANAP,OPARANAS:
                                    OPSGTIPS,OPSGTISS,OPSGTISE,OPARANAE:
                                    OPSGTIDR,OPARTIRF,OPARTIRB,OPARTIRD:
                                    OPARTIDP,OPARTETC
.
          PACK      KEY8,OPDAADMN,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,CMBLIS10
.
          MATCH     SP70,THEATRES
          GOTO      CMBLIS15 IF EQUAL
.
          MATCH     "1",THEATRES       * Theatre complete?
          IF        @EQUAL
.           MATCH     "1",PMVXATCP
            MATCH     "1",OPDAACPD
            GOTO      CMBLIS10 IF NOT EQUAL
          ENDIF
.
          MATCH     "2",THEATRES       * Theatre incomplete
          IF        @EQUAL
.           MATCH     "1",PMVXATCP
            MATCH     "1",OPDAACPD
            GOTO      CMBLIS10 IF EQUAL
          ENDIF
.
CMBLIS15  IF        IBCNMHOS=1
            MATCH     WBSEHOSP,OPSEHOSP     * Check hospital with user
            GOTO      CMBLIS10 IF NOT EQUAL
          ENDIF
.
          CALL      CASDET00
.
          CALL      IBACLOCK
          IF        PCEASE=1
            MOVE      PDECDTE,AGEDATE            * Date of Death
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      PATLN000    * format Name with System Parameter
          CALL      PATFLD00
          MOVE      KEY3,PATFOLDR
.
          MOVE      OPSECLIN,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
            MOVE      DOCFNAME,SURFNAME
          ELSE
            MOVE      "&nbsp;",SURFNAME
          ENDIF
. 
.   Get Surgeon & Anaesthetist Name
.
          MOVE      ZERO,ANESINDC
          MOVE      ZERO,SURNINDC
          MOVE      SP70,SURGNAME
          MOVE      SP70,ANESNAME
          PACK      KEY35,OPDAUNIQ,SP70
          CALL      RSOPTSM1
CMBLIS16  CALL      RKOPTSM1
          BRANCH    OVRCD,CMBLIS17
.
          MATCH     OPTSUNID,OPDAUNIQ
          GOTO      CMBLIS17 IF NOT EQUAL
. 
          IF        OPTSSIND=0|OPTSSIND=1
            PACK      KEY6,OPTSSCDE,SP70
            CALL      RDDOCT1
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
            IF        OPTSSIND=0 & SURNINDC=0
              MOVE      DOCFNAME,SURGNAME           * Surgeon Name
              MOVE      ONE,SURNINDC                * Surgeon Indicator
            ELSE
              IF          OPTSSIND=1 & ANESINDC=0
                MOVE      "OP",CATEGORY
                MOVE      OPTSSTYP,CODE
                CALL      GETCOD00
                MATCH     "N",TCINDC1
                IF        @EQUAL
                  MOVE      DOCFNAME,ANESNAME           * Anaesthetist Name
                  MOVE      ONE,ANESINDC                * Anaesthetist Indicator
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          GOTO        CMBLIS16
. 
CMBLIS17  CLEAR     HTMLLINK
          APPEND    "javascript:VisitLink('",HTMLLINK
          APPEND    OPDAURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    OPDAADMN,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          CLEAR     HTMLLNK2
          APPEND    "javascript:PatientLink('",HTMLLNK2
          APPEND    OPDAURNO,HTMLLNK2
          APPEND    "','",HTMLLNK2
          APPEND    OPDAADMN,HTMLLNK2
          APPEND    "')",HTMLLNK2
          RESET     HTMLLNK2
.
          MATCH     SP70,CMBSSTAT
          GOTO      CMBLIS18 IF EQUAL
.
          PACK      KEY14,OPDAADMN,SP70
          CALL      RSPMMTI1
          GOTO      CMBLIS25                     * <= - careful
.
.
CMBLIS18  MOVE      ZERO,LOOPCNTR           * Record outout reset counter 
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":            * 0
                             "#"",OPDADATE,"#",":                     * 1
                             "#"",OPDAURNO,"#",":                     * 2
                             "#"",OPDAADMN,"#",":                     * 3
                             "#"",FORMATNM,"#",":                     * 4
                             "#"",SURFNAME,"#",":                     * 5
                             "#"",PATFOLDR,"#",":                     * 6
                             "#"",HTMLLNK4,"#",":                     * 7
                             "#"",OPDAEXPS,"#",":                     * 8
                             "#"",HTMLLNK2,"#",";                     * 9
.
          PACK      KEY14,OPDAADMN,SP70
          CALL      RSPMMTI1
CMBLIS20  CALL      RKPMMTI1
          BRANCH    OVRCD,CMBLIS50
.
          MATCH     OPDAADMN,PMMIVISN
          GOTO      CMBLIS50 IF NOT EQUAL
.
          MATCH     OPDAUNIQ,PMMIUNIQ
          GOTO      CMBLIS20 IF NOT EQUAL
.
          MATCH     "2",PMMIRTYP
          GOTO      CMBLIS20 IF NOT EQUAL
.
          WRITE     HTMLFILE;"#"",PMMIITEM,SP1,PMMIDESC,"#",":       * 10
                               "#"<input type=checkbox name=dummy ": * 11
                               " value='",PMMIVISN,PMMITRAN,"'":
                               " onclick='CMBSConfirm(this);'";
          MATCH     "1",PMMICCON
          IF        @EQUAL
            WRITE     HTMLFILE;" checked";
          ENDIF
          WRITE     HTMLFILE;">#",":
                             "#"<input type=checkbox name=dummy": * 12
                             " value='",PMMIVISN,"'":
                             " onclick='TheatreConfirm(this,\#"",OPDAUNIQ:
                             "\#");'";
.         MATCH     "1",PMVXATCP
          MATCH     "1",OPDAACPD
          IF        @EQUAL
            WRITE     HTMLFILE;" checked";
          ENDIF
.
CMBLIS21  WRITE     HTMLFILE;">#",":
                             "#"",ENDDTIME,"#",":                * 13
                             "#"",DESCRICM,"#",":                * 14
                             "#"",SURGNAME,"#",":                * 15 Surgeon
                             "#"",ANESNAME,"#");"                * 16 Anaesthetist
.
.
CMBLIS25  CALL      RKPMMTI1
          BRANCH    OVRCD,CMBLIS10
.
          MATCH     OPDAADMN,PMMIVISN
          GOTO      CMBLIS10 IF NOT EQUAL
.
          MATCH     OPDAUNIQ,PMMIUNIQ
          GOTO      CMBLIS25 IF NOT EQUAL
.
          MATCH     "2",PMMIRTYP
          GOTO      CMBLIS25 IF NOT EQUAL
.
          MATCH     SP70,CMBSSTAT
          GOTO      CMBLIS30 IF EQUAL
.
          MATCH     "1",CMBSSTAT
          IF        @EQUAL
            MATCH     "1",PMMICCON
            GOTO      CMBLIS25 IF NOT EQUAL
          ENDIF
.
          MATCH     "2",CMBSSTAT
          IF        @EQUAL
            MATCH     "1",PMMICCON
            GOTO      CMBLIS25 IF EQUAL
          ENDIF
.
CMBLIS30  MOVE      ZERO,LOOPCNTR           * Record outout reset counter
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":        * 0
                             "#"",OPDADATE,"#",":                 * 1
                             "#"",OPDAURNO,"#",":                 * 2
                             "#"",OPDAADMN,"#",":                 * 3
                             "#"",FORMATNM,"#",":                 * 4
                             "#"",SURFNAME,"#",":                 * 5
                             "#"",PATFOLDR,"#",":                 * 6
                             "#"",HTMLLNK4,"#",":                 * 7
                             "#"",OPDAEXPS,"#",":                 * 8
                             "#"",HTMLLNK2,"#",":                 * 9
                             "#"",PMMIITEM,SP1,PMMIDESC,"#",":    * 10
                             "#"<input type=checkbox name=dummy": * 11
                             " value='",PMMIVISN,PMMITRAN:
                             "' onclick='CMBSConfirm(this);'";
          MATCH     "1",PMMICCON
          IF        @EQUAL
            WRITE     HTMLFILE;" checked";
          ENDIF
          WRITE     HTMLFILE;">#",":
                             "#"<input type=checkbox name=dummy": * 12
                             " value='",PMMIVISN,"'":
                             " onclick='TheatreConfirm(this,\#"",OPDAUNIQ:
                             "\#");'";
.         MATCH     "1",PMVXATCP
          MATCH     "1",OPDAACPD
          IF        @EQUAL
            WRITE     HTMLFILE;" checked";
          ENDIF
          WRITE     HTMLFILE;">#",":
                             "#"",ENDDTIME,"#",":                * 13
                             "#"",DESCRICM,"#",":                * 14
                             "#"",SURGNAME,"#",":                * 15 Surgeon
                             "#"",ANESNAME,"#");"                * 16 Anaesthetist
          GOTO      CMBLIS25
.
CMBLIS50  WRITE     HTMLFILE;"#"#",":                             * 10
                             "#"#",":                             * 11
                             "#"<input type=checkbox name=dummy": * 12
                             " value='",OPDAADMN,"'":
                             " onclick='TheatreConfirm(this,\#"",OPDAUNIQ:
                             "\#");'";
.         MATCH     "1",PMVXATCP
          MATCH     "1",OPDAACPD
          IF        @EQUAL
            WRITE     HTMLFILE;" checked";
          ENDIF
          WRITE     HTMLFILE;">#",":
                             "#"",ENDDTIME,"#",":                * 13
                             "#"",DESCRICM,"#",":                * 14
                             "#"",SURGNAME,"#",":                * 15 Surgeon 
                             "#"",ANESNAME,"#");"                * 16 Anaesthetist
          REP       "+ ",OPDAUNIQ
          GOTO      CMBLIS10
.
CMBLIS99  RETURN
+
.-----------------------------------------------------------------------------
. CMBS Confirmation List - Unique Id 
.-----------------------------------------------------------------------------
CMBLIA00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          PACK      KEY26,WBSEHOSP,FRSTDATE,SP70
          CALL      RSOPDEA1
CMBLIA10  CALL      RKOPDEA1
          BRANCH    OVRCD,CMBLIA99
.
          MATCH     WBSEHOSP,OPDAHOSP
          GOTO      CMBLIA99 IF NOT EQUAL
.
.------------------------------------------------------
. Loop counter to prevent timeout internal server error
.------------------------------------------------------
          ADD       ONE,LOOPCNTR
.
          IF        (LOOPCNTR > 100)
            WRITE     HTMLFILE;"//-- ",LOOPCNTR,"--"
            MOVE      ZERO,LOOPCNTR
          ENDIF
.
          MATCH     OPDADATE,LASTDATE
          GOTO      CMBLIA99 IF LESS
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
          CALL      RDOPSES1
          BRANCH    OVRCD,CMBLIA10
.
          COMPARE   THREE,OPDASTAT
          GOTO      CMBLIA10 IF EQUAL
.
          MATCH     SP70,DOCTCODE
          IF        !@EQUAL
            MATCH     OPDACLIN,DOCTCODE
            GOTO      CMBLIA10 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,SURGTYPE
          IF        !@EQUAL
            MATCH     SURGTYPE,OPSEDGSI
            GOTO      CMBLIA10 IF NOT EQUAL
          ENDIF
.
          PACK      DESCRICM,SP70      * Clear reason theatre charges incomplete
          MATCH     "1",OPDAACPD
          GOTO      CMBLIA20 IF EQUAL
.
          MATCH     SP70,OPDARICM
          GOTO      CMBLIA20 IF EQUAL
.
          PACK      KEY5,CATGY_Rt,OPDARICM,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      TDESC,DESCRICM
          ENDIF

CMBLIA20  PACK      KEY10,OPDAUNIQ,SP70
          CALL      RDOPARD1
          IF        OVRCD=1
            CALL      CLOPRARD
          ENDIF
.
          PACK      KEY11,OPDAUNIQ,ONE,SP70
          CALL      RDOPSRG1
          IF        OVRCD=1
            CALL      CLOPRSRG
          ENDIF
.
          CALL      RCVT0000              * determine time in recovery
.
          MOVE      SP70,ENDDTIME
          LOAD      ENDDTIME,OPCNTTIM,OPARTCAL,OPARATIM,OPARANAP,OPARANAS:
                                    OPSGTIPS,OPSGTISS,OPSGTISE,OPARANAE:
                                    OPSGTIDR,OPARTIRF,OPARTIRB,OPARTIRD:
                                    OPARTIDP,OPARTETC
.
          PACK      KEY8,OPDAADMN,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,CMBLIA10
.
          MATCH     SP70,THEATRES
          GOTO      CMBLIA30 IF EQUAL
.
          MATCH     "1",THEATRES       * Theatre complete?
          IF        @EQUAL
            MATCH     "1",OPDAACPD
            GOTO      CMBLIA10 IF NOT EQUAL
          ENDIF
.
          MATCH     "2",THEATRES       * Theatre incomplete
          IF        @EQUAL
            MATCH     "1",OPDAACPD
            GOTO      CMBLIA10 IF EQUAL
          ENDIF
.
CMBLIA30  IF        IBCNMHOS=1
            MATCH     WBSEHOSP,OPSEHOSP     * Check hospital with user
            GOTO      CMBLIA10 IF NOT EQUAL
          ENDIF
.
          CALL      CASDET00
.
          CALL      IBACLOCK
          IF        PCEASE=1
            MOVE      PDECDTE,AGEDATE            * Date of Death
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      PATLN000    * format Name with System Parameter
          CALL      PATFLD00
          MOVE      KEY3,PATFOLDR
.
          MOVE      OPSECLIN,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
            MOVE      DOCFNAME,SURFNAME
          ELSE
            MOVE      "&nbsp;",SURFNAME
          ENDIF
.
.   Get Surgeon & Anaesthetist Name
.
          MOVE      ZERO,ANESINDC
          MOVE      ZERO,SURNINDC
          MOVE      SP70,SURGNAME
          MOVE      SP70,ANESNAME
          PACK      KEY35,OPDAUNIQ,SP70
          CALL      RSOPTSM1
CMBLIA40  CALL      RKOPTSM1
          BRANCH    OVRCD,CMBLIA50
.
          MATCH     OPTSUNID,OPDAUNIQ
          GOTO      CMBLIA50 IF NOT EQUAL
.
          IF        OPTSSIND=0|OPTSSIND=1
            PACK      KEY6,OPTSSCDE,SP70
            CALL      RDDOCT1
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
            IF        OPTSSIND=0 & SURNINDC=0
              MOVE      DOCFNAME,SURGNAME           * Surgeon Name
              MOVE      ONE,SURNINDC                * Surgeon Indicator
            ELSE
              IF          OPTSSIND=1 & ANESINDC=0
                MOVE      "OP",CATEGORY
                MOVE      OPTSSTYP,CODE
                CALL      GETCOD00
                MATCH     "N",TCINDC1
                IF        @EQUAL
                  MOVE      DOCFNAME,ANESNAME           * Anaesthetist Name
                  MOVE      ONE,ANESINDC                * Anaesthetist Indicator
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          GOTO        CMBLIA40
.
CMBLIA50  CALL      MBSDSC00
.
          MATCH     SP70,CMBSSTAT
          GOTO      CMBLIA60 IF EQUAL
.
          IF        MBITMCNT = 0
            GOTO      CMBLIA10
          ENDIF
.
CMBLIA60  CLEAR     HTMLLINK
          APPEND    "javascript:VisitLink('",HTMLLINK
          APPEND    OPDAURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    OPDAADMN,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          CLEAR     HTMLLNK2
          APPEND    "javascript:PatientLink('",HTMLLNK2
          APPEND    OPDAURNO,HTMLLNK2
          APPEND    "','",HTMLLNK2
          APPEND    OPDAADMN,HTMLLNK2
          APPEND    "')",HTMLLNK2
          RESET     HTMLLNK2
.
          CLEAR     HTMLLNK3
          APPEND    "javascript:ReasonTheatreInComplete('",HTMLLNK3
          APPEND    OPDAURNO,HTMLLNK3
          APPEND    "','",HTMLLNK3
          APPEND    OPDAADMN,HTMLLNK3
          APPEND    "','",HTMLLNK3
          APPEND    OPDAUNIQ,HTMLLNK3
          APPEND    "')",HTMLLNK3
          RESET     HTMLLNK3
.
CMBLIA65  MOVE      ZERO,LOOPCNTR           * Record outout reset counter
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":            * 0
                             "#"",OPDADATE,"#",":                     * 1
                             "#"",OPDAURNO,"#",":                     * 2
                             "#"",OPDAADMN,"#",":                     * 3
                             "#"",FORMATNM,"#",":                     * 4
                             "#"",SURFNAME,"#",":                     * 5
                             "#"",PATFOLDR,"#",":                     * 6
                             "#"",HTMLLNK4,"#",":                     * 7
                             "#"",OPDAEXPS,"#",":                     * 8
                             "#"",HTMLLNK2,"#",";                     * 9
.
          IF        MBITMCNT = 0
            WRITE     HTMLFILE;"#"#",":                               * 10
                               "#"#"";                                * 11
          ELSE
            WRITE     HTMLFILE;"#"",MDBDESCS,"#",":                   * 10
                               "#"<input type=checkbox name=dummy ":  * 11
                               " value='",OPDAADMN,"'":
                               " onclick='CMBSConfirmUniq(this,\#"",OPDAUNIQ:
                               "\#");'";
            MATCH     "1",SPMICCON
            IF        @EQUAL
              WRITE     HTMLFILE;" checked";
            ENDIF
            WRITE     HTMLFILE;">#""
          ENDIF
.
          WRITE     HTMLFILE;",":
                             "#"<input type=checkbox name=dummy":     * 12
                             " value='",OPDAADMN,"'":
                             " onclick='TheatreConfirm(this,\#"",OPDAUNIQ:
                             "\#");'";
          MATCH     "1",OPDAACPD
          IF        @EQUAL
            WRITE     HTMLFILE;" checked";
          ENDIF
.
CMBLIA80  WRITE     HTMLFILE;">#",":
                             "#"",ENDDTIME,"#",":              * 13
                             "#"",DESCRICM,"#",":              * 14
                             "#"",SURGNAME,"#",":              * 15 Surgeon
                             "#"",ANESNAME,"#",":              * 16 Anaesthetist
                             "#"",HTMLLNK3,"#");"              * 17
.
.
          GOTO      CMBLIA10
.
CMBLIA99  RETURN
.
.----------------------------------------------------------------------------
. Get misc item and description for a uniqlue id 
.----------------------------------------------------------------------------
MBSDSC00  CLEAR     MDBDESCS
          PACK      MDBDESCS,SP70,SP70,SP70,SP70,SP70,SP70 
          MOVE      SP70,SPMICCON
          MOVE      0,MBITMCNT   
          PACK      KEY14,OPDAADMN,SP70
          CALL      RSPMMTI1
MBSDSC20  CALL      RKPMMTI1
          BRANCH    OVRCD,MBSDSC80
.
          MATCH     OPDAADMN,PMMIVISN
          GOTO      MBSDSC80 IF NOT EQUAL
.
          MATCH     OPDAUNIQ,PMMIUNIQ
          GOTO      MBSDSC20 IF NOT EQUAL
.
          MATCH     "2",PMMIRTYP
          GOTO      MBSDSC20 IF NOT EQUAL
.
          MATCH     SP70,CMBSSTAT
          GOTO      MBSDSC30 IF EQUAL
.
          MATCH     "1",CMBSSTAT
          IF        @EQUAL
            MATCH     "1",PMMICCON
            GOTO      MBSDSC20 IF NOT EQUAL
          ENDIF
.
          MATCH     "2",CMBSSTAT
          IF        @EQUAL
            MATCH     "1",PMMICCON
            GOTO      MBSDSC20 IF EQUAL
          ENDIF
.
MBSDSC30  MOVE      SP70,PROSREQD
          PACK      KEY17,PMMIITEM,OPDADATE,Z70
          CALL      RDSITEM1
          CALL      RDPITEM1
          BRANCH    OVRCD,MBSDSC40
.
          MATCH     PMMIITEM,ITEMNO
          GOTO      MBSDSC40 IF NOT EQUAL
.
          MATCH     "I",PTITACTV            * Inactive ?
          GOTO      MBSDSC40 IF EQUAL       * yes
.
          MATCH     PTITEFDT,OPDADATE
          GOTO      MBSDSC40 IF LESS
.
          MATCH     SP70,PTITETDT
          IF        !@EQUAL
            MATCH     OPDADATE,PTITETDT
            GOTO      MBSDSC40 IF LESS
          ENDIF
.
          MATCH     "1",PTITIWRN
          GOTO      MBSDSC40 IF NOT EQUAL
.
          MOVE      " (Pros Required)",PROSREQD  
.
MBSDSC40  IF        MBITMCNT > 0
            APPEND    "<br>",MDBDESCS
          ENDIF 
          APPEND    PMMIITEM,MDBDESCS
          APPEND    SP1,MDBDESCS
          APPEND    PMMIDESC,MDBDESCS
.
          MATCH     SP70,PROSREQD
          IF        !@EQUAL
            APPEND    PROSREQD,MDBDESCS
          ENDIF
.
          MOVE      PMMICCON,SPMICCON 
          ADD       ONE,MBITMCNT
. 
          GOTO      MBSDSC20
.
MBSDSC80  RESET     MDBDESCS
.
MBSDSC99  RETURN
+
.------------------------------------------------------------
. Clear Booking Details
.------------------------------------------------------------
CLBOK000  MOVE      "       0",BKREURNO
          MOVE      SP20,BKRESNAM
          MOVE      SP2,BKRELOCK
          MOVE      SP6,BKREADOC
          MOVE      SP6,BKRESDOC
          MOVE      SP8,BKREEDAT            * BOKRC1FD vars used
          MOVE      SP5,BKREATIM
          MOVE      SP8,BKREPDAT
          MOVE      SP5,BKREPTIM
          MOVE      SP8,BKREADAT
          MOVE      SP3,BKRETYPE
          MOVE      SP3,BKRECANC
          MOVE      ZERO,BKRESTAT
          MOVE      SP1,BKREOMEM
          MOVE      SP1,BKREPREV
          MOVE      SP3,BKREACCM
          MOVE      SP9,BKREPROC
          MOVE      ZERO,BKRECNT
          MOVE      SP3,BKREWARD
          MOVE      ZEROVISN,BKREADMN
          MOVE      SP1,BKREPREA
          MOVE      SP3,BKREEBED
          MOVE      SP3,BKRECLSS
          MOVE      SP3,BKREATYP
          MOVE      SP3,BKREDEPT
          MOVE      SP6,BKRETDOC
          MOVE      SP3,BKRECLMT
          PACK      BKRESPAR,SP20,SP20
.
CLBOK999  RETURN
+
.------------------------------------------------------------
. Check for Admissions for Day Onc
.------------------------------------------------------------
CHKVIO00  MOVE      ZERO,NODAYS
          MATCH     SP70,URNUMBER
          GOTO      CHKVIO99 IF EQUAL
.
. --- check for current admissions before session date ---
.
CHKVIO10  PACK      KEY17,URNUMBER,TWO,SP70
          CALL      RSPTMI18
          CALL      RKPTMI18
          BRANCH    OVRCD,CHKVIO15
.
          MATCH     URNUMBER,AURNO
          GOTO      CHKVIO15 IF NOT EQUAL
.
          COMPARE   TWO,ASTAT
          GOTO      CHKVIO15 IF NOT EQUAL
.
          CALL      DSPADO00
.
.
. --- check for preadmissions  ---
.
          PACK      KEY17,URNUMBER,ONE,Z70
          CALL      RSPTMI18
CHKVIO15  CALL      RPPTMI18
          BRANCH    OVRCD,CHKVIO20
.
          MATCH     URNUMBER,AURNO
          GOTO      CHKVIO20 IF NOT EQUAL
.
          COMPARE   ONE,ASTAT
          GOTO      CHKVIO20 IF NOT EQUAL
.
          CALL      IBACLOCK
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
.
          MATCH     KEY8,ADATE
          GOTO      CHKVIO15 IF LESS
.
.....     MATCH     ADATE,OPSEDATE
.....     GOTO      CHKVIO20 IF LESS
.
.....     DAYSEP    ADATE,OPSEDATE,NODAYS
.....     COMPARE   EIGHT,NODAYS
.....     GOTO      CHKVIO20 IF NOT LESS
.
          CALL      DSPADO00
          GOTO      CHKVIO15
.
. --- check for on leave admissions before session date ---
.
CHKVIO20  PACK      KEY17,URNUMBER,FOUR,SP70
          CALL      RSPTMI18
          CALL      RKPTMI18
          BRANCH    OVRCD,CHKVIO99
.
          MATCH     URNUMBER,AURNO
          GOTO      CHKVIO99 IF NOT EQUAL
.
          COMPARE   FOUR,ASTAT
          GOTO      CHKVIO99 IF NOT EQUAL
.
          CALL      DSPADO00
.
CHKVIO99  RETURN
+
.------------------------------------------------------------
. Display admission row for Oncology
.------------------------------------------------------------
DSPADO00  LOAD      STATUS,ASTAT,PREAD,ADMIT,DISCH,LEAVE
.
          IF        ASTAT=1
            MOVE      PTMIXWRD,AWARD
            MOVE      PTMIEBED,ABED
          ENDIF
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDBKREC1
          IF        OVRCD=1
            CALL      CLBOK000
          ENDIF
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDBKRX11
          IF        OVRCD=1
            CALL      CLBOKRX1
          ENDIF
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDPTDAD1
          IF        OVRCD=1
            UNPACK    SP70,PTDAADTS,PTDADCTS
          ENDIF
.
          CALL      CLPMSVX1
          PACK      KEY8,AADMNO,SP70
          CALL      RDPMVX11
          IF        OVRCD=0
            MATCH     SP70,BKRXADWD
            IF        @EQUAL
              MOVE      PMVXAPWD,BKRXADWD
            ENDIF
          ENDIF
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      ADMNDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      SP70,DOCFNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
          ENDIF
.
          MOVE      "CL",CATEGORY
          MOVE      ACLAIM,CODE
          CALL      GETCOD00
          MOVE      TDESC,CLAIMDSC
.
          MOVE      "CC",CATEGORY
          MOVE      ACARE,CODE
          CALL      GETCOD00
          MOVE      TDESC,CAREDESC
          MOVE      THCSCOD,DIM2
          MOVE      TCINDC21,DIM1A
.
          MOVE      "A ",CATEGORY
          MOVE      ATYPE,CODE
          CALL      GETCOD00
          MOVE      TDESC,ATYPDESC
          MOVE      TCINDC15,DIM1
.
          CALL      PTHV0000         * check for previous theatre booking
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDPTELG1          * check for eligibility information
          IF        OVRCD=1
            CALL      CLPATELG
          ENDIF
.
          MOVE      SP70,REGIME01
          MOVE      SP70,REGIME02
          MOVE      SP70,REGIME03
.
DSPADO50  PACK      KEY10,AADMNO,SP1,ONE,SP70
          CALL      RDPTRGM1 
          IF        OVRCD=0
            MOVE      PTRGREGM,REGIME01
          ENDIF
.
          PACK      KEY10,AADMNO,SP1,TWO,SP70
          CALL      RDPTRGM1
          IF        OVRCD=0
            MOVE      PTRGREGM,REGIME02
          ENDIF
.
          PACK      KEY10,AADMNO,SP1,THREE,SP70
          CALL      RDPTRGM1
          IF        OVRCD=0
            MOVE      PTRGREGM,REGIME03
          ENDIF
.
          MATCH     DIM1A,SP70
          IF        !@EQUAL
            PACK      DISPCLSS,ONCLCLSS,DIM1A,SP70
            SQUEEZE   DISPCLSS
            WRITE     HTMLFILE;"<tr><td class=",DISPCLSS,">";
          ELSE
            WRITE     HTMLFILE;"<tr><td>";
          ENDIF
.
          WRITE     HTMLFILE;"<img src=../images/AppointmentIcon.gif ":
                             " class=ListIcon onclick='updateParent(#"":
                             AADMNO,"#",#"",ADMNDATE,"#",#"":
                             ATIME,"#",#"",ACLAIM,"#",#"":
                             ACARE,"#",#"",DIM2,"#",#"":
                             ASTAY,"#",#"",BKRXADWD,"#",#"":
                             AWARD,"#",#"",ASRCE,"#",#"":
                             ACLSS,"#",#"",ATYPE,"#",#"",DIM1,"#",#"":
                             PTHVFLAG,"#",#"",AMBS,"#",#"":
                             ABED,"#",#"",PMVXPOWD,"#",#"":
                             PMVXPOBD,"#",#"",ASTAT,"#",#"":
                             PMVXIDUS,"#",#"",PMVXUDIN,"#",#"":
                             ADIAG1,"#",#"",PTELSPEA,"#",#"":
                             PTELEADM,"#",#"",AFUNDH,"#",#"":
                             AFNDTB,"#",#"",PMVXFNDM,"#",#"":
                             PMVXPOEC,"#",#"",PMVXPILC,"#",#"":
                             PTDAADTS,"#",#"",ADOCTA,"#",#"":
                             ACLSSFT,"#",#"",REGIME01,"#",#"":
                             REGIME02,"#",#"",REGIME03,"#",#"":
                             PMVXRHC1,"#",#"",PMVXIDUS,"#");'> ";
.
          WRITE     HTMLFILE;ADMNDATE,SP1,ATIME,"</td><td>":
                             STATUS,"</td><td>":
                             "&nbsp",DOCFNAME,"</td><td>":
                             "&nbsp",CAREDESC,"</td><td>":
                             "&nbsp",CLAIMDSC,"</td><td>":
                             "&nbsp",ATYPDESC,"</td></tr>"
.
          RETURN
+
.------------------------------------------------------------------------------
.         Report 5 - Cancelled CMBS & Miscellaneous Items
.------------------------------------------------------------------------------
CCMI0000  MOVE      ZERO,EXIT
          RESET     UPDATETY
          MATCH     SP70,UPDATETY
          GOTO      CCMI7000 IF EQUAL
.
CCMI7000  CALL      IBACLOCK
          CALL      CURPER00
          CALL      CALCDT00   * Calculate Next and Last Period Dates
.
          MOVE      "Cancelled CMBS and Miscellaneous Items",WEBTITLE
          CALL      WEBHED00
          BRANCH    EXIT,CCMI9999
.
          CALL      WEBBOD00
          CALL      WEBEND00
          MOVE      ONE,EXIT
          GOTO      CCMI9999
.
CCMI9999  RETURN
+
.------------------------------------------------------------------------------
.         Cancelled CMBS & Miscellaneous Items field processing
.------------------------------------------------------------------------------
CMFP0000  BRANCH    FLDITMNO,CMFP0100,CMFP0200,CMFP0300,CMFP0400,CMFP0500:
                             CMFP0600,CMFP0700
          GOTO      CMFP9999
.
CMFP0100  CALL      CCMLST00           * Cancelled CMBS & Misc Items List
          GOTO      CMFP9999
.
CMFP0200  WRITE     HTMLFILE;VIEWTYPE; * View Type (Period Dates)
          GOTO      CMFP9999
.
CMFP0300  WRITE     HTMLFILE;FILTSRCE; * Filter Source
          GOTO      CMFP9999
.
CMFP0400  WRITE     HTMLFILE;FILTSTAT; * Filter Status
          GOTO      CMFP9999
.
CMFP0500  CALL      SELV0000           * Period Dates Selection List
          GOTO      CMFP9999
.
CMFP0600  CALL      CCMPRV00           * Previous button link
          GOTO      CMFP9999
.
CMFP0700  CALL      CCMNXT00           * Next button link
          GOTO      CMFP9999
.
CMFP9999  RETURN
+
.------------------------------------------------------------------------------
.         Cancelled CMBS & Misc Items List
.------------------------------------------------------------------------------
CCMLST00  MOVE      ZERO,CMRECCNT
          PACK      KEY22,FRSTDATE,SP70
          CALL      RSPMCMB2
CCMLST10  CALL      RKPMCMB2
          BRANCH    OVRCD,CCMLST99
.
          MATCH     PMCBRDTE,LASTDATE
          GOTO      CCMLST99 IF LESS
.
          MATCH     SP70,FILTSRCE
          GOTO      CCMLST20 IF EQUAL
.
          MATCH     PMCBSTAT,FILTSRCE
          GOTO      CCMLST10 IF NOT EQUAL
.
CCMLST20  MATCH     PMCBRSTA,FILTSTAT
          GOTO      CCMLST10 IF NOT EQUAL
.
          PACK      KEY8,PMCBVISN,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,CCMLST10
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,CCMLST10
.
          CALL      RDPMPX21
          CALL      PATLN000           * format Name with System Parameter
          CALL      PATFLD00
          MOVE      KEY3,PATFOLDR
.
          MOVE      SP70,CMRECTYP
          MOVE      ZERO,F1
          MOVE      PMCBRTYP,F1
          LOAD      CMRECTYP,F1,RECTYPE1,RECTYPE2
.
          MOVE      SP70,CMRECSRC
          MOVE      ZERO,F1
          MOVE      PMCBSTAT,F1
          LOAD      CMRECSRC,F1,RECSRCE1,RECSRCE2
.
          MOVE      RECSTAT0,CMRECSTA
          MOVE      ZERO,F1
          MOVE      PMCBRSTA,F1
          LOAD      CMRECSTA,F1,RECSTAT1,RECSTAT2
.
          CLEAR     HTMLLINK
          APPEND    "javascript:PatientLink('",HTMLLINK
          APPEND    AURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    DAADMNO,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          CLEAR     HTMLLNK2
          APPEND    "javascript:InputChargeLink('",HTMLLNK2
          APPEND    AURNO,HTMLLNK2
          APPEND    "','",HTMLLNK2
          APPEND    DAADMNO,HTMLLNK2
          APPEND    "')",HTMLLNK2
          RESET     HTMLLNK2
.
          CLEAR     PATNCELL
          APPEND    FORMATNM,PATNCELL
          APPEND    "&nbsp;&nbsp;",PATNCELL
          APPEND    "<a href=\#"javascript:InputChargeLink('",PATNCELL
          APPEND    PMCBUNID,PATNCELL
          APPEND    "');\#" title='Input Theatre Charges'>",PATNCELL
          APPEND    "<img src='../images/ActionIcon.gif'>",PATNCELL
          APPEND    "</a>",PATNCELL
          RESET     PATNCELL
.
          PACK      KEY14,PMCBVISN,PMCBCNTR
          MOVE      CMRECCNT,DIM3
          REP       " 0",DIM3
.
          WRITE     HTMLFILE;"t.addRow(#"",*+,HTMLLINK,*-,"#",":     * 0
                             "#"",PATFOLDR,"#",":                    * 1
                             "#"",*+,PATNCELL,*-,"#",":              * 2
                             "#"",DAADMNO,"#",":                     * 3
                             "#"",PMCBRDTE,"#",":                    * 4
                             "#"",CMRECTYP,"#",":                    * 5
                             "#"",CMRECSRC,"#",";                    * 6
.
          MOVE      SP70,DISBLSTR
          MATCH     "1",PMCBRSTA            * Removal Status = Record Deleted
          IF        @EQUAL
            MOVE      " disabled ",DISBLSTR
          ENDIF
.
.
.         Theatre checkbox
.
          CLEAR     FIELDNAM
          APPEND    "chkT_",FIELDNAM
          APPEND    DIM3,FIELDNAM
          RESET     FIELDNAM
.
          WRITE     HTMLFILE;"#"<input type=checkbox name='",FIELDNAM,"' id='",FIELDNAM,"'";
          MATCH     "1",PMCBTACT
          IF        @EQUAL
            WRITE     HTMLFILE;" checked ";
          ELSE
            WRITE     HTMLFILE;DISBLSTR;
          ENDIF
          WRITE     HTMLFILE;" onclick='TheatreSelect(this)'>#",":  * 7
                             "#"",PMCBTACT,"#",";                   * 8
.
.
.         Coding checkbox
.
          CLEAR     FIELDNAM
          APPEND    "chkC_",FIELDNAM
          APPEND    DIM3,FIELDNAM
          RESET     FIELDNAM
.
          WRITE     HTMLFILE;"#"<input type=checkbox name='",FIELDNAM,"' id='",FIELDNAM,"'";
          MATCH     "1",PMCBCACT
          IF        @EQUAL
            WRITE     HTMLFILE;" checked ";
          ELSE
            WRITE     HTMLFILE;DISBLSTR;
          ENDIF
          WRITE     HTMLFILE;" onclick='CodingSelect(this)'>#",":   * 9
                             "#"",PMCBCACT,"#",";                   * 10
.
.
.         Billing checkbox
.
          CLEAR     FIELDNAM
          APPEND    "chkB_",FIELDNAM
          APPEND    DIM3,FIELDNAM
          RESET     FIELDNAM
.
          WRITE     HTMLFILE;"#"<input type=checkbox name='",FIELDNAM,"' id='",FIELDNAM,"'";
          MATCH     "1",PMCBBACT
          IF        @EQUAL
            WRITE     HTMLFILE;" checked ";
          ELSE
            WRITE     HTMLFILE;DISBLSTR;
          ENDIF
          WRITE     HTMLFILE;" onclick='BillingSelect(this)'>#",":  * 11
                             "#"",PMCBBACT,"#",";                   * 12
.
.
.         Delete Record fields (checkbox and comments field)
.
          MOVE      SP70,DISBLSTR
          MATCH     "1",PMCBTACT
          IF        @EQUAL
            MOVE      " disabled ",DISBLSTR
          ENDIF
.
          MATCH     "1",PMCBCACT
          IF        @EQUAL
            MOVE      " disabled ",DISBLSTR
          ENDIF
.
          MATCH     "1",PMCBBACT
          IF        @EQUAL
            MOVE      " disabled ",DISBLSTR
          ENDIF
.
          CLEAR     DELRCELL
          APPEND    "<input type=checkbox name=chkD_",DELRCELL
          APPEND    DIM3,DELRCELL
          APPEND    " id=chkD_",DELRCELL
          APPEND    DIM3,DELRCELL
          MATCH     "1",PMCBRSTA
          IF        @EQUAL
            APPEND    " checked ",DELRCELL
          ENDIF
          APPEND    DISBLSTR,DELRCELL
          APPEND    " onclick='DeleteSelect(this)'>&nbsp;&nbsp;",DELRCELL
          APPEND    " <input type=text name=inpt_",DELRCELL
          APPEND    DIM3,DELRCELL
          APPEND    " id=inpt_",DELRCELL
          APPEND    DIM3,DELRCELL
.
          MATCH     SP70,PMCBDCOM
          IF        !@EQUAL
            STRIP     PMCBDCOM
            APPEND    " value=\#"",DELRCELL
            APPEND    PMCBDCOM,DELRCELL       * Deletion Comments
            APPEND    "\#"",DELRCELL
          ENDIF
          MATCH     "1",PMCBRSTA
          IF        @EQUAL
            APPEND    " class=Mandatory ",DELRCELL
          ELSE
            APPEND    " disabled ",DELRCELL
          ENDIF
.
          APPEND    DISBLSTR,DELRCELL       * Disabled string
          APPEND    " size=38 maxlength=50 title='Deletion Comments'",DELRCELL
          APPEND    " onblur='onBlurComment(this)'>",DELRCELL
          RESET     DELRCELL
.
          WRITE     HTMLFILE;"#"",*+,DELRCELL,*-,"#",":              * 13
                             "#"",PMCBRSTA,"#",":                    * 14
                             "#"",CMRECSTA,"#",":                    * 15
                             "#"",DIM3,"#",":                        * 16
                             "#"",KEY14,"#",":                       * 17
                             "#"",PMCBITEM,"#",":                    * 18
                             "#"",PMCBQUAN,"#");"                    * 19
.
          ADD       ONE,CMRECCNT
          GOTO      CCMLST10
.
CCMLST99  RETURN
+
.------------------------------------------------------------------------------
.         Cancelled CMBS & Misc Items Previous Link
.------------------------------------------------------------------------------
CCMPRV00  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          WRITE     HTMLFILE;"oprweb02.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",PREFDATE:
                             "&lastdate=",PRELDATE:
                             "&filtsrce=",FILTSRCE:
                             "&filtstat=",FILTSTAT:
                             "&viewtype=",VIEWTYPE;
.
CCMPRV99  RETURN
+
.------------------------------------------------------------------------------
.         Cancelled CMBS & Misc Items Next Link
.------------------------------------------------------------------------------
CCMNXT00  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          WRITE     HTMLFILE;"oprweb02.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",NXTFDATE:
                             "&lastdate=",NXTLDATE:
                             "&filtsrce=",FILTSRCE:
                             "&filtstat=",FILTSTAT:
                             "&viewtype=",VIEWTYPE;
.
CCMNXT99  RETURN
+
.------------------------------------------------------------------------------
.         Remote Scripting Processing
.------------------------------------------------------------------------------
REMOTE00  CALL      XMLHED00
          BRANCH    EXIT,REMOTE99
.
          BRANCH    REMOTENO,REMOTE10,REMOTE20,REMOTE30
          GOTO      REMOTE98
.
REMOTE10  CALL      UPDCCM00           * Update Cancelled CMBS & Misc record
          GOTO      REMOTE98
.
REMOTE20  CALL      RSTHIC00           * Update Reason Theatre Incomplete
          GOTO      REMOTE98
.
REMOTE30  CALL      CHKUAV00
          GOTO      REMOTE98
.
REMOTE98  CALL      XMLEND00
REMOTE99  RETURN
+
.------------------------------------------------------------------------------
.         Update Cancelled CMBS & Misc record (Remote Scripting)
.------------------------------------------------------------------------------
UPDCCM00  REP       "+ ",UPDATKEY
          PACK      KEY14,UPDATKEY,SP70
          CALL      RDPMCMB1
          BRANCH    OVRCD,UPDCCM91
.
          CALL      IBACLOCK
.
          MOVE      ZERO,F1
          MOVE      UPDATTYP,F1
          BRANCH    F1,UPDCCM10,UPDCCM20,UPDCCM30,UPDCCM40
          GOTO      UPDCCM91
.
.         Update Theatre Action Taken
UPDCCM10  MATCH     "1",UPDATVAL
          IF        @EQUAL
            MOVE      "1",PMCBTACT               * update Theatre Action Taken
            MOVE      USERID,PMCBTUID            * User ID
            PACK      PMCBTDTE,CCC,CYY,CMM,CDD   * Date
            MOVE      CTIMEIS,PMCBTTIM           * Time
.
            MATCH     "1",PMCBCACT          * check Coding Action Taken flag
            IF        @EQUAL
              MATCH     "1",PMCBBACT        * check Billing Action Taken flag
              IF        @EQUAL
                MOVE      "2",PMCBRSTA      * Record Removed since others ticked
              ENDIF
            ENDIF
          ELSE
            MOVE      SP70,PMCBTACT              * reset Theatre Action Taken
            UNPACK    SP70,PMCBTUID,PMCBTDTE,PMCBTTIM   * clear fields
            MOVE      "0",PMCBRSTA          * reset Removal Status
          ENDIF
          GOTO      UPDCCM90
.
.         Update Coding Action Taken
UPDCCM20  MATCH     "1",UPDATVAL
          IF        @EQUAL
            MOVE      "1",PMCBCACT               * update Coding Action Taken
            MOVE      USERID,PMCBCUID            * User ID
            PACK      PMCBCDTE,CCC,CYY,CMM,CDD   * Date
            MOVE      CTIMEIS,PMCBCTIM           * Time
.
            MATCH     "1",PMCBTACT          * check Theatre Action Taken flag
            IF        @EQUAL
              MATCH     "1",PMCBBACT        * check Billing Action Taken flag
              IF        @EQUAL
                MOVE      "2",PMCBRSTA      * Record Removed since others ticked
              ENDIF
            ENDIF
          ELSE
            MOVE      SP70,PMCBCACT              * reset Theatre Action Taken
            UNPACK    SP70,PMCBCUID,PMCBCDTE,PMCBCTIM   * clear fields
            MOVE      "0",PMCBRSTA          * reset Removal Status
          ENDIF
          GOTO      UPDCCM90
.
.         Update Billing Action Taken
UPDCCM30  MATCH     "1",UPDATVAL
          IF        @EQUAL
            MOVE      "1",PMCBBACT               * update Billing Action Taken
            MOVE      USERID,PMCBBUID            * User ID
            PACK      PMCBBDTE,CCC,CYY,CMM,CDD   * Date
            MOVE      CTIMEIS,PMCBBTIM           * Time
.
            MATCH     "1",PMCBTACT          * check Theatre Action Taken flag
            IF        @EQUAL
              MATCH     "1",PMCBCACT        * check Coding Action Taken flag
              IF        @EQUAL
                MOVE      "2",PMCBRSTA      * Record Removed since others ticked
              ENDIF
            ENDIF
          ELSE
            MOVE      SP70,PMCBBACT              * reset Theatre Action Taken
            UNPACK    SP70,PMCBBUID,PMCBBDTE,PMCBBTIM   * clear fields
            MOVE      "0",PMCBRSTA          * reset Removal Status
          ENDIF
          GOTO      UPDCCM90
.
UPDCCM40  MATCH     "1",UPDATVAL
          IF        @EQUAL
            MOVE      "1",PMCBRSTA               * update Removal Status -> 1
            MOVE      USERID,PMCBDUID            * User ID
            PACK      PMCBDDTE,CCC,CYY,CMM,CDD   * Date
            MOVE      CTIMEIS,PMCBDTIM           * Time
            MOVE      UPDATTXT,PMCBDCOM          * Deletion Comments
          ELSE
            MOVE      "0",PMCBRSTA               * reset Removal Status
            UNPACK    SP70,PMCBDUID,PMCBDDTE,PMCBDTIM   * clear fields
            MOVE      SP70,PMCBDCOM              * clear Deletion Comments
          ENDIF
          GOTO      UPDCCM90
.
UPDCCM90  CALL      UPPMCMB1
          WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>0</RETURN_VALUE>"
          GOTO      UPDCCM99
.
UPDCCM91  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>1</RETURN_VALUE>"
          GOTO      UPDCCM99
.
UPDCCM99  RETURN
+
.------------------------------------------------------------------
. Check for Admissions that can be linked to statistical admissions
.------------------------------------------------------------------
STAVIS00  MATCH     SP70,URNUMBER
          GOTO      STAVIS99 IF EQUAL
.
          MATCH     SP70,ADMISSNO
          GOTO      STAVIS99 IF EQUAL
.
          MATCH     "P",SRCHTYPE            * Pre Admissions and bookings
          GOTO      STAVIS05 IF EQUAL
.
          MATCH     "D",SRCHTYPE            * Discharged and current only
          GOTO      STAVIS20 IF EQUAL
.
          GOTO      STAVIS99
.
. --- check for bookings ---
.
STAVIS05  PACK      KEY16,URNUMBER,Z70
          CALL      RSBKREC6
STAVIS10  CALL      RPBKREC6
          BRANCH    OVRCD,STAVIS15
.
          MATCH     URNUMBER,BKREURNO
          GOTO      STAVIS15 IF NOT EQUAL
.
          COMPARE   ZERO,BKRESTAT
          GOTO      STAVIS10 IF NOT EQUAL
.
          CALL      DSPBOK00
          GOTO      STAVIS10
.
. --- check for preadmissions  ---
.
STAVIS15  PACK      KEY17,URNUMBER,ONE,Z70
          CALL      RSPTMI18
STAVIS16  CALL      RPPTMI18
          BRANCH    OVRCD,STAVIS20
.
          MATCH     URNUMBER,AURNO
          GOTO      STAVIS20 IF NOT EQUAL
.
          COMPARE   ONE,ASTAT
          GOTO      STAVIS20 IF NOT EQUAL
.
          CALL      DSPSAD00
          GOTO      STAVIS16
.
. --- check for on current admissions ---
.
STAVIS20  MATCH     "P",SRCHTYPE            * Bookings and pre admissions only
          GOTO      STAVIS99 IF EQUAL
.
          PACK      KEY17,URNUMBER,TWO,SP70
          CALL      RSPTMI18
STAVIS21  CALL      RKPTMI18
          BRANCH    OVRCD,STAVIS25
.
          MATCH     URNUMBER,AURNO
          GOTO      STAVIS25 IF NOT EQUAL
.
          MATCH     ADMISSNO,DAADMNO
          GOTO      STAVIS21 IF EQUAL
.
          COMPARE   TWO,ASTAT
          GOTO      STAVIS25 IF NOT EQUAL
.
          CALL      DSPSAD00
          GOTO      STAVIS21
.
. --- check for discharged admissions within a admission date  ---
. --- the same as this admissions discharge date               ---
.
STAVIS25  MATCH     SP70,VALDDATE
          GOTO      STAVIS99 IF EQUAL
.
          PACK      KEY17,URNUMBER,THREE,Z70
          CALL      RSPTMI18
STAVIS30  CALL      RPPTMI18
          BRANCH    OVRCD,STAVIS99
.
          MATCH     URNUMBER,AURNO
          GOTO      STAVIS99 IF NOT EQUAL
.
          COMPARE   THREE,ASTAT
          GOTO      STAVIS99 IF NOT EQUAL
.
          MATCH     ADMISSNO,DAADMNO
          GOTO      STAVIS30 IF EQUAL
.
          MATCH     VALDDATE,ADATE               * Admitted on proposed
          GOTO      STAVIS30 IF NOT EQUAL        * discharge date
.
          CALL      DSPSAD00
          GOTO      STAVIS30
.
STAVIS99  RETURN
+
.------------------------------------------------------------------------------
.                   Check unavailability of theatre
.------------------------------------------------------------------------------

CHKUAV00  PACK      DIM6,VALDCODE,SP70 

          PACK      KEY19,DIM6,VALDDATE,SP70   * Check for unavailability
          CALL      RSOPUAV1         * sets the position on the DB
CHKUAV01  CALL      RKOPUAV1         * reads the entry that has been selected
          BRANCH    OVRCD,CHKUAV99

.         Checks if the theatre code is correct
          MATCH     DIM6,OPUATHET
          GOTO      CHKUAV99 IF NOT EQUAL

.         Checks if the date is correct
          MATCH     VALDDATE,OPUADATE
          GOTO      CHKUAV99 IF NOT EQUAL

.         Checks to see if the start time begins during the shutdown period
          MATCH     OPUASTIM,VALDFTIM
          IF        !@LESS | @EQUAL
            MATCH     OPUAETIM,VALDFTIM
            IF        @LESS | @EQUAL
              GOTO      CHKUAV10
            ENDIF
          ENDIF

.         Checks to see if the start time begins during the shutdown period
          MATCH     OPUASTIM,VALDTTIM
          IF        !@LESS | @EQUAL
            MATCH     OPUAETIM,VALDTTIM
            IF        @LESS | @EQUAL
              GOTO      CHKUAV10
            ENDIF
          ENDIF

.         Checks to see if the shutdown period appears during the booking 
          MATCH     VALDFTIM,OPUASTIM
          IF        !@LESS | @EQUAL
            MATCH     VALDTTIM,OPUAETIM
            IF        @LESS | @EQUAL
              GOTO      CHKUAV10
            ENDIF
          ENDIF
          GOTO      CHKUAV01     

.         Massages date into human readable form
CHKUAV10  UNPACK    OPUADATE,CCENT,CYEAR,CMON,CDAY
          MOVE      SP70,DIM11
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DIM11,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR 

          WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>",ONE:
                             "|",DIM11:
                             "|",OPUASTIM:
                             "|",OPUAETIM:
                             "|",OPUATHET:
                             "</RETURN_VALUE>"
          GOTO      CHKUAV99
.
CHKUAV99  RETURN
.------------------------------------------------------------------------------
.
          INC       CLBOKRX1
          INC       CLOPRARD
          INC       CLOPRSRG
          INC       CLPATMIS
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       MEHVI1IO/INC
          INC       NHIMASIO/INC
          INC       OBSPCOIO/INC
          INC       OPRDEAIO/INC
          INC       OPRDEBIO/INC
          INC       OPRDECIO/INC
          INC       OPROPRIO/INC
          INC       OPRARDIO/INC
          INC       OPRSESIO/INC
          INC       OPRSRGIO/INC
          INC       OPRUAVIO/INC    * Operating Room - Unavailable Theatre
          INC       PATRGMIO/INC
          INC       PATCMCIO/INC
          INC       PATHSPIO/INC
          INC       PATDADIO/INC
          INC       PATDO1IO/INC
          INC       PATITMIO/INC
          INC       PATELGIO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC
          INC       PATMI1IO/INC
          INC       PATDSCIO/INC
          INC       PATNIDIO/INC
          INC       PATNURIO/INC
          INC       PATPR1IO/INC
          INC       PATWR1IO/INC
          INC       PATVISIO/INC
.
          INC       PMSCCDIO/INC   * Concession Card Details
          INC       PMSVX1IO/INC
          INC       PMSMTIIO/INC
          INC       PMSCMBIO/INC       * Cancelled CMBS and Miscellaneous Items
          INC       OPRTSMIO/INC
          INC       MRTLOCIO/INC
          INC       CLPATELG
          INC       CLPMSPX2
          INC       CLPMSVX1
          INC       DGCLIITM
          INC       OPRECOVT 
          INC       PATDOCTM      * Template Fill-in for Doctors File
          INC       PATDOCCD 
          INC       PATWRDTM      * Template Fill-in for Patient Master File
          INC       NAMSTRCD      * Create Name String
          INC       IBAWEBCD      * Standard WEB Functions
          INC       WEBDATCD    
          INC       PATNAMCD    
          INC       DAYOFWEK
          INC       CALCAGE 
          INC       PMIGTNID
          INC       PMSPX2TM
          INC       OPTHCMPL      * Theatre complete (all charges posted)
          INC       OPTHETAT      * Theatre audit

.******************************************************************************
.* System   : OUTPATIENT                                                      *
.* Program  : IBAOUT39           (N.Z. Only)                                  *
.* Desc     : Daily Booking Schedule 2                                        *
.******************************************************************************
.* Date     :  01/11/89                                                       *
.* Author   :  K.Peak                                                         *
.* Comment  :                                                                 *
.* Mods.                                                                      *
.******************************************************************************
.*        V12.00.01 16/05/2025  Ebon Clements    TSK 0955096                  *
.*                  Alphanueric visit number changes                          *
.*        V11.02.01 09/02/2022  Thanh T          TSK 0905641                  *
.*                  Recompiled as OUTMA1FD changes                            *
.*        V10.11.02 27/11/2017  Thanh T.      TSK 0821710                     *
.*                  Recompiled as PATMISTD changed                            *
.*        V10.11.01 08/09/2017 Thanh T.       TSK 0821710                     *
.*                  Audit Amission/outpatient visit comments                  *
.******************************************************************************
.*        V10.07.01 30/10/2015  Ebon Clements CAR 268970                      *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*        V10.03.02 Ania P                    CAR 286027                      *
.*                  Added Confirmed/PMVXCONF column to report.                *
.*        V10.03.01 25/10/2013  Davin         CAR 213566                      *
.*                  Show unavailable slots (and slot comments if they exist)  *
.*        V10.00.01 08/09/2010  Mike Laming   CAR 229472                      *
.*                  Move "Empty Slots" bit back onto "Date  Start Time" line  *
.*        V9.12.01  02/03/2010  Ebon Clements CAR 215852                      *
.*                  Added option to print empty slots when printing           *
.*                  an individual clinic                                      *
.*        V9.10.01  08/08/2008  Ebon Clements CAR 172767                      *
.*                  Fixed line number count and print of empty slots          *
.*                  when running by an individual clinic                      *
.*        V9.09.02  04/03/2008  Peter Vela    CAR 162914                      *
.*                  Recompiled for KYOUTCLI - readk of outcliaf               *
.*        V9.09.01  28/07/2007  Jill Habasque CAR 103645                      *
.*                  Recompiled for KYOUTCLI - read of pmshcpaf                *
.*        V9.08.02  09/02/2007  Ebon Clements  CAR 130213                     *
.*                  Added print all clinics option                            *
.*        V9.08.01  19/12/2006  Jill Habasque  CAR 128724                     *
.*                  Added output of alternate ID                              *
.*        V9.07.02  20/12/2006  Jill Habasque CAR 128724                      *
.*                  Added output of extra comment lines                       *
.*        V9.07.01  19/12/2006  Jill Habasque CAR 128724                      *
.*                  Added output of alternate ID                              *
.*        V9.04.01  17/02/2005  Mike Laming   CAR 57896                       *
.*                  Fix read of "patpramf" for blank Outpatient No at PRT0500 *
.*        V5.08.02  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B03 26/04/2000  Jill Habasque    SRF 646694                   *
.*                  Fixed printing of supressed slots                         *
.*        V5.08.B02  13/03/2000  Steve Armstrong   5.08 Mods                  *
.*                   Mods for changes to OUTCLIFD (effective date)            *
.*        V5.08.B01  30/12/1999  Steve Armstrong                              *
.*                   Fixed global recompile bugs                              *
.******************************************************************************
.*       V5.07.B03  19/01/1999  Nicole Harrington 507 rebound 097             *
.*                  Mods to display century on date of birth                  *
.*       V5.07.B02  18/01/1999  Nicole Harrington 507 rebound 095             *
.*                  Mods to use HEAD132                                       *
.*       V5.07.B01  30/10/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.******************************************************************************
.*        V5.01.02  03/11/89 S.Barcham                                        *
.*                  Fix Comments                                              *
.*        V5.01.03  08/11/89 K.Peak                     SRF 103002            *
.*                  Added second line of comments                             *
.*        V5.01.04  17.11.89 D.Di.Paola                 SRF 103067            *
.*                  Put in break on report if there is more than one          *
.*                  clinic on the same day with the same Id.                  *
.*                  Also put in clinic description and session start          *
.*                  time on header....                                        *
.*        V5.01.05  21.11.89 D.Di.Paola                 SRF 103002            *
.*                  If a person has a 0 U/R No. then and Name, D.O.B.         *
.*                  etc, don't print, therefore looks in Patient Pre-         *
.*                  Admission File to get master details....                  *
.*        V5.01.06  24/11/89 K.Peak                     SRF 103114            *
.*                  Changed Clinic Code to Clinic Id                          *
.*        V5.01.07  29/03/90 Steve O'Gorman             SRF 103938            *
.*                  Added check for Zero UR in PRT Routine                    *
.*        V5.01.08  30/03/90 Steve O'Gorman             SRF 103938            *
.*                  Added check in patpramf file                              *
.*        V5.01.09  01/05/90 Graeme Williams            SRF 104477            *
.*                  Redesigned print format to Wellington specs               *
.*        V5.01.10  05/07/90 i. chung                   SRF 105514            *
.*                  Insert '*' for U/R Numbers that has case notes            *
.*        V5.01.11  11/07/90 Paul Duncan                                      *
.*                  recomp for OUTBOA & OUTBOB audits                         *
.*        V5.01.12  17/07/90 Paul Duncan                                      *
.*                  ask for number of copies to print                         *
.*        V5.01.13  19/07/90 i. chung                   SRF 105693            *
.*                  a) Read CCNOTES as part of program initialization         *
.*                  b) If OBAOUTNO differs from PCADMN during printing,       *
.*                  proceed as if OVRCD has occurred                          *
.*        V5.01.121 07/11/90 D.Di.Paola                 SRF 107027            *
.*                  Alter mainline to allow cursor to go back to input        *
.*                  of clinic-id after printing finished, to save time.       *
.*        V5.01.122 14/11/90 D.Di.Paola                 SRF 107027            *
.*                  Fix page breaks for control break rotuine....             *
.*        V5.01.123 27/11/90 Glen Hobby                                       *
.*                  Recompiled for changes to PATMX1FD                        *
.*        V5.01.124 02/08/1991    Justin Coates  Quote 6994  SRF 109232       *
.*                  Suppress display of extended slots if desired             *
.*        V5.01.125 09/12/1992    Justin Coates  Quote 7415  SRF 113418       *
.*                  Added charge status and card details using OTCNXDBS       *
.*        V5.01.126 02/03/93  ROD                                             *
.*                  recopmiled for outma1af                                   *
.*        V5.01.127 03.03.93 Neeriem "I Hate Support" Dye                     *
.*                  Modify to print full address                              *
.*        V5.01.128 19/05/93 J.Tan    SRF 122218                              *
.*                  Mods to initialise the heading variables                  *
.*                  Fixed printing multiple copies                            *
.*        V5.01.129 31/08/1994 Max White SRF 600970                           *
.*                  Optionally select clinics by start time if control file   *
.*                  parameter OTCNDBTM is set to 'Yes'.                       *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       OUTBOAFD/INC     * clinic booking file
          INC       OUTBOXFD/INC     * Unavailable slot booking comments
          INC       OUTCLIFD/INC     * clinic file
          INC       OUTCONFD/INC     * control file
          INC       OUTCTYFD/INC     * clinic type file
          INC       OUTMA1FD/INC     * clinic master file
          INC       OUTSESFD/INC     * session file
          INC       OUTSITFD/INC     * site file
          INC       VISMDTFD/INC     * comments file
          INC       VISMTXFD/INC
          INC       PATMISTD/INC
          INC       PATCODFD/INC     * codes file
          INC       PATDO1FD/INC     * patient doctor file
          INC       PMSHCPFD/INC     * National HCP file
          INC       PATMA1FD/INC     * master file
          INC       PATPR1FD/INC     * patient pre-admission file
          INC       PMSVX1FD/INC     * Master extension file
+
.**********************************************************************
.*                         VARIABLES                                  *
.**********************************************************************
.
ADDR1     DIM       20       * address line 1
ADDR2     DIM       20       * address line 2
.
BDATE     DIM       10       * patients brth date
BUS       INIT      "W:"
.
CASENOTE  DIM       1        * case note output     * added for V5.01.13
CCNOTES   DIM       1        * case notes symbol
CODEDESC  DIM       25       * description for unavailable slots
CLINIC    DIM       6        * outpatient clinic id
CLNDESC   DIM       30
COMMENT1  DIM       60       * comments
COMMENT2  DIM       60       * comments
COMMENT3  DIM       60       * comments
COMMENT4  DIM       60       * comments
.
DIM3      DIM       3
DIM11     DIM       11
DIM40     DIM       40
DIM9      DIM       9 
DOCTNAM   DIM       50       * doctor's formatted name
EXP10     DIM       10       * expiry date dd/mm/ccyy
.
FORM3     FORM      3
FSTDDD    FORM      2
FSTMON    FORM      2        * fromdate
FSTYRR    FORM      2        * fromdate
FSTCNT    FORM      2        * fromdate
FROMDATE  DIM       8        * fromdate used for key
.
HOM       INIT      "H:"
.
.
NOPRINT   FORM      1
.
PAGENO    FORM      3        * page counter
PFDATE    DIM       10       * print from date
PNAME     DIM       25       * patients name
.
SAVCTYP   DIM       6        * outpatient clinic type
SAVCLIN   DIM       6        * save clinic id
SAVSTRT   DIM       5        * save variable for session start time
STARTD    DIM       8        * keyin date
STARTT    DIM       5        * keyin time
SITE      DIM       6        * site code
SP12      INIT      "            "
.
TELHOM20  DIM       17       * home telephone
TELBUS20  DIM       17       * business telephone
TELHOM    DIM       14       * home telephone
TELBUS    DIM       14       * business telephone
ADDRESS   DIM       50
PRTEMPTY  FORM      1        * Print empty slots (Single clinic option only)
PTCNDAUR  DIM       1
VAR       DIM       127
LENGTH    FORM      3
.
.-----------------------------------------------------------------------
PRGID     INIT      "IBAOUT39"
PRGNAM    INIT      "Daily Booking Schedule 2"
VERSION   INIT      "V12.01.00"
.-----------------------------------------------------------------------
+
.**********************************************************************
.*                         MAINLINE                                   *
.**********************************************************************
.
ML0000    CALL      INIT0000        * initialisation
          BRANCH    EXIT,ML9999     * invalid site
.
ML1000    CALL      SCR0000         * select option
          BRANCH    EXIT,ML9999
.
          BRANCH    OPTION,ML2000
.
ML2000    CALL      CLR0000         * clear variables
          CALL      STR0000         * display screen
          CALL      DOPT0000        * display which option
.
ML2500    CALL      CLN0000         * keyin clinic
          BRANCH    EXIT,ML1000
.
ML3000    CALL      DAT0000         * keyin date
          BRANCH    EXIT,ML2500
.
ML3500    CALL      TIM0000         * keyin time
          BRANCH    EXIT,ML3000
.
          CALL      EMP0000         * keyin print empty slots
          BRANCH    EXIT,ML3000
.
          CALL      CHK0000         * ok to continue
          BRANCH    EXIT,ML3500
.
          CALL      PRC0000         * process data
.
          COMPARE   ZERO,PAGENO
          GOTO      ML5000 IF EQUAL * No report generated
.
          CALL      NCOP0000        * get number of copies
ML4000    SUB       ONE,FORM3
          COMPARE   ZERO,FORM3
          GOTO      ML5000 IF EQUAL
          MOVE      "60",CLNO       * line counter
          CALL      PRC0000         * process data
          GOTO      ML4000
.
ML5000    CALL      END0000         * end of report
.
.----------------------------------------------------------------------
.                          .. The End                                  
.----------------------------------------------------------------------
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                         INIT0000                                   *
.*                      Initialisation                                *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ONE,NOPRINT             * inititialize to no printout
.
          DISPLAY   *P56:24,"Opening patma1af";
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
          MOVE      IDDD,SITE
          MOVE      IDDD,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,INIT9900          * invalid site
.
          DISPLAY   *P64:24,"outbokaf";
          OPEN      OUTBOKA1,"outbokaf"
.
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"vismdtaf";
          OPEN      VISMDTA1,"vismdtaf"
          DISPLAY   *P64:24,"vismtxaf";
          OPEN      VISMTXA1,"vismtxaf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          PACK      CFNAME WITH UID,FILBOKA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA1,CFNAME
.
          PACK      CFNAME WITH UID,FILBOXA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOXA1,CFNAME
.
          PACK      CFNAME WITH UID,FILCLIA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME WITH UID,FILSESA2
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTSESA2,CFNAME
.
          PACK      CFNAME WITH UID,FILCTYA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCTYA1,CFNAME
.
          PACK      CFNAME WITH UID,FILMA1A1
          DISPLAY   *P64:24,CFNAME;
          CALL       OPOTMA11
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN9;*75,CCNOTES
          READ      CONTROLF,THIRTY1;*155,OTCNSUPX,*205,OTCNXDBS
          READ      CONTROLF,THIRTY2;*32,OTCNDBTM
          MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9900  DISPLAY   *P1:24,*B,"Invalid Site Code.  ",*EL;
          CALL      HITENTER 
          MOVE      ONE,EXIT
.
INIT9999  RETURN
+
.**********************************************************************
.*                         CLR0000                                    *
.*                     Clear Variables                                *
.**********************************************************************
CLR0000   MOVE      ZERO,PAGENO     * page counter
          MOVE      SP5,SAVSTRT
          MOVE      "60",CLNO        * line counter
          MOVE      SP30,CLNDESC
          MOVE      SP10,SAVCTYP
          MOVE      SP10,SAVCLIN
          MOVE      SP30,OCTDESC
          MOVE      SP10,PFDATE
          MOVE      SP10,SAVSTRT
.
CLR9999   RETURN
+
.**********************************************************************
.*                         SCR0000                                    *
.*                      Main Option Screen                            *
.**********************************************************************
SCR0000   MOVE      ZERO,EXIT
          HLINE     *G37,2,52,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Daily Booking Schedule 2" 
.
SCR1000   DISPLAY   *P1:7,*EL,"Option : "
          KEYIN     *P10:7,*V2LON,OPTION
.
          BRANCH    OPTION,SCR9999
.
          COMPARE   ZERO,OPTION
          GOTO      SCR9000 IF EQUAL
          BEEP
          GOTO      SCR1000
.
SCR9000   MOVE      ONE,EXIT
.
SCR9999   RETURN
+
.**********************************************************************
.*                         DOPT0000                                   *
.*                      Which Option to Display                       *
.**********************************************************************
DOPT0000  DISPLAY   *P52:2,*V2LON,"- Daily Booking Schedule 2 "
DOPT9999  RETURN
+
.**********************************************************************
.*                         STR0000                                    *
.*                     Display Screen                                 *
.**********************************************************************
STR0000   DISPLAY   *P1:9,*EF,"Clinic Id   :":
                    *P1:11,"Keyin Date  :" 
.
          COMPARE   ONE,OTCNDBTM
          GOTO      STR9999 IF NOT EQUAL
.
          DISPLAY   *P1:13,"Keyin Time  :"
.
STR9999   RETURN
+
.**********************************************************************
.*                         CLN0000                                    *
.*                       Keyin Clinic                                 *
.**********************************************************************
CLN0000   MOVE      ZERO,EXIT
.         KEYIN     *P15:9,*EL,*V2LON,CLINIC
.
          MOVE      NINE,EVERT
          MOVE      TEN5,ECOL
          MOVE      ZERO,CKYIMAND
          CALL      KYOUTCLI
CLN0500   BRANCH    EXIT,CLN1000,CLN9000,CLN1000
.
CLN1000   PACK      CLINIC,OCACLIN,SP70
.
          MATCH     SP70,CLINIC
          IF        @EQUAL
            MOVE      SP70,CLNDESC
            MOVE      "All Clinics",CLNDESC
            GOTO      CLN1700
          ENDIF
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,SITE,CLINIC,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     SITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     CLINIC,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          BRANCH    OVRCD,CLN2000
.
          MATCH     SP6,OCADOCT               * if no doctor, then get clinic
          GOTO      CLN1500 IF EQUAL
.
          PACK      KEY6,OCADOCT              * if using doctor, get description
          CALL      RDDOCT1
          BRANCH    OVRCD,CLN9999
.
          CALL      FDOC0000                  * call format doctor name routine
          MOVE      OCADESC,CLNDESC
          GOTO      CLN1700
.
CLN1500   MOVE      OCADESC,CLNDESC           * get clinic description

CLN1700   DISPLAY   *P30:9,CLNDESC
          MOVE      ZERO,EXIT
          GOTO      CLN9999
.
CLN2000   DISPLAY   *P30:9,*B,*V2LON,"** Clinic Does Not Exist **",*W;
          GOTO      CLN0000
.
CLN9000   MOVE      ONE,EXIT
.
CLN9999   RETURN
+
.**********************************************************************
.*                         DAT0000                                    *
.*                       Keyin Date                                   *
.**********************************************************************
.
DAT0000   UNPACK    SP6,CYEAR,CMON,CDAY  * set up defaults
          MOVE      "15",CCOL            * column for keyin
          MOVE      "11",CVERT           * row for keyin
          MOVE      "30",ECOL            * column for error
          MOVE      "11",EVERT           * row for error
          MOVE      ZERO,CHIGHLT         * 0 = highlight 1 = no highlight
          MOVE      CCC,CCENT
          MOVE      ONE,CDEFDTE          * 0 = check all fields for default
.                                        * 1 = check day only for default
          CALL      KEYDATE
          BRANCH    OVRCD,DAT9000        * no date keyed in
.
          MOVE      CDAY,FSTDDD
          MOVE      CMON,FSTMON
          MOVE      CYEAR,FSTYRR
          MOVE      CCENT,FSTCNT
.
          PACK      STARTD WITH FSTCNT,FSTYRR,FSTMON,FSTDDD
          PACK      PFDATE WITH FSTDDD,SLASH,FSTMON,SLASH,FSTCNT,FSTYRR
          REP       " 0",PFDATE

          REP       " 0",STARTD
          RESET     STARTD
.
          MATCH     SP70,CLINIC             * All clinics
          GOTO      DAT8000 IF EQUAL
.
          PACK      KEY20,SITE,CLINIC,STARTD
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     SITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     CLINIC,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
          MOVE      OCADESC,CLNDESC     * Save the clinic description
          DISPLAY   *P30:9,CLNDESC
.
DAT8000   MOVE      ZERO,EXIT
          GOTO      DAT9999
.
DAT9000   MOVE      ONE,EXIT
DAT9999   RETURN
+
.**********************************************************************
.*                         TIM0000                                    *
.*                       Keyin Time                                   *
.**********************************************************************
TIM0000   MOVE      SP5,STARTT
          COMPARE   ONE,OTCNDBTM
          GOTO      TIM9999 IF NOT EQUAL
.
.         Set up variables for the keyin of the time.
.
          UNPACK    STARTT,CHOUR,ANS,CMIN * Default values for keyin (ie. blank)
          MOVE      "13",CVERT            * Line to keyin the date
          MOVE      "15",CCOL             * Column to keyin the date
          MOVE      "13",EVERT            * Line for error messages
          MOVE      "30",ECOL             * Column for error messages
          MOVE      ONE,CCANLDTE          * can cancel default
.
          CALL      KEYTIME               * Key in the time
          BRANCH    CQUEST,TIM0000        * ? option selected.
          BRANCH    OVRCD,TIM8000         * No time input
.
.         We have a valid time
.
          PACK      STARTT,IHOUR,COLON,IMIN
          REP       " 0",STARTT          * Set up the selected time
.
TIM8000   MOVE      ZERO,EXIT            * Returning with a time
          GOTO      TIM9999              * Return with this time
.
TIM9000   MOVE      ONE,EXIT             * Time not required - set flag 
.
TIM9999   RETURN
+
.**********************************************************************
.*                         EMP0000                                    *
.* Keyin print empty slots if printing a single clinic                *
.**********************************************************************
EMP0000   MOVE       ZERO,EXIT
          MOVE       ZERO,PRTEMPTY          * Don't print empty slots
.
          MATCH      SP70,CLINIC
          GOTO       EMP9999 IF EQUAL
.
          MOVE       "N",ANS
          KEYIN      *P1:15,*EL,"Print Empty Slots : ",*V2LON,*RV,ANS;
          RESET      ANS
          REP        UPPLOW,ANS
.
          CMATCH     "Y",ANS
          GOTO       EMP9999 IF NOT EQUAL
.
          MOVE       ONE,PRTEMPTY           * Yes print empty slots
.
EMP9999   RETURN
+
.**********************************************************************
.*                         CHK0000                                    *
.*                   Check if ok to continue                          *
.**********************************************************************
CHK0000   MOVE      ZERO,EXIT
          KEYIN     *P1:24,*EL,"Ok to continue (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
.
          CMATCH    "N",ANS
          GOTO      CHK1000 IF EQUAL
.
          CMATCH    "Y",ANS
          GOTO      CHK2000 IF EQUAL
          BEEP
          GOTO      CHK0000
.
CHK1000   DISPLAY   *P29:11,*EL,*P29:13,*EL,*P29:14,*EL,*P1:16,*EF
          MOVE      ONE,EXIT
          RETURN         
.
CHK2000   MOVE      ZERO,NOPRINT
          RETURN         
.
+
.**************************************************************************
.*                              FDOC0000                                  *
.*                      Format doctors name                               *
.**************************************************************************
FDOC0000  MOVE      DTITL,DOCTNAM             * move doctors title to DOCTNAM
          ENDSET    DOCTNAM
.
.------ Move to the end of doctors title ------
.
FDOC1000  CMATCH    SP1,DOCTNAM
          GOTO      FDOC2000 IF NOT EQUAL
          BUMP      DOCTNAM,-1
          GOTO      FDOC9000 IF EOS
          GOTO      FDOC1000
.
.------ Append a space and then the doctors given name to DOCTNAM ------
.
FDOC2000  APPEND    SP1,DOCTNAM
          APPEND    DGNAME,DOCTNAM
          ENDSET    DOCTNAM
.
.------ Move to the end of doctors given name ------
.
FDOC2200  CMATCH    SP1,DOCTNAM
          GOTO      FDOC3000 IF NOT EQUAL
          BUMP      DOCTNAM,-1
          GOTO      FDOC2200
.
.------ Append a space and then the doctors surname to DOCTNAM ------
.
FDOC3000  APPEND    SP1,DOCTNAM
          APPEND    DSNAME,DOCTNAM
          ENDSET    DOCTNAM
.
.------ Move to the end of doctors surname ------
.
FDOC3200  CMATCH    SP1,DOCTNAM
          GOTO      FDOC9000 IF NOT EQUAL
          BUMP      DOCTNAM,-1
          GOTO      FDOC3200
.
.------ Reset the formatted doctors name ------
.
FDOC9000  RESET     DOCTNAM
          MOVE      DOCTNAM,OCADESC
.
FDOC9999  RETURN
+
.**********************************************************************
.*                         PRC0000                                    *
.*                      Process Data                                  *
.**********************************************************************
.
PRC0000   DISPLAY    *P1:24,*EL,*P10:24,"U/R Number : ",*P33:24,"Slot : "
.
          MOVE       STARTD,FROMDATE        * date with century
.
          PACK       KEY25,FROMDATE,STARTT,SP70
          CALL       RDSESA2
          IF         OVRCD<>1
            GOTO       PRC1100
          ENDIF
.
          CALL       RDSSESA2
PRC1000   CALL       RDKSESA2
          BRANCH     OVRCD,PRC9999
.
PRC1100   MATCH      OSEDATE,FROMDATE       * check session date
          GOTO       PRC9999 IF NOT EQUAL
.
          MATCH      SP70,STARTT
          IF         !@EQUAL
            MATCH      OSESTRT,STARTT       * check session start time
            GOTO       PRC9999 IF NOT EQUAL
          ENDIF
.
          MATCH      OSESITE,SITE           * check session site
          GOTO       PRC1000 IF NOT EQUAL
.
          MATCH      SP70,CLINIC
          IF         !@EQUAL
            MATCH      OSECLIN,CLINIC       * check clinic
            GOTO       PRC1000 IF NOT EQUAL
          ENDIF
.
          PACK       KEY28,OSESITE,OSECLIN,OSEDATE,OSESTRT,SP20
          CALL       RDSBOKA1
PRC2000   CALL       RDKBOKA1
          BRANCH     OVRCD,PRC9999
.
          MATCH      OBASITE,OSESITE        * same site as session
          GOTO       PRC1000 IF NOT EQUAL
.
          MATCH      OBACLIN,OSECLIN        * same clinic as session
          GOTO       PRC1000 IF NOT EQUAL
.
          MATCH      OBADATE,OSEDATE        * same date as session
          GOTO       PRC1000 IF NOT EQUAL
.
          MATCH      OBASTRT,OSESTRT        * same start time as session 
          GOTO       PRC1000 IF NOT EQUAL
.
          MOVE      SP70,CODEDESC
          PACK      KEY5,ANSC,ANSV,OBAVISIT,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            MATCH     ANSU,TCINDC6            * Get slot comment if unavailable
            IF        @EQUAL
              MOVE      TDESC,CODEDESC        * print instead of name
              PACK      KEY28,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT,SP70
              CALL      RDOTBOX1
              IF        OVRCD=0
                MATCH     SP70,OTBXCOMM
                IF        !@EQUAL
                  MOVE      OTBXCOMM,CODEDESC    * print instead of name
                ENDIF
              ENDIF
              GOTO      PRC4000               * don't skip unavailable slot
            ENDIF
          ENDIF
.
          MATCH      SP70,CLINIC
          IF         @EQUAL
            MATCH     "       0",DOBAOUTN   * skip empty slots if all clinics
            GOTO       PRC2000 IF EQUAL
          ELSE
            IF         PRTEMPTY=0
              MATCH     "       0",DOBAOUTN * skip empty slots if print empty
              GOTO       PRC2000 IF EQUAL   * slots is no
            ENDIF
          ENDIF
.
PRC4000   MATCH      OBACLIN,SAVCLIN        * clinic id changed
          GOTO       PRC4500 IF NOT EQUAL 
.
          MATCH      OBACTYP,SAVCTYP        * clinic type changed
          GOTO       PRC4500 IF NOT EQUAL 
.
          MATCH      OBASTRT,SAVSTRT        * start time changed
          GOTO       PRC4500 IF NOT EQUAL 
.
          GOTO       PRC5000
.
PRC4500   MOVE       OBACLIN,SAVCLIN        * move clinic id to save var
          MOVE       OBACTYP,SAVCTYP        * move clinic type to save var
          MOVE       OBASTRT,SAVSTRT        * move start time to save var
.
          CALL       CBRK0000               * reprint clinic and date
.
PRC5000   COMPARE    THREE,OBASTAT          * see if suppress extended slots
          GOTO       PRC6000 IF NOT EQUAL   * not an extended slot
.
          COMPARE    ONE,OTCNSUPX
          GOTO       PRC6000 IF NOT EQUAL   * not checking on suppression
.
          PACK       KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
          CALL       RDMASA1
          BRANCH     OVRCD,PRC2000          * no master file record
.
          MATCH      ANSY,OTMASUPX
          GOTO       PRC2000 IF EQUAL       * suppress extended slot
.
PRC6000   DISPLAY    *P23:24,*V2LON,OBAURNO,*P40:24,OBASLOT;
          CALL       PRT0000
          GOTO       PRC2000
.
PRC9999   RETURN
+
.**********************************************************************
.*                         END0000                                    *
.*                      End of Report                                 *
.**********************************************************************
.
END0000   COMPARE   ZERO,PAGENO
          GOTO      END2000 IF NOT EQUAL
.
.         No data selected
.
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** No Record Selected **",*W;
          CALL      HEAD0000
          PRINT     *N,*N,"  ***  END OF REPORT  ***"
          GOTO      END9999
.
END2000   PRINT     *N,*N,"  ***  END OF REPORT  ***"
          DISPLAY   *P1:23,*EL,*P1:24,*EL,*V2LON:
                    "** Report Generation Completed ** ";
.
.          CALL      HITENTER
.
          GOTO      END9999
.
END9999   DISPLAY   *P1:14,*EF
          RETURN
+
.**********************************************************************
.*                         PRT0000                                    *
.*                        Print Data                                  *
.**********************************************************************
.
PRT0000   COMPARE   "54",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PACK      DIM11,SP20,SP20
          PACK      DIM9,SP20,SP20
          PACK      PNAME,SP20,SP20        * patients name
          PACK      ADDRESS,SP20,SP20      * patients full address
          MOVE      SP20,ADDR1             * patients address
          MOVE      SP20,ADDR2             * patients address
          MOVE      SP20,TELHOM            * patients (H) phone
          MOVE      SP20,TELBUS            * patients (B) phone
          MOVE      SP20,TELHOM20          * patients (H) phone
          MOVE      SP20,TELBUS20          * patients (B) phone
          MOVE      SP10,BDATE             * patients birth date
          MOVE      SP8,PTMXCEXP           * expiry date
          MOVE      SP3,PTMXCXMP           * exemption code
          MOVE      SP20,PTMXCARD          * card number
.
          MOVE      SP70,COMMENT1          * Clearing Comments Lines
          MOVE      SP70,COMMENT2
          MOVE      SP70,COMMENT3          * Clearing Comments Lines
          MOVE      SP70,COMMENT4
.
          MOVE      SP20,PSNAME
          MOVE      SP1,CASENOTE           * clear case notes output variable
.
          MOVE      OBAURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PRT0500          * if not on master, read patpram.
          GOTO      PRT0600
.
.------- read patient pre admissions file, if not in patient master -------
.
PRT0500   MATCH     "       0",DOBAOUTN                                *I-57896
          GOTO      PRT3500 IF EQUAL                                   *I-57896
.
          PACK      KEY8,OBAOUTNO          * read with outpatient no.
          CALL      RDPRAM1
          BRANCH    OVRCD,PRT3500
.
.         get case notes symbol if PCASE is "1"
.
PRT0600   LOAD      CASENOTE,PCASE,CCNOTES
          UNPACK    PTMXCEXP,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,EXP10
.
.         Pack patient name
.
PRT0700   ENDSET    PGNAME
.
PRT1000   CMATCH    SP1,PGNAME
          GOTO      PRT1500 IF NOT EQUAL
          BUMP      PGNAME BY -1
          GOTO      PRT1000 IF NOT EOS
.
PRT1500   LENSET    PGNAME
          RESET     PGNAME
.
          ENDSET    PSNAME
PRT2000   CMATCH    SP1,PSNAME
          GOTO      PRT2500 IF NOT EQUAL
          BUMP      PSNAME BY -1
          GOTO      PRT2000 IF NOT EOS
.
PRT2500   LENSET    PSNAME
          RESET     PSNAME
.
          PACK      PNAME,PSNAME,SP1,PGNAME
.
          CALL      ADDR0000           * get address
.
          PACK      TELHOM,HOM,PTELEP
          PACK      TELBUS,BUS,PTELEB
          PACK      TELHOM20,HOM,PTELEP
          PACK      TELBUS20,BUS,PTELEB
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,BDATE
.
PRT3000   MOVE      ZERO,F1
          MOVE      OBAOUTNO,KEY8
          CALL      VSCMNT00
.
PRT3500   OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND10;*249,PTCNDAUR
          PACK      DIM9,OBAURNO,SP70
          MATCH     SP70,OBAURNO
          IF        !@EQUAL    
            MATCH     "1",PTCNDAUR
            IF        @EQUAL
              PACK      KEY20,SP70
              PROC      GETALIDS
              MATCH     SP70,KEY20
              IF        !@EQUAL
                UNPACK    KEY20,DIM11,DIM9
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP70,CODEDESC
          IF        !@EQUAL
            MOVE      CODEDESC,PNAME        * for unavailable slots
          ENDIF
.
          MOVE      SP70,DIM3
          PACK      KEY8,OBAOUTNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,PRT3510
.
          MATCH     "1",PMVXCONF
          IF        @EQUAL
            MOVE      "Yes",DIM3
          ELSE
            MATCH     "0",PMVXCONF
            IF        @EQUAL
              MOVE      "No ",DIM3
            ENDIF
          ENDIF
.
PRT3510   PRINT     *1,"|",OBASLOT,*6,"|",OBATIME:
                    *12,"| ",OBAVISIT,*18,"|",DIM9,*28,"|":
                    BDATE,*39,"|",PNAME,*65,"|",ADDR1:
                    *86,"|",TELHOM20,*115,"|    ",DIM3:
                    *132,"|"
.
          MOVE      ZERO,F1
          MATCH     ZEROVISN,OBAOUTNO
          IF        !@EQUAL
            MOVE      ONE,F1
          ENDIF
.
          IF        OTCNXDBS = ONE & F1 = 1
            PRINT     *1,"|Exp: ",EXP10," |EX : ",PTMXCXMP," | Card No : ":
                      PTMXCARD,*65,"|",ADDR2:
                      *86,"|",TELBUS20,*115,"|    ":
                      *132,"|"
          ELSE
            PRINT     *1,"|",*6,"|",*12,"|",*18,"|",*28,"|":
                    *39,"|",*65,"|",ADDR2:
                      *86,"|",TELBUS20,*115,"|    ":
                  *132,"|"
          ENDIF
.
          STRIP     COMMENT1
          MOVELPTR  COMMENT1,LENGTH
          SFORMAT   VAR,LENGTH
          MOVE      COMMENT1,VAR
          PRINT     *1,"|",VAR,SP1,COMMENT2,*132,"|"
          STRIP     COMMENT3
          MOVELPTR  COMMENT3,LENGTH
          SFORMAT   VAR,LENGTH
          MOVE      COMMENT3,VAR
          PRINT     *1,"|",VAR,SP1,COMMENT4,*132,"|"
          ADD       "4",CLNO
          CALL      UND132
.
PRT9999   RETURN
.----------------------------------------------------------------------
. Retrieve the 1st 4 comment text lines of the latest active visit
. comment details
.----------------------------------------------------------------------
VSCMNT00  PACK      KEY17,KEY8,VSCMTTYP,Z70      * Key for start of comment
          CALL      RSVSMDT1
VSCMNT20  CALL      RPVSMDT1
          BRANCH    OVRCD,VSCMNT99
.
          MATCH     KEY8,VSMDVISN
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     VSCMTTYP,VSMDTYPE
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     "1",VSMDDELT          * is comment deleted
          GOTO      VSCMNT20 IF EQUAL
.
. Retrieve the comment text line details
.
          MOVE      ZERO,F1
          PACK      KEY20,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
          CALL      RSVSMTX1
VSCMNT60  CALL      RKVSMTX1
          BRANCH    OVRCD,VSCMNT99
.
          MATCH     VSMDVISN,VSMTVISN
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     VSMDTYPE,VSMTTYPE
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     VSMDNOTE,VSMTNOTE
          GOTO      VSCMNT99 IF NOT EQUAL
.
          ADD       ONE,F1
          IF        F1 = ONE
            MOVE      VSMTCMNT,COMMENT1    * get 1st comment line
            GOTO      VSCMNT60
          ENDIF
.
          IF        F1 = TWO
            MOVE      VSMTCMNT,COMMENT2    * get 2nd comment line
            GOTO      VSCMNT60
          ENDIF
.
          IF        F1 = THREE
            MOVE      VSMTCMNT,COMMENT3    * get 3rd comment line
            GOTO      VSCMNT60
          ENDIF
.
          IF        F1 = FOUR
            MOVE      VSMTCMNT,COMMENT4    * get 4th comment line
            GOTO      VSCMNT99
          ENDIF
.
          GOTO      VSCMNT99
.
VSCMNT99  RETURN
.**********************************************************************
.*                             ADDR0000                               *
.*                     Pack Patients Address                          *
.**********************************************************************
.
ADDR0000
          PACK      ADDRESS,SP70
          CLEAR     ADDRESS
.
          MATCH     SP70,PADD1
          IF        !@EQUAL
            STRIP     PADD1
            APPEND    PADD1,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD2
          IF        !@EQUAL
            STRIP     PADD2
            APPEND    PADD2,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PSUBRB
          IF        !@EQUAL
            STRIP     PSUBRB
            APPEND    PSUBRB,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD4
          IF        !@EQUAL
            STRIP     PADD4
            APPEND    PADD4,ADDRESS
            APPEND    " ",ADDRESS
          ENDIF
.
          MATCH     SP70,PPOST
          IF        !@EQUAL
            SQUEEZE   PPOST
            APPEND    PPOST,ADDRESS
          ENDIF
.
          RESET     ADDRESS
          CALL      BKLIN000
.
          RETURN
+
.********************************************************************
. BKLIN - Break up ADDRESS into two neat halves        Called by lots
.         Requires - ADDRESS   (original line)
.         Returns  - ADDR1     (line 1)
.                    ADDR2     (line 2)
.********************************************************************
BKLIN000  RESET     ADDRESS,21
          CMATCH    SP70,ADDRESS
          WHILE     !@EQUAL&!@EOS
            BUMP      ADDRESS,-1
            IF        !@EOS
              CMATCH    SP70,ADDRESS
            ENDIF
          DO
.
          IF        @EOS
            SETLPTR   ADDRESS
            RESET     ADDRESS
            UNPACK    ADDRESS,ADDR1,ADDR2
          ELSE
            LENSET    ADDRESS
            RESET     ADDRESS
            MOVE      ADDRESS,ADDR1
            PACK      ADDR1,ADDR1,SP70
.
            ENDSET    ADDRESS
            SETLPTR   ADDRESS
            BUMP      ADDRESS
            PACK      ADDR2,ADDRESS,SP70
          ENDIF
.
BKLIN999  RETURN
+
.**********************************************************************
.*                         HEAD0000                                   *
.*                      Print Headings                                *
.**********************************************************************
.
HEAD0000  ADD       ONE,PAGENO
          MOVE      ONE,CNOUNDLN 
          CALL      IBACLOCK
          CALL      HEAD132
.
          PRINT     *40,"Clinic : ",CLINIC,*65,CLNDESC,*N:
                    *40,"Date   : ",PFDATE,*65,"Start Time : ",STARTT;
.
          MATCH      SP70,CLINIC
          IF         !@EQUAL
            IF        PRTEMPTY=1
              PRINT     *90,"Print Empty Slots : Yes"
            ELSE
              PRINT     *90,"Print Empty Slots : No"
            ENDIF
          ELSE
            PRINT     *90,SP1
          ENDIF
.
          ADD       TWO,CLNO
          CALL      UND132
.
HEAD9999  RETURN
+
.**********************************************************************
.*                         CBRK0000                                   *
.*                    control break for change of session time        *
.**********************************************************************
CBRK0000  
.
.         Get the clinic type description
.
          PACK     KEY12,IDDD,OBACTYP,SP70
          CALL     RDCTYA1
          IF       OVRCD=1
            MOVE     SP70,OCTCTYP
            MOVE     SP70,OCTDESC
          ENDIF
.
.         Get the clinic description
.
          PACK      KEY20,SITE,OBACLIN,OBADATE,SP70
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     SITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OBACLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      SP70,OCACLIN
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
.
          MATCH     SP6,OCADOCT               * if no doctor, then get clinic
          GOTO      CBRK1000 IF EQUAL
.
          PACK      KEY6,OCADOCT              * if using doctor, get description
          CALL      RDDOCT1
          BRANCH    OVRCD,CBRK1000
.
          CALL      FDOC0000                  * call format doctor name routine
.
CBRK1000  COMPARE   "48",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *45,"Clinic : ",OBACLIN,*65,OCADESC,*N:
                    *45,"Type   : ",OBACTYP,*65,OCTDESC,*N:
                    *45,"Date   : ",PFDATE,*65,"Start Time : ",SAVSTRT
.
          ADD      THREE,CLNO                      * increment line count
.
          CALL     UND132                         * print line
.
          PRINT     *1,"|Slot",*6,"|Time",*12,"|Visit":
                    *18,"| U/R No.",*28,"| D.O.B.":
                    *39,"|Patients Name",*65,"|Address",*86,"|Phone":
                    *115,"|Confirmed ",*126,"|Proc",*132,"|",*N:
                    *1,"|",*6,"|",*12,"|":
                    *18,"|",*28,"|":
                    *39,"|",*65,"|",*86,"|":
                    *115,"|",*126,"|Qty",*132,"|"
.
          ADD      TWO,CLNO                      * increment line count
          CALL     UND132                         * print line
.
CBRK9999  RETURN
+
.************************************************************************
.*                         NCOP0000                                     *
.*         Get number of copies to print                                *
.*                   called by : ML                                     *
.************************************************************************
NCOP0000  MOVE      ONE,FORM3             * Default to one
          DISPLAY   *P1:24,*EL,"Number of copies to print : ",ONE;
          KEYIN     *P29:24,*V2LON,FORM3
.
          COMPARE   ZERO,FORM3                 * nothing entered
          GOTO      NCOP9000 IF NOT EQUAL
.
          MOVE      ONE,FORM3
          DISPLAY   *P29:24,*V2LON,FORM3;      * display default
          GOTO      NCOP9999
.
NCOP9000  DISPLAY   *P29:24,*V2LON,FORM3;
.
NCOP9999  RETURN
.*****************************************************************************
.*  GETALTID
.*  Procedure to get an alternate ID
.*  Exit=0  No alternate ID
.*       1  Alternate ID's exist
.*****************************************************************************
          DEFPROC   GETALIDS
.
          INC       PMSAIDFD/INC
.
          MOVE      ZERO,EXIT
          PACK      KEY20,SP70
          OPEN      PMSAIDA1,"pmsaidaf"
.
          PACK      KEY31,PURNO,SP70
          CALL      RSPMAID1           * Read alternate id Table
GETALI10  CALL      RKPMAID1
          BRANCH    OVRCD,GETALI99
.
          MATCH     PURNO,PMAIURNO     * Check same UR
          GOTO      GETALI99 IF NOT EQUAL
.
          MATCH     "1",PMAIDISU
          GOTO      GETALI10 IF NOT EQUAL
.
          PACK      KEY20,PMAIALID,SP70
          MOVE      ONE,EXIT
          GOTO      GETALI99
.
          INC       PMSAIDIO/INC
          INC       IBAOVRIO/INC
.
GETALI99  CLOSE     PMSAIDA1
.
          ENDPROC
.**********************************************************************
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
          INC       STD001IO
          INC       KYOUTCLI
          INC       OUTBOAIO/INC
          INC       OUTBOXIO/INC
          INC       OUTCLIIO/INC
          INC       OUTCTYIO/INC
          INC       OUTDSCLN
          INC       OUTMA1IO/INC     * clinic master file
          INC       OUTSESIO/INC
          INC       OUTSITIO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PMSHCPIO/INC            * National HCP file
          INC       PATMA1IO/INC
          INC       PATPR1IO/INC
          INC       PMSVX1IO/INC
...............................................................................

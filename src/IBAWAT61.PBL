.******************************************************************************
.* System         : Waiting Lists                                             *
.* Program        : IBAWAT61.PBL                                              *
.* Name           : NSW HDP Waiting List Extraction                           *
.******************************************************************************
.* Date           : 27/06/1997                                                *
.* Author         : Greg Horvat                                               *
.* Function       : Extract waiting lists details via a date range & load the *
.*                  details into a text file.                                 *
.* Modifications  :                                                           *
.*                                                                            *
.******************************************************************************
.*       V12.00.02 15.05.2025  DA Sarkies     TSK 0955096                     *
.*                  Updated for Alpha-Numeric Visit Number                    *
.*       V11.02.01 10/02/2022  Thanh T       TSK 0889899                      *
.*                  Recompiled as WATTR1FD changes                            *
.******************************************************************************
.*       V11.00.01  28/07/2020  Jill Parkinson Task 0894859                   *
.*                  Increased valid wl proc from <277 to <280                 *
.*       V10.05.01  01/08/2015  Ania P         CAR 261630                     *
.*                  Removed use of PORT for temp file naming                  *
.*       V10.03.03  31/10/2013 Sandra Barcham  CAR 293465                     *
.*                  Allow for more than 9 priotity changes                    *
.*       V10.03.02  09/08/2012 Sandra Barcham  CAR 270947                     *
.*                  Extend indicator procedure codes from 231 - 276           *
.*       V10.01.01  03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*       V10.00.01  04/08/2010  Davin          CAR 226944                     *
.*                  Use confirmed admission date (bkreedat) if it is not blank*
.*                  for planned admission date.                               *
.*                  Use confirmed operation date (opdadate) if it is not blank*
.*                  for planned procedure date.                               *
.*        V9.12.11  22/02/2010  Jill Parkinson CAR 215782                     *
.*                  Added indigenous status                                   *
.*        V9.12.10  30/09/2009  Jill Parkinson CAR 206805                     *
.*                  Changes for patients RFC after end of extract date        *
.*        V9.12.09  17/09/2009  Jill Parkinson CAR 206097                     *
.*                  More mods to calculate NRFC days if currently rfc         *
.*        V9.12.08  17/09/2009  Jill Parkinson CAR 206097                     *
.*                  Mods to calculate NRFC days if currently rfc              *
.*        V9.12.07  26/08/2009  Mike Laming    CAR 203054                     *
.*                  Add checks for URGCAT & LSTSTS at VALD1700                *
.*        V9.12.06  24.07.2009  Jill Habasque  CAR 197190                     *
.*                  Mods to move 1 to LSTSTS if wtsutdat is before end date   *
.*        V9.12.05  17.07.2009  Jill Habasque  CAR 197190                     * 
.*                  Reversed order in GPRM0000                                *
.*        V9.12.04  10.07.2009  Jill Habasque  CAR 201096                     *
.*                  Mods to move COUNT to URGCATCT in GTPC9000                *
.*        V9.12.03  01.07.2009 Jill Habasque  CAR 197190                      *
.*                  Commented out add of one to DYNRFCRE                      *
.*        V9.12.02  18.05.2009 Sandra Barcham CAR 196936                      *
.*                  Fix removal date and date of last listing status          *
.*        V9.12.01  20.04.2009 Sandra Barcham CAR 194858                      *
.*                  Translate VL for V6                                       *
.*                  Blank provisional admission date                          *
.*        V9.11.01  11/03/2009 Jill Habasque  CAR 179959                      *
.*                  Mods for missing V6 fields                                *
.*        V9.10.00  03/02/2009 Mike Laming    CAR 179959                      *
.*                  Ported to V9.10 - see "..v6." for incompatible parts      *
.*                  Still to be defined in v9.10                              *
.*                     WATDATFD/IO   (used in routine GPDC0000)               *
.*                     WTWMCLAI      - added to WATTR1FD                      *
.*                     WTWMDTE5      - added to WATTR1FD                      *
.*                     WTWMDTE6      - added to WATTR1FD                      *
.*                     WTTRDATE      - added to WATCATFD                      *
.*        -----------------------------------------------------------         *
.*        V6.16.03  30/09/2008 Sandra Barcham CAR 167554                      *
.*                  Fix loop of TRREMDTE                                      *
.*        V6.16.02  02/06/2008 Sandra Barcham CAR 167554                      *
.*                  TRREMDTE incorrect due to 2 WR records in watcataf        *
.*        V6.16.01  31/03/2008 Sandra Barcham CAR 165101                      *
.*                  Allowed procedure hdp equivalent 200 to 231               *
.*        V6.15.02  01/08/2007 Sandra Barcham CAR 146284                      *
.*                  Fixed error message  Invalid Current Urgency Category     *
.*        V6.15.01  27/07/2007 Sandra Barcham CAR 146284                      *
.*                  Added category SC indicator 1 value of 9                  *
.*        V6.13.04  09/11/2005  Jill Habasque CAR 83230                       *
.*                  Put back the line of code with jjjj (was removed for 60297)*
.*                  Moved packing of BOOKIDEN & ADMISSID to be at GWLD0500    *
.*        V6.13.03  25/10/2005  Mike Laming   CAR 80775                       *
.*                  Correct GOTOs around Removal Date check                   *
.*        V6.13.02  24/10/2005  Mike Laming   CAR 80775                       *
.*                  Correct calculation of DURGCATC at GTPC9700               *
.*        V6.13.01  13/09/2005  Mike Laming   CAR 60297                       *
.*                  1) Change to output BOOKIDEN & ADMISSID at GWLD2000       *
.*                  2) Fix output of "Previous Urgency Cats" from GTPC9000 on.*
.*        V6.12.00  17/05/2004  Mike Laming   CAR 49016                       *
.*                  July 2004 PRS/2 - changed to use sex routine SEXDSC00     *
.*        V6.09.03  05/10/2001  Tonii  SRF 10321 (REBOUND)                    *
.*                  - New fixit written to correct data error in watcataf, no *
.*                    records were being written to it when removing a W/L via*
.*                    IBAOPR40                                                *
.*                  - Double check made on Urgency Count (URGCATCT)           *
.*        V6.09.02  28/06/2001  Steve Downing  SRF 10321                      *
.*                  Added extra validation to conform with WLCOS edit rules   *
.*        V6.09.01  15/06/2001  Steve Downing                                 *
.*                  Added WLDELIND, BOOKIDEN & ADMISSID                       *
.*        V6.09.B01 09/01/2001  Jill Habasque                                 *
.*                  Finally fixed urgency category changes                    *
.*        V6.07.03  04/12/2000  Jill Habasque SRF  6057                       *
.*                  Rebound                                                   *
.*        V6.07.02  09/11/2000  Jill Habasque SRF  6057                       *
.*                  Fixed Urgency category                                    *
.*        V6.07.01  06/09/2000  Steve Downing                                 *
.*                  Mods to include Indeterminate and Unknown sex             *
.*        V6.07.B01 16/03/2000  J Habasque                                    *
.*                  Added check of ENDDTE8 in GTPC0000                        *
.******************************************************************************
.*        V6.06.08  09/09/1999  Jill Habasque                                 *
.*                  Fixed problem with watcataf records where cat TP changes  *
.*                  are made on the same day                                  *
.*        V6.06.07  07/09/1999  Jill Habasque                                 *
.*                  Fixed Delay dates                                         *
.*        V6.06.06  02/09/1999  Jill Habasque SRF 627940                      *
.*                  Changed to use new OPDACDTE - Cancellation Date           *
.*        V6.06.05  01/09/1999  Jill Habasque                                 *
.*                  Fixed Change dates                                        *
.*        V6.06.04  26/08/1999  Jill Habasque                                 *
.*                  Fixed Urgency Category changes                            *
.*        V6.06.03  21/07/1999  Jill Habasque                                 *
.*                  Zero filled fields                                        *
.*        V6.06.02  12/05/1999  Jill Habasque                                 *
.*                  1999 Health Dept changes                                  *
.*        V6.06.01  18/01/1999  Jill Habasque                                 *
.*                  Recompiled for CALCDAYS                                   *
.*        V6.05.11  04/01/1999  Greg Horvat                                   *
.*                  Removed the plan proc/admn date & proc code ind validation*
.*        V6.05.10  10/12/1998  Greg Horvat                                   *
.*                  - Changed the planned/provisional admin date validation   *
.*                  - Changed to get the urgency category & list status from  *
.*                    the category changes file                               *
.*                  - Changed to get the NRFC days to date from the admin or  *
.*                    removal dates if before extract end date                *
.*        V6.05.09  24/11/1998  Nicole Harrington                             *
.*                  Removed CCENTRY                                           *
.*        V6.05.08  20/11/1998  Greg Horvat                                   *
.*                  - Fixed a bug counting the delay counts                   *
.*                  - Changed to extract decline & delay details for all pats *
.*                  - Changed to extract planned/pro dates only when theres   *
.*                    values in the dates from where the data is coming from??*
.*                  - Changed the delay validation                            *
.*                  - Changed to not do the GBKD0000 routine                  *
.*                  - Changed to not get delay info when the cancellation code*
.*                    is set to not delayed                                   *
.*        V6.05.07  12/11/1998  J.Tan                                         *
.*                  If planned/provisional admin date are blank, use WMDATE3  *
.*        V6.05.06  19/10/1998  Greg Horvat                                   *
.*                  - Changed to only get delay info in the extract date range*
.*                  - Changed to extract the planned & provisional admin dates*
.*                    differently.  Got any questions ask Helen Crick         *
.*        V6.05.05  01/10/1998  Greg Horvat                                   *
.*                  - Changed for new 98/99 format                            *
.*                  - Changed to default the reason for removal date correctly*
.*                  - Fixed a problem counting the no of delays               *
.*        V6.05.04  03/09/1998  Greg Horvat                                   *
.*                  Changed to keep looping thru watcataf when the effective  *
.*                  date is in the future                                     *
.*        V6.05.03  13/08/1998  Greg Horvat                                   *
.*                  - Changed to calculate the no of days not ready for care  *
.*                    not just how many times not ready for care              *
.*                  - Changed to only count the no of times delayed in the    *
.*                    current period                                          *
.*        V6.05.02  31/07/1998  Greg Horvat      SRF 627135                   *
.*                  Changed to get the right info for patients if they were   *
.*                  that status during the extract period                     *
.*        V6.05.01  10/07/1998  Greg Horvat      SRF 626448                   *
.*                  Changed to ignore the 1st category change record for each *
.*                  procedure                                                 *
.*        V6.05.B01 10/06/1998  Greg Horvat      SRF 626182                   *
.*                  Changed to ignore discharged patients                     *
.*        V6.04.08  13/05/1998  Greg Horvat                                   *
.*                  - Changed to only extract planned admin date changes when *
.*                    there is a change in the dates                          *
.*                  - Changed to get the delay last date & declined offer of  *
.*                    admin last date whenever the counts are incremented     *
.*        V6.04.07  26/02/1998  Darren Jones                                  *
.*                  Check if any unscheduled patients have been delayed or    *
.*                  declined                                                  *
.*        V6.04.06  24/11/1997  Greg Horvat                                   *
.*                  October 97 changes                                        *
.*        V6.04.05  12/09/1997  Greg Horvat                                   *
.*                  Fixed a problem getting change records                    *
.*        V6.04.04  28/08/1997  Greg Horvat                                   *
.*                  Changed to send a new extract record when a U/R has a     *
.*                  different procedure code                                  *
.*        V6.04.03  26/08/1997  Greg Horvat                                   *
.*                  Changed to send the removal date in the correct format    *
.*        V6.04.02  21/08/1997  Greg Horvat                                   *
.*                  Added validation between files                            *
.*        V6.04.01  12/08/1997  Greg Horvat                                   *
.*                  Changed to default the admin date to the removal reason   *
.*                  date                                                      *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
.
          INC       BOKRC1FD/INC            * Booking file
          INC       IBACONFD/INC
          INC       OPRDEAFD/INC            * Detail file
          INC       PATCODFD/INC            * Codes file
          INC       PATCONFD/INC            * Patient control file
          INC       PATDO1FD/INC            * Doctor file
          INC       PMSHCPFD/INC
          INC       PATDSCFD/INC            * Disch file
          INC       PATMA1FD/INC            * Master file
          INC       PMSPX2FD/INC            * Master extn file
          INC       PATMI1FD/INC            * Admission file
          INC       PMSVX1FD/INC            * Admission file
          INC       WATCATFD/INC            * Code changes file
          INC       WATPROFD/INC            * Procedure file
          INC       WATTR1FD/INC            * Treatment file
          INC       WATHISFD/INC            * History file
          INC       WATSUSFD/INC            * Suspension file
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
. ----- Tempfile variables -----
.
TEMP1     IFILE     SQL, FIXED=351, KEY="U1-8,9-17,18-19"
FILELIN1  INIT      " 351 U1-8,9-17,18-19"
.
.Name     Type    Length  Physical  Start  Description
.-------  ----    ------  --------  -----  -----------
DTMPURNO  DIM       8        8        1    U/R no
TMPROCOD  DIM       9        9        9    Procedure code
TMPROCNT  DIM       2        2        18   Procedure count
DTMPSTAT  DIM       1        1        20   W/L status
TMPADATE  DIM       8        8        21   Admin date (ccyymmdd)
.
.HOSPCOD  DIM       4        4        29   Hospital code
.SEX      DIM       1        1        33   Sex
.DOB      DIM       8        8        34   DOB (ddmmccyy)
.STRETNO  DIM       5        5        42   Street no
.STRETNAM DIM       39       39       47   Street name
.SUBURB   DIM       40       40       86   Suburb
.POSTCOD  DIM       4        4       126   Postcode
.AMONO    DIM       10       10      130   AMO no
.LISTDTE  DIM       8        8       140   List date (ddmmccyy)
.TRLISTDT DIM       8        8       148   Transaction Date of Listing Date
.DISINT   DIM       1        1       156   Disch intention
.PROCODIN DIM       3        3       157   Procedure code indicator
.SPECCOD  DIM       2        2       160   Speciality code
.URGCAT   DIM       1        1       162   Current urgency category
.URGCATDT DIM       8        8       163   Urgency category last date (ddmmccyy)
.PURGCAT1 DIM       1        1       171   Previous urgency category (1st)
.PURGDTE1 DIM       8        8       172   Previous urgency category date (1st)
.PURGCAT2 DIM       1        1       180   Previous urgency category (2nd)
.PURGDTE2 DIM       8        8       181   Previous urgency category date (2nd)
.PURGCAT3 DIM       1        1       189   Previous urgency category (3rd)
.URGCATCT FORM      2        2       190   Urgency category count
.ACCSTS   DIM       1        1       192   Accommodation status
.ACCSTSCT FORM      2        2       193   Accommodation status count
.ACCSTSDT DIM       8        8       195   Accom status last date (ddmmccyy)
.ACCELADM DIM       1        1       203   Accommodation elected admin
.LSTSTS   DIM       1        1       204   List status
.LSTSTSCT FORM      2        2       205   List status count
.LSTSTSDT DIM       8        8       207   List status last date (ddmmccyy)
.DYNRFCRE FORM      4        4       215   Days not ready for care
.PLADDT   DIM       8        8       219   Planned admin date (ddmmccyy)
.PLADDTCT FORM      2        2       227   Planned admin date count
.PLADDTDT DIM       8        8       229   Planned adm date last date (ddmmccyy)
.PRADDT   DIM       8        8       237   Provisional admin date (ddmmccyy)
.NTPDADDT DIM       2        2       245   No of times pat. delayed on planned
.DELSTS   DIM       1        1       247   Delay status
.DELSTSCT FORM      2        2       248   Delay count (current delays only)
.THTDELCT FORM      2        2       250   Theatre delay count
.BEDDELCT FORM      2        2       252   Bed delay count
.DOCDELCT FORM      2        2       254   Doctor delay count
.NSPDELCT FORM      2        2       256   Not specified delay count
.DELLSTDT DIM       8        8       258   Delay last date (ddmmccyy)
.REVRES   DIM       1        1       266   Removal reason
.REVRESDT DIM       8        8       267   Removal reason date (ddmmccyy)
.TRREMDTE DIM       8        8       275   Transaction date for Date of Removal
.PLPRDT   DIM       8        8       283   Planned procedure date (ddmmccyy)
.PDOOAD   DIM       1        1       291   Patient declined offer of admin
.PDOOADCT FORM      2        2       292   Patient declined offer of admin count
.PDOOADDT DIM       8        8       294   Patient declined offer of admin last
.                                          date (ddmmccyy)
.DEFRCT   FORM      2        2       302   Deferral count
.WLDELIND DIM       6        6       304   Wait List Record Deletion Indicator
.BOOKIDEN DIM       20       20      310   Booking Identifier
.ADMISSID DIM       20       20      330   Admission Identifier
.INDIGSTA DIM       1        1       350   Indigenous Status     
.End of Record                       351
.
.Extract file layout
.-------------------
TEMP2     FILE      ASCII, FIXED=337
TEMP3     FILE      ASCII, FIXED=338
.
.Header Record
.
.Name     Type    Length    Start  Description
.-------  ----    ------    -----  -----------
RECID     DIM       4         1    Record id
EXTDTE    DIM       8         5    Extract date (ddmmccyy)
DRECCNT   DIM       5         13   Record no
.SP253    INIT      273       18   253 spaces
.End of Record               337
.
.Detail Record
.
.Name     Type    Length    Start  Description
.-------  ----    ------    -----  -----------
.DRECCNT  DIM       5         1    Record count
HOSPCOD   DIM       4         6    Hospital code
DMURNO    DIM       10        10   U/R no
SEX       DIM       1         20   Sex
DOB       DIM       8         21   DOB (ddmmccyy)
STRETNO   DIM       5         29   Street no
STRETNAM  DIM       39        34   Street name
SUBURB    DIM       40        73   Suburb
POSTCOD   DIM       4        113   Postcode
AMONO     DIM       10       117   AMO no
LISTDTE   DIM       8        127   List date (ddmmccyy)
TRLISTDT  DIM       8        135   Transaction Date of Listing Date
DISINT    DIM       1        143   Disch intention
PROCODIN  DIM       3        144   Procedure code indicator
SPECCOD   DIM       2        147   Speciality code
URGCAT    DIM       1        149   Current urgency category
URGCATDT  DIM       8        150   Urgency category last date (ddmmccyy)
PURGCAT1  DIM       1        158   Previous urgency category (1st)
PURGDTE1  DIM       8        159   Previous urgency category date (1st)
PURGCAT2  DIM       1        167   Previous urgency category (2nd)
PURGDTE2  DIM       8        168   Previous urgency category date (2nd)
PURGCAT3  DIM       1        176   Previous urgency category (3rd)
DURGCATC  DIM       2        177   Urgency category count
ACCSTS    DIM       1        179   Accommodation status
DACCSTSC  DIM       2        180   Accommodation status count
ACCSTSDT  DIM       8        182   Accommodation status last date (ddmmccyy)
ACCELADM  DIM       1        190   Accommodation elected admin
LSTSTS    DIM       1        191   List status
DLSTSTSC  DIM       2        192   List status count
LSTSTSDT  DIM       8        194   List status last date (ddmmccyy)
DDYNRFCR  DIM       4        202   Days not ready for care
PLADDT    DIM       8        206   Planned admin date (ddmmccyy)
DPLADDTC  DIM       2        214   Planned admin date count
PLADDTDT  DIM       8        216   Planned admin date last date (ddmmccyy)
PRADDT    DIM       8        224   Provisional admin date (ddmmccyy)
NTPDADDT  DIM       2        232   No of times patient delayed on Planned Adm
DELSTS    DIM       1        234   Delay status
DDELSTSC  DIM       2        235   Delay count (current delays only)
DTHTDELC  DIM       2        237   Theatre delay count (Historical)
DBEDDELC  DIM       2        239   Bed delay count (Historical)
DDOCDELC  DIM       2        241   Doctor delay count (Historical)
DNSPDELC  DIM       2        243   Not specified delay count (Historical)
DELLSTDT  DIM       8        245   Delay last date (ddmmccyy)  (Historical)
REVRES    DIM       1        253   Removal reason (Historical)
REVRESDT  DIM       8        254   Removal reason date (ddmmccyy)
TRREMDTE  DIM       8        262   Transaction Date for Date of Removal
PLPRDT    DIM       8        270   Planned procedure date (ddmmccyy)
PDOOAD    DIM       1        278   Patient declined offer of admin
DPDOOADC  DIM       2        279   Patient declined offer of admin count
PDOOADDT  DIM       8        281   Patient dec offer of adm last date (ddmmccyy)
DDEFRCT   DIM       2        289   Deferral count
WLDELIND  DIM       6        291   Wait List Record Deletion Indicator
BOOKIDEN  DIM       20       297   Booking Identifier
ADMISSID  DIM       20       317   Admission Identifier
INDIGSTA  DIM       1        337   Indigenous Status
.End of Record                338
.
. ----- Program Variables -----
.
ACCSTSCT  FORM      2
ADDCNT    FORM      5                       * Additions to list count
ADMCNT    FORM      5                       * Admins count
BEDDELCT  FORM      2
.
CATWR     INIT      "WR"
CDYSDAYS  FORM      6                       * For CALCDAYS
CDYSFDTE  DIM       8                       * For CALCDAYS
CDYSTDTE  DIM       8                       * For CALCDAYS
CDYSYEAR  FORM      2                       * For CALCDAYS
CFNAMEB   DIM       20
CMDLINE   DIM       100
CODFROM   DIM       3
CONFADTE  DIM       8
COUNT     FORM      2 
CURDTE8   DIM       8                       * Current date (ccyymmdd)
.
DEFRCT    FORM      2
DELSTSCT  FORM      2
DIM3      DIM       3
DIM4      DIM       4
DIM8      DIM       8
DOCDELCT  FORM      2
DYNRFCRE  FORM      4
DNRFCFLG  FORM      1                       * Days not ready for care flag
DSPCNT    FORM      5                       * Display count
.
ENDDTE8   DIM       8                       * End date (ccyymmdd)
ERRCNT    FORM      5                       * Error count
ERRMSG    DIM       55                      * Error message
.
FILENAME  DIM       8                       * Extract filename
FNTPDADD  FORM      2
FORM3     FORM      3
FROMFLAG  FORM      1
.
KEY8A     DIM       8
LSTSTSCT  FORM      2
NEWFLAG   FORM      1
NSPDELCT  FORM      2
PDOOADCT  FORM      2
PLADDTCT  FORM      2
.
RECCNT    FORM      5                       * Record count
REMCNT    FORM      5                       * Removals count
RFCFLAG   FORM      1
.
SAVINDC3  DIM       1                       * Save 3rd indicator
SAVINDC4  DIM       1                       * Save 4th indicator
STRTDTE8  DIM       8                       * Start date (ccyymmdd)
.
THTDELCT  FORM      2
TMPSTAT   FORM      1                       * W/L status
TMPURNO   FORM      8                       * U/R no
.
URNO      FORM      10                      * U/R no
URGFLAG   FORM      1
URGCATCT  FORM      2
.
. ----- Program Constants -----
.
SP127     INIT      "                                                  ":
                    "                                                  ":
                    "                           "
ZED30     INIT      "~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~"
.
PRGID     INIT      "IBAWAT61"
PRGNAM    INIT      "NSW HDP Waiting List Extraction"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    OPTION,ML2000
          BRANCH    EXIT,ML9999
.
ML2000    CALL      DATE0000                * Keyin the date range
          BRANCH    EXIT,ML1000
.
ML3000    CALL      FILE0000                * Keyin the extract filename
          BRANCH    EXIT,ML2000
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML4000,ML3000,ML1000
.
ML4000    CALL      OPEN0000                * Open the tempfile
          CALL      HEAD0000                * Print report header
.
          DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Loading : ";
          MOVE      ZERO,ADDCNT             * Reset the additions to list count
          MOVE      ZERO,ADMCNT             * Reset the admins count
          MOVE      ZERO,ERRCNT             * Reset the error counter
          MOVE      ZERO,REMCNT             * Reset the removals count
.
          CALL      GTRE0000                * Get treatment patients
          CALL      VALD0000                * Validate the extract data
          MOVE      URGCATCT,DURGCATC
          REP       " 0",DURGCATC
          CALL      WRIT0000                * Write to the extract file
          CALL      TAIL0000                * Print report tail
          CALL      KILL0000                * Delete the tempfile
          CALL      MVEXT000
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                       Initialise Variable & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
          CALL      TFILENAM
.
          DISPLAY   *P56:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SEVENTY9;*115,PTCNDGUP,*116,PTCNDGUA,*239,PTCNDGAD:
                                      *240,PTCNDGDS,*241,PTCNDGTR,*242,PTCNDGRG
.
          DISPLAY   *P64:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
.
          DISPLAY   *P64:24,"oprdetaf";
          OPEN      OPRDETA2,"oprdetaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"watcataf";
          OPEN      WATCATA1,"watcataf"
          OPEN      WATCATA2,"watcataf"
.
          DISPLAY   *P64:24,"watproaf";
          OPEN      WATPROA1,"watproaf"
.
          DISPLAY   *P64:24,"wathisaf";
          OPEN      WATHISA1,"wathisaf"
.
          DISPLAY   *P64:24,"watsusaf";
          OPEN      WATSUSA1,"watsusaf"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
.
          CLOCK     TIME,CTIMEIS
          PACK      CURDTE8,CCC,CYY,CMM,CDD
          REP       " 0",CURDTE8
          READ      CONTROLF,TEN;*54,CFILENO
.
INIT9000  MOVE      ZERO,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Date Range":
                    *P1:7,"Select Option : ";
OPTN1000  KEYIN     *P17:7,*V2LON,*DV,UNDLN2,*P17:7,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  DATE0000              Called by: ML0000   *
.*                            Keyin The Date Range                            *
.******************************************************************************
DATE0000  MOVE      ZERO,NEWFLAG
          MOVE      "21",CCOL
          MOVE      "9",CVERT
          DISPLAY   *P1:CVERT,"Starting Date     : ",*EF;
          ADD       ONE,CVERT
          DISPLAY   *P1:CVERT,"Ending Date       : ";
.
          MOVE      ONE,CCANLDTE            * Blank cancel default
          MOVE      ZERO,CHIGHLT            * Use highlight
.
. ----- Enter starting date -----
.
DATE1000  SUB       ONE,CVERT
DATE2000  MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY     * No defaults
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9500          * Nothing entered
          BRANCH    CQUEST,DATE2000
.
          PACK      STRTDTE8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDTE8
.
          MATCH     STRTDTE8,CURDTE8
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Date must not be in the future.  ";
            CALL      HITENTER
            GOTO      DATE2000
          ENDIF
.
. ----- Enter ending date -----
.
          ADD       ONE,CVERT
DATE3000  UNPACK    STRTDTE8,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE1000          * Nothing entered
          BRANCH    CQUEST,DATE3000
.
          PACK      ENDDTE8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDTE8
.
          MATCH     ENDDTE8,CURDTE8
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Date must not be in the future.  ";
            CALL      HITENTER
            GOTO      DATE3000
          ENDIF
.
          MATCH     STRTDTE8,ENDDTE8
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must be after start date.  ";
            CALL      HITENTER
            GOTO      DATE3000
          ENDIF
.
          MATCH     "20100401",ENDDTE8
          IF        !@LESS
            MOVE      ONE,NEWFLAG
          ENDIF
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.******************************************************************************
.*                                  FILE0000              Called by: ML0000   *
.*                         Keyin The Extract Filename                         *
.******************************************************************************
FILE0000  MOVE      "21",CCOL
          MOVE      "12",CVERT
          DISPLAY   *P1:CVERT,*EF,"Extract File Name : ";
FILE1000  MOVE      SP8,FILENAME
          KEYIN     *PCCOL:CVERT,*EL,*V2LON,*DV,FILENAME:
                    *PCCOL:CVERT,*EL,*RV,FILENAME;
.
          ENDSET    FILENAME
          APPEND    SP8,FILENAME
          RESET     FILENAME
.
          CMATCH    EXITCHAR,FILENAME
          GOTO      FILE9200 IF EQUAL       * Exit character
.
          MATCH     SP8,FILENAME
          GOTO      FILE9100 IF EQUAL       * Exit
.
          STRIP     FILENAME
          SQUEEZE   FILENAME
          MOVE      SP20,CFNAMEB
          CLEAR     CFNAMEB
          APPEND    FILENAME,CFNAMEB
          RESET     CFNAMEB
.
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,*+,FILENAME,".txt";
.
          BRANCH    NEWFLAG,FILE1500
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TEMP2,CFNAMEB
          TRAPCLR   IO
          BRANCH    OVRCD,FILE9000          * File doesnt exist
.
          CLOSE     TEMP2
          GOTO      FILE2000
.
FILE1500  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TEMP3,CFNAMEB
          TRAPCLR   IO
          BRANCH    OVRCD,FILE9000          * File doesnt exist
.
          CLOSE     TEMP3
.
FILE2000  KEYIN     *P1:24,*B,*EL,"An extraction file with this name":
                    " already exists. Overwrite it (",*V2LON,*DV,ANSY:
                    *HOFF,"/",*V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          GOTO      FILE9000 IF EQUAL       * Overwrite file
.
          MATCH     ANSN,ANS
          GOTO      FILE1000 IF EQUAL       * Keyin filename again
.
          BEEP
          GOTO      FILE2000
.
FILE9000  MOVE      ZERO,EXIT
          GOTO      FILE9999
.
FILE9100  MOVE      ONE,EXIT
          GOTO      FILE9999
.
FILE9200  MOVE      TWO,EXIT
FILE9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: ML0000   *
.*                             Print Report Header                 & PRNT0000 *
.******************************************************************************
HEAD0000  CALL      HEAD80                  * Print report header
.
          UNPACK    STRTDTE8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the start date
          PRINT     *20,"Starting Date : ",CPCDATE
          UNPACK    ENDDTE8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the end date
          PRINT     *20,"Ending Date   : ",CPCDATE,*N
.
          CALL      UND80
          PRINT     *1,"| U/R No |Proc Code|Cnt| Error Message",*80,"|"
          CALL      UND80
          ADD       "4",CLNO
.
HEAD9000  MOVE      ZERO,EXIT
HEAD9999  RETURN
+
.******************************************************************************
.*                                  GTRE0000              Called by: ML0000   *
.*                           Get Treatment Patients                           *
.******************************************************************************
GTRE0000  MOVE      ZERO,DSPCNT
          PACK      KEY19,SP20
          CALL      RDSTREA1                * Position on & read a treatment
GTRE1000  CALL      RDKTREA1                  file record
          BRANCH    OVRCD,GTRE9000
.
          ADD       ONE,DSPCNT
          IF        DSPCNT%10=1
            DISPLAY   *P12:24,*V2LON,WMSTAT,SP2,WMURNO;
          ENDIF
.
          CALL      VPTD0000                * Validate patient details
          BRANCH    EXIT,GTRE1000
.
          PACK      KEY19,WMURNO,WMCODE,WMCNT
          CALL      RDTEMP1                 * Read a tempfile read
          IF        OVRCD=1
            CALL      CLRV0000              * Clear tempfile variables
            CALL      GMFD0000              * Get master file data
            BRANCH    EXIT,GTRE1000
          ENDIF
.
          CALL      GPTD0000                * Get patient details
          CALL      GTPC0000                * Get category TP changes
          CALL      GCLC0000                * Get category CL changes
          CALL      GPDC0000                * Get planned date changes
          CALL      DNRC0000                * Get NRFC
.
          MOVE      URGCATCT,DURGCATC
          REP       " 0",DURGCATC
          PACK      KEY19,TMPURNO,TMPROCOD,TMPROCNT
          CALL      RATEMP1                 * Position read a tempfile record
          IF        OVRCD=1
            CALL      WRTEMP1               * Write a tempfile record
          ELSE
            CALL      UPTEMP1               * Update a tempfile record
          ENDIF
          IF        DSPCNT%10=1
            DISPLAY   *P45:24,*V2LON,TMPURNO;
          ENDIF
          GOTO      GTRE1000
.
GTRE9000  MOVE      ZERO,EXIT
GTRE9999  RETURN
+
.******************************************************************************
.*                                  VPTD0000              Called by: GTRE0000 *
.*                          Validate Patient Details                          *
.******************************************************************************
VPTD0000  MOVE      WMURNO,TMPURNO          * U/R no
          MOVE      WMCODE,TMPROCOD         * Procedure code
          MOVE      WMCNT,TMPROCNT          * Procedure count
          MOVE      SP8,TMPADATE            * Clear the admin date
          MOVE      WMSTAT,TMPSTAT          * W/L status
          MOVE      SP8,CONFADTE            * confirmed admission date
.
          IF        WMSTAT=2 | WMSTAT=3 | WMSTAT=4 | WMSTAT=5
            PACK      KEY8,WMBOOK
            CALL      RDBKREC1              * Read a booking file record
            IF        OVRCD=1
              MOVE      "Missing Booking file record",ERRMSG
              CALL      PRNT0000            * Print error message
              GOTO      VPTD9500
            ELSE
              MATCH     SP8,BKREEDAT
              IF        !@EQUAL
                MOVE      BKREEDAT,CONFADTE
              ENDIF
            ENDIF
          ENDIF
.
          IF        WMSTAT=3 | WMSTAT=4 | WMSTAT=5
            PACK      KEY8,WMBOOK
            CALL      RDMISS1               * Read an admin file record
            IF        OVRCD=1
              MOVE      "Missing Admission file record",ERRMSG
              CALL      PRNT0000            * Print error message
              GOTO      VPTD9500
            ENDIF
            MOVE      ADATE,TMPADATE        * Admin date
          ENDIF
.
          IF        WMSTAT=5
            PACK      KEY8,WMBOOK
            CALL      RDDSCH1               * Read a disch file record
            IF        OVRCD=1
              MOVE      "Missing Discharge file record",ERRMSG
              CALL      PRNT0000            * Print error message
              GOTO      VPTD9500
            ENDIF
          ENDIF
.
          MATCH     WMDATE1,ENDDTE8
          GOTO      VPTD9500 IF LESS        * End date < on list date
.
          IF        WMSTAT=3
            COMPARE   ONE,ASTAT
            GOTO      VPTD9500 IF NOT EQUAL * Not pre admitted
          ENDIF
.
          IF        WMSTAT=4
            COMPARE   TWO,ASTAT
            GOTO      VPTD9500 IF NOT EQUAL * Not admitted
          ENDIF
.
          IF        WMSTAT=5
            MATCH     STRTDTE8,ADATE
            GOTO      VPTD9500 IF LESS      * Admin date < start date
.
            COMPARE   THREE,ASTAT
            GOTO      VPTD9500 IF NOT EQUAL * Not discharged
          ENDIF
.
          IF        WMSTAT=6
            MATCH     STRTDTE8,WMDATE4
            GOTO      VPTD9500 IF LESS      * Removal date < start date
          ENDIF
.
VPTD9000  MOVE      ZERO,EXIT
          GOTO      VPTD9999
.
VPTD9500  MOVE      ONE,EXIT
VPTD9999  RETURN
+
.******************************************************************************
.*                                  CLRV0000              Called by: GTRE0000 *
.*                        Clear The Tempfile Variables                        *
.******************************************************************************
CLRV0000  UNPACK    SP127,HOSPCOD,SEX,DOB,STRETNO,STRETNAM
          UNPACK    SP127,SUBURB,POSTCOD,AMONO,LISTDTE,DISINT
          UNPACK    SP127,TRLISTDT,TRREMDTE
          UNPACK    SP127,PROCODIN,SPECCOD,URGCAT,URGCATDT,ACCSTS
          UNPACK    SP10,DACCSTSC 
          UNPACK    SP127,PURGCAT1,PURGDTE1,PURGCAT2,PURGDTE2,PURGCAT3
          UNPACK    SP127,ACCSTSDT,ACCELADM,LSTSTS,LSTSTSDT,PLADDT
          UNPACK    SP127,PLADDTDT,PRADDT,DELLSTDT,REVRES,REVRESDT
          UNPACK    SP127,PLPRDT,PDOOAD,PDOOADDT
          UNPACK    SP127,WLDELIND,BOOKIDEN,ADMISSID,INDIGSTA
.
          MOVE      ZERO,URGCATCT
          MOVE      ZERO,DURGCATC
          MOVE      ZERO,NTPDADDT
          MOVE      ZERO,LSTSTSCT
          MOVE      ZERO,DLSTSTSC
          MOVE      ZERO,DDYNRFCR
          MOVE      ZERO,PLADDTCT
          MOVE      ZERO,DPLADDTC
          MOVE      ZERO,DELSTSCT
          MOVE      ZERO,FNTPDADD
          MOVE      ZERO,BEDDELCT
          MOVE      ZERO,DOCDELCT
          MOVE      ZERO,DDELSTSC
          MOVE      ZERO,DELSTSCT
          MOVE      ZERO,DTHTDELC
          MOVE      ZERO,THTDELCT
          MOVE      ZERO,DBEDDELC
          MOVE      ZERO,DNSPDELC
          MOVE      ZERO,NSPDELCT
          MOVE      ZERO,DPDOOADC
          MOVE      ZERO,DDEFRCT
.
          MOVE      "8",ACCSTS              * Accom status - default to 8
          MOVE      "1",DELSTS              * Delay status - default to 1
.
CLRV9000  MOVE      ZERO,EXIT
CLRV9999  RETURN
+
.******************************************************************************
.*                                  GMFD0000              Called by: GTRE0000 *
.*                            Get Master File Data                            *
.******************************************************************************
GMFD0000  MOVE      WMURNO,TMPURNO
          PACK      KEY8,WMURNO
          CALL      RDMAST1                 * Read a master file record
          BRANCH    OVRCD,GMFD9500
          CALL      RDPMPX21                * Read a master extn file record
          BRANCH    OVRCD,GMFD9500
.
          MOVE      CFILENO,HOSPCOD         * Hospital code
.
.                                           * original code for sex no. has
.                                           * been deleted & replaced by :-
          MOVE      "MFI",DIM3   
          SCAN      PSEX,DIM3
          IF        @EQUAL
            MOVE      PSEX,SEX
            REP       "M1F2I3",SEX          * NSW HDP sexes
          ELSE
            MOVE      SP1,SEX
            MOVE      "Invalid sex type",ERRMSG
            CALL      PRNT0000              * Print error message
          ENDIF
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DOB,CDAY,CMON,CCENT,CYEAR
          REP       " 0",DOB                * DOB
          MOVE      "9",INDIGSTA
.
          MATCH     SP3,PMPXABRG
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSA,PMPXABRG,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      INDIGSTA,THCSCOD,SP70
            ENDIF
          ENDIF
.
          CALL      GSNN0000                * Get street no & name
.
          PACK      SUBURB,PADD2,SP30
          MATCH     SP20,PSUBRB
          IF        !@EQUAL
            PACK      SUBURB,PSUBRB,SP30    * Suburb
          ENDIF
.
          MOVE      PPOST,POSTCOD           * Postcode
.
GMFD9000  MOVE      ZERO,EXIT
          GOTO      GMFD9999
.
GMFD9500  MOVE      ONE,EXIT
GMFD9999  RETURN
+
.******************************************************************************
.*                                  GSNN0000              Called by: GMFD0000 *
.*                            Get Street No & Name                            *
.******************************************************************************
GSNN0000  PACK      KEY25,PADD1,SP30
          RESET     KEY25
          CMATCH    SP1,PADD1
          GOTO      GSNN3000 IF EQUAL       * Blank address 1, no street no
.
          MOVE      KEY25,KEY1
          TYPE      KEY1
          GOTO      GSNN3000 IF NOT EQUAL   * Not numeric, no street no
.
GSNN1000  CMATCH    "/",KEY25
          GOTO      GSNN2000 IF EQUAL       * Slash found, ignore chars to point
.
          BUMP      KEY25
          GOTO      GSNN9000 IF EOS         * End of address, exit
.
          CMATCH    SP1,KEY25
          GOTO      GSNN1000 IF NOT EQUAL   * Address 1 not blank, get next char
.
          BUMP      KEY25
          PACK      STRETNAM,KEY25,SP127    * Street name
.
          BUMP      KEY25,-2
          LENSET    KEY25
          RESET     KEY25
          PACK      STRETNO,KEY25,SP5       * Street no
          GOTO      GSNN9000
.
GSNN2000  BUMP      KEY25
          PACK      KEY25,KEY25,SP30
          GOTO      GSNN1000
.
GSNN3000  PACK      STRETNAM,KEY25,SP127    * Street name
.
GSNN9000  MOVE      ZERO,EXIT
GSNN9999  RETURN
+
.******************************************************************************
.*                                  GPTD0000              Called by: GTRE0000 *
.*                             Get Patient Details                            *
.******************************************************************************
GPTD0000  IF        WMSTAT=4 | WMSTAT=5 | WMSTAT=6
            CALL      GRSD0000              * Get removal status defaults
          ENDIF
.
          CALL      GWLD0000                * Get waiting list data
.
.....          IF        WMSTAT=2 | WMSTAT=3 | WMSTAT=4 | WMSTAT=5
.....            CALL      GBKD0000              * Get booking details
.....          ENDIF
.
          IF        WMSTAT=4 | WMSTAT=5
            CALL      GAFD0000              * Get admin file details
          ENDIF
.
          CALL      GDDS0000                * Get delay/decline stuff
.
          IF        WMSTAT=1
            ADD       ONE,ADDCNT            * Increment the add to list counter
          ENDIF
.
          IF        WMSTAT=4
            MATCH     ADATE,ENDDTE8
            IF        !@LESS
              ADD       ONE,ADMCNT          * Increment the admin counter
              ADD       ONE,REMCNT          * Increment the removal counter
            ENDIF
          ENDIF
.
          IF        WMSTAT=5
            MATCH     STRTDTE8,ADATE
            IF        !@LESS
              MATCH     ADATE,ENDDTE8
              IF        !@LESS
                ADD       ONE,ADMCNT        * Increment the admin counter
                ADD       ONE,REMCNT        * Increment the removal counter
              ENDIF
            ENDIF
          ENDIF
.
          IF        WMSTAT=6
            MATCH     WMDATE4,ENDDTE8
            IF        !@LESS
              ADD       ONE,REMCNT          * Increment the removal counter
            ENDIF
          ENDIF
.
GPTD9000  MOVE      ZERO,EXIT
GPTD9999  RETURN
+
.******************************************************************************
.*                                  GTPC0000              Called by: GTRE0000 *
.*                           Get Category TP Changes                          *
.******************************************************************************
GTPC0000  MOVE      ZERO,RECCNT
          MOVE      ZERO,COUNT
          MOVE      ZERO,URGFLAG
          MOVE      ZERO,DYNRFCRE
          MOVE      ZERO,LSTSTSCT
          MOVE      ZERO,DEFRCT
          MOVE      ZERO,URGCATCT
          PACK      KEY29,WMURNO,WMCODE,WMCNT,ANST,ANSP,SP20
          CALL      RSWTCAT2                * Position on & read code changes
GTPC1000  CALL      RKWTCAT2                  file record
          BRANCH    OVRCD,GTPC9000
.
          MATCH     WMURNO,WTCAURNO
          GOTO      GTPC9000 IF NOT EQUAL   * Different U/R no
.
          MATCH     WMCODE,WTCAPROC
          GOTO      GTPC9000 IF NOT EQUAL   * Different procedure code
.
          COMPARE   WMCNT,WTCAPCNT
          GOTO      GTPC9000 IF NOT EQUAL   * Different procedure count
.
          MATCH     "TP",WTCACATF
          GOTO      GTPC9000 IF NOT EQUAL   * Different category
.
          MATCH     SP8,WTCAEDAT
          GOTO      GTPC1000 IF EQUAL       * Blank effective date
.
          MATCH     WTCAEDAT,ENDDTE8
          GOTO      GTPC9000 IF LESS        * End date < effective date
.
          UNPACK    SP2,TCINDC3,TCINDC4
          MATCH     SP3,WTCACODF
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSP,WTCACODF
            CALL      RDCODE1               * Read a codes file record
          ENDIF
.
          MOVE      TCINDC3,SAVINDC3        * Save the 3rd indicator
.
          UNPACK    SP2,TCINDC3,TCINDC4
          MATCH     SP3,WTCACODT
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSP,WTCACODT
            CALL      RDCODE1               * Read a codes file record
          ENDIF
.
          ADD       ONE,RECCNT
          IF        RECCNT=1
            MATCH     SP3,WTCACODF
            GOTO      GTPC2000 IF EQUAL     * Blank 1st from change code
          ENDIF
.
          MATCH     SAVINDC3,TCINDC3
          IF        !@EQUAL
.           IF        URGFLAG=1
              ADD       ONE,URGCATCT          * Increment the urgency cat count
.           ENDIF
            MATCH     SP8,WTCAEDAT
            IF        !@EQUAL
              UNPACK    WTCAEDAT,CCENT,CYEAR,CMON,CDAY
              PACK      URGCATDT,CDAY,CMON,CCENT,CYEAR
              REP       " 0",URGCATDT       * Urgency category last date
            ENDIF
          ENDIF
.
GTPC2000  MOVE      TCINDC3,URGCAT          * Current urgency catgeory
          MOVE      ONE,URGFLAG
          GOTO      GTPC1000
.
GTPC9000  MOVE      ZERO,EXIT
          MOVE      ZERO,FROMFLAG
          MATCH     SP1,URGCAT
          GOTO      GTPC9999 IF EQUAL
.
          COMPARE   ZERO,URGCATCT           * check for "Previous" Urgencies
          GOTO      GTPC9999 IF EQUAL
.
          MOVE      ZERO,URGCATCT           * reset "Previous" counter *I-60297
          MOVE      ZERO,URGFLAG
          MOVE      URGCAT,SAVINDC3
          MOVE      ONE,COUNT
          PACK      KEY29,WMURNO,WMCODE,WMCNT,SP30
          CALL      RSWTCAT2                * Position on last record        
GTPC9200  CALL      RKWTCAT2                                
          BRANCH    OVRCD,GTPC9500
.
          MATCH     WMURNO,WTCAURNO
          GOTO      GTPC9500 IF NOT EQUAL
.
          MATCH     WTCAPROC,WMCODE
          GOTO      GTPC9500 IF NOT EQUAL
.
          COMPARE   WTCAPCNT,WMCNT
          GOTO      GTPC9500 IF NOT EQUAL
.
          GOTO      GTPC9200
.
.
GTPC9500  MOVE      ZERO,EXIT
.
GTPC9600  CALL      RPWTCAT2                * Read previous change
          BRANCH    OVRCD,GTPC9999
.
          MATCH     WTCAURNO,WMURNO
          GOTO      GTPC9999 IF NOT EQUAL
.
          MATCH     WTCAPROC,WMCODE
          GOTO      GTPC9999 IF NOT EQUAL
.
          COMPARE   WTCAPCNT,WMCNT
          GOTO      GTPC9999 IF NOT EQUAL
.
          MATCH     WTCAEDAT,ENDDTE8                                   *M-60297
          GOTO      GTPC9600 IF LESS        * End date < eff. date     *M-60297
.
          MATCH     "TP",WTCACATF
          GOTO      GTPC9600 IF NOT EQUAL
.
          MATCH     SP3,WTCACODT
          GOTO      GTPC9600 IF EQUAL   
.
          MATCH     SP3,WTCACODF
          IF        @EQUAL
            MOVE      SP8,WTCAEDAT                    *jjjjj            D-60297
            MOVE      ZERO,FROMFLAG
          ELSE
            MOVE      ONE,FROMFLAG
            MOVE      WTCACODF,CODFROM
          ENDIF
.
GTPC9700  PACK      KEY5,ANST,ANSP,WTCACODT
          CALL      RDCODE1                 * Read a codes file record
          MATCH     TCINDC3,SAVINDC3        * (always equal on first pass)
          IF        @EQUAL
            IF        COUNT > 1
              SUB       ONE,COUNT           * over-write last date
            ELSE
              COMPARE   ZERO,URGCATCT
              IF        @EQUAL
                GOTO      GTPC9600
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      COUNT,URGCATCT                                     *I-80775
          UNPACK    WTCAEDAT,CCENT,CYEAR,CMON,CDAY
          PACK      DIM8,CDAY,CMON,CCENT,CYEAR
          STORE     DIM8,COUNT,PURGDTE1,PURGDTE2
          STORE     TCINDC3,COUNT,PURGCAT1,PURGCAT2,PURGCAT3
.
          ADD       ONE,COUNT
          MOVE      TCINDC3,SAVINDC3
.
          GOTO      GTPC9600
.
GTPC9999  IF        FROMFLAG=1
            PACK      KEY5,ANST,ANSP,CODFROM
            CALL      RDCODE1
            MATCH     TCINDC3,SAVINDC3
            IF        !@EQUAL
              IF        COUNT=1
                MOVE      COUNT,URGCATCT
              ENDIF
              STORE     TCINDC3,COUNT,PURGCAT1,PURGCAT2,PURGCAT3
            ENDIF
          ENDIF
          RETURN
+
.******************************************************************************
.*                                  GCLC0000              Called by: GTRE0000 *
.*                           Get Category CL Changes                          *
.******************************************************************************
GCLC0000  MOVE      ZERO,RECCNT
          MOVE      ZERO,ACCSTSCT
          PACK      KEY29,WMURNO,WMCODE,WMCNT,ANSC,ANSL,SP20
          CALL      RSWTCAT2                * Position on & read code changes
GCLC1000  CALL      RKWTCAT2                  file record
          BRANCH    OVRCD,GCLC9000
.
          MATCH     WMURNO,WTCAURNO
          GOTO      GCLC9000 IF NOT EQUAL   * Different U/R no
.
          MATCH     WMCODE,WTCAPROC
          GOTO      GCLC9000 IF NOT EQUAL   * Different procedure code
.
          COMPARE   WMCNT,WTCAPCNT
          GOTO      GCLC9000 IF NOT EQUAL   * Different procedure count
.
          MATCH     "CL",WTCACATF
          GOTO      GCLC9000 IF NOT EQUAL   * Different category
.
          MATCH     WTCAEDAT,ENDDTE8
          GOTO      GCLC9000 IF LESS        * End date < effective date
.
          ADD       ONE,RECCNT
          IF        RECCNT=1
            MATCH     SP3,WTCACODF
            GOTO      GCLC1000 IF EQUAL     * Blank 1st from change code
          ENDIF
.
          MOVE      SP1,TCINDC4
          MATCH     SP3,WTCACODF
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,WTCACODF
            CALL      RDCODE1               * Read a codes file record
          ENDIF
.
          MOVE      TCINDC4,SAVINDC4        * Save the 4th indicator
.
          MOVE      SP1,TCINDC4
          MATCH     SP3,WTCACODT
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,WTCACODT
            CALL      RDCODE1               * Read a codes file record
          ENDIF
.
          MATCH     SAVINDC4,TCINDC4
          IF        !@EQUAL
            ADD       ONE,ACCSTSCT          * Increment the accom status count
            MATCH     SP8,WTCAEDAT
            IF        !@EQUAL
              UNPACK    WTCAEDAT,CCENT,CYEAR,CMON,CDAY
              PACK      ACCSTSDT,CDAY,CMON,CCENT,CYEAR
              REP       " 0",ACCSTSDT       * Accommodation status last date
            ENDIF
          ENDIF
          GOTO      GCLC1000
.
GCLC9000  MOVE      ZERO,EXIT
GCLC9999  RETURN
+
.******************************************************************************
.*                                  GPDC0000              Called by: GTRE0000 *
.*                          Get Planned Date Changes                          *
.******************************************************************************
GPDC0000  MOVE      ZERO,RECCNT
.
          MOVE      ZERO,PLADDTCT
          PACK      KEY31,WMURNO,WMCODE,WMCNT,SP70
          CALL      RSWTHIS1
GPDC1000  CALL      RKWTHIS1
          BRANCH    OVRCD,GPDC9999
.
          MATCH     WMURNO,WTHIURNO
          GOTO      GPDC9000 IF NOT EQUAL   * Different U/R no
          MATCH     WMCODE,WTHIPROC
          GOTO      GPDC9000 IF NOT EQUAL   * Different procedure code
          COMPARE   WMCNT,WTHIPCNT
          GOTO      GPDC9000 IF NOT EQUAL   * Different procedure count
.
          MATCH     WTHIEDAT,ENDDTE8
          GOTO      GPDC9000 IF LESS        * End date < effective date
.
          ADD       ONE,RECCNT
          IF        RECCNT=1
            MATCH     SP8,WTHIDTAD
            GOTO      GPDC1000 IF EQUAL     * Blank 1st from change date
          ENDIF
.
          ADD       ONE,PLADDTCT       * Incre the planned admin date count
          MATCH     SP8,WTHIEDAT
          IF        !@EQUAL
            UNPACK    WTHIEDAT,CCENT,CYEAR,CMON,CDAY
            PACK      PLADDTDT,CDAY,CMON,CCENT,CYEAR
            REP       " 0",PLADDTDT         * Planned admin date last date
          ENDIF
          GOTO      GPDC1000
.
GPDC9000  MOVE      ZERO,EXIT
GPDC9999  RETURN
+
.******************************************************************************
.*                                  GPRM0000              Called by: GTRE0000 *
.*                          Get Removal Transaction Date                      *
.******************************************************************************
GPRM0000  PACK      KEY31,WMURNO,WMCODE,WMCNT,Z70
          CALL      RSWTHIS1
GPRM1000  CALL      RPWTHIS1
          BRANCH    OVRCD,GPRM9999
.
          MATCH     WMURNO,WTHIURNO
          GOTO      GPRM9000 IF NOT EQUAL   * Different U/R no
          MATCH     WMCODE,WTHIPROC
          GOTO      GPRM9000 IF NOT EQUAL   * Different procedure code
          COMPARE   WMCNT,WTHIPCNT
          GOTO      GPRM9000 IF NOT EQUAL   * Different procedure count
.
          MATCH     "20",WTHIBSCD
          IF        @EQUAL
            MOVE      ONE,EXIT
            GOTO      GPRM9999
          ENDIF
.
          GOTO      GPRM1000
.
GPRM9000  MOVE      ZERO,EXIT
GPRM9999  RETURN
+
.******************************************************************************
.*                                  GRSD0000              Called by: GPTD0000 *
.*                         Get Removed Status Defaults                        *
.******************************************************************************
GRSD0000  IF        WMSTAT=4 | WMSTAT=5
            MATCH     ADATE,ENDDTE8
            GOTO      GRSD9000 IF LESS      * End date < admin date
          ENDIF
.
          IF        WMSTAT=6
            MATCH     WMDATE4,ENDDTE8
            GOTO      GRSD9000 IF LESS      * End date < removal date
          ENDIF
.
          MOVE      "1",REVRES              * Removal reason - default to 1
.
          PACK      KEY8,WMBOOK
          CALL      RDMISS1                 * Read an admin file record
          IF        OVRCD<>1
            UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
            PACK      REVRESDT,CDAY,CMON,CCENT,CYEAR
            REP       " 0",REVRESDT         * Removal reason date - default to
            PACK      TRREMDTE,CDAY,CMON,CCENT,CYEAR
            REP       " 0",TRREMDTE
          ENDIF                               admin date
.
GRSD9000  MOVE      ZERO,EXIT
GRSD9999  RETURN
+
.******************************************************************************
.*                                  GWLD0000              Called by: GPTD0000 *
.*                            Get Waiting List Data                           *
.******************************************************************************
GWLD0000  MATCH     SP6,WMDOCTOR
          IF        !@EQUAL
            PACK      KEY6,WMDOCTOR
            CALL      RDDOCT1               * Read the doctors file
            IF        OVRCD<>1
              MOVE      PTDOMREG,AMONO      * AMO no
            ENDIF
          ENDIF
.
          MATCH     SP8,WMDATE1
          IF        !@EQUAL
            UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
            PACK      LISTDTE,CDAY,CMON,CCENT,CYEAR
            REP       " 0",LISTDTE          * List date
            UNPACK    WMKEYDT,CCENT,CYEAR,CMON,CDAY
            PACK      TRLISTDT,CDAY,CMON,CCENT,CYEAR
            REP       " 0",TRLISTDT         * Transaction date of listing date
          ENDIF
.
          MOVE      TWO,DISINT              * Disch intention - overnight
          IF        WMSTAY=0
            MOVE      ONE,DISINT            * Disch intention - daycase
          ENDIF
.
          MATCH     SP9,WMCODE
          IF        !@EQUAL
            PACK      KEY9,WMCODE
            CALL      RDPROA1               * Read a procedure file record
            IF        OVRCD<>1
              MOVE      WTWPHDPE,PROCODIN   * Procedure code indicator
            ENDIF
          ENDIF
.
SPECCOD   MATCH     SP3,WMUNIT
          IF        !@EQUAL
            PACK      KEY5,ANSW,ANSU,WMUNIT
            CALL      RDCODE1               * Read a codes file record
            IF        OVRCD<>1
              CALL      RJUS0000
              UNPACK    DIM4,DIM2,DIM2
              MOVE      DIM2,SPECCOD     * Speciality code
              REP       " 0",SPECCOD
            ENDIF
          ENDIF
.
          MATCH     SP3,WMPTY
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSP,WMPTY
            CALL      RDCODE1               * Read a codes file record
            IF        OVRCD<>1
              MOVE      TCINDC3,URGCAT      * Current urgency catgeory
            ENDIF
          ENDIF
.
          MATCH     SP3,WTWMACAT
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,WTWMACAT,SP70
            CALL      RDCODE1               * Read a codes file record
            IF        OVRCD<>1
              MOVE      TCINDC4,ACCSTS      * Accommodation status
            ENDIF
          ENDIF
.
          MOVE      WMDATE1,CDYSFDTE
          MATCH     SP8,WMDATE3
          IF        !@EQUAL
            MOVE      WMDATE3,CDYSTDTE
            CALL      CALCDAYS              * Calculate diff between dates
            SUB       ONE,CDYSDAYS
            IF        CDYSDAYS<=30
              UNPACK    WMDATE3,CCENT,CYEAR,CMON,CDAY
              PACK      PLADDT,CDAY,CMON,CCENT,CYEAR
              REP       " 0",PLADDT         * Planned admin date
              MOVE      SP8,PRADDT          * Provisional admin date - blank
            ELSE
              MATCH     SP8,WTWMDTAD
              IF        !@EQUAL
                UNPACK    WTWMDTAD,CCENT,CYEAR,CMON,CDAY
                PACK      PRADDT,CDAY,CMON,CCENT,CYEAR
                REP       " 0",PRADDT       * Provisional admin date
              ENDIF
              MOVE      SP8,PRADDT          *194858 send blank
              MOVE      SP8,PLADDT          * Planned admin date - blank
            ENDIF
          ELSE
            MATCH     SP8,WTWMDTAD
            IF        !@EQUAL
              UNPACK    WTWMDTAD,CCENT,CYEAR,CMON,CDAY
              PACK      PRADDT,CDAY,CMON,CCENT,CYEAR
              REP       " 0",PRADDT         * Provisional admin date
            ENDIF
            MOVE      SP8,PRADDT          *194858 send blank
            MOVE      SP8,PLADDT            * Planned admin date - blank
          ENDIF
.
          MATCH     SP8,PLADDT
          IF        @EQUAL
            MATCH     SP8,WTWMDTAD
            IF        !@EQUAL
              MOVE      WTWMDTAD,CDYSTDTE
              CALL      CALCDAYS            * Calculate diff between dates
              SUB       ONE,CDYSDAYS
              IF        CDYSDAYS<=30
                UNPACK    WTWMDTAD,CCENT,CYEAR,CMON,CDAY
                PACK      PLADDT,CDAY,CMON,CCENT,CYEAR
                REP       " 0",PLADDT       * Planned admin date
                MOVE      SP8,PRADDT        * Provisional admin date - blank
              ENDIF
            ENDIF
          ENDIF
.
. ----- Check if both planned and provisional admin date are blank -----
.
          MATCH     SP8,PLADDT
          IF        @EQUAL
            MATCH     SP8,PRADDT
            IF        @EQUAL
              MATCH     SP8,WMDATE3
              IF        !@EQUAL
                UNPACK    WMDATE3,CCENT,CYEAR,CMON,CDAY
                PACK      PRADDT,CDAY,CMON,CCENT,CYEAR
                REP       " 0",PRADDT       * Provisional admin date
                MOVE      SP8,PRADDT          *194858 send blank
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP8,WTWMDABD
          IF        !@EQUAL
            UNPACK    WTWMDABD,CCENT,CYEAR,CMON,CDAY
            PACK      PLPRDT,CDAY,CMON,CCENT,CYEAR
            REP       " 0",PLPRDT           * Planned procedure date
          ENDIF
.
. Use confirmed admission date (if it is not blank) for Planned admission date
.
          MATCH     SP8,CONFADTE
          IF        !@EQUAL
            UNPACK    CONFADTE,CCENT,CYEAR,CMON,CDAY
            PACK      PLADDT,CDAY,CMON,CCENT,CYEAR
            REP       " 0",PLADDT           * Planned admission date
          ENDIF
.
. Use confirmed operation date (if it is not blank) for Planned procedure date
.
          CALL      GETTHE00
          MATCH     SP8,OPDADATE
          IF        !@EQUAL
            UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
            PACK      PLPRDT,CDAY,CMON,CCENT,CYEAR
            REP       " 0",PLPRDT           * Planned procedure date
          ENDIF
.
          IF        WMSTAT=2 | WMSTAT=3 | WMSTAT=4 | WMSTAT=5
            PACK      KEY8,WMBOOK
            CALL      RDBKREC1
            BRANCH    OVRCD,GWLD9000
            PACK      BOOKIDEN,SP10,SP2,WMBOOK
          ENDIF
.
          IF        WMSTAT=3 | WMSTAT=4 | WMSTAT=5
            PACK      KEY8,WMBOOK
            CALL      RDMISS1
            BRANCH    OVRCD,GWLD9000
.
            MOVE      ZERO,FORM1
            MOVE      DASTAT,FORM1
            IF        FORM1>1 & FORM1<5
              PACK      ADMISSID,SP10,SP2,WMBOOK
            ENDIF
          ENDIF
.
GWLD0500  COMPARE   SIX,WMSTAT
          GOTO      GWLD9000 IF NOT EQUAL   * Patient not removed
.
          MATCH     WMDATE4,ENDDTE8
          GOTO      GWLD9000 IF LESS        * End date < removal date
.
          MATCH     SP3,WMREMOVE
          IF        !@EQUAL
            PACK      KEY5,ANSW,ANSR,WMREMOVE
            CALL      RDCODE1               * Read a codes file record
            IF        OVRCD<>1
              MOVE      TCINDC1,REVRES      * Removal reason
              CALL      GPRM0000
              IF        EXIT=1
                UNPACK    WTHIEDAT,CCENT,CYEAR,CMON,CDAY
                PACK      TRREMDTE,CDAY,CMON,CCENT,CYEAR,SP70
                REP       " 0",TRREMDTE        * Transaction date of removal
              ENDIF
            ENDIF
          ENDIF
.
GWLD2000  MATCH     SP8,WMDATE4
          IF        !@EQUAL
            UNPACK    WMDATE4,CCENT,CYEAR,CMON,CDAY
            PACK      REVRESDT,CDAY,CMON,CCENT,CYEAR
            REP       " 0",REVRESDT         * Removal reason date
          ENDIF
.
GWLD9000  MOVE      ZERO,EXIT
GWLD9999  RETURN
+
.******************************************************************************
.*                                  GDDS0000              Called by: GPTD0000 *
.*                          Get Delay & Decline Stuff                         *
.******************************************************************************
GDDS0000  MOVE      ZERO,FNTPDADD
          MOVE      ZERO,NSPDELCT
          MOVE      ZERO,DOCDELCT
          MOVE      ZERO,PDOOADCT
          PACK      KEY16,WMURNO,SP10
          CALL      RSBKREC6                * Position on & read a booking
GDDS1000  CALL      RKBKREC6                  file record
          BRANCH    OVRCD,GDDS9000
.
          MOVE      BKREURNO,KEY8
          MATCH     KEY8,WMURNO
          GOTO      GDDS9000 IF NOT EQUAL   * Different U/R no
.
          MATCH     BKREPROC,WMCODE
          GOTO      GDDS1000 IF NOT EQUAL   * Different procedure code
.
          COMPARE   BKRECNT,WMCNT
          GOTO      GDDS1000 IF NOT EQUAL   * Different procedure count
.
          MATCH     BKREPDAT,ENDDTE8
          GOTO      GDDS1000 IF LESS        * End date < cancellation date
.
          PACK      KEY31,BKREBOOK,SP30
          CALL      RSOPDEA2                * Position on & read an operation
GDDS2000  CALL      RKOPDEA2                  file record
          BRANCH    OVRCD,GDDS1000
.
          MOVE      BKREBOOK,KEY8
          MATCH     KEY8,OPDAADMN
          GOTO      GDDS1000 IF NOT EQUAL   * Different booking no
.
          MATCH     SP3,OPDACANC
          GOTO      GDDS2000 IF EQUAL       * Blank cancellation code, get next
.
          PACK      KEY5,ANSS,ANSC,OPDACANC
          CALL      RDCODE1                 * Read a codes file record
          BRANCH    OVRCD,GDDS2000
.
          TYPE      TCINDC1
          GOTO      GDDS2000 IF NOT EQUAL   * Not numeric
.
          MATCH     "1",TCINDC1             * Not delayed
          GOTO      GDDS2000 IF EQUAL
.
          MATCH     SP8,OPDACDAT            * Operation canc. date blank,use
          IF        @EQUAL
            MOVE      BKREPDAT,OPDACDAT     * booking cancellation date
          ENDIF
.
          MATCH     OPDACDAT,OPDADATE
          IF        !@LESS
            ADD       ONE,FNTPDADD
          ENDIF
.
          MOVE      ZERO,FORM1
          MOVE      TCINDC1,FORM1
          IF        FORM1>=6
            MOVE      SP1,KEY1
            LOAD      KEY1,FORM1,SP1,SP1,SP1,SP1,SP1,ONE,TWO,THREE,FOUR
            MOVE      KEY1,PDOOAD           * Patient declined offer of admin
.
            ADD       ONE,PDOOADCT          * Increment the pat dec off adm cnt
            MATCH     SP8,OPDACDAT
            IF        !@EQUAL
              UNPACK    OPDACDAT,CCENT,CYEAR,CMON,CDAY
              PACK      PDOOADDT,CDAY,CMON,CCENT,CYEAR
              REP       " 0",PDOOADDT       * Pat dec off admin last date
            ENDIF
          ELSE
            MOVE      ZERO,FORM2
            LOAD      FORM2,FORM1,ZERO,THTDELCT,BEDDELCT,DOCDELCT,NSPDELCT
            ADD       ONE,FORM2
            STORE     FORM2,FORM1,FORM2,THTDELCT,BEDDELCT,DOCDELCT,NSPDELCT
.
            MATCH     SP8,OPDACDAT
            IF        !@EQUAL
              UNPACK    OPDACDAT,CCENT,CYEAR,CMON,CDAY
              PACK      DELLSTDT,CDAY,CMON,CCENT,CYEAR
              REP       " 0",DELLSTDT       * Delay last date
            ENDIF
.
            MATCH     STRTDTE8,OPDACDAT
            IF        !@LESS
              MATCH     OPDACDAT,ENDDTE8
              IF        !@LESS
                MOVE      TCINDC1,DELSTS    * Delay status - current period only
.
                ADD       ONE,DELSTSCT      * Increment the delay count
              ENDIF
            ENDIF
          ENDIF
          GOTO      GDDS2000
.
GDDS9000  MOVE      ZERO,EXIT
GDDS9999  RETURN
+
.******************************************************************************
.*                                  GAFD0000              Called by: GPTD0000 *
.*                           Get Admin File Details                           *
.******************************************************************************
GAFD0000  IF        WMSTAT=4 | WMSTAT=5
            MATCH     ADATE,ENDDTE8
            GOTO      GAFD9000 IF LESS      * End date < admin date
          ENDIF
.
          MATCH     SP3,ACLAIM
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,ACLAIM
            CALL      RDCODE1               * Read a codes file record
            IF        OVRCD<>1
              MOVE      TCINDC4,ACCELADM    * Accommodation elected admin
            ENDIF
          ENDIF
.
GAFD9000  MOVE      ZERO,EXIT
GAFD9999  RETURN
+
.******************************************************************************
.*                                  VALD0000              Called by: ML0000   *
.*                          Validate The Extract Data                         *
.******************************************************************************
VALD0000  MOVE      ZERO,DOCDELCT
          MOVE      ZERO,THTDELCT
          DISPLAY   *P1:24,*EL,"Validating : ";
          MOVE      ZERO,DSPCNT
          PACK      KEY19,SP20
          CALL      RSTEMP1                 * Position on & read a tempfile
VALD1000  CALL      RKTEMP1                   record
          BRANCH    OVRCD,VALD9000
.
          ADD       ONE,DSPCNT
          IF        DSPCNT%10=1
            DISPLAY   *P14:24,*V2LON,TMPURNO;
          ENDIF
.
          MOVE      ZERO,FORM3
          MOVE      PROCODIN,FORM3
.
          IF        FORM3>199 & FORM3<280
            GOTO      VALD1500 
          ENDIF
.
          IF        FORM3<1 | (FORM3>97 & FORM3<100) | (FORM3>199)
.
            COMPARE   "800",FORM3
            GOTO      VALD1500 IF EQUAL
.
.           COMPARE   "810",FORM3
.           GOTO      VALD1500 IF EQUAL
.
.           COMPARE   "820",FORM3
.           GOTO      VALD1500 IF EQUAL
.
.           COMPARE   "900",FORM3
.           GOTO      VALD1500 IF EQUAL
.
.           COMPARE   "910",FORM3
.           GOTO      VALD1500 IF EQUAL
.
            COMPARE   "997",FORM3
            GOTO      VALD1500 IF EQUAL
.
            COMPARE   "998",FORM3
            GOTO      VALD1500 IF EQUAL
.
            COMPARE   "999",FORM3
            GOTO      VALD1500 IF EQUAL

            MOVE      "Invalid indicator procedure code",ERRMSG
            CALL      PRNT0000              * Print error message
          ENDIF
.
VALD1500  MOVE      ZERO,FORM2
          MOVE      SPECCOD,FORM2
          IF        FORM2<1 | FORM2>14
            MOVE      "Invalid speciality code",ERRMSG
            CALL      PRNT0000              * Print error message
          ENDIF
.
          MATCH     "A",URGCAT
          GOTO      VALD1600 IF EQUAL
          MATCH     "B",URGCAT
          GOTO      VALD1600 IF EQUAL
          MATCH     "C",URGCAT
          GOTO      VALD1600 IF EQUAL
          MATCH     "D",URGCAT
          GOTO      VALD1600 IF EQUAL
.
          MOVE      "Invalid Current urgency category",ERRMSG
          CALL      PRNT0000              * Print error message
.
VALD1600  MATCH     PURGCAT1,SP1
          IF        @EQUAL
            MATCH     PURGDTE1,SP8
            IF        !@EQUAL
              MOVE      "Prev Urgency Category 1 = blank, date <> 0",ERRMSG
              CALL      PRNT0000              * Print error message
            ENDIF
.
            MOVE      ZERO,URGCATCT
            MOVE      DURGCATC,URGCATCT
            COMPARE   URGCATCT,ZERO
            IF        !@EQUAL
              MOVE      "Prev Urgency Category 1 = blank, Urgency Count <> 0",ERRMSG
              CALL      PRNT0000              * Print error message
            ENDIF
          ENDIF
.
          MATCH     PURGCAT2,SP1
          IF        @EQUAL
            MATCH     PURGDTE2,SP8
            IF        !@EQUAL
              MOVE      "Prev Urgency Category 2 = blank, date <> 0",ERRMSG
              CALL      PRNT0000              * Print error message
            ENDIF
.
            MOVE      ZERO,URGCATCT
            MOVE      DURGCATC,URGCATCT
            IF        URGCATCT > ONE
              MOVE      "Prev Urgency Category 2 = blank, Urgency Count > 1",ERRMSG
              CALL      PRNT0000              * Print error message
            ENDIF
          ENDIF
.
          MATCH     PURGCAT3,SP1
          IF        @EQUAL
            MOVE      ZERO,URGCATCT
            MOVE      DURGCATC,URGCATCT
            IF        URGCATCT > TWO
              MOVE      "Prev Urgency Category 3 = blank, Urgency Count > 2",ERRMSG
              CALL      PRNT0000              * Print error message
            ENDIF
          ENDIF
.
          MOVE      ZERO,FORM1
          MOVE      ACCSTS,FORM1
          IF        FORM1<1 | FORM1>9
            MOVE      "Invalid anticipated accommodation status",ERRMSG
            CALL      PRNT0000              * Print error message
          ENDIF
.
          MOVE      ZERO,FORM1
          MOVE      ACCELADM,FORM1
          IF        FORM1<0 | FORM1>9
            MOVE      "Invalid accommodation elected on admission",ERRMSG
            CALL      PRNT0000              * Print error message
          ENDIF
.
          MOVE      ZERO,FORM1
          MOVE      LSTSTS,FORM1
          IF        FORM1<1 | FORM1>3
            MOVE      "Invalid patient listing status",ERRMSG
            CALL      PRNT0000              * Print error message
          ENDIF
.
.                                                                     *I-203054
VALD1700  MOVE      LSTSTS,FORM1
          IF        FORM1 = 2 | FORM1 = 3
            MOVE      URGCAT,KEY1
            REP       "A-B-C-",KEY1
            MATCH     "-",KEY1
            IF        @EQUAL
              CLEAR     ERRMSG
              APPEND    "Urgency = A, B, or C but ",ERRMSG
              APPEND    "Listing Status is 2 or 3.",ERRMSG
              RESET     ERRMSG
              CALL      PRNT0000              * Print error message
            ENDIF
          ELSE
            IF        FORM1 = 1
              MATCH     "D",URGCAT
              IF        @EQUAL
                CLEAR     ERRMSG
                APPEND    "Urgency of D is incompatible ",ERRMSG
                APPEND    "with Listing Status of 1",ERRMSG
                RESET     ERRMSG
                CALL      PRNT0000              * Print error message
              ENDIF
            ENDIF
          ENDIF
.
.
          MOVE      ZERO,FORM1
          MOVE      DELSTS,FORM1
          IF        FORM1<0 | FORM1>5
            MOVE      "Invalid delay status",ERRMSG
            CALL      PRNT0000              * Print error message
          ENDIF
.
          IF        FORM1>1 & FORM1<6
            MATCH     SP8,DELLSTDT
            IF        @EQUAL
              MOVE      "Patient delayed - Blank delay last date",ERRMSG
              CALL      PRNT0000            * Print error message
            ENDIF
.
            MOVE      ONE,FORM3
            REPEAT
              MOVE      ZERO,FORM2
              LOAD      FORM2,FORM3,DELSTSCT,THTDELCT,BEDDELCT,DOCDELCT,NSPDELCT
              COMPARE   FORM2,ZERO
              GOTO      VALD2000 IF LESS    * 0 < delay count
.
              ADD       ONE,FORM3
            UNTIL     FORM3>5
.
            MOVE      "Patient delayed - No delay counts for patient",ERRMSG
            CALL      PRNT0000              * Print error message
          ENDIF
.
VALD2000  MOVE      ZERO,FORM1
          MOVE      REVRES,FORM1
          IF        FORM1<0 | FORM1>8
            MOVE      "Invalid removal reason",ERRMSG
            CALL      PRNT0000              * Print error message
          ELSE
            MATCH     SP8,REVRESDT
            IF        !@EQUAL
              MATCH     SP8,TRREMDTE
              IF        @EQUAL
                MOVE      "Trans Date for Removal Date is Missing",ERRMSG
                CALL      PRNT0000              * Print error message
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     REVRESDT,TRREMDTE
          IF        @LESS
            MOVE      "Trans Date for Removal < Removal Date",ERRMSG
            CALL      PRNT0000              * Print error message
          ENDIF
.
.         MATCH     SP8,PLADDT
.         IF        @EQUAL
.           MATCH     SP8,PRADDT
.           IF        @EQUAL
.             IF        TMPSTAT<4
.               MOVE      "Blank planned/provisional admission date",ERRMSG
.               CALL      PRNT0000          * Print error message
.             ELSE
.               MATCH     STRTDTE8,TMPADATE
.               IF        !@LESS
.                 MOVE      "Blank planned/provisional admission date",ERRMSG
.                 CALL      PRNT0000        * Print error message
.               ENDIF
.             ENDIF
.           ENDIF
.         ENDIF
.
          MOVE      ZERO,FORM1
          MOVE      PDOOAD,FORM1
          IF        FORM1<0 | FORM1>3
            MOVE      "Invalid patient declined offer of admission",ERRMSG
            CALL      PRNT0000              * Print error message
          ENDIF
.
          IF        FORM1>0 & FORM1<4
            MATCH     SP8,PDOOADDT
            IF        @EQUAL
              MOVE      "Decline status exists but not Decline Date",ERRMSG
              CALL      PRNT0000              * Print error message
            ENDIF
          ENDIF
.
          IF        DELSTSCT=1|THTDELCT=1|BEDDELCT=1|DOCDELCT=1|NSPDELCT=1
            MATCH     SP8,DELLSTDT
            IF        @EQUAL
              MOVE      "Delay Change Count<>0, Date=0",ERRMSG
              CALL      PRNT0000
            ENDIF
          ENDIF
.
          GOTO      VALD1000
.
VALD9000  MOVE      ZERO,EXIT
VALD9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: VALD0000 *
.*                             Print Error Message                            *
.******************************************************************************
PRNT0000  COMPARE   CLNO,FIFTY8
          CALL      HEAD0000 IF LESS        * Print report header
.
          PACK      ERRMSG,ERRMSG,SP127
          PRINT     *1,"|",TMPURNO,"|",TMPROCOD,"|",TMPROCNT," |",ERRMSG,"|"
          ADD       ONE,CLNO
          ADD       ONE,ERRCNT              * Increment the error counter
          PACK      ERRMSG,SP127
.
PRNT9000  MOVE      ZERO,EXIT
PRNT9999  RETURN
+
.******************************************************************************
.*                                  WRIT0000              Called by: ML0000   *
.*                           Write The Extract File                           *
.******************************************************************************
WRIT0000  IF        NEWFLAG=1
            PREP      TEMP3,CFNAMEB
          ELSE
            PREP      TEMP2,CFNAMEB
          ENDIF
          DISPLAY   *P1:24,*EL,"Writing : ";
          MOVE      "HEAD",RECID
          UNPACK    ENDDTE8,CCENT,CYEAR,CMON,CDAY
          PACK      EXTDTE,CDAY,CMON,CCENT,CYEAR
          REP       " 0",EXTDTE
          MOVE      ZERO,RECCNT
          MOVE      RECCNT,DRECCNT
          REP       " 0",DRECCNT
          IF        NEWFLAG=1
            WRITE     TEMP3,SEQ;RECID,EXTDTE,DRECCNT,SP127,SP30:
                                SP30,SP20,SP30,SP30,SP7
.
          ELSE
            WRITE     TEMP2,SEQ;RECID,EXTDTE,DRECCNT,SP127,SP30:
                                SP30,SP20,SP30,SP30,SP6
          ENDIF
.
          MOVE      ZERO,DSPCNT
          PACK      KEY19,SP20
          CALL      RSTEMP1                 * Position on & read a tempfile
WRIT1000  CALL      RKTEMP1                   record
          BRANCH    OVRCD,WRIT2000
.
          ADD       ONE,DSPCNT
          IF        DSPCNT%10=1
            DISPLAY   *P11:24,*V2LON,TMPURNO;
          ENDIF
.
          ADD       ONE,RECCNT              * Increment the record counter
          MOVE      RECCNT,DRECCNT
          REP       " 0",DRECCNT            * Record counter
.
          MOVE      TMPURNO,URNO
          MOVE      URNO,DMURNO
          REP       " 0",DMURNO             * U/R no
.
          MOVE      ACCSTSCT,DACCSTSC
          REP       " 0",DACCSTSC
.
          MOVE      LSTSTSCT,DLSTSTSC
          REP       " 0",DLSTSTSC
.
          MOVE      PLADDTCT,DPLADDTC
          REP       " 0",DPLADDTC
.
          MOVE      FNTPDADD,NTPDADDT
          REP       " 0",NTPDADDT
.
          MOVE      DELSTSCT,DDELSTSC
          REP       " 0",DDELSTSC
.
          MOVE      THTDELCT,DTHTDELC
          REP       " 0",DTHTDELC
.
          MOVE      BEDDELCT,DBEDDELC
          REP       " 0",DBEDDELC
.
          MOVE      DOCDELCT,DDOCDELC
          REP       " 0",DDOCDELC
.
          MOVE      NSPDELCT,DNSPDELC
          REP       " 0",DNSPDELC
.
          MOVE      PDOOADCT,DPDOOADC
          REP       " 0",DPDOOADC
.
          MOVE      DEFRCT,DDEFRCT
          REP       " 0",DDEFRCT
.
          MOVE      DYNRFCRE,DDYNRFCR
          REP       " 0",DDYNRFCR
.
          IF        NEWFLAG=1
            WRITE     TEMP3,SEQ;DRECCNT,HOSPCOD,DMURNO,SEX,DOB:
                                STRETNO,STRETNAM,SUBURB,POSTCOD,AMONO:
                                LISTDTE,TRLISTDT,DISINT,PROCODIN,SPECCOD,URGCAT:
                                URGCATDT,PURGCAT1,PURGDTE1,PURGCAT2,PURGDTE2:
                                PURGCAT3,DURGCATC,ACCSTS,DACCSTSC,ACCSTSDT:
                                ACCELADM,LSTSTS,DLSTSTSC,LSTSTSDT,DDYNRFCR:
                                PLADDT,DPLADDTC,PLADDTDT,PRADDT,NTPDADDT,DELSTS:
                                DDELSTSC,DTHTDELC,DBEDDELC,DDOCDELC,DNSPDELC:
                                DELLSTDT,REVRES,REVRESDT,TRREMDTE,PLPRDT,PDOOAD:
                                DPDOOADC,PDOOADDT,DDEFRCT,WLDELIND,BOOKIDEN:
                                ADMISSID,INDIGSTA
          ELSE
            WRITE     TEMP2,SEQ;DRECCNT,HOSPCOD,DMURNO,SEX,DOB:
                                STRETNO,STRETNAM,SUBURB,POSTCOD,AMONO:
                                LISTDTE,TRLISTDT,DISINT,PROCODIN,SPECCOD,URGCAT:
                                URGCATDT,PURGCAT1,PURGDTE1,PURGCAT2,PURGDTE2:
                                PURGCAT3,DURGCATC,ACCSTS,DACCSTSC,ACCSTSDT:
                                ACCELADM,LSTSTS,DLSTSTSC,LSTSTSDT,DDYNRFCR:
                                PLADDT,DPLADDTC,PLADDTDT,PRADDT,NTPDADDT,DELSTS:
                                DDELSTSC,DTHTDELC,DBEDDELC,DDOCDELC,DNSPDELC:
                                DELLSTDT,REVRES,REVRESDT,TRREMDTE,PLPRDT,PDOOAD:
                                DPDOOADC,PDOOADDT,DDEFRCT,WLDELIND,BOOKIDEN:
                                ADMISSID
          ENDIF
          GOTO      WRIT1000
.
WRIT2000  MOVE      RECCNT,DRECCNT
          REP       " 0",DRECCNT
          IF        NEWFLAG=1
            WRITAB    TEMP3,ZERO;*13,DRECCNT
          ELSE
            WRITAB    TEMP2,ZERO;*13,DRECCNT
          ENDIF
          DISPLAY   *P1:23,*EF,*B,"Extraction Finished.  ":
                    *P1:24,*V2LON,RECCNT,*HOFF," record(s) extracted.  ":
                    *P30:24,*V2LON,ERRCNT,*HOFF," error record(s).  ";
          CALL      HITENTER
.
WRIT9000  MOVE      ZERO,EXIT
WRIT9999  RETURN
+
.******************************************************************************
.*                                  TAIL0000              Called by: ML0000   *
.*                              Print Report Tail                             *
.******************************************************************************
TAIL0000  CALL      UND80
          PRINT     *N:
                    *1,"Number of Records Extracted    : ",RECCNT,*N:
                    *1,"Number of Error Records        : ",ERRCNT,*N,*N:
                    *1,"Number of Patients on List     : ",ADDCNT,*N:
                    *1,"Number of Removals from List   : ",REMCNT,*N:
                    *1,"Number of Admissions from List : ",ADMCNT,*N,*N:
                    *1,"*** End of Report ***"
.
TAIL9000  MOVE      ZERO,EXIT
TAIL9999  RETURN
+
.******************************************************************************
.*                   Move field to be right justified                         *
.******************************************************************************
RJUS0000  MOVE      ZERO,FORM4
          MOVE      SP10,DIM4
          RESET     THCSCOD,4
          GOTO      RJUS2500
.
RJUS2000  BUMP      THCSCOD,-1
          GOTO      RJUS9999 IF EOS
.
RJUS2500  CMATCH    SP1,THCSCOD
          GOTO      RJUS2000 IF EQUAL
.
          MOVEFPTR  THCSCOD,FORM2
.
          SETLPTR   THCSCOD,FORM2
.
RJUS3000  RESET     THCSCOD
          MOVE      THCSCOD,FORM4
          MOVE      FORM4,DIM4
          RESET     DIM4
.
RJUS9999  RETURN
+
.******************************************************************************
.*                                  OPEN0000              Called by: ML0000   *
.*                              Open The Tempfile                             *
.******************************************************************************
OPEN0000  CALL      KILL0000                * Delete the tempfile
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    FILELIN1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMP1,TFILNAME
.
OPEN9000  MOVE      ZERO,EXIT
OPEN9999  RETURN
+
.******************************************************************************
.*                                  KILL0000              Called by: ML0000   *
.*                             Delete The Tempfile                 & OPEN0000 *
.******************************************************************************
KILL0000  CLOSE     TEMP1
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9000  MOVE      ZERO,EXIT
KILL9999  RETURN
+
.******************************************************************************
.*                       I/O Routines For The Tempfile                        *
.******************************************************************************
RATEMP1   MOVE      ZERO,OVRCD
          RESET     KEY19
          READ      TEMP1,KEY19;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY19
          READ      TEMP1,KEY19;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY19
          READ      TEMP1,KEY19;DTMPURNO,TMPROCOD,TMPROCNT,DTMPSTAT,TMPADATE:  
                          HOSPCOD,SEX,DOB,STRETNO,STRETNAM:                    
                          SUBURB,POSTCOD,AMONO,LISTDTE,TRLISTDT,DISINT:        
                          PROCODIN,SPECCOD,URGCAT,URGCATDT,PURGCAT1:           
                          PURGDTE1,PURGCAT2,PURGDTE2,PURGCAT3,DURGCATC:        
                          ACCSTS,ACCSTSCT,ACCSTSDT,ACCELADM,LSTSTS:            
                          LSTSTSCT,LSTSTSDT,DYNRFCRE,PLADDT,PLADDTCT:          
                          PLADDTDT,PRADDT,FNTPDADD,DELSTS,DELSTSCT:            
                          THTDELCT,BEDDELCT,DOCDELCT,NSPDELCT,DELLSTDT:        
                          REVRES,REVRESDT,TRREMDTE,PLPRDT,PDOOAD:              
                          PDOOADCT,PDOOADDT,DEFRCT,WLDELIND,BOOKIDEN:
                          ADMISSID,INDIGSTA
          GOTO      OVERCOND IF OVER
          MOVE      DTMPURNO,TMPURNO
          MOVE      DTMPSTAT,TMPSTAT
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMP1;DTMPURNO,TMPROCOD,TMPROCNT,DTMPSTAT,TMPADATE:        
                          HOSPCOD,SEX,DOB,STRETNO,STRETNAM:                    
                          SUBURB,POSTCOD,AMONO,LISTDTE,TRLISTDT,DISINT:        
                          PROCODIN,SPECCOD,URGCAT,URGCATDT,PURGCAT1:           
                          PURGDTE1,PURGCAT2,PURGDTE2,PURGCAT3,DURGCATC:        
                          ACCSTS,ACCSTSCT,ACCSTSDT,ACCELADM,LSTSTS:            
                          LSTSTSCT,LSTSTSDT,DYNRFCRE,PLADDT,PLADDTCT:          
                          PLADDTDT,PRADDT,FNTPDADD,DELSTS,DELSTSCT:            
                          THTDELCT,BEDDELCT,DOCDELCT,NSPDELCT,DELLSTDT:        
                          REVRES,REVRESDT,TRREMDTE,PLPRDT,PDOOAD:              
                          PDOOADCT,PDOOADDT,DEFRCT,WLDELIND,BOOKIDEN:
                          ADMISSID,INDIGSTA
          GOTO      OVERCOND IF OVER
          MOVE      DTMPURNO,TMPURNO
          MOVE      DTMPSTAT,TMPSTAT
          RETURN
.
UPTEMP1   MOVE      TMPURNO,DTMPURNO
          MOVE      TMPSTAT,DTMPSTAT
          UPDATE    TEMP1;DTMPURNO,TMPROCOD,TMPROCNT,DTMPSTAT,TMPADATE:
                          HOSPCOD,SEX,DOB,STRETNO,STRETNAM:
                          SUBURB,POSTCOD,AMONO,LISTDTE,TRLISTDT,DISINT:
                          PROCODIN,SPECCOD,URGCAT,URGCATDT,PURGCAT1:
                          PURGDTE1,PURGCAT2,PURGDTE2,PURGCAT3,DURGCATC:
                          ACCSTS,ACCSTSCT,ACCSTSDT,ACCELADM,LSTSTS:
                          LSTSTSCT,LSTSTSDT,DYNRFCRE,PLADDT,PLADDTCT:
                          PLADDTDT,PRADDT,FNTPDADD,DELSTS,DELSTSCT:
                          THTDELCT,BEDDELCT,DOCDELCT,NSPDELCT,DELLSTDT:
                          REVRES,REVRESDT,TRREMDTE,PLPRDT,PDOOAD:
                          PDOOADCT,PDOOADDT,DEFRCT,WLDELIND,BOOKIDEN:
                          ADMISSID,INDIGSTA
          RETURN
.
WRTEMP1   RESET     KEY19
          MOVE      TMPURNO,DTMPURNO
          MOVE      TMPSTAT,DTMPSTAT
          WRITE     TEMP1,KEY19;DTMPURNO,TMPROCOD,TMPROCNT,DTMPSTAT,TMPADATE:
                                HOSPCOD,SEX,DOB,STRETNO,STRETNAM:
                                SUBURB,POSTCOD,AMONO,LISTDTE,TRLISTDT,DISINT:
                                PROCODIN,SPECCOD,URGCAT,URGCATDT,PURGCAT1:
                                PURGDTE1,PURGCAT2,PURGDTE2,PURGCAT3,DURGCATC:
                                ACCSTS,ACCSTSCT,ACCSTSDT,ACCELADM,LSTSTS:
                                LSTSTSCT,LSTSTSDT,DYNRFCRE,PLADDT,PLADDTCT:
                                PLADDTDT,PRADDT,FNTPDADD,DELSTS,DELSTSCT:
                                THTDELCT,BEDDELCT,DOCDELCT,NSPDELCT,DELLSTDT:
                                REVRES,REVRESDT,TRREMDTE,PLPRDT,PDOOAD:
                                PDOOADCT,PDOOADDT,DEFRCT,WLDELIND,BOOKIDEN:
                                ADMISSID,INDIGSTA
          RETURN
+
.******************************************************************************
.*                                  DNRC0000              Called by: GTRE0000 *
.*         Calculate the total number of not ready for care days              *
.******************************************************************************
DNRC0000  MOVE      ZERO,DNRFCFLG
          MOVE      ZERO,DYNRFCRE
          MOVE      ZERO,LSTSTSCT
          MOVE      ONE,LSTSTS
          MOVE      ZERO,RFCFLAG
.
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP70
          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
          CALL      RSWTSUS1
DNRC1000  CALL      RKWTSUS1
          BRANCH    OVRCD,DNRC3000
.
          MATCH     WMURNO,WTSUURNO
          GOTO      DNRC3000 IF NOT EQUAL   * Different U/R no
.
          MATCH     WMCODE,WTSUCODE
          GOTO      DNRC3000 IF NOT EQUAL   * Different procedure code
.
          COMPARE   WMCNT,WTSUCNT
          GOTO      DNRC3000 IF NOT EQUAL
.
          MATCH     WTSUFDAT,ENDDTE8
          GOTO      DNRC1000 IF LESS        * End date < suspension start
.
          MATCH     SP70,WTSUTDAT
          IF        !@EQUAL
            MATCH     WTSUTDAT,ENDDTE8
            IF        !@LESS
              UNPACK    WTSUTDAT,CCENT,CYEAR,CMON,CDAY
              PACK      LSTSTSDT,CDAY,CMON,CCENT,CYEAR,SP70
              REP       " 0",LSTSTSDT
              ADD       TWO,LSTSTSCT
              MOVE      ONE,RFCFLAG
              MOVE      ONE,LSTSTS
            ELSE
              UNPACK    WTSUFDAT,CCENT,CYEAR,CMON,CDAY
              PACK      LSTSTSDT,CDAY,CMON,CCENT,CYEAR,SP70
              REP       " 0",LSTSTSDT
              ADD       ONE,LSTSTSCT
              MOVE      ZERO,RFCFLAG
            ENDIF
          ELSE
            UNPACK    WTSUFDAT,CCENT,CYEAR,CMON,CDAY
            PACK      LSTSTSDT,CDAY,CMON,CCENT,CYEAR,SP70
            REP       " 0",LSTSTSDT
            ADD       ONE,LSTSTSCT
            MOVE      ZERO,RFCFLAG
          ENDIF
.
          PACK      TCINDC1,SP2
          MATCH     SP3,WTSULSTS
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSL,WTSULSTS
            CALL      RDCODE1               * Read a codes file record
          ENDIF
.
          MATCH     "R",TCINDC1
          GOTO      DNRC1000 IF EQUAL
.
          IF        RFCFLAG=1
            GOTO      DNRC1500
          ENDIF
.
          MATCH     "D",TCINDC1
          IF        @EQUAL
            MOVE      "2",LSTSTS
            GOTO      DNRC1500
          ENDIF
.
          MOVE      "3",LSTSTS
.
DNRC1500  MOVE      WTSUFDAT,CDYSFDTE
          MOVE      WTSUTDAT,CDYSTDTE
.
          MATCH     SP8,WTSUTDAT
          IF        @EQUAL
            MOVE      ENDDTE8,CDYSTDTE
          ELSE
            MATCH     ENDDTE8,WTSUTDAT
            IF        !@LESS
              MOVE      ENDDTE8,CDYSTDTE
            ENDIF
          ENDIF
.
          CALL      CALCDAYS              * Calculate diff between dates
          SUB       ONE,CDYSDAYS
          ADD       CDYSDAYS,DYNRFCRE     * Incre the days not ready for care
.
          MATCH     SP70,WTSUTDAT
          IF        @EQUAL
....        ADD       ONE,DYNRFCRE        * still NRFC at end date, so add 1
          ELSE
            MATCH     ENDDTE8,WTSUTDAT
            IF        !@LESS
....          ADD       ONE,DYNRFCRE      * still NRFC at end date, so add 1
            ENDIF
          ENDIF
.
          GOTO      DNRC1000
.
DNRC3000  
.
DNRC9000  MOVE      ZERO,EXIT
DNRC9999  RETURN
+
.------------------------------------------------------------
. Get Theatre Booking Details for Waiting List
.------------------------------------------------------------
GETTHE00  COMPARE   TWO,WMSTAT
          GOTO      GETTHE05 IF EQUAL
.         
          COMPARE   THREE,WMSTAT
          GOTO      GETTHE05 IF EQUAL
.         
          COMPARE   FOUR,WMSTAT
          GOTO      GETTHE05 IF EQUAL
.         
          COMPARE   FIVE,WMSTAT
          GOTO      GETTHE05 IF EQUAL
.
          GOTO      GETTHE98
.
GETTHE05  MATCH     ZEROVISN,WMBOOK
          GOTO      GETTHE98 IF EQUAL
.
          PACK      KEY31,WMBOOK,ZERO,SP70
          CALL      RSOPDEA2
GETTHE10  CALL      RKOPDEA2
          BRANCH    OVRCD,GETTHE98
          MOVE      WMBOOK,D8
          MATCH     D8,OPDAADMN
          GOTO      GETTHE98 IF NOT EQUAL
          COMPARE   THREE,OPDASTAT
          GOTO      GETTHE10 IF EQUAL
          GOTO      GETTHE99
.
GETTHE98  MOVE      SP8,OPDADATE
.
GETTHE99  RETURN
+
.------------------------------------------------------------
. Move Extract to Web Directory
.------------------------------------------------------------
MVEXT000  CLEAR     CMDLINE                 * header file
          APPEND    "ibawat61.us1 ",CMDLINE
          APPEND    FILENAME,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
MVEXT999  RETURN
.==============================================================================
.
          INC       STD001IO
.
          INC       CALCDAYS                * Calculate diff between dates
.
          INC       BOKRC1IO/INC            * Booking file
          INC       OPRDEAIO/INC            * Detail file
          INC       PATDO1IO/INC            * Doctor file
          INC       PMSHCPIO/INC
          INC       PATDSCIO/INC            * Disch file
          INC       PATCODIO/INC            * Codes file
          INC       PATMA1IO/INC            * Master file
          INC       PATMI1IO/INC            * Master file
          INC       PMSVX1IO/INC            * Master file
          INC       PMSPX2IO/INC            * Master ext file
          INC       WATCATIO/INC            * Code changes file
          INC       WATPROIO/INC            * Procedure file
          INC       WATTR1IO/INC            * Treatment file
          INC       WATHISIO/INC            * History file
          INC       WATSUSIO/INC            * Suspension
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
+
.


.***************************************************************************
.* System    :   INPATIENT                                                 *
.* Program   :   IBAPMS41                                                  *
.* Desc      :   Period Range Register Program                             *
.***************************************************************************
.* Date      :   05/07/89                                                  *
.* Author    :   Graeme Williams                                           *
.* Mods.     :                                                             *
.***************************************************************************
          INC       STD001FD
          INC       PATDRGFD/INC  
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
CURRYR    DIM       4
.
.
PRGNAM    INIT      "Period Range Master Listing"
PRGID     INIT      "IBAPMS41"
VERSION   INIT      "V12.01.00"
.**********************************************************************
.*                           MAINLINE                                 *
.*                       Controlling Logic                            *
.**********************************************************************
ML0000    CALL      INIT0000            * initialization
.                                       * open files
.                                       * display header
.
ML1000    CALL      OPTN0000            * select option
          BRANCH    EXIT,ML9999
.
ML2000    CALL      PRIN0000            * print all details
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*               Open files and display header                        *
.**********************************************************************
INIT0000    
          CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening ":
                    *P60:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"                           
.
INIT9999  RETURN
+
.**********************************************************************
.*                           OPTN0000                                 *
.*                   Display the main option screen                   *
.**********************************************************************
OPTN0000  MOVE       FALSE,EXIT
          DISPLAY    *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                     *P1:5,*V2LON,ONE,*HOFF," = Print Date Ranges";
.
OPTN1000  KEYIN      *P1:7,*EL,"Please Select : ",*V2LON,OPTION;
.
          COMPARE    TWO,OPTION
          GOTO       OPTN1000 IF NOT LESS
.
          DISPLAY    *P1:7,*EL;
.
          BRANCH     OPTION,OPTN9999
          MOVE       TRUE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                             PRIN0000                               *
.*                Read file and print all details                     *
.**********************************************************************
PRIN0000  MOVE       ZERO,CPAGENO
          CLOCK      TIME,CTIMEIS
          MOVE       SP6,KEY6
          CALL       RDSDRGA1                      * position to start of file
          CALL       RDKDRGA1
          BRANCH     OVRCD,PRIN9000
          CALL       HEAD0000                      * print header
          CALL       REC0000                       * print a record
          MOVE       DRGYR,CURRYR                  * Save the current year
.
PRIN1000  CALL       RDKDRGA1
          BRANCH     OVRCD,PRIN8000
.
          MATCH      DRGYR,CURRYR
          CALL       UND132 IF NOT EQUAL
.
          CALL       REC0000                       * print a record
          MOVE       DRGYR,CURRYR                  * Save the current year
          GOTO       PRIN1000
.
PRIN8000  CALL       UND132
          PRINT      *N,*N,"*** END OF REPORT  ***";
          GOTO       PRIN9999
.
PRIN9000  KEYIN     *P1:24,*EL,"No Date Range Data Available - Tap <",*V2LON:
                                 "ENTER",*HOFF,"> ",*EOFF,ANS;
.
PRIN9999  RETURN
+
.**********************************************************************
.*                                HEAD0000                            *
.*                          Print page header                         *
.**********************************************************************
HEAD0000  CALL      HEAD132
.
          PRINT     *N;
          ADD       ONE,CLNO
          CALL      UND132
          PRINT     *1,"!  Period Date":
                    *17,"!  From  Date":
                    *32,"!   To  Date":
                    *47,"!  Calendar Date":
                    *65,"! Long Description":
                    *89,"!  Medium Description":
                    *111,"!  Short Description":
                    *132,"!"
          ADD       ONE,CLNO
          CALL      UND132
.
HEAD9999  RETURN
+
.**********************************************************************
.*                                REC0000                             *
.*                   Routine to print a record                        *
.**********************************************************************
REC0000   COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          DISPLAY   *P10:24,"Printing Period Date ":
                    *P35:24,*EL,*V2LON,DRGNUM,SLASH,DRGYR;
.
          PRINT     *1,"!    ",DRGNUM,SLASH,DRGYR;
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *17,"!  ",CPCDATE;
.
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *32,"!  ",CPCDATE:
                    *47,"!    ",DRGCNUM,SLASH,DRGCYR:
                    *65,"! ",DRGLDSC:
                    *89,"!    ",DRGMDSC:
                    *111,"!       ",DRGSDSC:
                    *132,"!"
          ADD       ONE,CLNO
.
REC9999   RETURN
+
.
.
          INC       STD001IO
          INC       PATDRGIO/INC

.***************************************************************************
.* System    :   REFERRAL MANAGEMENT                                       *
.* Program   :   FX0875530                                                 *
.* Desc      :   Update hospital code relating to the legal status         *
.***************************************************************************
.* Date      :   26/06/2019                                                *
.* Author    :   Sunny (Majid Khan)                                        *
.* Function  :   An issue with the Legal Status Resp. The Update template  *
.*               saves the first three letter of the hospital description  *
.*               instead of the dhb code. This fix program will resolve    *
.*               the data that has been saved incorrectly as a result of   *
.*               error.                                                    *
.*                                                                         *
.* Modifications  :                                                        *
.*                                                                         *
.* V10.14.00 26/06/2019  Sunny              TSK 0875530                    *
.*                       Created program                                   *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. ------------------------- 
          INC       MEHHLSFD/INC
          INC       PATHSPFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
MIGCOUNT  FORM      8
DIM3      DIM       3
.
PRGID     INIT      "FX875530"
PRGNAM    INIT      "Update Hosp. code of Legal Status Resp"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      MEHHLSA1,"mehhlsaf"
          OPEN      PATHSPA1,"pathspaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,MIGCOUNT           * Migrated record count        
.
          PACK      KEY16,SP70
          CALL      RSMHHLS1
PROC1000  CALL      RKMHHLS1                * Loop all mehhlsaf records
          BRANCH    OVRCD,PROC9000
.
.         For each record in mehhlsaf match the MHHLRDHB with blank spaces
.
          MATCH     SP3,MHHLRDHB            
          GOTO      PROC1000 IF EQUAL
.
.         For each record in mehhlsaf match the MHHLRDHB with PTHSNAME
.
          PACK      KEY3,SP70
          CALL      RSPTHSP1
PROC2000  CALL      RKPTHSP1
          BRANCH    OVRCD,PROC1000          * Branch to PROC1000??          
.
          MOVE      SP3,DIM3
          MOVE      PTHSNAME,DIM3
          MATCH     MHHLRDHB,DIM3 
PROC2100  GOTO      PROC2000 IF NOT EQUAL       
.
          MATCH     SP3,PTHSHDHB
          GOTO      PROC1000 IF EQUAL 
.
          MOVE      PTHSHDHB,MHHLRDHB           
          PACK      KEY16,MHHLURNO,MHHLUNIQ,SP70
          CALL      UPMHHLS1 
.
          ADD       ONE,MIGCOUNT
          PRINT     *1,"|",MHHLURNO,"|",MHHLUNIQ,"|",MHHLRDHB
.
          GOTO      PROC1000
.
PROC2200  DISPLAY   *P15:22,*EL,*V2LON,"Error"
          GOTO      PROC9999
.
PROC9000  DISPLAY   *P15:22,*EL,*V2LON,"Total Records Updated : ":
                    MIGCOUNT
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
          GOTO      PROC9999
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       MEHHLSIO/INC
          INC       PATHSPIO/INC

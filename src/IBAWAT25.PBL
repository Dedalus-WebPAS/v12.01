.******************************************************************************
.* System    :  Booking List Summary Report                                   *
.* Program   :  IBAWAT25                                                      *
.* Desc      :  List of patients scheduled for treatment (by doctor or unit)  *
.******************************************************************************
.* Date      :  18/10/1988                                                    *
.* Author    :  N.S.                                                          *
.* Mods.     :                                                                *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V10.05.01 05/01/2015  Ebon Clements     CAR 261630                  *
.*                  Removed port number from temp file name                   *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01  30/12/1999  Steve Armstrong                              *
.*                   Fixed global recompile bugs                              *
.******************************************************************************
.*        V5.07.B02 12/01/1999  Nicole Harrington  5.07 rebounds Error 34     *
.*                  Fixed header date printing                                *
.*                  22/10/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
.*        V5.04.01  25/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.******************************************************************************
.*        V5.01     03/06/1989  S.Barcham                                     *
.*                  Recompiled for locking in PATIOMAS                        *
.*        V5.02     08/06/1989  M.Ohleiter                                    *
.*                  New Zealand changes                                       *
.*        V5.03     10/06/1989  M.Ohleiter        SRF 101029                  *
.*                  Changed displays                                          *
.*        V5.04     15/06/1989  M.Ohleiter        SRF 100926                  *
.*                  Included '?' option for unit and doctor                   *
.*        V5.01.01  01/07/1989  M.Ohleiter                                    *
.*                  Changed UR/, Admission, Booking to length 8 - Rel. 5.01   *
.*                  09/07/1989  J.Clark                                       *
.*                  Added KEY26 variable added to PATIODOC by SOG             *
.*                  20/07/1989  M.Ohleiter        SRF 101626                  *
.*                  Fixed report output                                       *
.*        V5.01.121 27/11/1990  Glen Hobby                                    *
.*                  Recompiled for changes to PATMX1FD                        *
.*        V5.01.122 02/03/93  Neeriem "I Hate Support" Dye                    *
.*                  Reformat print to print nicely for new address lengths    *
.*        V5.01.123 23/03/93  Neeriem "I Hate Support" Dye                    *
.*                  Fixed name print                                          *
.*        V5.01.124 27/03/1993  i chung           (Dudley change)             *
.*                  Added users security check                                *
.*        V5.01.125 26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBASEQFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       TFILEVAR/INC
          INC       WATCONFD/INC
          INC       WATSEAFD/INC
          INC       WATSEIFD/INC
          INC       WATTR1FD/INC
          INC       WEBERRFD/INC
+
.*****************************************************************************
.*        Temporary Indexed file Declaration                                 *
.*****************************************************************************
SRTKEY    DIM      30
WATMP1    IFILE    SQL, FIXED=442, KEY=SRTKEY
SORT1     INIT     "U129-131,126-128,1-19"
SORT2     INIT     "U152-157,126-128,1-19"
.
.WMURNO   DIM       8        8     1
.WMCODE   DIM       9        9     9
.DWMCNT   DIM       2        2    18
.PSNAME   DIM      20       20    20
TEMP18    DIM      18       18    40 
DIM25     DIM      25       25    58        * store wmdesc for print
PNAME     DIM      33       33    83        * patient name to print (su ti giv)
PATDOB    DIM      10       10   116        * patient DOB to print
.WMPTY    DIM       3        3   126
.WMUNIT   DIM       3        3   129
DESCUNIT  DIM      20       20   132
.WMDOCTOR DIM       6        6   152
INDDOCNAM DIM      30       30   158
.PTELEP   DIM      12       12   188
.PTELEB   DIM      12       12   200
DESCSEX   DIM       6        6   212
DSUNIT    DIM      20       20   218
.WMSTAY   FORM      5        4   238
.WMTIME   FORM      5        4   242
DAT110    DIM      10       10   246        * WMDATE1 (DD/MM/CCYY)
DAT210    DIM      10       10   256        * WMDATE2 (DD/MM/CCYY)
DATE40    DIM      10       10   266
DSPTY     DIM      20       20   276
PATAD     DIM      50       50   296
.End of Record                   442
+
.
.         Work Variables
.
ALLFLG    FORM      1                * =1 all units/doctors  =0 one unit/doctor
CODE      DIM       2
CMDLINE   DIM       50
.
DDESCWU   DIM      20
DESCOPT   DIM      23                * option description in heading
DIM3      DIM       3
DIMURNO   DIM       8
DOCCODE   DIM       6
DOCNAM    DIM      32
DOCNAME   DIM      32                * doctor name display
DOCTOR    DIM       6                * doctor code keyed in
DUNIQUE   DIM      8
.
FNAME1    DIM       8
FROMDATE  DIM       8                * From Date (CCYYMMDD)
INDSORT1  DIM      30
.
PATCNT    FORM      5                * counts number of patients with multiples
PWMDOCTOR DIM      6
PWMUNIT   DIM      3
.
STATUS    FORM      1
SEQ1      FORM      8
SWMURNO   DIM       8
.
TEMPFLG   FORM      1
TODATE    DIM       8                * To Date   (CCYYMMDD)
UNIQUE    FORM     8
UNIT      DIM       3                * unit keyed in
.
WU        DIM       5
.
.         Constants
.
CODEWU    INIT      "WU"             * catagory WU (unit)
DESCOPT2  INIT      "Doctor  "
SP19      INIT      "                   "
SEQUENCE  INIT      " Sequence"
THREE7    FORM      "37"
.
PRGID     INIT      "IBAWAT25"
PRGNAM    INIT      "Booking List Summary Report"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
ML0000    CALL      INIT0000               * open files     
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999
          CLOSE     WATMP1
          BRANCH    OPTION TO ML1000,ML2000
.
. ------  By Unit  ------
.
ML1000    PACK      KEY5 WITH CODEWU,SP5
          CALL      RDCODE1
          BRANCH    OVRCD TO ML0100
          MOVE      TDESC,DDESCWU
          MOVE      TDESC,DESCOPT
          DISPLAY   *P54:2,*V2LON,"- By ",*+,DESCOPT,SP1;
          CALL      DAT0000                * keyin from/to date
          BRANCH    EXIT,ML0100
          CALL      COD00000               * keyin unit code
          CALL      CONT0000               * ok to continue ?
          BRANCH    EXIT,ML1000,ML0100
          CALL      TEMP0000
          CALL      CLR0000                * clear vars patient master
          CALL      CLR1000                * clear vars waiting master
          CALL      CLR3000                * clear vars patdocfd
          CALL      PROC0000               * read wattrefd
          BRANCH    EXIT,ML0100            * empty tempfile
          CALL      PRTT0000               * print
          GOTO      ML0100
.
. ------  By Doctor  ------
.
ML2000    MOVE      DESCOPT2,DESCOPT
          DISPLAY   *P54:2,*V2LON,"- By ",*+,DESCOPT,SP1;
          CALL      DAT0000                * keyin from/to date
          BRANCH    EXIT,ML0100
          CALL      COD00000               * keyin doctor code
          CALL      CONT0000               * ok to continue ?
          BRANCH    EXIT,ML2000,ML0100
          CALL      TEMP0000
          CALL      CLR0000                * clear vars patient master
          CALL      CLR1000                * clear vars waiting master
          CALL      CLR3000                * clear vars patdocfd
          CALL      PROC0000               * read wattrefd
          BRANCH    EXIT,ML0100            * empty tempfile
          CALL      PRTT0000               * print 
          GOTO      ML0100
.
ML9999    CALL      KILL0000               * Delete temop file
          CHAIN     PGM
+
.***************************************************************************
.*        Initialisation , opening files                                   *
.***************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*2,CDD,CMM,CYY,CCC
          READ      CONTROLF,THREE7;*211,WTCNWLSC
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          IF        WTCNWLSC = 1
            DISPLAY   *P64:24,"watseaaf";
            OPEN      WATSEAA1,"watseaaf"
            DISPLAY   *P64:24,"watseiaf";
            OPEN      WATSEIA2,"watseiaf"
          ENDIF
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTR1A2,"wattr1af"
          OPEN      WATTR1A3,"wattr1af"
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,FNAME1
.
          RETURN
+
.**************************************************************************
.*        Option Menu                                                     *
.**************************************************************************
OPTN0000  MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
.
          CALL      SCRO0000                * display main option screen
          DISPLAY   *P1:8,"Select Option : ";
.
OPTN1000  MOVE      FALSE,EXIT
          KEYIN     *P17:8,*EL,*V2LON,OPTION;
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
          DISPLAY   *P17:8,*V2LON,OPTION;
.
          COMPARE   THREE,OPTION
          GOTO      OPTN9999 IF LESS
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      TRUE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*        Main option screen                                                  *
.******************************************************************************
SCRO0000  PACK      KEY5 WITH CODEWU,SP5
          CALL      RDCODE1
          MOVE      TDESC,DDESCWU
          HLINE     *G37,2,54,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = By ",DDESCWU:
                    *P1:6,*V2LON,TWO,*HOFF," = By Doctor";
          RETURN
+
.******************************************************************************
.*        Keyin from / to date                                                *
.*        fromdate < sched.admiss.date = wmdate3 < todate                     *
.******************************************************************************
DAT0000   DISPLAY    *P1:12,*EF,"From  Date  :":
                         *P1:13,"To    Date  :";
.
          MOVE       FALSE,EXIT             * reset exit 
          MOVE       "40",ECOL
          MOVE       TEN2,EVERT
          MOVE       TEN5,CCOL
.
DAT1000   DISPLAY    *P15:12,*EL,*P15:13,*EF;
          MOVE       TEN2,CVERT
.
          UNPACK     SP6 INTO CYEAR,CMON,CDAY
          MOVE       CCC,CCENT
          CALL       KEYDATE
          BRANCH     CQUEST,DAT1000
          BRANCH     OVRCD OF DAT9000
.
          PACK       FROMDATE WITH CCENT,CYEAR,CMON,CDAY
          REP        " 0",FROMDATE
.
          MOVE       TEN3,CVERT
          MOVE       CVERT,EVERT
.
DAT2000   UNPACK     SP6 INTO CYEAR,CMON,CDAY
          MOVE       CCC,CCENT
          CALL       KEYDATE
          BRANCH     CQUEST,DAT2000
          BRANCH     OVRCD OF DAT1000
.
          PACK       TODATE WITH CCENT,CYEAR,CMON,CDAY
          REP        " 0",TODATE
.
          MATCH      FROMDATE,TODATE
          GOTO       DAT9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** To Date must be greater than or equal From Date **   ";
          CALL      HITENTER
          GOTO      DAT2000
.
DAT9000   MOVE      TRUE,EXIT
.
DAT9999   RETURN
+
.**************************************************************************
.         Keyin specific unit (option1) or doctor (option 2)              *
.**************************************************************************
COD00000  MOVE      FALSE,EXIT
          BRANCH    OPTION TO CODU0000,CODD0000
.
. ------  Keyin Unit code ------
.
CODU0000  PACK      KEY5 WITH CODEWU,SP5
          CALL      RDCODE1
          BRANCH    OVRCD OF CODU2000
          MOVE      TDESC,DDESCWU
          PACK      UNIT WITH SP3
          PACK      TDESC WITH SP30
          DISPLAY   *P1:15,*EL,DDESCWU," : ",*V2LON,UNDLN3;
.
CODU1000  KEYIN     *P24:15,*V2LON,UNIT,*P24:15,*V2LON,*DV,UNIT;
          ENDSET    UNIT
          APPEND    SP3,UNIT
          RESET     UNIT
.
          MATCH     SP3,UNIT
          GOTO      CODU3000 IF EQUAL
          CMATCH    QUEST,UNIT
          GOTO      CODU4000 IF EQUAL
.
          PACK      KEY5 WITH CODEWU,UNIT
          CALL      RDCODE1
          BRANCH    OVRCD TO CODU2000
          DISPLAY   *P30:15,*EL,TDESC;
          GOTO      CODU9999
.
CODU2000  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Unit **    ";
          CALL      HITENTER
          GOTO      CODU1000
.
CODU3000  DISPLAY   *P24:15,*EL,*V2LON,"ALL ",DDESCWU;
          GOTO      CODU9999
.
CODU4000  MOVE      CODEWU,CODE
          CALL      PATCODDS
          CALL      REDIS
          GOTO      CODU0000
.
CODU9999  MOVE      SP6,DOCTOR
          RETURN
+
.
. ----- Keyin Doctor code -----
.
CODD0000  PACK      DOCTOR WITH SP6
          MOVE      "ALL Doctors                     ",DOCNAME
          DISPLAY   *P1:15,*EL,"For Doctor  : "
.
          MOVE      "15",ECOL
          MOVE      "15",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,CODD3000,CODD0000
.
          MOVE      DCODE,DOCTOR
.
          PACK      KEY6 WITH DOCTOR
          CALL      DOCT1000               * concatenate doctor name
          DISPLAY   *P25:15,*EL,DOCNAME;
          GOTO      CODD9999
.
CODD3000  DISPLAY   *P15:15,*EL,*V2LON,DOCNAME;
          GOTO      CODD9999
.
CODD9999  MOVE      SP3,UNIT
          RETURN
+
.**************************************************************************
.*        Redisplay                                                       *
.**************************************************************************
REDIS     CALL      SCRO0000           *display main option screen
          DISPLAY   *P54:2,*V2LON,"- ",*+,DESCOPT,SP1,*HOFF:
                    *P1:8,"Select Option : ",*V2LON,OPTION,*HOFF:
                    *P1:12,"From  Date  :":
                    *P1:13,"To    Date  :";
          MOVE      TEN5,CCOL
          MOVE      TEN2,CVERT
.
          UNPACK    FROMDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      DSPDATE
.
          MOVE      TEN3,CVERT
          UNPACK    TODATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      DSPDATE
          RETURN
+
.**************************************************************************
.*        Ok to continue ? (Y/N)                                          *
.**************************************************************************
CONT0000  MOVE      FALSE,EXIT                         * reset exit
          KEYIN     *P1:24,*EL,"Ok to Continue (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSN,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSC,*HOFF,") ? ",*V2LON,ANS;
.
          MATCH     ANSN,ANS
          GOTO      CONT2000 IF EQUAL
          MATCH     ANSY,ANS
          GOTO      CONT9999 IF EQUAL
          MATCH     ANSC,ANS
          GOTO      CONT1000 IF EQUAL
          BEEP
          GOTO      CONT0000
.
CONT1000  MOVE      TWO,EXIT
          GOTO      CONT9999
.
CONT2000  MOVE      TRUE,EXIT
.
CONT9999  RETURN
.
.*****************************************************************************
.*        Open tempfile for U/R    sequence :                                *
.*                          Doctor sequence :                                *
.*        Kill indexed temp files, and create new indexed tempfiles          *
.*****************************************************************************
TEMP0000  DISPLAY   *P1:24,*EL,*V2LON,"Creating temporary work file.  ":
                           *BLINKON,"Please wait...";
.
. ----- Kill indexed tempfile -----
.
          CALL      KILL0000
.
. ----- Create indexed tempfile -----
.
          LOAD      SRTKEY USING OPTION OF SORT1,SORT2
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME1,CMDLINE
          APPEND    " 442 ",CMDLINE
          APPEND    SRTKEY,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
. ----- Open tempfile -----
.
          OPEN      WATMP1,FNAME1
.
TEMP9999  RETURN
.
.
.*****************************************************************************
. Kill indexed tempfile 
.*****************************************************************************
KILL0000  CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
.
.*****************************************************************************
.*        Clear PATMA1FD variables                                           *
.*****************************************************************************
CLR0000   MOVE      SP8,PURNO
          RETURN
+
.*****************************************************************************
.*        Clear WATTR1FD variables                                           *
.*****************************************************************************
CLR1000   MOVE      ZERO,WMURNO
          RETURN
+
.*****************************************************************************
.*        Clear PATDO1FD variables                                           *
.*****************************************************************************
CLR3000   PACK      DCODE WITH SP10
          PACK      DTITL WITH SP10
          PACK      DGNAME WITH SP30
          PACK      DSNAME WITH SP20
          PACK      DRTYPE WITH SP10
          PACK      DRNAME WITH SP30
          PACK      DSADD1 WITH SP30
          PACK      DSADD2 WITH SP30
          PACK      DSADD3 WITH SP20
          PACK      DSPOST WITH SP10
          PACK      DTELES WITH SP20
          PACK      DTELEP WITH SP20
          MOVE      ZERO,DPAGER
          PACK      DASSOC1 WITH SP30
          PACK      DASSOC2 WITH SP30
          RETURN
+
.****************************************************************************
.*        Read WATTR1FD only patients with treatment status =2 (wmstat)     *
.*        Option 1 (=By unit )                                              *
.*        Option 2 (=By doctor)                                             *
.****************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,*V2LON,*P28:24,"***  Extracting Data  ***",*W:
                    *HOFF,*P16:24,*EL,"U/R Number : ",*P47:24,"Scanning : ";
          MOVE      ZERO,TEMPFLG
          MOVE      ZERO,UNIQUE
          LOAD      INDSORT1 USING OPTION OF UNIT,DOCNAM
.
          MOVE      FALSE,EXIT
          MOVE      SP8,SWMURNO
          MOVE      ZERO,ALLFLG
          PACK      KEY19 WITH SP19
          CALL      RDSTREA1
.
PROC1000  CALL      RDKTREA1
          BRANCH    OVRCD TO PROC7000
          DISPLAY   *P58:24,*EL,WMURNO;
          CMATCH    "2",DWMSTAT
          GOTO      PROC1000 IF NOT EQUAL   * status is not 2
          MATCH     FROMDATE,WMDATE3
          GOTO      PROC1000 IF LESS        * DATE3 < From Date
          MATCH     WMDATE3,TODATE
          GOTO      PROC1000 IF LESS        * DATE3 > To Date
          MATCH     SP3,UNIT
          GOTO      PROC1100 IF EQUAL       * print all units
          MATCH     UNIT,WMUNIT
          GOTO      PROC1000 IF NOT EQUAL   * unit not right
.
PROC1100  MATCH     SP6,DOCTOR 
          GOTO      PROC2000 IF EQUAL       * print all doctors      
          MATCH     DOCTOR,WMDOCTOR
          GOTO      PROC1000 IF NOT EQUAL   * doctor not right
.
.         check if user has security access to this record
.
PROC2000  COMPARE   ONE,WTCNWLSC
          GOTO      PROC3000 IF NOT EQUAL
.
          PACK      KEY4,PASSCODE
          CALL      RDWTSEA1
          COMPARE   ZERO,OVRCD
          GOTO      PROC3000 IF EQUAL       * All access
          PACK      KEY13,PASSCODE,WMDOCTOR,WMUNIT
          CALL      RDWTSEI2
          BRANCH    OVRCD,PROC1000          * no access
.
PROC3000  CALL      PAT0000    
          BRANCH    OVRCD TO PROC1000
          MOVE      PURNO,WMURNO
          CALL      DET0000
          CALL      DOCT0000
          MOVE      DOCNAME,INDDOCNAM
          MOVE      WMDESC,DIM25
.
          ADD       ONE,UNIQUE
          MOVE      UNIQUE,DUNIQUE
          BRANCH    OPTION TO PROC4000,PROC5000
.
PROC4000  PACK      KEY25 WITH WMUNIT,WMPTY,WMURNO,WMCODE,DWMCNT
          RESET     KEY25
          WRITE     WATMP1,KEY25;WMURNO,WMCODE,DWMCNT,PSNAME,TEMP18,DIM25,PNAME:
                                 PATDOB,WMPTY,WMUNIT,DESCUNIT,WMDOCTOR:
                                 INDDOCNAM,PTELEP,PTELEB,DESCSEX,DSUNIT,WMSTAY:
                                 WMTIME,DAT110,DAT210,DATE40,DSPTY,PATAD
          GOTO      PROC6000
.
PROC5000  PACK      KEY28 WITH WMDOCTOR,WMPTY,WMURNO,WMCODE,DWMCNT
          RESET     KEY28
          WRITE     WATMP1,KEY28;WMURNO,WMCODE,DWMCNT,PSNAME,TEMP18,DIM25,PNAME:
                                 PATDOB,WMPTY,WMUNIT,DESCUNIT,WMDOCTOR:
                                 INDDOCNAM,PTELEP,PTELEB,DESCSEX,DSUNIT,WMSTAY:
                                 WMTIME,DAT110,DAT210,DATE40,DSPTY,PATAD
PROC6000  MOVE      ONE,TEMPFLG
          DISPLAY   *P29:24,*V2LON,WMURNO;
          GOTO      PROC1000
.
PROC7000  COMPARE   ZERO,TEMPFLG
          GOTO      PROC8000 IF EQUAL
          CALL      CLR1000
          GOTO      PROC9999
.
PROC8000  MOVE      TRUE,EXIT
          DISPLAY   *P1:24,*EL,*B,*V2LON,*P27:24,"***  No Data Extracted  ***":
                    *W;
.
PROC9999  RETURN
+
.****************************************************************************
.*        PRT0000                                                           *
.*        Print records                                                     *
.****************************************************************************
PRTT0000  DISPLAY   *P1:24,*EL,*P27:24,*V2LON,"***  Generating Report  ***",*W:
                    *HOFF,*P26:24,*EL,"Printing U/R Number : ";
.
          MOVE       ZERO,PATCNT
          MOVE       SP8,SWMURNO
.
          BRANCH     OPTION TO PRTT1000,PRTT2000
PRTT1000  PACK       KEY25 WITH SP30,SP30
          MOVE       WMUNIT,INDSORT1
          READ       WATMP1,KEY25;;
          GOTO       PRTT3000
.
PRTT2000  PACK       KEY28 WITH SP30,SP30
          MOVE       DOCNAME,INDSORT1
          READ       WATMP1,KEY28;;
.
PRTT3000  READKS     WATMP1;WMURNO,WMCODE,DWMCNT,PSNAME,TEMP18,DIM25,PNAME:  
                            PATDOB,WMPTY,WMUNIT,DESCUNIT,WMDOCTOR,INDDOCNAM:
                            PTELEP,PTELEB,DESCSEX,DSUNIT,WMSTAY,WMTIME:
                            DAT110,DAT210,DATE40,DSPTY,PATAD
          GOTO       PRTT9000 IF OVER
          BRANCH     OPTION TO PRTT4000,PRTT5000
.
PRTT4000  MATCH      WMUNIT,PWMUNIT
          GOTO       PRTT6000 IF EQUAL
          MOVE       WMUNIT,INDSORT1
          PACK       KEY5 WITH CODEWU,WMUNIT
          CALL       RDCODE1
          CALL       HEAD000
          GOTO       PRTT6000
.
PRTT5000  MATCH      WMDOCTOR,PWMDOCTOR
          GOTO       PRTT6000 IF EQUAL
          PACK       KEY6 WITH WMDOCTOR
          CALL       RDDOCT1
          BRANCH     OVRCD OF PRTT3000
          CALL       DOCT1000
          MOVE       WMDOCTOR,INDSORT1
          CALL       HEAD000
.
PRTT6000  COMPARE    "55",CLNO
          CALL       HEAD000 IF NOT LESS
          MOVE       WMURNO,DIMURNO
.
          MATCH      SP8,SWMURNO
          GOTO       PRTT7000 IF EQUAL
          MATCH      SWMURNO,WMURNO
          GOTO       PRTT7000 IF NOT EQUAL
          SUB        ONE,PATCNT
.
          COMPARE    TEN2,CLNO
          GOTO       PRTT7000 IF EQUAL
          PACK       DIMURNO,SP8
          PACK       PNAME,SP30,SP30
          GOTO       PRTT8000
.
PRTT7000  CALL       LINE0000
.
PRTT8000  PRINT      *1,"| ",DIMURNO,*12,"| ",PNAME,*48,"| ",PATDOB:
                     *60,"| ",WMCODE,*72,DIM25,*98,"|":
                     *100,DAT110,*113,"|",*115,DAT210,*132,"|"
                     
          ADD        ONE,CLNO
          ADD        ONE,PATCNT                    * add one to patient counter
          MOVE       WMURNO,SWMURNO
          MOVE       WMUNIT,PWMUNIT
          MOVE       WMDOCTOR,PWMDOCTOR
          DISPLAY    *P48:24,*V2LON,WMURNO;
          GOTO       PRTT3000
.
. ----- Last record in tempfile read -----
.
PRTT9000  CALL       ENDR0000               * end-of-report details
.
PRTT9999  RETURN
+
.*****************************************************************************
.         printing END of Report details
.*****************************************************************************
ENDR0000  COMPARE    ZERO,CPAGENO
          GOTO       ENDR9000 IF EQUAL
.
          CALL       LINE0000
.
          PRINT      *N,*1,"Number of Patients printed : ",PATCNT:
                     *N,*N,*N,"***  End of Report  ***"
          DISPLAY    *P1:24,*EL,*V2LON,*P22:24:
                     "***  Report Generation Completed  ***",*W;
          RETURN
.
ENDR9000  DISPLAY    *P1:24,*EL,*B,*V2LON,*P26:24:
                     "***  No Report Generated  ***",*W;
          RETURN
+
.****************************************************************************
.*        Heading for print                                                 *
.****************************************************************************
HEAD000   COMPARE   ZERO,CPAGENO
          CALL      LINE0000 IF NOT EQUAL
.
          STRIP     DESCOPT
          CALL      IBACLOCK
          MOVE      ONE,CNOUNDLN
          PACK      CPHDROPT,DESCOPT,SEQUENCE   
.
          CALL      HEAD132
.
          BRANCH    ALLFLG TO HEAD1000          * 1= for all units/doctors
.
HEAD1000  UNPACK    FROMDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *40,"From :  ",CPCDATE
.
          UNPACK    TODATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *40,"To   :  ",CPCDATE
.
          BRANCH    OPTION TO HEAD2000,HEAD3000
.
HEAD2000  PRINT     *N,*1,"For ",DDESCWU," : ",INDSORT1," ",TDESC
          GOTO      HEAD4000
.
HEAD3000  PRINT     *N,*1,"For Doctor : ",DOCNAME
.                                                  
HEAD4000  CALL      LINE0000
          PRINT      *1,"|  U/R No.",*12,"| Patient's Name":
                    *48,"|   D.O.B.",*60,"| Procedure Code & Description":
                    *98,"| Date On List",*113,"| Last Review Date",*132,"|"
          MOVE      TEN,CLNO
          RETURN
+
.****************************************************************************
.
LINE0000  PRINT     *1,"*--------------------------------------------":
                       "---------------------------------------------":
                       "-----------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.******************************************************************************
.*        Get treatment details description                                   *
.******************************************************************************
DET0000   MOVE      WMDESC,DIM25
          PACK      DAT110 WITH SP10
          PACK      DAT210 WITH SP10
.
          MATCH     SP2,WMDATE1
          GOTO      DET1000 IF EQUAL
          UNPACK    WMDATE1 INTO XCENT,XYEAR,XMON,XDAY
          PACK      DAT110 WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
DET1000   MATCH     SP2,WMDATE2
          GOTO      DET9999 IF EQUAL
          UNPACK    WMDATE2 INTO XCENT,XYEAR,XMON,XDAY
          PACK      DAT210 WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
DET9999   RETURN
+
.*****************************************************************************
.         Get patient name in a DIM33  and patdob (dd/mm/ccyy)
.*****************************************************************************
PAT0000   PACK      PNAME WITH SP30,SP30
          MOVE      SP10,PATDOB
          MOVE      WMURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF PAT9999
.
          CALL      NAME0000
          UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
          PACK      PATDOB WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
PAT9999   RETURN
+
.**********************************************************************
.*                             NAME0000                               *
.*                     pack patient's NAME                            *
.**********************************************************************
NAME0000  STRIP     PSNAME
          STRIP     PGNAME
          STRIP     PTITL
          PACK      PNAME,PTITL,SP1,PGNAME,SP1,PSNAME,SP30,SP30
          RETURN
+
.****************************************************************************
.*          concatenate DOCTor's name                                       *
.****************************************************************************
DOCT0000  MOVE      WMDOCTOR,KEY6
          CALL      RDDOCT1
DOCT1000  MOVE      DGNAME,ANS
          PACK      DOCNAME WITH DTITL,SP1,ANS,DOT,DSNAME
          RETURN
+
.****************************************************************************
.
          INC       STD001IO       
.
          INC       PATCODKY
          INC       PATDOCKY
          INC       TFILENAM
.
          INC       IBASEQIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       WATTR1IO/INC
          INC       WATSEAIO/INC
          INC       WATSEIIO/INC
          INC       WEBERRIO/INC
+

.******************************************************************************
.* System    :   Patient Management System                                    *
.* Program   :   IBAPAT56  (renamed from F92PTGSR)                            *
.* Desc      :   Reload patgsrnf from the patma1af file                       *
.******************************************************************************
.* Date      :   09/05/2002                                                   *
.* Author    :   Jill Habasque (copy of FXPATGSR with delete for merges)      *
.* Function  :   This program will loop through the patma1af file and         *
.*               for each record found, will create a record in the           *
.*               patgsrnf file.                                               *
.* Mods      :                                                                *
.*        V12.00.01 23/05/2025  Bella Turco       TSK 0955096                 *
.*                  Alphanumeric changes                                      *
.*        V10.02.02 13/07/2011  Steve Armstrong   CAR 240722                  *
.*                  Further mods to cater for second given name as            *
.*                  part of PATGSRFD keys                                     *
.*        V10.02.01 22/06/2011  Steve Armstrong   CAR 240722                  *
.*                  Mods to cater for changes to PATGSRFD:                    *
.*                       - GSRSNAM & GSRGNAM extended to 30 chars.            *
.*                       - PTGSGNM2 added.                                    *
.*                       - all indexes changed in length.                     *
.******************************************************************************
.* 05/07/2010    Jill Parkinson CAR 225196                                    *
.*               Mods to regenerate soundex key even if record exists         *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATMA1FD/INC                 * patient master file
          INC       PATGSRFD/INC                 * surname / given name file
          INC       PMSPX2FD/INC
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTR1   FORM      10
COUNTR2   FORM      10
.
.
. PROGRAM CONSTANTS
. -----------------
.
.
PRGID     INIT      "IBAPAT56"
PRGNAM    INIT      "Reload Surname File"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                              ML0000                                        *
.*                      Controlling Logic (Mainline code)                     *
.******************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML2000:        * proceed with check of merged recs
                            ML1000         * proceed with reload
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      CHEK0000               * process
          GOTO      ML0100
.
ML9999    CHAIN     PGM                    * chain back to program
+
.******************************************************************************
.*                                INIT0000               Called by: ML0000    *
.*                             initialisation                                 *
.*  The initialisation routine is used to display headings and open files.    *
**.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patgsrnf";
          OPEN      PATGSRN1,"patgsrnf"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
INIT9999  RETURN
+
.******************************************************************************
.*                                OPTN0000               Called by: ML0000    *
.*                        get user options or exit                            *
.*                                                                            *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                             *
.*                        TRUE  (1)  Exit option selected                     *
.******************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Surname File Reload"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                               CHEK0000                  Called by: ML0000  *
.*              Check if all records on the master file are valid             *
.******************************************************************************
.
CHEK0000  MOVE      ZERO,COUNTR1                 * initialise counters
          MOVE      ZERO,COUNTR2
          CALL      HEAD80
          PRINT     *1,"New Records"
          DISPLAY   *P1:24,*EL,"Processing:";
.
          MOVE      SP8,KEY8
          CALL      RDSMAST1                     * position at start of file
CHEK0500  CALL      RDKMAST1                     * read next record
          BRANCH    OVRCD,CHEK9000               * eof - finished
.
          CALL      CLPMSPX2
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
.
          ADD       ONE,COUNTR2                  * increment total record count
          IF        (COUNTR2%1000) = 0 | COUNTR2 = 1
            DISPLAY   *P13:24,*EL,*V2LON,PURNO;
          ENDIF
.
          COMPARE   ZERO,PSTAT                   * valid U/R ?
          IF        @EQUAL
            CALL      PROC0000                   * write surname record
          ENDIF
          COMPARE   ONE,PSTAT
          IF        @EQUAL
            CALL      REMM0000                   * removed merged patients
          ENDIF
.
          GOTO      CHEK0500                     * get next record
.
CHEK9000  DISPLAY   *P1:24,*EL,"Processing Complete.  ";
          CALL      HITENTER
.
CHEK9999  RETURN
+
.******************************************************************************
.*                               PROC0000                Called by: CHEK0000  *
.*              Write a new record to the patgsrnf file                       *
.******************************************************************************
.
PROC0000  MOVE      PTMASNAM,GSRSNAM    * Set up the Surname
          MOVE      PMPXFNAM,GSRGNAM    * Set up the First Given Name
          MOVE      PMPXSNAM,PTGSGNM2   * Set up the Second Given Name
          MOVE      PURNO,GSRURNO       * Set up the U/R Number
          MOVE      PBDATE,GSRDOB       * Set up the DOB
          MOVE      PSEX,GSRSEX         * Set up the Sex
          MOVE      ZEROVISN,GSRBILL    * Set up the billing number
.
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      RDPTGSR1            * Check if this record exists
          BRANCH    OVRCD,PROC1000      * Old record not found
.
.         Old record found, update with the U/R number
.
          MOVE      PURNO,GSRURNO       * Set up the U/R number
          MOVE      ZEROVISN,GSRBILL    * Change to a billing number of zero
          MOVE      ZERO,GSRSYS         * Change to a system of zero
          MOVE      PBDATE,GSRDOB       * Date of birth
          MOVE      PSEX,GSRSEX         * Sex
          CALL      SOUNDEX             * get surname soundex
          CALL      SOUNDX2             * get given name soundexs
          CALL      UPPTGSR1            * Update the surname record
          GOTO      PROC9999
.
.         Write the surname index record
.
PROC1000  MOVE      PURNO,GSRURNO       * Set up the U/R number
          MOVE      ZEROVISN,GSRBILL    * Set up the Billing number
          MOVE      ZERO,GSRSYS         * Set up the System
          MOVE      PBDATE,GSRDOB       * Date of birth
          MOVE      PSEX,GSRSEX         * Sex
          CALL      SOUNDEX             * get surname soundex
          CALL      SOUNDX2             * get given name soundexs
.
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      RAPTGSR1            * is it already on file
          BRANCH    OVRCD,PROC9000
          CALL      UPPTGSR1            * yes - update it
.
          GOTO      PROC9999
.
PROC9000  CALL      WRPTGSR1            * Write the new surname record
          PRINT     *1,GSRURNO
.
PROC9999  RETURN
+
.******************************************************************************
.*                               REMM0000                 Called by: CHEK0000  *
.*              Remove merged UR from the patgsrnf file                       *
.******************************************************************************
.
REMM0000  MOVE      PTMASNAM,GSRSNAM    * Set up the Surname
          MOVE      PMPXFNAM,GSRGNAM    * Set up the First Given Name
          MOVE      PMPXSNAM,PTGSGNM2   * Set up the Second Given Name
          MOVE      PURNO,GSRURNO       * Set up the U/R Number
          MOVE      PBDATE,GSRDOB       * Set up the DOB
          MOVE      PSEX,GSRSEX         * Set up the Sex
          MOVE      ZEROVISN,GSRBILL    * Set up the billing number
.
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      RDPTGSR1            * Check if this record exists
          BRANCH    OVRCD,REMM9999      * Old record not found
.
          CALL      DEPTGSR1
REMM9999  RETURN
.
. =============================================================================
.         I/O Includes
. =============================================================================
.
          INC       STD001IO
.
          INC       CLPMSPX2
          INC       PATSNDX
          INC       PATSNX2
.
          INC       PATMA1IO/INC                 * patient master file
          INC       PATGSRIO/INC                 * surname / given name file
          INC       PMSPX2IO/INC

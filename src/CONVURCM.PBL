.******************************************************************************
.* System    :   MEDICAL RECORDS TRACKING                                     *
.* Program   :   CONVURCM                                                     *
.* Desc      :   Convert U/R comments                                         *
.******************************************************************************
.* Date      :   07/05/1999                                                   *
.* Author    :   Greg Horvat                                                  *
.* Mods      :                                                                *
.*        V5.08.01  01/05/2000  Greg Horvat                                   *
.*                  Changed to open patmx1af when opening patma1af            *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCOMFD/INC
          INC       PATMA1FD/INC
.
TMPURCM   FILE      ASCII, FIXED=79
.
.Name     Type   Length  Start loc.  Description
.----     ----   ------  ----------  -----------
XURNO     DIM       8         1       U/R number
XCOMMENT  DIM      70         9       Comment
.
.End of Record               79
.
. ----- Program variables -----
.
COUNT     FORM      6
ERRMSG    DIM       100
KEY127    DIM       127
TEMP      FILE      ASCII, FIXED=256
.
. ----- Program constants -----
.
SP127     INIT      "                                          ":
                    "                                          ":
                    "                                           "
.
PRGNAM    INIT      "Convert U/R Comments Data"
PRGID     INIT      "CONVURCM"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables
          BRANCH    EXIT,ML9000
.
ML1000    CALL      OPTN0000                * Select option
          BRANCH    EXIT,ML9000
.
          CALL      CLER0000                * Clear the files before proceeding
          BRANCH    EXIT,ML9000
.
          CALL      OPEN0000                * Open files
          CALL      PROC0000                * Process the file
.
ML9000    CLOSE     TEMP,DELETE
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                            Initialise Variables                            *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TMPURCM,"convurcm"
          COMPARE   ONE,OVRCD
          GOTO      INIT9000 IF NOT EQUAL   * File found
.
          DISPLAY   *P1:24,*EL,*B,"convurcm.txt does not exist.  ";
          CALL      HITENTER
          GOTO      INIT9500
.
INIT9000  MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9500  MOVE      ONE,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      SP1,ANS
          MOVE      FALSE,EXIT
.
          DISPLAY   *P1:4,*EF,"This program converts all records in the ":
                    "convurcm.txt file";
          KEYIN     *P1:24,*EL,"Ok to Continue (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSY,ANS
          GOTO      OPTN9000 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      OPTN9500 IF EQUAL
.
          BEEP
          GOTO      OPTN0000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  CLER0000              Called by: ML0000   *
.*                      Clear The Files Before Proceeding                     *
.******************************************************************************
CLER0000  KEYIN     *P1:24,*EL,*B,"Clear the files before proceeding (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          GOTO      CLER1000 IF EQUAL
.
          MATCH     ANSN,ANS
          GOTO      CLER9000 IF EQUAL
.
          BEEP
          GOTO      CLER0000
.
CLER1000  DISPLAY   *P1:24,*EL,"Checking patcomaf",*W;
          EXECUTE   "ldat patcomaf.dat > temp.txt",TASKID
          CALL      FFLP0000                * Find file path
          BRANCH    EXIT,CLER9500
.
          DISPLAY   *P1:24,*EL,"Saving patcomaf",*W;
          EXECUTE   "cp `ldat patcomaf.dat` savcomaf.dat",TASKID
          EXECUTE   "cp `ldat patcomaf.idx` savcomaf.idx",TASKID
.
          DISPLAY   *P1:24,*EL,"Deleting patcomaf",*W;
          MOVE      SP127,KEY127
          CLEAR     KEY127
          APPEND    "iserase ",KEY127
          APPEND    KEY99,KEY127
          RESET     KEY127
          EXECUTE   KEY127,TASKID
.
          DISPLAY   *P1:24,*EL,"Creating patcomaf",*W;
          CLEAR     KEY127
          APPEND    "isbuild ",KEY127
          APPEND    KEY99,KEY127
          APPEND    " 100 UC1-8,9-11",KEY127
          RESET     KEY127
          EXECUTE   KEY127,TASKID
.
CLER9000  MOVE      ZERO,EXIT
          GOTO      CLER9999
.
CLER9500  MOVE      ONE,EXIT
CLER9999  RETURN
+
.******************************************************************************
.*                                  FFLP0000              Called by: CLER0000 *
.*                               Find File Path                               *
.******************************************************************************
FFLP0000  CLOSE     TEMP
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TEMP,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,FFLP3000
.
          READ      TEMP,SEQ;KEY99
          GOTO      FFLP3000 IF OVER
.
          ENDSET    KEY99
FFLP1000  CMATCH    ".",KEY99
          GOTO      FFLP2000 IF EQUAL
.
          BUMP      KEY99,-1
          GOTO      FFLP1000 IF NOT EOS
          GOTO      FFLP3000
.
FFLP2000  BUMP      KEY99,-1
          LENSET    KEY99
          RESET     KEY99
          PACK      KEY99,KEY99,SP127
          STRIP     KEY99
          GOTO      FFLP9000
.
FFLP3000  DISPLAY   *P1:24,*EL,*B,"Error clearing files.  Files not cleared.  ";
          CALL      HITENTER
          GOTO      FFLP9500
.
FFLP9000  MOVE      ZERO,EXIT
          GOTO      FFLP9999
.
FFLP9500  MOVE      ONE,EXIT
FFLP9999  RETURN
+
.******************************************************************************
.*                                  OPEN0000                                  *
.*                                 Open Files                                 *
.******************************************************************************
OPEN0000  DISPLAY   *P56:24,"Opening patcomaf";
          OPEN      PATCOMA1,"patcomaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
OPER9000  MOVE      ZERO,EXIT
OPEN9999  RETURN
+
.******************************************************************************
.*                                  PROC0000              Called by: ML0000   *
.*                              Process The File                              *
.******************************************************************************
PROC0000  CLOCK     TIME,CTIMEIS
          CALL      HEAD0000                * Print report header
          DISPLAY   *P1:6,*EF,"Started : ",*V2LON,CTIMEIS,*HOFF:
                    *P30:6,"Finished : ":
                    *P1:8,"U/R Number : ":
                    *P1:9,"Records    : ";
.
PROC1000  READ      TMPURCM,SEQ;XURNO,XCOMMENT
          GOTO      PROC8000 IF OVER
.
          ADD       ONE,COUNT
          IF        COUNT%100=1
            DISPLAY   *P14:8,*V2LON,XURNO:
                      *P14:9,COUNT;
          ENDIF
.
          PACK      KEY8,XURNO
          CALL      RDMAST1                 * Read a master file record
          IF        OVRCD=1
            MOVE      "Invalid U/R number",ERRMSG
            CALL      PTER0000              * Print error
            GOTO      PROC1000
          ENDIF
.
          MOVE      XURNO,PTCOURNO
          PACK      KEY11,XURNO,ANSZ,ANSZ,ANSZ
          CALL      RSPTCOM1                * Position on & read a U/R comments
          CALL      RPPTCOM1                  file record
          BRANCH    OVRCD,PROC2000
.
          MATCH     XURNO,PTCOURNO
          GOTO      PROC3000 IF EQUAL       * Same U/R no
.
PROC2000  MOVE      ZERO,PTCOCLNO
PROC3000  ADD       ONE,PTCOCLNO
.
          MOVE      XCOMMENT,PTCOLINE
          MOVE      SP4,PTCOOPER
          MOVE      SP20,PTCOSPAR
.
          PACK      KEY11,PTCOURNO,PTCOCLNO
          CALL      RAPTCOM1                * Read a U/R comments file record
          COMPARE   ONE,OVRCD
          GOTO      PROC3000 IF NOT EQUAL   * Record exists
.
          CALL      WRPTCOM1                * Write a U/R comments file record
          GOTO      PROC1000
.
PROC8000  CLOCK     TIME,CTIMEIS
          DISPLAY   *P41:6,*V2LON,CTIMEIS,*P14:9,COUNT,*HOFF:
                    *P1:24,*EL,*B,"Conversion finished.  ";
          CALL      HITENTER
          CALL      UND132
          PRINT     *N,"*** End of Report ***";
.
PROC9000  MOVE      ZERO,EXIT
PROC9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: PROC0000 *
.*                             Print Report Header                            *
.******************************************************************************
HEAD0000  CALL      HEAD132                 * Print report header
          CALL      UND132
          PRINT     *1,"|U/R Number|Error",*132,"|"
          CALL      UND132
          ADD       ONE,CLNO
.
HEAD9000  MOVE      ZERO,EXIT
HEAD9999  RETURN
+
.******************************************************************************
.*                                  PTER0000              Called by: PROC0000 *
.*                                 Print Error                                *
.******************************************************************************
PTER0000  COMPARE   SIXTY,CLNO
          CALL      HEAD0000 IF NOT LESS    * Line count >= 60
.
          PACK      ERRMSG,ERRMSG,SP127
          PRINT     *1,"| ",XURNO," | ",ERRMSG,*132,"|"
          ADD       ONE,CLNO
.
PTER9000  MOVE      ZERO,EXIT
PTER9999  RETURN
+
.==============================================================================
.
          INC       STD001IO
.
          INC       PATCOMIO/INC
          INC       PATMA1IO/INC
+
.


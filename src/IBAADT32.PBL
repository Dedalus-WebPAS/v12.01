.******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAADT32                                                 *
.* Description    :  Journals Upload                                          *
.******************************************************************************
.* Date           :  11/12/2009                                               *
.* Author         :  J.Tan                                                    *
.* Function       :  Upload Journal from ASCII file                           *
.* Modifications  :                                                           *
.******************************************************************************
.* V12.00.01  28/05/2025  J.Tan          TSK 0955096                          *
.*            Added Alphanumeric visit number changes                         *
.* V11.05.03  05/03/2025  J.Tan   TSK 0955356                                 *
.*            Added SKEY40 for PATCALFN                                       *
.* V11.05.02  21/02/2025  J.Tan   TSK 0955356                                 *
.*            Added FININVN,FINADMN,FINURNO                                   *
.* V11.05.01 27/11/2024  Thanh T        TSK 0939466                           *
.*           Added Distance in kms field                                      *
.******************************************************************************
.*                   V11.02.01  12/07/2022  Jacob Jackson  TSK 0920361        *
.*                              Open patijrnf so RDIJRN1/WRIJRN1 can run      *
.*                   V11.01.01  18/02/2021  J.Tan          TSK 0888639        *
.*                              Added PTDTTMNO - Theatre Team no              *
.*                   V10.13.02  19/09/2018  J.Tan        TSK 0863727          *
.*                              Added Restrictive Override code               *
.*                   V10.13.01  21/08/2017  J.Tan        TSK 0859742          *
.*                              Added ECLIPSE new fields from aaedtr          *
.*                   V10.07.01  21/12/2015  J.Tan          CAR 0303942        *
.*                              Mods to initialise Payment code               *
.*                   V10.04.02  16/04/2014  J.Tan         CAR 261630          *
.*                              Removed patauj audit files                    *
.*                   V10.04.01  20/01/2014  J.Tan        CAR 249828           *
.*                              Added PTDTPMBS - Patient MBS Team and Counter *
.*                   V10.01.01  Ebon Clements  CAR 233156                     *
.*                              Added created by to journals                  *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCONFD/INC
          INC       PATCODFD/INC            * Codes file
          INC       PATJRNFD/INC            * Journal file
          INC       PATDTRFD/INC
          INC       PATINVFD/INC
          INC       PATMI1FD/INC
          INC       PATVISFD/INC
          INC       OUTSITFD/INC
          INC       OUTDTRFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       AAEDTRFD/INC
          INC       EMRVISFD/INC
          INC       PATMA1FD/INC
          INC       PATIJRFD/INC
          INC       PATFINFD/INC
          INC       PATFIGFD/INC
          INC       NZPFINFD/INC
          INC       NZPFIGFD/INC
          INC       NZPRFNFD/INC
          INC       PATRFNFD/INC
          INC       PATDRGFD/INC
          INC       PATRFGFD/INC
          INC       PMSVX1FD/INC
.
.         Journals Upload file
.
JOURNTXT  FILE       HL7, FIXED=1000        * Upload file for journals
.
.Name     Type     Size     Start
.----     ----     ----     -----
XINVNO    DIM         8     Invoice Number
XADMNO    DIM         8     Admission Number
XJRTYP    DIM         2     Journal type
XJRDES    DIM        20     Journal Description
XJRAMT    DIM      12.2     Amount
.
. ----- Program Variables -----
.
BATCHNO   FORM      8
CRETURN   INIT      015                * carriage return character
CMDLINE   DIM       100
CODEJ     INIT      "J "
ERRFLAG   FORM      1
ERRNUMB   FORM      6
FORM8     FORM      8
FNAMEI    DIM       150
FNAMEB    DIM       8
FGSTFLAG  FORM      1
FORM12P2  FORM      12.2
GSTAMT    FORM      12.2
HEADSEC   FORM      5
IBCNMHOS  FORM      1
JOURAMTT  FORM      12.2
JOURDATE  DIM        8
JTYPE     DIM        1
MAXGSTJ   FORM      12.2
RECNUMB   FORM       6
RECUPDT   FORM       6
USERID    DIM       10
PATHNAME  DIM       100
PNAME     DIM       25
SEC1      FORM      "00001"
SKEY40    DIM       40
TIMEIS    DIM       8
TOTAMNT   FORM      12.2
.
XFLD0001  DIM       30
XFLD0002  DIM       30
XFLD0003  DIM       30
XFLD0004  DIM       30
XFLD0005  DIM       30
.
PRGID     INIT      "IBAADT32"
PRGNAM    INIT      "Upload Journals"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000              * Initailize variables & open files
          CALL      KOPT0000              * Keyin Upload or Update option
          BRANCH    EXIT,ML9999
.
          CALL      KASC0000              * Keyin ascii file
          BRANCH    EXIT,ML9900
.
          CALL      KUSR0000              * Keyin user id
          BRANCH    EXIT,ML0000
.
          CALL      KPTH0000              * Keyin pathname
          BRANCH    EXIT,ML0000
.
          CALL      PRHD0000              * Print header
          CALL      POST0000              * Updating file
          GOTO      ML9999
.
ML9900    CALL      HEAD132
          PRINT     *N,"Failed to open upload file. "
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000                                   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patjrnlf"
          OPEN      PATJRNL1,"patjrnlf"
          OPEN      PATIJRN1,"patijrnf"
.
          OPEN      PATDTRA1,"patdtraf"
          OPEN      AAEDTRE1,"aaedtref"
          OPEN      PATINVR1,"patinvrf"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATDTRA1,"patdtraf"
          OPEN      PATDTRA1,"patdtraf"
          OPEN      OUTSITA1,"outsitaf"
          OPEN      PATFINS1,"patfinsf"
          OPEN      PATFINS1,"patfinsf"
          OPEN      EMRVISA1,"emrvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*2,CDD,CMM,CYY,CCC:
                                  *43,IBCNMHOS
          READ      CONTROLF,FIFTY9;*62,CINVJRN
          READ      CONTROLF,NINTY8;*130,PTCNAGST
          READ      CONTROLF,SEVENTY9;*69,PTCNJFEE
.
          MOVE      ZERO,ERRFLAG
INIT9999  RETURN
+
.******************************************************************************
.*                                 KOPT0000                                   *
.*                            Upload Journals                                 *
.******************************************************************************
KOPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COPTION
          DISPLAY   *P1:6,*EF:
                    *P1:6,*V2LON,"0",*HOFF," = Exit":
                    *P1:7,*V2LON,"1",*HOFF," = Upload Journals":
                    *P1:10,"Select Option : ";
.
KOPT1000  KEYIN     *P17:10,*V2LON,COPTION;
          BRANCH    COPTION,KOPT9999
.
          COMPARE   ZERO,COPTION
          GOTO      KOPT9000 IF EQUAL
          BEEP
          GOTO      KOPT1000
.
KOPT9000  MOVE      ONE,EXIT
KOPT9999  RETURN
+
.******************************************************************************
.*                                 KASC0000                                   *
.*                            Keyin ASCI file                                 *
.******************************************************************************
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEI WITH SP70,SP70,SP70
.
          DISPLAY   *P19:13,*EF,"Journals Load File";
.
          KEYIN     *P1:14,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEI
.
          PACK      FNAMEI,FNAMEI,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEI
          GOTO      KASC9000 IF EQUAL
.
          SCAN      ".txt",FNAMEI
          IF        !@EQUAL
            SQUEEZE   FNAMEI
            ENDSET    FNAMEI
            APPEND    ".txt",FNAMEI      * add ".txt" if there is a path
            APPEND    SP70,FNAMEI
            RESET     FNAMEI
          ENDIF
          RESET     FNAMEI
.
          DISPLAY   *P26:14,*V2LON,*EL,FNAMEI;
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      JOURNTXT,FNAMEI
          TRAPCLR   IO
          BRANCH    OVRCD,KASC8000
.
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
+
.******************************************************************************
.*                                  KUSR0000                                  *
.*                     Keyin user id                                          *
.******************************************************************************
KUSR0000  KEYIN     *P1:16,*EL,"Keyin Username: ",*P26:16,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.******************************************************************************
.*                                  KPTH0000                                  *
.*                     Keyin path name                                        *
.******************************************************************************
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P1:17,*EL,"Keyin Pathname: ",*P26:17,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
.         PACK      PATHNAME,PATHNAME,SP70,SP70,SP70
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.******************************************************************************
.*                                  POST0000                                  *
.*                        Post record                                         *
.******************************************************************************
POST0000  MOVE      ZERO,RECNUMB
          MOVE      ZERO,RECUPDT
          MOVE      ZERO,ERRNUMB
          MOVE      ZERO,TOTAMNT
.
          OPEN      JOURNTXT,FNAMEI
POST1000  READ      JOURNTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005
          GOTO      POST9000 IF OVER
          ADD       ONE,RECNUMB
.
          MOVE      ZERO,FORM8
          PACK      KEY8,XFLD0001,SP70
          SQUEEZE   KEY8
          MOVE      KEY8,FORM8
          MOVE      FORM8,XINVNO
.
          PACK      KEY8,XFLD0002,SP70
          SQUEEZE   KEY8
          MOVE      KEY8,XADMNO
.
          UNPACK    SP70,XJRTYP,XJRDES,XJRAMT
          PACK      XJRTYP,XFLD0003,SP70
          PACK      XJRDES,XFLD0004,SP70
          MOVE      XFLD0005,XJRAMT
          CALL      CRET0000              * Check for carriage return
.
          CALL      VALJ0000                  * Validating
          BRANCH    EXIT,POST1000
.
          CALL      UJNL0000              * Update Journal Record
.
          ADD       ONE,RECUPDT
          ADD       JOURAMTT,TOTAMNT
.
          GOTO      POST1000
.
.
.
POST9000  CLOSE     JOURNTXT
.
          PRINT     *N,*1,"Number of Records in txt file : ",RECNUMB:
                    *N,*1,"Number of Records Updated     : ",RECUPDT:
                    *N,*1,"Number of Errors occured      : ",ERRNUMB:
                    *N,*1,"Total amount of Journals added: ",TOTAMNT
.
POST9999  RETURN
+
.******************************************************************************
.         Check for Carriage return
.******************************************************************************
CRET0000  MOVE      SP70,KEY15
          CLEAR     KEY15
          SQUEEZE   XJRAMT
          RESET     XJRAMT
          GOTO      CRET1200
.
CRET1000  BUMP      XJRAMT
          GOTO      CRET2000 IF EOS
.
CRET1200  MATCH     CRETURN,XJRAMT
          GOTO      CRET2000 IF EQUAL
.
          MOVE      XJRAMT,ANS
          APPEND    ANS,KEY15
          GOTO      CRET1000
.
CRET2000  APPEND    SP70,KEY15
          RESET     KEY15
          SQUEEZE   KEY15
          MOVE      ZERO,JOURAMTT
          MOVE      KEY15,JOURAMTT
CRET9999  RETURN
+
.******************************************************************************
.                             VALJ0000
.                         Validation
.******************************************************************************
VALJ0000  MATCH     SP70,XINVNO
          GOTO      VALJ8000 IF EQUAL
.
          PACK      KEY8,XINVNO,SP70
          CALL      RDINV1
          BRANCH    OVRCD,VALJ8000,VALJ8000
.
          MOVE      SP70,PNAME
          MOVE      PINVUR,KEY8
          CALL      RDMAST1                  * read masterfile
          IF        OVRCD=0
            MOVE      PGNAME,ANS
            PACK      PNAME,ANS,DOT,PSNAME
          ENDIF
.
          MATCH     DPINVADM,XADMNO
          GOTO      VALJ8200 IF NOT EQUAL     * mismatched
.
          MATCH     SP70,XADMNO
          GOTO      VALJ8100 IF EQUAL
.
          PACK      KEY8,XADMNO
          CALL      RDPMVX11
          BRANCH    OVRCD,VALJ8100
          CALL      RDVISA1
          BRANCH    OVRCD,VALJ8100
.
          BRANCH    PVITYPE,VALJ1000,VALJ2000,VALJ3000
          GOTO      VALJ8900
.
VALJ1000  MOVE      PVIBILL,KEY8
          CALL      RDEMVIS1
          BRANCH    OVRCD,VALJ8100
.
          MOVE      EMVICOMP,ACLAIM
          GOTO      VALJ5000
.
VALJ2000  MOVE      "out",OSTFILE
          MOVE      PVISITE,KEY6
          CALL      RDSITA1                 * Get the file header for this site
.
          PACK      CFNAME WITH OSTFILE,FILBB1A1
          CALL      OPOTBB11
.
          MOVE      PVIBILL,KEY8
          CALL      RDBOKB1
          BRANCH    OVRCD,VALJ8100
.
          MOVE      OBACOMP,ACLAIM
          GOTO      VALJ5000
.
VALJ3000  MOVE      PVIBILL,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,VALJ8100
.
VALJ5000  COMPARE   ZERO,JOURAMTT
          GOTO      VALJ8300 IF EQUAL
.
          MATCH     SP70,XJRTYP                * Journal type
          GOTO      VALJ8400 IF EQUAL
.
          PACK      KEY5,CODEJ,XJRTYP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,VALJ8500
          MOVE      THCSCOD,JTYPE
.
.         Check for GST Journal
.
          MATCH     "G",JTYPE
          GOTO      VALJ7000 IF NOT EQUAL   * not GST journal
.
          BRANCH    PINVSYS,VALJ8600     * not applicable for Emergency
.
          MOVE      PTINGSTA,MAXGSTJ
          ADD       PTINGSTJ,MAXGSTJ     * Maximum GST Journal amount
          MOVE      JOURAMTT,GSTAMT
          COMPARE   JOURAMTT,MAXGSTJ
          GOTO      VALJ8700 IF LESS      * invalid GST journal amount
.
VALJ7000  MOVE      ZERO,EXIT
          GOTO      VALJ99999
.
VALJ8000  CALL      PREC0000                  * Print record details
          PRINT     *77,"Invalid Invoice Number"
          GOTO      VALJ9000
.
VALJ8100  CALL      PREC0000                  * Print record details
          PRINT     *77,"Invalid Admission Number"
          GOTO      VALJ9000
.
VALJ8200  CALL      PREC0000                  * Print record details
          PRINT     *77,"Invoice/Admission Number mismatch"
          GOTO      VALJ9000
.
VALJ8300  CALL      PREC0000                  * Print record details
          PRINT     *77,"Journal Amount is zero"
          GOTO      VALJ9000
.
VALJ8400  CALL      PREC0000                  * Print record details
          PRINT     *77,"Invalid Journal Type"
          GOTO      VALJ9000
.
VALJ8500  CALL      PREC0000                  * Print record details
          PRINT     *77,"Journal Type not Found on Category/Codes Table"
          GOTO      VALJ9000
.
VALJ8600  CALL      PREC0000                  * Print record details
          PRINT     *77,"GST Journals are not applicable to Emergency."
          GOTO      VALJ9000
.
VALJ8700  CALL      PREC0000                  * Print record details
          PRINT     *77,"GST Adjustment must be less than $.",MAXGSTJ
          GOTO      VALJ9000
.
VALJ8800  CALL      PREC0000                  * Print record details
          PRINT     "Invoice Error Contact IBA "
          GOTO      VALJ9000
.
VALJ8900  CALL      PREC0000                  * Print record details
          PRINT     "Invalid Visit Type "
          GOTO      VALJ9000
.
VALJ9000  MOVE      ONE,EXIT
          ADD       ONE,ERRNUMB
VALJ9999  RETURN
+
.----------------------------------------------------------------------
. Subroutine to post a journal to an account
.----------------------------------------------------------------------
UJNL0000  MOVE      PVIBILL,KEY8
          CALL      TRVISA1
          CALL      CHKD0000                * get the DTR record
          COMPARE   ZERO,OVRCD
          GOTO      UJNL0000 IF EQUAL
          MOVE      PVITRAN,TTRANSN1
          MOVE      TTRANSN1,ATRANNO
.
          MOVE      ZERO,TPATAMTT
          MOVE      ZERO,TPATAMTP        * Inpatient
          MOVE      ZERO,TREBATE
          MOVE      ZERO,PTDTGSTM
          PACK      JOURDATE,CCC,CYY,CMM,CDD,SP70
          REP       " 0",JOURDATE
.
          MATCH     "G",JTYPE
          GOTO      UJNL0200 IF NOT EQUAL
.
          MOVE      GSTAMT,PTDTGSTM       * GST Adjustment amount
          MULT      SEQ,PTDTGSTM
          MOVE      PTDTGSTM,OTDTGSTM
          MULT      SEQ,GSTAMT
          MOVE      PTCNAGST,PTDTGSTC
          MOVE      PTCNAGST,OTDTGSTC
          GOTO      UJNL0300
.
UJNL0200  MATCH     "R",JTYPE
          IF        @EQUAL
            MOVE      ZERO,TPATAMTT
            MOVE      JOURAMTT,TREBATE
          ELSE
            MOVE      JOURAMTT,TPATAMTT
            MOVE      ZERO,TREBATE
          ENDIF
.
UJNL0300  MOVE      "P",TTYPEDEB
          MATCH     "A",JTYPE              * Adjustment Journal
          IF        @EQUAL
            MULT      SEQ BY TPATAMTT   * Reverse sign adj journals(DTRAN only)
          ENDIF
          MATCH     SP70,XJRDES
          IF        @EQUAL
            PACK      TDDESC,TDESC,SP70
          ELSE
            PACK      TDDESC,XJRDES,SP70
          ENDIF
.
          CALL      SETJNL00
.
          MATCH     "R",JTYPE
          GOTO      UJNL3000 IF EQUAL
.
          MOVE     "C",FINTYPE
          IF       PTCNJFEE=1
            PACK     FINCODE,ACLAIM,XJRTYP,SP20
.                            Claim Code, Journal Code
          ELSE
            PACK     FINCODE,XJRTYP,SP20
.                            Journal Code
          ENDIF
.
          MOVE     TPATAMTT,FINAMT
          MOVE     JOURDATE,FINDATE
.
          MOVE     PINVNO,FININVN
          MOVE     PINVADM,FINADMN
          MOVE     PINVUR,FINURNO
.
          MOVE     PVITYPE,FINSYS
          MOVE     PINVSITE,FINSITE
.
          MATCH    "G",JTYPE
          IF       @EQUAL
            IF       GSTAMT<>0
              MOVE     GSTAMT,FINAMT
              MULT     SEQ,FINAMT
              MOVE     ONE,FGSTFLAG
              MOVE     PMVXMHOS,FINHOSP
              CALL     PATCALF
            ENDIF
          ELSE
            MOVE     TPATAMTT,FINAMT
            MOVE     ZERO,FGSTFLAG
            MOVE     PMVXMHOS,FINHOSP
            CALL     PATCALF
          ENDIF
.
.         SEQ IS A FORM -1
.
          MULT      SEQ,TPATAMTT
          MULT      SEQ,ATPATAMT
          MULT      SEQ,OTPATAMT
.
          GOTO      UJNL4000
.
UJNL3000  MOVE      ZERO,TPATAMTP        * Inpatient
          SUB       TREBATE,TPATAMTP
.
          MOVE      ZERO,OTPATPOR        * Outpatient
          SUB       OTREBPOR,OTPATPOR
.
          MOVE      ZERO,ATPATPOR        * A&E
          SUB       ATREBPOR,ATPATPOR
.
UJNL4000  BRANCH    PVITYPE OF UJNL4300,UJNL4200,UJNL4100
          GOTO      UJNL9999
.
.         Inpatient invoice
.
UJNL4100  PACK      KEY22 WITH TADMNO,TREF,TTRANSN1,SP70
          CALL      WRDTRN1
          GOTO      UJNL5000
.
.         Outpatient invoice
.
UJNL4200  MOVE      "out",OSTFILE
          MOVE      PINVSITE,KEY6
          CALL      RDSITA1
.
          MOVE      ZERO,OTSERVS
          MOVE      SP70,OTDTDSKM
          PACK      OTSPARE,SP70
.
          PACK      CFNAME,OSTFILE,FILDTRO1
          PACK      KEY22 WITH OTNUMB,OTINVNO,OTTRANS,SP70
          OPEN      OUTDTRO1,CFNAME
.
          CALL      WRDTRO1
          CLOSE     OUTDTRO1
          MOVE      OTNUMB,TADMNO
          GOTO      UJNL5000
.
.         A & E invoice
.
UJNL4300  PACK      KEY22 WITH ATNUMB,ATINVNO,ATTRANS,SP70
          CALL      WRDTRE1
          MOVE      ATNUMB,TADMNO
.
.         Update the Journal summary file
.
UJNL5000  MOVE      XJRTYP,JCODE
          MOVE      JOURDATE,JDATE
.
          MOVE      TADMNO,JADMN
          MOVE      TTRANSN1,JTRNS
          MOVE      PINVNO,JINVN
          MOVE      SP70,PTJRINDC
.
          MOVE      USERID,PTJRCUID              * Created by
          PACK      PTJRCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTJRCDAT                * Date created
          CLOCK     TIME,PTJRCTIM                * Time created
          MOVE      SP70,JSPARE
.
          PACK      KEY24 WITH JCODE,JDATE,JADMN,JTRNS,SP70
          CALL      WRJRNL1
.
          MATCH     "B",JTYPE               * Bad Debt Journal
          GOTO      UJNL6000 IF NOT EQUAL
.
.         UPDATE BAD DEBT INDICATOR IN MASTER FILE
.
          MOVE      PINVUR,KEY8
          CALL      RDMAST1                 * read and lock masterfile
.
          MOVE      ONE,PBDEBT              * move "1",bad debt indicator
          CALL      UPMAST1                 * update master
.
.         NOW ADD TPATAMTP,JOURNAL FIELD IN INV1
.         NEG TAKEN CARE OF ABOVE
.
UJNL6000  MOVE      PINVNO,KEY8
          CALL      RDPTINV1
          BRANCH    OVRCD,UJNL9999
.
          MATCH     "R",JTYPE
          GOTO      UJNL6100 IF EQUAL
          ADD       TPATAMTT,PINVJOUR
.
          MATCH     "G",JTYPE
          GOTO      UJNL6200 IF NOT EQUAL
          ADD       GSTAMT,PTINGSTJ         * Add GST adjustment journal
          GOTO      UJNL6200
.
UJNL6100  ADD       TREBATE,PINVREB
.
.         Check if the invoice now balances
.
UJNL6200  MOVE      PINVAMT,FORM12P2     * Invoice amount +
          ADD       PINVPATA,FORM12P2    * Patient payments +
          ADD       PINVHFDA,FORM12P2    * H.F payments +
          ADD       PINVOTHA,FORM12P2    * Other payments +
          ADD       PTINCRTT,FORM12P2    * Credit Notes +
          ADD       PINVJOUR,FORM12P2    * Journals = amount outstanding
          ADD       PTINGSTJ,FORM12P2    * Journal GST = amount outstanding
.
          MOVE      "99999999",PINVBLCD  * Assume it doesn't balance
.
          COMPARE   ZERO,FORM12P2        * Does it balance ?
          GOTO      UJNL8000 IF NOT EQUAL
.
.         Invoice is now balanced. Record the date that this occured
.
          PACK      PINVBLCD,CCC,CYY,CMM,CDD
          REP       " 0",PINVBLCD
.
UJNL8000  PACK      PTINUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTINUDAT             * date updated
          CLOCK     TIME,PTINUTIM             * time updated
          MOVE      USERID,PTINUUID           * web user id
.
          CALL      UPINV1
          PROC      FLAGDEBT
.
          COMPARE   ONE,CINVJRN
          CALL      UPDJRN00 IF EQUAL
.
UJNL9999  RETURN
+
.----------------------------------------------------------------------
. CHKD0000 : Check the Debtors Transaction Records
.            Set up key depending on the system (A+E, Outpatient, Inpatient)
.----------------------------------------------------------------------
CHKD0000  BRANCH    PVITYPE,CHKD1000,CHKD2000,CHKD3000,CHKD2000
.
.         A & E Dtran
.
CHKD1000  PACK      KEY22,PVIBILL,PINVNO,PVITRAN
          CALL      RDADTRE1
          GOTO      CHKD9999
.
.         Outpatient Dtran
.
CHKD2000  MOVE      "out",OSTFILE
          MOVE      PINVSITE,KEY6
          CALL      RDSITA1
.
          PACK      CFNAME,OSTFILE,FILDTRO1
          OPEN      OUTDTRO1,CFNAME
.
          PACK      KEY22,PVIBILL,PINVNO,PVITRAN
          CALL      RDADTRO1
          GOTO      CHKD9999
.
.         Inpatient Dtran
.
CHKD3000  PACK      KEY22,AADMNO,PINVNO,PVITRAN
          CALL      RDADTRN1
.
CHKD9999  RETURN
.
.----------------------------------------------------------------------
.         Setup Journal record
.----------------------------------------------------------------------
.
SETJNL00  BRANCH    PINVSYS OF SETJNL10,SETJNL20,SETJNL30
          GOTO      SETJNL99
.
.         A & E Dtran setup
.
SETJNL10  MOVE      PVIBILL,ATNUMB
          MOVE      PINVNO,ATINVNO
          MOVE      TTRANSN1,ATTRANS
          MOVE      TDDESC,ATDDESC
          MOVE      TPATAMTT,ATPATAMT
          MOVE      TPATAMTT,ATPATPOR
          MOVE      TREBATE,ATREBPOR
          MOVE      JOURDATE,ATDDATE
          UNPACK    JOURDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,TFCENT
          MOVE      CYEAR,TFYEAR
          MOVE      CMON,TFMONTH
          MOVE      CDAY,TFDAY
.
          MOVE      SP10,ATITEMNO
          MOVE      XJRTYP,ATTYPE
          MOVE      ZERO,ATPAYTYP
          MOVE      SP20,ATRECEPT
          MOVE      ONE,ATINVPRT
          MOVE      THREE,ATRECTYP
          MOVE      SP3,ATMISGRP
          MOVE      SP3,ATDEPTYP
          MOVE      ZERO,ATBATCHN
          MOVE      ZERO,ATSPARE1
          MOVE      ZERO,ATSPARE2
          MOVE      SP70,ATSPARE
          PACK      ATDTEFFD,CCC,CYY,CMM,CDD
          REP       " 0",ATDTEFFD
          MOVE      SP70,ATDTPCOD
          PACK      ATDTACOI,SP70       * After Care Override Indicator
          PACK      ATDTDSOV,SP70       * Duplicate Service Override
          PACK      ATDTSTXT,SP70       * Service Text
          PACK      ATDTLSPN,SP70       * Location Specific Practice No(LSPN)
          PACK      ATDTMPOV,SP70       * Multi Procedure Override
          PACK      ATDTNMPT,SP70       * Number of Patients Seen
          PACK      ATDTSDCD,SP70       * Self Deem Code (Cat Sd)
          PACK      ATDTTDUR,SP70       * Time Duration (Mins)
          PACK      ATDTROVR,SP70       * Restricted Override Code (Cat Ro)
.
          MOVE      ZERO,ATDTSCHF
          MOVE      SP70,ATDTITYP
          GOTO      SETJNL99
+
.         Outpatient Dtran setup
.
SETJNL20  MOVE      PVIBILL,OTNUMB
          MOVE      PINVNO,OTINVNO
          MOVE      TTRANSN1,OTTRANS
          MOVE      TDDESC,OTDDESC
          MOVE      TPATAMTT,OTPATAMT
          MOVE      TPATAMTT,OTPATPOR
          MOVE      TREBATE,OTREBPOR
          MOVE      JOURDATE,OTDDATE
          UNPACK    JOURDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,TFCENT
          MOVE      CYEAR,TFYEAR
          MOVE      CMON,TFMONTH
          MOVE      CDAY,TFDAY
.
          MOVE      SP10,OTITEMNO
          MOVE      XJRTYP,OTTYPE
          MOVE      ZERO,OTPAYTYP
          MOVE      SP8,OTRECEPT
          MOVE      ONE,OTINVPRT
          MOVE      THREE,OTRECTYP
          MOVE      SP3,OTMISGRP
          MOVE      SP3,OTDEPTYP
          MOVE      ZERO,OTBATCHN
          MOVE      ZERO,OTNINVS
          MOVE      ZERO,OTSERVS
          MOVE      SP70,OTDTPCOD
          PACK      OTDTEFFD,CCC,CYY,CMM,CDD
          REP       " 0",OTDTEFFD
          MOVE      SP70,OTDTDSKM
          PACK      OTSPARE,SP70
          GOTO      SETJNL99
.
.         Inpatient Dtran setup
.
SETJNL30  MOVE      PVIBILL,TADMNO
          MOVE      PINVUR,TDCODE
          MOVE      SIX,TRECTYPE            * RECORD TYPE JOURNAL
          MOVE      ONE,TINVPRT             * set PRINT FLAG
          MOVE      ZERO,TPATAMTG
          MOVE      ZERO,TPATAMTH
          MOVE      ZERO,TPATAMTI
          MOVE      TPATAMTT,TPATAMTP
          MOVE      ZERO,TTDAY
          MOVE      ZERO,TTMONTH
          MOVE      ZERO,TTYEAR
          MOVE      ZERO,TTCENT
          MOVE      SP10,TITEMNO
          MOVE      XJRTYP,TTYPE
          MOVE      SP3,TMISGRP
          MOVE      ZERO,TPAYTYPE
          MOVE      PINVNO,TREF             * PUT ONTO INVOICE
          MOVE      ZERO,TNODAYS
          MOVE      SP20,TRECEIPT            * should key in ref
          MOVE      ZERO,TCLAIM
          MOVE      PINVCLM,TCLMTYP
          MOVE      SP70,TADMTYP
          MOVE      ZERO,TBATCHN
          MOVE      SP70,PTDTBTYP
          MOVE      ZERO,PTDTLSPT
          MOVE      ZERO,PTDTLSRB
          MOVE      ZERO,PTDTADPT
          MOVE      ZERO,PTDTADRB
          MOVE      ZERO,PTDTDTYP
          MOVE      ZERO,PTDTEPSD
          MOVE      ZERO,PTDTCCU
..          MOVE      SP6,PTDTSURG
..          MOVE      ZERO,PTDTAPAY
          PACK      PTDTDES2,SP20,SP20
          PACK      PTDTUNIQ,SP70
          MOVE      SP8,PTDTTIME                                       *I-61746
          MOVE      SP1,PTDTCRST                                       *I-61746
          MOVE      SP70,PTDTTMNO
          MOVE      SP70,PTDTPMBS
          PACK      TSPARE,SP30,SP30
          UNPACK    JOURDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,TFCENT
          MOVE      CYEAR,TFYEAR
          MOVE      CMON,TFMONTH
          MOVE      CDAY,TFDAY
          MOVE      SP70,PTDTPCOD
          PACK      PTDTEFFD,CCC,CYY,CMM,CDD
          REP       " 0",PTDTEFFD
          MOVE      SP70,PTDTPCOD
.
SETJNL99  RETURN
+
.----------------------------------------------------------------------
.         Subroutine to write to patient invoice with journal file
.----------------------------------------------------------------------
UPDJRN00  MOVE      ZERO,BATCHNO
          PACK      KEY16,BATCHNO,PINVNO
          CALL      RDIJRN1
          COMPARE   ZERO,OVRCD
          RETURN    IF EQUAL
.
          MOVE      PINVNO,IJINVNO
          MOVE      PVIBILL,IJADMNO
          MOVE      BATCHNO,IJBATCH
          MOVE      SP20,IJSPARE
          CALL      WRIJRN1
UPDJRN99  RETURN
+
.******************************************************************************
.         Print record
.******************************************************************************
PREC0000  PRINT     *1,XINVNO,*12,XADMNO,*24,XJRTYP,*35,XJRDES,*57,JOURAMTT;
PREC9999  RETURN
+
.******************************************************************************
.*                                  PRHD0000                                  *
.*                        Print the selection details                         *
.******************************************************************************
PRHD0000  CALL      HEAD132
.
          PRINT     *N,"Date               : ",CDATE:
                    *N,"Time               : ",CTIMEIS:
                    *N,"User ID            : ",USERID:
                    *N,"Journals Load file : ",PATHNAME:
                    *N,*N,*1,"Invoice No",*12,"  Adm.No",*22,"Journal Type":
                    *35,"Journal Desc.",*63," Amount",*77,"Error Description":
                       *N,*1,"----------",*12,"--------",*22,"------------":
                    *35,"-------------",*57,"---------------":
                    *77,"---------------------------------"
PRHD9999  RETURN
+
.******************************************************************************
          INC       STD001IO
          INC       PATCALFN
          INC       PMSOSDTM
.
          INC       PATCODIO/INC            * Codes file
          INC       PATJRNIO/INC            * Journal file
          INC       PATDTRIO/INC
          INC       PATINVIO/INC
          INC       PATMI1IO/INC
          INC       PATVISIO/INC
          INC       OUTSITIO/INC
          INC       OUTDTRIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       AAEDTRIO/INC
          INC       EMRVISIO/INC
          INC       PATMA1IO/INC
          INC       PATIJRIO/INC
          INC       PATFINIO/INC
          INC       PATFIGIO/INC
          INC       PATRFNIO/INC
          INC       PATDRGIO/INC
          INC       PATRFGIO/INC
          INC       NZPFINIO/INC
          INC       NZPFIGIO/INC
          INC       NZPRFNIO/INC
          INC       PMSVX1IO/INC
.
.
.
.

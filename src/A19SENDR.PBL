.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   A19SENDR                                                    *
.* Desc      :   This is the front-end test harness for testing the sending  *
.*               of an QRY^A19 message for the NSW UPI interface.            *
.*****************************************************************************
.* Date      :   18/01/2018                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will                                           *
.*                                                                           *
.* Mods      :                                                               *
.*        V10.14.01 13/03/2019  Steve Armstrong   TSK 0871671                *
.*                  Recompiled for A19HNDLR - check for usual address type   *
.*****************************************************************************
.*        V10.13.04 03/10/2018  Davin             TSK 0857734                *
.*                  Recompiled for A19HNDLR - remove "" from medi expiry date*
.*        V10.13.03 10/09/2018  Davin             TSK 0863288                *
.*                  Recompiled for A19HNDLR - additional array range check   *
.*        V10.13.02 07/09/2018  Davin             TSK 0863288                *
.*                  Recompiled for A19HNDLR - Check for max patient limit    *
.*        V10.13.01 28/08/2018  Davin             TSK 0857732                *
.*                  Recompiled for A19HNDLR (added NK1 and PID.26)           *
.*****************************************************************************
.*        V10.12.12 17/05/2018  Davin             TSK 0856298                *
.*                  Recompiled for A19HNDLR (log data in smaller sections)   *
.*        V10.12.11 08/05/2018  Davin             TSK 0844685                *
.*                  Recompiled for A19HNDLR (strip blanks from data array)   *
.*        V10.12.10 01/05/2018  Davin             TSK 0844685                *
.*                  Recompiled for A19HNDLR (added 8 fields to data array)   *
.*        V10.12.09 20/04/2018  Davin             TSK 0844685                *
.*                  Added medicare number search parameter (HL7PTMED)        *
.*        V10.12.08 20/04/2018  Davin             TSK 0844685                *
.*                  Recompiled for A19HNDLR (added NSLHD PersonID to QRD.8)  *
.*        V10.12.07 20/04/2018  Davin             TSK 0844685                *
.*                  Recompiled for A19HNDLR (cater for the QAK/PV1/OBX/OBX)  *
.*        V10.12.06 18/04/2018  Davin             TSK 0844685                *
.*                  Recompiled for A19HNDLR (added alias tempfile for PID.9) *
.*        V10.12.05 13/03/2018  Davin             TSK 0844685                *
.*                  Added exit option to search and recompiled for A19HNDLR  *
.*        V10.12.04 07/03/2018  Davin             TSK 0844685                *
.*                  Recompiled for A19HNDLR (remove STRTBLOK from QRD/QRF)   *
.*        V10.12.03 01/03/2018  Davin             TSK 0844685                *
.*                  Mods to receive query response and display results       *
.*        V10.12.02 02/02/2018  Davin             TSK 0844685                *
.*                  Recompiled for changes to A19UPIVR and A19HNDLR.         *
.*        V10.12.01 30/01/2018  Steve Armstrong   TSK 0844685                *
.*                  Added validation for minimum data entry.                 *
.*                  Recompiled for changes to A19UPIVR and A19HNDLR.         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       TFILEVAR/INC
          INC       A19UPIVR/INC
          INC       IBACONFD/INC
          INC       IBASEQFD/INC
          INC       PATCONFD/INC
          INC       WEBERRFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
DATEDIS1  DIM       10       * Date display 1
DATEDIS2  DIM       10       * Date display 2
FORM3     FORM      3
.
.
. PROGRAM CONSTANTS
. -----------------
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
SP100     INIT      "                                                  ":
                    "                                                  "
.
.
PRGID     INIT      "A19SENDR"
PRGNAM    INIT      "A19 Message Sender"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000:      * proceed with test message
                            MAIN1000:      * proceed with test connection
                            MAIN1000       * proceed with test patient retrieve
.
MAIN1000  CALL      PROC0000               * process
          GOTO      MAIN0100
.
MAIN9999  CALL      KILLT000               * remove existing file
          CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          CALL      CREAT000                     * create temporary table
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run Test message                        *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  CALL      DISPHEAD                  * display heading
          DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Test Message":
                    *P1:6,*V2LON,TWO,*HOFF,". Run Test Connect":
                    *P1:7,*V2LON,THREE,*HOFF,". Run Test Patient Retrieve"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run test message
                            OPTN9000:            * run test connection
                            OPTN9000             * run test patient retrieve
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*     Get the patient search criteria and trigger a QRY^A19 message         *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,HL7SMODE                * init vars for initial search
          MOVE      ONE,HL7MXREC
          PACK      HL7UPIVL,SP70
          PACK      HL7NSPID,SP70
          PACK      HL7SURNM,SP70
          PACK      HL7FGNAM,SP70
          PACK      HL7SGNAM,SP70
          PACK      HL7GENDR,SP70
          PACK      HL7PTDOB,SP70
          PACK      HL7PTMED,SP70
          PACK      HL7CONTP,SP70,SP70,SP70
.
          IF        COPTION = 2
            PACK    HL7CONTP,SP70,SP70,SP70
            MOVE    THREE,HL7SMODE               * set mode for connection only
            GOTO    PROC1000
          ENDIF
.
          IF        COPTION = 3
.davupi     CALL      GUPI0000                   * get single patient ID
            CALL      GLHD0000                   * get NSLHD PersonID
            MOVE      TWO,HL7SMODE               * set mode for UPI retrieve
            GOTO      PROC0500
          ENDIF
.
          CALL      GSNM0000                     * get patient surname
          CALL      GFGN0000                     * get patient first name
          CALL      GSGN0000                     * get patient second name
          BRANCH    EXIT,PROC9999                * nothing entered
.
          CALL      GSEX0000                     * get patient gender
.
          CALL      GDOB0000                     * get patient DoB
.
          CALL      GMED0000                     * get patient medicare number
.
          CALL      GMAX0000                     * get max no. of records
.
PROC0500  CALL      CONTQST                      * Ok to continue ?
          BRANCH    CEXIT,PROC1000:              * yes
                          PROC0000:              * no
                          PROC9999               * cancel
.
.         We are sending the message
.
PROC1000  CALL      A19HNDLR                     * format and send message
          BRANCH    EXIT,PROC9100:               * connection failure
                         PROC9200:               * timeout on response
                         PROC9300:               * insufficient data provided
                         PROC9400:               * blank destination details
                         PROC9500:               * socket connection failed
                         PROC9600                * response format error
.
PROC9000  DISPLAY   *P1:10,*EF,"Message successfully sent.  "
.
          IF        COPTION <> 2
            CALL      DISP0000                   * display response message
            CALL      DISRES00                   * display results to screen
            BRANCH    EXIT,PROC9999              * exit search
          ENDIF
.
          MATCH     SP100,HL7CONTP
          IF        !@EQUAL
            MOVE      ONE,HL7SMODE               * set mode for continuation
            DISPLAY   "More results exist. Press '\' to exit or ";
            CALL      HITENTER
            CMATCH    EXITCHAR,ANS               * exit search
            GOTO      PROC9999 IF EQUAL
            GOTO      PROC1000                   * get next set of results
          ENDIF
.
          DISPLAY   "End of search.  ";
          CALL      HITENTER
          GOTO      PROC9999
.
PROC9100  DISPLAY   *P1:24,*EL,*B,"Connection to NSLHD failed.  ";
          CALL      HITENTER
          GOTO      PROC9999
.
PROC9200  DISPLAY   *P1:24,*EL,*B,"Timeout on response from NSLHD.  ";
          CALL      HITENTER
          GOTO      PROC9999
.
PROC9300  DISPLAY   *P1:24,*EL,*B,"Insufficient data provided for query.  ";
          CALL      HITENTER
          GOTO      PROC9999
.
PROC9400  DISPLAY   *P1:24,*EL,*B,"Destination data incomplete.  ";
          CALL      HITENTER
          GOTO      PROC9999
.
PROC9500  DISPLAY   *P1:24,*EL,*B,"Connection to socket failed.  ";
          CALL      HITENTER
          GOTO      PROC9999
.
PROC9600  DISPLAY   *P1:24,*EL,*B,"Response Format Error.  ";
          CALL      HITENTER
          GOTO      PROC9999
.
PROC9999  RETURN
+
.*****************************************************************************
.*                               GUPI0000              Called by: PROC0000   *
.*                   Get the patient's UPI number                            *
.* Returns: HL7UPIVL - Patient UPI number                                    *
.*****************************************************************************
.
GUPI0000  KEYIN     *P1:3,*EF,"Enter patient ID number      ",*P31:3,":":
                    *P33:3,*V2LON,*JR,HL7UPIVL
.
          PACK      HL7UPIVL,HL7UPIVL,SP10
          RJUSTIFY  HL7UPIVL
.
          MOVE      ONE,HL7MXREC
          RJUSTIFY  HL7MXREC
.
GUPI9999  RETURN
+
.*****************************************************************************
.*                               GLHD0000              Called by: PROC0000   *
.*                   Get the patient's LHD number                            *
.* Returns: HL7NSPID - Patient LHD number                                    *
.*****************************************************************************
.
GLHD0000  KEYIN     *P1:3,*EF,"Enter NSLHD PersonID number  ",*P31:3,":":
                    *P33:3,*V2LON,*JR,HL7NSPID
.
          PACK      HL7NSPID,HL7NSPID,SP10
          RJUSTIFY  HL7NSPID
.
          MOVE      ONE,HL7MXREC
          RJUSTIFY  HL7MXREC
.
GLHD9999  RETURN
+
.*****************************************************************************
.*                               GSNM0000              Called by: PROC0000   *
.*                   Get the patient's surname                               *
.* Returns: HL7SURNM - Patient surname                                       *
.*****************************************************************************
.
GSNM0000  KEYIN     *P1:3,*EF:
                    *P1:4,"Surname",*P31:4,":":
                    *P33:4,*V2LON,HL7SURNM
.
          PACK      HL7SURNM,HL7SURNM,SP70
.
GSNM9999  RETURN
+
.*****************************************************************************
.*                               GFGN0000              Called by: PROC0000   *
.*                   Get the patient's first given name                      *
.* Returns: HL7FGNAM - Patient first given name                              *
.*****************************************************************************
.
GFGN0000  KEYIN     *P1:5,"First Given Name",*P31:5,":":
                    *P33:5,*V2LON,HL7FGNAM
.
          PACK      HL7FGNAM,HL7FGNAM,SP70
.
GFGN9999  RETURN
+
.*****************************************************************************
.*                               GSGN0000              Called by: PROC0000   *
.*                  Get the patient's second given name                      *
.* Returns: HL7SGNAM - Patient second given name                             *
.*****************************************************************************
.
GSGN0000  KEYIN     *P1:6,"Second Given Name",*P31:6,":":
                    *P33:6,*V2LON,HL7SGNAM
.
          PACK      HL7SGNAM,HL7SGNAM,SP70
.
GSGN9000  MOVE      ZERO,EXIT
          GOTO      GSGN9999
.
GSGN9100  MOVE      ONE,EXIT
.
GSGN9999  RETURN
+
.*****************************************************************************
.*                               GSEX0000              Called by: PROC0000   *
.*                     Get the patient's gender                              *
.* Returns: HL7GENDR - Patient gender                                        *
.*****************************************************************************
.
.>>>>>>>  Need to check if Sex and/or DoB could be sent without any name
.         details, in which case maybe we need to send as "DEM" instead
.         of "APN" in QRD.9.
.
GSEX0000  KEYIN     *P1:24,"(",*V2LON,*DV,ANSM,*HOFF,")ale, (",*V2LON,*DV,ANSF:
                    *HOFF,")emale, (",*V2LON,*DV,ANSA,*HOFF,")mbiguous, (":
                    *V2LON,*DV,ANSO,*HOFF,")ther, (",*V2LON,*DV,ANSU,*HOFF:
                    ")nknown":
                    *P1:7,"Gender",*P31:7,":":
                    *P33:7,*V2LON,HL7GENDR:
                    *P1:24,*EL
.
          PACK      HL7GENDR,HL7GENDR,SP70
          MATCH     SP1,HL7GENDR
          GOTO      GSEX9999 IF EQUAL
.
          REP       UPPLOW,HL7GENDR
.
          MATCH     ANSM,HL7GENDR
          IF        @EQUAL
            DISPLAY   *P40:7,"Male"
            GOTO      GSEX9999
          ENDIF
.
          MATCH     ANSF,HL7GENDR
          IF        @EQUAL
            DISPLAY   *P40:7,"Female"
            GOTO      GSEX9999
          ENDIF
.
          MATCH     ANSA,HL7GENDR
          IF        @EQUAL
            DISPLAY   *P40:7,"Ambiguous"
            GOTO      GSEX9999
          ENDIF
.
          MATCH     ANSO,HL7GENDR
          IF        @EQUAL
            DISPLAY   *P40:7,"Other"
            GOTO      GSEX9999
          ENDIF
.
          MATCH     ANSU,HL7GENDR
          IF        @EQUAL
            DISPLAY   *P40:7,"Unknown"
            GOTO      GSEX9999
          ENDIF
.
          BEEP
          GOTO      GSEX0000
.
GSEX9999  RETURN
+
.*****************************************************************************
.*                               GDOB0000              Called by: PROC0000   *
.*                     Get the patient's date of birth                       *
.* Returns: HL7PTDOB - Patient Date of Birth                                 *
.*****************************************************************************
.
GDOB0000  DISPLAY   *P1:8,"Date of Birth",*P31:8,":"
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT
          MOVE      EIGHT,CVERT
          MOVE      THIRTY3,CCOL
          UNPACK    SP8,CCENT,CYEAR,CMON,CDAY
.
          MOVE      SP8,HL7PTDOB
          CALL      KEYDATE
          BRANCH    OVRCD,GDOB9999
.
          PACK      HL7PTDOB,CCENT,CYEAR,CMON,CDAY
.
GDOB9999  RETURN
+
.*****************************************************************************
.*                               GMED0000              Called by: PROC0000   *
.*                   Get the patient's Medicare Number                       *
.* Returns: HL7PTMED - Patient Medicare Number                               *
.*****************************************************************************
.
GMED0000  KEYIN     *P1:9,*EF,"Enter medicare Number        ",*P31:9,":":
                    *P33:9,*V2LON,HL7PTMED
.
          PACK      HL7PTMED,HL7PTMED,SP70
.
GMED9999  RETURN
+
.*****************************************************************************
.*                               GMAX0000              Called by: PROC0000   *
.*                     Get the Maximum Number of records                     *
.* Returns: HL7MXREC - Maximum Number of records to be returned in a patient *
.*                     search                                                *
.*****************************************************************************
.
GMAX0000  KEYIN     *P1:10,"Maximum No. of Search records",*P31:10,":":
                    *P33:10,*V2LON,*JR,*DE,HL7MXREC
.
          PACK      HL7MXREC,HL7MXREC,SP3
          MATCH     SP3,HL7MXREC
          GOTO      GMAX9100 IF EQUAL
.
          TYPE      HL7MXREC
          GOTO      GMAX9100 IF NOT EQUAL
.
          MOVE      HL7MXREC,FORM3
          COMPARE   ZERO,FORM3
          GOTO      GMAX9100 IF EQUAL
.
          GOTO      GMAX9999
.
GMAX9100  BEEP
          GOTO      GMAX0000
.
GMAX9999  RETURN
+
.*****************************************************************************
.*                               DISP0000                                    *
.*                     Display response message to screen                    *
.*****************************************************************************
DISP0000  MOVE      ONE,DISPFLAG                 * display
          MOVE      ZERO,DISPFLAG                * no display
          RESET     RECVMESS
          IF        DISPFLAG = 1
            DISPLAY   "End of Block received",*R,*N;  * display full message
            STRIP     RECVMESS
            MOVE      ZERO,COUNTR
            MOVELPTR  RECVMESS,FORM4
            DISPLAY   *R,*N:
                      "----------------------------------------":
                      "----------------------------------------",*R,*N:
                      "Full Message received is ",FORM4," bytes (including ":
                      "control characters):",*R,*N,*R,*N;
            WHILE     FORM4 > 0
              ADD       ONE,COUNTR
              MATCH     CRETURN,RECVMESS
              IF        @EQUAL
                DISPLAY   *R,*N,*R,*N;
                MOVE      ZERO,COUNTR
              ELSE
                MOVE      RECVMESS,ANS
                DISPLAY   *HON,ANS,*HOFF;
                IF        COUNTR = 80
                  DISPLAY   "]",*R,*N;
                  MOVE      ZERO,COUNTR
                ENDIF
              ENDIF
              SUB       ONE,FORM4
              BUMP      RECVMESS
            DO
            DISPLAY   "----------------------------------------":
                      "----------------------------------------":
                      *R,*N,*R,*N;
            RESET     RECVMESS
          ENDIF
DISP9999  RETURN
+
.*****************************************************************************
.*                               DISRES00                                    *
.*                     Display search results to screen                      *
.*****************************************************************************
DISRES00  MOVE      ONE,LOOPCNT1
.
          WHILE     LOOPCNT1 <= DATARCNT
.
.2345678901234567890123456789012345678901234567890123456789012345678901234567890
.2345678901234567890123456789012345678901234567890123456789012345678901234567890
.
.         Name
          DISPLAY   LOOPCNT1
          DISPLAY              "~Name: ",*+,DATARRAY[LOOPCNT1][2],"|":
                                            DATARRAY[LOOPCNT1][3],"|":
                                            DATARRAY[LOOPCNT1][4],"|":
                                            DATARRAY[LOOPCNT1][1],"|";
.         Address
          DISPLAY           "~Address: ",*+,DATARRAY[LOOPCNT1][7],"|":
                                            DATARRAY[LOOPCNT1][8],"|":
                                            DATARRAY[LOOPCNT1][9],"|":
                                            DATARRAY[LOOPCNT1][10],"|":
                                            DATARRAY[LOOPCNT1][11],"|"
.
          MOVE      SP70,DATEDIS1
          MATCH     SP70,DATARRAY[LOOPCNT1][6]
          IF        !@EQUAL
            UNPACK    DATARRAY[LOOPCNT1][6],CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            REP       " 0",CPCDATE
            MOVE      CPCDATE,DATEDIS1
          ENDIF
.
          MOVE      SP70,DATEDIS2
          MATCH     SP70,DATARRAY[LOOPCNT1][16]
          IF        !@EQUAL
            UNPACK    DATARRAY[LOOPCNT1][16],CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            REP       " 0",CPCDATE
            MOVE      CPCDATE,DATEDIS2
          ENDIF
.
.         U/R Number
          DISPLAY              "  ~UR: ",*+,DATARRAY[LOOPCNT1][14],"|":
.         Gender
                                   "~Sex: ",DATARRAY[LOOPCNT1][5],"|":
.         Date of Birth
                                   "~DOB: ",DATEDIS1,"|":
.         Medicare Number
                                  "~Medi: ",DATARRAY[LOOPCNT1][13],"|"
.         UPI Number
          DISPLAY              " ~UPI: ",*+,DATARRAY[LOOPCNT1][15],"|":
.         NSHLD PersonID
                                 "~PersonID: ",DATARRAY[LOOPCNT1][12],"|":
.         Last Update Date
                          "~Last Updated: ",DATEDIS2,"|"
.
          ADD       ONE,LOOPCNT1
          IF        (LOOPCNT1%3)=1
            DISPLAY   *N;
            DISPLAY   "Press '\' to exit or ";
            CALL      HITENTER
            CMATCH    EXITCHAR,ANS               * exit search
            GOTO      DISRES95 IF EQUAL
            DISPLAY   *P1:10,*EF;
          ENDIF
          DO
.
DISRES90  MOVE      ZERO,EXIT
          GOTO      DISRES99
.
DISRES95  MOVE      ONE,EXIT
DISRES99  RETURN
.
WEBSIG00  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       TFILENAM
          INC       A19HNDLR
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC

.---------------------------------------------------------------------------
. Program        : OUTWEB06
.
. Function       : Outpatient - Medical Records Movements Server
.
. Modifications  :
.---------------------------------------------------------------------------
. V12.01.01 05/02/2025  Ebon Clements    TSK 0972339
.           Added clear of DIFFURFL at PRETB000 and PRBTB000
. V11.05.01 15/11/2042  Thanh T          TSK 0953878
.           Changed PRETB600 and PRBTB600 to fix issue with infinitety
.           looping when retrieving the pulling list
. V11.04.07 06.09.2024  Alina Bhari      TSK 0922829
.           Fixed the issue with the MRT Status-FILRW810,PRETB810,PRBTB810   
. V11.04.06 07.08.2024  Alina Bhari      TSK 0922829
.           displayed MRT status description as per the configured hex value 
.           color in Cat RS-FILRW810,PRETB810,PRBTB810                         
. V11.04.05 08.07.2024  Alina Bhari      TSK 0948807
.           Correctly displayed the patient without a medical record based on
.           the parameter to include patients with no MR -PRETB450,PRBTB450     
. V11.04.04 15.02.2024  DA Sarkies       TSK 0871324
.           Fixed error where code was being sent to incorrect section
. V11.04.03 12.02.2024  DA Sarkies       TSK 0871324
.           Added code to default to keep on list
. V11.04.02 25.01.2024  DA Sarkies       TSK 0871324
.           Fixed issue with date not placing 0's next to single figures    
. V11.04.01 07.12.2023  DA Sarkies       TSK 0871324
.           Added functions to add MRs to pull list as either keep or remove
.           Added functionality to display/hide MRs on pull list.
.           Added option to display current admissions as well.
. V11.02.01 10/02/2022  Thanh T          TSK 0905641
.           Recompiled as OUTHEDFD/OUTMA1FD changes
. V11.01.03 23/12/2021  Ebon Clements   Task 0913820
.           Set FOUNDFLG=1 when records are already in the correct location
.           FILRW500
. V11.01.02 15/07/2021  Ebon Clements   Task 0908003
.           Added read on PMI extension file - FILRW000, PRETB000 and PRBTB100
. V11.01.01 26/04/2021  Ebon Clements  TSK 0901388
.           Recompiled for MRTPSHFD - Free text description
. V11.00.02 11/01/2020  Ebon Clements  TSK 0901663
.           If displaying all medical records on the pulling list do not exit
.           the loop if a MR is already in the correct location.
.           Send FILRW500 to FILRW900 and reset EXCPFLAG
. V11.00.01 08/12/2020  Davin          TSK 0892763 CrossBrowser
.           Added ids to checkbox output (FILRW000/PRETB000/PRBTB000)
.           09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
.---------------------------------------------------------------------------
. V10.14.01 10/05/2019  Thanh T.       TSK 0872708
.           Changed HISSAV00. When MRCNCDOP = 1, uses the current date/time
.           for MRHIDATE/MRHITIME, otherwise, use the appointment date/time
.           for MRHIDATE/MRHITIME with ":00" appending at the end of the
.           MRHITIME
.---------------------------------------------------------------------------
. V10.12.01 06/03/2018  J.Tan          Task 0852398
.           Mod PRETB0000 PRBTB000 to check for blank FRSTDATE
. V10.11.03 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.02 04/09/2017  Thanh T.          TSK 0821710
.           Audit Amission/outpatient visit comments
. V10.11.01 30/07/2017  Thanh T.          TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
.---------------------------------------------------------------------------
. V10.10.02 07/07/2017  Davin             TSK 0841288
.           Mods to output booking doctor and diagnosis @ PRBTB830
. V10.10.01 28/03/2017  Tracey Nguyen     TSK 0832213
.           Added read PMI extension table to get PMPXFLDR/PMPXPRVI for
.           PATFLD00
. V10.09.03 28/02/2017  Tracey Nguyen     CAR 0814451
.           Added Pre-Admission Pull List view for admitting doctor & diagnosis
. V10.09.02 24/02/2017  Tracey Nguyen     CAR 0832213
.           Added Patient folder to Pre-Admission Pull List                    
. V10.09.01 24/02/2017  Tracey Nguyen     CAR 0832783
.           Added Record Status field to Outpatient and Pre-Admission Pull List
. V10.08.01 02/06/2016  Ebon Clements     TSK 0322962
.           Fixed blank user clinic id default in OP pulling list - FILTB000
. V10.07.01 11/12/2015  Peter Vela        CAR 323754
.           Added submit and display for superlev
. V10.06.01 19/03/2015  Ebon Clements     CAR 268970
.           Change to use OUTCLIFD index 1 instead of index 2
. V10.05.05 28/11/2014  Ebon Clements     CAR 309259
.           Added display of OP visit slot type - FILRW801
. V10.05.04 28/11/2014  Ebon Clements     CAR 308834
.           If not multi hospital clear the OP clinic MR home hospital
.           FILTB000
. V10.05.03 06/10/2014  Ebon Clements     CAR 268970
.           Removed OUTCLIFD index 4 open as it was not used
. V10.05.02 02/10/2014  Ebon Clements     CAR 306687
.           If not multi hospital set MR location hospital fields to spaces
.           in - HISSAV00
. V10.05.01 22/07/2014  Ebon Clements   CAR 303362
.           Added OTCNHSEC - Using Hospital Security Access in Mult
.                            Hospital Outpatients
. V10.04.03 01/07/2014  Ebon Clements   CAR 302816
.           Added allhomeh - Show all home hospital medical records on 
.           the IP and OP pulling lists
. V10.04.02 07/04/2014  Peter Vela      CAR 286258
.           Recompiled for PATMASTM
. V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.---------------------------------------------------------------------------
. V10.03.10 30/06/2013  Davin             CAR 283202
.           Recompiled for changes to hl7 messaging
. V10.03.09 24/07/2013  Ebon Clements    CAR 285472
.           Fixed the document type display and added HOMEHOSP test to the pre
.           admission pulling list - PRETB000 PRBTB000
. V10.03.08 29/04/2013  Patrick Adair    CAR 281116
.           Correct Clinic description in Outpatient Pulling List
. V10.03.07 25/02/2013 Ania P            CAR 281021
.           Removed unnecessary modification of constants.
. V10.03.06 02/08/2012  Ebon Clements    CAR 266290
.           Fixed in transit display for IP bookings - PRBTB000
. V10.03.05 31/07/2012  Patrick Adair    CAR 233039
.           Included Visit Type in Outpatient Pulling List for NZ
. V10.03.04 18/07/2012  Ebon Clements    CAR 266290
.           Added display in transit functionality
. V10.03.03 18/04/2012  Don Nguyen        CAR 263125
.           Removed 50 record limit on Outpatient Pull List
. V10.03.02 30/01/2012  Mike Laming       CAR 259106
.           Mods to use PTHSRCTY to set correct "Red" Hospital fields.
. V10.03.01 21/11/2011  Mike Laming       CAR 254569
.           More mods to Hospital Id (with colours)
. V10.02.09 24/10/2011  Ebon Clements     CAR 253599
.           Check MR home hosp when selecting MR for OP pulling list - FILRW000
. V10.02.08 16/09/2011  Mike Laming       CAR 248696
.           Added Hospital Id to "Location" in Lists PRETB000, PRBTB000 & FILTR0
. V10.02.07 03/08/2011  Ebon Clements     CAR 243608
.           Added hospital filter to OP and IP pulling lists
. V10.02.06 28/07/2011  Ebon Clements     CAR 243582
.           Added using tracking receipt functionality
. V10.02.05 20/07/2011  Peter McMullen    CAR 246597
.           Added processing for 'exclude from pull list' clinic options 
. V10.02.04 04/07/2011  Ebon Clements     CAR 240724
.           Mods for MRT hospital/location - Added hospital to location key
. V10.02.03 17/05/2011  Peter McMullen    CAR 240725
.           change ward selection lust to sort by name, not code
. V10.02.02 17/05/2011  Mike Laming       CAR 240716
.           Mods to MRT Tracking sort order - created include MRTRKSEQ, see
. V10.02.01 17/05/2011  Mike Laming       CAR 240716
.           Mods to MRT Tracking sort order - created include MRTRKSEQ, see
.           CALLs GTSRTIND & MRSRTORD. Var SORTORDR replaces TERMDIGT
. V10.01.01 15/03/2011  Peter McMullen    CAR 237836
.           Remove 50 record limit on Pull List  
. V10.00.07 19/10/2010  Ebon Clements     CAR 231954
.           Fixed exceptions on OP pulling list when patient has 2 bookings in
.           two clinics on the same day - VLSD0000
. V10.00.06 10/09/2010  Ebon Clements     CAR 225816
.           Default document type filter from hospital file if multi hospital
. V10.00.05 03/09/2010  Ebon Clements     CAR 214145
.           Added document type filter to MR IP pulling list
. V10.00.04 03/09/2010  Ebon Clements     CAR 214145
.           Added document type filter to MR OP pulling list
. V10.00.03 03/08/2010  Ebon Clements     CAR 202256
.           Added MRCNNOMR - Include patients with no MR on pulling lists
. V10.00.02 07/06/2010  Ebon Clements     CAR 205620
.           Added clinic description with code to FILRW000
. V10.00.01 19/03/2010  Steve Armstrong   CAR 135505
.           Added HL7TRGID & HL7INCLD setting for all broadcast messages
.-------------------------------------------------------------------------------
. V9.12.03  17/12/2009  Peter McMullen CAR 210130
.           Concert to newer Dr name format routine DOCNM000
. V9.12.02  01/10/2009  Ebon Clements CAR 202247
.           Changed FILRW500 to test OTHELOCN not OTMALOCN
. V9.12.01  30/07/2009  Ebon Clements CAR 202529
.           Commented out OBAPULL tests at FILTB110 and FILTB350 from CAR 150948
. V9.11.02  20/03/2009  Jill Habasque CAR 150948 
.           Moved update of UPBOKA1 so OBADATE doesn't get changed 
. V9.11.01  26/02/2009  Jill Habasque CAR 150948
.           Mods to update OBAPULL=1 if mrcndisa=0 and not display in filtb000
. V9.09.01  28/08/2007  Peter Vela    CAR 148631
.           Modified Terminal Digit view for CABRINI Health @ FILTB000,
.           PRETB000, PRBTB000
. V9.08.03  11/05/2007  Peter Vela    CAR 136558
.           Added last movement date to FILTB000, PRETB000, PRBTB000 for
.           outweb0601001 sjog-vic and outweb0602001 sjog-vic
. V9.08.02  15/02/2007  Janet George  CAR 123299
.           Added tags to display Clinic Id and Clinic Type
. V9.08.01  25/01/2007  Peter Vela     CAR 127930
.           Added MRMADTCR MRMATMCR before WRTMAS1
. V9.07.01  22/09/2006  Ebon Clements CAR 119308
.           Added terminal digit to pre admission pulling list
. V9.05.01  09/03/2006  Ebon Clements CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.05.B01 30/01/2006  Ebon Clements CAR 93186
.           Mods for REDIRECT length change. Recompiled for IBAWEBDF
. V9.04.05  17/06/2005  Jill Habasque    CAR 63280
.           Fixed multiple volumes in pulling lists
. V9.04.04  16/06/2005  Jill Habasque    CAR 63280
.           Added output of comments flag
. V9.04.03  19/05/2005  Jill Habasque    CAR 59013
.           Added use of MRCNDISA - display all volumes in pulling list
. V9.04.02  29/04/2005  Peter Vela       CAR 55214
.           Recompiled for MRTCONFD
. V9.04.01  31/08/2004 Jill Habasque CAR 52930 
.           Added check for multi hospital
. V9.03.11  07/06/2004 Peter Vela     CAR 49016
.           2004 Statutory Changes - recompiled for PMSPX2TM
. V9.03.10  20/04/2004 Ebon Clements  CAR 48343
.           Added exceptions option to the outpatient pulling list
. V9.03.09  30/03/2004 Ebon Clements  CAR 48433
.           Fixed allocation of MRHIDDUE in CALDUE00                           
. V9.03.08  26/03/2004 Peter Vela     CAR 13038
.           Recompiled for PATDOCCD - Doctor Name format Parameter
. V9.03.07  04/03/2004  Davin         CAR 34825
.           Write user id associated with MRT security number to mrthis (kids)
. V9.03.06  02/03/2004 Jill Habasque  CAR 47831
.           Added MRCNCDOP - current date for OP pulling list movement
. V9.03.05  18/12/2003 Peter Vela     CAR 43134
.           Recompiled for MRTCONFD
. V9.03.04  11/12/2003 Peter Vela     CAR 40355
.           Recompiled for PATNAMCD
. V9.03.03  30/09/2003 Ebon Clements  CAR 43674
.           Close files before opening in OPNOUT00
. V9.03.02  29/09/2003 Ebon Clements CAR 22361
.           Added reads of outhedaf to display the correct session setup details
. V9.03.01  15/05/2003 Pat Dirito                     SRF 39121
.           Now using Addmiting point for MR Location in combination with
.           PTWDLOCN(Ward File MR Location)
.           Removed PFIL1800(Saved in my home directory)
.---------------------------------------------------------------------------
. V9.02.48  15/05/2003 Pat Dirito                     SRF 39121 
.           Now using Addmiting point for MR Location in combination with
.           PTWDLOCN(Ward File MR Location)
.           Removed PFIL1800(Saved in my home directory)
. V9.02.47  03/04/2003 Pat Dirito              
.           Added PFIL1800 for testing new sort at RCH. Copied routines PRETB000
.           & PRBTB000 
.           07/04/03 J.Tan  SRF 38022
.           Mods to read ward file after read code cat "ap"
. V9.02.46  01/04/2003 Ebon Clements SRF 23146
.           Added a read of the ward file with the HDP from the admitting point
.           to check the medical records locations
. V9.02.45  07/03/2003 J.Tan
.           Changed admitting point to code category 'ap'
. V9.02.44  12/12/2002  Jill Habasque SRF 23682             
.           Changed to not move records if the location and admitting point
.           are blank
. V9.02.43  03/12/2002  Lina Vo                             
.           Added CALL OPNOUT00 in INIT0000 and removed duplicate open of
.           outpatient files.
. V9.02.42  07/10/2002  Peter Vela               SRF 17693
.           Mods to change Admitting Point to read from patcodes
. V9.02.41  08/08/2002  Ebon Clements            SRF 20239
.           Changed VLSD0000 to ignore extended slots.
. V9.02.40  01/07/2002  Pat Dirito               SRF 18089
.           Re-added LOCATION check in SAVPRE00 & PRESAV00 as is still needed.
. V9.02.39  29/06/2002  Ebon Clements            SRF 19105
.           Recompiled for sigpipe trap 
. V9.02.38  24/06/2002  Pat Dirito               SRF 18960
.           Removed LOCATION check in PRESAV00 and add var ADMPOINT in PRBTB000
. V9.02.37  18/06/2002  Pat Dirito               SRF 18688
.           HISSAV00 now writing to History file with full time in secs.
. V9.02.36  17/06/2002 Pat Dirito                SRF 18476
.           Fixed output of error messages in SAVPRE00 & SAVHIS00
. V9.02.35  08/06/2002 J.Tan             
.           Re-name pre-admission indicator
. V9.02.34  31/05/2002 Ebon Clements             SRF 18229
.           Added trap if sigpipe
. V9.02.33  27/05/2002 Ebon Clements             SRF 17950
.           Changed FILTB350 to goto FILTB300 not FILTB999 for OBAPULL filter
.           Changed FILTB250 to clear BOOKNKEY after is it has been used
.           to the next session booking are selected coprrecty
. V9.02.32  21/05/2002 Pat Dirito                SRF 17716
.           Modified PRETB000, PRBTB000 to not check against MRCNHLOC 
. V9.02.31  16/04/2002 Pat Dirito   
.           Modified FILRW000 to not check against MRCNHLOC only MRCNRCTY.
. V9.02.30  25/03/2002 Pat Dirito   
.           Modified FILRW000 to not check filled row based on OBAPULL.
.           Modified FILTB000 for NEXTPAGE parameter.                  
. V9.02.29  18/03/2002 Pat Dirito   
.           Modfied Flag for Microfilmed in PRETB000, PRBTB000 & FILRW000 
. V9.02.28  27/02/2002 Pat Dirito   
.           Added Flag for Microfilmed in PRETB000, PRBTB000 & FILRW000 
. V9.02.27  26/02/2002 Ebon Clements
.           Added TRAPS to writes to MRTHISFD for I * D                        
. V9.02.26  25/02/2002 Pat Dirito 
.           Modified Routine CALDUE00 as was changing VARS CCC,CYY,CMM,CDD does
.           need to!
. V9.02.25  12/02/2002 B.G.Ackland
.           Performance
. V9.02.24  12/02/2002 B.G.Ackland
.           Limit to 50 Records Output
. V9.02.23  12/02/2002 B.G.Ackland
.           Limit to 50 Records Output
. V9.02.22  11/02/2002 Ebon Clements                           
.           Move ZERO to MRKEYCNT in CLRPAR00 to fix array out of bounds       
. V9.02.21  29/01/2002 Bronko Sosic                            
.           Modified PRBTB000 overcond on Codes File now going to PRBTB400 was
.           going to PRBTB100
. V9.02.20  23/01/2002 Bronko Sosic                            
.           Cleared the array ERRORLST[] in CLRPAR00               
. V9.02.19  23/01/2002 Pat Dirito                              
.           Fixed calls to IBACLOCK and removed variable CURRDATE
. V9.02.18  12/01/2002 Bronko Sosic                            
.           Changed reades on MRTMASFD to be on index 3 for most current MRT
.           Record 
. V9.02.17  10/01/2002 Bronko Sosic                            
.           Changes to O/P pulling list with new filter of clinic location
. V9.02.16  03/12/2001 Katie Nelson/Harvinder   SRF 648628(nz)
.           Recompiled for NHI tags added to PATMASTM
. V9.02.15  26.11.2001 Bronko Sosic  
.           Changed Pulling List for OP so that urnumber is sorted for 
.           Terminal Digit
. V9.02.14  17.11.2001 B.G.Ackland   
.           Fix Pulling List for OP
. V9.02.13  29.10.2001 Bronko Sosic  
.           Pre-Admissions pulling list defect 1771 enhancement 1773 
. V9.02.12  23.10.2001 Pat Dirito    
.           Outpatient pulling list not defaulting to ticked.(PRETB000,PRBTB000)
. V9.02.11  19.10.2001 Ebon Clements 
.           Added test for default document type and home location to the
.           outpatient and inpatient pulling lists
. V9.02.10  17.10.2001 Ebon Clements 
.           Recompiled for PATCODFD 
. V9.02.09  13.10.2001 Ebon Clements 
.           Save the clinic id and description in the requested by field
.           when a medical record is moved by the pulling list
.           04.10.2001 Pat Dirito    
.           Changed Index read to 3 on MRTMASFD in routine FILTB000        
. V9.02.08  28.09.2001 Ebon Clements 
.           Corrected format of ward description in ward bed selection list
.           28.09.2001 Bronko Sosic
.           Changed Outpatient pulling & Pre-admission list to take 
.           the most current Medical Record and then if there isent one
.           to take the most current Outpatient Record or Emergency Record
. V9.02.07  J.Tan
.           Move records,when a record is filled the history location should
.           be the location of clinic
. V9.02.06  J.Tan
.           Mods Updating current location (mrtmasaf) to MR location of
.           admitting point
. V9.02.05  14/09/2001  Mike Laming    RCH/SVHM
.           Activate Datagate API (see below V9.02.03)                 
.           and         J.Tan
.           Mods to print admitting point for preadmission pulling list
. V9.02.04  03/09/2001  J.Tan
.           Mods checking for admitting point for pre-adm pulling list
. V9.02.03  30/08/2001  Mike Laming    RCH/SVHM
.           Add Datagate API Medical Records Movement (DGCLICRM)
. V9.02.02  24.08.2001 J.Tan
.           Fixed Pulling list for 'All' clinic id again
. V9.02.01  21.08.2001 J.Tan
.           Fixed Pulling list for 'All' clinic id
. V9.01.02  16.08.2001 J.Tan
.           Recompiled for PATMASTM
. V9.00.001 13.08.2001 J.Tan
.           Recompiled for PATMASTM
. V9.00.B03 13.07.2001 Sandra Barcham
.           Replace GP with HCP
. V9.00.B02 14.06.2001 B.G.Ackland
.           Recompile for Changes to PATMASTM
. V9.00.B01 22.05.2001 Ebon Clements 
.           Added clinic type and clinic id to display
.           Added Booked patients to the list
.----------------------------------------------------------------------
. V5.10.00 22.02.2001 Bronko Sosic
.          Created Program
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF

          INC       PATDHEAD/INC
.
          INC       APSMASFD/INC
          INC       MLTSECFD/INC
          INC       PATMI1FD/INC
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC
          INC       PATMISTD/INC
          INC       PMSVX1FD/INC
          INC       PATMA1FD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCGFD/INC
          INC       PMSPX2FD/INC
          INC       PMSPX2TD/INC
          INC       PATVISFD/INC   * Patients Visits File
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATALRFD/INC
          INC       PATCALAG/INC
          INC       PATCONFD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATHSPFD/INC
          INC       OUTSITFD/INC   * Outpatients - Site Codes File
          INC       OUTSESFD/INC   * Outpatients - Session
          INC       PATFN1FD/INC   * Health Fund File
          INC       PATIN1FD/INC   * Insurance File
          INC       PATPR1FD/INC   * New Patient Master File
          INC       PATWR1FD/INC
          INC       PMSCCDFD/INC            * Concession Card Details

          INC       MRTCONFD/INC
          INC       MRTMASFD/INC
          INC       MRTLOCFD/INC
          INC       MRTMOVFD/INC
          INC       MRTPDSFD/INC
          INC       MRTPSHFD/INC
          INC       MRTHISFD/INC
          INC       OUTBOAFD/INC
          INC       MRTRQHFD/INC
          INC       MRTRQDFD/INC
          INC       OUTCTYFD/INC
          INC       OUTCLIFD/INC
          INC       OUTMA1FD/INC
          INC       BOKDIAFD/INC
          INC       BOKRX1FD/INC
          INC       BOKRC1FD/INC
.
          INC       PMSHCPTD/INC
          INC       PMSHCGTD/INC
.
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
          INC       OUTCONFD/INC
          INC       OUTHEDFD/INC
          INC       MRTPLLFD/INC
.
SORTORDN  DIM       1                       * MR Tracking sort order indicator
SORTORDR  DIM       8                       * MR Tracking sort order value
T1A       DIM       1
T2A       DIM       2
T2B       DIM       2
T3A       DIM       3
T4A       DIM       4
T5A       DIM       5
.
ADMPOINT  DIM       45
ALLHOMEH  FORM      1       * All home hospital medical records on pulling list
APOIDESC  DIM       45
ATDESTNA  DIM       1
MEDILOCN  DIM       4       * MR Location
MEDILOCH  DIM       3       * MR Location Hospital
SAVEDOTY  DIM       3
SESSKEYS  DIM       25
CFILEPRE  DIM       3
COMTFLAG  FORM      1
COUNTER   FORM      2       * counter
COMMLINK  DIM       127
CLINIC    DIM       6       * Clinic ID
DEFTHOSP  DIM       3
DFLTWARD  DIM       3
HOSPID01  DIM       35
HOMEHOSP  DIM       3       * Home Hospital
SITE      DIM       6       * Site ID
END       FORM      "95"    * End counter for form
FOUNDFLG  FORM      1
DIFFURFL  FORM      1
.
INTRANST  DIM       26
LOCATION  DIM       4       * Movement Location
LOCNHOSP  DIM       3       * Movement Location Hospital
UPDATETY  DIM       1       * Update Type
NEXTPAGE  DIM       1       * Nextpage parameter CGI parameter
NOMRFLAG  FORM      1       * No MR found for this patient
VARIABLE  DIM       127                   
.
ERRORCNT  FORM      3
ERRORLST  DIM       50[99]
MRRECKEY  DIM       40[99]  * Array of MR Keys
MRKEPLST  DIM       40[99]  * Array of MR Keys
MRRMVLST  DIM       40[99]  * Array of MR Keys
MRKEYCNT  FORM      3       * Count of MR Keys
MRKEYEND  FORM      3       * Count of MR Keys
MICROFLM  DIM       25      * MicroFilm Address
UNITCODE  DIM       4       * Unit code
WARDCODE  DIM       3       * Ward Code
POSTWARD  DIM       3       * Post-Op Ward
ADMITTPT  DIM       3       * Admittion Point
ALLCLIN   FORM      1
DIM20     DIM       20
DIM40     DIM       40
.
DOCTCODE  DIM       6       * Doctor Code
RECCOUNT  FORM      6       * Counter
RECCNT    FORM      4       * Counter
DRECCNT   DIM       4       * Counter
FORM8     FORM      8       * Form of Length 8
CURRYEAR  DIM       4       * Current Year   
DATE      DIM       8       * Current Date 
DSCDATE   DIM       17      * Display date and time
TIME      DIM       8       * Current Time   
SESDATE   DIM       8       * Session Date
SESTIME   DIM       5       * Session Time
STIME     DIM       5       * Start Time for Booking
SAVKEY44  DIM       44      * Save variable
SAVKEY28  DIM       28      * Save KEY28 for Pulling List
SAVDATE   DIM       8       * Save date
SAVURNO   DIM       8       * save u/r number
SDAYFLAG  FORM      1       * More than one booking in a day 
SDAYDESC  DIM       3       * More than one booking in a day description
.
MRTSECID  DIM       10      * MRT security number User ID
MRTSECNO  DIM       8       * MRT security number
.
NEXTSESS  DIM       25      * Next Session Key
BOOKNKEY  DIM       28      * Next Record Key
CLINICID  DIM       6       * Clinic ID
CLINICTY  DIM       6       * Clinic Type
ADDITION  FORM      1       * Additions to pull list
EXCEPTNS  FORM      1       * Show Exceptions on pulling list 0=No 1=Yes
EXCPFLAG  FORM      1       * Is this record an exception 0=No 1=Yes
FILTDOTY  DIM       3       * MR documnet type filter
FILTVITY  DIM       3       * Visit type filter                       Car 233039
FIRSTFLG  FORM      1       * More than one booking in a day
SAVCLIN   DIM       6       * Save Clinic ID
SCLIFLAG  FORM      1 
SUPERLEV  DIM       1
DEFTVIEW  FORM      1
.
INCLADMS  FORM      1       * Include Current Admns Ticked 

.
DASH      INIT      "-"
RECORD    INIT      "recordky"
SQUOTE    INIT       "'"
CODETY    INIT      "TY"
CODEAP    INIT      "ap"
CODERS    INIT      "RS"
SEPCHAR   INIT      "-"
OBB       INIT      "("
CBB       INIT      ")"
OBR       INIT      "<font color=red>("
CBR       INIT      ")</font>"
STTRED    INIT      "<font color=red>"
ENDFONT   INIT      "</font>"
.
.---------------------------------------------------------------------------
PRGID     INIT      "OUTWEB06"
VERSION   INIT      "V12.01.01"
PRGNAM    INIT      "Outpatients - Medical Records Movement Server"
.---------------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          CALL      OUTFIL00       * Check Correct Files Are Open & Valid Site
          BRANCH    EXIT,MAIN1000
.
          CALL      GTSRTIND       * get MRT Tracking sort order indicator
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      MRTOUT00           * Outpatient Request Fill List.
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      MRTPRE00           * Medical Record Pre-admition Fill List.
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40,NXTPAG50
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      GOBACK00
          GOTO      NXTPAG99
.
NXTPAG50  CALL      PREDIR00
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.------------------------------------------------------------------------------
.  Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
.                             " alert(#"",WEBTITLE,"#");":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      INTMAS00     * Open routine for PATMASTM
          OPEN      PATMI1A6,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
          OPEN      VISMDTA1,"vismdtaf"
          OPEN      VISMTXA1,"vismtxaf"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATWR1A7,"patwr1af"
          OPEN      PATVISA2,"patvisaf"
          OPEN      PATVISA2,"patvisaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATDSCH1,"patdschf"
          OPEN      MRTMASA1,"mrtmasaf"
          OPEN      MRTMASA2,"mrtmasaf"
          OPEN      MRTMASA3,"mrtmasaf"
          OPEN      MRTMASA4,"mrtmasaf"
          OPEN      MRTLOCA1,"mrtlocaf"
          OPEN      MRTMOVA1,"mrtmovaf"
          OPEN      MRTPDSA1,"mrtpdsaf"
          OPEN      MRTPSHA1,"mrtpshaf"
          OPEN      MRTHISA1,"mrthisaf"
          OPEN      BOKDIAA1,"bokdiaaf"
          OPEN      BOKRX1A1,"bokrx1af"
          OPEN      BOKRC1A4,"bokrc1af"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      MLTSECA1,"mltsecaf"
          OPEN      PMSCCDA1,"pmsccdaf"
          OPEN      MRTPLLA1,"mrtpllaf"
.
          READ      CONTROLF,FIFTY9;*34,CTYPEUR
          READ      CONTROLF,SIXTY;*79,MRCNLAYT,*84,MRCNHLOC,*88,MRCNRCTY:
                                   *108,MRCNSTAT,*131,MRCNCHGL,*135,MRCNMOVR:
                                   MRCNMOVP 
          READ      CONTROLF,NINTY7;*156,MRCNCDOP
          READ      CONTROLF,NINTY7;*160,MRCNDISA,*172,MRCNNOMR,*176,MRCNTREC
          READ      CONTROLF,THIRTY2;*116,OTCNCFMT,*141,OTCNHSEC
          READ      CONTROLF,ZERO;*43,IBCNMHOS        * Multi Hosp Indicator
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
.  Use site prefix to open outpatient files
.
          MOVE      "out",CFILEPRE
          CALL      OPNOUT00
.
INIT9999  RETURN
.*******************************************************************************
.                     Check Correct Files are Open for System
.*******************************************************************************
OUTFIL00  MOVE      WBSESIT,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,OUTFIL90
          MATCH     OSTFILE,CFILEPRE         * Save Current File Prefix
          IF        !@EQUAL
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open Outpatient Files
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      OUTFIL99
.
OUTFIL90  MOVE      "Outpatient Site Invalid",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
OUTFIL99  RETURN
.*******************************************************************************
.                    Open Outpatient Files
.*******************************************************************************
OPNOUT00  PACK      CFNAME,CFILEPRE,FILBOKA1  * Get the name of the BOKA1 file
          CLOSE     OUTBOKA1
          OPEN      OUTBOKA1,CFNAME           * Open the file
          CLOSE     OUTBOKA3
          OPEN      OUTBOKA3,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILSESA1  * Get the name of the SESA1 file
          CLOSE     OUTSESA1
          OPEN      OUTSESA1,CFNAME           * Open the file
          CLOSE     OUTSESA2
          OPEN      OUTSESA2,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILCLIA1  * Get the name of the CLIA1 file
          CLOSE     OUTCLIA1
          OPEN      OUTCLIA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILCTYA1  * Get the name of the CLIA2 file
          CLOSE     OUTCTYA1
          OPEN      OUTCTYA1,CFNAME           * Open the file
          CLOSE     OUTCTYA2
          OPEN      OUTCTYA2,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILMA1A1
          CLOSE     OUTMA1A1
          OPEN      OUTMA1A1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILHEDA1
          CLOSE     OUTHEDA1
          OPEN      OUTHEDA1,CFNAME           * Open the file
.
          RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,URNUMBER
          MOVE      SP70,LOCATION
          MOVE      SP70,LOCNHOSP
          MOVE      SP70,ADMISSNO
          MOVE      SP70,UPDATETY
          MOVE      SP70,NEXTPAGE
          MOVE      SP70,FRSTDATE
          MOVE      SP70,LASTDATE
          MOVE      ZERO,VIEWTYPE
          MOVE      SP70,VYEARMTH
          MOVE      SP70,CLINICID         * Clinic ID
          MOVE      SP70,NEXTSESS                       
          MOVE      SP70,BOOKNKEY         
          MOVE      SP70,CLINICTY         * Clinic Type
          MOVE      SP70,MICROFLM
          MOVE      TWO,ADDITION          * Additions to the pull list
          MOVE      ZERO,EXCEPTNS         * Show exceptions in pulling list
          MOVE      SP70,WARDCODE         
          MOVE      SP70,POSTWARD         
          MOVE      SP70,ADMITTPT         
          MOVE      SP70,UNITCODE         
          MOVE      SP70,DATE
          MOVE      SP70,TIME
          MOVE      ZERO,MRKEYEND
          MOVE      SP70,MRTSECID
          MOVE      SP70,MRTSECNO
          MOVE      SP70,ATDESTNA
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      Z70,FILTDOTY
          MOVE      SP70,FILTVITY                                   * Car 233039
          MOVE      ZERO,ALLHOMEH
          MOVE      SP70,SUPERLEV
          MOVE      ZERO,DEFTVIEW          * Default view
          MOVE      ZERO,INCLADMS          * Include Admissions 
.
          MOVE      ZERO,MRKEYEND
          REPEAT
            ADD       ONE,MRKEYEND
            PACK      MRRECKEY[MRKEYEND],SP70
            PACK      MRKEPLST[MRKEYEND],SP70
            PACK      MRRMVLST[MRKEYEND],SP70
          UNTIL     MRKEYEND=99  
          MOVE      ZERO,MRKEYEND
.
          RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "microflm=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,MICROFLM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "location=",QRYNAME
          IF        @EQUAL
            PACK      LOCATION,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "locnhosp=",QRYNAME
          IF        @EQUAL
            PACK      LOCNHOSP,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updatety=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDATETY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "frstdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FRSTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "lastdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LASTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "vyearmth=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VYEARMTH
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "viewtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VIEWTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "clinicid=",QRYNAME
          IF        @EQUAL
            PACK      CLINICID,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "booknkey=",QRYNAME
          IF        @EQUAL
            PACK      BOOKNKEY,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextsess=",QRYNAME
          IF        @EQUAL
            PACK      NEXTSESS,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "clinicty=",QRYNAME
          IF        @EQUAL
            PACK      CLINICTY,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "addition=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADDITION
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "exceptns=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,EXCEPTNS
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "wardcode=",QRYNAME
          IF        @EQUAL
            PACK      WARDCODE,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "postward=",QRYNAME
          IF        @EQUAL
            PACK      POSTWARD,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admittpt=",QRYNAME
          IF        @EQUAL
            PACK      ADMITTPT,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "unitcode=",QRYNAME
          IF        @EQUAL
            PACK      UNITCODE,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "mrreckey=",QRYNAME
          IF        @EQUAL
            COMPARE   "99",MRKEYEND
            GOTO      SETPAR99 IF EQUAL
            ADD       ONE,MRKEYEND
            PACK      MRRECKEY[MRKEYEND],QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "mrkeplst=",QRYNAME
          IF        @EQUAL
            COMPARE   "99",MRKEYEND
            GOTO      SETPAR99 IF EQUAL
            ADD       ONE,MRKEYEND
            PACK      MRKEPLST[MRKEYEND],QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "mrrmvlst=",QRYNAME
          IF        @EQUAL
            COMPARE   "99",MRKEYEND
            GOTO      SETPAR99 IF EQUAL
            ADD       ONE,MRKEYEND
            PACK      MRRMVLST[MRKEYEND],QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "mrtsecid=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,MRTSECID
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "mrtsecno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,MRTSECNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "atdestna=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ATDESTNA
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtdoty=",QRYNAME
          IF        @EQUAL
            PACK      FILTDOTY,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.                                                                     Car 233039
          MATCH     "filtvity=",QRYNAME
          IF        @EQUAL
            PACK      FILTVITY,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "allhomeh=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ALLHOMEH
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "superlev=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SUPERLEV
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "deftview=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DEFTVIEW
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "incladms=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,INCLADMS
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.------------------------------------------------------------
. Outpatient Medical Records Movement Lists by Date
.------------------------------------------------------------
MRTOUT00  RESET     UPDATETY
          MATCH     SP1,UPDATETY
          GOTO      MRTOUT40 IF EQUAL
.                                                           
          MATCH     "A",UPDATETY    * Add formaction        
          GOTO      MRTOUT10 IF EQUAL                       
.                                                           
          GOTO      MRTOUT99                                
.                                                           
.         ************ ADD FORMACTION **********
.
MRTOUT10  CALL      SAVHIS00      * Loop through MR's and save Movements
          MOVE      ZERO,EXIT
          COMPARE   ZERO,ERRORCNT
          GOTO      MRTOUT99 IF EQUAL
          CALL      ERRLST00
          MOVE      ONE,EXIT
          GOTO      MRTOUT99
.
.         ************ DISPLAY FORMACTION **********
.
MRTOUT40  MOVE      ZERO,VIEWTYPE  * Must be Daily List
          CALL      CURPER00       * Determine Current Period
          CALL      CALCDT00       * Calculate Next and Last Period Dates
. 
          MOVE      "Outpatient Pulling List/Movement",WEBTITLE
          CALL      WEBHED00
          BRANCH    EXIT,MRTOUT99
.
          CALL      WEBBOD00
.
          CALL      WEBEND00
          MOVE      ONE,EXIT
          GOTO      MRTOUT99
.
MRTOUT91  MOVE      "Medical Record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MRTOUT99
.
MRTOUT92  MOVE      "Medical Record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MRTOUT99
.
MRTOUT93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MRTOUT99
.
MRTOUT99  RETURN
.----------------------------------------------------------------------
. Output Javascript List of Error for Processing Range of U/R or MR Keys
.----------------------------------------------------------------------
ERRLST00  CALL      OPENHTML
          BRANCH    EXIT,ERRLST95
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {"
.
          MOVE      ZERO,F3
ERRLST10  ADD       ONE,F3
          IF        F3>ERRORCNT | F3>99
            GOTO      ERRLST20
          ENDIF
          WRITE     HTMLFILE;"    alert(#"",ERRORLST[F3],"\n#");"
          GOTO      ERRLST10


ERRLST20  WRITE     HTMLFILE;"    alert(#"All Other Records Processed/n#");"
          WRITE     HTMLFILE;"    if (window.name.substr(0,4)=='Hide' ) {":
                             "    self.close() } ":
                             "    else {":
                             "    history.go(-1)}":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      ERRLST99
.
ERRLST95  DISPLAY   "*** Fifo Not Found ***"
.
ERRLST99  RETURN
.-----------------------------------------------------------------------------
.                               HISSAV00
. Note OBATIME is a DIM 5 Outpatients only saves it in the format (00:00). 
. Medical Records requires it must be in the format (00:00:00).
.-----------------------------------------------------------------------------
HISSAV00  MOVE      KEY10,MRHIKEY       * Medical Record Key
          MOVE      OBADATE,MRHIDATE    * Movement Date
          MOVE      OBATIME,MRHITIME    * Movement Time
.
          IF        MRCNCDOP=1
            CLOCK     TIME,TIME
            PACK      MRHITIME,TIME,SP70
          ELSE
            ENDSET    MRHITIME
            APPEND    ":00",MRHITIME    * Append 0 second with colon
            RESET     MRHITIME
          ENDIF
.
          IF        IBCNMHOS=1
            MOVE      OTHEHOSP,MRHIDHOS   * Location hospital
          ELSE
            MOVE      SP70,MRHIDHOS       * Set Location hospital to blank
          ENDIF
.
          MOVE      OTHELOCN,MRHILOC    * Location
          MOVE      SP70,MRHIRECV       * Reciever
          MOVE      MRCNMOVR,MRHIREAS   * Reason
          MOVE      SP70,MRHIOPER       * Operator
.
          CALL      CALDUE00              * Calculates the due date of record
.
          PACK      KEY7,MRHIDHOS,MRHILOC,SP70
          CALL      RDMRLOC1
          MATCH     "2",MRLOINDC
          IF        @EQUAL
            MOVE      ONE,MRHISTAT        * external
          ELSE
            MOVE      TWO,MRHISTAT        * internal
          ENDIF
.
          UNPACK    SP70,MRHIRCST,MRHIRCUI,MRHIRCDT,MRHIRCTM
.
          MATCH     "1",MRCNTREC            * System Using Tracking Receipt
          IF        @EQUAL
            MATCH     "1",MRLOTREC          * Location Using Tracking Receipt
            IF        @EQUAL
              MOVE      "1",MRHIRCST        * In Transit
            ELSE
              MOVE      "3",MRHIRCST        * Sent Direct
            ENDIF
          ENDIF
.
          MOVE      SP70,MRHIREQB
          PACK      KEY20,OBASITE,OBACLIN,Z70
          CALL      RDSCLIA1
          CALL      RDPCLIA1
          BRANCH    OVRCD,HISSAV80
.
          MATCH     OBASITE,OCASITE
          GOTO      HISSAV80 IF NOT EQUAL
.
          MATCH     OBACLIN,OCACLIN
          GOTO      HISSAV80 IF NOT EQUAL
.
          MOVE      OCACLIN,D6
          STRIP     D6
          PACK      MRHIREQB,D6,SP1,SEPCHAR,SP1,OCADESC,SP70              
.
HISSAV80  MATCH     SP70,MRTSECNO
          IF        !@EQUAL
            MOVE      MRTSECID,MRHIUSID   * MRT Security number User ID
          ELSE
            MOVE      WBSEUID,MRHIUSID    * Web User ID
          ENDIF
          MOVE      SP70,MRHILOCK
          MOVE      SP70,MRHIEXTN
          MOVE      SP70,MRHIPAGE
          MOVE      SP70,MRHICOMM
.
HISSAV99  RETURN
.------------------------------------------------------------
. * Calculates the due dat of a Medical Record
.------------------------------------------------------------
CALDUE00  MOVE      SP70,MRHIDDUE
.
          PACK      KEY4,MRHIREAS
          CALL      RDMRMOV1
          BRANCH    OVRCD,CALDUE99
.
          CALL      IBACLOCK
          MOVE      CDD,DD
          MOVE      CMM,MM
          MOVE      CYY,YY
          MOVE      CCC,CC
          CALL      DMYCON                      * convert to julian date
          ADD       MRMOPERD,JULDAY             * add no. of days to date
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON                      * convert julian date back
.
          PACK      MRHIDDUE,CC,YY,MM,DD        * Returned Date
          REPLACE   " 0",MRHIDDUE               * replace spaces with zero's
.
CALDUE99  RETURN
.------------------------------------------------------------
. Loop through Records and save Movements
.------------------------------------------------------------
SAVHIS00  MOVE      ZERO,MRKEYCNT
          MOVE      ZERO,ERRORCNT
.
          PACK      DATE,CCC,CYY,CMM,CDD
          REP       " 0",DATE          
          CLOCK     TIME,TIME
.
SAVHIS10  ADD       ONE,MRKEYCNT
          IF        MRKEYCNT>MRKEYEND
            GOTO       SAVHIS99
          ENDIF
.
          MATCH     SP70,MRRECKEY[MRKEYCNT]  * = corresponding UR
          GOTO      SAVHIS10 IF EQUAL
          UNPACK    MRRECKEY[MRKEYCNT],KEY10,KEY28
.
          PACK      KEY10,KEY10,SP70   * Reads MRTMASFD
          CALL      RDMRMAS2
          BRANCH    OVRCD,SAVHIS90
.
          PACK      KEY28,KEY28,SP70        
          CALL      RDBOKA1                   * Reads OUTBOAFD 
          BRANCH    OVRCD,SAVHIS91
.
          PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT,SP70
          CALL      RDMASA1                   * read outmas
          BRANCH    OVRCD,SAVHIS92
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
          CALL      RDSESA1
          BRANCH    OVRCD,SAVHIS92
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,SAVHIS92
.
.                                      * CAR 150948
SAVHIS40  MATCH     "0",MRCNDISA       * don't display all records
          IF        @EQUAL
            MOVE      ONE,OBAPULL
            CALL      UPBOKA1
          ENDIF
.
          IF        MRCNCDOP=1
            PACK      OBADATE,DATE,SP70
            PACK      OBATIME,TIME,SP70
          ENDIF
.
          PACK      KEY26,MRMAKEY,OBADATE,OBATIME,SP70
          CALL      RDMRHIS1
          IF        OVRCD=1
            CALL      HISSAV00  * Save Variables
            PACK      KEY26,MRHIKEY,OBADATE,OBATIME,SP70
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            CALL      WRMRHIS1
            TRAPCLR   IO
            BRANCH    OVRCD,SAVHIS10
.
            MOVE      ONE,HL7TRGID
            MOVE      SP8,HL7INCLD
            PROC      DGCLICRM              * DG Pass Med.Rec.Movement *I-90203
.
            PACK      KEY20,MRMAURNO,MRMAHHOS,MRMAHLOC,MRMADOTY,DMRMAVOL,SP70
            CALL      RDMRMAS1       * Updates the master file with current loc
            MOVE      MRHIDHOS,MRMACHOS
            MOVE      MRHILOC,MRMACLOC
            CALL      IBACLOCK
            PACK      MRMADTUP,CCC,CYY,CMM,CDD
            REP       " 0",MRMADTUP
            MOVE      CTIMEIS,MRMATMUP
            MOVE      WBSEUID,MRMAUUID
            CALL      UPMRMAS1
          ENDIF
          GOTO      SAVHIS10
.
SAVHIS90  ADD       ONE,ERRORCNT
          IF        ERRORCNT > 99
            GOTO      SAVHIS10
          ENDIF 
          CLEAR     ERRORLST[ERRORCNT]
          APPEND    "Invalid Medical Record Key - ",ERRORLST[ERRORCNT]
          APPEND    KEY10,ERRORLST[ERRORCNT]
          RESET     ERRORLST[ERRORCNT]
          GOTO      SAVHIS10
.
SAVHIS91  ADD       ONE,ERRORCNT
          IF        ERRORCNT > 99
            GOTO      SAVHIS10
          ENDIF 
          CLEAR     ERRORLST[ERRORCNT]
          APPEND    "Invalid Outpatient booking for UR - ",ERRORLST[ERRORCNT]
          APPEND    MRMAURNO,ERRORLST[ERRORCNT]
          RESET     ERRORLST[ERRORCNT]
          GOTO      SAVHIS10
.
SAVHIS92  ADD       ONE,ERRORCNT
          IF        ERRORCNT > 99
            GOTO      SAVHIS10
          ENDIF 
          CLEAR     ERRORLST[ERRORCNT]
          APPEND    "Invalid Clinic Record - ",ERRORLST[ERRORCNT]
          APPEND    KEY18,ERRORLST[ERRORCNT]
          RESET     ERRORLST[ERRORCNT]
          GOTO      SAVHIS10
.
SAVHIS99  RETURN
.------------------------------------------------------------
. Pre-Admission Medical Records Movement Lists by Date
.------------------------------------------------------------
MRTPRE00  RESET     UPDATETY
          MATCH     SP1,UPDATETY
          GOTO      MRTPRE40 IF EQUAL
.                                                           
          MATCH     "A",UPDATETY    * Add formaction        
          GOTO      MRTPRE10 IF EQUAL                       
.                                                           
          GOTO      MRTPRE99                                
.                                                           
.         ************ ADD FORMACTION **********
.
MRTPRE10  CALL      SAVPRE00      * Loop through MR's and save Movements
          MOVE      ZERO,EXIT
          COMPARE   ZERO,ERRORCNT
          GOTO      MRTPRE99 IF EQUAL
          CALL      ERRLST00
          MOVE      ONE,EXIT
          GOTO      MRTPRE99
.
.         ************ DISPLAY FORMACTION **********
.
MRTPRE40  CALL      CURPER00   * Determine Current Period
          CALL      CALCDT00   * Calculate Next and Last Period Dates
.
          MOVE      "Pre-admission Pulling List/Movement",WEBTITLE
          CALL      WEBHED00
          BRANCH    EXIT,MRTPRE99
.
          CALL      WEBBOD00
.
          CALL      WEBEND00
          MOVE      ONE,EXIT
          GOTO      MRTPRE99
.
MRTPRE91  MOVE      "Medical Record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MRTPRE99
.
MRTPRE92  MOVE      "Medical Record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MRTPRE99
.
MRTPRE93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MRTPRE99
.
MRTPRE99  RETURN
.-----------------------------------------------------------
. Save routine - move cgi parameters into file variables
.-----------------------------------------------------------
PRESAV00  MOVE      ZERO,EXIT
          MOVE      MRMAKEY,MRHIKEY
          MOVE      DATE,MRHIDATE
          MOVE      TIME,MRHITIME
.
          MATCH     SP70,LOCATION
          IF        @EQUAL
            MOVE      MEDILOCH,MRHIDHOS  
            MOVE      MEDILOCN,MRHILOC   * Admitting Point MR Location
          ELSE
            MOVE      LOCNHOSP,MRHIDHOS  
            MOVE      LOCATION,MRHILOC
          ENDIF
.
          MATCH     SP70,MRHILOC         * dont move if no location
          GOTO      PRESAV90 IF EQUAL
.
          MOVE      SP70,MRHIRECV
.
          MOVE      MRCNMOVP,MRHIREAS
          MOVE      SP70,MRHIOPER
.
          CALL      CALDUE00              * Calculates the due date of record
.
          PACK      KEY7,MRHIDHOS,MRHILOC,SP70
          CALL      RDMRLOC1
          MATCH     "2",MRLOINDC
          IF        @EQUAL
            MOVE      ONE,MRHISTAT        * external
          ELSE
            MOVE      TWO,MRHISTAT        * internal
          ENDIF
.
          UNPACK    SP70,MRHIRCST,MRHIRCUI,MRHIRCDT,MRHIRCTM
.
          MATCH     "1",MRCNTREC            * System Using Tracking Receipt
          IF        @EQUAL
            MATCH     "1",MRLOTREC          * Location Using Tracking Receipt
            IF        @EQUAL
              MOVE      "1",MRHIRCST        * In Transit
            ELSE
              MOVE      "3",MRHIRCST        * Sent Direct
            ENDIF
          ENDIF
.
          MOVE      SP70,MRHIREQB
..          MOVE      WBSEUID,MRHIREQB  * Requested By
          MOVE      SP70,MRHIREQB  * Requested By
.
          MATCH     SP70,MRTSECNO
          IF        !@EQUAL
            MOVE      MRTSECID,MRHIUSID   * MRT Security number User ID
          ELSE
            MOVE      WBSEUID,MRHIUSID    * Web User ID
          ENDIF
.
          MOVE      SP70,MRHILOCK
          MOVE      SP70,MRHIEXTN
          MOVE      SP70,MRHIPAGE
          MOVE      SP70,MRHICOMM
          GOTO      PRESAV99
.
PRESAV90  MOVE      ONE,EXIT
.
PRESAV99  RETURN
.------------------------------------------------------------
. Loop through Pre-admission Medical Records and save Movements
.------------------------------------------------------------
SAVPRE00  MOVE      ZERO,MRKEYCNT
          MOVE      ZERO,ERRORCNT
.
          PACK      DATE,CCC,CYY,CMM,CDD
          REP       " 0",DATE          
          CLOCK     TIME,TIME
.
SAVPRE10  ADD       ONE,MRKEYCNT
          IF        MRKEYCNT>MRKEYEND
            GOTO       SAVPRE99
          ENDIF
.
.         Not fulfilling Medical Record
          MATCH     SP70,MRRECKEY[MRKEYCNT]  * = corresponding UR
          IF        @EQUAL
. 
.           Move Medical Record to keep List
            MATCH     SP70,MRKEPLST[MRKEYCNT]  
            IF        !@EQUAL
              UNPACK    MRKEPLST[MRKEYCNT],KEY10,KEY8 
.
.             Is the report already present in the table
              PACK      KEY20,KEY8,KEY10,ZERO,ONE,SP70
              CALL      RDMRPLL1
              IF        OVRCD = 0
                GOTO      SAVPRE10
              ENDIF
              PACK      KEY20,KEY8,KEY10,ZERO,TWO,SP70
              CALL      RDMRPLL1
              IF        OVRCD = 0
                GOTO      SAVPRE10
              ENDIF
.
.             If not then the report is added to the table
              CALL      CLMRTPLL
              MOVE      USERID,MRPLCUID
              CLOCK     TIME,MRPLCTIM
              PACK      MRPLCDAT,DATE
              MOVE      "01",MRPLRQST
              MOVE      KEY8,MRPLVISN
              MOVE      KEY10,MRPLMRKY
              PACK      KEY20,MRPLVISN,MRPLMRKY,MRPLRQST,SP70
              CALL      WRMRPLL1
              GOTO      SAVPRE10
            ENDIF 
.
.           Remove from Pull List
            MATCH     SP70,MRRMVLST[MRKEYCNT]
            IF        !@EQUAL
              UNPACK    MRRMVLST[MRKEYCNT],KEY10,KEY8 
              PACK      KEY20,KEY8,KEY10,ZERO,TWO,SP70
              CALL      RDMRPLL1
              IF        OVRCD = 0
                GOTO      SAVPRE10
              ENDIF
              PACK      KEY20,KEY8,KEY10,ZERO,ONE,SP70
              CALL      RDMRPLL1
              IF        OVRCD = 0
                MOVE      "02",MRPLRQST
                MOVE      USERID,MRPLUUID
                CLOCK     TIME,MRPLUTIM
                PACK      MRPLUDAT,DATE
                CALL      UPMRPLL1
                GOTO      SAVPRE10
              ENDIF
              CALL      CLMRTPLL
              MOVE      USERID,MRPLCUID
              CLOCK     TIME,MRPLCTIM
              PACK      MRPLCDAT,DATE
              MOVE      "02",MRPLRQST
              MOVE      KEY8,MRPLVISN
              MOVE      KEY10,MRPLMRKY
              PACK      KEY20,MRPLVISN,MRPLMRKY,MRPLRQST,SP70
              CALL      WRMRPLL1
              GOTO      SAVPRE10
            ENDIF 
            GOTO      SAVPRE10
          ENDIF
.
          UNPACK    MRRECKEY[MRKEYCNT],KEY10,KEY8,D1
          MOVE      D1,F1  * Slot No from OUTBOAFD
.
          PACK      KEY20,KEY8,KEY10,ZERO,ONE,SP70
          CALL      RDMRPLL1
          IF        OVRCD = 0
            CALL      DEMRPLL1
          ENDIF
.
          MOVE      SP10,MEDILOCN   * Clear Admitting Point Location
          RESET     LOCATION        * Must reset CGI VARS before match!
.                                   * just incase it is not on template
.
.         Get the M/R Location of the admitting point & set to moved off list!
.
          COMPARE   ZERO,F1
          GOTO      SAVPRE20 IF NOT EQUAL
.
          PACK      KEY8,KEY8,SP70     
          CALL      RDMISS1                   * Reads PATMI1FD  
          BRANCH    OVRCD,SAVPRE90
.
          CALL      RDPMVX11                   
          BRANCH    OVRCD,SAVPRE30
.
          MATCH     SP70,PMVXAPWD
          IF        !@EQUAL
            PACK      KEY5,CODEAP,PMVXAPWD,SP70
            CALL      RDCODE1
            IF        OVRCD=1 
              MATCH     SP70,LOCATION
              GOTO      SAVPRE93 IF EQUAL
            ENDIF
.
            MOVE      PTCDUDF2,MEDILOCH     * Admitting Point MR Loc Hospital
            MOVE      PTCDNHCP,MEDILOCN     * Admitting Point MR Location
.
            MATCH     SP70,PTCDNHCP
            IF        @EQUAL
              PACK      KEY6,THCSCOD,SP70   * Read Ward to get mr location
              CALL      RDWARD1
              IF        OVRCD=1 
                MATCH     SP70,LOCATION
                GOTO      SAVPRE93 IF EQUAL
              ENDIF
              MOVE      PTWDHOSN,MEDILOCH
              MOVE      PTWDLOCN,MEDILOCN
            ENDIF
          ELSE
            MATCH     SP70,LOCATION
            GOTO      SAVPRE93 IF EQUAL
          ENDIF
.
          MOVE      "Y",PTMIDMOV            * Move off List
          CALL      UPPTMIS1
.
          GOTO      SAVPRE30
.
SAVPRE20  PACK      KEY8,KEY8,SP70     
          CALL      RDBKREC1
          BRANCH    OVRCD,SAVPRE91
.
          CALL      RDBKRX11
          BRANCH    OVRCD,SAVPRE91
.
          MATCH     SP70,BKRXADWD
          IF        !@EQUAL
            PACK      KEY5,CODEAP,BKRXADWD,SP70
            CALL      RDCODE1
            IF        OVRCD=1 
              MATCH     SP70,LOCATION
              GOTO      SAVPRE93 IF EQUAL
            ENDIF
.
            MOVE      PTCDUDF2,MEDILOCH     * Admitting Point MR Loc Hospital
            MOVE      PTCDNHCP,MEDILOCN     * Admitting Point MR Location
.
            MATCH     SP70,PTCDNHCP
            IF        @EQUAL
              PACK      KEY6,THCSCOD,SP70   * Read Ward to get mr location
              CALL      RDWARD1
              IF        OVRCD=1 
                MATCH     SP70,LOCATION
                GOTO      SAVPRE93 IF EQUAL
              ENDIF
              MOVE      PTWDHOSN,MEDILOCH
              MOVE      PTWDLOCN,MEDILOCN
            ENDIF
          ELSE
            MATCH     SP70,LOCATION
            GOTO      SAVPRE93 IF EQUAL
          ENDIF
.
          MOVE      "Y",BKREINDC            * Move off List
          CALL      UPBKREC1                * Update Movement indicator
.
SAVPRE30  PACK      KEY10,KEY10,SP70        
          CALL      RDMRMAS2                  * Reads MRTMASFD 
          BRANCH    OVRCD,SAVPRE92
.
          PACK      KEY26,MRMAKEY,DATE,TIME,SP70
          CALL      RDMRHIS1
          IF        OVRCD=0
            GOTO      SAVPRE10
          ENDIF
.
          CALL      PRESAV00  * Save Variables
          BRANCH    EXIT,SAVPRE93
.
          PACK      KEY26,MRMAKEY,DATE,TIME,SP70
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRMRHIS1
          TRAPCLR   IO
          BRANCH    OVRCD,SAVPRE10
.
          MOVE      TWO,HL7TRGID
          MOVE      SP8,HL7INCLD
          PROC      DGCLICRM              * DG Pass Med.Rec.Movement *I-90203
.
          PACK      KEY20,MRMAURNO,MRMAHHOS,MRMAHLOC,MRMADOTY,DMRMAVOL,SP70
          PACK      KEY10,MRMAKEY,SP70
          CALL      RDMRMAS2       * Updates the master file with current loc
          BRANCH    OVRCD,SAVPRE92

.     Update the current location with the M/R LOCATION CGI var or the 
.     Admitting point
.
          MATCH     SP70,LOCATION
          IF        @EQUAL
            MOVE      MEDILOCH,MRMACHOS  * Set Current Location Hospital
            MOVE      MEDILOCN,MRMACLOC  * Set Current Location
          ELSE
            MOVE      LOCNHOSP,MRMACHOS  * Set Current Location Hospital
            MOVE      LOCATION,MRMACLOC  * Set Current Location
          ENDIF
.                
          MATCH     SP70,MRMACLOC
          GOTO      SAVPRE93 IF EQUAL
.
          CALL      IBACLOCK
          PACK      MRMADTUP,CCC,CYY,CMM,CDD
          REP       " 0",MRMADTUP
          MOVE      CTIMEIS,MRMATMUP
          MOVE      WBSEUID,MRMAUUID
.
          PACK      KEY20,MRMAURNO,MRMAHHOS,MRMAHLOC,MRMADOTY,DMRMAVOL,SP70
          CALL      RAMRMAS1
          IF        OVRCD=0         
            CALL      UPMRMAS1
          ENDIF
          GOTO      SAVPRE10
.
SAVPRE90  ADD       ONE,ERRORCNT
          IF        ERRORCNT > 99
            GOTO      SAVPRE10
          ENDIF 
          CLEAR     ERRORLST[ERRORCNT]
          APPEND    "Invalid Admission Rec- ",ERRORLST[ERRORCNT]
          APPEND    KEY8,ERRORLST[ERRORCNT]
          RESET     ERRORLST[ERRORCNT]
          GOTO      SAVPRE10
.
SAVPRE91  ADD       ONE,ERRORCNT
          IF        ERRORCNT > 99
            GOTO      SAVPRE10
          ENDIF 
          CLEAR     ERRORLST[ERRORCNT]
          APPEND    "Invalid Booking Record - ",ERRORLST[ERRORCNT]
          APPEND    KEY8,ERRORLST[ERRORCNT]
          RESET     ERRORLST[ERRORCNT]
          GOTO      SAVPRE10
.
SAVPRE92  ADD       ONE,ERRORCNT
          IF        ERRORCNT > 99
            GOTO      SAVPRE10
          ENDIF 
          CLEAR     ERRORLST[ERRORCNT]
          APPEND    "Invalid Medical Record Key - ",ERRORLST[ERRORCNT]
          APPEND    KEY10,ERRORLST[ERRORCNT]
          RESET     ERRORLST[ERRORCNT]
          GOTO      SAVPRE10
.
SAVPRE93  ADD       ONE,ERRORCNT
          IF        ERRORCNT > 99
            GOTO      SAVPRE10
          ENDIF
          CLEAR     ERRORLST[ERRORCNT]
          APPEND    "Missing Location - ",ERRORLST[ERRORCNT]
          APPEND    KEY10,ERRORLST[ERRORCNT]
          RESET     ERRORLST[ERRORCNT]
          GOTO      SAVPRE10
.
SAVPRE94  ADD       ONE,ERRORCNT
          IF        ERRORCNT > 99
            GOTO      SAVPRE10
          ENDIF
          CLEAR     ERRORLST[ERRORCNT]
          APPEND    "Invalid admitting point- ",ERRORLST[ERRORCNT]
          APPEND    KEY10,ERRORLST[ERRORCNT]
          RESET     ERRORLST[ERRORCNT]
          GOTO      SAVPRE10
.
SAVPRE99  RETURN
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20

          GOTO      PROFLD99
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11
.
PROFLD11  CALL      MFIL0000                * O/P Request Filled List
          GOTO      PROFLD99
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21
.
PROFLD21  CALL      PFIL0000                * Pre-admission Request Filled List
          GOTO      PROFLD99
.
PROFLD99  RETURN
.------------------------------------------------------------
. List by Date Range
.------------------------------------------------------------
MFIL0000  BRANCH    FLDITMNO,MFIL0100,MFIL0200,MFIL0300,MFIL0400:
                             MFIL0500,MFIL0600,MFIL0700,MFIL0800:
                             MFIL0900,MFIL1000,MFIL1100,MFIL1200:
                             MFIL1300,MFIL1400,MFIL1500,MFIL1600:
                             MFIL1700,MFIL1800,MFIL1900,MFIL2000:
                             MFIL2100
.
MFIL0100  CALL      NEXT0000
          GOTO      MFIL9999
.
MFIL0200  CALL      PREV0000
          GOTO      MFIL9999
.
MFIL0300  CALL      CURSTD00
          GOTO      MFIL9999
.
MFIL0400  CALL      CUREND00
          GOTO      MFIL9999
.
MFIL0500  CALL      CURYR000
          GOTO      MFIL9999
.
MFIL0600  CALL      CURMON00
          GOTO      MFIL9999
.
MFIL0700  CALL      SELV0000
          GOTO      MFIL9999
.
MFIL0800  WRITE     HTMLFILE;TEMPLATE;
          GOTO      MFIL9999
.
MFIL0900  WRITE     HTMLFILE;REPORTNO;
          GOTO      MFIL9999
.
MFIL1000  WRITE     HTMLFILE;VIEWTYPE;
          GOTO      MFIL9999
.
MFIL1100  CALL      FILTB000               * Table of Outpatient Pulling Lists
          GOTO      MFIL9999
.
MFIL1200  CALL      CLITYP00               * Clinic type selection list
          GOTO      MFIL9999
.
MFIL1300  CALL      CLIOPT00               * Clinic ID selection list 
          GOTO      MFIL9999
.
MFIL1400  WRITE     HTMLFILE;ADDITION;
          GOTO      MFIL9999
.
MFIL1500  MOVE      "CT",CATEGORY
          MOVE      LOCATION,CODE
          CALL      CODITM00
          GOTO      MFIL9999
.
MFIL1600  WRITE     HTMLFILE;EXCEPTNS;
          GOTO      MFIL9999
.
MFIL1700  CALL      CLIDET00                     * Clinic Id's
          GOTO      MFIL9999
.
MFIL1800  WRITE     HTMLFILE;CLINICTY;           * Clinic Type
          GOTO      MFIL9999
.
MFIL1900  MOVE      "TY",CATEGORY                * MR document type filter
          MATCH     Z70,FILTDOTY                
          IF        @EQUAL
            IF        IBCNMHOS=1
              PACK      KEY3,WBSEHOSP,SP70       * If multi hospital default
              CALL      RDPTHSP1                 * document type from hospital
              IF        OVRCD=1
                MOVE      MRCNRCTY,FILTDOTY
              ELSE
                MATCH     SP70,PTHSRCTY
                IF        @EQUAL
                  MOVE      MRCNRCTY,FILTDOTY
                ELSE
                  MOVE      PTHSRCTY,FILTDOTY
                ENDIF
              ENDIF
            ELSE
              MOVE      MRCNRCTY,FILTDOTY
            ENDIF
          ENDIF

          MOVE      FILTDOTY,CODE
          CALL      CODITM00
          GOTO      MFIL9999
.                                                                     Car 233039
MFIL2000  MOVE      "CV",CATEGORY
          MOVE      FILTVITY,CODE
          CALL      CODITM00
          GOTO      MFIL9999
.
MFIL2100  WRITE     HTMLFILE;ALLHOMEH;           * All home hospitals on pulling
          GOTO      MFIL9999                     * list
.
MFIL9999  RETURN
.*********************************************************************
.             CLIDET00-Clinic Id
.*********************************************************************
CLIDET00  UNPACK    DIM127,ANS,KEY1,DIM127
          MATCH     SP70,CLINICID
          GOTO      CLIDET99 IF EQUAL
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,CLIDET10
.
          PACK      KEY20,WBSESIT,CLINICID,Z70
          CALL      RDSCLIA1
          CALL      RDPCLIA1
          BRANCH    OVRCD,CLIDET99
.
          MATCH     OCASITE,WBSESIT
          GOTO      CLIDET99 IF NOT EQUAL
.
          MATCH     OCACLIN,CLINICID
          GOTO      CLIDET99 IF NOT EQUAL
.
          WRITE     HTMLFILE;OCADESC;
          GOTO      CLIDET99
.
CLIDET10  WRITE     HTMLFILE;CLINICID;
.
CLIDET99  RETURN
.------------------------------------------------------------
. Next Day, Week, Month
.------------------------------------------------------------
NEXT0000  MOVE      VIEWTYPE,F1
          BRANCH    F1,NEXT0100,NEXT0200
          WRITE     HTMLFILE;"SelectNextDay();";
          GOTO      NEXT9999
NEXT0100  WRITE     HTMLFILE;"SelectNextWeek();";
          GOTO      NEXT9999
NEXT0200  WRITE     HTMLFILE;"SelectNextMonth();";
          GOTO      NEXT9999
NEXT9999  RETURN
.------------------------------------------------------------
. Last Day, Week, Month
.------------------------------------------------------------
PREV0000  MOVE      VIEWTYPE,F1
          BRANCH    F1,PREV0100,PREV0200
          WRITE     HTMLFILE;"SelectLastDay();";
          GOTO      PREV9999
PREV0100  WRITE     HTMLFILE;"SelectLastWeek();";
          GOTO      PREV9999
PREV0200  WRITE     HTMLFILE;"SelectLastMonth();";
          GOTO      PREV9999
PREV9999  RETURN
.------------------------------------------------------------
. Table of Request Filled List                 
.------------------------------------------------------------
FILTB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          MOVE      ZERO,RECCOUNT
          MOVE      SP70,HOMEHOSP
.
          IF        IBCNMHOS=1
            PACK      KEY3,WBSEHOSP,SP70
            CALL      RDPTHSP1
            IF        OVRCD<>1
              MOVE     PTHSHHOS,HOMEHOSP    * Save hospitals MR home hospital
            ENDIF
          ENDIF
.
          MATCH     SP70,CLINICID
          IF        @EQUAL
            PACK      CLINICID,WBSECLI,SP70
            MATCH     SP70,CLINICID
            IF        @EQUAL
              MOVE      "999999",CLINICID       * All Clinics
            ENDIF
          ENDIF
.
          MATCH     FRSTDATE,LASTDATE       * Single Day for List Only
          GOTO      FILTB920 IF NOT EQUAL
.
          MATCH     SP70,WBSESIT            * NO ALL OPTION for Clinic
          GOTO      FILTB930 IF EQUAL
.
          MATCH     "999999",CLINICID       * All Clinics
          GOTO      FILTB190 IF EQUAL
.
          PACK      KEY20,WBSESIT,CLINICID,FRSTDATE,Z70
          CALL      RDCLIA1
          IF        OVRCD=1
            CALL      RDSCLIA1                 * Get clinic id description
            CALL      RDPCLIA1                 * Get clinic id description
            BRANCH    OVRCD,FILTB010
.
            MATCH     WBSESIT,OCASITE
            GOTO      FILTB010 IF NOT EQUAL
.
            MATCH     CLINICID,OCACLIN
            GOTO      FILTB020 IF EQUAL
         ENDIF
.
FILTB010  MOVE      "Unknown clinic",OCADESC
.
FILTB020  MATCH     SP70,BOOKNKEY      * Next Group of Records Key!!
          IF        !@EQUAL
            PACK      KEY28,BOOKNKEY,SP70
            CALL      RDBOKA1
            BRANCH    OVRCD,FILTB999
            GOTO      FILTB110 
          ENDIF
.
          PACK      KEY28,WBSESIT,CLINICID,FRSTDATE,SP70
          CALL      RDSBOKA1  
FILTB100  CALL      RDKBOKA1                * Reads Clinic file
          BRANCH    OVRCD,FILTB999
.
FILTB110  PACK      KEY28,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT,SP70
          PACK      KEY20,WBSESIT,CLINICID,FRSTDATE,SP70
.
          MATCH     KEY20,KEY28
          GOTO      FILTB999 IF NOT EQUAL                      
.                                                                     Car 233039
          MATCH     SP70,FILTVITY
          IF        @EQUAL
            GOTO      FILTB115
          ENDIF
.
          MATCH     OBAVISIT,FILTVITY
          IF        !@EQUAL
            GOTO      FILTB100
          ENDIF
.
.....                                  * CAR 150948
.....     MATCH     "0",MRCNDISA       * don't display all records
.....     IF        @EQUAL
.....       COMPARE   ONE,OBAPULL      * ignore already pulled records
.....       GOTO      FILTB100 IF EQUAL
.....     ENDIF
.
FILTB115  COMPARE   TWO,ADDITION            * If Additions passes back Check
          IF        !@EQUAL
            COMPARE   OBAPULL,ADDITION
            GOTO      FILTB100 IF NOT EQUAL
          ENDIF
.
          BRANCH    OBASTAT,FILTB120,FILTB100,FILTB100,FILTB120
          GOTO      FILTB100
.
FILTB120  UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      DAYOFWEK
          PACK      KEY18,OBASITE,OBACLIN,WEEKDAY,OBASTRT
          CALL      RDMASA1
          BRANCH    OVRCD,FILTB100  
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
          CALL      RDSESA1
          BRANCH    OVRCD,FILTB100
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,FILTB100
.
          MATCH     "1",OTHEXPUL         * exclude from pull list ? 
          GOTO      FILTB100 IF EQUAL
.
          IF        IBCNMHOS=1
            MATCH     "1",OTCNHSEC
            IF        @EQUAL
              PACK      KEY13,USERID,SP70              * All hospital access
              CALL      RDMLSEC1
              IF        OVRCD=1
                PACK      KEY13,USERID,OTHEHOSP,SP70   * This hospital access
                CALL      RDMLSEC1
                BRANCH    OVRCD,FILTB100
              ENDIF
.
              PACK      KEY3,OTHEHOSP,SP70
              CALL      RDPTHSP1
              IF        OVRCD<>1
                MOVE      PTHSHHOS,HOMEHOSP    * Save hospitals MR home hospital
              ENDIF
            ELSE
              MATCH     WBSEHOSP,OTHEHOSP          * Only display bookings for
              GOTO      FILTB100 IF NOT EQUAL      * the users current hospital
            ENDIF
          ELSE
            MOVE        SP70,OTHEHOSP              * Clear hospital field
          ENDIF
.
          MATCH     SP70,LOCATION   * Filter on Clinic Location 
          IF        !@EQUAL
            MATCH     LOCATION,OTHELTYP
            GOTO      FILTB100 IF NOT EQUAL
          ENDIF         
.
          MATCH     SP70,CLINICTY
          IF        !@EQUAL
            MATCH     CLINICTY,OTHECTYP 
            GOTO      FILTB100 IF NOT EQUAL
          ENDIF
.
.          IF        RECCOUNT>50
.            PACK      KEY28,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT,SP70
.            WRITE     HTMLFILE;"LimitReached();"
.            WRITE     HTMLFILE;"NextRecordSet('",KEY28,"');"
.            GOTO      FILTB999
.          ENDIF
          CALL      FILRW000                * Output Pulling List Row
          GOTO      FILTB100
.
.   ***  All Clinics  ***
.
FILTB190  MATCH     SP70,BOOKNKEY      * Next Group of Records Key!! 
          IF        !@EQUAL
            UNPACK    BOOKNKEY,SITE,CLINIC,SESDATE,SESTIME    * Set Session Key
            PACK      KEY25,SESDATE,SESTIME,SITE,CLINIC,SP70  * Set Session Key
            CALL      RDSESA2
            BRANCH    OVRCD,FILTB999
            GOTO      FILTB250
          ENDIF
.
          MATCH     SP70,NEXTSESS
          IF        !@EQUAL
            PACK      KEY25,NEXTSESS,SP70
            CALL      RDSESA2
            BRANCH    OVRCD,FILTB999
            GOTO      FILTB250
          ENDIF
.
          PACK      KEY25,FRSTDATE,SP70
          CALL      RDSSESA2
FILTB200  CALL      RDKSESA2
          BRANCH    OVRCD,FILTB999
.
FILTB250  MATCH     OSEDATE,FRSTDATE
          GOTO      FILTB999 IF NOT EQUAL
.
          MATCH     OSESITE,WBSESIT
          GOTO      FILTB200 IF NOT EQUAL
.
          PACK      KEY18,OSESITE,OSECLIN,OSEDAY,OSESTRT
          CALL      RDMASA1
          BRANCH    OVRCD,FILTB200  
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,FILTB200
.
          MATCH     "1",OTHEXPUL         * exclude from pull list ?
          GOTO      FILTB200 IF EQUAL
.
          IF        IBCNMHOS=1
            MATCH     "1",OTCNHSEC
            IF        @EQUAL
              PACK      KEY13,USERID,SP70              * All hospital access
              CALL      RDMLSEC1
              IF        OVRCD=1
                PACK      KEY13,USERID,OTHEHOSP,SP70   * This hospital access
                CALL      RDMLSEC1
                BRANCH    OVRCD,FILTB200
              ENDIF
.
              PACK      KEY3,OTHEHOSP,SP70
              CALL      RDPTHSP1
              IF        OVRCD<>1
                MOVE      PTHSHHOS,HOMEHOSP    * Save hospitals MR home hospital
              ENDIF
            ELSE
              MATCH     WBSEHOSP,OTHEHOSP          * Only display bookings for
              GOTO      FILTB200 IF NOT EQUAL      * the users current hospital
            ENDIF
          ELSE
            MOVE        SP70,OTHEHOSP              * Clear hospital field
          ENDIF
.
          MATCH     SP70,LOCATION   * Filter on Clinic Location 
          IF        !@EQUAL
            MATCH     LOCATION,OTHELTYP
            GOTO      FILTB200 IF NOT EQUAL
          ENDIF         
.
          MATCH     SP70,CLINICTY
          IF        !@EQUAL
            MATCH     CLINICTY,OTHECTYP 
            GOTO      FILTB200 IF NOT EQUAL
          ENDIF
.
          PACK      NEXTSESS,OSEDATE,OSESTRT,OSESITE,OSECLIN,SP70
.          IF        RECCOUNT>50
.            WRITE     HTMLFILE;"LimitReached();"
.            WRITE     HTMLFILE;"SetNextPage('",NEXTSESS,"');"
.            GOTO      FILTB999
.          ENDIF
.
          PACK      KEY20,OSESITE,OSECLIN,OSEDATE,Z70
          CALL      RDCLIA1
          IF        OVRCD=1
            CALL      RDSCLIA1                 * Get clinic id description
            CALL      RDPCLIA1                 * Get clinic id description
.        
            MATCH     SP70,BOOKNKEY            * Next Group of Records Key!! 
            IF        !@EQUAL
              PACK      KEY28,BOOKNKEY,SP70
              MOVE      SP70,BOOKNKEY          * Clear so next sessions bookings
              CALL      RDBOKA1                * are not changed
              BRANCH    OVRCD,FILTB200
              GOTO      FILTB350
            ENDIF
          ENDIF
.
          PACK      SESSKEYS,OSESITE,OSECLIN,OSEDATE,OSESTRT,SP70
          PACK      KEY28,SESSKEYS,SP70
          CALL      RDSBOKA1  
FILTB300  CALL      RDKBOKA1                * Reads Clinic file
          BRANCH    OVRCD,FILTB200
.
          PACK      KEY28,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT,SP70
          MATCH     SESSKEYS,KEY28
          GOTO      FILTB200 IF NOT EQUAL                      
.                                                                     Car 233039
          MATCH     SP70,FILTVITY
          IF        @EQUAL
            GOTO      FILTB350
          ENDIF
.
          MATCH     OBAVISIT,FILTVITY
          IF        !@EQUAL
            GOTO      FILTB300
          ENDIF
.
FILTB350  COMPARE   TWO,ADDITION            * If Additions passes back Check
          IF        !@EQUAL
            COMPARE   OBAPULL,ADDITION
            GOTO      FILTB300 IF NOT EQUAL
          ENDIF
.
.....                                  * CAR 150948
.....     MATCH     "0",MRCNDISA       * don't display all records
.....     IF        @EQUAL
.....       COMPARE   ONE,OBAPULL      * ignore already pulled records
.....       GOTO      FILTB300 IF EQUAL
.....     ENDIF
.
          BRANCH    OBASTAT,FILTB310,FILTB300,FILTB300,FILTB310
          GOTO      FILTB300
.
.FILTB310  IF        RECCOUNT>50
.            PACK      KEY28,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT,SP70
.            WRITE     HTMLFILE;"LimitReached();"
.            WRITE     HTMLFILE;"NextRecordSet('",KEY28,"');"
.            GOTO      FILTB999
.          ENDIF
FILTB310  CALL      FILRW000                * Output Pulling List Row
          GOTO      FILTB300
.
FILTB910  WRITE     HTMLFILE;"alert('Clinic ID Must be Selected.');"
          GOTO      FILTB999
.
FILTB920  WRITE     HTMLFILE;"alert('Pulling List must be for a Single Day.');"
          GOTO      FILTB999
.
FILTB930  WRITE     HTMLFILE;"alert('Outpatient Site Invalid.');"
          GOTO      FILTB999
.
FILTB999  RETURN
.------------------------------------------------------------
. Output Pulling List Row
.------------------------------------------------------------
FILRW000  MOVE      ZERO,EXCPFLAG
          MOVE      ZERO,FOUNDFLG
          MOVE      ZERO,NOMRFLAG
          MOVE      SP70,SAVEDOTY
.
          PACK      SAVKEY28,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT
          CALL      VLSD0000                * Check for multiple bookings 
          IF        SDAYFLAG=1&FIRSTFLG=0&SCLIFLAG=1
            IF        EXCEPTNS=1
              MOVE      ONE,EXCPFLAG             * Show this exception record
              GOTO      FILRW150
            ENDIF
            GOTO      FILRW999
          ENDIF
.
FILRW150  MOVE      SAVKEY28,KEY28
          CALL      RDBOKA1
.
. If filtering by document type use index 4 to get highest volume by document
. type. If not using document type filter use index 3 to get the highest 
. volume for this UR
.
          PACK      KEY20,OBAURNO,Z70
.
          MATCH     Z70,FILTDOTY         
          IF        @EQUAL
            CALL      RSMRMAS3
          ELSE
            CALL      RSMRMAS4
          ENDIF
FILRW200  MATCH     Z70,FILTDOTY                  * Reads MRT master file
          IF        @EQUAL
            CALL      RPMRMAS3
          ELSE
            CALL      RPMRMAS4
          ENDIF
          BRANCH    OVRCD,FILRW300
.
          MATCH     MRMAURNO,OBAURNO 
          GOTO      FILRW300 IF NOT EQUAL
.
          IF        IBCNMHOS=1 & ALLHOMEH<>1
            MATCH     HOMEHOSP,MRMAHHOS          * Check home hospital
            GOTO      FILRW200 IF NOT EQUAL
          ENDIF
.
          MATCH     Z70,FILTDOTY                 * Using document type filter
          IF        @EQUAL
            MATCH     MRCNRCTY,MRMADOTY          * Look for Medical Record
            GOTO      FILRW200 IF NOT EQUAL
          ELSE
            MATCH     SP70,FILTDOTY              * All document type filter
            IF        @EQUAL
              MATCH     "1",MRCNDISA             * If not displaying all volumes
              IF        !@EQUAL
                MATCH     SP70,SAVEDOTY 
                IF        !@EQUAL
                  MATCH     SAVEDOTY,MRMADOTY    * Display the highest volume of
                  GOTO      FILRW200 IF EQUAL    * each document type
                ENDIF
              ENDIF
            ELSE
              MATCH     FILTDOTY,MRMADOTY        * Check document type filter
              GOTO      FILRW200 IF NOT EQUAL
            ENDIF
          ENDIF
.
          GOTO      FILRW500

FILRW300  BRANCH    FOUNDFLG,FILRW999           * Exit if MR already found
.
          MATCH     SP70,FILTDOTY               * All document type filter
          GOTO      FILRW450 IF EQUAL           * So do No MR test
.
          MATCH     Z70,FILTDOTY                * Using document type filter
          IF        !@EQUAL
            MATCH     MRCNRCTY,FILTDOTY         * Finished if filter doesn't
            GOTO      FILRW999 IF NOT EQUAL     * match default document type
          ENDIF
.  
          PACK      KEY20,OBAURNO,Z70
          CALL      RSMRMAS3
FILRW400  CALL      RPMRMAS3                    * Reads MRT master file
          BRANCH    OVRCD,FILRW450
.
          MATCH     MRMAURNO,OBAURNO 
          GOTO      FILRW450 IF NOT EQUAL
.
          IF        IBCNMHOS=1 & ALLHOMEH<>1
            MATCH     HOMEHOSP,MRMAHHOS          * Check home hospital
            GOTO      FILRW400 IF NOT EQUAL
          ENDIF
.
          PACK      KEY5,CODETY,MRMADOTY,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,FILRW400
.
          MATCH     "1",TCINDC1                 * Look for OUT or EMR record
          GOTO      FILRW400 IF NOT EQUAL
          GOTO      FILRW500
.
FILRW450  BRANCH    FOUNDFLG,FILRW999         
.
          MATCH     "1",MRCNNOMR                 * Include patients with no MR
          GOTO      FILRW999 IF NOT EQUAL        * on pulling lists
.
          MOVE      ONE,NOMRFLAG                 * Set No MR flag to yes
          CALL      CLMRTMAS                     * Clear MR master file fields
          MOVE      OBAURNO,MRMAURNO             * Set U/R from OP visit
.
FILRW500  IF        EXCEPTNS=1
            MATCH     MRMACHOS,OTHEHOSP          * check to see is current loc
            IF        @EQUAL
              MATCH     MRMACLOC,OTHELOCN        * check to see is current loc
              IF        @EQUAL
                MOVE      ONE,EXCPFLAG           * Show this exception record
              ENDIF
            ENDIF
          ELSE
            MATCH     MRMACHOS,OTHEHOSP          * check to see is current loc
            IF        @EQUAL
              MATCH     MRMACLOC,OTHELOCN         * check to see is current loc
              IF        @EQUAL
                MOVE      ONE,FOUNDFLG
                GOTO      FILRW900
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP70,MRMACHOS
          IF        @EQUAL
            MOVE      MRMAHHOS,MRMACHOS
          ENDIF
.
          MATCH     SP70,MRMACLOC
          IF        @EQUAL
            MOVE      MRMAHLOC,MRMACLOC
          ENDIF
.
          MOVE      SP70,MRLODESC      *Location Required
          PACK      KEY7,MRMACHOS,MRMACLOC,SP70
          CALL      RDMRLOC1
.
          MOVE      SP70,HOSPID01                                     *I-248696
          IF        IBCNMHOS = 1
            PACK      HOSPID01,OBB,MRMACHOS,CBB     * set default to black
            MATCH     MRMAHHOS,MRMACHOS
            IF        !@EQUAL
              MOVE      SP3,PTHSHHOS                        * start   *I-259106
              PACK      KEY3,MRMACHOS            * current hosp
              CALL      RDPTHSP1
              MATCH     PTHSHHOS,MRMAHHOS        * same as the home hospital ?
              IF        !@EQUAL
                PACK      HOSPID01,OBR,MRMACHOS,CBR     * set to red
              ELSE
                UNPACK    SP20,TCINDC1,TCINDC2,TCINDC3,TCINDC4
                PACK      KEY5,CODETY,PTHSRCTY
                CALL      RDCODE1
                MATCH     "B",TCINDC1
                IF        !@EQUAL
                  PACK      HOSPID01,OBR,MRMACHOS,CBR   * set to red
                ENDIF
              ENDIF                                         * end     *I-259106
            ENDIF
          ENDIF
.
          MOVE      SP70,PATFNAME
          MOVE      MRMAURNO,KEY8
          CALL      RDMAST1                 * Read Patient Master File
          BRANCH    OVRCD,FILRW999
.
          MOVE      MRMAURNO,KEY8
          CALL      RDPMPX21                * Read Patient Master Extn File
          BRANCH    OVRCD,FILRW999
.
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          REP       " 0",AGEDATE
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      PATLN000
.
          MOVE      CODETY,CATEGORY
          MOVE      MRMADOTY,CODE
          CALL      GETCOD00
.
          PACK      KEY40,MRMAKEY,WBSESIT,OBACLIN,OBADATE,OBASTRT,DOBASLOT,SP70
.
          IF        SDAYFLAG = 1
            MOVE     "Yes",SDAYDESC
          ELSE
            MOVE     "No ",SDAYDESC
          ENDIF
.
          MOVE      MRMAURNO,SORTORDR       *                         *I-240716
          CALL      MRSRTORD                * create sort order from SORTORDR
.
          MOVE      ONE,FOUNDFLG
          MOVE      MRMADOTY,SAVEDOTY       * Save last printed document type
.
          CALL      ITRN0000                     * Check if in transit 
.
          PACK      MRHIDATE,SP70
          PACK      KEY26,MRMAKEY,Z70
          CALL      RSMRHIS1
          CALL      RPMRHIS1
          IF        OVRCD=0
            MATCH     MRHIKEY,MRMAKEY
            IF        !@EQUAL
              PACK      MRHIDATE,SP70
            ENDIF
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateHeader('",HTMLLINK
          APPEND    MRMAKEY,HTMLLINK
          APPEND    "');",HTMLLINK
          RESET     HTMLLINK
          ADD       ONE,RECCOUNT
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                    "#"",MRMAURNO,"#",":
                    "#"",FORMATNM,"#",":
                    "#"",OBATIME,"#",":
                    "#"",TDESC,"#",";
.
          IF        EXCPFLAG=1
            MATCH     SP70,INTRANST                * Set by ITRN0000
            IF        !@EQUAL 
              WRITE     HTMLFILE;"#"",INTRANST,HOSPID01,STTRED,MRLODESC,ENDFONT,"#",";
            ELSE
              WRITE     HTMLFILE;"#"",HOSPID01,STTRED,MRLODESC,ENDFONT,"#",";
            ENDIF
          ELSE
            MATCH     SP70,INTRANST                * Set by ITRN0000
            IF        !@EQUAL 
              WRITE     HTMLFILE;"#"",INTRANST,HOSPID01,MRLODESC,"#",";
            ELSE
              WRITE     HTMLFILE;"#"",HOSPID01,MRLODESC,"#",";
            ENDIF
          ENDIF
.
          IF        NOMRFLAG=1
            WRITE     HTMLFILE;"#"#",";
          ELSE
            WRITE     HTMLFILE;"#"",MRMAVOLN,"#",";
          ENDIF
.
          WRITE     HTMLFILE;"#"",OBACTYP,"#",":
                             "#"",OCADESC,"#",":
                             "#"",SDAYDESC,"#",":
                             "#"",SORTORDR,"#",";
.
          IF        NOMRFLAG=1
            WRITE     HTMLFILE;"#"#",";
          ELSE
            COMPARE   OBAPULL,ZERO
            IF        @EQUAL
              WRITE     HTMLFILE;"#"<input type=checkbox name=dummy id=dummy ":
                                 "checked disabled>#",";
            ELSE
              WRITE     HTMLFILE;"#"<input type=checkbox name=dummy id=dummy":
                                 " disabled>#",";
            ENDIF         
          ENDIF
.
          IF        NOMRFLAG=1
            WRITE     HTMLFILE;"#"#",";
          ELSE
            IF        EXCPFLAG=1
              WRITE     HTMLFILE;"#"<input type=checkbox disabled ":
                        "name=mrreckey id=mrreckey":
                        " value='",KEY40,"' onclick=RemoveTable(this.value);>":
                        "#",";
            ELSE
              WRITE     HTMLFILE;"#"<input type=checkbox ":
                        "name=mrreckey id=mrreckey":
                        " value='",KEY40,"' onclick=RemoveTable(this.value);>":
                        "#",";
            ENDIF
          ENDIF
.
          MATCH     SP70,MRMAFILM     * MicroFilmed?
          IF        @EQUAL
            WRITE     HTMLFILE;"#"#",#"#",#"B#"";
          ELSE
            WRITE     HTMLFILE;"#"Yes#",#"javascript:ViewMicroFilm('":
                               MRMAFILM,"#')#",#"0#"";
          ENDIF
.
FILRW700  MOVE      DOBAOUTN,KEY8
          CALL      CHVSCM00 
.
FILRW800  CLEAR     COMMLINK
          APPEND    "javascript:UpdateComm('",COMMLINK
          aPPEND    DOBAOUTN,COMMLINK
          APPEND    "','",COMMLINK
          APPEND    OBAURNO,COMMLINK
          APPEND    "')",COMMLINK
          RESET     COMMLINK
.
          MATCH     "0",OTCNCFMT
          IF        @EQUAL
            PACK      DIM40,OCACLIN,SP1,DASH,SP1,OCADESC,SP70
          ELSE
            MATCH     "2",OTCNCFMT
            IF        @EQUAL
              PACK      DIM40,OCADESC,SP1,DASH,SP1,OCACLIN,SP70
            ELSE
              PACK      DIM40,OCACLIN,SP70
            ENDIF
          ENDIF
.                                                                     Car 233039
          PACK      TDESC,SP70
          MATCH     SP70,OBAVISIT
          GOTO      FILRW801 IF EQUAL
.
          MOVE      "CV",CATEGORY
          MOVE      OBAVISIT,CODE
          CALL      GETCOD00
.
FILRW801  WRITE     HTMLFILE;",#"",COMTFLAG,"#"":
                             ",#"",COMMLINK,"#"":
                             ",#"",MRHIDATE,"#"":
                             ",#"",DIM40,"#"":
                             ",#"",TDESC,"#"";
.
FILRW810  MOVE      "RS",CATEGORY
          MOVE      MRMASTAT,CODE
          CALL      GETCOD00
.
          MATCH     SP70,PTCDUDF3
          IF        @EQUAL          
            WRITE     HTMLFILE;",#"",TDESC,"#");"
. 
          ELSE
            WRITE     HTMLFILE;",#"<span ":                      
                        " style=color:",PTCDUDF3,">":
                        TDESC,"</span>":
                          "#");"
          ENDIF
.          WRITE     HTMLFILE;",#"",TDESC,"#");"
.
FILRW900  BRANCH    NOMRFLAG,FILRW999       * Exit if no MR for this patient
.                      
. Reset exceptions flag.
.
          MOVE      ZERO,EXCPFLAG             * Show this exception record
          IF        SDAYFLAG=1&FIRSTFLG=0&SCLIFLAG=1
            IF        EXCEPTNS=1
              MOVE      ONE,EXCPFLAG             * Show this exception record
            ENDIF
          ENDIF
.
          MATCH     "1",MRCNDISA            * Display all volumes
          IF        @EQUAL
            MATCH     Z70,FILTDOTY          * Using document type filter
            GOTO      FILRW200 IF NOT EQUAL
.
            MATCH     MRCNRCTY,MRMADOTY
            IF        @EQUAL
              GOTO      FILRW200
            ELSE
              GOTO      FILRW400
            ENDIF
          ELSE                              * Not displaying all volumes
            MATCH     SP70,FILTDOTY         * All option document type filter
            GOTO      FILRW200 IF EQUAL 
          ENDIF
.
FILRW999  RETURN
.
.----------------------------------------------------------------------
. check if visit comment exists
.----------------------------------------------------------------------
CHVSCM00  MOVE      ZERO,COMTFLAG
          PACK      KEY17,KEY8,VSCMTTYP,Z70      * Key for start of comment
          CALL      RSVSMDT1
CHVSCM20  CALL      RPVSMDT1
          BRANCH    OVRCD,CHVSCM99
.
          MATCH     KEY8,VSMDVISN
          GOTO      CHVSCM99 IF NOT EQUAL
.
          MATCH     VSCMTTYP,VSMDTYPE
          GOTO      CHVSCM99 IF NOT EQUAL
.
          MATCH     "1",VSMDDELT          * is comment deleted
          GOTO      CHVSCM20 IF EQUAL
.
          MOVE      ONE,COMTFLAG
.
CHVSCM99  RETURN
.
.*******************************************************************************
.                           Clinic Option List
.*******************************************************************************
CLIOPT00  PACK      KEY20,WBSESIT,SP70
          CALL      RDSCLIA1
CLIOPT10  CALL      RDKCLIA1
          BRANCH    OVRCD,CLIOPT99
.
          MATCH     OCASITE,WBSESIT     * Must be in the Same Site
          GOTO      CLIOPT99 IF NOT EQUAL
.
          PACK      KEY20,WBSESIT,OCACLIN,Z70
          CALL      RDSCLIA1
          CALL      RDPCLIA1
          MATCH     "1",OTCLIACT
          GOTO      CLIOPT10 IF EQUAL
          MATCH     SP70,OCADESC        * Ignore ID with no Description
          GOTO      CLIOPT20 IF NOT EQUAL
          MOVE      OCADOCT,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
            MOVE      DOCFNAME,OCADESC
          ELSE
            MOVE      "Invalid Doctor Code",OCADESC
          ENDIF
.
CLIOPT20  MATCH     CLINICID,OCACLIN
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"",OCACLIN,"#" selected>(":
                               OCACLIN,") ",OCADESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=#"",OCACLIN,"#">(":
                               OCACLIN,") ",OCADESC,"</option>"
          ENDIF
          GOTO      CLIOPT10
.
CLIOPT99  PACK      KEY20,WBSESIT,CLINICID,Z70
          CALL      RDSCLIA1          * Read Clinic Record
          CALL      RDPCLIA1          * Read Clinic Record
          RETURN
.------------------------------------------------------------
. Clinic Option List
.------------------------------------------------------------
CLITYP00  PACK      KEY42,WBSESIT,SP70
          CALL      RDSCTYA2
CLITYP10  CALL      RDKCTYA2
          BRANCH    OVRCD,CLITYP99
          MATCH     OCTSITE,WBSESIT
          GOTO      CLITYP99 IF NOT EQUAL
          MATCH     "1",OCTACT
          GOTO      CLITYP10 IF EQUAL
.
          MATCH     CLINICTY,OCTCTYP
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"",OCTCTYP,"#" selected>":
                               OCTDESC,"</option>";
          ELSE
            WRITE     HTMLFILE;"<option value=#"",OCTCTYP,"#">":
                               OCTDESC,"</option>";
          ENDIF
          GOTO      CLITYP10
.
CLITYP99  RETURN
.------------------------------------------------------------
. Pre-admission Pulling List by Date Range
.------------------------------------------------------------
PFIL0000  BRANCH    FLDITMNO,PFIL0100,PFIL0200,PFIL0300,PFIL0400,PFIL0500:
                             PFIL0600,PFIL0700,PFIL0800,PFIL0900,PFIL1000:
                             PFIL1100,PFIL1200,PFIL1300,PFIL1400,PFIL1500:
                             PFIL1600,PFIL1700,PFIL1800,PFIL1900,PFIL2000:
                             PFIL2100,PFIL2200,PFIL2300
          GOTO      PFIL9999
.
PFIL0100  CALL      NEXT0000
          GOTO      PFIL9999
.
PFIL0200  CALL      PREV0000
          GOTO      PFIL9999
.
PFIL0300  CALL      CURSTD00
          GOTO      PFIL9999
.
PFIL0400  CALL      CUREND00
          GOTO      PFIL9999
.
PFIL0500  CALL      CURYR000
          GOTO      PFIL9999
.
PFIL0600  CALL      CURMON00
          GOTO      PFIL9999
.
PFIL0700  CALL      SELV0000
          GOTO      PFIL9999
.
PFIL0800  WRITE     HTMLFILE;TEMPLATE;
          GOTO      PFIL9999
.
PFIL0900  WRITE     HTMLFILE;REPORTNO;
          GOTO      PFIL9999
.
PFIL1000  WRITE     HTMLFILE;VIEWTYPE;
          GOTO      PFIL9999
.
PFIL1100  MOVE      ZERO,RECCNT    * Set Record Count for both routines here
          CALL      PRETB000       * Table of Pre-admissions pull Lists
          CALL      PRBTB000       * Table of Booked patients pull list
          GOTO      PFIL9999
.
PFIL1200  MOVE      "AC",CATEGORY          * Unit selection List
          MOVE      UNITCODE,CODE
          CALL      CODITM00
          GOTO      PFIL9999
.
PFIL1300  PACK      DFLTWARD,WARDCODE 
          CALL      WSEL0000               * Ward selection list 
          GOTO      PFIL9999
.                                              
PFIL1400  MOVE      "ap",CATEGORY          * I SRF 17693
          MOVE      ADMITTPT,CODE          * Set parameter
          CALL      CODITM00               * Look in patcodes for desc
          GOTO      PFIL9999               * end I SRF 17693
.
PFIL1500  PACK      DFLTWARD,POSTWARD
          CALL      WSEL0000               * Ward selection list 
          GOTO      PFIL9999
.
PFIL1600  MOVE      WBSEHOSP,DEFTHOSP
          CALL      HSEL0000               * Hospital Selection List
          GOTO      PFIL9999
.
PFIL1700  WRITE     HTMLFILE;MICROFLM;
          GOTO      PFIL9999
.
PFIL1800  WRITE     HTMLFILE;EXCEPTNS;
          GOTO      PFIL9999
.
PFIL1900  MOVE      "TY",CATEGORY                * MR document type filter
          MATCH     Z70,FILTDOTY
          IF        @EQUAL
            IF        IBCNMHOS=1
              PACK      KEY3,WBSEHOSP,SP70       * If multi hospital default
              CALL      RDPTHSP1                 * document type from hospital
              IF        OVRCD=1
                MOVE      MRCNRCTY,FILTDOTY
              ELSE
                MATCH     SP70,PTHSRCTY
                IF        @EQUAL
                  MOVE      MRCNRCTY,FILTDOTY
                ELSE
                  MOVE      PTHSRCTY,FILTDOTY
                ENDIF
              ENDIF
            ELSE
              MOVE      MRCNRCTY,FILTDOTY
            ENDIF
          ENDIF
          MOVE      FILTDOTY,CODE
          CALL      CODITM00
          GOTO      PFIL9999
.
PFIL2000  WRITE     HTMLFILE;ALLHOMEH;           * All home hospitals on pulling
          GOTO      PFIL9999                     * list
.
PFIL2100  WRITE     HTMLFILE;SUPERLEV;
          GOTO      PFIL9999
.
PFIL2200  WRITE     HTMLFILE;DEFTVIEW;            * Default view
          GOTO      PFIL9999
.
PFIL2300  WRITE     HTMLFILE;INCLADMS;            * Include Admissions
          GOTO      PFIL9999
.
PFIL9999  RETURN
.
.------------------------------------------------------------
. Hospital Selection
.------------------------------------------------------------
HSEL0000  PACK      KEY3,SP70
          CALL      RSPTHSP1
HSEL0100  CALL      RKPTHSP1
          BRANCH    OVRCD,HSEL9999
.
          MOVE      PTHSNAME,KEY50
          PACK      KEY5,QUOTE,PTHSHOSP,QUOTE
.
          MATCH     DEFTHOSP,PTHSHOSP
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY5," selected>":
                               KEY50,"</option>"
          ELSE
            IF        IBCNMHOS=1
              PACK      KEY13,USERID,SP70            * All hospital access
              CALL      RDMLSEC1
              IF        OVRCD=1
                PACK      KEY13,USERID,PTHSHOSP,SP70 * This hospital access
                CALL      RDMLSEC1
                BRANCH    OVRCD,HSEL0100
              ENDIF
            ENDIF
.
            WRITE     HTMLFILE;"<option value=",KEY5,">",KEY50,"</option>"
          ENDIF
          GOTO      HSEL0100
.
HSEL9999  RETURN
.
.------------------------------------------------------------
. Table of Request Filled List                 
.------------------------------------------------------------
PRETB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          MOVE      ZERO,FOUNDFLG
          MOVE      ZERO,NOMRFLAG
          MOVE      SP70,SAVEDOTY
          MOVE      SP70,HOMEHOSP
          MOVE      ZERO,DIFFURFL
.
          IF        IBCNMHOS=1
            PACK      KEY3,WBSEHOSP,SP70
            CALL      RDPTHSP1
            IF        OVRCD<>1
              MOVE     PTHSHHOS,HOMEHOSP    * Save hospitals MR home hospital
            ENDIF
          ENDIF
.
          MATCH     SP70,FRSTDATE
          GOTO      PRETB999 IF EQUAL
.
          PACK      KEY16,FRSTDATE,SP70
          CALL      RDSMISS3  
PRETB100  CALL      RDKMISS3                * Reads Admission file
          BRANCH    OVRCD,PRETB999
.
          MATCH     ADATE,LASTDATE          * Check for date range
          GOTO      PRETB999 IF LESS
.
          COMPARE   "1",ASTAT
          IF        !@EQUAL
            COMPARE   "0",INCLADMS          * Including Admitted Patients
            IF        @EQUAL
              GOTO      PRETB100
            ELSE
              COMPARE   "2",ASTAT
              GOTO      PRETB100 IF NOT EQUAL
            ENDIF
          ENDIF 
.
          PACK      KEY8,DAADMNO,SP70       * Reads Visit Extention file
          CALL      RDPMVX11                   
          BRANCH    OVRCD,PRETB100
.
          IF        IBCNMHOS=1
            MATCH     WBSEHOSP,PMVXMHOS          * Only display pre admissions
            GOTO      PRETB100 IF NOT EQUAL      * for users current hospital
          ENDIF
.
          MATCH     SP70,ADMITTPT
          IF        !@EQUAL
            MATCH     ADMITTPT,PMVXAPWD
            GOTO      PRETB100 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,POSTWARD
          IF        !@EQUAL
            MATCH     POSTWARD,PMVXPOWD
            GOTO      PRETB100 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,UNITCODE
          IF        !@EQUAL
            MATCH     UNITCODE,ACLSSFT
            GOTO      PRETB100 IF NOT EQUAL
          ENDIF
.
          MOVE      ZERO,FOUNDFLG
          MOVE      ZERO,NOMRFLAG
          MOVE      SP70,SAVEDOTY
          MOVE      ZERO,DIFFURFL
.
          PACK      KEY20,AURNO,Z70
.
          MATCH     Z70,FILTDOTY
          IF        @EQUAL
            CALL      RSMRMAS3
          ELSE
            CALL      RSMRMAS4
          ENDIF
PRETB200  MATCH     Z70,FILTDOTY
          IF        @EQUAL
            CALL      RPMRMAS3                    * Reads MRT master file
          ELSE
            CALL      RPMRMAS4
          ENDIF
.          BRANCH    OVRCD,PRETB300
          IF        OVRCD = 1
            MOVE      ONE,DIFFURFL
            GOTO      PRETB300
          ENDIF
.
          MATCH     MRMAURNO,AURNO  
.          GOTO      PRETB300 IF NOT EQUAL
          IF        !@EQUAL
            MOVE      ONE,DIFFURFL
            GOTO      PRETB300
          ENDIF
.
          IF        IBCNMHOS=1 & ALLHOMEH<>1
            MATCH     HOMEHOSP,MRMAHHOS          * Check home hospital
            GOTO      PRETB200 IF NOT EQUAL
          ENDIF
.
          MATCH     Z70,FILTDOTY                 * Using document type filter
          IF        @EQUAL
            MATCH     MRCNRCTY,MRMADOTY          * Look for Medical Record
            GOTO      PRETB200 IF NOT EQUAL
          ELSE
            MATCH     SP70,FILTDOTY              * All document type filter
            IF        @EQUAL
              MATCH     "1",MRCNDISA             * If not displaying all volumes
              IF        !@EQUAL
                MATCH     SP70,SAVEDOTY
                IF        !@EQUAL
                  MATCH     SAVEDOTY,MRMADOTY    * Display the highest volume of
                  GOTO      PRETB200 IF EQUAL    * each document type
                ENDIF
              ENDIF
            ELSE
              MATCH     FILTDOTY,MRMADOTY        * Check document type filter
              GOTO      PRETB200 IF NOT EQUAL
            ENDIF
          ENDIF
.
.         Check if removed from pulling list 
          PACK      KEY20,DAADMNO,MRMAKEY,ZERO,TWO,SP70
          CALL      RDMRPLL1
          IF        OVRCD = 0
            GOTO      PRETB200
          ENDIF

          GOTO      PRETB500

PRETB300  BRANCH    FOUNDFLG,PRETB100
.
          MATCH     SP70,FILTDOTY               * All document type filter
          GOTO      PRETB450 IF EQUAL           * So do No MR test
.
          MATCH     Z70,FILTDOTY                * Using document type filter
          IF        !@EQUAL
            MATCH     MRCNRCTY,FILTDOTY         * Finished if filter doesn't
            GOTO      PRETB100 IF NOT EQUAL     * match default document type
          ENDIF
.
          PACK      KEY20,AURNO,Z70
          CALL      RSMRMAS3
PRETB400  CALL      RPMRMAS3                    * Reads MRT master file
          BRANCH    OVRCD,PRETB450
.
          MATCH     MRMAURNO,AURNO
          GOTO      PRETB450 IF NOT EQUAL
.
          IF        IBCNMHOS=1 & ALLHOMEH<>1
            MATCH     HOMEHOSP,MRMAHHOS          * Check home hospital
            GOTO      PRETB400 IF NOT EQUAL
          ENDIF
.
          PACK      KEY5,CODETY,MRMADOTY,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PRETB400

          MATCH     "1",TCINDC1                 * Look for OUT or EMR record
          GOTO      PRETB400 IF NOT EQUAL
          GOTO      PRETB500
.
PRETB450  BRANCH    FOUNDFLG,PRETB100
.
          MATCH     "1",MRCNNOMR                 * Include patients with no MR
          GOTO      PRETB100 IF NOT EQUAL        * on pulling lists
.
          PACK      KEY40,MRMAKEY,DAADMNO,F1,SP70
.
          MOVE      ONE,NOMRFLAG                 * Set No MR flag to yes
          CALL      CLMRTMAS                     * Clear MR master file fields
          MOVE      AURNO,MRMAURNO               * Set U/R from Pre adm visit
.          GOTO      PRETB100
.
PRETB500  MOVE      SP70,APOIDESC
          MOVE      SP70,WBDESC
.
          MATCH     SP70,PMVXAPWD
          GOTO      PRETB600 IF EQUAL
.
          PACK      KEY5,CODEAP,PMVXAPWD,SP70    * Admitting Point
          CALL      RDCODE1
          BRANCH    OVRCD,PRETB100
.
          MOVE      TDESC,APOIDESC        * Admitting Point Desc
.
          MOVE      PTCDUDF2,MEDILOCH     * Admitting Point MR Loc Hospital
          MOVE      PTCDNHCP,MEDILOCN     * Set MR Location
.
          MATCH     SP70,PTCDNHCP         * Adm Point Location Set?
          IF        @EQUAL
            PACK      KEY6,THCSCOD,SP70     *  Read Ward to get mr location
            CALL      RDWARD1
            IF        OVRCD=1       
              CLEAR     APOIDESC            * Location not found
              APPEND    "<font color=red>",APOIDESC
              APPEND    TDESC,APOIDESC
              APPEND    "</font>",APOIDESC
              RESET     APOIDESC
              GOTO      PRETB600
            ENDIF
            MOVE      PTWDHOSN,MEDILOCH
            MOVE      PTWDLOCN,MEDILOCN     * Set MR Location
          ENDIF
.                                           * end I SRF 17693
          MATCH     SP70,MRMACLOC
          IF        !@EQUAL
            IF        EXCEPTNS<>1
              MATCH     MRMACHOS,MEDILOCH          * check to see is current loc
              IF        @EQUAL
                MATCH     MRMACLOC,MEDILOCN        * check to see is current loc
                GOTO      PRETB100 IF EQUAL
              ENDIF
            ENDIF
          ENDIF
.
PRETB600  MATCH     "Y",PTMIDMOV                * check indicator if record is
          IF        @EQUAL
            PACK      KEY20,DAADMNO,MRMAKEY,ZERO,ONE,SP70
            CALL      RDMRPLL1
.            IF        OVRCD <> 0
.              GOTO      PRETB200
.            ENDIF
            IF        OVRCD <> 0
              IF        DIFFURFL = 1
                GOTO      PRETB100
              ELSE
                GOTO      PRETB200
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP70,MRMACHOS
          IF        @EQUAL
            MOVE      MRMAHHOS,MRMACHOS
          ENDIF
.
          MATCH     SP70,MRMACLOC
          IF        @EQUAL
            MOVE      MRMAHLOC,MRMACLOC
          ENDIF
.
          MOVE      SP70,MRLODESC               * Location Required
          PACK      KEY7,MRMACHOS,MRMACLOC,SP70
          CALL      RDMRLOC1
....      MOVE      MRLODESC,KEY20
.
          MOVE      SP70,HOSPID01                                     *I-248696
          IF        IBCNMHOS = 1
            PACK      HOSPID01,OBB,MRMACHOS,CBB     * set default to black
            MATCH     MRMAHHOS,MRMACHOS
            IF        !@EQUAL
              MOVE      SP3,PTHSHHOS                        * start   *I-259106
              PACK      KEY3,MRMACHOS            * current hosp 
              CALL      RDPTHSP1
              MATCH     PTHSHHOS,MRMAHHOS        * same as the home hospital ?
              IF        !@EQUAL
                PACK      HOSPID01,OBR,MRMACHOS,CBR     * set to red
              ELSE  
                UNPACK    SP20,TCINDC1,TCINDC2,TCINDC3,TCINDC4
                PACK      KEY5,CODETY,PTHSRCTY
                CALL      RDCODE1
                MATCH     "B",TCINDC1
                IF        !@EQUAL
                  PACK      HOSPID01,OBR,MRMACHOS,CBR   * set to red
                ENDIF   
              ENDIF                                         * end     *I-259106
            ENDIF
          ENDIF
.
          MOVE      SP70,PATFNAME
          MOVE      MRMAURNO,KEY8
          CALL      RDMAST1                 * Read Patient Master File
          BRANCH    OVRCD,PRETB100
.
          MOVE      MRMAURNO,KEY8
          CALL      RDPMPX21                * Read Patient Master Extn File
          BRANCH    OVRCD,PRETB100
.
          MOVE      MRMAURNO,SORTORDR       *                         *I-240716
          CALL      MRSRTORD                * create sort order from SORTORDR
.
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      PATLN000
.
          MOVE      ZERO,F1                 * Admitted
          PACK      KEY40,MRMAKEY,DAADMNO,F1,SP70
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL:
                               AUG,SEP,OCT,NOV,DEC
          PACK      DSCDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,SP1,ATIME
.
          MOVE      CODETY,CATEGORY
          MOVE      MRMADOTY,CODE
          CALL      GETCOD00
          MOVE      TDESC,KEY20
.
          CALL      ITRN0000                     * Check if in transit
.
          PACK      MRHIDATE,SP70
          PACK      KEY26,MRMAKEY,Z70       
          CALL      RSMRHIS1
          CALL      RPMRHIS1
          IF        OVRCD=0
            MATCH     MRHIKEY,MRMAKEY
            IF        !@EQUAL
              PACK      MRHIDATE,SP70
            ENDIF
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateHeader('",HTMLLINK
          APPEND    MRMAKEY,HTMLLINK
          APPEND    "');",HTMLLINK
          RESET     HTMLLINK
.
          ADD       ONE,RECCNT
          MOVE      RECCNT,DRECCNT
          REP       " 0",DRECCNT
          MOVE      ONE,FOUNDFLG
          MOVE      MRMADOTY,SAVEDOTY       * Save last printed document type
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":            *0
                    "#"",AURNO,"#",":                                 *1
                    "#"",DAADMNO,"#",":                               *2
                    "#"",FORMATNM,"#",":                              *3
                    "#"",DSCDATE,"#",":                               *4
                    "#"",APOIDESC,"#",";                              *5
.
          MATCH     SP70,INTRANST                * Set by ITRN0000    *6
          IF        !@EQUAL
            WRITE     HTMLFILE;"#"",INTRANST,HOSPID01,MRLODESC,"#",";
          ELSE
            WRITE     HTMLFILE;"#"",HOSPID01,MRLODESC,"#",";
          ENDIF
.
          WRITE     HTMLFILE;"#"",KEY20,"#",";                        *7
.
          IF        NOMRFLAG=1                                        *8
            WRITE     HTMLFILE;"#"#",";
          ELSE
            WRITE     HTMLFILE;"#"<input type=checkbox name=mrreckey",DRECCNT:
                      " id=mrreckey",DRECCNT:
                      " value='",KEY40,"' onclick=RemoveTable(this);>":
                      "#",";
          ENDIF
.
          MATCH     SP70,MRMAFILM     * MicroFilmed?                 *9-11
          IF        @EQUAL
            WRITE     HTMLFILE;"#"#",#"#",#"B#"";
          ELSE 
            WRITE     HTMLFILE;"#"Yes#",#"javascript:ViewMicroFilm('":
                               MRMAFILM,"#')#",#"0#"";
          ENDIF
.
.
PRETB700  MOVE      DAADMNO,KEY8
          CALL      CHVSCM00 
.
PRETB800  CLEAR     COMMLINK
          APPEND    "javascript:UpdateComm('",COMMLINK                 
          APPEND    DAADMNO,COMMLINK
          APPEND    "','",COMMLINK
          APPEND    AURNO,COMMLINK
          APPEND    "')",COMMLINK
          RESET     COMMLINK
.
          IF        NOMRFLAG=1
            WRITE     HTMLFILE;",#"#"";                             *12
          ELSE
            WRITE     HTMLFILE;",#"",MRMAVOLN,"#"";
          ENDIF
.
          WRITE     HTMLFILE;",#"",COMTFLAG,"#"":                   *13
                             ",#"",COMMLINK,"#"":                   *14 
                             ",#"",SORTORDR,"#"":                   *15
                             ",#"",MRHIDATE,"#"";                   *16
.
PRETB810  MOVE      "RS",CATEGORY
          MOVE      MRMASTAT,CODE
          CALL      GETCOD00
.          
          MATCH     SP70,PTCDUDF3
          IF        @EQUAL
            WRITE     HTMLFILE;",#"",TDESC,"#"";
          ELSE 
            WRITE     HTMLFILE;",#"<span ":
                        " style=color:",PTCDUDF3,">":
                        TDESC,"</span>":
                          "#"";
          ENDIF
.          WRITE     HTMLFILE;",#"",TDESC,"#"";                     *17
.
PRETB820  CALL      PATLN000
.
          PACK      KEY8,PURNO,SP20
          CALL      RDPMPX21          * Get PMPXFLDR/PMPXPRVI for PATFLD00
          CALL      PATFLD00          * Returns KEY3 for folder colour code   
.
          MOVE      SP70,DOCFNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
          ENDIF
.
          WRITE     HTMLFILE;",#"",LINKPRG,"#.pbl":                  *18
                                  "?reportno=",LINKREP:              
                                  "&template=",LINKTEM:
                                  "&urnumber=",MRMAURNO,"#"":
                             ",#"",KEY3,"#"":                        *19
                             ",#"",DOCFNAME,"#"":                    *20
                             ",#"",ADIAG1,"#","                     *21
.
          IF        NOMRFLAG=1                                       *22  
            WRITE     HTMLFILE;"#"#",";                               
          ELSE
            WRITE     HTMLFILE;"#"<input type=checkbox name=mrkeplst",DRECCNT:
                      " id=mrkeplst",DRECCNT:
                      " value='",KEY40,"'checked onclick=RemoveTable(this);>":
                      "#",";
          ENDIF
.
          IF        NOMRFLAG=1                                       *23  
            WRITE     HTMLFILE;"#"#");";                               
          ELSE
            WRITE     HTMLFILE;"#"<input type=checkbox name=mrrmvlst",DRECCNT:
                      " id=mrrmvlst",DRECCNT:
                      " value='",KEY40,"' onclick=RemoveTable(this);>":
                      "#");";
          ENDIF
.
.          IF        RECCNT>50
.            WRITE     HTMLFILE;"alert('Limit reached too many patients to":
.                               " view!');";
.            GOTO      PRETB999
.          ENDIF
.
          BRANCH    NOMRFLAG,PRETB100       * Next pre adm if no MR for
.                                           * this patient
          MATCH     "1",MRCNDISA            * Display all volumes
          IF        @EQUAL
            MATCH     Z70,FILTDOTY          * Using document type filter
            GOTO      PRETB200 IF NOT EQUAL
.
            MATCH     MRCNRCTY,MRMADOTY
            IF        @EQUAL
              GOTO      PRETB200
            ELSE
              GOTO      PRETB400
            ENDIF
          ELSE                              * Not displaying all volumes
            MATCH     SP70,FILTDOTY         * All option document type filter
            GOTO      PRETB200 IF EQUAL
          ENDIF
.
          GOTO      PRETB100
.
PRETB999  RETURN
.---------------------------------------------------------------------------
. Table of Booked patients filled list         
.---------------------------------------------------------------------------
PRBTB000  MOVE      ZERO,FOUNDFLG
          MOVE      ZERO,NOMRFLAG
          MOVE      SP70,SAVEDOTY
          MOVE      SP70,HOMEHOSP
          MOVE      ZERO,DIFFURFL
.
          IF        IBCNMHOS=1
            PACK      KEY3,WBSEHOSP,SP70
            CALL      RDPTHSP1
            IF        OVRCD<>1
              MOVE     PTHSHHOS,HOMEHOSP    * Save hospitals MR home hospital
            ENDIF
          ENDIF
.
          MATCH     SP70,FRSTDATE
          GOTO      PRBTB999 IF EQUAL
.
          PACK      KEY16,FRSTDATE,SP70
          CALL      RSBKREC4  
PRBTB100  CALL      RKBKREC4                * Read Booking file   
          BRANCH    OVRCD,PRBTB999

          MATCH     BKREEDAT,LASTDATE       * Check for date range
          GOTO      PRBTB999 IF LESS
.
          COMPARE   ZERO,BKRESTAT           * Must be a Booking Record
          GOTO      PRBTB100 IF NOT EQUAL
.
          PACK      KEY8,DBKREBOO           * Reads Booking Extention file
          CALL      RDBKRX11
          BRANCH    OVRCD,PRBTB100
.
          IF        IBCNMHOS=1
            PACK      KEY6,BKREWARD,SP70         * Read expected ward to get
            CALL      RDWARD1                    * hospital
            BRANCH    OVRCD,PRBTB100
.
            MATCH     WBSEHOSP,PTWDHOSN          * Only display bookings      
            GOTO      PRBTB100 IF NOT EQUAL      * for users current hospital
          ENDIF
.
          MOVE      SP70,ADMPOINT           * Clear Admission Point Desc
.
          MOVE      BKREURNO,KEY8
          CALL      RDMAST1                 * Read Patient Master File
          BRANCH    OVRCD,PRBTB100
.
          MOVE      BKREURNO,KEY8
          CALL      RDPMPX21                * Read Patient Master Extn File
          BRANCH    OVRCD,PRBTB100
.
          CALL      FLTPRB00                * Check Filters
          BRANCH    EXIT,PRBTB100
.
          MOVE      ZERO,FOUNDFLG
          MOVE      ZERO,NOMRFLAG
          MOVE      SP70,SAVEDOTY
          MOVE      ZERO,DIFFURFL
.
          PACK      KEY20,BKREURNO,Z70
.
          MATCH     Z70,FILTDOTY
          IF        @EQUAL
            CALL      RSMRMAS3
          ELSE
            CALL      RSMRMAS4
          ENDIF
PRBTB200  MATCH     Z70,FILTDOTY
          IF        @EQUAL
            CALL      RPMRMAS3                    * Reads MRT master file
          ELSE
            CALL      RPMRMAS4
          ENDIF
.          BRANCH    OVRCD,PRBTB300
          IF        OVRCD = 1
            MOVE      ONE,DIFFURFL
            GOTO      PRBTB300
          ENDIF
.
          MATCH     MRMAURNO,BKREURNO  
.          GOTO      PRBTB300 IF NOT EQUAL
          IF        !@EQUAL
            MOVE      ONE,DIFFURFL
            GOTO      PRBTB300
          ENDIF
.
          IF        IBCNMHOS=1 & ALLHOMEH<>1
            MATCH     HOMEHOSP,MRMAHHOS          * Check home hospital
            GOTO      PRBTB200 IF NOT EQUAL
          ENDIF
.
          MATCH     Z70,FILTDOTY                 * Using document type filter
          IF        @EQUAL
            MATCH     MRCNRCTY,MRMADOTY          * Look for Medical Record
            GOTO      PRBTB200 IF NOT EQUAL
          ELSE
            MATCH     SP70,FILTDOTY              * All document type filter
            IF        @EQUAL
              MATCH     "1",MRCNDISA             * If not displaying all volumes
              IF        !@EQUAL
                MATCH     SP70,SAVEDOTY
                IF        !@EQUAL
                  MATCH     SAVEDOTY,MRMADOTY    * Display the highest volume of
                  GOTO      PRBTB200 IF EQUAL    * each document type
                ENDIF
              ENDIF
            ELSE
              MATCH     FILTDOTY,MRMADOTY        * Check document type filter
              GOTO      PRBTB200 IF NOT EQUAL
            ENDIF
          ENDIF
.
.         Check if removed from pulling list 
          PACK      KEY20,DBKREBOO,MRMAKEY,ZERO,TWO,SP70
          CALL      RDMRPLL1
          IF        OVRCD = 0
            GOTO      PRBTB200
          ENDIF
.
          GOTO      PRBTB500
.
PRBTB300  BRANCH    FOUNDFLG,PRBTB100
.
          MATCH     SP70,FILTDOTY               * All document type filter
          GOTO      PRBTB450 IF EQUAL           * So do No MR test
.
          MATCH     Z70,FILTDOTY                * Using document type filter
          IF        !@EQUAL
            MATCH     MRCNRCTY,FILTDOTY         * Finished if filter doesn't
            GOTO      PRBTB100 IF NOT EQUAL     * match default document type
          ENDIF
.
          PACK      KEY20,BKREURNO,Z70
          CALL      RSMRMAS3
PRBTB400  CALL      RPMRMAS3                    * Reads MRT master file
          BRANCH    OVRCD,PRBTB450
.
          MATCH     MRMAURNO,BKREURNO
          GOTO      PRBTB450 IF NOT EQUAL
.
          IF        IBCNMHOS=1 & ALLHOMEH<>1
            MATCH     HOMEHOSP,MRMAHHOS          * Check home hospital
            GOTO      PRBTB400 IF NOT EQUAL
          ENDIF
.
          PACK      KEY5,CODETY,MRMADOTY,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PRBTB400
.
          MATCH     "1",TCINDC1             * Look for OUT or EMR record
          GOTO      PRBTB400 IF NOT EQUAL
          GOTO      PRBTB500
.
PRBTB450  BRANCH    FOUNDFLG,PRBTB100
.
          MATCH     "1",MRCNNOMR                 * Include patients with no MR
          GOTO      PRBTB100 IF NOT EQUAL        * on pulling lists
.
          MOVE      ONE,NOMRFLAG                 * Set No MR flag to yes
          CALL      CLMRTMAS                     * Clear MR master file fields
          MOVE      BKREURNO,MRMAURNO            * Set U/R from booking
.          GOTO      PRBTB100
.
PRBTB500  MATCH     SP70,BKRXADWD           * Booking Ward Set
          GOTO      PRBTB600 IF EQUAL
.
          PACK      KEY5,CODEAP,BKRXADWD,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PRBTB100
.
          MOVE      TDESC,ADMPOINT        * Admitting Point Desc 
.  
          MOVE      PTCDUDF2,MEDILOCH     * Admitting Point MR Loc Hospital 
          MOVE      PTCDNHCP,MEDILOCN     * Set MR Location
.
          MATCH     SP70,PTCDNHCP         * Adm Point Location Set?
          IF        @EQUAL
            PACK      KEY6,THCSCOD,SP70     * Read Ward to get mr location
            CALL      RDWARD1
            IF        OVRCD=1       
              CLEAR     ADMPOINT            * Location not found
              APPEND    "<font color=red>",ADMPOINT
              APPEND    TDESC,ADMPOINT
              APPEND    "</font>",ADMPOINT
              RESET     ADMPOINT
              GOTO      PRBTB600
            ENDIF   
            MOVE      PTWDHOSN,MEDILOCH
            MOVE      PTWDLOCN,MEDILOCN   * MR Location
          ENDIF
.                                           
          MATCH     SP70,MRMACLOC           
          IF        !@EQUAL
            MATCH     MRMACHOS,MEDILOCH   * Check Current Loc / Adm Point Loc
            IF        @EQUAL
              MATCH     MRMACLOC,MEDILOCN   * Check Current Loc / Adm Point Loc
              GOTO      PRBTB100 IF EQUAL
            ENDIF
          ENDIF
.
PRBTB600  MATCH     "Y",BKREINDC            * Check for movement indicator
          IF        @EQUAL
            PACK      KEY20,DBKREBOO,MRMAKEY,ZERO,ONE,SP70
            CALL      RDMRPLL1
.            IF        OVRCD <> 0
.              GOTO      PRBTB200
.            ENDIF
            IF        OVRCD <> 0
              IF        DIFFURFL = 1
                GOTO      PRBTB100
              ELSE
                GOTO      PRBTB200
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP70,MRMACHOS          * Current Location Hospital Blank?
          IF        @EQUAL
            MOVE      MRMAHHOS,MRMACHOS    * Set to home location hospital
          ENDIF
.
          MATCH     SP70,MRMACLOC          * Current Location Blank?
          IF        @EQUAL
            MOVE      MRMAHLOC,MRMACLOC    * Set Current Loc to Home Loc  
          ENDIF
.
          MOVE      SP70,MRLODESC           * Current Location
          PACK      KEY7,MRMACHOS,MRMACLOC,SP70
          CALL      RDMRLOC1
.
          MOVE      SP70,HOSPID01                                     *I-248696
          IF        IBCNMHOS = 1
            PACK      HOSPID01,OBB,MRMACHOS,CBB     * set default to black
            MATCH     MRMAHHOS,MRMACHOS
            IF        !@EQUAL
              MOVE      SP3,PTHSHHOS                        * start   *I-259106
              PACK      KEY3,MRMACHOS            * current hosp 
              CALL      RDPTHSP1
              MATCH     PTHSHHOS,MRMAHHOS        * same as the home hospital ?
              IF        !@EQUAL
                PACK      HOSPID01,OBR,MRMACHOS,CBR     * set to red
              ELSE  
                UNPACK    SP20,TCINDC1,TCINDC2,TCINDC3,TCINDC4
                PACK      KEY5,CODETY,PTHSRCTY
                CALL      RDCODE1
                MATCH     "B",TCINDC1
                IF        !@EQUAL
                  PACK      HOSPID01,OBR,MRMACHOS,CBR   * set to red
                ENDIF   
              ENDIF                                         * end     *I-259106
            ENDIF
          ENDIF
.
          MOVE      MRMAURNO,SORTORDR       *                         *I-240716
          CALL      MRSRTORD                * create sort order from SORTORDR
. 
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      PATLN000
.
          MOVE      ONE,F1                  * Patient is not admitted
          PACK      KEY40,MRMAKEY,BKREBOOK,F1,SP70
.
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL:
                               AUG,SEP,OCT,NOV,DEC
          PACK      DSCDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,SP1
.
          MOVE      CODETY,CATEGORY
          MOVE      MRMADOTY,CODE
          CALL      GETCOD00
          MOVE      TDESC,KEY20             * Document Type 
.
          CALL      ITRN0000                     * Check if in transit
.
          PACK      MRHIDATE,SP70
          PACK      KEY26,MRMAKEY,Z70
          CALL      RSMRHIS1
          CALL      RPMRHIS1
          IF        OVRCD=0
            MATCH     MRHIKEY,MRMAKEY
            IF        !@EQUAL
              PACK      MRHIDATE,SP70
            ENDIF   
          ENDIF   
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateHeader('",HTMLLINK
          APPEND    MRMAKEY,HTMLLINK
          APPEND    "');",HTMLLINK
          RESET     HTMLLINK
.
          ADD       ONE,RECCNT
          MOVE      RECCNT,DRECCNT
          REP       " 0",DRECCNT
          MOVE      ONE,FOUNDFLG
          MOVE      MRMADOTY,SAVEDOTY       * Save last printed document type
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":             *0
                    "#"",BKREURNO,"#",":                               *1
                    "#"",BKREBOOK,"#",":                               *2
                    "#"",FORMATNM,"#",":                               *3
                    "#"",DSCDATE,"#",":                                *4
                    "#"",ADMPOINT,"#",";                               *5
.
          MATCH     SP70,INTRANST                * Set by ITRN0000
          IF        !@EQUAL
            WRITE     HTMLFILE;"#"",INTRANST,HOSPID01,MRLODESC,"#",";  *6
          ELSE
            WRITE     HTMLFILE;"#"",HOSPID01,MRLODESC,"#",";             
          ENDIF
.
          WRITE     HTMLFILE;"#"",KEY20,"#",";                         *7
.
          IF        NOMRFLAG=1                                         *8 
            WRITE     HTMLFILE;"#"#",";                               
          ELSE
            WRITE     HTMLFILE;"#"<input type=checkbox name=mrreckey",DRECCNT:
                      " id=mrreckey",DRECCNT:
                      " value='",KEY40,"' onclick=RemoveTable(this); >":
                      "#",";
          ENDIF
.
          MATCH     SP70,MRMAFILM     * MicroFilmed?                   *9-11
          IF        @EQUAL
            WRITE     HTMLFILE;"#"#",#"#",#"B#"";
          ELSE
            WRITE     HTMLFILE;"#"Yes#",#"javascript:ViewMicroFilm('":
                               MRMAFILM,"#')#",#"0#"";
          ENDIF
.
PRBTB700  MOVE      BKREBOOK,KEY8
          CALL      CHVSCM00 
.
PRBTB800  CLEAR     COMMLINK
          APPEND    "javascript:UpdateComm('",COMMLINK
          APPEND    DBKREBOO,COMMLINK
          APPEND    "','",COMMLINK
          APPEND    BKREURNO,COMMLINK
          APPEND    "')",COMMLINK
          RESET     COMMLINK
.
          IF        NOMRFLAG=1                                       *12
            WRITE     HTMLFILE;",#"#"";
          ELSE
            WRITE     HTMLFILE;",#"",MRMAVOLN,"#"";
          ENDIF
.
          WRITE     HTMLFILE;",#"",COMTFLAG,"#"":                    *13
                             ",#"",COMMLINK,"#"":                    *14
                             ",#"",SORTORDR,"#"":                    *15
                             ",#"",MRHIDATE,"#"";                    *16
.
PRBTB810  MOVE      "RS",CATEGORY
          MOVE      MRMASTAT,CODE
          CALL      GETCOD00
.
          MATCH     SP70,PTCDUDF3
          IF        @EQUAL
            WRITE     HTMLFILE;",#"",TDESC,"#"";
          ELSE 
            WRITE     HTMLFILE;",#"<span ":
                        " style=color:",PTCDUDF3,">":
                        TDESC,"</span>":
                          "#"";
          ENDIF
.          WRITE     HTMLFILE;",#"",TDESC,"#"";                       *17
.
PRBTB820  CALL      PATLN000
.
          PACK      KEY8,PURNO,SP20
          CALL      RDPMPX21          * Get PMPXFLDR/PMPXPRVI for PATFLD00
          CALL      PATFLD00          * Returns KEY3 for folder colour code   
.
          MOVE      SP70,DOCFNAME
          MATCH     SP70,BKREADOC
          GOTO      PRBTB830 IF EQUAL
.
          MOVE      BKREADOC,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
          ENDIF
.
PRBTB830  PACK      KEY18,BKREBOOK,SP9,ONE,SP70
          CALL      RDBKDIA1                * get booking diagnosis
          IF        OVRCD=1
            MOVE      SP70,BKDIDES1
          ENDIF
.
          WRITE     HTMLFILE;",#"",LINKPRG,"#.pbl":                  *18
                                  "?reportno=",LINKREP:
                                  "&template=",LINKTEM:
                                  "&urnumber=",MRMAURNO,"#"":
                             ",#"",KEY3,"#"":                        *19
                             ",#"",DOCFNAME,"#"":                    *20
                             ",#"",BKDIDES1,"#",";                    *21
.
          IF        NOMRFLAG=1                                       *22  
            WRITE     HTMLFILE;"#"#",";                               
          ELSE
            WRITE     HTMLFILE;"#"<input type=checkbox name=mrkeplst",DRECCNT:
                      " id=mrkeplst",DRECCNT:
                      " value='",KEY40,"' checked onclick=RemoveTable(this); >":
                      "#",";
          ENDIF
.
          IF        NOMRFLAG=1                                       *23  
            WRITE     HTMLFILE;"#"#");";                               
          ELSE
            WRITE     HTMLFILE;"#"<input type=checkbox name=mrrmvlst",DRECCNT:
                      " id=mrrmvlst",DRECCNT:
                      " value='",KEY40,"' onclick=RemoveTable(this); >":
                      "#");";
          ENDIF
.
.          IF        RECCNT>50
.            WRITE     HTMLFILE;"alert('Limit reached too many patients to":
.                               " view!');";
.            GOTO      PRBTB999
.          ENDIF
.
          BRANCH    NOMRFLAG,PRBTB100       * Next pre adm if no MR for
.                                           * this patient
          MATCH     "1",MRCNDISA            * Display all volumes
          IF        @EQUAL
            MATCH     Z70,FILTDOTY          * Using document type filter
            GOTO      PRBTB200 IF NOT EQUAL
. 
            MATCH     MRCNRCTY,MRMADOTY
            IF        @EQUAL
              GOTO      PRBTB200
            ELSE
              GOTO      PRBTB400
            ENDIF
          ELSE                              * Not displaying all volumes
            MATCH     SP70,FILTDOTY         * All option document type filter
            GOTO      PRBTB200 IF EQUAL
          ENDIF
.
          GOTO      PRBTB100
.
PRBTB999  RETURN
.----------------------------------------------------------------------------
. Check Filters
.----------------------------------------------------------------------------
FLTPRB00  MATCH     SP70,ADMITTPT
          IF        !@EQUAL
            MATCH     ADMITTPT,BKRXADWD  
            GOTO      FLTPRB95 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,POSTWARD
          IF        !@EQUAL
            MATCH     POSTWARD,BKRXPOWD
            GOTO      FLTPRB95 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,UNITCODE
          IF        !@EQUAL
            MATCH     UNITCODE,BKREDEPT
            GOTO      FLTPRB95 IF NOT EQUAL
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      FLTPRB99
FLTPRB95  MOVE      ONE,EXIT
FLTPRB99  RETURN
.------------------------------------------------------------
. Ward Slection List
.------------------------------------------------------------
WSEL0000  PACK      KEY29,SP70
          IF        IBCNMHOS = 1
            PACK      KEY29,SP3,WBSEHOSP,SP70
          ENDIF
          CALL      RDSWARD7
WSEL0100  CALL      RDKWARD7
          BRANCH    OVRCD,WSEL9999
.
.                         if we have a bed number we have passed all the ward
.                         master records, so exit
          MATCH     SP70,WBED
          GOTO      WSEL9999 IF NOT EQUAL
.
          COMPARE   WACTIVE,ZERO                 *List only active wards
          GOTO      WSEL0100 IF NOT EQUAL
.
          IF        IBCNMHOS = 1
            MATCH      SP3,WBSEHOSP
            IF        !@EQUAL
              MATCH     WBSEHOSP,PTWDHOSN
              GOTO      WSEL9999 IF NOT EQUAL
            ENDIF
          ENDIF
.
.                       write the select option.
          WRITE     HTMLFILE;"<option value=#"",WWARD,"#"";
          MATCH     DFLTWARD,WWARD
          IF        @EQUAL
            WRITE     HTMLFILE;" selected";
          ENDIF
          WRITE     HTMLFILE;">",WBDESC;
.
          IF        IBCNMHOS = 1
            MATCH      SP3,WBSEHOSP
            IF        @EQUAL
              WRITE     HTMLFILE;" - ",PTWDHOSN;
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"</option>"
          GOTO      WSEL0100
.
WSEL9999  RETURN
.
.------------------------------------------------------------
. VLSD0000 Check if patient has more bookings for today
.------------------------------------------------------------
VLSD0000  MOVE      ZERO,SDAYFLAG           * No more bookings
          MOVE      ZERO,SCLIFLAG           * Same Clinic
          MOVE      TWO,FIRSTFLG            * First Found ?
.
          MOVE      OBAURNO,SAVURNO         * Save u/r number
          MOVE      OBADATE,SAVDATE         * Save date
          MOVE      OBACLIN,SAVCLIN         * Save Clinic
.
          PACK      KEY36,OBAURNO,OBADATE,SP70
          CALL      RDSBOKA3
VLSD1000  CALL      RDKBOKA3
          BRANCH    OVRCD,VLSD9999
.
          MATCH     SAVURNO,OBAURNO         * Same U/R
          GOTO      VLSD9999 IF NOT EQUAL
.
          MATCH     SAVDATE,OBADATE         * Same Day
          GOTO      VLSD9999 IF NOT EQUAL
.
          COMPARE   THREE,OBASTAT           * Ignore extended slots
          GOTO      VLSD1000 IF EQUAL
.
          PACK      KEY28,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT
          MATCH     SAVKEY28,KEY28
          GOTO      VLSD2000 IF EQUAL
.
          MOVE      ONE,SDAYFLAG            * Yes more than one booking on
.
          MATCH     SAVCLIN,OBACLIN
          GOTO      VLSD1000 IF NOT EQUAL
.
          IF        FIRSTFLG=2
            MOVE      ZERO,FIRSTFLG
          ENDIF
.
          MOVE      ONE,SCLIFLAG
          GOTO      VLSD1000
.
VLSD2000  IF        FIRSTFLG=2
            MOVE      ONE,FIRSTFLG
          ENDIF
          GOTO      VLSD1000
.
VLSD9999  RETURN
.
.---------------------------------------------------------------------------
. Check if this MR is in transit to the current location
.---------------------------------------------------------------------------
ITRN0000  MOVE      SP70,INTRANST
.
          MATCH     "1",MRCNTREC                 * System Using Tracking Receipt
          GOTO      ITRN9999 IF NOT EQUAL
.
          PACK      KEY26,MRMAKEY,Z70
          CALL      RSMRHIS1
          CALL      RPMRHIS1                     * Read last history record
          BRANCH    OVRCD,ITRN9999
.
          MATCH     MRMAKEY,MRHIKEY              * Correct MR history record
          GOTO      ITRN9999 IF NOT EQUAL
.
          MATCH     MRMACHOS,MRHIDHOS            * Check last history record matches
          GOTO      ITRN9999 IF NOT EQUAL        * the current hospital
.
          MATCH     MRMACLOC,MRHILOC             * Check last history record matches
          GOTO      ITRN9999 IF NOT EQUAL        * the current location
.
          MATCH     "1",MRHIRCST                 * In Transit
          GOTO      ITRN9999 IF NOT EQUAL
.
          MOVE      "<font color=red>(T)</font>",INTRANST
.
ITRN9999  RETURN
.
.------------------------------------------------------------------------------
.
          INC       IBAWEBCD
          INC       WEBDATCD
          INC       DGCLICRM                * DG API Med.Rec.Movement  *I-90203
.
          INC       APSMASIO/INC
          INC       PMSVX1IO/INC
          INC       PATMA1IO/INC
          INC       PATIN1IO/INC
          INC       PMSHCPIO/INC
          INC       PMSHCGIO/INC
          INC       PMSPX2IO/INC
          INC       PATMI1IO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       PATVISIO/INC   * Patients Visits File
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATALRIO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       OUTSITIO/INC   * Outpatients - Site Codes File
          INC       PATHSPIO/INC
          INC       PATFN1IO/INC   * Doctor File
          INC       PATPR1IO/INC   * New Patient Master File
          INC       PATWR1IO/INC
          INC       PMSCCDIO/INC   * Concession Card Details

          INC       MRTMASIO/INC
          INC       MLTSECIO/INC
          INC       MRTLOCIO/INC
          INC       MRTMOVIO/INC
          INC       MRTPDSIO/INC
          INC       MRTPSHIO/INC
          INC       MRTHISIO/INC
          INC       OUTBOAIO/INC
          INC       MRTRQHIO/INC
          INC       MRTRQDIO/INC
          INC       OUTCTYIO/INC
          INC       OUTCLIIO/INC
          INC       OUTMA1IO/INC
          INC       BOKDIAIO/INC
          INC       BOKRX1IO/INC
          INC       BOKRC1IO/INC
          INC       OUTSESIO/INC   * Outpatients - Session
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
          INC       OUTHEDIO/INC
          INC       MRTPLLIO/INC   * Pulled Medical Records
.
          INC       CLNHIMAS       * Clear nhimasaf file variables
          INC       PATDOCTM
          INC       PATMASTM
          INC       CLPMSPX2
          INC       CLMRTMAS
          INC       PMSPX2TM
          INC       PATMSXTM
          INC       PMSHCPTM
          INC       PMSHCGTM
          INC       PATDOCCD
          INC       CALCAGE
          INC       NAMSTRCD
          INC       PATNAMCD
          INC       DAYOFWEK
          INC       MRTRKSEQ       * Medical Records Tracking Sort Order
          INC       CLMRTPLL       * Pulled Medical Records

.

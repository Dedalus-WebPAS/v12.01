. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAPAT90                                            *
. *                                                                           *
. *     FUNCTION:   CURRENT ADMISSIONS BY DOCTOR REPORT                       *
. *                                                                           *
. *     DATE WRITTEN:     23/09/85                                            *
. *                                                                           *
. *     AUTHOR:           MALCOLM HAYSE (I.B.A)                               *
. *                                                                           *
. *     MODS:                                                                 *
. *                                                                           *
. *****************************************************************************
. * V12.00.01 19/05/2025 Thanh T         TSK 0955096                          *
. *           Changed for alphanumeric visit number                           *
. *****************************************************************************
. *     V10.04.02 02/07/2014  J.Tan         CAR 283481                        *
. *               Added option for GP/Practice                                *
. *     V10.04.01 30/01/2014  Peter Vela    CAR 296381                        *
. *               Added multi hospital                                        *
. *     V10.02.01 27/09/2011  Saroeun Soeur CAR 251935                        *
. *               fixed print out                                             *
. *     V9.12.01  05/03/2010  Mike Laming   CAR 210454                        *
. *               Add PMSVX1FD.pmvxrhc1 for Referring Doc.(if ADOCTR is blank)*
. *     V9.04.02  14/06/2005 Sandra Barcham  62811                            *
. *               Add Admitting Diagnosis                                     *
. *     V9.04.01  07/06/2005 Sandra Barcham  62811                            *
. *               Fix printin of Admission Date                               *
. *     V9.04.00  11/11/2004 Lina Vo  CAR 54858                               *
. *               Ported from V6.12                                           *
. *****************************************************************************
. *                   V6.05.01  27/10/1998 N Harrington                       *
. *                             Use HEAD132                                   *
. *****************************************************************************
. *                       W2.00 05/03/86 M.Hayse (I.B.A)                      *
. *                             New Displays                                  *
. *                       W2.01 14.05.87 S.Barcham (I.B.A.)                   *
. *                             Change PORT to PORTNO                         *
. *                       W2.02 31/08/87 G.Williams (I.B.A)                   *
. *                             Use INDEX instead of SORT                     *
. *                       V3.01 13/09/88 G.Williams (I.B.A)                   *
. *                             Add option for new page                       *
. *                       V4.01 06/12/88 M.Hayse (I.B.A)                      *
. *                             STANBY PATIENT (WARD/BED)                     *
. *                             SRF : 005249                                  *
. *                       V5.01 11/05/89 D.Di.Paola (IBA)                     *
. *                             Altered report to allow                       *
. *                             "8" character U/R & Adm No.                   *
. *                             Put in STD001FD                               *
. *                       V5.02 29/05/89 J.Tan                                *
. *                             Fixed Mother Adm. to form8                    *
. *                             (PATMISFD).     SRF 100750                    *
. *                       V5.03 06/06/89 J.Tan                                *
. *                             Mods Locking master file                      *
. *                             (PATIOMAS)       SRF 100809                   *
. *                       V5.04 17/06/89 J.Tan                                *
. *                             Removed Standby file                          *
. *                             SRF 101101                                    *
. *****************************************************************************
.
          INC       STD001FD   
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
          INC       PMSPX2FD/INC
          INC       PATHSPFD/INC
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC
          INC       PMSVX1FD/INC                                      *I-210454
          INC       PATDO1FD/INC
          INC       PMSHCPFD/INC                                      *I-210454
          INC       PATWR1FD/INC
+
.
.               WORK AREA
.
.
PATTMP    IFILE     SQL, FIXED=100, KEY="uc1-10,11-18"
.
. ----- Tempfile consists of following vars -----
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
.DOCNUM         DIM        10         10            1
DUNIQUE         DIM        8          8            11
.AWARD          DIM        3          3            19
.ABED           DIM        3          3            22
.AURNO          DIM        8          8            25
.AADMNO         DIM        8          8            33
.ADATE          DIM        8          8            41
.ADIAG1         DIM        50         50           49
DOCTYP          FORM       1          1            99     1=PATDO1af 2=PMSHCPaf
. ----- EOR 97 -----
.
PATHCP    IFILE     SQL, FIXED=110, KEY="uc1-10,11-20,21-28"
.
. ----- Tempfile consists of following vars -----
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
.HCPRAC         DIM        10         10            1
.DOCNUM         DIM        10         10           11
.DUNIQUE        DIM        8          8            21
.AWARD          DIM        3          3            29
.ABED           DIM        3          3            32
.AURNO          DIM        8          8            35
.AADMNO         FORM       8          8            43
.ADATE          DIM        8          8            51
.ADIAG1         DIM        50         50           59
.DOCTYP         FORM       1          1           109     1=PATDO1af 2=PMSHCPaf
. ----- EOR 107 -----
.
.
FNAMET    DIM       8
.
.      EXTRA VARIABLES
.
CMDLINE   DIM       50
.
SKIP      FORM      1
SDOCNUM   DIM       10
DOCNUM    DIM       10
SHCPRAC   DIM       10
HCPRAC    DIM       10
HEADFLAG  FORM      1
.
DALPHA    DIM       6
DIAG1     DIM       20
DNAME     DIM       30
DIM20     DIM       20
.
IBCNMHOS  FORM      1
.
PNAME     DIM       30
RECNUMB   FORM      8
.
UNIQUE    FORM      8
.
HOSPITAL  INIT      "HOSPITAL: "
.
PRGID     INIT      "IBAPAT90"
PRGNAM    INIT      "CURRENT ADMISSIONS BY DOCTOR REPORT"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          DISPLAY   *ES;
          CALL      DISPHEAD
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMET
.
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P56:24,*EL,"Opening patmi1af";
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A4,"patwr1af"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS = 1
            DISPLAY   *P64:24,"pathspaf";
            OPEN      PATHSPA1,"pathspaf"
          ENDIF
+
.
.        START OF PROGRAM
.
.             SELECT OPTION
.
START     KEYIN     *P1:4,*EF:
                    *P1:4,*V2LON,*DV,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,*DV,ONE,*HOFF," = Attending Doctor Sequence":
                    *P1:6,*V2LON,*DV,TWO,*HOFF," = Referring Doctor Sequence":
                    *P1:7,*V2LON,*DV,THREE,*HOFF," = GP/Practice":
                    *P1:9,"Option : ",*EL,*V2LON,OPTION
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
BRNCH     BRANCH    OPTION OF PSKIP,PSKIP,PSKIP
          BEEP
          GOTO      START
.
NOADMN    DISPLAY   *P20:24,*B,*EL,*V2LON:
                    "** No Current Admissions by Doctor **",*W3;
          GOTO      START
.
PSKIP     KEYIN     *P1:11,"Do you want a new page for each Doctor/GP Practice (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*EL,*V2LON,ANS;
          REP       "nNyY",ANS
.
          CMATCH    "Y",ANS
          GOTO      PSKIPY IF EQUAL
.
          CMATCH    "N",ANS
          GOTO      PSKIPN IF EQUAL
.
          BEEP
          GOTO      PSKIP
.
PSKIPY    MOVE      ZERO,SKIP
          GOTO      GETHOS
.
PSKIPN    MOVE      ONE,SKIP
.
GETHOS    CALL      KHOS0000                * keyin hospital (multi-campus only)
          BRANCH    EXIT,PSKIP
.
.          GET ONLY CURRENT ADMISSIONS..IE ASTAT=2
.
ADMNNO    CALL      KILLIDX
          CALL      CREAIDX
          CALL      DTMP0000                * clear tempfile
.
          MOVE      ONE TO UNIQUE
.
          PACK      KEY9 WITH TWO,SP8
          CALL      RDSMISS2
          MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** Scanning ADMISSIONS **",*W2;
.
          DISPLAY   *P20:24,*EL,"Admission :";
.
NEXTA     CALL      RDKMISS2
          BRANCH    OVRCD OF SORTF
.
          COMPARE   TWO,ASTAT
          GOTO      SORTF IF NOT EQUAL
.
          MOVE      SP70,HCPRAC
....      LOAD      DOCNUM FROM OPTION OF ADOCTA,ADOCTR
          MOVE      ONE,DOCTYP
          IF        OPTION = 2
            PACK      DOCNUM,ADOCTR,SP70
            MATCH     SP6,ADOCTR
            IF        @EQUAL
              UNPACK    SP20,PMVXRHC1
              PACK      KEY8,DAADMNO,SP70
              CALL      RDPMVX11
              PACK      DOCNUM,PMVXRHC1,SP70
              MOVE      TWO,DOCTYP
            ENDIF
          ELSE
            IF        OPTION = 1
              PACK      DOCNUM,ADOCTA,SP70
            ELSE
              UNPACK    SP70,PMPXRHC1,PMPXRH1G
              MOVE      AURNO,KEY8
              CALL      RDPMPX21
              PACK      DOCNUM,PMPXRHC1,SP70
              PACK      HCPRAC,PMPXRH1G,SP70
              MOVE      TWO,DOCTYP
            ENDIF
          ENDIF                                               * end   *I-210454
.
          IF        IBCNMHOS=1
            MATCH     SP70,PTHSHOSP                * all hospitals ?
            GOTO      CHECKDOC IF EQUAL            * yes - ignore check
.
            PACK      KEY8,DAADMNO,SP70
            CALL      RDPMVX11
            BRANCH    OVRCD,NEXTA
.
            MATCH     PMVXMHOS,PTHSHOSP            * same hospital ?
            GOTO      NEXTA IF NOT EQUAL           * no - ignore record
          ENDIF
.
CHECKDOC  IF        OPTION=3
            PACK      KEY20,HCPRAC,DOCNUM,SP70
            MATCH     SP70,KEY20
            GOTO      NEXTA IF EQUAL
          ELSE
            MATCH     SP70,DOCNUM
            GOTO      NEXTA IF EQUAL
          ENDIF
.
          DISPLAY   *P32:24,*V2LON,AADMNO;
.
          MATCH     SP3,AWARD
          GOTO      ENDWRD IF NOT EQUAL
.
.         CHECK FOR STANDBY PATIENTS
.
          MOVE       AADMNO,WSTBY
          PACK       KEY14 WITH WSTBY,SP6
          CALL       RDSWARD4
          CALL       RDKWARD4
          BRANCH    OVRCD OF ENDWRD
.
          MATCH     AADMNO,WSTBY
          GOTO      ENDWRD IF NOT EQUAL
.
          MOVE      WWARD,AWARD
          MOVE      WBED,ABED
.
ENDWRD    MOVE      UNIQUE,DUNIQUE
          IF        OPTION=3
            PACK      KEY28 WITH HCPRAC,DOCNUM,DUNIQUE
          ELSE
            PACK      KEY18 WITH DOCNUM,DUNIQUE
          ENDIF
          CALL      WRTEMPD
          ADD       ONE,UNIQUE
          GOTO      NEXTA
+
.
SORTF     DISPLAY   *P1:14,*EF
.
          MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,*P20:24,"Doctor Number :";
.
          MOVE      ZERO,HEADFLAG
          MOVE      ZERO,RECNUMB
          MOVE      SP20,KEY18
          MOVE      SP20,KEY28
          CALL      RDSSORTD
NEXTS     CALL      RDKSORTD
          BRANCH    OVRCD OF ENDP
.
          MOVE      SP30,DNAME
.
          IF        DOCTYP = 1
            UNPACK    SP70,ANS,DGNAME,DSNAME
            MOVE      DOCNUM,KEY6
            CALL      RDDOCT1
.
            MOVE      DGNAME,ANS
            PACK      DNAME,ANS,DOT,DSNAME,SP30
            MOVE      DSNAME,DALPHA
          ELSE                                        * read from PMSHCPaf
            UNPACK    SP70,PMHCGNAM,PMHCSNAM
            PACK      KEY10,DOCNUM,SP70
            CALL      RDPMHCP1
.
            MOVE      PMHCGNAM,ANS
            PACK      DNAME,ANS,DOT,PMHCSNAM
            REP       UPPLOW,DNAME
            MOVE      PMHCSNAM,DALPHA
          ENDIF
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME                 * Format the patient name
          MOVE      PACFNAME,PNAME
.
          UNPACK    ADATE INTO XCENT,XYEAR,XMON,XDAY
          MOVE      ADIAG1 TO DIAG1
          CALL      DISPDA
          ADD       ONE,RECNUMB
.
          DISPLAY   *P37:24,*V2LON,DOCNUM;
          GOTO      NEXTS
+
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NOADMN IF EQUAL
.
          CALL      PAGEND
          PRINT     *N,"  ***  END OF REPORT  ***"
.
          CALL      KILLIDX
.
          DISPLAY   *P20:24,*EL,*V2LON:
                    "** Report Generation Completed **",*W1;
          GOTO      START
+
.
.     PRINT ACTUAL DATA
.
DISPDA    MOVE      ZERO,HEADFLAG
.
          IF        OPTION=3
            COMPARE   ZERO,RECNUMB
            GOTO      DISP30 IF EQUAL         * First record
.
            PACK      KEY20,HCPRAC,DOCNUM,SP70
            PACK      DIM20,SHCPRAC,SDOCNUM,SP70
            MATCH     KEY20,DIM20
            GOTO      DISP70 IF EQUAL
.
            MATCH     HCPRAC,SHCPRAC
            IF        !@EQUAL
              BRANCH    SKIP OF DISP30
              MOVE      "60",CLNO
            ELSE
              COMPARE   "55",CLNO
              CALL      HEAD IF NOT LESS
.
              BRANCH    HEADFLAG,DISP40                 * a new page
              PRINT     *1,"|",*12,"|",DNAME;
              GOTO      DISP60
            ENDIF
          ELSE
            MATCH     DOCNUM,SDOCNUM
            GOTO      DISP70 IF EQUAL
.
            BRANCH    SKIP OF DISP30
            MOVE      "60",CLNO
          ENDIF
.
DISP30    COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          COMPARE   THREE,OPTION
          GOTO      DISP50 IF NOT EQUAL
.
DISP40    MATCH     SP70,HCPRAC
          IF        @EQUAL
            PRINT     *1,"|--BLANK--",*12,"|",DNAME;
          ELSE
            PRINT     *1,"|",HCPRAC,*12,"|",DNAME;
          ENDIF
          GOTO      DISP60
.
DISP50    PRINT     *1,"|",DOCNUM,*12,"|",DNAME;
.
DISP60    PRINT     *43,"|",AURNO,*52,"|",PNAME:
                    *83,"|",DIAG1:
                    *104,"|",AWARD,SLASH,ABED:
                    *112,"|",AADMNO:
                    *121,"|",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR,*132,"|":
                    *N,"|",*12,"|",*43,"|",*52,"|",*83,"|":
                    *104,"|",*112,"|",*121,"|",*132,"|"
.
          MOVE      DOCNUM,SDOCNUM
          MOVE      HCPRAC,SHCPRAC
          ADD       TWO,CLNO
          GOTO      DISP98
.
DISP70    COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          IF        OPTION=3
            BRANCH    HEADFLAG,DISP40                 * a new page
          ENDIF
.
          PRINT     *1,"|",SP10,*12,"|",SP30:
                    *43,"|",AURNO,*52,"|",PNAME:
                    *83,"|",DIAG1:
                    *104,"|",AWARD,SLASH,ABED:
                    *112,"|",AADMNO:
                    *121,"|",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR,*132,"|":
                    *N,"|",*12,"|",*43,"|",*52,"|",*83,"|":
                    *104,"|",*112,"|",*121,"|",*132,"|"
.
          ADD       TWO,CLNO
.
DISP98    MOVE      ZERO,HEADFLAG
DISP99    RETURN
+
.
.          LAST LINE ON THE PAGE
.
PAGEND    PRINT     "*---------------------------------------------------":
                    "--------------------------------------------------------":
                    "-----------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      IF        IBCNMHOS=1
            IF        OPTION=2
              MOVE      "CURRENT ADMISSIONS BY DOCTOR REPORT - REFERRING DOCTOR",CPHDROP1
            ELSE
              IF        OPTION=1
                MOVE      "CURRENT ADMISSIONS BY DOCTOR REPORT - ADMITTING DOCTOR",CPHDROP1
              ELSE
                MOVE      "CURRENT ADMISSIONS BY DOCTOR REPORT - GP/PRACTICE",CPHDROP1
              ENDIF
            ENDIF
.
            MATCH     SP70,PTHSHOSP
            IF        @EQUAL
              PACK      CPHDROP2,HOSPITAL,ANSA,ANSL,ANSL
            ELSE
              PACK      CPHDROP2,HOSPITAL,PTHSNAME,SP70
            ENDIF
.
          ELSE
            IF        OPTION=2
              MOVE      " - REFERRING DOCTOR",CPHDROPT
            ELSE
              IF        OPTION=1
                MOVE      " - ADMITTING DOCTOR",CPHDROPT
              ELSE
                MOVE      " - GP/PRACTICE",CPHDROPT
              ENDIF
            ENDIF
          ENDIF
.
          CLOCK     TIME,CTIMEIS
          CALL      HEAD132
.
          CALL      PAGEND
.
          IF        OPTION=3
            PRINT     *1,"|GP",*12,"|":
                      *43,"|U/R ",*52,"|",*83,"|":
                      *104,"|Ward/",*112,"|Admiss.":
                      *121,"|Admiss.",*132,"|":
                      *N,*1,"|Practice",*12,"|GP Name";
          ELSE
            PRINT     *1,"|Doctor",*12,"|":
                      *43,"|U/R ",*52,"|",*83,"|":
                      *104,"|Ward/",*112,"|Admiss.":
                      *121,"|Admiss.",*132,"|":
                      *N,*1,"|Number",*12,"|Doctors Name";
          ENDIF
.
          PRINT     *43,"|Number",*52,"|Patients Name":
                    *83,"|Admitting Diagnosis":
                    *104,"|Bed",*112,"|Number":
                    *121,"|Date",*132,"|"
.
          CALL      PAGEND
.
.    *** CAUSES DOCNUM OR HCPRAC TO BE PRINTED ON A NEW PAGE NO MATTER WHAT
.
          MOVE      SP70,SDOCNUM
          MOVE      SP70,SHCPRAC
          MOVE      EIGHT,CLNO
          MOVE      ONE,HEADFLAG
          RETURN
+
.******************************************************************************
.*                                  KHOS0000       Called by:                 *
.*                For multi-hospital, get the hospital code                   *
.******************************************************************************
.
KHOS0000  COMPARE   ONE,IBCNMHOS                 * using multi-hospital ?
          GOTO      KHOS9999 IF NOT EQUAL        * no - finished
.
          DISPLAY   *P1:15,*EL,"Hospital Id               : ";
          MOVE      TWENTY9,ECOL
          MOVE      "15",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND                * Not Mandatory
.
          CALL      PATHSPKY                     * get code
          BRANCH    EXIT,KHOS2000:               * nothing input
                         KHOS9500                * exitchar input
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          DISPLAY   *P29:15,*V2LON,PTHSNAME;
          MOVE      ZERO,EXIT
          GOTO      KHOS9999
.
KHOS3000  DISPLAY   *P37:15,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
.
KHOS9999  RETURN
+
.******************************************************************************
.       Deleting  AN INDEXED FILE BASED ON PORT
.******************************************************************************
KILLIDX   CLOSE     PATTMP
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
.******************************************************************************
.       Build tempfile
.******************************************************************************
CREAIDX   CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMET,CMDLINE
          IF        OPTION =3   
            APPEND    " 110 uc1-10,11-20,21-28",CMDLINE
          ELSE
            APPEND    " 100 uc1-10,11-18",CMDLINE
          ENDIF
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          IF        OPTION=3
            OPEN      PATHCP,FNAMET
          ELSE
            OPEN      PATTMP,FNAMET
          ENDIF
          RETURN
+
.******************************************************************************
.         Clear tempfile
.******************************************************************************
DTMP0000  PACK      KEY18,SP70
          PACK      KEY28,SP70
          CALL      RDSSORTD
          CALL      RDKSORTD
          BRANCH    OVRCD,DTMP9999
.
          IF        OPTION=3
            PACK      KEY28,HCPRAC,DOCNUM,DUNIQUE,SP70
          ELSE
            PACK      KEY18,DOCNUM,DUNIQUE,SP70
          ENDIF
          CALL      DELSORTD
          GOTO      DTMP0000
.
DTMP9999  RETURN
+
.******************************************************************************
+
WRTEMPD   IF        OPTION=3
            WRITE     PATHCP,KEY28;HCPRAC,DOCNUM,DUNIQUE,AWARD,ABED,AURNO:
                                   AADMNO,ADATE,ADIAG1,DOCTYP
          ELSE
            WRITE     PATTMP,KEY18;DOCNUM,DUNIQUE,AWARD,ABED,AURNO,AADMNO,ADATE:
                                   ADIAG1,DOCTYP
          ENDIF
          RETURN
.
RDSSORTD  IF        OPTION=3
            READ      PATHCP,KEY28;;
          ELSE
            READ      PATTMP,KEY18;;
          ENDIF
          RETURN
.
RDKSORTD  MOVE      ZERO,OVRCD
          IF        OPTION=3
            READKS    PATHCP;HCPRAC,DOCNUM,DUNIQUE,AWARD,ABED,AURNO:
                             AADMNO,ADATE,ADIAG1,DOCTYP
          ELSE
            READKS    PATTMP;DOCNUM,DUNIQUE,AWARD,ABED,AURNO,AADMNO,ADATE:
                             ADIAG1,DOCTYP
          ENDIF
          GOTO      OVERCOND IF OVER
          MOVE      DUNIQUE,UNIQUE
          RETURN
.
DELSORTD  MOVE      ZERO,OVRCD
          IF        OPTION=3
            RESET     KEY28
            DELETE    PATHCP,KEY28
          ELSE
            RESET     KEY18
            DELETE    PATTMP,KEY18
          ENDIF
          RETURN
.
          INC       PMSPX2IO/INC
          INC       PATDO1IO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC                                      *I-210454
          INC       PMSHCPIO/INC                                      *I-210454
          INC       PATWR1IO/INC
.
          INC       PATHSPKY/PBL
          INC       STD001IO     
          INC       TFILENAM                     * Generate Temp File Name
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       WEBERRIO/INC                 * Web Server Error Logging
.
ENDIT     CHAIN     PGM
          STOP

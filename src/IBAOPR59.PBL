.**************************************************************************
.*    Program     : IBAOPR59                                              *
.*    Name        : Session Statistics                                    *
.*    Date        : 03/01/91                                              *
.*    Author      : Rod Knowles                                           *
.*    Function    : This program will be run periodically to print        *
.*                  the session statistics                                *
.*    Mod's       :                                                       *
.*                                                                        *
.**************************************************************************
.*       V11.03.02  31/01/2023  Thanh T.         TSK 0919770              *
.*                  Recompiled for OPRDPHFD/IO changes                    *
.**************************************************************************
.*       V10.15.01  21/11/2019  Peter Vela     Task 0871644               *
.*                  Recompiled for OPRPCMFD                               *
.*       V10.04.01  15/04/2014  J.Tan          CAR 261630                 *
.*                  Removed port number from temp file name               *
.*       V9.07.01   19/09/2006 Peter Vela CAR 118228                      *
.*                  Added OPCSSTIM Shared Session Time to report          *
.**************************************************************************
.*                  04/02/91  ROD                                         *
.*                  Print Time Period for % Utilised                      *
.*                  06/02/91  ROD                                         *
.*                  Display Extra sessions booked (new requirements)      *
.*                  V5.01.02  22/05/91  Steve O'Gorman                    *
.*                            Fixed Errors found by the New Compiler      *
.*                  V5.01.03  07/02/92 J.Tan     SRF 112960               *
.*                            Fixed the definition of time utilised       *
.*       V5.01.04   18/06/92  Phantom & Desert Storm                      *
.*                  Display/Print date range for period                   *
.*                  Fixed Extra Booked session calculation                *
.*       V5.01.05   20/07/92  ROD                                         *
.*                  Fixed sessions cancelled error                        *
.*                                                                        *
.**************************************************************************
.
.$$$$$$
.
.    Ask 'The Phantom' what to do and if he doesn't know, ask 'Desert Storm'.
.    and if he doesn't know ask Ledge (Leg) and if he doesn't know your stuffed.
.
.$$$$$$
.
          INC       STD001FD
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       PATCONFD/INC
          INC       OPRCONFD/INC
          INC       OPRCSCFD/INC
          INC       OPRCSUFD/INC
          INC       OPRPCMFD/INC
          INC       PATDRGFD/INC
          INC       PATCODFD/INC
.
ADDSESUT  FORM      7
BOOKFLAG  FORM      1
SHARFLAG  FORM      1
CANCFLAG  FORM      1
CMDLINE   DIM       70
CODE      DIM       2
CODEOC    INIT      "OC"
CODEOD    INIT      "OD"
CODEOO    INIT      "OO"
CODESP    INIT      "SP"
CPTDATE   DIM       8
CURRDATE  DIM       8
DELYFLAG  FORM      1
ENDPER    DIM       6
FROMMON   DIM       2
FROMYEAR  DIM       4
MEDDESC1  DIM       9
MEDDESC2  DIM       9
OVERFLAG  FORM      1
PERCENTU  FORM      3.2
. PERCUTL   FORM      7.3
PERCUTL   FORM      7.5
PRFDATE1  DIM       10
PRTDATE1  DIM       10
PRFDATE2  DIM       10
PRTDATE2  DIM       10
REASON    DIM       20
REASHRT   DIM       17
SAVECENT  FORM      2
SAVEEND   DIM       6
SAVEMON   FORM      2
SAVEMON1  DIM       2
SAVEMON2  DIM       2
SAVESTR   DIM       6
SAVEXCOD  DIM       3
SAVEXREF  FORM      1
SAVEYEAR  FORM      2
SAVEYR1   DIM       4
SAVEYR2   DIM       4
SESBOK    INIT      "Sessions booked"
SESUSE    INIT      "Sessions used"
SESCAN    INIT      "Sessions cancelled"
SESDEL    INIT      "Sessions delayed"
SESOVE    INIT      "Sessions overtime"
STARTPER  DIM       6
TBOKFLAG  FORM      1
TBOOK1    FORM      7
TBOOK2    FORM      7
TIMBOK    INIT      "Time booked"
TIMUTI    INIT      "Time utilised"
TIMSHR    INIT      "Time shared"
TOMON     DIM       2
TOTALBOK  FORM      7
TOTALCAN  FORM      7
TOTALDEL  FORM      7
TOTALSHR  FORM      7
TOTALOVR  FORM      7
TOTALTTB  FORM      7
TOTALTTU  FORM      7
TOTALUSE  FORM      7
TOTALUTL  FORM      7
TOTBOKBO  FORM      7
TOTBOKCA  FORM      7
TOTBOKDE  FORM      7
TOTBOKOV  FORM      7
TOTBOKSH  FORM      7
TOTBOKTB  FORM      7
TOTBOKTU  FORM      7
TOTBOKUS  FORM      7
TOTBOKA1  FORM      7
TOTBOKA2  FORM      7
TOTSHRA1  FORM      7
TOTCANA1  FORM      7
TOTDELA1  FORM      7
TOTOVRA1  FORM      7
TOTPCTB1  FORM      7
TOTPCTB2  FORM      7
TOTUSEA1  FORM      7
TOTXTRSH  FORM      7
TOTXTRBO  FORM      7
TOTXTRCA  FORM      7
TOTXTRDE  FORM      7
TOTXTROV  FORM      7
TOTXTRTB  FORM      7
TOTXTRTU  FORM      7
TOTXTRUS  FORM      7
TOYEAR    DIM       4
TUTLFLAG  FORM      1
UNDLN39   INIT      "---------------------------------------"
USEDFLAG  FORM      1
UTIL1     FORM      7
UTIL2     FORM      7
UTIL3     FORM      7
WHATDESC  DIM       18
Z30       INIT      "ZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZ"
.
FNAME     DIM       8         
OPRTMPA1  IFILE     SQL, FIXED=19, KEY="U1-1,2-4"
. temp file vars
. --------------
DXREF     DIM       1       1    Type of record
XCODE     DIM       3       2    Break-up code
XBOOK     FORM      7       5    Number for booked sessions
XEXTRA    FORM      7      12    Number for extra sessions
XREF      FORM      1            1 = Sessions Booked
.                                2 = Sessions Used
.                                3 = Sessions Cancelled
.                                4 = Sessions Delayed
.                                5 = Sessions Overtime
.                                6 = Time Booked
.                                7 = Time Shared
.                                8 = Time Utilised
.
PRGID     INIT      "IBAOPR59"
PRGNAM    INIT      "Session Statistics"
VERSION   INIT      "V12.01.00"
.
.**************************************************************************
.*                               ML0000                                   *
.*                  Mainline code - Controlling logic                     *
.**************************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      SCRN0000                      * get main options
          BRANCH    EXIT,ML9999                   * Exit selected
.
ML2000    CALL      GDAT0000                      * get date
          BRANCH    EXIT,ML1000                   * nothing entered for start
          CALL      CONTQST                       * ask Continue (Y/N/C)?
          BRANCH    CEXIT,ML3000,ML2000,ML1000    
.
ML3000    CALL      PROC0000                      * do processing
.
ML4000    CALL      RPRT0000                      * print report 
          GOTO      ML1000
.
ML9999    CALL      KILL0000                      * remove temp file
          CHAIN     PGM
+
.**************************************************************************
.*                               INIT0000            Called By: ML0000    *
.*                      Initialization                                    *
.**************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P1:3,*EF,*P56:24,"controlf"
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P56:24,"oprcscaf"
          OPEN      OPRCSCA1,"oprcscaf"
          DISPLAY   *P56:24,"oprcsuaf"
          OPEN      OPRCSUA1,"oprcsuaf"
          DISPLAY   *P56:24,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
          DISPLAY   *P56:24,"oprpcmaf"
          OPEN      OPRPCMA1,"oprpcmaf"
          DISPLAY   *P56:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          READ      CONTROLF,FIFTY9;*27,CPERMTH
          READ      CONTROLF,FORTY2;*48,OPCNR59A
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAME
.         
INIT9999  RETURN
+
.**************************************************************************
.*                               SCRN0000            CALLED BY: ML0000    *
.*                         Display main menu screen                       *
.**************************************************************************
SCRN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Session Statistics":
                    *P1:7,"Select Option : "
.
SCRN1000  KEYIN     *P17:7,*EL,*DV,UNDLN1:
                    *P17:7,*V2LON,OPTION:
                    *P17:7,*DV,OPTION
.
          COMPARE   ZERO,OPTION                   * keyin = 0 ?
          GOTO      SCRN9000 IF EQUAL
.
          BRANCH    OPTION,SCRN2000
.
          BEEP 
          GOTO      SCRN1000
.
SCRN2000  MOVE      FALSE,EXIT
          GOTO      SCRN9999
.
SCRN9000  MOVE      TRUE,EXIT
.
SCRN9999  RETURN
+
.***************************************************************************
.*                                 GDAT0000          Called by: ML0000     *
.*                         keyin a date                                    *
.***************************************************************************
GDAT0000  DISPLAY   *P1:9,*EF,"Starting ",CPERMTH,*P17:9,":":
                    *P1:11,"Ending ",CPERMTH,*P17:11,":"
.
          MOVE      NINE,CVERT                    * set up defaults
          MOVE      "19",CCOL
          MOVE      "24",EVERT
          MOVE      "1",ECOL
          MOVE      ZERO,CHIGHLT
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          UNPACK    SP4,CMON,CYEAR
          MOVE      CCC,CCENT
.
GDAT1000  CALL      KEYPER                        * keyin a date
          BRANCH    CQUEST,GDAT1000               * don't allow for "?"
          BRANCH    OVRCD,GDAT9000                * nothing entered
.
. no check if on PATDRGFD
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6                     * zero fill date
          MOVE      KEY6,STARTPER                 * save start period
.
          CALL      RDDRGA3
          BRANCH    OVRCD,GDAT8500
.
. ending date of selected period must be less than current date
.
          MOVE      DRGYR,SAVEYR1                 * save values (financial)
          MOVE      DRGNUM,SAVEMON1
          MOVE      DRGMDSC,MEDDESC1              * save description
          PACK      SAVESTR,DRGYR,DRGNUM
.
          MATCH     CURRDATE,DRGTDTE
          IF        @LESS
            UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,PRFDATE1
            UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,PRTDATE1
            DISPLAY   *P30:9,PRFDATE1
            GOTO      GDAT5000
          ENDIF
          GOTO      GDAT8500                      * error
.
. --- now keyin Ending period ---
.
GDAT5000  MOVE      "11",CVERT                    * set up defaults
          MOVE      "19",CCOL
          UNPACK    STARTPER,CCENT,CYEAR,CMON
          MOVE      MEDDESC1,MEDDESC2             * default desc
.
GDAT6000  CALL      KEYPER                        * keyin a date
          BRANCH    CQUEST,GDAT6000               * don't allow for "?"
          BRANCH    OVRCD,GDAT8700                * nothing entered (default)[
.
. no check if on PATDRGFD
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6                     * zero fill date
          MOVE      KEY6,ENDPER
          CALL      RDDRGA3
          BRANCH    OVRCD,GDAT8400
.
. ending period must be less than current period
.
          MOVE      DRGMDSC,MEDDESC2              * save description
          MOVE      DRGYR,SAVEYR2                 * save values (financial)
          MOVE      DRGNUM,SAVEMON2
          PACK      SAVEEND,DRGYR,DRGNUM
.
          MATCH     CURRDATE,DRGTDTE
          GOTO      GDAT6100 IF LESS              * Ok.
          GOTO      GDAT8400                      * error
.
. ending period must be >= starting period
.
GDAT6100  MATCH     KEY6,STARTPER                 
          GOTO      GDAT8700 IF LESS
          GOTO      GDAT8700 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Invalid Range.  ";
          CALL      HITENTER
          GOTO      GDAT0000
.
GDAT8400  DISPLAY   *P1:24,*B,*EL,"Invalid Ending Period.  ";
          CALL      HITENTER
          GOTO      GDAT5000
.
GDAT8500  DISPLAY   *P1:24,*B,*EL,"Invalid Starting Period.  ";
          CALL      HITENTER
          GOTO      GDAT0000
.
GDAT8700  UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRFDATE2
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRTDATE2
          DISPLAY   *P30:11,PRTDATE2
.
          MOVE      FALSE,EXIT
          GOTO      GDAT9999
.
GDAT9000  MOVE      TRUE,EXIT                     * nothing entered 
.
GDAT9999  RETURN
+
.*****************************************************************************
.*                                  PROC0000            Called By: ML0000    *
.*                        do processing for report                           *
.*****************************************************************************
PROC0000  CALL      CRET0000                      * create temp file
.
          PACK      KEY16,SAVEYR1,SAVEMON1,SP20   * starting period
          CALL      RSOPCSU1                      * positional read
PROC1000  CALL      RKOPCSU1                      * read next record
          BRANCH    OVRCD,PROC5000                * no more records
.
          MATCH     OPCSDATE,SAVEEND              * still in range?
          GOTO      PROC5000 IF LESS              * No.
.
          CALL      BCOD0000                      * get the break-up code
.
          MATCH     ANSB,OPCSTYPE                 * Booked?
          GOTO      PROC3000 IF NOT EQUAL
.
          MOVE      ONE,XREF             * -- Sessions Booked -- *
          PACK      KEY4,XREF,XCODE
          CALL      RDOPRTMP
          BRANCH    OVRCD,PROC1100                * doesn't exist so write
          ADD       OPCSBSES,XBOOK
          CALL      UPOPRTMP                      * update
          GOTO      PROC1200
.
PROC1100  MOVE      OPCSBSES,XBOOK                * write a new record
          MOVE      ZERO,XEXTRA                   * no extras
          CALL      WROPRTMP
.
PROC1200  MOVE      TWO,XREF             * -- Sessions Used -- *
          PACK      KEY4,XREF,XCODE
          CALL      RDOPRTMP
          BRANCH    OVRCD,PROC1300                * doesn't exist so write
          ADD       OPCSUSES,XBOOK
          CALL      UPOPRTMP                      * update
          GOTO      PROC1400
PROC1300  MOVE      OPCSUSES,XBOOK
          MOVE      ZERO,XEXTRA                   * no extras
          CALL      WROPRTMP
.
PROC1400  MOVE      SIX,XREF
          PACK      KEY4,XREF,XCODE
          CALL      RDOPRTMP
          BRANCH    OVRCD,PROC1500                * doesn't exist so write
          ADD       OPCSBTIM,XBOOK
          CALL      UPOPRTMP                      * update
          GOTO      PROC1600
PROC1500  MOVE      OPCSBTIM,XBOOK
          MOVE      ZERO,XEXTRA                   * no extras
          CALL      WROPRTMP
.
PROC1600  MOVE      EIGHT,XREF
          PACK      KEY4,XREF,XCODE
          CALL      RDOPRTMP
          BRANCH    OVRCD,PROC1700
          ADD       OPCSUTIM,XBOOK
          CALL      UPOPRTMP                      * update
          GOTO      PROC1800
PROC1700  MOVE      OPCSUTIM,XBOOK
          MOVE      ZERO,XEXTRA                   * no extras
          CALL      WROPRTMP
.
PROC1800  MOVE      SEVEN,XREF                    * - Time Shared
          PACK      KEY4,XREF,XCODE
          CALL      RDOPRTMP                      * already on file?
          BRANCH    OVRCD,PROC1850
          ADD       OPCSSTIM,XBOOK
          CALL      UPOPRTMP                      * update
          GOTO      PROC1000
PROC1850  MOVE      OPCSSTIM,XBOOK
          MOVE      ZERO,XEXTRA                   * no extras
          CALL      WROPRTMP                      * write a new record
          GOTO      PROC1000
.
PROC3000  MOVE      ONE,XREF             * -- EXTRA Sessions Booked -- *
          PACK      KEY4,XREF,XCODE
          CALL      RDOPRTMP
          BRANCH    OVRCD,PROC3100                * doesn't exist so write
          ADD       OPCSBSES,XEXTRA
          CALL      UPOPRTMP                      * update
          GOTO      PROC4000
.
PROC3100  MOVE      OPCSBSES,XEXTRA               * write a new record
          MOVE      ZERO,XBOOK                    * no booked
          CALL      WROPRTMP
.
PROC4000  MOVE      TWO,XREF                  * now do Extra sessions used
          PACK      KEY4,XREF,XCODE
          CALL      RDOPRTMP
          BRANCH    OVRCD,PROC4200
          ADD       OPCSUSES,XEXTRA
          CALL      UPOPRTMP                      * update
          GOTO      PROC4250
PROC4200  MOVE      OPCSUSES,XEXTRA
          MOVE      ZERO,XBOOK                    * no booked
          CALL      WROPRTMP
.
.
PROC4250  MOVE      SIX,XREF
          PACK      KEY4,XREF,XCODE
          CALL      RDOPRTMP
          BRANCH    OVRCD,PROC4270                * doesn't exist so write
          ADD       OPCSBTIM,XEXTRA
          CALL      UPOPRTMP                      * update
          GOTO      PROC4300
PROC4270  MOVE      OPCSBTIM,XEXTRA
          MOVE      ZERO,XBOOK                    * no booked
          CALL      WROPRTMP
.
PROC4300  MOVE      EIGHT,XREF
          PACK      KEY4,XREF,XCODE
          CALL      RDOPRTMP
          BRANCH    OVRCD,PROC4600
          ADD       OPCSUTIM,XEXTRA
          CALL      UPOPRTMP                      * update
          GOTO      PROC4700
PROC4600  MOVE      OPCSUTIM,XEXTRA
          MOVE      ZERO,XBOOK                    * no booked
          CALL      WROPRTMP
.
PROC4700  MOVE      SEVEN,XREF                    * - time shared
          PACK      KEY4,XREF,XCODE
          CALL      RDOPRTMP                      * record already exist?
          BRANCH    OVRCD,PROC4750
          ADD       OPCSSTIM,XEXTRA
          CALL      UPOPRTMP                      * update
          GOTO      PROC1000
PROC4750  MOVE      OPCSSTIM,XEXTRA
          MOVE      ZERO,XBOOK                    * no booked
          CALL      WROPRTMP
          GOTO      PROC1000
.
.
PROC5000  PACK      KEY20,SAVEYR1,SAVEMON1,SP20   * starting period
          CALL      RSOPCSC1                      * positional read
PROC6000  CALL      RKOPCSC1                      * read next record
          BRANCH    OVRCD,PROC9999                * no more records
.
          MATCH     OPCCDATE,SAVEEND
          GOTO      PROC9999 IF LESS              * No.
.
          CALL      ECOD0000                      * get the break-up code
.
          MATCH     ANSB,OPCCTYPE                 * Booked?
          GOTO      PROC7000 IF NOT EQUAL
.
          MOVE      OPCCCTYP,XREF
          ADD       TWO,XREF
          PACK      KEY4,XREF,XCODE
          CALL      RDOPRTMP
          BRANCH    OVRCD,PROC6100
          ADD       OPCCNUMB,XBOOK
          CALL      UPOPRTMP                      * update temp file
          GOTO      PROC6000
PROC6100  MOVE      OPCCNUMB,XBOOK
          MOVE      ZERO,XEXTRA
          CALL      WROPRTMP
          GOTO      PROC6000
.
PROC7000  MOVE      OPCCCTYP,XREF
          ADD       TWO,XREF
          PACK      KEY4,XREF,XCODE
          CALL      RDOPRTMP
          BRANCH    OVRCD,PROC7100
          ADD       OPCCNUMB,XEXTRA
          CALL      UPOPRTMP                      * update temp file
          GOTO      PROC6000
PROC7100  MOVE      OPCCNUMB,XEXTRA
          MOVE      ZERO,XBOOK
          CALL      WROPRTMP
          GOTO      PROC6000
.
PROC9999  RETURN
+
.****************************************************************************
.*                                 BCOD0000             Called by: PROC0000 *
.*                        get breakup code for temp file (use CSUFD)        *
.****************************************************************************
BCOD0000  COMPARE   TWO,OPCNR59A                  * sys par 1 or 2 ?
          GOTO      BCOD5000 IF EQUAL
.
          MOVE      "zzz",XCODE
          GOTO      BCOD9999
.
BCOD5000  MOVE      OPCSTIME,XCODE
.
BCOD9999  RETURN
+
.****************************************************************************
.*                                 ECOD0000             Called by: PROC0000 *
.*                        get breakup code for temp file (use CSCFD)        *
.****************************************************************************
ECOD0000  COMPARE   TWO,OPCNR59A                  * sys par 1 or 2 ?
          GOTO      ECOD5000 IF EQUAL
.
          MOVE      OPCCCODE,XCODE
          GOTO      ECOD9999
.
ECOD5000  MOVE      OPCCTIME,XCODE
.
ECOD9999  RETURN
+
.****************************************************************************
.*                                 RPRT0000             Called by: ML0000   *
.*                          print report                                    *
.****************************************************************************
RPRT0000  CALL      SETV0000                      * initialize variables
          CALL      IBACLOCK    
.
          CALL      PAGE0000                      * set up new page
.
. get details from temp file
.
          PACK      KEY4,SP4
          CALL      RSOPRTMP
RPRT1000  CALL      RKOPRTMP
          BRANCH    OVRCD,RPRT9000                * end of records
.
. show user something is happening
.
          LOAD      WHATDESC,XREF,SESBOK,SESUSE,SESCAN,SESDEL,SESOVE,TIMBOK:
                             TIMSHR,TIMUTI
          DISPLAY   *P40:24,"Printing... ",WHATDESC,"  Code:",XCODE,*EL
.
. check if we need a new page
.
          COMPARE   "60",CLNO 
          CALL      PAGE0000 IF NOT LESS
.
. check if the type of record has changed
.
          COMPARE   XREF,SAVEXREF
          GOTO      RPRT1050 IF EQUAL
.
. the type of record has changed so print a line of underlines
.
          PRINT     *1,"*",UNDLN39,UNDLN39,"*"
.
RPRT1050  MOVE      XREF,SAVEXREF                 * save type of record
          ADD       ONE,CLNO                      * increment line counter
.
          COMPARE   TWO,OPCNR59A                  * report type 2 ?
          GOTO      RPRT5000 IF EQUAL
.
. print report type 1 ....................
. check what type of record ie. what type of statistic
.
          BRANCH    XREF,RPRT1100,RPRT1200,RPRT1300,RPRT1400,RPRT1500,RPRT1600:
                         RPRT1650,RPRT1700
.
. print Sessions booked
.
RPRT1100  PRINT     *1,"| Sessions Booked",*47,"|",*50,XBOOK,*58,"|":
                    *61,XEXTRA,*69,"|";
          ADD       XBOOK,TOTBOKA1                * get total
          ADD       XEXTRA,TOTBOKA1 
          PRINT     *72,TOTBOKA1," |"
          ADD       ONE,CLNO
          GOTO      RPRT1000
.
. print Sessions used
.
RPRT1200  PRINT     *1,"| Sessions Used",*47,"|",*50,XBOOK,*58,"|":
                    *61,XEXTRA,*69,"|";
          ADD       XBOOK,TOTUSEA1                * get total
          ADD       XEXTRA,TOTUSEA1
          PRINT     *72,TOTUSEA1," |"
          ADD       ONE,CLNO
          GOTO      RPRT1000
.
. print Sessions Cancelled
.
RPRT1300  MOVE      CODEOC,CODE
          CALL      REAS0000                      * get canc,overtime... descs
.
          PRINT     *1,"|";
          BRANCH    CANCFLAG,RPRT1310             * do we print heading again?
          PRINT     *3,"Sessions Cancelled";
RPRT1310  PRINT     *22,"- ",REASON:
                    *47,"|",*50,XBOOK,*58,"|",*61,XEXTRA,*69,"|";
          MOVE      ZERO,TOTCANA1
          ADD       XBOOK,TOTCANA1                * get total
          ADD       XEXTRA,TOTCANA1
          PRINT     *72,TOTCANA1," |"
          ADD       ONE,CLNO
          ADD       XBOOK,TOTBOKCA                * total booked sessions canc
          ADD       XEXTRA,TOTXTRCA               * total extra sessions canc
          MOVE      ONE,CANCFLAG                  * already printed heading flag
. now see if the next record is the same type. If not display the totals
          CALL      RKOPRTMP
          BRANCH    OVRCD,RPRT1350
          COMPARE   XREF,SAVEXREF                 * same as last one
          GOTO      RPRT1350 IF NOT EQUAL         * No.
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. print Total sess. cancelled 
.
RPRT1350  ADD       TOTBOKCA,TOTALCAN             * total sessions canc.
          ADD       TOTXTRCA,TOTALCAN
          PRINT     *1,"*",UNDLN39,UNDLN39,"*"
          PRINT     *1,"| Total Sessions Cancelled",*47,"|",*50,TOTBOKCA:
                    *58,"|",*61,TOTXTRCA,*69,"|",*72,TOTALCAN," |"
          ADD       TWO,CLNO
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.         
.print sessions delayed
.
RPRT1400  MOVE      CODEOD,CODE
          CALL      REAS0000                      * get canc,delay,o'time descs
          PRINT     *1,"|";
          BRANCH    DELYFLAG,RPRT1410             * do we print heading again?
          PRINT     *3,"Sessions Delayed";
RPRT1410  PRINT     *22,"- ",REASON:
                    *47,"|",*50,XBOOK,*58,"|",*61,XEXTRA,*69,"|";
          MOVE      ZERO,TOTDELA1
          ADD       XBOOK,TOTDELA1                * get total
          ADD       XEXTRA,TOTDELA1
          PRINT     *72,TOTDELA1," |"
          ADD       ONE,CLNO
          ADD       XBOOK,TOTBOKDE                * total booked sessions delay
          ADD       XEXTRA,TOTXTRDE               * total extra sessions delay
          MOVE      ONE,DELYFLAG                  * already printed a heading 
. now see if the next record is the same type. If not display the totals
          CALL      RKOPRTMP
          BRANCH    OVRCD,RPRT1450
          COMPARE   XREF,SAVEXREF                 * same as last one
          GOTO      RPRT1450 IF NOT EQUAL         * No.
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. print Total sess. Delayed 
.
RPRT1450  ADD       TOTBOKDE,TOTALDEL             * total sessions delayed
          ADD       TOTXTRDE,TOTALDEL
          PRINT     *1,"*",UNDLN39,UNDLN39,"*"
          PRINT     *1,"| Total Sessions Delayed",*47,"|",*50,TOTBOKDE:
                    *58,"|",*61,TOTXTRDE,*69,"|",*72,TOTALDEL," |"
          ADD       TWO,CLNO
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
.print sessions overtime
.
RPRT1500  MOVE      CODEOO,CODE
          CALL      REAS0000                      * get canc,delay,o'time descs
          PRINT     *1,"|";
          BRANCH    OVERFLAG,RPRT1510             * do we print heading again?
          PRINT     *3,"Sessions Overtime";
RPRT1510  PRINT     *22,"- ",REASON:
                    *47,"|",*50,XBOOK,*58,"|",*61,XEXTRA,*69,"|";
          MOVE      ZERO,TOTOVRA1
          ADD       XBOOK,TOTOVRA1                * get total
          ADD       XEXTRA,TOTOVRA1
          PRINT     *72,TOTOVRA1," |"
          ADD       ONE,CLNO
          ADD       XBOOK,TOTBOKOV                * total booked sessions delay
          ADD       XEXTRA,TOTXTROV               * total extra sessions delay
          MOVE      ONE,OVERFLAG                  * already printed a heading 
. now see if the next record is the same type. If not display the totals
          CALL      RKOPRTMP
          BRANCH    OVRCD,RPRT1550
          COMPARE   XREF,SAVEXREF                 * same as last one
          GOTO      RPRT1550 IF NOT EQUAL         * No.
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. print Total sess. overtime 
.
RPRT1550  ADD       TOTBOKOV,TOTALOVR             * total sessions canc.
          ADD       TOTXTROV,TOTALOVR
          PRINT     *1,"*",UNDLN39,UNDLN39,"*"
          PRINT     *1,"| Total Sessions Overtime",*47,"|",*50,TOTBOKOV:
                    *58,"|",*61,TOTXTROV,*69,"|",*72,TOTALOVR," |"
          ADD       TWO,CLNO
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. now print time booked
.
RPRT1600  PRINT     *1,"| Time Booked (Minutes)",*47,"|",*50,XBOOK:
                    *58,"|",*61,XEXTRA,*69,"|";
          MOVE      XBOOK,TOTBOKA1         * save values for % calc's
          MOVE      XEXTRA,TOTBOKA2
          MOVE      XBOOK,TOTALBOK         * get total
          ADD       XEXTRA,TOTALBOK
          PRINT     *72,TOTALBOK," |"
          ADD       ONE,CLNO
          GOTO      RPRT1000
.
. now print time shared
. 
RPRT1650  PRINT     *1,"| Time Shared          ",*47,"|",*50,XBOOK:
                    *58,"|",*61,XEXTRA,*69,"|";
          MOVE      XBOOK,TOTALSHR         * get total
          ADD       XEXTRA,TOTALSHR
          ADD       ONE,CLNO
          PRINT     *72,TOTALSHR," |"
          GOTO      RPRT1000
.
. print Time utilised
.
RPRT1700  PRINT     *1,"| Time Utilised (Minutes)",*47,"|",*50,XBOOK:
                    *58,"|",*61,XEXTRA,*69,"|";
          ADD       XBOOK,TOTALUTL                * get total
          ADD       XEXTRA,TOTALUTL
          MOVE      XBOOK,UTIL1                   * save values
          MOVE      TOTALUTL,UTIL2 
          MOVE      XEXTRA,UTIL3      
          PRINT     *72,TOTALUTL," |"
          PRINT     *1,"*",UNDLN39,UNDLN39,"*"
          ADD       TWO,CLNO
.
. now print Percentage utilised (& calculate)
.
          MOVE      UTIL1,PERCUTL    * calculate and print % booked sessions
          DIV       TOTBOKA1,PERCUTL
          MULT      "100",PERCUTL
          MOVE      PERCUTL,PERCENTU
          PRINT     *1,"| Percentage Utilised",*47,"|",*51,PERCENTU,*58,"|";
.
          MOVE      UTIL3,PERCUTL    * calculate and print % extra sessions
          DIV       TOTBOKA2,PERCUTL
          MULT      "100",PERCUTL
          MOVE      PERCUTL,PERCENTU
          PRINT     *61,PERCENTU,*69,"|";
.
          MOVE      UTIL2,PERCUTL    * calculate and print % total sessions
          DIV       TOTALBOK,PERCUTL
          MULT      "100",PERCUTL
          MOVE      PERCUTL,PERCENTU
          PRINT     *73,PERCENTU," |"
.
          PRINT     *1,"*",UNDLN39,UNDLN39,"*"
.
          GOTO      RPRT9000
.
RPRT5000
. report type 2
. check what type of record ie. what type of statistic
.
          MOVE      CODESP,CODE
          BRANCH    XREF,RPRT5100,RPRT5200,RPRT5300,RPRT5400,RPRT5500,RPRT5600:
                         RPRT5800,RPRT5700
.
. print sessions booked
.
RPRT5100  PRINT     *1,"|";
          CALL      REAS0000                      * get time period
          BRANCH    BOOKFLAG,RPRT5110
          PRINT     *3,"Sessions Booked";
RPRT5110  PRINT     *28,"|",*30,REASHRT,*47,"|",*50,XBOOK,*58,"|":
                    *61,XEXTRA,*69,"|";
          MOVE      ZERO,TOTBOKA1
          ADD       XBOOK,TOTBOKA1                * get total
          ADD       XEXTRA,TOTBOKA1 
          PRINT     *72,TOTBOKA1," |"
          ADD       XBOOK,TOTBOKBO                * keep running total
          ADD       XEXTRA,TOTXTRBO               * keep running total
          ADD       ONE,CLNO
          MOVE      ONE,BOOKFLAG                  * already printed a heading
. now see if the next record is the same type. If not display the totals
          CALL      RKOPRTMP
.......   BRANCH    OVRCD,RPRT5150
          COMPARE   XREF,SAVEXREF                 * same as last one
          GOTO      RPRT5150 IF NOT EQUAL         * No.
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000       
.
. print total sessions 
.
RPRT5150  PRINT     *1,"*",UNDLN39,UNDLN39,"*"
          PRINT     *1,"| Total Sessions Booked",*47,"|  ",TOTBOKBO:
                    *58,"|",*61,TOTXTRBO,*69,"|";
          ADD       TOTBOKBO,TOTALBOK             * get grand total
          ADD       TOTXTRBO,TOTALBOK
          PRINT     *72,TOTALBOK,*80,"|"
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. print Sessions used
.
RPRT5200  PRINT     *1,"|";
          CALL      REAS0000                      * get time period
          BRANCH    USEDFLAG,RPRT5210
          PRINT     *3,"Sessions Used";
RPRT5210  PRINT     *28,"|",*30,REASHRT,*47,"|",*50,XBOOK,*58,"|  ":
                    XEXTRA," |  ";
          MOVE      ZERO,TOTUSEA1
          ADD       XBOOK,TOTUSEA1                * get total
          ADD       XEXTRA,TOTUSEA1
          ADD       XBOOK,TOTBOKUS                * column totals
          ADD       XEXTRA,TOTXTRUS      
          PRINT     *72,TOTUSEA1," |"
          ADD       TWO,CLNO
          MOVE      ONE,USEDFLAG                  * already printed a heading
. now see if the next record is the same type. If not display the totals
          CALL      RKOPRTMP
......    BRANCH    OVRCD,RPRT5250
          COMPARE   XREF,SAVEXREF                 * same as last one
          GOTO      RPRT5250 IF NOT EQUAL         * No.
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. print total sessions 
.
RPRT5250  PRINT     *1,"*",UNDLN39,UNDLN39,"*"
          PRINT     *1,"| Total Sessions Used",*47,"|  ",TOTBOKUS," |  ":
                    TOTXTRUS," |  ";
          ADD       TOTBOKUS,TOTALUSE             * get grand total
          ADD       TOTXTRUS,TOTALUSE
          PRINT     *72,TOTALUSE,*80,"|"
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. print Sessions Cancelled
.
RPRT5300  PRINT     *1,"|";
          CALL      REAS0000                      * get time period
          BRANCH    CANCFLAG,RPRT5310             * need to print a heading
          PRINT     *3,"Sessions Cancelled";
RPRT5310  PRINT     *28,"|",*30,REASHRT,*47,"|",*50,XBOOK,*58,"|",*61,XEXTRA:
                    *69,"|";
          MOVE      ZERO,TOTCANA1
          ADD       XBOOK,TOTCANA1                * get total
          ADD       XEXTRA,TOTCANA1
          PRINT     *72,TOTCANA1," |"
          ADD       ONE,CLNO
          ADD       XBOOK,TOTBOKCA                * total booked sessions canc
          ADD       XEXTRA,TOTXTRCA               * total extra sessions canc
          MOVE      ONE,CANCFLAG                  * already printed a heading
. now see if the next record is the same type. If not display the totals
          CALL      RKOPRTMP
......    BRANCH    OVRCD,RPRT5350
          COMPARE   XREF,SAVEXREF                 * same as last one
          GOTO      RPRT5350 IF NOT EQUAL         * No.
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. print Total sess. cancelled
.
RPRT5350  ADD       TOTBOKCA,TOTALCAN             * total sessions canc.
          ADD       TOTXTRCA,TOTALCAN
          PRINT     *1,"*",UNDLN39,UNDLN39,"*"
          PRINT     *1,"| Total Sessions Cancelled",*47,"|  ",TOTBOKCA," |  ":
                    TOTXTRCA," |  ",TOTALCAN," |"
          ADD       THREE,CLNO
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. print Sessions Delayed  
.
RPRT5400  PRINT     *1,"|";
          CALL      REAS0000                      * get time period
          BRANCH    DELYFLAG,RPRT5410             * need to print a heading?
          PRINT     *3,"Sessions Delayed";
RPRT5410  PRINT     *28,"|",*30,REASHRT,*47,"|",*50,XBOOK,*58,"|",*61,XEXTRA:
                    *69,"|";
          MOVE      ZERO,TOTDELA1
          ADD       XBOOK,TOTDELA1                * get total
          ADD       XEXTRA,TOTDELA1
          PRINT     *72,TOTDELA1," |"
          ADD       ONE,CLNO
          ADD       XBOOK,TOTBOKDE                * total booked sessions delay
          ADD       XEXTRA,TOTXTRDE               * total extra sessions delay
          MOVE      ONE,DELYFLAG                  * already printed a heading
. now see if the next record is the same type. If not display the totals
          CALL      RKOPRTMP
......    BRANCH    OVRCD,RPRT5450
          COMPARE   XREF,SAVEXREF                 * same as last one
          GOTO      RPRT5450 IF NOT EQUAL         * No.
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. print Total sess. Delayed
.
RPRT5450  ADD       TOTBOKDE,TOTALDEL             * total sessions delayed
          ADD       TOTXTRDE,TOTALDEL
          PRINT     *1,"*",UNDLN39,UNDLN39,"*"
          PRINT     *1,"| Total Sessions Delayed",*47,"|  ",TOTBOKDE," |  ":
                    TOTXTRDE," |  ",TOTALDEL," |"
          ADD       THREE,CLNO
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. Print sessions overtime
.
RPRT5500  PRINT     *1,"|";
          CALL      REAS0000                      * get time period
          BRANCH    OVERFLAG,RPRT5510             * need to print a heading?
          PRINT     *3,"Sessions Overtime";
RPRT5510  PRINT     *28,"|",*30,REASHRT,*47,"|",*50,XBOOK,*58,"|",*61,XEXTRA:
                    *69,"|";
          MOVE      ZERO,TOTOVRA1
          ADD       XBOOK,TOTOVRA1                * get total
          ADD       XEXTRA,TOTOVRA1
          PRINT     *72,TOTOVRA1," |"
          ADD       ONE,CLNO
          ADD       XBOOK,TOTBOKOV                * total booked sessions delay
          ADD       XEXTRA,TOTXTROV               * total extra sessions delay
          MOVE      ONE,OVERFLAG                  * already printed a heading
. now see if the next record is the same type. If not display the totals
          CALL      RKOPRTMP
......    BRANCH    OVRCD,RPRT5550
          COMPARE   XREF,SAVEXREF                 * same as last one
          GOTO      RPRT5550 IF NOT EQUAL         * No.
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. print Total sess. overtime
.
RPRT5550  ADD       TOTBOKOV,TOTALOVR             * total sessions canc.
          ADD       TOTXTROV,TOTALOVR
          PRINT     *1,"*",UNDLN39,UNDLN39,"*"
          PRINT     *1,"| Total Sessions Overtime",*47,"|  ",TOTBOKOV," |  ":
                    TOTXTROV," |  ",TOTALOVR," |"
          ADD       THREE,CLNO
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. now print time booked
.
RPRT5600  PRINT     *1,"|";
          CALL      REAS0000                      * get time period
          BRANCH    TBOKFLAG,RPRT5610
          PRINT     *3,"Time Booked (Minutes)";
RPRT5610  PRINT     *28,"|",*30,REASHRT,*47,"|",*50,XBOOK,*58,"|",*61:
                    XEXTRA," |";
          MOVE      ZERO,TOTBOKA1
          ADD       XBOOK,TOTBOKA1                * get total
          ADD       XEXTRA,TOTBOKA1
          PRINT     *72,TOTBOKA1," |"
          ADD       XBOOK,TOTBOKTB                * totals
          ADD       XEXTRA,TOTXTRTB      
          ADD       TWO,CLNO
          MOVE      ONE,TBOKFLAG                  * already printed a heading
. now see if the next record is the same type. If not display the totals
          CALL      RKOPRTMP
......    BRANCH    OVRCD,RPRT5650
          COMPARE   XREF,SAVEXREF                 * same as last one
          GOTO      RPRT5650 IF NOT EQUAL         * No.
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. print total time booked 
.
RPRT5650  ADD       TOTBOKTB,TOTALTTB           
          ADD       TOTXTRTB,TOTALTTB
          PRINT     *1,"*",UNDLN39,UNDLN39,"*"
          PRINT     *1,"| Total Time Booked (Minutes)",*47,"|":
                    *50,TOTBOKTB,*58,"|",*61,TOTXTRTB,*69,"|",*72,TOTALTTB," |"
          ADD       TWO,CLNO
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. print Time utilised 
.
RPRT5700  PRINT     *1,"|";
          CALL      REAS0000                      * get time period
          BRANCH    TUTLFLAG,RPRT5710             * need to print a heading?
          PRINT     *3,"Time Utilised (Minutes)";
RPRT5710  PRINT     *28,"|",*30,REASHRT,*47,"|",*50,XBOOK,*58,"|",*61,XEXTRA:
                    *69,"|";
          MOVE      ZERO,TOTALUTL
          ADD       XBOOK,TOTALUTL                * get total
          ADD       XEXTRA,TOTALUTL
          PRINT     *72,TOTALUTL," |"
          ADD       XBOOK,TOTBOKTU                * totals
          ADD       XEXTRA,TOTXTRTU      
          ADD       TWO,CLNO
          MOVE      ONE,TUTLFLAG                  * already printed a heading
. now see if the next record is the same type. If not display the totals
          CALL      RKOPRTMP
          BRANCH    OVRCD,RPRT5750
          COMPARE   XREF,SAVEXREF                 * same as last one
          GOTO      RPRT5750 IF NOT EQUAL         * No.
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. print total time utilised 
.
RPRT5750  ADD       TOTBOKTU,TOTALTTU             * total sessions canc.
          ADD       TOTXTRTU,TOTALTTU
          PRINT     *1,"*",UNDLN39,UNDLN39,"*"
          PRINT     *1,"| Total Time Utilised (Minutes)",*47,"|  ":
                    TOTBOKTU," |  ":
                    TOTXTRTU," |  ",TOTALTTU," |"
          PRINT     *1,"*",UNDLN39,UNDLN39,"*"
          ADD       THREE,CLNO
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT6000
.
. print Sessions Shared
.
RPRT5800  PRINT     *1,"|";
          CALL      REAS0000                      * get time period
          BRANCH    SHARFLAG,RPRT5810             * need to print a heading
          PRINT     *3,"Time Shared";
RPRT5810  PRINT     *28,"|",*30,REASHRT,*47,"|",*50,XBOOK,*58,"|",*61,XEXTRA:
                    *69,"|";
          MOVE      ZERO,TOTSHRA1
          ADD       XBOOK,TOTSHRA1                * get total
          ADD       XEXTRA,TOTSHRA1
          PRINT     *72,TOTSHRA1," |"
          ADD       ONE,CLNO
          ADD       XBOOK,TOTBOKSH                * total booked sessions Share
          ADD       XEXTRA,TOTXTRSH               * total extra sessions Shared
          MOVE      ONE,SHARFLAG                  * already printed a heading
. now see if the next record is the same type. If not display the totals
          CALL      RKOPRTMP
.......   BRANCH    OVRCD,RPRT5850
          COMPARE   XREF,SAVEXREF                 * same as last one
          GOTO      RPRT5850 IF NOT EQUAL         * No.
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. print Total sess. shared
.
RPRT5850  ADD       TOTBOKSH,TOTALSHR             * total sessions canc.
          ADD       TOTXTRSH,TOTALSHR
          PRINT     *1,"*",UNDLN39,UNDLN39,"*"
          PRINT     *1,"| Total Time Shared",*47,"|  ",TOTBOKSH," |  ":
                    TOTXTRSH," |  ",TOTALSHR," |"
          ADD       THREE,CLNO
          CALL      RPOPRTMP                      * go back to where we were
          GOTO      RPRT1000
.
. calculate percentage utilised and print
.
RPRT6000  MOVE      SP3,SAVEXCOD
          MOVE      ZERO,TUTLFLAG
RPRT6500  PACK      KEY4,SIX,SAVEXCOD
          CALL      RSOPRTMP
RPRT7000  CALL      RKOPRTMP
          BRANCH    OVRCD,RPRT8000                * no more records
.
          COMPARE   SIX,XREF                      * still same record type?
          GOTO      RPRT8000 IF NOT EQUAL
.
          PRINT     *1,"|";
          CALL      REAS0000
          BRANCH    TUTLFLAG,RPRT7100
          PRINT     *3,"Percentage Utilised";
.
RPRT7100  MOVE      ONE,TUTLFLAG                  * dont display heading again
          MOVE      XBOOK,TBOOK1                  * save values
          MOVE      XEXTRA,TBOOK2
          MOVE      XCODE,SAVEXCOD
.
. we have a time booked record so get corresponding time utilised record
.
          PACK      KEY4,EIGHT,XCODE
          CALL      RDOPRTMP
.
          MOVE      XBOOK,PERCUTL    * calculate % booked sessions
          DIV       TBOOK1,PERCUTL
          MULT      "100",PERCUTL
          MOVE      PERCUTL,PERCENTU
          PRINT     *28,"|",*30,REASHRT,*47,"|",*51,PERCENTU,*58,"|";
.
          MOVE      XEXTRA,PERCUTL    * calculate % booked sessions
          DIV       TBOOK2,PERCUTL
          MULT      "100",PERCUTL
          MOVE      PERCUTL,PERCENTU
          PRINT     *61,PERCENTU,*69,"|";
.
          MOVE      ZERO,ADDSESUT
          ADD       XBOOK,ADDSESUT                * get total utilisation
          ADD       XEXTRA,ADDSESUT
.
          MOVE      ADDSESUT,PERCUTL    * calculate % total sessions
          DIV       TBOOK1,PERCUTL
          MULT      "100",PERCUTL
          MOVE      PERCUTL,PERCENTU
          PRINT     *73,PERCENTU," |"
.
          GOTO      RPRT6500
.
RPRT8000  PRINT     *1,"*",UNDLN39,UNDLN39,"*"
.
. now print total percentage utilised
.
          MOVE      TOTBOKTU,PERCUTL    * calculate % booked sessions
          DIV       TOTBOKTB,PERCUTL
          MULT      "100",PERCUTL
          MOVE      PERCUTL,PERCENTU
          PRINT     *1,"| Total Percentage Utilised",*47,"|",*51,PERCENTU;
.
          MOVE      TOTXTRTU,PERCUTL    * calculate % extra sessions
          DIV       TOTXTRTB,PERCUTL
          MULT      "100",PERCUTL
          MOVE      PERCUTL,PERCENTU
          PRINT     *58,"|",*61,PERCENTU,*69,"|";
.
          MOVE      TOTALTTU,PERCUTL    * calculate % total sessions
          DIV       TOTALTTB,PERCUTL
          MULT      "100",PERCUTL
          MOVE      PERCUTL,PERCENTU
          PRINT     *73,PERCENTU,*80,"|"
.
          PRINT     *1,"*",UNDLN39,UNDLN39,"*"
.
RPRT9000  CALL      FNSH0000                      * finish off report
.
RPRT9999  RETURN
+
.****************************************************************************
.*                                 PAGE0000             Called by: PROC0000 *
.*                     set-up for a new page                                *
.****************************************************************************
PAGE0000  CALL      HEAD80 
.
          UNPACK    STARTPER,FROMYEAR,FROMMON
          UNPACK    ENDPER,TOYEAR,TOMON
          PRINT     *20,"From : ",FROMMON,"/",FROMYEAR,"  ",PRFDATE1
          PRINT     *20,"To   : ",TOMON,"/",TOYEAR,"  ",PRTDATE2,*N
.
          MOVE      "10",CLNO
.
          BRANCH    OPCNR59A,PAGE3000
.
          PRINT     *1,"*",UNDLN39,UNDLN39,"*"
          PRINT     *1,"|",*28,"|",*47,"|   Booked |    Extra |    Total |"
          PRINT     *1,"|",*28,"| Time Period      | Sessions | Sessions |":
                    " Sessions |"
          GOTO      PAGE9999
.
PAGE3000  PRINT     *1,"*",UNDLN39,UNDLN39,"*"
          PRINT     *1,"|",*47,"|   Booked |    Extra |    Total |"
          PRINT     *1,"|",*47,"| Sessions | Sessions | Sessions |"
.
PAGE9999  RETURN
+
.*************************************************************************
.*                             FNSH0000             Called by:           *
.*                       finish of report                                *
.*************************************************************************
FNSH0000  PRINT     *N,*1,"*** End of Report ***",*N
          PRINT     *1,"Time booked         = Total expected duration for":
                    " session - Time for breaks"
          PRINT     *1,"Time utilised       = Total actual duration for ":
                    "session"
          PRINT     *1,"Time Shared         = Total shared/reallocation ":
                    "time"
          PRINT     *1,"Percentage utilised = Total utilised / Time booked *100"
          PRINT     *N,*1,"NB: Sessions Cancelled will always appear in the":
                    " Extra sessions column"
.
FNSH9999  RETURN
+
.*************************************************************************
.*                             REAS0000             Called by: RPRT0000  *
.*  get cancellation, delay, overtime descriptions & Time Period         *
.*************************************************************************
REAS0000  PACK      KEY5,CODE,XCODE
          CALL      RDCODE1
          BRANCH    OVRCD,REAS5000
.
          MOVE      TDESC,REASON
          MOVE      TDESC,REASHRT
          GOTO      REAS9999
.
REAS5000  MOVE      "Missing Code:",REASON
          ENDSET    REASON
          APPEND    XCODE,REASON
          APPEND    SP20,REASON
          RESET     REASON
          MOVE      REASON,REASHRT
.
REAS9999  RETURN
+
.*************************************************************************
.*                             SETV0000             Called by: RPRT0000  *
.*                      initialize variables                             *
.*************************************************************************
SETV0000  MOVE      ZERO,CANCFLAG
          MOVE      ZERO,SHARFLAG
          MOVE      ZERO,BOOKFLAG
          MOVE      ZERO,DELYFLAG
          MOVE      ZERO,OVERFLAG
          MOVE      ZERO,TBOKFLAG
          MOVE      ZERO,TUTLFLAG
          MOVE      ZERO,USEDFLAG
          MOVE      ZERO,TOTALBOK 
          MOVE      ZERO,TOTALCAN
          MOVE      ZERO,TOTALDEL 
          MOVE      ZERO,TOTALOVR
          MOVE      ZERO,TOTALSHR
          MOVE      ZERO,TOTALTTB
          MOVE      ZERO,TOTALTTU
          MOVE      ZERO,TOTALUSE
          MOVE      ZERO,TOTALUTL 
          MOVE      ZERO,TOTBOKBO 
          MOVE      ZERO,TOTBOKCA 
          MOVE      ZERO,TOTBOKDE 
          MOVE      ZERO,TOTBOKOV 
          MOVE      ZERO,TOTBOKSH
          MOVE      ZERO,TOTBOKTB 
          MOVE      ZERO,TOTBOKTU 
          MOVE      ZERO,TOTBOKUS 
          MOVE      ZERO,TOTSHRA1
          MOVE      ZERO,TOTCANA1 
          MOVE      ZERO,TOTDELA1 
          MOVE      ZERO,TOTOVRA1 
          MOVE      ZERO,TOTUSEA1 
          MOVE      ZERO,TOTXTRBO 
          MOVE      ZERO,TOTXTRCA 
          MOVE      ZERO,TOTXTRDE 
          MOVE      ZERO,TOTXTRSH
          MOVE      ZERO,TOTXTROV 
          MOVE      ZERO,TOTXTRTB 
          MOVE      ZERO,TOTXTRTU
          MOVE      ZERO,TOTXTRUS
          MOVE      ZERO,TUTLFLAG
.
SETV9999  RETURN
+
.*************************************************************************
.*                             KILL0000             Called by:           *
.*                   erase temporary file                                *
.*************************************************************************
KILL0000  CLOSE     OPRTMPA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    " 19 U1-1,2-4",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      OPRTMPA1,FNAME
.
CRET9999  RETURN
+
.***************************************************************************
.*                          IO ROUTINES FOR TEMP FILE                      *
.***************************************************************************
RSOPRTMP  MOVE      ZERO,OVRCD
          RESET     KEY4
          READ      OPRTMPA1,KEY4;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDOPRTMP  MOVE      ZERO,OVRCD
          RESET     KEY4
          READ      OPRTMPA1,KEY4;DXREF,XCODE,XBOOK,XEXTRA
          GOTO      OVERCOND IF OVER
          MOVE      DXREF,XREF
          RETURN
.
RKOPRTMP  MOVE      ZERO,OVRCD
          READKS    OPRTMPA1;DXREF,XCODE,XBOOK,XEXTRA
          GOTO      OVERCOND IF OVER
          MOVE      DXREF,XREF
          RETURN
.
RPOPRTMP  MOVE      ZERO,OVRCD
          READKP    OPRTMPA1;DXREF,XCODE,XBOOK,XEXTRA
          GOTO      OVERCOND IF OVER
          MOVE      DXREF,XREF
          RETURN
.
UPOPRTMP  MOVE      XREF,DXREF
          UPDATE    OPRTMPA1;DXREF,XCODE,XBOOK,XEXTRA
          RETURN
.
WROPRTMP  MOVE      XREF,DXREF
          RESET     KEY4
          WRITE     OPRTMPA1,KEY4;KEY4,XBOOK,XEXTRA
          RETURN
.
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
.
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
          INC       OPRCSCIO/INC
          INC       OPRCSUIO/INC
          INC       OPRPCMIO/INC
          INC       PATDRGIO/INC
          INC       PATCODIO/INC
          INC       PATCOMN3
.

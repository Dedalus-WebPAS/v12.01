.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBABIL53                                             *
.*                                                                            *
.*     FUNCTION:         NSW HEALTH DEPT. Extract - Date Reset                *
.*                                                                            *
.*     DATE WRITTEN:     05/03/2003                                           *
.*                                                                            *
.*     AUTHOR:           Mike Laming                                          *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*          V10.10.01  25/05/2017 J.Tan          TSK 0836455                  *
.*                     Mod multi hospital to use pmsprdaf for from/todate     *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCONFD/INC
          INC       PMSPRDFD/INC
          INC       PATHSPFD/INC
.
IBCNMHOS    FORM          1           43      Multi Hospital Indicator
.
.         WORK AREA
.
SAVDATE   DIM       8
NEWDATE   DIM       8
WORKDATE  DATE      8
MNTH      DIM       20
NN        FORM      2
MTH1      INIT      "January "
MTH2      INIT      "February "
MTH3      INIT      "March "
MTH4      INIT      "April "
MTH5      INIT      "May "
MTH6      INIT      "June "
MTH7      INIT      "July "
MTH8      INIT      "August "
MTH9      INIT      "September "
MTH10     INIT      "October "
MTH11     INIT      "November "
MTH12     INIT      "December "
.
PRGID     INIT      "IBABIL53"
PRGNAM    INIT      "NSW Health Dept Extract Date Reset"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
ML0000    CALL      INIT0000                * initialization and open files
.
ML1000    CALL      SDAT0000                * Set date and Keyin hospital if req
          BRANCH    EXIT,ML1000
.
          CALL      KEYD0000                * Keyin Extract Date required
          BRANCH    EXIT,ML9000
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML5000,ML1000,ML9000
.                         Yes    No     Cancel
.
ML5000    DISPLAY   *P1:24,*EL,"Updating the Date file",*W2;
          MOVE      NEWDATE,CHCSST
          MOVE      NEWDATE,CHCSED
.
          IF        IBCNMHOS=1
            PACK      KEY3,PTHSHOSP,SP70
            CALL      RDPMPRD1
            IF        OVRCD=0
              MOVE      CHCSST,PMPRSDAT
              MOVE      CHCSED,PMPREDAT
              CALL      UPPMPRD1
            ENDIF
          ELSE
            WRITAB    CONTROLF,EIGHTY8;*124,CHCSST,*132,CHCSED
          ENDIF
.
ML9000    CLOSE     CONTROLF
.
ML9999    CHAIN     PGM
          STOP
+
.******************************************************************************
.*                                  INIT0000                                  *
.*                      Initialization routine                                *
.******************************************************************************
INIT0000  CALL      DISPHEAD
          CALL      IBACLOCK
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"pmsprdaf";
          OPEN      PMSPRDA1,"pmsprdaf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,EIGHTY8;*124,CHCSST,*132,CHCSED
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  SDAT0000              Called by: ML0000   *
.*            Set date &  Enter selected Hospital code                        *
.******************************************************************************
SDAT0000  COMPARE   ONE,IBCNMHOS
          GOTO      SDAT8000 IF NOT EQUAL
.
          MOVE      "10",EVERT
          DISPLAY   *P1:EVERT,*EL,"Hospital Id   : ";
          MOVE      TWENTY7,ECOL
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
.
          OPEN      PATHSPA1,"pathspaf"
          CALL      PATHSPKY
          BRANCH    EXIT,SDAT9500,SDAT9500
.
          PACK      KEY3,PTHSHOSP,SP70
          CALL      RDPMPRD1
          IF        OVRCD=0
            PACK      CHCSST,PMPRSDAT,SP70
            PACK      CHCSED,PMPREDAT,SP70
          ENDIF
          DISPLAY   *P25:EVERT,*V2LON,PTHSNAME;
.
SDAT8000  MOVE      CHCSED,WORKDATE
          ADD       ONE,WORKDATE
          MOVE      WORKDATE,SAVDATE
          UNPACK    SAVDATE,CCENT,CYEAR,CMON,CDAY
          CALL      MNTH0000
          DISPLAY   *P1:7,*EF,"Last Extract was for ",*V2LON,MNTH,*HOFF:
                    *P1:8,"Enter a date in the Month for which you want ":
                    "to run the Extract";
.
          UNPACK    CHCSED,CCENT,CYEAR,CMON,CDAY
          MOVE      ZERO,EXIT
          GOTO      SDAT9999
.
SDAT9500  MOVE      ONE,EXIT
SDAT9999  RETURN
+
.******************************************************************************
.*                                  KEYD0000                                  *
.*                      Keyin Extract Date RequireD                           *
.******************************************************************************
KEYD0000  MOVE      ZERO,EXIT
          MOVE      "12",CVERT
          DISPLAY   *P1:CVERT,*EF,"Extract Date : ";
          MOVE      "16",CCOL
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          CALL      KEYDATE
          BRANCH    OVRCD,KEYD9500
.
          MOVE      "01",CDAY
          PACK      SAVDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",SAVDATE
.
          MOVE      SAVDATE,WORKDATE
          SUB       ONE,WORKDATE
          MOVE      WORKDATE,NEWDATE
          REP       " 0",NEWDATE
.
          UNPACK    NEWDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          UNPACK    SAVDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CMON     
          CALL      MNTH0000
.
.....     DISPLAY   *P1:17,*EF,*B,"Extract Selected : ",MNTH:
.....               "  (End Date ",CPCDATE,")  ":
          DISPLAY   *P1:17,"Be sure to select ",*V2LON:
                    "'Processing the new Run'",*HOFF," to run the ":
                    "Extract for ",MNTH;
          GOTO      KEYD9999
.
KEYD9500  MOVE      ONE,EXIT
.
KEYD9999  RETURN
+
.******************************************************************************
.*                                  MNTH0000                                  *
.*                                                                            *
.******************************************************************************
MNTH0000  MOVE      CMON,NN
          BRANCH    NN,MNTH1001,MNTH1002,MNTH1003,MNTH1004:
                       MNTH1005,MNTH1006,MNTH1007,MNTH1008:
                       MNTH1009,MNTH1010,MNTH1011,MNTH1012 
          GOTO      MNTH9000
.
MNTH1001  PACK      MNTH,MTH1,CCENT,CYEAR
          GOTO      MNTH9999
MNTH1002  PACK      MNTH,MTH2,CCENT,CYEAR
          GOTO      MNTH9999
MNTH1003  PACK      MNTH,MTH3,CCENT,CYEAR
          GOTO      MNTH9999
MNTH1004  PACK      MNTH,MTH4,CCENT,CYEAR
          GOTO      MNTH9999
MNTH1005  PACK      MNTH,MTH5,CCENT,CYEAR
          GOTO      MNTH9999
MNTH1006  PACK      MNTH,MTH6,CCENT,CYEAR
          GOTO      MNTH9999
MNTH1007  PACK      MNTH,MTH7,CCENT,CYEAR
          GOTO      MNTH9999
MNTH1008  PACK      MNTH,MTH8,CCENT,CYEAR
          GOTO      MNTH9999
MNTH1009  PACK      MNTH,MTH9,CCENT,CYEAR
          GOTO      MNTH9999
MNTH1010  PACK      MNTH,MTH10,CCENT,CYEAR
          GOTO      MNTH9999
MNTH1011  PACK      MNTH,MTH11,CCENT,CYEAR
          GOTO      MNTH9999
MNTH1012  PACK      MNTH,MTH12,CCENT,CYEAR
          GOTO      MNTH9999
MNTH9000  MOVE      "Unknown",MNTH
.
MNTH9999  RETURN
+
+
          INC       STD001IO
          INC       PATHSPKY
          INC       PMSPRDIO/INC
          INC       PATHSPIO/INC

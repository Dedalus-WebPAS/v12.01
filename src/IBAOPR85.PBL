.******************************************************************************
.* System   : Operating Room System                                           *
.* Program  : IBAOPR85                                                        *
.* Desc     : Operation Average Listing                                       *
.******************************************************************************
.* Date     :  19/12/1989                                                     *
.* Author   :  MMO                                                            *
.* Mods.    :                                                                 *
.*       V11.05.01  20/03/2025  Davin          TSK 0956210                    *
.*                  Add ICD10 Ed.13 - Go-live Date PTCNGDX3 (Sect.130 Pos.86) *
.*       V11.02.01  31/03/2022  Davin          TSK 0917793                    *
.*                  Add ICD10 Ed.12 - Go-live Date PTCNGDX2 (Sect.128 Pos.231)*
.*       V11.00.01  24/02/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*       V10.10.01  17/03/2017  Davin          TSK 0833093     HDP 2017/2018  *
.*                  Add ICD10 Ed.10 - Go-live Date PTCNGDVX (Sect.110 Pos.85) *
.*       V10.06.01  17/03/2015  Davin         CAR 311669      HDP 2015/16     *
.*                  Add PTCNGDV9 for ICD10 Ed.9 Go-live Date (Sect.110 Pos.77)*
.*       V10.03.01  23/04/2013  Davin         CAR 284420      HDP 2013/14     *
.*                  Add PTCNGDV8 for ICD10 Ed.8 Go-live Date (Sect.110 Pos.69)*
.*       V10.00.01  07/04/2010  Mike Laming   CAR 219246      HDP 2010        *
.*                  Add PTCNGDV7 for ICD10 Ed.7 Go-live Date (Sect.110 Pos.61)*
.*        V9.09.01  20/03/2008  Mike Laming   CAR 163918     HDP 2008         *
.*                  Add PTCNGDV6 for ICD10 Ed.6 Go-live                       *
.*        V9.06.01  15/05/2006  Mike Laming   CAR 105244      2006 HDP        *
.*                  ADD PTCNGDV5 for ICD10 Ed.5 Go-live                       *
.*        V9.03.02  23/06/2004  Henry Son        CAR 49037                    *
.*                  ICD10 Ed.4 changes - add PTCNGDV4.                        *
.*       V9.03.01   16/04/2004  Sylvek Litewka  CAR 20222                     *
.*                  Modified program to read ICD Operation file using         *
.*                  new common IO routine RDPTICO1.                           *
.* ------------------------------------------------------------------         *
.*       V5.07.B01  18/01/1999  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.**********************************************************************
.*             24/09/90 Steve Armstrong                                       *
.*                  Updated to use operation code/doctor index to             *
.*                  list in doctor or operation order                         *
.*        V5.01.02  22/05/91  Steve O'Gorman                                  *
.*                  Fixed Errors found by the New Compiler                    *
.*        V5.01.03  24/05/1995  Matt Surridge                                 *
.*                  Fixed bugs for global recompile.                          *
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.*                                                                            *
.******************************************************************************
.
.$$$$$
.    Operating Room System Operation Average Master List (IBAOPR85 )
.    ---------------------------------------------------------------
.
.    ML0000
.    Initialise and open files
.
.    ML1000
.    Display the menu and wait for a valid option to be selected.
.    If 0 is slected, chain back to the calling program.
.    If option 1 or 2 is selected, call the routine to get the
.    starting operation code.
.
.    ML2000
.    Prompt the user to enter the starting operation code.
.    If exitchar is entered, the program returns to get the next option.
.    If nothing is entered, the search will begin from the first code
.    on file.
.    If a "?" is entered, and we are using mbs codes, a listing of all
.    the codes on file is displayed.  The user can then enter a listed code.
.    If a code is entered, it is validated against the icd9 or mbs files.
.    Once a valid code has been entered, call the routine to get the
.    ending operation code.
.
.    ML3000
.    Prompt the user to enter the ending operation code.
.    If exitchar is entered, the program returns to get the next option.
.    If nothing is entered, the search will procede to the last code
.    on file.
.    If a "?" is entered, and we are using mbs codes, a listing of all
.    the codes on file is displayed.  The user can then enter a listed code.
.    If a code is entered, it is validated against the icd9 or mbs files.
.    Once a valid code has been entered, call the routine to get the
.    starting doctor code.
.    
.    ML4000
.    Prompt the user to enter the starting doctor code.
.    If exitchar is entered, the program returns to get the next option.
.    If nothing is entered, the search will begin from the first code
.    on file.
.    If a "?" is entered, a listing of all codes on file is displayed.
.    The user can then enter a listed code.
.    If a code is entered, it is validated against the doctor's file.
.    Once a valid code has been entered, call the routine to get the
.    ending doctor code.
.
.    ML5000
.    Prompt the user to enter the ending doctor code.
.    If exitchar is entered, the program returns to get the next option.
.    If nothing is entered, the search will procede to the last code
.    on file.
.    If a "?" is entered, a listing of all codes on file is displayed.
.    The user can then enter a listed code.
.    If a code is entered, it is validated against doctor's file.
.    Once a valid code has been entered, call the routine to see if a 
.    report is required.
.
.    ML6000
.    Ask the user if a report is required.
.    If "N" is entered, return to get the next starting operation code.
.    If "C" is entered, return to get the next option.
.    If "Y" is entered, call the routine to print the report.
.
.    ML7000
.    Depending on the option selected from the main menu, call the
.    appropriate routine to print the report.
.
.    ML8000
.    Print the report in operation sequence, then return to get the next
.    option.
.
.    ML9000
.    Print the report in doctor sequence, then return to get the next
.    option.
.
.    ML9999
.    Chain back to the calling program
.
.$$$$$

          INC       STD001FD
          INC       OPRAVEFD/INC           * Operation average file
          INC       OPRCONFD/INC           * Operating Room Control file
          INC       PATITMFD/INC           * Patient item file
          INC       PATICOFD/INC           * Patient icd9 file
          INC       PATCODFD/INC
          INC       PATDO1FD/INC           * doctor file
.
.                        GLOBAL VARIABLES                              
.
DASH58    INIT      "----------------------------------------------------------"
DCODE1    DIM       6                      * starting doctor code
DCODE2    DIM       6                      * ending doctor code
DDESC1    DIM       40                     * starting doctor desc.
DDESC2    DIM       40                     * ending doctor desc.
DIM1A     DIM       1                      * used in patdsitm
DIM3      DIM       3                      * patdsdoc
DIM20     DIM       20                     * used in patdsitm
DOCCODE   DIM       6                      * entered doctor code
DOCDEF    INIT      "Hospital Average                       "
DOCTOTL   FORM      5                      * counter for doctor codes
DSEQ      INIT      "- Doctor Sequence"
ENOPCOD   DIM       9
ENOPDES   DIM       40
ENDCCOD   DIM       6
ENDCDES   DIM       40
FINISH    INIT      "Finish"
LINE      FORM      3
NEWPAGE   FORM      1
OCODE1    DIM       9                      * starting operation code
OCODE2    DIM       9                      * ending operation code
ODESC1    DIM       40                     * starting operation desc.
ODESC2    DIM       40                     * ending operation desc.
OPRCODE   DIM       9                      * operation code
OPRDEF    INIT      "Description missing                    "
OPTOTL    FORM      5                      * operation code counter
OSEQ      INIT      "- Operation Sequence"
PIPE      INIT      "|"
PTCNDCQS  FORM      1
PTCNGDV3  DIM       8
PTCNGDV4  DIM       8                       * Ed4 Golive date          *I-49037
PTCNGDV5  DIM       8                       * Ed5 golive date         *I-105244
PTCNGDV6  DIM       8                       * Ed.6 Go-live date       *I-163918
PTCNGDV7  DIM       8                       * Ed.7 Go-live date       *I-219246
PTCNGDV8  DIM       8                       * Ed.8 Go-live date       *I-284420
PTCNGDV9  DIM       8                       * Ed.9 Go-live date       *I-311669
PTCNGDVX  DIM       8                       * Ed.10 Go-live date      *I-0833093
PTCNGDX1  DIM       8                       * Go live date for ICD10 Ed.11
PTCNGDX2  DIM       8                       * Go live date for ICD10 Ed.12
PTCNGDX3  DIM       8                       * Go live date for ICD10 Ed.13
PTCNI10D  DIM       8
RANGE     FORM      1
SP40      INIT      "                                        "
START     INIT      "Start "
STDOCOD   DIM       6
STDODES   DIM       40
STOPCOD   DIM       9
STOPDES   DIM       40
TEMPCODE  DIM       9                      * temporary operation code
TEMPDESC  DIM       40                     * temp codes desc
TEMPNAME  DIM       40                     * temp doc name 
TOTAL     FORM      5                      * total records count
ZZZ       INIT      "zzzzzzzzz"
.
PRGID     INIT      "IBAOPR85"
PRGNAM    INIT      "Operation Average Listing"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
.
ML0000    CALL      INIT0000               * initialization and open files
.
ML1000    CALL      MENU0000               * display menu
          CALL      OPTN0000               * get option
          BRANCH    EXIT,ML9999            * 0 chosen from menu
.
ML2000    CALL      SOPR0000               * get start operation code
          BRANCH    EXIT,ML1000            * get next option
.
ML3000    CALL      EOPR0000               * get ending operation code
          BRANCH    EXIT,ML2000:           * get start code again
                         ML2000
.
ML4000    CALL      SDOC0000               * get start doctor code
          BRANCH    EXIT,ML1000            * get next option
.
ML5000    CALL      EDOC0000               * get ending doctor code
          BRANCH    EXIT,ML4000:           * get start code again
                         ML4000
.
ML6000    CALL      CONTQST                * report required ?
          BRANCH    CEXIT,ML7000:          * yes
                          ML2000:          * get new codes
                          ML1000           * cancel - get next option
.
ML7000    BRANCH    COPTION,ML8000:        * operation sequence
                            ML9000         * doctor sequence
.
ML8000    CALL      ORPT0000               * produce report
          GOTO      ML1000                 * get next option
.
ML9000    CALL      DRPT0000               * print report
          GOTO      ML1000                 * get new option
.
ML9999    CHAIN     PGM                    * chain back to program
+
.**********************************************************************
.*                  INIT0000                     Called by: ML0000    *
.*        Initialisation Routine                                      *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,FORTY;*112,OPCNCODE
.
          BRANCH    OPCNCODE,INIT1000       * using mbs codes
.
          DISPLAY   *P64:24,"paticdof";     
          CALL      OPICO1                  * Open ICD Operation files
          GOTO      INIT2000
.
INIT1000  DISPLAY   *P64:24,"patitemn";
          OPEN      PATITEM1,"patitemn"
.
INIT2000  DISPLAY   *P64:24,"opraveaf";
          OPEN      OPRAVEA1,"opraveaf"
          OPEN      OPRAVEA2,"opraveaf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          MOVE      ONE,PACFRMT
.
INIT9999  RETURN
+
.**************************************************************************
.*                  MENU0000                     Called by: ML1000        *
.*        Display the menu and get the selected option                    *
.*        EXIT   1 = 0 entered                                            *
.*               0 = option 1 or 2 selected                               *
.**************************************************************************
.
MENU0000  DISPLAY   *P1:4,*EF,*V2LON,"0":
                    *P1:5,"1":
                    *P1:6,"2",*HOFF:
                    *P3:4,"= Exit":
                    *P3:5,"= Operation Sequence":
                    *P3:6,"= Doctor Sequence"      
.
MENU9999  RETURN
+
.**************************************************************************
.*                  OPTN0000                     Called by: ML1000        *
.*               Get the option                                           *
.**************************************************************************
.
OPTN0000  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:  * get option
                    *P17:8,*V2LON,COPTION:
                    *P17:8,*DV,COPTION
.
          BRANCH    COPTION,OPTN1000:       * valid selection
                            OPTN1000        * 
.
          COMPARE   ZERO,COPTION            * zero entered ?
          GOTO      OPTN9000 IF EQUAL       * yes
.
          GOTO      OPTN0000                * get option again
OPTN1000  MOVE      ZERO,EXIT               * valid option
          GOTO      OPTN9999
.
OPTN9000  MOVE      ONE,EXIT                * exit selected
.
OPTN9999  RETURN
+
.**************************************************************************
.*                  SOPR0000                     Called by: ML2000        *
.*             Get the start operation code                               *
.*        EXIT    0 = code entered                                        *
.*                1 = exitchar entered                                    *
.**************************************************************************
.
SOPR0000  CALL      DISP0000                * display entry field captions
. 
          MOVE      TEN,LINE
SOPR0500  CALL      GETO0000                * get operation code 
.
          CALL      CHKO0000                * see what was entered
          BRANCH    EXIT,SOPR9999:          * exitchar entered 
                         SOPR4000:          * ? entered
                         SOPR0500:          * invalid code   
                         SOPR9000           * nothing entered
.
SOPR1000  MOVE      OPRCODE,OCODE1          * save code & desc.
          MOVE      TEMPDESC,ODESC1
          MOVE      OPRCODE,STOPCOD         * display var
          MOVE      TEMPDESC,STOPDES        * display var
          GOTO      SOPR9500
.
SOPR4000  BUMP      OCODE1
          PACK      DIM1A,OCODE1,SP1
          MOVE      ONE,CNEWDS
          CALL      PATITMDS                * list codes on file
.
          MOVE      SP9,OPRCODE
.
SOPR5000  KEYIN     *P1:24,*EL,"Operation Code: ",*P17:24,*DV,UNDLN9:
                    *P17:24,*V2LON,OPRCODE:
                    *P17:24,*DV,OPRCODE
.
          CALL      CHKO0000                * see what was entered
          BRANCH    EXIT,SOPR9999:          * exitchar entered
                         SOPR4000:          * ? entered
                         SOPR5000           * invalid code
.
          CALL      MENU0000                * redisplay menu
          DISPLAY   *P1:8,"Select Option :",COPTION
          CALL      DISP0000                * redisplay field captions
.
          COMPARE   EXIT,FOUR               * anything entered ?
          GOTO      SOPR9000 IF EQUAL       * no 
.
          GOTO      SOPR1000
.
SOPR9000  MOVE      SP6,OCODE1              * pack code and description
          MOVE      SP40,ODESC1             * with blanks
          MOVE      START,STOPCOD
          MOVE      SP40,STOPDES        
.
.         Display using "start" for code
.
          DISPLAY   *P23:10,*V2LON,START,*HOFF,*P34:10,ODESC1
          MOVE      ZERO,EXIT
          GOTO      SOPR9999
.
.         Code entered, so display it
.
SOPR9500  DISPLAY   *P23:10,*V2LON,OCODE1,*HOFF,*P34:10,ODESC1
          MOVE      ZERO,EXIT
.
SOPR9999  RETURN
+
.**************************************************************************
.*                  EOPR0000                     Called by: ML3000        *
.*             Get the end operation code                                 *
.*        EXIT    0 = code entered                                        *
.*                1 = exitchar entered                                    *
.**************************************************************************
.
EOPR0000  MOVE      TEN1,LINE
EOPR0500  CALL      GETO0000                * get end code 
.
          CALL      CHKO0000                * see what was entered
          BRANCH    EXIT,EOPR9999:          * exitchar entered 
                         EOPR4000:          * ? entered
                         EOPR0500:          * invalid code
                         EOPR9000           * nothing entered  
.
EOPR1000  MOVE      OPRCODE,OCODE2          * save code and desc.
          MOVE      TEMPDESC,ODESC2
          MOVE      OPRCODE,ENOPCOD         * display var
          MOVE      TEMPDESC,ENOPDES        * display var
          GOTO      EOPR9500 
.
EOPR4000  BUMP      OCODE2
          PACK      DIM1A,OCODE2,SP1
          MOVE      ONE,CNEWDS
          CALL      PATITMDS                * list codes on file
.
          MOVE      SP9,OPRCODE
.
EOPR5000  KEYIN     *P1:24,*EL,"Operation Code: ",*P17:24,*DV,UNDLN9:
                    *P17:24,*V2LON,OPRCODE,*HOFF:
                    *P17:24,*DV,OPRCODE
.
          CALL      CHKO0000                * see what was entered
          BRANCH    EXIT,EOPR9999:          * exitchar entered 
                         EOPR4000:          * ? entered
                         EOPR5000           * invalid code
.
          CALL      MENU0000                * redisplay menu
          DISPLAY   *P1:8,"Select Option :",COPTION
          CALL      DISP0000                * redisplay field captions
          DISPLAY   *P23:10,*V2LON,STOPCOD,*HOFF,*P34:10,STOPDES
.
          COMPARE   EXIT,FOUR               * anything entered ?
          GOTO      EOPR9000 IF EQUAL       * no
.
          GOTO      EOPR1000                * check code
.
.         Pack code with "zzzzzzzzz", then display "finish" instead of
.         the code
.
EOPR9000  MOVE      ZZZ,OCODE2
          MOVE      SP40,ODESC2
          MOVE      FINISH,ENOPCOD
          MOVE      SP40,ENOPDES
          DISPLAY   *P23:11,*V2LON,FINISH,*HOFF,*P34:11,ODESC2
          MOVE      ZERO,EXIT
          GOTO      EOPR9999
.
EOPR9500  MATCH     OCODE1,OCODE2           * code 2 less than code 1 ?
          GOTO      EOPR9700 IF LESS        * yes
.
.         Code entered, so display it and desc.
.
EOPR9600  DISPLAY   *P23:11,*V2LON,OCODE2,*HOFF,*P34:11,ODESC2
          MOVE      ZERO,EXIT
          GOTO      EOPR9999

.
EOPR9700  DISPLAY   *P23:11,*V2LON,OCODE2,*HOFF,*P34:11,ODESC2
          DISPLAY   *P1:24,*B,"Invalid Operation Code Range.  ",*EL;
          CALL      HITENTER
          MOVE      TWO,EXIT
.
EOPR9999  RETURN
+
.**************************************************************************
.*                  SDOC0000                     Called by: ML4000        *
.*        Get the starting doctor code                                    *
.*        EXIT    0 = code entered                                        *
.*                1 = exitchar entered                                    *
.**************************************************************************
.
SDOC0000  MOVE      TEN3,LINE
          DISPLAY   *P23:13,*EL,*P23:14,*EL
. 
SDOC0500  CALL      GETD0000                * get first description
.
          CALL      CHKD0000                * see what was entered
          BRANCH    EXIT,SDOC9999:          * exitchar entered 
                         SDOC4000:          * ? entered
                         SDOC0500:          * invalid code
                         SDOC9000           * nothing entered
.
SDOC1000  MOVE      DOCCODE,DCODE1          * save code & desc.
          MOVE      PACFNAME,DDESC1
          MOVE      DOCCODE,STDOCOD
          MOVE      PACFNAME,STDODES
          GOTO      SDOC9500
.
SDOC4000  MOVE      SP6,DOCCODE             * initialise variables
          MOVE      SP9,DTITL
          PACK      DGNAME,SP20,SP5
          MOVE      SP20,DSNAME
          CALL      GETSCR00
          CALL      PATDOCDS
          CALL      PUTSCR00
          BRANCH    EXIT,SDOC9000
          MOVE      FALSE,EXIT                    * valid input
          PACK      KEY6,DOCCODE
          CALL      RDDOCT1
          CALL      DOCF0000                * format doctor name
          GOTO      SDOC1000
.
.         No code entered, so pack with blanks, then display "Start"
.
SDOC9000  MOVE      SP6,DCODE1
          MOVE      SP40,DDESC1
          MOVE      START,STDOCOD
          MOVE      SP40,STDODES
          DISPLAY   *P23:13,*V2LON,START,*HOFF,*P34:13,DDESC1
          MOVE      ZERO,EXIT
          GOTO      SDOC9999
.
.         Code entered, so display it and desc.
.
SDOC9500  DISPLAY   *P23:13,*V2LON,DCODE1,*HOFF,*P34:13,DDESC1
          MOVE      ZERO,EXIT
.
SDOC9999  RETURN
+
.**************************************************************************
.*                  EDOC0000                     Called by: ML5000        *
.*             Get the ending doctor code                                 *
.*        EXIT    0 = code entered                                        *
.*                1 = exitchar entered                                    *
.**************************************************************************
.
EDOC0000  MOVE      TEN4,LINE
EDOC0500  CALL      GETD0000                * get end code 
.
          CALL      CHKD0000                * see what was entered
          BRANCH    EXIT,EDOC9999:          * exitchar entered 
                         EDOC4000:          * ? entered
                         EDOC0500:          * invalid code
                         EDOC9000           * nothing entered  
.
EDOC1000  MOVE      DOCCODE,DCODE2          * save code and desc.
          MOVE      PACFNAME,DDESC2
          MOVE      DOCCODE,ENDCCOD
          MOVE      PACFNAME,ENDCDES
          GOTO      EDOC9500 
.
EDOC4000  MOVE      SP6,DOCCODE             * initialise variables
          MOVE      SP9,DTITL
          PACK      DGNAME,SP20,SP5
          MOVE      SP20,DSNAME
          CALL      GETSCR00
          CALL      PATDOCDS
          CALL      PUTSCR00
          BRANCH    EXIT,EDOC9000
          MOVE      FALSE,EXIT                    * valid input
          PACK      KEY6,DOCCODE
          CALL      RDDOCT1
          CALL      DOCF0000                * format doctor name
          GOTO      EDOC1000
.
.         No code entered, so pack with "zzzzzz", then display "Finish"
.
EDOC9000  MOVE      ZZZ,DCODE2
          MOVE      SP40,DDESC2
          DISPLAY   *P23:14,*V2LON,FINISH,*HOFF,*P34:14,DDESC2
          MOVE      ZERO,EXIT
          GOTO      EDOC9999
.
EDOC9500  MATCH     DCODE1,DCODE2           * code 2 less than code 1 ?
          GOTO      EDOC9700 IF LESS        * yes
.
.         Code entered, so display it and desc.
.
EDOC9600  DISPLAY   *P23:14,*V2LON,DCODE2,*HOFF,*P34:14,DDESC2
          MOVE      ZERO,EXIT
          GOTO      EDOC9999
.
EDOC9700  DISPLAY   *P23:14,*V2LON,DCODE2,*HOFF,*P34:14,DDESC2
          DISPLAY   *P1:24,*B,"Invalid Doctor Code Range.  ",*EL;
          CALL      HITENTER
          MOVE      TWO,EXIT
.
EDOC9999  RETURN
+
.**************************************************************************
.*                  DISP0000                     Called by: SOPR0000      *
.*           Display entry fields                           EOPR0000      *
.*                                                          SDOC0000      *
.*                                                          EDOC0000      *
.**************************************************************************
.
DISP0000  DISPLAY   *P1:10,*EF,"Operation Code From : ":
                    *P1:11,"Operation Code To   : ":
                    *P1:13,"Doctor Code From    : ":
                    *P1:14,"Doctor Code To      : "
.
DISP9999  RETURN
+
.**************************************************************************
.*                  GETO0000                     Called by: SOPR0000      *
.*                Get the operation code                    EOPR0000      *
.**************************************************************************
.
GETO0000  MOVE      SP9,OPRCODE
.
          KEYIN     *P23:LINE,*DV,UNDLN9:
                    *P23:LINE,*V2LON,OPRCODE:
                    *P23:LINE,*DV,OPRCODE
GETO9999  RETURN
+
.**************************************************************************
.*                  GETD0000                     Called by:  SDOC0000     *
.*            Get the doctor code                            EDOC0000     *
.**************************************************************************
.
GETD0000  MOVE      SP6,DOCCODE
          MOVE      SP9,DTITL
          PACK      DGNAME,SP20,SP5
          MOVE      SP20,DSNAME
.
          KEYIN     *P23:LINE,*DV,UNDLN6:
                    *P23:LINE,*V2LON,DOCCODE:
                    *P23:LINE,*DV,DOCCODE
GETD9999  RETURN
+
.**************************************************************************
.*                  CHKO0000                     Called by: SOPR0000      *
.*        See if anything was input for operation code      EOPR0000      *
.*        0 = valid code entered                                          *
.*        1 = exitchar entered                                            *
.*        2 = ? entered                                                   *
.*        3 = invalid code entered                                        *
.*        4 = nothing entered                                             *
.**************************************************************************
.
CHKO0000  ENDSET    OPRCODE                 * pad variable with blanks
          APPEND    SP9,OPRCODE
          RESET     OPRCODE
.
          MATCH     SP9,OPRCODE             * anything entered ?
          GOTO      CHKO9000 IF EQUAL       * no
.
          MATCH     EXITCHAR,OPRCODE        * exitchar entered ?
          GOTO      CHKO7000 IF EQUAL       * yes
.
          MOVE      SP40,TEMPDESC
          MOVE      OPRCODE,KEY9
          BRANCH    OPCNCODE,CHKO3000       * mbs code
.
          MOVE      SP70,ICDRDDTE
          CALL      RDPTICO1                * icd code on file ?
          BRANCH    OVRCD,CHKO9500          * no
.
          MOVE      ODESC,TEMPDESC          
          MOVE      ZERO,EXIT               * code entered
          GOTO      CHKO9999
.
CHKO3000  MATCH     QUEST,OPRCODE           * ? entered ?
          GOTO      CHKO8000 IF EQUAL       * yes
.
          PACK      KEY17,KEY9,SP70
          CALL      PATITMRD                * mbs code on file ?
          BRANCH    OVRCD,CHKO9500          * no
.
          MOVE      IDESC,TEMPDESC
          MOVE      ZERO,EXIT               * code entered
          GOTO      CHKO9999
.
CHKO7000  MOVE      ONE,EXIT
          GOTO      CHKO9999
.
CHKO8000  MOVE      TWO,EXIT
          GOTO      CHKO9999
.
CHKO9000  MOVE      FOUR,EXIT
          GOTO      CHKO9999
.
CHKO9500  DISPLAY   *P1:24,*B,"Operation code not on file.  ",*EL;
          CALL      HITENTER
          MOVE      THREE,EXIT
.
CHKO9999  RETURN
+
.**************************************************************************
.*                  CHKD0000                     Called by: SDOC0000      *
.*        See if anything was input for doctor code         EDOC0000      *
.*        0 = valid code entered                                          *
.*        1 = exitchar entered                                            *
.*        2 = ? entered                                                   *
.*        3 = invalid code entered                                        *
.*        4 = nothing entered                                             *
.**************************************************************************
.
CHKD0000  ENDSET    DOCCODE                 * pad variable with blanks
          APPEND    SP6,DOCCODE
          RESET     DOCCODE
.
          MATCH     SP6,DOCCODE             * anything entered ?
          GOTO      CHKD9000 IF EQUAL       * no
.
          MATCH     EXITCHAR,DOCCODE        * exitchar entered ?
          GOTO      CHKD7000 IF EQUAL       * yes
.
          MATCH     QUEST,DOCCODE           * ? entered ? 
          GOTO      CHKD8000 IF EQUAL       * yes
.
          MOVE      DOCCODE,KEY6
          CALL      RDDOCT1                 * doctor code on file ?
          BRANCH    OVRCD,CHKD9500          * no
          CALL      DOCF0000                * format doctor name
.
          MOVE      ZERO,EXIT               
          GOTO      CHKD9999
.
CHKD7000  MOVE      ONE,EXIT
          GOTO      CHKD9999
.
CHKD8000  MOVE      TWO,EXIT
          GOTO      CHKD9999
.
CHKD9000  MOVE      FOUR,EXIT
          GOTO      CHKD9999
.
CHKD9500  DISPLAY   *P1:24,*B,"Doctor code not on file.  ",*EL;
          CALL      HITENTER
          MOVE      THREE,EXIT
.
CHKD9999  RETURN
+
.**************************************************************************
.*                  ORPT0000                     Called by: ML8000        *
.*        Print report in operation sequence                              *
.**************************************************************************
.
ORPT0000  MOVE      OSEQ,CPHDROPT           
          DISPLAY   *P1:24,*EL,"Scanning:";
.
          CALL      PINT0000                * initialise page parameters
          CALL      PAGE0000                * write page headers
.
          PACK      KEY15,OCODE1,DCODE1
ORPT1000  CALL      RDOPAVE1                * try to read the record
          BRANCH    OVRCD,ORPT1100          * not found, get next record
          GOTO      ORPT1200                * we have exact key
.
ORPT1100  CALL      RKOPAVE1                * get the next record
          BRANCH    OVRCD,ORPT9000          * finished loop ?
.
ORPT1200  MATCH     OPAVCODE,OCODE2         * read too far ?
          GOTO      ORPT9000 IF LESS        * yes, finished
.
          MATCH     DCODE1,OPAVDOCT         * doctor code too low ?
          GOTO      ORPT1500 IF LESS        * yes
.
          MATCH     OPAVDOCT,DCODE2         * doctor code too high ?
          GOTO      ORPT2000 IF NOT LESS    * no              
.
.         Record just read has doctor code greater than required code, so pack
.         key with current operation code and "zzzzz" to position at the end of
.         this code, ready to read the next record
.
          PACK      KEY15,OPAVCODE,ZZZ 
          GOTO      ORPT1000
.
.         Record just read, has doctor code less then required code.  Pack key
.         with current code and starting doctor code to position with RK at next
.         record.
.
ORPT1500  PACK      KEY15,OPAVCODE,DCODE1
          GOTO      ORPT1000
. 
.         Valid record found
.
ORPT2000  COMPARE   CLNO,FIFTY5             * page full ?
          GOTO      ORPT3000 IF LESS
          GOTO      ORPT4000
.
ORPT3000  PRINT     ASK,DASH58,DASH58,ASK
          MOVE      ONE,NEWPAGE
          CALL      PAGE0000
.
ORPT4000  DISPLAY   *P13:24,*EL,OPAVCODE,OPAVDOCT;
.
.         Get the operation description and the doctors name
.
          CALL      DESC0000
.
          CALL      LDIS0000                * print record
          GOTO      ORPT1100                * get the next record
.
ORPT9000  CALL      PEND0000                * finish up report
.
ORPT9999  RETURN
+
.**************************************************************************
.*                  DRPT0000                     Called by: ML9000        *
.*        Print report in doctor sequence                                 *
.**************************************************************************
.
DRPT0000  MOVE      DSEQ,CPHDROPT
          DISPLAY   *P1:24,*EL,"Scanning:";
.
          CALL      PINT0000                * initialise page parameters
          CALL      PAGE0000                * write page headers
.
          PACK      KEY15,DCODE1,OCODE1
DRPT1000  CALL      RDOPAVE2                * try to read the record
          BRANCH    OVRCD,DRPT1100          * not found, get next record
          GOTO      DRPT1200                * we have exact key
.
DRPT1100  CALL      RKOPAVE2                * get the next record
          BRANCH    OVRCD,DRPT9000          * finished loop ?
.
DRPT1200  MATCH     OPAVDOCT,DCODE2         * read too far ?
          GOTO      DRPT9000 IF LESS        * yes, finished
.
          MATCH     OCODE1,OPAVCODE         * doctor code too low ?
          GOTO      DRPT1500 IF LESS        * yes
.
          MATCH     OPAVCODE,OCODE2         * doctor code too high ?
          GOTO      DRPT2000 IF NOT LESS    * no              
.
.         Record just read has operation code greater than required code, 
.         so pack key with current doctor code and "zzzzzz" to position at 
.         the end of this code, ready to read the next record
.
          PACK      KEY15,OPAVDOCT,ZZZ 
          GOTO      DRPT1000
.
.         Record just read, has operation code less than required code. 
.         Pack key with current code and starting operation code to 
.         position with RK at next record.
.
DRPT1500  PACK      KEY15,OPAVDOCT,OCODE1
          GOTO      DRPT1000
. 
.         Valid record found
.
DRPT2000  COMPARE   CLNO,FIFTY5             * page full ?
          GOTO      DRPT3000 IF LESS
          GOTO      DRPT4000
.
DRPT3000  PRINT     ASK,DASH58,DASH58,ASK
          MOVE      ONE,NEWPAGE
          CALL      PAGE0000
.
DRPT4000  DISPLAY   *P13:24,*EL,OPAVDOCT,OPAVCODE;
.
.         Get the operation description and the doctors name
.
          CALL      DESC0000
.
          CALL      LDIS0000                * print record
          GOTO      DRPT1100                * get the next record
.
DRPT9000  CALL      PEND0000                * finish up report
.
DRPT9999  RETURN
+
.**************************************************************************
.*                  DESC0000                     Called by: ORPT0000      *
.*        Get operation and doctor descriptions             DRPT0000      *
.**************************************************************************
.
DESC0000  MOVE      OPAVDOCT,KEY6
          CALL      RDDOCT1                 * get doctors record
.
          BRANCH    OVRCD,DESC1000          * doctor code not on file
.
          CALL      DOCF0000                * set doctor name format
          MOVE      PACFNAME,TEMPNAME
          GOTO      DESC2000
.
DESC1000  MOVE      DOCDEF,TEMPNAME         * use default name
.
.         Get operation description
.
DESC2000  MOVE      OPAVCODE,KEY9
          BRANCH    OPCNCODE,DESC3000       * mbs code
.
          MOVE      SP70,ICDRDDTE
          CALL      RDPTICO1                * get icd code 
          GOTO      DESC4000
.
DESC3000  PACK      KEY17,KEY9,SP70
          CALL      PATITMRD                * get mbs code
.
DESC4000  BRANCH    OVRCD,DESC6000          * code not found
.
          BRANCH    OPCNCODE,DESC5000       * mbs code
.
          MOVE      ODESC,TEMPDESC          * save icd9 desc.
          GOTO      DESC9999
.
DESC5000  MOVE      IDESC,TEMPDESC          * save mbs desc.
          GOTO      DESC9999
.
DESC6000  MOVE      OPRDEF,TEMPDESC         * use default code
.
DESC9999  RETURN
+
.**************************************************************************
.*                  PINT0000                     Called by: ORPT0000      *
.*        Initialise print parameters                       DRPT0000      *
.**************************************************************************
.
PINT0000  MOVE      ZERO,CPAGENO            * page no.
          CALL      IBACLOCK
          MOVE      ONE,CNOUNDLN
          MOVE      ZERO,TOTAL              * record count
          MOVE      ZERO,DOCTOTL            * doctor count
          MOVE      ZERO,OPTOTL             * operations total
          MOVE      "ZXZW42",DOCCODE        * move rubbish
          MOVE      SP9,OPRCODE
.
PINT9999  RETURN
+
.**************************************************************************
.*                  LDIS0000                     Called by: ORPT0000      *
.*        Print a record line                               DRPT0000      *
.**************************************************************************
.
LDIS0000  BRANCH    COPTION,LDIS5000        * operation sequence
.
          COMPARE   ONE,NEWPAGE             * are we on a new page?
          GOTO      LDIS2000 IF EQUAL
.
          MATCH     OPAVDOCT,DOCCODE        * code same as previous record ?
          GOTO      LDIS2100 IF EQUAL       * yes
.
          PRINT     ASK,DASH58,DASH58,ASK
          ADD       ONE,DOCTOTL             * increment total doctors
          ADD       ONE,CLNO
          MOVE      OPAVDOCT,DOCCODE        * save new code
.
.         Print with doctor code and name
.
LDIS1000  PRINT     PIPE,*3,OPAVDOCT,*10,PIPE,*12,TEMPNAME,*53,PIPE:
                    *55,OPAVCODE,*65,PIPE,*67,TEMPDESC,*108,PIPE,*110:
                    OPAVAVER,*118,PIPE
          GOTO      LDIS9000
.
.         Dont print doctor code and name
.
LDIS2000  PRINT     ASK,DASH58,DASH58,ASK
          PRINT     PIPE,*3,OPAVDOCT,*10,PIPE,*12,TEMPNAME,*53,PIPE:
                    *55,OPAVCODE,*65,PIPE,*67,TEMPDESC,*108,PIPE,*110:
                    OPAVAVER,*118,PIPE
          MOVE      ZERO,NEWPAGE
          GOTO      LDIS9000
.
LDIS2100  PRINT     PIPE,*10,PIPE,*53,PIPE:
                    *55,OPAVCODE,*65,PIPE,*67,TEMPDESC,*108,PIPE,*110:
                    OPAVAVER,*118,PIPE
          GOTO      LDIS9000
.
.         Operation sequence
.
LDIS5000  COMPARE   ONE,NEWPAGE
          GOTO      LDIS7000 IF EQUAL
          MATCH     OPAVCODE,OPRCODE        * code same as previous record ?
          GOTO      LDIS7100 IF EQUAL       * yes
.
          PRINT     ASK,DASH58,DASH58,ASK
          ADD       ONE,OPTOTL              * increment op. codes count
          ADD       ONE,CLNO
          MOVE      OPAVCODE,OPRCODE        * save new code
.
LDIS6000  PRINT     PIPE,*3,OPAVCODE,*13,PIPE,*15,TEMPDESC,*56,PIPE,*58:
                    OPAVDOCT,*65,PIPE,*67,TEMPNAME,*108,PIPE,*110:
                    OPAVAVER,*118,PIPE
          GOTO      LDIS9000
.
LDIS7000  PRINT     ASK,DASH58,DASH58,ASK
          PRINT     PIPE,*3,OPAVCODE,*13,PIPE,*15,TEMPDESC,*56,PIPE,*58:
                    OPAVDOCT,*65,PIPE,*67,TEMPNAME,*108,PIPE,*110:
                    OPAVAVER,*118,PIPE
          MOVE      ZERO,NEWPAGE
          GOTO      LDIS9000
.
LDIS7100  PRINT     PIPE,*13,PIPE,*56,PIPE,*58:
                    OPAVDOCT,*65,PIPE,*67,TEMPNAME,*108,PIPE,*110:
                    OPAVAVER,*118,PIPE
.
LDIS9000  ADD       ONE,CLNO                           * page line count
          ADD       ONE,TOTAL                          * record count
.
LDIS9999  RETURN
+
.**************************************************************************
.*                  DOCF0000                     Called by: CHKD0000      *
.*        Format doctors name                               DESC0000      *
.**************************************************************************
.
DOCF0000  MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          CALL      PACNAME
.
          RETURN
+
.**************************************************************************
.*                  PAGE0000                     Called by: ORPT0000      *
.*        Display page header lines                         DRPT0000      *
.**************************************************************************
.
PAGE0000  CALL      HEAD132
.
          PRINT     *45,"Operation Code From : ";
.
          MATCH     OCODE1,SP9
          GOTO      PAGE1100 IF EQUAL
.
          PRINT     OCODE1;
          GOTO      PAGE1150
.
PAGE1100  PRINT     START;
.
PAGE1150  PRINT     *74,ODESC1,*N,*45,"Operating Code To   : ";
.
          MATCH     OCODE2,ZZZ
          GOTO      PAGE1200 IF EQUAL 
.
          PRINT     OCODE2;
          GOTO      PAGE1250
.
PAGE1200  PRINT     FINISH;
.
PAGE1250  PRINT     *74,ODESC2,*N,*N,*45,"Doctor Code From    : ";
.
          MATCH     DCODE1,SP6
          GOTO      PAGE1300 IF EQUAL
.
          PRINT     DCODE1;
          GOTO      PAGE1350
.
PAGE1300  PRINT     START;
.
PAGE1350  PRINT     *74,DDESC1,*N,*45,"Doctor Code To      : ";
.
          MATCH     DCODE2,ZZZ
          GOTO      PAGE1400 IF EQUAL
.
          PRINT     DCODE2;
          GOTO      PAGE1450
.
PAGE1400  PRINT     FINISH;
.
PAGE1450  PRINT     *74,DDESC2,*N,*N,ASK,DASH58,DASH58,ASK
.
          BRANCH    COPTION,PAGE2000
.
          PRINT     PIPE," Doctor ",PIPE," Doctor Name",*53,PIPE:
                    " Operation ",PIPE," Description ":
                    *108,PIPE," Average ",PIPE
          GOTO      PAGE3000
.
PAGE2000  PRINT     PIPE," Operation ",PIPE," Description",*56,PIPE:
                    " Doctor ",PIPE," Doctor Name",*108,PIPE," Average ":
                    PIPE
.
PAGE3000  ADD       NINE,CLNO
.
PAGE9999  RETURN
+                 
.**************************************************************************
.*                  PEND0000                     Called by: ORPT0000      *
.*        Display the end of report details                 DRPT0000      *
.**************************************************************************
.
PEND0000  PRINT     ASK,DASH58,DASH58,ASK 
.
          BRANCH    COPTION,PEND1000
.
          PRINT     *N,"Number of doctors         : ",DOCTOTL
          GOTO      PEND2000 
.
PEND1000  PRINT     *N,"Number of operation codes : ",OPTOTL
.
PEND2000  PRINT     "Number of records         : ",TOTAL:
                    *N,*N,"*** END OF REPORT ***"
.
PEND9999  RETURN
+
.**************************************************************************
.
          INC       STD001IO
          INC       PATITMDS
          INC       PATITMRD
          INC       PATDOCDS
          INC       PATDOCKY
          INC       OPRAVEIO/INC
          INC       PATICOIO/INC
          INC       PATITMIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC

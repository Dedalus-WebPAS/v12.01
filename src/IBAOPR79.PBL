.******************************************************************************
.* System         : OPERATING ROOMS        (Release 6)                        *
.* Program        : IBAOPR79                                                  *
.* Desc           : Daily Theatre Statistics Report                           *
.******************************************************************************
.* Date           : 03/11/1993                                                *
.* Author         : Gabrielle                                                 *
.* Function       : This program will be used to produce reports on daily     *
.*                  theatre utilisation.                                      *
.* Modifications  :                                                           *
.******************************************************************************
.* V11.03.02 19/04/2023   Thanh T       TSK 0909393                           *
.*           Changed PROC1000 to use RSOPDEA6 instead of RSOPDEA1             * 
.* V11.03.01 20/03/2023  PJ Le Febour   TSK 0909393a                          *
.*           Amended KEY19 to KEY22 for theatre index changes                 *
.* V11.01.03 11/10/2021  Davin          TSK 0911422                           *
.*           Added date check when calculating number of sessions (SESS0000)  *
.* V11.01.02 27/04/2021  Thanh T        TSK 0895165                           *
.*           Recompiled for OPRARDFD changes                                  *
.* V11.01.01 16/02/2021  Tracey Nguyen  TSK 0888639                           *
.*           Recompiled for FD changes                                        *
.*           - oprpmbaf.OPRPMBA1 from KEY13 to KEY14                          *
.*           - oprpmbaf.OPRPMBA2 from KEY22 to KEY23                          *
.******************************************************************************
.*        V10.11.01 23/11/2017  Davin          TSK 0846231                    *
.*                  Recompiled for OPRPMBFD - added oppmrefn                  *
.*       V10.04.01  07/07/2014  J.Tan     CAR 261630                          *
.*                  Removed port number from temp file name                   *
.*       V10.00.02  22/10/2010  Ebon Clements CAR 231458                      *
.*                  Added surgical minutes column                             *
.*       V10.00.01  01/09/2010  Davin         CAR 228777                      *
.*                  Changed Operation Minutes calculation (oper0000) to use   *
.*                  opcnftim and opcnttim if repttype=1                       *
.*        V9.12.01  03/09/2009  Mike Laming   CAR 173405                      *
.*                  correct Recovery Time to use OPARTIRD if OPARTIRF is blank*
.*        V9.11.01  21/10/2008 Sandra Barcham CAR 181044                      *
.*                  Also exclude booking and pre-admitted patients            *
.*        V9.10.02  03/10/2008  Peter Vela    CAR 176961                      *
.*                  Added location filter                                     *
.*        V9.10.01  04/09/2008  Ebon Clements CAR 155301                      *
.*                  Fixed hospital filter and added to report header          *
.*        V9.08.01  18/01/2007  Peter Vela CAR 140351                         *
.*                  Ported from V6 to V9                                      *
.*        V6.07.02  06/06/2000  J.Tan                                         *
.*                  Recompiled for OPRMOPFD - added GST code                  *
.*        V6.07.01  30/05/2000 Caleb Sharman  SRF 2062                        *
.*                  Made totals variables form 6 instead of form 5            *
.*        V6.06.B01 22/01/1999 J.Tan   SRF 629070                             *
.*                  Added actual start and end date to session file           *
.*        V6.05.02  24/11/1998  Nicole Harrington                             *
.*                  Removed CCENTRY                                           *
.*        V6.05.01  20/10/1998 J Habasque                                     *
.*                  Changed to print centuries                                *
.*        V6.04.01  28/07/97 J.Tan    SRF 620853                              *
.*                  Mods to exclude cancelled session                         *
.*        V6.03.03  12/12/95 J.Tan    SRF 612799                              *
.*                  Fixed routine in calculating minutes                      *
.*                  Fixed bug in session minutes to check for doctor/date/time*
.*        V6.03.02  04/12/1995  Greg Horvat      SRF 612647                   *
.*                  Changed to open the tempfile & clear the save variables   *
.*                  each time the program starts processing again             *
.*        V6.03.01  17/11/1995  Steve Armstrong  SRF 612760                   *
.*                  Fixed bug in calculating operation minutes past midnight  *
.*        V6.03.B2  23/03/95  ROD                                             *
.*                  Theatre Workshop changes                                  *
.*                  V6.01.03 14/06/94 J.Tan    SRF 129851                     *
.*                  Fixed calculating Oper.minutes using the parameters       *
.*                  Mods session minutes to use the actual duration           *
.*                  V6.01.02 31/05/94 J.Tan    SRF 129654                     *
.*                  Fixed calculating Anaesthetic minutes using the parameter *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       OPRARDFD/INC             * Anaesthetic and recovery File
          INC       OPRDEAFD/INC             * Operation Detail File
          INC       OPRSRGFD/INC             * Operation Surgical Details File
          INC       OPRSESFD/INC             * Operation Session file
          INC       OPRPMBFD/INC             * Usage Operation codes
          INC       OPRCONFD/INC             * Control file
          INC       PMSVX1FD/INC             * Visit Extension File
          INC       PATHSPFD/INC             * Hospital File
          INC       IBACONFD/INC
          INC       PATCODFD/INC            
.
.         Variables
.
ATIME     FORM        5                      * Anaesthetic Minutes
.
CMDLINE   DIM         80                     * Command Line
CLINDATE  DIM         8                      * Clinic Date
CLINTIME  DIM         5                      * Clinic Time
.
DIM2A     DIM         2
DIM2B     DIM         2
DOCTOR    DIM         6                      * Doctor Code
.
ENDTIME   DIM         5                      * Last Operation Time for Session
.
FIRSTREC  FORM        1                      * Printing first record
FORM5     FORM        5
FORM5A    FORM        5
FORM5B    FORM        5
FROMDATE  DIM         8                      * Starting Date
.
GTIME     FORM        5                      * Surgical Minutes
.
HOSCODE   DIM       3
.
LOCAFILT  DIM         3                      * Location Filter
.
NPROC     FORM        5                      * Number of Procedures
NUMOPERS  FORM        6
NSESS     FORM        5                      * Number of Sessions
.
OTIME     FORM        5                      * Operation Minutes
.
PRCESSED  FORM        1                      * Data processed
.
REPNAME1  DIM         8                      * Report File Name
REPTTYPE  FORM        1                      * flag for report type
RTIME     FORM        5                      * Recovery Minutes
.
SAVECLIN  DIM         6                      * Save the current theatre clinic
SAVEDATE  DIM         8                      * Save the current theatre date
SAVEOPDT  DIM         8                      * Save the theatre date (oprdetaf)
SAVETIME  DIM         5                      * Save the current theatre time
SECTION   FORM        1                      * 0=Summary 1=Report
STARTIME  DIM         5                      * First Operation Time for Session
STIME     FORM        5                      * Session Minutes
SUMNAME1  DIM         8                      * Summary File Name
STRTIME   DIM         5                      * Earliest operation start time
SURGDSC1  DIM         25
SURGDSC2  DIM         25
.
TIME1     DIM         5                      * Start Time for calculating minute
TIME2     DIM         5                      * End Time for calculating minutes
TIME3     FORM        5                      * Calcualted minutes
TODATE    DIM         8                      * Ending Date
TODAY     DIM         8                      * Current Date
TOTCASES  FORM        6                      * Total Number of Cases
TOTANAE   FORM        6                      * Total Anaesthetic Minutes
TOTOPER   FORM        6                      * Total Operation Minutes
TOTRECV   FORM        6                      * Total Recovery Minutes
TOTNSESS  FORM        6                      * Total Number of Sessions
TOTSESS   FORM        6                      * Total Session Minutes
TOTPROCS  FORM        6                      * Total Number of Procedures
TOTSURG   FORM        6                      * Total Surgical Minutes
.
XXDATE    DIM         8                      * Theatre Date
XXDOCTOR  DIM         6                      * Doctor
XXCASES   FORM        5                      * Number of Cases
XXANAE    FORM        5                      * Anaesthetic Minutes
XXOPER    FORM        5                      * Operation Minutes
XXRECV    FORM        5                      * Recovery Minutes
XXNSESS   FORM        5                      * Number of Sessions
XXSESS    FORM        5                      * Session Minutes
XXPROCS   FORM        5                      * Number of Procedures
XXSURG    FORM        5                      * Surgical Minutes
.
OPERDU01  INIT        "Time Called For"
OPERDU02  INIT        "Time Arrived"
OPERDU03  INIT        "Anaesthetic Prep"
OPERDU04  INIT        "Anaesthetic Commenced"
OPERDU05  INIT        "Prep Start Time"
OPERDU06  INIT        "Surgery Start Time"
OPERDU07  INIT        "Surgery End Time"
OPERDU08  INIT        "Anaesthetic End"
OPERDU09  INIT        "Time of Dressing"
OPERDU10  INIT        "Recovery Front"
OPERDU11  INIT        "Recovery Back"
OPERDU12  INIT        "Recovery Day Procedure"
OPERDU13  INIT        "Ready to Depart"
OPERDU14  INIT        "Exit Theatre Complex"
.
OPERDSC1  DIM       25
OPERDSC2  DIM       25
.
ZED10     INIT        "zzzzzzzzzz"
.
.         Temporary Files
.
REPORT1   IFILE     SQL, FIXED=47, KEY="U1-8,9-14"
.
.NAME     TYPE      LENGTH   PHYSICAL   START      DESCRIPTION
.----     -----     ------   --------   -----      -------------------------
RPDATE    DIM         8         8         1        Theatre Date
RPDOCTOR  DIM         6         6         9        Doctor
RPCASE    FORM        5         4        15        Number of Cases
RPANAE    FORM        5         4        19        Anaesthetic Minutes
RPOPER    FORM        5         4        23        Operation Minutes
RPRECV    FORM        5         4        27        Recovery Minutes
RPNSESS   FORM        5         4        31        Number of Sessions
RPSESS    FORM        5         4        35        Session Minutes
RPPROCS   FORM        5         4        39        Number of Procedures
RPSURG    FORM        5         4        43        Surgical Minutes
.
.End of Record                           47
.
SUMMARY1  IFILE     SQL, FIXED=39, KEY="U1-6"
.
SMDOCTOR  DIM         6         6         1        Doctor
SMCASES   FORM        5         4         7        Number of Cases
SMANAE    FORM        5         4        11        Anaesthetic Minutes
SMOPER    FORM        5         4        15        Operation Minutes
SMRECV    FORM        5         4        19        Recovery Minutes
SMNSESS   FORM        5         4        23        Number of Sessions
SMSESS    FORM        5         4        27        Session Minutes
SMPROCS   FORM        5         4        31        Number of Procedures
SMSURG    FORM        5         4        35        Surgical Minutes
.                                       ----
.End of Record                           39 
.
PRGID     INIT      "IBAOPR79"
PRGNAM    INIT      "DAILY THEATRE STATISTICS"
VERSION   INIT      "V12.01.00"
+
.***************************************************************************
.*                            ML0000                                       *
.*              Mainline Code   ( Controlling Logic )                      *
.***************************************************************************
ML0000    CALL      INIT0000                * Display Headings & Open files 
          CALL      CLER0000                * Initialize global variables
.
ML0050    IF        IBCNMHOS=1
            DISPLAY   *P1:5,*EF,"Hospital Id: ";
            MOVE      TEN5,ECOL
            MOVE      FIVE,EVERT
            MOVE      SP3,CKYIDEF3
            MOVE      ONE,CKYIMAND
START       CALL      PATHSPKY
            BRANCH    EXIT,START,ML9999
            PACK      HOSCODE,KEY3,SP70
            DISPLAY   *P22:5,*EL,PTHSNAME;
          ENDIF
.
ML0100    CALL      KDAT0000                * Keyin Date Range
          BRANCH    EXIT,ML9999             * Spaces entered/Exit selected
.
          CALL      KLOC0000                * Keyin Location
          CALL      KREPT000                * keyin repttype to use parameters
.
          CALL      CONTQST
          BRANCH    CEXIT,ML1000,ML0100,ML9999
.
ML1000    CALL      PROC0000                * Process Data
.
          CALL      REPT0000                * Produce Report
          GOTO      ML0100
.
ML9999    CALL      DTMP0000                * Delete Temporary files 
          CHAIN     PGM
+
.*******************************************************************************
.*                            INIT0000                                         *
.*                    Initializtion and open files                             *
.*******************************************************************************
INIT0000  CALL      DISPHEAD 
          DISPLAY   *P56:24,"Opening";
.
          DISPLAY   *P64:24,"controlf"       * CONTROLF
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"oprpmbaf"       * OPRPMBFD
          OPEN      OPRPMBA1,"oprpmbaf"
.
          DISPLAY   *P64:24,"oprdetaf"       * OPRDEAFD
          OPEN      OPRDETA6,"oprdetaf"
.
          DISPLAY   *P64:24,"oprsrgaf"       * OPRSRGFD
          OPEN      OPRSRGA1,"oprsrgaf"
.
          DISPLAY   *P64:24,"oprsesaf"       * OPRSESFD
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRSESA7,"oprsesaf"
.
          DISPLAY   *P64:24,"oprardaf"       * OPRARDFD
          OPEN      OPRARDA1,"oprardaf"
.
          DISPLAY   *P64:24,"pmsvx1af"       * PMSVX1FD
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patcodes"       * PMSVX1FD
          OPEN      PATCODE1,"patcodes"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS=1
            DISPLAY   *P64:24,"pathspaf";
            OPEN      PATHSPA1,"pathspaf"
          ENDIF 
.
          READ      CONTROLF,FORTY;*115,OPCNNOOP
          READ      CONTROLF,FORTY2;*19,OPCNFOTM,OPCNTOTM,*47,OPCNANST:
                                    *235,OPCNFTIM,*237,OPCNTTIM
          READ      CONTROLF,HUND13;*11,OPCNSMFT,*13,OPCNSMTT
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,REPNAME1
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,SUMNAME1
.
          MOVE        SP5,STARTIME           * First Operation Time for session
          MOVE        SP5,ENDTIME            * Last Operation Time for session
.
INIT9999  RETURN
+
********************************************************************************
*                             CLER0000                                         *
*                            Clear global variables                            *
********************************************************************************
CLER0000  MOVE        SP8,SAVEDATE           * Initialize Date 
          MOVE        ZERO,TOTCASES          * Total Number of cases
          MOVE        ZERO,TOTANAE           * Total Anaesthetic Minutes
          MOVE        ZERO,TOTOPER           * Total Operation Minutes
          MOVE        ZERO,TOTRECV           * Total Recovery Minutes
          MOVE        ZERO,TOTNSESS          * Total Number of Sessions
          MOVE        ZERO,TOTSESS           * Total Sessions Minutes
          MOVE        ZERO,TOTPROCS          * Total Number of procedures
          MOVE        ZERO,TOTSURG           * Total Surgical Minutes
.
          MOVE        SP6,XXDOCTOR           * Doctor
          MOVE        ZERO,XXCASES           * Number of Cases
          MOVE        ZERO,XXANAE            * Anaesthetic Minutes
          MOVE        ZERO,XXOPER            * Operation Minutes
          MOVE        ZERO,XXRECV            * Recovery Minutes
          MOVE        ZERO,XXNSESS           * Number of Sessions
          MOVE        ZERO,XXSESS            * Session Minutes
          MOVE        ZERO,XXPROCS           * Number of Procedures
          MOVE        ZERO,XXSURG            * Surgical Minutes
.
          MOVE        ZERO,RPCASE            * Number of Cases
          MOVE        ZERO,RPANAE            * Anaesthetic Minutes
          MOVE        ZERO,RPOPER            * Operation Minutes
          MOVE        ZERO,RPRECV            * Recovery Minutes
          MOVE        ZERO,RPNSESS           * Number of Sessions
          MOVE        ZERO,RPSESS            * Session Minutes
          MOVE        ZERO,RPPROCS           * Number of Procedures
          MOVE        ZERO,RPSURG            * Surgical Minutes
.
          MOVE        ZERO,ATIME             * Anaesthetic Minutes
          MOVE        ZERO,OTIME             * Operation Minutes
          MOVE        ZERO,RTIME             * Recovery Minutes
          MOVE        ZERO,NSESS             * Number of Sessions
          MOVE        ZERO,NPROC             * Number of Procedures
          MOVE        ZERO,GTIME             * Surgical Minutes
.
CLER9999  RETURN
+
********************************************************************************
*                             KDAT0000                                         *
*                            Keyin Dates                                       *
********************************************************************************
KDAT0000  CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
          CALL      IDAT0000                 * Intialize 
          CALL      FDAT0000                 * Obtain Starting Discharge Date 
          BRANCH    EXIT,KDAT9999
.
          CALL      IDAT0000                 * Intialize 
          CALL      TDAT0000                 * Obtain Ending Discharge Date 
          BRANCH    EXIT,KDAT0000
.
.         Check that the From Discharge Date < To Dischage Date 
.
          MATCH     FROMDATE,TODATE
          IF @LESS
            DISPLAY   *P1:24,*B,*EL,"To date is less than From date. ";
            CALL      HITENTER
            GOTO      KDAT0000
          ENDIF
.
KDAT9999  RETURN
+
.****************************************************************************
.*                                IDAT0000           Called by: KDAT0000    * 
.*                         Initialize DATe variablies                       * 
.**************************************************************************** 
IDAT0000  MOVE      CCC,CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
.
IDAT9999  RETURN
.****************************************************************************
.*                                FDAT0000           Called by: KDAT0000    * 
.*                   Keyin From (Starting) Discharge Date                   *
.* :: FROMDATE                                                              *
.****************************************************************************
FDAT0000  KEYIN     *P1:4,*EF,"From Date : "
.
FDAT1000  MOVE      TEN3,CCOL
          MOVE      FOUR,CVERT 
.
          CALL      KEYDATE                  * Keyin Date Routine
          IF        OVRCD = ONE
            MOVE      TRUE,EXIT              * Nothing Input
          ELSE   
            MOVE      FALSE,EXIT           
            PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY 
            REP       " 0",FROMDATE	
.
            MATCH     FROMDATE,TODAY
            IF @LESS
              DISPLAY   *P1:24,*B,*EL,"Date must not be in the future. ";
              CALL      HITENTER
              CALL      IDAT0000
              GOTO      FDAT1000             * Error. Date in future
            ENDIF
          ENDIF
.
FDAT9999  RETURN
+
.****************************************************************************
.*                                TDAT0000              Called by: KDAT0000 * 
.*                   keyin TO (ending) discharge DATe                       *
.* :: TODATE                                                                *
.****************************************************************************
TDAT0000  KEYIN     *P1:5,"To Date   : "
.
          MOVE      TEN3,CCOL
          MOVE      FIVE,CVERT
.
TDAT1000  CALL      KEYDATE                  * Keyin Date Routine 
          IF        OVRCD = ONE
            MOVE      TRUE,EXIT              * Nothing Input
          ELSE
            MOVE      FALSE,EXIT         
            PACK      TODATE,CCENT,CYEAR,CMON,CDAY
            REP       " 0",TODATE
.
            MATCH     TODATE,TODAY
            IF @LESS
              DISPLAY   *P1:24,*B,*EL,"Date must not be in the future. ";
              CALL      HITENTER
              CALL      IDAT0000
              GOTO      TDAT1000             * Error. Date in future
            ENDIF
.
          ENDIF
.
TDAT9999  RETURN
+
.***************************************************************************
.*                            PROC0000                                     *
.*              Process and calculate the number of minutes.....           *
.***************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,"Processing Date : "; 
          MOVE      FALSE,PRCESSED
          MOVE      SP8,CLINDATE             * Clear save clinic date
          MOVE      SP8,CLINTIME             * Clear save clinic time
          MOVE      SP6,DOCTOR               * Clear save doctor
          CALL      CLER0000                 * Clear Variables 
          CALL      CTMP0000                 * Create the temporary files
          MOVE      ZERO,CPAGENO             * Start new page
          PACK      KEY26,FROMDATE,SP20 
          CALL      RSOPDEA6                 * Postional Read on oprdetaf
PROC1000  CALL      RKOPDEA6                 * Read next record
          BRANCH    OVRCD,PROC9900           * End of file 
.
          MATCH     OPDADATE,TODATE          * After end date ?
          GOTO      PROC9900 IF LESS         * No. Exit
.
          COMPARE   ZERO,OPDASTAT            * Booked ?
          GOTO      PROC1000 IF EQUAL        * Yes.
.
          COMPARE   ONE,OPDASTAT             * Pre-admitted ?
          GOTO      PROC1000 IF EQUAL        * Yes.
.
          COMPARE   THREE,OPDASTAT           * Cancelled ?
          GOTO      PROC1000 IF EQUAL        * Yes.
.
          IF        IBCNMHOS=1
            PACK      KEY22,OPDADATE,OPDATIME,OPDACLIN,OPDAHOSP,SP70
            CALL      RDOPSES7
            IF        OVRCD<>1
              MATCH     HOSCODE,OPSEHOSP
              GOTO      PROC1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MATCH     "All",LOCAFILT
          IF        !@EQUAL
            PACK      KEY22,OPDADATE,OPDATIME,OPDACLIN,OPDAHOSP,SP70
            CALL      RDOPSES7
            IF        OVRCD<>1
              MATCH     LOCAFILT,OPSEDGSI
              GOTO      PROC1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MOVE      TRUE,PRCESSED
.
          UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          DISPLAY   *P19:24,*EL,*V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          MATCH     SP6,DOCTOR               * Doctor exist ?
          GOTO      PROC1500 IF EQUAL
.
          MATCH     OPDACLIN,DOCTOR        * Same Doctor
          GOTO      PROC1300 IF NOT EQUAL
.
          MATCH     OPDATIME,CLINTIME      * Same time
          GOTO      PROC1300 IF NOT EQUAL
.
          MATCH     OPDADATE,CLINDATE      * Same date
          GOTO      PROC1500 IF EQUAL
.
PROC1300  CALL      SESM0000             * Calculate Session Minutes
          CALL      CLER0000             * Clear Variables 
. 
PROC1500  MOVE      OPDACLIN,DOCTOR          * Save Doctor
          MOVE      OPDADATE,CLINDATE        * Save Clinic Date
          MOVE      OPDATIME,CLINTIME        * Save Clinic Time
.
          CALL      CASE0000
.
          PACK      KEY10,OPDAUNIQ,SP70
          CALL      RDOPARD1
          IF        OVRCD=1
            MOVE      SP70,OPARTIRF
            MOVE      SP70,OPARTIRB
            MOVE      SP70,OPARTIRD
            MOVE      SP70,OPARTICU
            MOVE      SP70,OPARTEXP
            MOVE      SP70,OPARTETC
            MOVE      SP70,OPARANAS
            MOVE      SP70,OPARANAE
          ENDIF          
.
          CALL      SETR0000                 * Find first time in recovery
.     
          CALL      RECV0000                 * Calculate Recovery Minutes
.
.         Loop through oprdec file to include multiple team operation time
.
          MOVE      SP10,STRTIME             * Init. earliest oper.start time
          PACK      KEY11,OPDAUNIQ,SP5
          CALL      RSOPSRG1                 * Positional Read on oprdetcf
PROC2000  CALL      RKOPSRG1                 * Read Next Record
          BRANCH    OVRCD,PROC9000           * End of file; exit
.
          MATCH     OPDAUNIQ,OPSGUNID        * Still same uniqued id ?
          GOTO      PROC9000 IF NOT EQUAL    * No.
.
.         MATCH     "1",OPSGTMNO             * 1st team
.         IF        @EQUAL
.           CALL      CASE0000               * Number of Cases with usage
.         ENDIF
.
          CALL      OPRNOOPR                 * Calc no of procedures 
          ADD       NUMOPERS,NPROC           * returned from OPRNOOPR.PBL
.
          CALL      OPER0000                 * Calculate Operation Minutes
          CALL      SURG0000                 * Calculate Surgical Minutes
          CALL      SLOP0000                 * Save earliest oper.time starts
          GOTO      PROC2000                 * Next record
.
PROC9000  CALL      ANAE0000                 * Calculate Anaesthetic Minutes
          CALL      SESS0000                 * Number of Sessions
          CALL      WTMP0000                 * Write to Temporary Report File
.
          CALL      CLER0000                 * Clear Variables 
          MOVE      OPDACLIN,SAVECLIN        * Save clinic
          MOVE      OPDADATE,SAVEOPDT        * Save clinic date
          MOVE      OPDATIME,SAVETIME        * Save clinic time
          GOTO      PROC1000                 * Read next record
.
PROC9900  IF        PRCESSED = TRUE 
            CALL      SESM0000                 * Calculate Session Minutes
          ENDIF
PROC9999  RETURN
+
.***************************************************************************
.*                            CASE0000                                     *
.*                   Calculate the Number of Cases                         *
.***************************************************************************
CASE0000  PACK      KEY14,CLINDATE,DOCTOR    * Pack key with date & surgeon
          CALL      RDREPRT1                 * Record Exist
          IF        OVRCD = ONE
            MOVE      CLINDATE,RPDATE        * Set up Date
            MOVE      DOCTOR,RPDOCTOR        * Set up Doctor
            MOVE      ONE,RPCASE             * No. Initialize to 1
            CALL      WRREPRT1               * Write new record
          ELSE
            ADD       ONE,RPCASE             * Yes. Add 1
            CALL      UPREPRT1               * Update record
          ENDIF
.
CASE9999  RETURN
+
.***************************************************************************
.*                            SETR0000                                     *
.*  Populate all recovery variables with the first time in recovery        *
.*  First check recovery front, then back, then day, then ICU, then        *
.*  time patient died.                                                     *
.***************************************************************************
SETR0000  MATCH     SP70,OPARTIRF                * Recovery front
          IF        @EQUAL
            MOVE      OPARTIRB,OPARTIRF
          ENDIF
          MATCH     SP70,OPARTIRF
          IF        @EQUAL
            MOVE      OPARTIRD,OPARTIRF
          ENDIF
          MATCH     SP70,OPARTIRF
          IF        @EQUAL
            MOVE      OPARTICU,OPARTIRF
          ENDIF
          MATCH     SP70,OPARTIRF
          IF        @EQUAL
            MOVE      OPARTEXP,OPARTIRF
          ENDIF
.
          MATCH     SP70,OPARTIRB                * Recovery back
          IF        @EQUAL
            MOVE      OPARTIRD,OPARTIRB
          ENDIF
          MATCH     SP70,OPARTIRB
          IF        @EQUAL
            MOVE      OPARTICU,OPARTIRB
          ENDIF
          MATCH     SP70,OPARTIRB
          IF        @EQUAL
            MOVE      OPARTEXP,OPARTIRB
          ENDIF
.
          MATCH     SP70,OPARTIRD                * Recovery Day
          IF        @EQUAL
            MOVE      OPARTICU,OPARTIRD
          ENDIF
          MATCH     SP70,OPARTIRD
          IF        @EQUAL
            MOVE      OPARTEXP,OPARTIRD
          ENDIF
.
          MATCH     SP70,OPARTICU                * ICU
          IF        @EQUAL
            MOVE      OPARTEXP,OPARTIRD
          ENDIF
.
SETR9999  RETURN
+
.***************************************************************************
.*                            RECV0000                                     *
.*                   Calculate the Recovery Minutes                        *
.*      Recovery mins = TO time of operation --> End recovery time         *
.***************************************************************************
RECV0000  MOVE      SP10,TIME1
.                                           * To "Recovery - Front"
          MATCH     SP8,OPARTIRF                              * start *I-173405
          IF        @EQUAL
            MATCH     SP8,OPARTIRD
            IF        !@EQUAL
              MOVE      OPARTIRD,OPARTIRF
            ENDIF
          ENDIF                                                 * end *I-173405
.
          MOVE      OPARTIRF,TIME1
          MATCH     SP5,TIME1
          IF        @EQUAL
            MOVE      ZERO,RTIME                  * Recovery Time does not exist
            GOTO      RECV9999
          ENDIF
. calculate end time
          MOVE      SP10,TIME2
          MOVE      OPARTETC,TIME2
          MATCH     SP5,TIME2
          IF        @EQUAL
            MOVE      ZERO,RTIME                  * Recovery Time does not exist
            GOTO      RECV9999
          ENDIF
.
          CALL      CALC0000                      * Calculate Minutes
          MOVE      TIME3,RTIME                   * Recovery Minutes
.
RECV9999  RETURN
+
.*******************************************************************************
.*                            ANAE0000                                         *
.*                   Calculate the Anaesthetic Minutes                         *
.*******************************************************************************
ANAE0000  MOVE      SP10,TIME1
          MOVE      OPARANAS,TIME1
          MATCH     SP5,TIME1
          IF        @EQUAL
            MOVE      ZERO,ATIME             * Anaesthetic Time does not exist
          ELSE
            MOVE      SP10,TIME2
.
            MATCH     SP70,OPARANAE
            GOTO      ANAE0100 IF EQUAL
            MOVE      OPARANAE,TIME2
            GOTO      ANAE0500
.
ANAE0100    MATCH     SP70,STRTIME
            GOTO      ANAE0200 IF EQUAL
            MOVE      STRTIME,TIME2
            GOTO      ANAE0500
.
ANAE0200    MOVE      OPARANAS,TIME2
.
ANAE0500    CALL      CALC0000               * Calculate Minutes
            MOVE      TIME3,ATIME            * Anaesthetic Minutes
          ENDIF
.
ANAE9999  RETURN
+
.***************************************************************************
.*                            OPER0000                                     *
.*                   Calculate the Operation Minutes                       *
.***************************************************************************
OPER0000  MOVE      OPSGTISS,TIME1
.
          MATCH     SP70,OPSGTISE
          GOTO      OPER0100 IF EQUAL
          MOVE      OPSGTISE,TIME2
          GOTO      OPER0500
.
OPER0100  MOVE      OPSGTISS,TIME2
.>>>>
OPER0500  IF        REPTTYPE=1
.                                           * load Start time from selected
            LOAD      TIME1,OPCNFTIM,OPARTCAL,OPARATIM,OPARANAP,OPARANAS:
                                     OPSGTIPS,OPSGTISS,OPSGTISE,OPARANAE:
                                     OPSGTIDR,OPARTIRF,OPARTIRB,OPARTIRD:
                                     OPARTIDP,OPARTETC
.                                           * load End time from selected
            LOAD      TIME2,OPCNTTIM,OPARTCAL,OPARATIM,OPARANAP,OPARANAS:
                                     OPSGTIPS,OPSGTISS,OPSGTISE,OPARANAE:
                                     OPSGTIDR,OPARTIRF,OPARTIRB,OPARTIRD:
                                     OPARTIDP,OPARTETC
.
            MATCH     SP70,TIME1
            GOTO      OPER9999 IF EQUAL
.
            MATCH     SP70,TIME2
            GOTO      OPER9999 IF EQUAL
.
          ENDIF
.>>>>
.
          CALL      CALC0000                 * Calculate Minutes
          ADD       TIME3,OTIME              * Operation Minutes
.
.         CALL      SSSE0000                 * Calc. First Start/Last End Time
.
OPER9999  RETURN
+
.***************************************************************************
.*                            SLOP0000                                     *
.*                   Save the earliest operation time starts               *
.***************************************************************************
SLOP0000  MOVE      OPSGTISS,TIME1
.
          MATCH     SP5,STRTIME              * First time ?
          GOTO      SLOP1000 IF EQUAL
.
          MATCH     SP5,TIME1
          GOTO      SLOP9999 IF EQUAL
.
          MATCH     STRTIME,TIME1            * Is the time before previous time?
          GOTO      SLOP9999 IF NOT LESS
.
SLOP1000  MOVE      TIME1,STRTIME            * Save the operation time
SLOP9999  RETURN
+
.***************************************************************************
.*                            SSSE0000                                     *
.*  Calculate the session first operation time and last operation end time *
.***************************************************************************
SSSE0000  MATCH     SP5,STARTIME             * Initialize Start Time
          IF        @EQUAL
            MOVE      OPSGTISS,STARTIME
          ENDIF
          
          MATCH     OPSGTISS,STARTIME        * Start Time
          IF        !@LESS
            MOVE      OPSGTISS,STARTIME
          ENDIF
.
          MATCH     SP5,ENDTIME              * Initialize End Time
          IF        @EQUAL
            MOVE      OPSGTISE,ENDTIME
          ENDIF
.
          MATCH     OPSGTISE,ENDTIME         * End Time
          IF        @LESS
            MOVE      OPSGTISE,ENDTIME
          ENDIF
.
SSSE9999  RETURN
+
.***************************************************************************
.*                            SESM0000                                     *
.*                   Calculate the number of session minutes               *
.***************************************************************************
SESM0000  
........  MOVE      STARTIME,TIME1           * Start Time
........  MOVE      ENDTIME,TIME2            * End Time
........  CALL      CALC0000                 * Calculate Minutes
........  MOVE      TIME3,STIME              * Operation Minutes
.
          MOVE      ZERO,STIME               * Initialise Operation Minutes
          PACK      KEY22,CLINDATE,CLINTIME,DOCTOR      * Pack session key
          CALL      RDOPSES7                 * Read session file
.
          BRANCH    OVRCD,SESM1000           * Not found
          MOVE      OPSEACTD,STIME           * Use the actual duration
.
SESM1000  PACK      KEY14,CLINDATE,DOCTOR    * Pack key with date & surgeon
          CALL      RDREPRT1                 * Record Exist
          IF        OVRCD = ONE
            MOVE      CLINDATE,RPDATE        * Set up Date
            MOVE      DOCTOR,RPDOCTOR        * Set up Doctor
            MOVE      STIME,RPSESS           * Initialize Session Min
            CALL      WRREPRT1               * Write new record
          ELSE
            ADD       STIME,RPSESS           * Add Session Minutes
            CALL      UPREPRT1               * Update record
          ENDIF
.
          MOVE      SP5,STARTIME             * Clear Start Time
          MOVE      SP5,ENDTIME              * Clear End Time
          MOVE      ZERO,RPSESS              * Clear Session Minutes
.
SESM9999  RETURN
+
.*******************************************************************************
.*                            CALC0000                                         *
.*                   Calculate the Actual Minutes                              *
.* Parameters :: TIME1 : Start Time                                            *
.*               TIME2 : End Time                                              *
.* Retures    :: TIME3 : Calculated Mintues                                    *
.*******************************************************************************
CALC0000  MOVE      ZERO,TIME3                * Initialize Calculated Minutes
          MATCH     TIME1,TIME2               * Same Time ?
          GOTO      CALC9999 IF EQUAL         * Yes.
.
.         Format Time 1
.
          REPLACE   " 0",TIME1               * Replace " " with "0"
          REPLACE   ": ",TIME1               * Replace ":" with " "
          SQUEEZE   TIME1                    * Remove spaces
          UNPACK    TIME1,DIM2A,DIM2B
          MOVE      DIM2B,FORM5A
          MOVE      DIM2A,FORM5               * convert hours to minutes
          MULT      "60",FORM5
          ADD       FORM5,FORM5A              * start time in minutes
.
.         Format Time 2
.
          REPLACE   " 0",TIME2               * Replace " " with "0"
          REPLACE   ": ",TIME2               * Replace ":" with " "
          SQUEEZE   TIME2                    * Remove spaces
          UNPACK    TIME2,DIM2A,DIM2B
          MOVE      DIM2B,FORM5B
          MOVE      DIM2A,FORM5               * convert hours to minutes
          MULT      "60",FORM5
          ADD       FORM5,FORM5B              * end time in minutes
.
.         SRF 612760 - if time2 is less than time 1, we have gone past midnight,
.         so add 2400 to time2
.
          COMPARE   FORM5A,FORM5B
          IF        @LESS
            ADD     "1440",FORM5B
          ENDIF
.
.         Calculate  Minutes 
.
          SUB       FORM5A,FORM5B            * Sub Anae.Time from Op. Start Time
          MOVE      FORM5B,TIME3
.
CALC9999  RETURN
+
.***************************************************************************
.*                            WTMP0000                                     *
.*                   Write the values to Report Temporary files            *
.***************************************************************************
WTMP0000  PACK      KEY14,CLINDATE,DOCTOR    * Pack key with date & surgeon
          CALL      RDREPRT1                 * Record Exist
          IF        OVRCD = ONE
            MOVE      CLINDATE,RPDATE        * Set up Date
            MOVE      DOCTOR,RPDOCTOR        * Set up Doctor
            MOVE      ATIME,RPANAE           * Initialize Anaesthetic Min
            MOVE      RTIME,RPRECV           * Initialize Recovery Min
            MOVE      OTIME,RPOPER           * Initialize Operation Min
            MOVE      NPROC,RPPROCS          * Initialize No. of Procedures
            MOVE      NSESS,RPNSESS          * Initialize Number of sessions
            MOVE      GTIME,RPSURG           * Initialize Surgical Min
            CALL      WRREPRT1               * Write new record
          ELSE
            ADD       ATIME,RPANAE           * Add Anaesthetic Min
            ADD       RTIME,RPRECV           * Add Recovery Min
            ADD       OTIME,RPOPER           * Add Operation Min
            ADD       NPROC,RPPROCS          * Add No. of Procedures
            ADD       NSESS,RPNSESS          * Add Number of sessions
            ADD       GTIME,RPSURG           * Add Surgical Minutes
            CALL      UPREPRT1               * Update record
          ENDIF
.
WTMP9999  RETURN
+
.***************************************************************************
.*                            SESS0000                                     *
.*                   Calculate the number of sessions                      *
.***************************************************************************
SESS0000  MATCH     OPDACLIN,SAVECLIN        * Same Doctor ?
          IF        !@EQUAL
            MOVE      ONE,NSESS              * No
            GOTO      SESS9999
          ENDIF
.
          MATCH     OPDATIME,SAVETIME        * Different Times ?
          IF        !@EQUAL
            ADD       ONE,NSESS              * Yes. Add another Session
            GOTO      SESS9999
          ENDIF
.
          MATCH     OPDADATE,SAVEOPDT        * Different Date ?
          IF        !@EQUAL
            ADD       ONE,NSESS              * Yes. Add another Session
          ENDIF
.
SESS9999  RETURN
+
.***************************************************************************
.*                            REPT0000                                     *
.*              Process and calculate the number of minutes.....           *
.***************************************************************************
REPT0000  MOVE      ONE,SECTION              * Printing report section
          MOVE      TRUE,FIRSTREC            * Initailize first record
          CALL      HEAD0000                 * Print Header
          PACK      KEY14,SP10,SP4           * Set up Key
          CALL      RSREPRT1                 * Positional Read
REPT1000  CALL      RKREPRT1                 * Read next record
          BRANCH    OVRCD,REPT9000           * End of file; print summary
.
          COMPARE   "55",CLNO                * Less than 55 lines ?
          CALL      HEAD0000 IF NOT LESS     * No. Print Header
. 
          MATCH     RPDATE,SAVEDATE          * Date Same as previous date
          IF        !@EQUAL
            MATCH     SP8,SAVEDATE           * First date on page.
            IF        !@EQUAL
              CALL      TOTL0000             * No, obtain totals for this date.
              MOVE      TRUE,FIRSTREC
            ENDIF
          ENDIF
          MOVE      RPDATE,XXDATE            * Set up Date for printing
          MOVE      RPDOCTOR,XXDOCTOR        * Set up Doctor for printing
          MOVE      RPCASE,XXCASES           * Set up No. of Cases for printing
          MOVE      RPANAE,XXANAE            * Set up Anaesth Mins for printing
          MOVE      RPOPER,XXOPER            * Set up Oper. Mins. for printing
          MOVE      RPRECV,XXRECV            * Set up Recovery Mins for printing
          MOVE      RPNSESS,XXNSESS          * Set up No. of Sess. for printing
          MOVE      RPSESS,XXSESS            * Set up Session Mins. for printing
          MOVE      RPPROCS,XXPROCS          * Set up No. of Procs for printing
          MOVE      RPSURG,XXSURG            * Set up Surgical Mins for printing
          CALL      PRNT0000                 * Print each record (line)
          CALL      WRTS0000                 * Write record for summary
.
          MOVE      RPDATE,SAVEDATE          * Save the Date
          GOTO      REPT1000                 * Read Next Record
.
REPT9000  CALL      TOTL0000 
          CALL      SUMM0000                 * Summary
.
REPT9999  RETURN
+
.*******************************************************************************
.*                               HEAD0000                                      *
.*                      Print the Header of the Report                         *
.*******************************************************************************
HEAD0000  CALL      HEAD132
.
.         Clear date to ensure it is printed on the new page
.
.         MOVE      SP8,SAVEDATE             
.
.         Print the From and to Dates
.
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital : ",PTHSNAME
            ADD       "1",CLNO
          ENDIF
.
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          PRINT     *40,"From : ",CDAY,SLASH,CMON,SLASH,CCENT,CYEAR;
          UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          PRINT     *58,"To : ",CDAY,SLASH,CMON,SLASH,CCENT,CYEAR,*N
          ADD       ONE,CLNO
.
          MATCH     "All",LOCAFILT
          IF        @EQUAL
            PRINT     *40,"Location : ",LOCAFILT
          ELSE
            PACK      KEY5,ANSY,ANSD,LOCAFILT
            CALL      RDCODE1
            IF        OVRCD=0
              PRINT     *40,"Location : ",LOCAFILT," / ",TDESC
            ELSE
              PRINT     *40,"Location : ",LOCAFILT
            ENDIF
          ENDIF
.
          CALL      UND132
.
          PRINT     *1,"|":                  * Line 1 of header
                    *12,"|":
                    *20,"| No. of":
                    *29,"| No. of":
                    *38,"| Anaest":
                    *47,"| Surg ":
                    *55,"|Opern":
                    *63,"|Recov":
                    *72,"| No. of":
                    *80,"| Sessn":
                    *89,"|",*132,"|"
.
          PRINT     *1,"|",*4,"Date":        * Line 2 of header
                    *12,"|",*13,"Doctor":
                    *20,"| Cases":
                    *29,"| Procs":
                    *38,"| Mins.":
                    *47,"| Mins.":
                    *55,"| Mins.":
                    *63,"| Mins.":
                    *72,"| Sessns":
                    *80,"| Mins.":
                    *89,"|",*132,"|"

          ADD       TWO,CLNO
          CALL      UND132
.
HEAD9999  RETURN
+
.*******************************************************************************
.*                               PRNT0000                                      *
.*                      Print each record (line)                               *
.*******************************************************************************
PRNT0000  COMPARE   TRUE,FIRSTREC            * First Record for this Date
          GOTO      PRNT1000 IF EQUAL        * Yes. Print date 
          PRINT     *1,"|";                  * No. Just print record
          GOTO      PRNT9000
.
PRNT1000  COMPARE   ONE,SECTION              * Printing report layout?
          GOTO      PRNT3000 IF NOT EQUAL    * No. 
.
PRNT2000  UNPACK    XXDATE,CCENT,CYEAR,CMON,CDAY  * Print first line of date
          PRINT     *1 ,"|",*2,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR;
          GOTO      PRNT9000
.
PRNT3000  PRINT     *1 ,"|",*3,"Summary";    * Printing Summary
.
PRNT9000  PRINT     *12,"| ",XXDOCTOR:    * Print record
                    *20,"|  ",XXCASES:
                    *29,"|  ",XXPROCS:
                    *38,"|  ",XXANAE:
                    *47,"| ",XXSURG:
                    *55,"| ",XXOPER:
                    *63,"| ",XXRECV:
                    *72,"|  ",XXNSESS:
                    *80,"| ",XXSESS,*88,"|",*132,"|"
.
          ADD       ONE,CLNO
          MOVE      FALSE,FIRSTREC
.
.         Add the totals
.
          ADD       XXCASES,TOTCASES         * Total Number of Cases for date
          ADD       XXANAE,TOTANAE           * Total Anaesthetic Mins. for date
          ADD       XXOPER,TOTOPER           * Total Operation Mins. for date
          ADD       XXRECV,TOTRECV           * Total Recovery Mins. for date
          ADD       XXNSESS,TOTNSESS         * Total Number of Sessions for date
          ADD       XXSESS,TOTSESS           * Total Session Mins. for date
          ADD       XXPROCS,TOTPROCS         * Total No. of Procedures
          ADD       XXSURG,TOTSURG           * Total Surgical Mins. for date
.
PRNT9999  RETURN
+
.*******************************************************************************
.*                               TOTL0000                                      *
.*                      Print and calculate the totals for each date           *
.*******************************************************************************
TOTL0000  PRINT     *1,"|",*12,"|------------------------------------------":
                               "-------------------------------------------":
                               "----------------------------------|"
          PRINT     *1,"|":
                    *12,"|",*14,"Total":
                    *20,"|",*22,TOTCASES:
                    *29,"|",*31,TOTPROCS:
                    *38,"|",*40,TOTANAE:

                    *47,"|",*48,TOTSURG:

                    *55,"|",*56,TOTOPER:
                    *63,"|",*64,TOTRECV:
                    *72,"|",*74,TOTNSESS:
                    *80,"|",*81,TOTSESS,*88,"|",*132,"|"
          ADD       TWO,CLNO
          CALL      UND132
.
          MOVE      ZERO,TOTCASES            * Clear Total Number of cases
          MOVE      ZERO,TOTANAE             *       Total Anaesthetic Minutes
          MOVE      ZERO,TOTOPER             *       Total Operation Minutes
          MOVE      ZERO,TOTRECV             *       Total Recovery Minutes
          MOVE      ZERO,TOTNSESS            *       Total Number of Sessions
          MOVE      ZERO,TOTSESS             *       Total Sessions Minutes
          MOVE      ZERO,TOTPROCS            *       Total No. of Procedures
          MOVE      ZERO,TOTSURG             *       Total Surgical Minutes
.
TOTL9999  RETURN
+
.*******************************************************************************
.*                               WRTS0000                                      *
.*                      Write the record to print summary                      *
.*******************************************************************************
WRTS0000  PACK      KEY6,RPDOCTOR            * Set up Doctor for read 
          CALL      RDSUMRY1                 * Record Exist ?
          IF        OVRCD = ONE 
            MOVE      RPDOCTOR,SMDOCTOR      * No. Write new record
            MOVE      RPCASE,SMCASES
            MOVE      RPANAE,SMANAE
            MOVE      RPOPER,SMOPER
            MOVE      RPRECV,SMRECV
            MOVE      RPNSESS,SMNSESS
            MOVE      RPSESS,SMSESS
            MOVE      RPPROCS,SMPROCS
            MOVE      RPSURG,SMSURG
            CALL      WRSUMRY1
          ELSE
            MOVE      RPDOCTOR,SMDOCTOR      * Yes. Update record
            ADD       RPCASE,SMCASES
            ADD       RPANAE,SMANAE
            ADD       RPOPER,SMOPER
            ADD       RPRECV,SMRECV
            ADD       RPNSESS,SMNSESS
            ADD       RPSESS,SMSESS
            ADD       RPPROCS,SMPROCS
            ADD       RPSURG,SMSURG
            CALL      UPSUMRY1
          ENDIF
.
WRTS9999  RETURN
+
.*******************************************************************************
.*                               SUMM0000                                      *
.*                      Print the summary for the report                       *
.*******************************************************************************
SUMM0000  MOVE      ZERO,SECTION             * Printing summary section
          MOVE      ZERO,TOTCASES            * Clear Total Number of cases
          MOVE      ZERO,TOTANAE             *       Total Anaesthetic Minutes
          MOVE      ZERO,TOTOPER             *       Total Operation Minutes
          MOVE      ZERO,TOTRECV             *       Total Recovery Minutes
          MOVE      ZERO,TOTNSESS            *       Total Number of Sessions
          MOVE      ZERO,TOTSESS             *       Total Sessions Minutes
          MOVE      ZERO,TOTPROCS            *       Total No. of Procedures
          MOVE      ZERO,TOTSURG             *       Total Surgical Minutes
.
          COMPARE   FALSE,PRCESSED           * Data processed ?
          GOTO      SUMM9000 IF EQUAL        * No.
          MOVE      TRUE,FIRSTREC            * Print Header
.
          PACK      KEY6,SP6                 * Set up Key
          CALL      RSSUMRY1                 * Yes. Positional Read
SUMM1000  CALL      RKSUMRY1                 * Read next record
          BRANCH    OVRCD,SUMM9000           * End of File ; print totals
.
          COMPARE   "55",CLNO                * Less than 55 lines ?
          CALL      HEAD0000 IF NOT LESS     * No. Printer Header
.
          MOVE      SMDOCTOR,XXDOCTOR        * Set up Doctor for printing
          MOVE      SMCASES,XXCASES          * Set up No. of Cases for printing
          MOVE      SMANAE,XXANAE            * Set up Anaesth Mins for printing
          MOVE      SMOPER,XXOPER            * Set up Oper. Mins. for printing
          MOVE      SMRECV,XXRECV            * Set up Recovery Mins for printing
          MOVE      SMNSESS,XXNSESS          * Set up No. of Sess. for printing
          MOVE      SMSESS,XXSESS            * Set up Session Mins. for printing
          MOVE      SMPROCS,XXPROCS          * Set up No. of Procedures
          MOVE      SMSURG,XXSURG            * Set up Surgical Mins for printing
          CALL      PRNT0000                 * Print record
          GOTO      SUMM1000                 * Read next record
.
SUMM9000  CALL      TOTL0000
SUMM9100  COMPARE   "55",CLNO                * Less than 55 lines ?
          CALL      HEAD0000 IF NOT LESS     * No. Printer Header
.
          PRINT     *N,*1,"**** End of Report ****",*N:
                    *N,*1,"Number of Cases   : Number of cases (excludes ":
                          "Bookings, Pre-Admissions and ":
                    *N,*1,"                    Cancellations)":
                    *N,*1,"Number of Procs   : Number of MBS Procedure codes":
                    *N,*1,"Anaesth. Minutes  : Anaesth. Pre-Operative time":
                             " to the time Operation starts";
.
          IF        REPTTYPE=1
.                                           * load Start time from selected
            LOAD      OPERDSC1,OPCNFTIM,OPERDU01,OPERDU02,OPERDU03,OPERDU04:
                                     OPERDU05,OPERDU06,OPERDU07,OPERDU08:
                                     OPERDU09,OPERDU10,OPERDU11,OPERDU12:
                                     OPERDU13,OPERDU14
.                                           * load End time from selected
            LOAD      OPERDSC2,OPCNTTIM,OPERDU01,OPERDU02,OPERDU03,OPERDU04:
                                     OPERDU05,OPERDU06,OPERDU07,OPERDU08:
                                     OPERDU09,OPERDU10,OPERDU11,OPERDU12:
                                     OPERDU13,OPERDU14
.
            PRINT     *N,*1,"Operation Minutes : Operation duration is ":
                            "the difference between ",OPERDSC1:
                      *N,*21,"and ",OPERDSC2;
          ELSE
            PRINT     *N,*1,"Operation Minutes : The Operation duration ":
                            "includes multiple teams";
          ENDIF
.
          PRINT     *N,*1,"Recovery Minutes  : Post-Operative time one to two":
                    *N,*1,"No of Sessions    : Booked and Extra":
                    *N,*1,"Session Minutes   : The Actual Session Duration as":
                          " per Session Usage"
.
.                                           * Surgical Start time from selected
            MOVE      ZERO,F2
            MOVE      SP70,SURGDSC1
            MOVE      OPCNSMFT,F2
            LOAD      SURGDSC1,F2,OPERDU01,OPERDU02,OPERDU03,OPERDU04:
                                     OPERDU05,OPERDU06,OPERDU07,OPERDU08:
                                     OPERDU09,OPERDU10,OPERDU11,OPERDU12:
                                     OPERDU13,OPERDU14
.
.                                           * Surgical End time from selected
            MOVE      ZERO,F2
            MOVE      SP70,SURGDSC2
            MOVE      OPCNSMTT,F2
            LOAD      SURGDSC2,F2,OPERDU01,OPERDU02,OPERDU03,OPERDU04:
                                     OPERDU05,OPERDU06,OPERDU07,OPERDU08:
                                     OPERDU09,OPERDU10,OPERDU11,OPERDU12:
                                     OPERDU13,OPERDU14
.
            PRINT     *1,"Surgical Minutes  : Surgical minutes is ":
                            "the difference between ",SURGDSC1:
                      *N,*21,"and ",SURGDSC2;
.
SUMM9999  RETURN
+
.*******************************************************************************
.*                              CTMP0000                  CALLED BY :          *
.*                         Create temporary files                              *
.*******************************************************************************
CTMP0000  CALL      DTMP0000                 * Ensure Old Temp. file is removed
.
.         Build & Open Report File
.
          CLEAR     CMDLINE                  * Build Command Line
          APPEND    "isbuild ",CMDLINE
          APPEND    REPNAME1,CMDLINE
.
          APPEND    " 47 U1-8,9-14",CMDLINE
          RESET     CMDLINE
          STRIP     CMDLINE
.
          EXECUTE   CMDLINE,TASKID           
          OPEN      REPORT1,REPNAME1        * Open Temporary File
.
.         Build & Open Summary File
.
          CLEAR     CMDLINE                  * Build Command Line
          APPEND    "isbuild ",CMDLINE
          APPEND    SUMNAME1,CMDLINE
.
          APPEND    " 39 U1-6",CMDLINE
          RESET     CMDLINE
          STRIP     CMDLINE
.
          EXECUTE   CMDLINE,TASKID           
          OPEN      SUMMARY1,SUMNAME1        * Open Temporary File
CTMP9999  RETURN
+
.*******************************************************************************
.*                              DTMP0000                  CALLED BY :          *
.*                     Delete temporay files                                   *
.*******************************************************************************
DTMP0000  CLOSE     REPORT1                  * Remove Report File
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    REPNAME1,CMDLINE
          RESET     CMDLINE
          STRIP     CMDLINE
          EXECUTE   CMDLINE,TASKID           
.
          CLOSE     SUMMARY1                 * Remove Summary File
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    SUMNAME1,CMDLINE
          RESET     CMDLINE
          STRIP     CMDLINE
          EXECUTE   CMDLINE,TASKID           
.
DTMP9999  RETURN
+
.*********************************************************************
.*                  KLOC0000                    Called by : ML0000   *
.*        Enter the Location                                         *
.*********************************************************************
.
KLOC0000  MOVE      SP70,LOCAFILT
          DISPLAY   *P1:8,*EF:
                    *P1:8,"Location : "
.
          KEYIN     *P12:8,LOCAFILT
          MATCH     SP70,LOCAFILT   
          IF        @EQUAL | @EOS
            MOVE      "All",LOCAFILT
          ENDIF
          PACK      LOCAFILT,LOCAFILT,SP70
          DISPLAY   *P12:8,LOCAFILT
.
KLOC9999  RETURN
+
.*********************************************************************
.*                  KREPT000                    Called by : ML0000   *
.*        Enter the Report Type (1=use opr parameters for op. mins)  *
.*********************************************************************
KREPT000  MOVE      ZERO,REPTTYPE
          DISPLAY   *P1:10,*EF:
                    *P1:10,"Report Type (1=use opr parameters): "
          KEYIN     *P40:10,REPTTYPE
          IF        REPTTYPE=1
            GOTO      KREPT999
          ELSE
            MOVE      ZERO,REPTTYPE
          ENDIF
KREPT999  RETURN
+
.***************************************************************************
.*                            SURG0000                                     *
.*                   Calculate the Surgical Minutes                        *
.***************************************************************************
SURG0000  MOVE      ZERO,F2
          MOVE      OPCNSMFT,F2
          MOVE      SP70,TIME1
.
          LOAD      TIME1,F2,OPARTCAL,OPARATIM,OPARANAP,OPARANAS:
                             OPSGTIPS,OPSGTISS,OPSGTISE,OPARANAE:
                             OPSGTIDR,OPARTIRF,OPARTIRB,OPARTIRD:
                             OPARTIDP,OPARTETC
.                                           * load End time from selected
          MOVE      ZERO,F2
          MOVE      OPCNSMTT,F2
          MOVE      SP70,TIME2
.
          LOAD      TIME2,F2,OPARTCAL,OPARATIM,OPARANAP,OPARANAS:
                             OPSGTIPS,OPSGTISS,OPSGTISE,OPARANAE:
                             OPSGTIDR,OPARTIRF,OPARTIRB,OPARTIRD:
                             OPARTIDP,OPARTETC
.
          MATCH     SP70,TIME1              * Blank from time
          GOTO      SURG9999 IF EQUAL
.
          MATCH     SP70,TIME2              * Blank to time
          GOTO      SURG9999 IF EQUAL
.
          CALL      CALC0000                * Calculate Minutes
          ADD       TIME3,GTIME             * Surgical Minutes
.
SURG9999  RETURN
+
.
.*******************************************************************************
.*                    I/O Routines for temp files                              *
.*******************************************************************************
.
.  Report File
.
RSREPRT1  RESET     KEY14
          READ      REPORT1,KEY14;;
          RETURN
.
RKREPRT1  MOVE      ZERO,OVRCD
          READKS    REPORT1;RPDATE,RPDOCTOR,RPCASE,RPANAE,RPOPER:
                            RPRECV,RPNSESS,RPSESS,RPPROCS,RPSURG
          GOTO      OVERCOND IF OVER
          RETURN
.
RDREPRT1  MOVE      ZERO,OVRCD
          RESET     KEY14
          READ      REPORT1,KEY14;RPDATE,RPDOCTOR,RPCASE,RPANAE,RPOPER:
                                  RPRECV,RPNSESS,RPSESS,RPPROCS,RPSURG
          GOTO      OVERCOND IF OVER
          RETURN
.
UPREPRT1  UPDATE    REPORT1;RPDATE,RPDOCTOR,RPCASE,RPANAE,RPOPER:
                            RPRECV,RPNSESS,RPSESS,RPPROCS,RPSURG
          RETURN
.
WRREPRT1  RESET     KEY14
          WRITE     REPORT1,KEY14;RPDATE,RPDOCTOR,RPCASE,RPANAE,RPOPER:
                                  RPRECV,RPNSESS,RPSESS,RPPROCS,RPSURG
          RETURN
.
. Summary File
.
RSSUMRY1  RESET     KEY6
          READ      SUMMARY1,KEY6;;
          RETURN
.
RKSUMRY1  MOVE      ZERO,OVRCD
          READKS    SUMMARY1;SMDOCTOR,SMCASES,SMANAE,SMOPER:
                            SMRECV,SMNSESS,SMSESS,SMPROCS,SMSURG
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSUMRY1  MOVE      ZERO,OVRCD
          RESET     KEY6
          READ      SUMMARY1,KEY6;SMDOCTOR,SMCASES,SMANAE,SMOPER:
                                 SMRECV,SMNSESS,SMSESS,SMPROCS,SMSURG
          GOTO      OVERCOND IF OVER
          RETURN
.
UPSUMRY1  UPDATE    SUMMARY1;SMDOCTOR,SMCASES,SMANAE,SMOPER:
                            SMRECV,SMNSESS,SMSESS,SMPROCS,SMSURG
          RETURN
.
WRSUMRY1  RESET     KEY6
          WRITE     SUMMARY1,KEY6;SMDOCTOR,SMCASES,SMANAE,SMOPER:
                                 SMRECV,SMNSESS,SMSESS,SMPROCS,SMSURG
          RETURN
.
.*******************************************************************************
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       OPRDEAIO/INC             * Operation Detail File
          INC       OPRSRGIO/INC             * Operation Usage File
          INC       OPRSESIO/INC             * Operation Session File
          INC       OPRPMBIO/INC             * Usage Operation codes
          INC       PMSVX1IO/INC
          INC       PATHSPIO/INC
          INC       OPRARDIO/INC
          INC       PATCODIO/INC	
          INC       PATHSPKY
          INC       OPRNOOPR                 * Calc no. of opers/procedures

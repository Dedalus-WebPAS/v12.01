. **********************************************************
. *              P R O G R A M  S U M M A R Y              *
. *              -------------  -------------              *
. *                                                        *
. *     PROGRAM NAME:     IBAOUT60                         *
. *                                                        *
. *     FUNCTION:         CHARGE MASTER LISTING            *
. *                                                        *
. *     DATE WRITTEN:     02/09/88                         *
. *                                                        *
. *     MODIFICATIONS:    -                                *
. *                                                        *
. *     AUTHOR:           M.HAYSE                          *
. *                                                        *
. **********************************************************
. Modifications :
.----------------------------------------------------------------------.
.  V9.02.00   11.10.2001  Jeni Tan
.                         Copied from r5.09.
          INC       STD001FD
.
          INC       PATNIPFD/INC
          INC       PATCODFD/INC
.
.               Extra variables
.
LN55      FORM      "55"
.
.
CODET     INIT      "T "
CODECV    INIT      "CV"
CODECL    INIT      "CL"
CODECI    INIT      "CI"
.
VISTDESC  DIM       14                * charge file
RESDDESC  DIM       14                * charge file
COMPDESC  DIM       14
INDCDESC  DIM       14
.
PRGID     INIT      "IBAOUT60"
PRGNAM    INIT      "Charge Master Listing"
VERSION   INIT      "V12.01.00"
+
.         Start of program
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patnipbf";
          OPEN      PATNIPB1,"patnipbf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
MENU      DISPLAY   *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Print charges"
.
BEGIN     KEYIN     *P1:8,*EF,"Please Select : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF START
.
          DISPLAY   *P35:8,*EL,*B,*V2LON,"** Invalid Selection **",*W2;
          GOTO      BEGIN
.
.       Print resident status /compensation code/ clinic indicator plus
.       descriptions and charged amounts, for this site only
.
START     DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "*** Generating ***",*W2;
.
          MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
.
          DISPLAY   *P1:24,*EL,*P10:24,"Printing Charges : ";
.
          PACK      KEY13 WITH TWO,SP10
          CALL      RDSNIPB1
.
LOOPX     CALL      RDKNIPB1
          BRANCH    OVRCD OF ENDP
.
          COMPARE   TWO,NBSYST
          GOTO      ENDP IF NOT EQUAL
          MOVE      SP30,VISTDESC
.
          MOVE      SP30,VISTDESC
          PACK      KEY5 WITH CODECV,NBINDV
          CALL      RDCODE1
          BRANCH    OVRCD OF LOOPT
          MOVE      TDESC,VISTDESC
.
LOOPT     MOVE      SP30,RESDDESC
          PACK      KEY5 WITH CODET,NBRESD
          CALL      RDCODE1
          BRANCH    OVRCD OF LOOPC
          MOVE      TDESC,RESDDESC
.
LOOPC     MOVE      SP30,COMPDESC
          PACK      KEY5 WITH CODECL,NBCOMP
          CALL      RDCODE1
          BRANCH    OVRCD OF LOOPI
          MOVE      TDESC,COMPDESC
.
LOOPI     MOVE      SP30,INDCDESC
          PACK      KEY5 WITH CODECI,NBINDC
          CALL      RDCODE1
          BRANCH    OVRCD OF NEXT1
          MOVE      TDESC,INDCDESC
.
NEXT1     DISPLAY   *P45:24,*V2LON,NBINDC,*P50:24,NBRESD,*P56:24,NBCOMP
.
          CALL      PRNTDA
          GOTO      LOOPX
.
.
.
PRNTDA    COMPARE   LN55,CLNO
          CALL      HEAD IF NOT LESS
.
          PRINT     *1,NBINDC,SP1,INDCDESC:
                    *21,NBRESD,SP1,RESDDESC,*42,NBCOMP,SP1,COMPDESC:
                    *62,NBINDV,SP1,VISTDESC:
                    *83,NBITEM,*89,NBDESC,*121,"$ ",NBAMNT,*N;
.
          ADD       ONE,CLNO
          RETURN
.
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NOREC IF EQUAL
.
          PRINT     *N,*N,"***  END OF REPORT  ***"
          DISPLAY   *P1:24,*EL,*P15:24,*V2LON:
                    "*** Report Generation Completed ***",*W2;
          GOTO      MENU
.
NOREC     DISPLAY   *P1:24,*EL,*P20:24,*V2LON,*B:
                    "*** No Charge Data Available ***",*W2;
          GOTO      BEGIN
.
HEAD      ADD       ONE,CPAGENO
          CLOCK     TIME,CTIMEIS
          PRINT     *F,"IBAOUT60",*40,CNAME,*115,"DATE - ",CDATE:
                    *N,VERSION,*40,"*** CHARGE MASTER LISTING ***":
                    *115,"TIME -",CTIMEIS,*N:
                    *115,"PAGE -    ",CPAGENO,*N
.
          PRINT     *1,"Clinic Type",*21,"Resident Status":
                    *42,"Compensation Code":
                    *62,"Visit Type",*83,"Item":
                    *89,"Description",*121,"Amount",*N:
                    *1,"------------------":
                    *21,"-----------------":
                    *42,"-----------------":
                    *62,"-----------------":
                    *83,"----",*89,"------------------------------":
                    *121,"-----------",*N,*N;
          MOVE      SEVEN,CLNO
          RETURN
.
          INC       PATNIPIO/INC
          INC       PATCODIO/INC
          INCLUDE   STD001IO
.
ENDIT     CHAIN     PGM
          STOP
.

.**********************************************************************
.*  System    :  PATIENT                                              *
.*  Program   :  IBAAAE24                                             *
.*  Desc.     :  A & E LOG REPORT                                     *
.**********************************************************************
.*  Date      :  11/09/89                                             *
.*  Author    :  K.Peak                                               *
.*  Mods.     :                                                       *
.*       V12.00.01 22/05/2025  Alina Bhari   TSK 0955096              *
.*                 Added alpanumeric visit number generation          *
.*                  -ADANUMB                                          *
.*       V10.04.02  12/06/2014  Steve Armstrong     CAR 301639        *
.*                  Added call to KILL0000 when exiting program       *
.*       V10.04.01  06/03.2014  Ania P              CAR 261630        *
.*                  Remove the use of PORT for temp file names        *
.**********************************************************************
.*        V5.08.01  23/08/2000  Caleb Sharman                         *
.*                  Changed Health Fund variables to be 8 chars       *
.*        V5.07.B02 24/11/1998  Nicole Harrington                     *
.*                  507 rebounds                                      *
.*        V5.07.00  22/10/1998  Jim Stathopoulos                      *
.*                  507 Changes                                       *
.**********************************************************************
.*        V5.04.02  25/06/1997  Howellsy         505                  *
.*                  Use PATDOCKY instead of PATDOCDS                  *
.*        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857          *
.*                  Changed temp file name "pstidx" to standard format*
.**********************************************************************
.*        V5.03.01  16/04/1996    Howellsy   5.04                     *
.*                  Changed Accident & Emergency to Emergency.        *
.**********************************************************************
.*               V5.01.02 15/09/89 K.Peak                             *
.*                        Fixed I*D 03625, CODEEW, and Assessment     *
.*            :  V5.01.03 20/09/89 K.Peak                             *
.*                        Fixed number of patients & continuing of pge*
.*            :  V5.01.04 29.09.89 S.Barcham                          *
.*                        Print If Blank ADADOCT                      *
.*                        SRF 102465                                  *
.*               V5.01.05 27/10/89 S.Barcham                          *
.*                        Swap Surname And Given Name                 *
.*                        SRF 102866                                  *
.*            :  V5.01.06 15/01/90 DIG                                *
.*                        Re-Compiled for changes to                  *
.*                        AAEDI1FD + IO                               *
.*            :  V5.01.07 06/04/90 D.Di.Paola     SRF 104589          *
.*                        Put initialise of total patient counter     *
.*                        before reading tempfile in case no records  *
.*                        exist....                                   *
.*              V5.01.121 27/11/90 Glen Hobby                         *
.*                            Recompiled for changes to               *
.*                            PATMX1FD                                *
.*              V5.01.122 20/05/91 Steve O'Gorman                     *
.*                            Removed Unwanted Code found by new comp.*
.*        V5.01.123 24/05/1994  Matt Surridge                         * 
.*                  Fixed bugs for global recompile.                  *
.*        V5.01.124 11/10/1994  Matt Surridge                         * 
.*                  Changed Assessment to use ADSSSES instead of      *
.*                  ADADIAG.                                          *
.**********************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
          INC       AAEDE1FD/INC
          INC       AAEDI1FD/INC
.
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATMA1FD/INC

          INC       PATMI1FD/INC
          INC       PATTRNFD/INC
+
.**********************************************************************
.*                      VARIABLES                                     *
.**********************************************************************
.
ALL       INIT      "ALL"     * all doctors (consultants) selected
ASSESS    DIM       33        * shortened description of diagnosis
.
CMDLINE   DIM       60        * command line for temporary file
CUDATE    DIM       8         * current date
COMDESC   DIM       10        * completion code description
CENT      DIM       2         * century
CODEES    INIT      "ES"      * category for situation code
CODEEW    INIT      "EW"      * category for completion code
.
DOCODE    DIM       6         * doctor code 
DOCCODE   DIM       6
DOCFLG    FORM      1         * doctor flag 
DATEF     DIM       8         * fromdate
DIM2A     DIM       2         * port
DIM3      DIM       3         * used in patdsdoc
DAY       DIM       2         * day
.
FNAMER    DIM       8         * used in temporary file
FNAMET    DIM       8         * used in temporary file
FROMDATE  DIM       8         * fromdate for report
FSTFLG    FORM      1         * first time through flag
.
.
LNO       FORM      2         * line counter
.
MON       DIM       2         * month
.
PAGENO    FORM      3         * page number
PNAME     DIM       20        * patients name
PSTROL    FILE      ASCII, FIXED=256
PTCNDCQS  FORM      1
.
SITDESC   DIM       10        * situation code description
SNAME     DIM       20        * saved doctor name
SEC       FORM      2
SORT1     INIT      "u1-8,9-13,178-185"
SP12      INIT      "            "
SP14      INIT      "              "
SP50      INIT      "                                                  "
.
TODATE    DIM       8         * todate for report
TIMEIS    DIM       8         * time for report
TOTPAT    FORM      8         * total number of patients
.
TEMPFILE  IFILE     SQL, FIXED=186, KEY=SORT1 
.
UNIQUE    FORM      8         * temporary file counter
.
VERT      FORM      2         * vertical position
VDATE     DIM       10        * date visited
.
XDAY1     DIM       2         * fromdate day
XMON1     DIM       2         * fromdate month
XYEAR1    DIM       2         * fromdate year
XCENT1    DIM       2         * fromdate century
XDAY2     DIM       2         * todate day
XMON2     DIM       2         * todate month
XYEAR2    DIM       2         * todate year
XCENT2    DIM       2         * todate century
.
. -----  Temporary file layout -----
.
.NAME     TYPE     LENGTH  PHYSICAL   START     
.----     ----     ------  --------   -----    
.ADADATE  DIM         8          8       1
.ADATIME  DIM         5          5       9
DOCNAME   DIM        20         20      14
.ADAURNO  DIM         8          8      34
.ADANUMB  DIM         8          5      42
.PSNAME   DIM        20         20      47
.PGNAME   DIM        25         25      67
.ADADIAG  DIM        60         60      92
.ADAWAIT  DIM         3          3     152
CMPDESC   DIM        20         20     155
.ADASIT   DIM         3          3     175
DUNIQUE   DIM         8          8     178
.   ----- EOR 186 -----
.
PRGID     INIT     "IBAAAE24"
PRGNAM    INIT      "Emergency Log Report"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                      MAINLINE                                      *
.**********************************************************************
.
ML0000    CALL     INIT0000           * initialisation
.
ML1000    CALL     CLR0000            * clear variables
.
          CALL     SCR0000            * select option
          BRANCH   EXIT,ML9000
.
          CALL     DOPT0000           * display option selected
ML2000    CALL     DSC0000            * display screen
          CALL     DAT0000            * keyin date range
          BRANCH   EXIT,ML1000
          CALL     DOC0000            * keyin doctor  
          CALL     CHK0000            * check ok to continue
          BRANCH   EXIT,ML2000
.
          CALL     PRC0000            * process data
          CALL     PRT0000            * print report
          CALL     END0000            * end of report
.
          GOTO     ML1000
.
.----------------------------------------------------------------------
.                  ... The End 
.----------------------------------------------------------------------
ML9000    CALL      KILL0000
          CHAIN     PGM
+
.**********************************************************************
.*                      INIT0000                                      *
.*                   Initialisation                                   *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"aaede1af";
          OPEN      AAEDE1A3,"aaede1af"
.
          DISPLAY   *P64:24,"aaedi1af";
          OPEN      AAEDI1A1,"aaedi1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
.
          PACK      CUDATE WITH CCC,CYY,CMM,CDD
          REP       " 0",CUDATE
.
          CALL      TFILENAM   
          PACK      FNAMER,TFILNAME,SP70
.
          CALL      TFILENAM
          PACK      FNAMET,TFILNAME,SP70
.
          RETURN
+
.**********************************************************************
.*                      CLR0000                                       *
.*                   Clear Variables                                  *
.**********************************************************************
.
CLR0000   MOVE      "60",LNO
          MOVE      ZERO,PAGENO
          MOVE      ZERO,TOTPAT    * number of patients
          MOVE      ZERO,DOCFLG    * doctor flag
          MOVE      ZERO,FSTFLG    * print routine flag
          MOVE      SP20,DOCNAME   * doctors name
          MOVE      SP20,CMPDESC   * completion description
          MOVE      SP20,COMDESC   * completion description
          MOVE      SP20,SITDESC   * situation description
.
          RETURN
+
.**********************************************************************
.*                      SCR0000                                       *
.*                   Select Option                                    *
.**********************************************************************
.
SCR0000   MOVE      FALSE,EXIT
          MOVE      ZERO,OPTION
          HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Date Sequence"
.
SCR1000   DISPLAY   *P1:7,"Option : "
          KEYIN     *P10:7,*V2LON,OPTION
.
          BRANCH    OPTION,SCR9999
          GOTO      SCR2000
.
SCR2000   COMPARE   ZERO,OPTION
          GOTO      SCR9000 IF EQUAL
.
          COMPARE   ONE,OPTION
          GOTO      SCR0000 IF NOT LESS
          GOTO      SCR9999
.
SCR9000   MOVE      TRUE,EXIT
.
SCR9999   RETURN
+
.**********************************************************************
.*                      DOPT0000                                      *
.*                   Display Option                                   *
.**********************************************************************
.
DOPT0000  DISPLAY   *P50:2,*V2LON,"- Date Sequence "
          RETURN
+
.**********************************************************************
.*                      DSC0000                                       *
.*                   Display Screen                                   *
.**********************************************************************
.
DSC0000   DISPLAY   *P1:9,*EF,"From Date  :":
                    *P1:11,"To   Date  :":
                    *P1:13,"Consultant :"
          RETURN
+
.**********************************************************************
.*                      DAT0000                                       *
.*                Keyin a range of dates                              *
.**********************************************************************
.
DAT0000   MOVE      FALSE,EXIT
          UNPACK    SP6,CYEAR,CMON,CDAY
.
.         Keyin from date
.
DAT1000   MOVE      TEN5,CCOL
          MOVE      NINE,CVERT
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
          MOVE      CCC,CCENT
.
DAT2000   CALL      KEYDATE
          BRANCH    OVRCD,DAT9000
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
          MOVE      FROMDATE,DATEF
.
.         Keyin to date
.
DAT3000   UNPACK    SP6,CYEAR,CMON,CDAY
.
          MOVE      TEN5,CCOL
          MOVE      TEN1,CVERT
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
          MOVE      CCC,CCENT
.
DAT4000   CALL      KEYDATE
          BRANCH    OVRCD,DAT1000
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      DAT8000 IF LESS
          RETURN
.
DAT8000   DISPLAY   *P1:24,*B,*EL,*V2LON:
                    "** FROM Date Greater than TO Date ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL
          GOTO      DAT1000
.
DAT9000   MOVE      TRUE,EXIT
          RETURN
+
.**********************************************************************
.*                      DOC0000                                       *
.*                   Keyin Doctor                                     *
.**********************************************************************
DOC0000   MOVE      SP30,DOCNAME
          MOVE      "15",ECOL
          MOVE      "13",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,DOC0800,DOC0000
.
          MOVE      DCODE,DOCODE
          MOVE      DGNAME,ANS
          DISPLAY   *P25:13,*EL,*V2LON,ANS,DOT,DSNAME
          GOTO      DOC9999
.
.         Selected doctors selected
.
DOC0800   DISPLAY   *P15:13,*V2LON,ALL
          MOVE      ONE,DOCFLG
          GOTO      DOC9999
.
DOC9999   RETURN
+
.**********************************************************************
.*                      CHK0000                                       *
.*                Check ok to continue                                *
.**********************************************************************
.
CHK0000   MOVE      FALSE,EXIT
          KEYIN     *P1:24,*EF," Ok to Continue (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
.
          CMATCH    "N",ANS
          GOTO      CHK1000 IF EQUAL
.
          CMATCH    "Y",ANS
          RETURN    IF EQUAL
          BEEP
          GOTO      CHK0000
.
CHK1000   MOVE      TRUE,EXIT
          RETURN
+
.**********************************************************************
.*                      PRC0000                                       *
.*                    Process Data                                    *
.**********************************************************************
.
PRC0000   CALL      KILL0000        * remove temp file
          CALL      CREA0000        * create temp file
.
          OPEN      TEMPFILE,FNAMET
          MOVE      ONE,UNIQUE
          DISPLAY   *P1:24,*EL:
                    *P10:24,"Event No. : ",*P40:24,"Scanning : "
.
          PACK      ADSSSES,SP30,SP30,SP30
          PACK      KEY21,FROMDATE,SP20     * a&e detail file
          CALL      RDSDETA3
PRC1000   CALL      RDKDETA3
          BRANCH    OVRCD OF PRC9999
.
          DISPLAY   *P51:24,ADANUMB;
.
.         If the patient visited after the todate, ignore it
.
          MATCH     ADADATE,TODATE
          GOTO      PRC9999 IF LESS
.
.         Check if specific doctor selected
.
          COMPARE   ONE,DOCFLG
          GOTO      PRC2000 IF EQUAL
.
          MATCH     ADADOCT,DOCODE
          GOTO      PRC1000 IF NOT EQUAL
.
.         Read master file
.
PRC2000   MOVE      ADAURNO,KEY8
          CALL      RDMAST1
.
          MOVE      SP30 TO DOCNAME
.
          MATCH     SP6,ADADOCT
          GOTO      PRC4800 IF EQUAL
.
.         Read doctor file
.
          MOVE      ADADOCT,KEY6
          CALL      RDDOCT1
.
.         Get doctors name
.
          ENDSET    DGNAME
PRC3000   CMATCH    SP1,DGNAME
          GOTO      PRC3500 IF NOT EQUAL
          BUMP      DGNAME BY -1
          GOTO      PRC3500 IF EOS
          GOTO      PRC3000
PRC3500   LENSET    DGNAME
          RESET     DGNAME
.
          ENDSET    DSNAME
PRC4000   CMATCH    SP1,DSNAME
          GOTO      PRC4500 IF NOT EQUAL
          BUMP      DSNAME BY -1
          GOTO      PRC4500 IF EOS
          GOTO      PRC4000
PRC4500   LENSET    DSNAME
          RESET     DSNAME
.
          CLEAR     DOCNAME
          APPEND    DGNAME,DOCNAME
          APPEND    SP1,DOCNAME
          APPEND    DSNAME,DOCNAME
          RESET     DOCNAME
.
PRC4800   MOVE      ADANUMB,KEY8
          CALL      RDDISA1
.
.         Get completion code description
.
          MOVE      SP20,CMPDESC
          MATCH     SP3,ADAWAIT
          GOTO      PRC5000 IF EQUAL
.
          PACK      KEY5,CODEEW,ADAWAIT
          CALL      RDCODE1
          BRANCH    OVRCD,PRC5000
          MOVE      TDESC,CMPDESC
.
PRC5000   DISPLAY   *P24:24,*V2LON,ADANUMB;
.
          MOVE      UNIQUE,DUNIQUE
          PACK      KEY21,ADADATE,ADATIME,DUNIQUE
          WRITE     TEMPFILE,KEY21;ADADATE,ADATIME,DOCNAME,ADAURNO,ADANUMB:
                                   PSNAME,PGNAME,ADSSSES,ADAWAIT,CMPDESC:
                                   ADASIT,DUNIQUE
          ADD       ONE,UNIQUE
          PACK      ADSSSES,SP30,SP30,SP30
          GOTO      PRC1000
.
PRC9999   RETURN
+
.**********************************************************************
.*                      PRT0000                                       *
.*                   Print Report                                     *
.**********************************************************************
.
PRT0000   COMPARE   "55",LNO
          CALL      HEAD0000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*V2LON:
                    *P20:24,"** Printing Report **",*W,*HOFF,*P1:24,*EL
.
          MOVE      ZERO,UNIQUE
          PACK      KEY21,SP30
          READ      TEMPFILE,KEY21;;
.
PRT1000   READKS    TEMPFILE;ADADATE,ADATIME,DOCNAME,ADAURNO,ADANUMB:
                             PSNAME,PGNAME,ADSSSES,ADAWAIT,CMPDESC:
                             ADASIT,DUNIQUE
          GOTO      PRT9999 IF OVER
          MOVE      DUNIQUE,UNIQUE
.
.         Get patients name
.
.         ENDSET    PGNAME
.RT2000   CMATCH    SP1,PGNAME
.         GOTO      PRT2500 IF NOT EQUAL
.         BUMP      PGNAME BY -1
.         GOTO      PRT2500 IF EOS
.         GOTO      PRT2000
.RT2500   LENSET    PGNAME
.         RESET     PGNAME
.
          ENDSET    PSNAME
PRT3000   CMATCH    SP1,PSNAME
          GOTO      PRT3500 IF NOT EQUAL
          BUMP      PSNAME BY -1
          GOTO      PRT3500 IF EOS
          GOTO      PRT3000
PRT3500   LENSET    PSNAME
          RESET     PSNAME
.
          CLEAR     PNAME
          APPEND    PSNAME,PNAME
          APPEND    ",",PNAME
          APPEND    PGNAME,PNAME
          RESET     PNAME
.
          MOVE      ADSSSES,ASSESS      * diagnosis description
          MOVE      CMPDESC,COMDESC     * completion code description
.
.         Get situation code description
.
          MOVE      SP20,SITDESC
          MATCH     SP3,ADASIT
          GOTO      PRT3700 IF EQUAL
.
          PACK      KEY5,CODEES,ADASIT
          CALL      RDCODE1
          BRANCH    OVRCD,PRT3700
          MOVE      TDESC,SITDESC
.
PRT3700   UNPACK    ADADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VDATE
.
.         Check whether first time through print routine
.
          COMPARE   ZERO,FSTFLG
          GOTO      PRT4000 IF NOT EQUAL
          MOVE      DOCNAME,SNAME
          MOVE      ONE,FSTFLG
          GOTO      PRT4500 
.
PRT4000   COMPARE   "55",LNO
          GOTO      PRT4300 IF LESS
          CALL      PGE0000
          PRINT     *N,*N,"  ***  CONTINUES ***"
          MOVE      SP20,SNAME
          CALL      HEAD0000
.
PRT4300   MATCH     DOCNAME,SNAME         * match doctors name
          GOTO      PRT4500 IF NOT EQUAL
.
          PRINT     *1,"!",*20,"!",VDATE,*31,"!",ADATIME:
                    *37,"!",ADAURNO,*46,"!",ADANUMB,*55,"!",PNAME:
                    *76,"!",ASSESS,*110,"!",COMDESC,*121,"!",SITDESC,*132,"!"
.
          GOTO      PRT5000 
.
PRT4500   PRINT     *1,"!",DOCNAME,*20,"!",VDATE,*31,"!",ADATIME:
                    *37,"!",ADAURNO,*46,"!",ADANUMB,*55,"!",PNAME:
                    *76,"!",ASSESS,*110,"!",COMDESC,*121,"!",SITDESC,*132,"!"
.
PRT5000   ADD       ONE,LNO
          MOVE      DOCNAME,SNAME
          GOTO      PRT1000
.
PRT9999   RETURN
+
.**********************************************************************
.*                      END0000                                       *
.*                    End of Report                                   *
.**********************************************************************
.
END0000   COMPARE   ZERO,PAGENO
          GOTO      END1000 IF NOT EQUAL
.
          MOVE      UNIQUE,TOTPAT
          DISPLAY   *P10:24,*EL,*V2LON:
                    "** No Report Generated **",*W2;
          CALL      HEAD0000
          PRINT     *N,"Total Number of Patients:",TOTPAT:
                    *N,*N,"  ***  END OF REPORT  ***"
          GOTO      END2000
.
END1000   
          MOVE      UNIQUE,TOTPAT
          CALL      PGE0000
          PRINT     *N,"Total Number of Patients:",TOTPAT:
                    *N,*N,"  ***  END OF REPORT  ***"
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Report Generation Completed **",*W2;
.
END2000   CALL      KILL0000
          RETURN
+
.**********************************************************************
.*                      CREA0000                                      *
.*       Create the temporary to count number of patients             *
.**********************************************************************
.
CREA0000  PREP      PSTROL,FNAMER
          WRITE     PSTROL,SEQ;"isbuild ",FNAMET," 186 ",SORT1
          CLOSE     PSTROL
          DISPLAY   *P1:14,*EF,*P1:14
          CLEAR     CMDLINE
          APPEND    "sh ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P1:14,*EF,*P1:14
          PREP      PSTROL,FNAMER
          CLOSE     PSTROL,DELETE
          RETURN  
+
.**********************************************************************
.*                      KILL0000                                      *
.*          Deleting an indexed file based on port                    *
.**********************************************************************
.
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P1:14,*EF;
          RETURN
+
.**********************************************************************
.*                      HEAD0000                                      *
.*                Headings for Output                                 *
.**********************************************************************
.
HEAD0000  ADD       ONE,PAGENO
          PRINT     *F,*1,PRGID,*42,CNAME,*116,"DATE - ",CDATE:
                    *N,*1,VERSION:
                    *42,PRGNAM;
.
          PRINT     " (DATE - SEQUENCE)";
.
          UNPACK    FROMDATE,XCENT1,XYEAR1,XMON1,XDAY1
          UNPACK    TODATE,XCENT2,XYEAR2,XMON2,XDAY2
.
          CLOCK     TIME,TIMEIS
.
          PRINT     *118,"TIME - ",TIMEIS:
                    *N,*118,"PAGE - ",*130,PAGENO,*N:
                    *42,"From Date : ",XDAY1,"/",XMON1,"/",XCENT1,XYEAR1:
                    *72,"To Date : ",XDAY2,"/",XMON2,"/",XCENT2,XYEAR2,*N
.
          CALL      PGE0000
.
          PRINT     *1,"!Consultant",*20,"!Vis Dte",*31,"!A/Tim":
                    *37,"!U/R No.",*46,"!Event No":
                    *55,"!Patient Name",*76,"!Assessment":
                    *110,"!Completion",*121,"!Situation",*132,"!"
.
          CALL      PGE0000
          MOVE      NINE,LNO
          RETURN
+
.**********************************************************************
.*                      PGE0000                                       *
.*                Print Line Separator                                *
.**********************************************************************
.
PGE0000   PRINT     "*----------------------------":
                    "--------------------------------------------------":
                    "----------------------------------------------------*"
          RETURN
+
.**********************************************************************
.*                      RED0000                                       *
.*                Redisplay Screeen                                   *
.**********************************************************************
.
RED0000   UNPACK    FROMDATE,XCENT1,XYEAR1,XMON1,XDAY1
          UNPACK    TODATE,XCENT2,XYEAR2,XMON2,XDAY2
.
          HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Date Sequence":
                    *P1:7,"Option : ",OPTION:
                    *P50:2,*V2LON,"- Date Sequence ":
                    *P1:9,*HOFF,*EF,"From Date  :":
                    *P14:9,*EL,*V2LON,XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1:
                    *P1:11,*HOFF,"To   Date  :":
                    *P14:11,*EL,*V2LON,XDAY2,SLASH,XMON2,SLASH,XCENT2,XYEAR2:
                    *P1:13,*HOFF,"Consultant :"
.
          MOVE      SP6,DOCODE
          RETURN
+
.**********************************************************************
.*                      I/O ROUTINES                                  *
.**********************************************************************
.
          INC       STD001IO
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.
          INC       PATDOCKY
.
          INC       AAEDE1IO/INC
          INC       AAEDI1IO/INC
.
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC

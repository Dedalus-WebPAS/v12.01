.******************************************************************************
.* System   : Waiting List                                                    *
.* Program  : IBAWAT32           (N.Z. Only)                                  *
.* Desc.    : DOCTORS WAITING LIST PRIORITY SUMMARY                           *
.******************************************************************************
.* Date     : 14/02/1990                                                      *
.* Author   : K.Peak                                                          *
.* Mods.    :                                                                 *
.*            V10.05.01 05/01/2015  Ebon Clements     CAR 261630              *
.*                      Removed port number from temp file name               *
.*            V5.07.B01 23/11/1998  Nicole Harrington                         *
.*                      507 changes                                           *
.*            V5.01.02  23/07/90 i chung           SRF 105810                 *
.*                      Fixed page overflow problem                           *
.*            V5.01.10  09/08/90 Bill Dew                                     *
.*                      Convert WAT32 to accept the New waiting list stats    *
.*                      Also fix SRF 106014                                   *
.*                      Print Priority code after description on report       *
.*            V5.01.11  24/04/91 i chung           SRF 108251                 *
.*                      Changed available option from "Generate Report" to :  *
.*                        1. Surgical Units Only      )  using Category "WU"  *
.*                        2. Non-Surgical Units Only  )    and TCINDC1        *
.*                        3. All Units                )                       *
.*        V5.01.12  01.04.93 Neeriem "I Hate Support" Dye (I.B.A.)            *
.*                  Add security access                                       *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       PATCOMM/INC
.
          INC       IBASEQFD/INC
          INC       PATCODFD/INC     * Codes file
          INC       PATCONFD/INC     * Control file
          INC       PATDO1FD/INC     * Doctor file
          INC       PATDRGFD/INC     * Period Date Range file
          INC       TFILEVAR/INC
          INC       WATCONFD/INC     * W/List Control file
          INC       WATUDSFD/INC     * W/List Unit/Doctor Summary file
          INC       WATUNSFD/INC     * W/List Unit Summary file
          INC       WATSEAFD/INC
          INC       WATSEIFD/INC
          INC       WEBERRFD/INC
+
.**********************************************************************
.*                         VARIABLES                                  *
.**********************************************************************
ANATMP    DIM       8       * temporary file index 1
.
CMDLINE   DIM       50
CODETP    INIT      "TP"    * Priority code category
CODEWU    INIT      "WU"    * Waiting List Unit code category
CSTPER    DIM       6       * ccyymm
CURPER    DIM       6       * current period
.
DOCDESC   DIM       33      * doctor description
DRNG1     FORM      3       * day range 1 + 1   *** these are ranges in days
DRNG2     FORM      3       * day range 2 + 1
DRNG3     FORM      3       * day range 3 + 1
DRNG4     FORM      3       * day range 4 + 1
.
ENDDATE   DIM       10      * enddate
ERNG1     FORM      2       * month range 1 + 1 *** these are ranges in months
ERNG2     FORM      2       * month range 2 + 1
ERNG3     FORM      2       * month range 3 + 1
ERNG4     FORM      2       * month range 4 + 1
ERNG5     FORM      2       * month range 5 + 1 (not used)
.
FADMDTE   DIM       8       * ddmmyy
.
GDOCT     DIM       6       * Doctor Code or "ZZZZZZ" - g/total
GPRTY     DIM       3       * Priority Code (Cat TP) - g/total
GRNG1     FORM      8       * No pats < WTCNRNG1 - g/total
GRNG2     FORM      8       * No pats >= WTCNRNG1 & <= WTCNRNG2 - g/total
GRNG3     FORM      8       * No pats >= WTCNRNG2 & <= WTCNRNG3 - g/total
GRNG4     FORM      8       * No pats >= WTCNRNG3 & <= WTCNRNG4 - g/total
GRNG5     FORM      8       * No pats >= WTCNRNG4 & <= WTCNRNG5 - g/total
GRNG6     FORM      8       * No pats > WTCNRNG5 - g/total
GIAAS     FORM      8       * No of IAA patients - g/total
GTOTAL    FORM      8       * Total of ranges - g/total
.
.
PDATE     DIM       7        * period for printing
PFDATE    DIM       8        * print from date
PTDATE    DIM       8        * print to date
PRTDESC   DIM       20       * priority description
PRTFLG    FORM      1        * print routine flag
.
RNG1      FORM      2        * month range 1    *** actual end points of ranges
RNG2      FORM      2        * month range 2
RNG3      FORM      2        * month range 3
RNG4      FORM      2        * month range 4
RNG5      FORM      2        * month range 5
.
SIXZ      INIT      "ZZZZZZ"
SRNG1     FORM      3        * month range 1    *** used in calculations only
SRNG2     FORM      3        * month range 2
SRNG3     FORM      3        * month range 3
SRNG4     FORM      3        * month range 4
SRNG5     FORM      3        * month range 5
STRDATE   DIM       10       * start date
.
TADMDTE   DIM       8        * ddmmyy
TODATE    DIM       8        * todate 
TOTAL     FORM      8        * total of ranges
.
ANATMPXX  IFILE SQL, FIXED=45, KEY="u1-6,7-9"
.
.----------------------------------------------------------------------
.         Temporary File Variables
.
.Name     Type   Length   Physical   Start    Description
.----     ----   ------   --------   -----    -----------
XDOCT     DIM         6          6       1    Doctor Code or "ZZZZZZ"
XPRTY     DIM         3          3       7    Priority Code (Cat TP)
XRNG1     FORM        8          5      10    No pats < WTCNRNG1
XRNG2     FORM        8          5      15    No pats >= WTCNRNG1 & <= WTCNRNG2
XRNG3     FORM        8          5      20    No pats >= WTCNRNG2 & <= WTCNRNG3
XRNG4     FORM        8          5      25    No pats >= WTCNRNG3 & <= WTCNRNG4
XRNG5     FORM        8          5      30    No pats >= WTCNRNG4 & <= WTCNRNG5
XRNG6     FORM        8          5      35    No pats > WTCNRNG5
XIAAS     FORM        8          5      40    No of IAA patients
.
.End of record                          45
.----------------------------------------------------------------------
.
PRGID     INIT      "IBAWAT32"
PRGNAM    INIT      "Doctors Waiting List Priority Summary"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                         MAINLINE                                   *
.**********************************************************************
ML0000
.MLQQ      SPLOPEN   "data"
          CALL      INIT0000        * initialization
.
ML1000    CALL      SCR0000         * select option
          BRANCH    EXIT,ML9000
.
ML1500    CALL      CLR0000         * clear variables
.
          CALL      DAT0000         * keyin period
          BRANCH    EXIT,ML1000
          CALL      CONTQST         * Ok to continue Y/N ?
          BRANCH    CEXIT,ML2000,ML1500,ML1000
.
ML2000    CALL      CREA0000        * create temporary file
          CALL      DSPS0000        * initialize counters & display screen
.
          CALL      WATS0000        * process watudsfd data
          CALL      WATN0000        * process watunsfd data
          CALL      PRNT0000        * print detail data
          CALL      END0000         * end of report
          GOTO      ML1000
.
.----------------------------------------------------------------------
.                          .. The End                                  
.----------------------------------------------------------------------
ML9000    CALL      KILL0000        * remove temporary file
.MLQQQ     SPLCLOSE  "data"
          CHAIN     PGM
+
.**********************************************************************
.*                         INIT0000                                   *
.*                      Initialization                                *
.**********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P64:24,"watudsaf";
          OPEN      WATUDSA1,"watudsaf"
.
          DISPLAY   *P64:24,"watunsaf";
          OPEN      WATUNSA1,"watunsaf"
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,ANATMP
.
          READ      CONTROLF,THIRTY7;*158,WTCNMRG1,WTCNMRG2,WTCNMRG3:
                                          WTCNMRG4,WTCNMRG5,*211,WTCNWLSC
          READ      CONTROLF,FIFTY9;*27,CPERMTH
.
          IF        WTCNWLSC = 1
            DISPLAY   *P64:24,"watseaaf";
            OPEN      WATSEAA1,"watseaaf"
            DISPLAY   *P64:24,"watseiaf";
            OPEN      WATSEIA2,"watseiaf"
          ENDIF
.
.         To print the headings in month ranges when the control file ranges
.         are in days, we must divide each range by 30 
.
          MOVE      WTCNMRG1,SRNG1     * day amount in srng1
          DIV       THIRTY,SRNG1       * turn days into months
          MOVE      SRNG1,RNG1         * range one
          MOVE      WTCNMRG1,DRNG1     * set up the day range
          ADD       ONE,DRNG1          * day after first range
.
          MOVE      WTCNMRG2,SRNG2
          DIV       THIRTY,SRNG2
          MOVE      SRNG2,RNG2         * range two
          MOVE      SRNG2,ERNG2
          ADD       ONE,ERNG2          * month after second range
          MOVE      WTCNMRG2,DRNG2     * set up the day range
          ADD       ONE,DRNG2          * day after second range
.
          MOVE      WTCNMRG3,SRNG3
          DIV       THIRTY,SRNG3
          MOVE      SRNG3,RNG3         * range three
          MOVE      SRNG3,ERNG3
          ADD       ONE,ERNG3          * month after third range
          MOVE      WTCNMRG3,DRNG3     * set up the day range
          ADD       ONE,DRNG3          * day after third range
.
          MOVE      WTCNMRG4,SRNG4
          DIV       THIRTY,SRNG4
          MOVE      SRNG4,RNG4         * range four
          MOVE      SRNG4,ERNG4
          ADD       ONE,ERNG4          * month after fourth range
          MOVE      WTCNMRG4,DRNG4     * set up the day range
          ADD       ONE,DRNG4          * day after fourth range
.
          MOVE      WTCNMRG5,SRNG5
          DIV       THIRTY,SRNG5
          MOVE      SRNG5,RNG5         * range five
          MOVE      SRNG5,ERNG5
          ADD       ONE,ERNG5          * month after fifth range
.
.         Initialize HEAD132 input requirement
.
          MOVE      ONE,CNOUNDLN
          RETURN
+
.**********************************************************************
.*                         CLR0000                                    *
.*                     Clear Variables                                *
.**********************************************************************
CLR0000   MOVE      SP6,XDOCT       
          MOVE      SP3,XPRTY    
          MOVE      ZERO,XRNG1   
          MOVE      ZERO,XRNG2  
          MOVE      ZERO,XRNG3 
          MOVE      ZERO,XRNG4
          MOVE      ZERO,XRNG5  
          MOVE      ZERO,XRNG6 
          MOVE      ZERO,XIAAS
          MOVE      ZERO,TOTAL
.
          MOVE      SP6,GDOCT       * grand totals
          MOVE      SP3,GPRTY    
          MOVE      ZERO,GRNG1   
          MOVE      ZERO,GRNG2  
          MOVE      ZERO,GRNG3 
          MOVE      ZERO,GRNG4
          MOVE      ZERO,GRNG5  
          MOVE      ZERO,GRNG6 
          MOVE      ZERO,GIAAS
          MOVE      ZERO,GTOTAL
          RETURN
+
.**********************************************************************
.*                         SCR0000                                    *
.*                      Main Option Screen                            *
.**********************************************************************
SCR0000   HLINE     *G37,2,56,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Surgical Units Only": 
                    *P1:6,*V2LON,TWO,*HOFF," = Non-Surgical Units Only": 
                    *P1:7,*V2LON,THREE,*HOFF," = All Units": 
                    *P1:9,"Select Option :";
.
SCR1000   KEYIN     *P17:9,*EF,*DV,UNDLN1,*P17:9,*V2LON,OPTION;
          COMPARE   ZERO,OPTION
          GOTO      SCR9000 IF EQUAL
.
          BRANCH    OPTION,SCR5000,SCR6000,SCR7000
          BEEP
          GOTO      SCR1000
.
SCR5000   DISPLAY   *P56:2,*V2LON,"- Surgical Units ";
          MOVE      "    (Surgical Units)",CPHDROPT
          GOTO      SCR8000
.
SCR6000   DISPLAY   *P56:2,*V2LON,"- Non-Surgical Units ";
          MOVE      "(Non-Surgical Units)",CPHDROPT
          GOTO      SCR8000
.
SCR7000   DISPLAY   *P56:2,*V2LON,"- All Units ";
          MOVE      "     (All Units)",CPHDROPT
.
SCR8000   MOVE      FALSE,EXIT
          GOTO      SCR9999
.
SCR9000   MOVE      TRUE,EXIT
.
SCR9999   RETURN
+
.**********************************************************************
.*                         DAT0000                                    *
.*                       Keyin Period                                 *
.**********************************************************************
DAT0000   DISPLAY   *P1:13,*EF,"Enter the ",CPERMTH," of the report : ";
.
          PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
          CALL      GPERD
          BRANCH    EXIT,DAT2000
.
          PACK      CURPER,DRGYR,DRGNUM
.
.         Find previous period
.
          PACK      KEY6,CURPER
          CALL      RDDRGA1
          CALL      RDPDRGA1
          PACK      CSTPER,DRGCYR,DRGCNUM
.
          UNPACK    CSTPER,CCENT,CYEAR,CMON
          MOVE      ZERO,CHIGHLT         * 0 = highlight 1 = no highlight
          MOVE      "34",CCOL            * column for keyin
          MOVE      TEN3,CVERT           * row for keyin
          CALL      KEYPER
          BRANCH    OVRCD,DAT0000        * no date keyed in
.
.         Validate the date entered
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,DAT3000
          PACK      PDATE,CMON,SLASH,CCENT,CYEAR
.
.         Pack appropriate start dates
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,STRDATE
          PACK      FADMDTE,CCENT,CYEAR,CMON,CDAY
.
.         Pack appropriate end dates
.
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,ENDDATE
          PACK      TADMDTE,CCENT,CYEAR,CMON,CDAY
.
          PACK      CSTPER,DRGYR,DRGNUM
.
          DISPLAY   *P43:13,*EL,DRGLDSC:
                    *P43:14,*EL,STRDATE," to ",ENDDATE;
          GOTO      DAT8000
.
.         No current period on file
.
DAT2000   KEYIN     *P1:24,*EL,*B,*V2LON:
                    "** Error: Current date not found in Period file **",*HOFF:
                    "   Hit <",*V2LON,"ENTER",*HOFF,"> to exit ",ANS,*P1:24;
          GOTO      DAT9000
.
.         Invalid date entered
.
DAT3000   DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Date **    ";
          CALL      HITENTER
          GOTO      DAT0000
.
DAT8000   MOVE      FALSE,EXIT
          GOTO      DAT9999
.
DAT9000   MOVE      TRUE,EXIT
DAT9999   RETURN
+
.**********************************************************************
.*                         DSPS0000                                   *
.*             Initialize counters and display screen                 *
.**********************************************************************
DSPS0000  MOVE      ZERO,CPAGENO         * page counter
          MOVE      "60",CLNO            * line counter
.
          DISPLAY   *P1:24,*EL,*V2LON,"*** ",*BLINKON,"Extracting Data",*HOFF:
                               *V2LON," ***",*HOFF:
                    *P37:24,"Doctor  :  ",*P63:24,"Unit  :  ";
          RETURN
+
.**********************************************************************
.*                         WATS0000                                   *
.*                   Process WATUDSFD Data                            *
.*   Modified 10/08/1990 Bill Dew
.*   write NUmber of immediate admissions to tempfile according to priority
.**********************************************************************
.         Loop through the unit/doctor summary file and put the data 
.         found into the temporary file
.
WATS0000  PACK      KEY18,CSTPER,SP20            * change KEY15 to KEY18 BD
          CALL      RDSUDSA1
.
WATS1000  CALL      RDKUDSA1
          BRANCH    OVRCD,WATS9999
.
          MATCH     WTUDDATE,CSTPER    
          GOTO      WATS9999 IF NOT EQUAL
.
          IF        ONE=WTCNWLSC
            PACK      KEY4,PASSCODE
            CALL      RDWTSEA1
            IF        1=OVRCD
              PACK      KEY13,PASSCODE,WTUDDOCT,WTUDUNIT,SP20
              CALL      RDWTSEI2
              BRANCH    OVRCD,WATS1000          * no access
            ENDIF
          ENDIF
.
          BRANCH    OPTION,WATS2000,WATS3000,WATS4000
          GOTO      WATS4000
.
.         Surgical Units Only option - TCINDC1 must = "S"
.
WATS2000  PACK      KEY5,CODEWU,WTUDUNIT
          CALL      RDCODE1
          BRANCH    OVRCD,WATS1000
          MATCH     ANSS,TCINDC1
          GOTO      WATS4000 IF EQUAL
          GOTO      WATS1000
.
.         Non-Surgical Units Only option - TCINDC1 must not = "S"
.
WATS3000  PACK      KEY5,CODEWU,WTUDUNIT
          CALL      RDCODE1
          BRANCH    OVRCD,WATS1000
          MATCH     ANSS,TCINDC1
          GOTO      WATS4000 IF NOT EQUAL
          GOTO      WATS1000
.
WATS4000  DISPLAY   *P48:24,*V2LON,WTUDDOCT,*P72:24,WTUDUNIT;
.
          PACK      KEY9,WTUDDOCT,SP3
          CALL      CUDS0000                     * Total for doctor
          PACK      KEY9,WTUDDOCT,WTUDPRTY
          CALL      CUDS0000                     * total for doctor and priority
          PACK      KEY9,SIXZ,WTUDPRTY
          CALL      CUDS0000                     * total for priority
          PACK      KEY9,SIXZ,SP3
          CALL      CUDS0000                     * Grand total for all doctors
          GOTO      WATS1000
.
WATS9999  RETURN
+
.**********************************************************************
.*                         CUDS0000                                   *
.*                Write watudsaf data to Temporary File               *
.**********************************************************************
CUDS0000  CALL      RDTMP1
          BRANCH    OVRCD,CUDS1000
.
          ADD       WTUDIAA,XIAAS
          CALL      UPTMP1
          GOTO      CUDS9999
.
CUDS1000  MOVE      WTUDIAA,XIAAS
          MOVE      ZERO,XRNG1
          MOVE      ZERO,XRNG2
          MOVE      ZERO,XRNG3
          MOVE      ZERO,XRNG4
          MOVE      ZERO,XRNG5
          MOVE      ZERO,XRNG6
          CALL      WRTMP1
.
CUDS9999  RETURN
+
.**********************************************************************
.*                         WATN0000                                   *
.*                   Process WATUNSFD Data                            *
.*   Modified 10/08/1990 Bill Dew
.**********************************************************************
.         Loop through the unit summary file and put the data 
.         found into the temporary file
.
WATN0000  PACK      KEY18,CSTPER,SP20
          CALL      RDSUNSA1
.
WATN1000  CALL      RDKUNSA1
          BRANCH    OVRCD,WATN9999
.
          MATCH     WTUNDATE,CSTPER    
          GOTO      WATN9999 IF NOT EQUAL
.
          IF        ONE=WTCNWLSC
            PACK      KEY4,PASSCODE
            CALL      RDWTSEA1
            IF        1=OVRCD
              PACK      KEY13,PASSCODE,WTUNDOCT,WTUNUNIT,SP20
              CALL      RDWTSEI2
              BRANCH    OVRCD,WATN1000          * no access
            ENDIF
          ENDIF
.
          BRANCH    OPTION,WATN2000,WATN3000,WATN4000
          GOTO      WATN4000
.
.         Surgical Units Only option - TCINDC1 must = "S"
.
WATN2000  PACK      KEY5,CODEWU,WTUNUNIT
          CALL      RDCODE1
          BRANCH    OVRCD,WATN1000
          MATCH     ANSS,TCINDC1
          GOTO      WATN4000 IF EQUAL
          GOTO      WATN1000
.
.         Non-Surgical Units Only option - TCINDC1 must not = "S"
.
WATN3000  PACK      KEY5,CODEWU,WTUNUNIT
          CALL      RDCODE1
          BRANCH    OVRCD,WATN1000
          MATCH     ANSS,TCINDC1
          GOTO      WATN4000 IF NOT EQUAL
          GOTO      WATN1000
.
WATN4000  DISPLAY   *P48:24,*V2LON,WTUNDOCT,*P72:24,WTUNUNIT;
.
          PACK      KEY9,WTUNDOCT,WTUNPRTY
          CALL      CUDN0000            * write data to temp index file
.
          PACK      KEY9,WTUNDOCT,SP3
          CALL      CUDN0000            * write totals for specific doctor
.
          PACK      KEY9,SIXZ,WTUNPRTY
          CALL      CUDN0000            * write totals for prioritys
.
          PACK      KEY9,SIXZ,SP3
          CALL      CUDN0000            * write totals for all docs & priorities
          GOTO      WATN1000
.
WATN9999  RETURN
+
.**********************************************************************
.*                         CUDN0000                                   *
.*                Write watunsaf data to Temporary File               *
.**********************************************************************
CUDN0000  CALL      RDTMP1
          BRANCH    OVRCD,CUDN1000
.
          ADD       WTUNRNG1,XRNG1
          ADD       WTUNRNG2,XRNG2
          ADD       WTUNRNG3,XRNG3
          ADD       WTUNRNG4,XRNG4
          ADD       WTUNRNG5,XRNG5
          ADD       WTUNRNG6,XRNG6
.
          CALL      UPTMP1
          GOTO      CUDN9999
.
CUDN1000  MOVE      WTUNRNG1,XRNG1
          MOVE      WTUNRNG2,XRNG2
          MOVE      WTUNRNG3,XRNG3
          MOVE      WTUNRNG4,XRNG4
          MOVE      WTUNRNG5,XRNG5
          MOVE      WTUNRNG6,XRNG6
          MOVE      ZERO,XIAAS
.
          CALL      WRTMP1
.
CUDN9999  RETURN
+
.**********************************************************************
.*                         END0000                                    *
.*                      End of Report                                 *
.**********************************************************************
END0000   COMPARE   ZERO,CPAGENO
          GOTO      END1000 IF NOT EQUAL
.
.         No data selected
.
          DISPLAY   *P1:24,*EL,*B,*P29:24,*V2LON:
                    "** No Data Extracted **",*W;
          CALL      HEAD0000
          PRINT     *N,*N,"*** End of Report ***"
          GOTO      END9999
.
END1000   COMPARE   "47",CLNO
          GOTO      END1100 IF LESS
          CALL      HEAD0000
          GOTO      END1200
.
END1100   CALL      PGE0000
.
END1200   PRINT     *N,*N,"IAA = Number of patients who were added to & ":
                    "admitted from the Waiting List in the period requested.":
                    *N,"Note: This report uses 30 day months.":
                    *N,"      The actual day ranges are : ":
                    "Less than ",RNG1," Month  =   0 to ",WTCNMRG1," Days":
                    *N,*35,RNG1," to ",RNG2," Months     = ",DRNG1," to ":
                    WTCNMRG2," Days":
                    *N,*35,ERNG2," to ",RNG3," Months     = ",DRNG2," to ":
                    WTCNMRG3," Days":
                    *N,*35,ERNG3," to ",RNG4," Months     = ",DRNG3," to ":
                    WTCNMRG4," Days":
                    *N,*35,ERNG4," to ",RNG5," Months     = ",DRNG4," to ":
                    WTCNMRG5," Days":
                    *N,*36,"Over ",RNG5," Months     = Over   ",WTCNMRG5:
                           " Days":
                    *N,*N,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W;
.
END9999   CALL      KILL0000        * remove temporary file 
          RETURN
+
.**********************************************************************
.*                         PRNT0000                                   *
.*                        Print Data                                  *
.**********************************************************************
PRNT0000  DISPLAY   *P1:24,*EL,*V2LON,"*** ",*BLINKON,"Generating Report",*HOFF:
                               *V2LON," ***",*HOFF,*P45:24,"Doctor  :  ";
.
          MOVE      ZERO,PRTFLG
          MOVE      SP10,KEY9
.
          CALL      RDSTMP1
PRNT1000
          CALL      RDKTMP1
          BRANCH    OVRCD,PRNT9999
.
          DISPLAY   *P56:24,*V2LON,XDOCT;
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      XRNG1,TOTAL      * calculate the total number of patients
          ADD       XRNG2,TOTAL
          ADD       XRNG3,TOTAL
          ADD       XRNG4,TOTAL
          ADD       XRNG5,TOTAL
          ADD       XRNG6,TOTAL
.
.         Determine whether to print grand totals by checking whether 
.         the doctor code is equal to "ZZZZZZ"
.
          MATCH     XDOCT,SIXZ
          GOTO      PRNT2000 IF NOT EQUAL
.
.         If PRTFLG is not set to zero print the Doctor Grand Totals,
.         else print the priority routine Grand Totals
.
          COMPARE   ZERO,PRTFLG
          GOTO      PRNT2000 IF NOT EQUAL
.
          COMPARE   "51",CLNO
          GOTO      PRNT1100 IF LESS
          CALL      HEAD0000
          GOTO      PRNT1200
.
PRNT1100  PRINT     *1,"|",*37,"|",*48,"|",*60,"|",*72,"|",*84,"|":
                    *96,"|",*108,"|",*120,"|",*132,"|"
          ADD       ONE,CLNO
          CALL      PGE0000
.
PRNT1200  MOVE      "Grand Totals                     ",DOCDESC
          MOVE      ONE,PRTFLG
.
          PRINT     *1,"| ",DOCDESC,*37,"|",*39,TOTAL,*48,"|",*51,XRNG1:
                    *60,"|",*63,XRNG2,*72,"|",*75,XRNG3,*84,"|",*87,XRNG4:
                    *96,"|",*98,XRNG5,*108,"|",*111,XRNG6,*120,"|":
                    *123,XIAAS,*132,"|"
          ADD       ONE,CLNO
          GOTO      PRNT1000 
.
.         If XPRTY = spaces it means a change in doctor has occurred
.         so print the new doctors name and totals
.
PRNT2000  MATCH     SP3,XPRTY
          GOTO      PRNT4000 IF NOT EQUAL
.
          MOVE      "Unknown Doctor                   ",DOCDESC     
          MOVE      XDOCT,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,PRNT3000
          CALL      DNAM0000                * pack doctors name
.
PRNT3000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,"|",*37,"|",*48,"|",*60,"|",*72,"|",*84,"|":
                    *96,"|",*108,"|",*120,"|",*132,"|":
                    *N,*1,"| ",DOCDESC,*37,"|",*39,TOTAL,*48,"|",*51,XRNG1:
                    *60,"|",*63,XRNG2,*72,"|",*75,XRNG3,*84,"|",*87,XRNG4:
                    *96,"|",*98,XRNG5,*108,"|",*111,XRNG6,*120,"|":
                    *123,XIAAS,*132,"|"
          ADD       TWO,CLNO
          GOTO      PRNT1000 
.
.         Print Priority Totals
.
PRNT4000  MOVE      "Unknown Priority    ",PRTDESC     * priority description
          PACK      KEY5,CODETP,XPRTY
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT5000
          MOVE      TDESC,PRTDESC
.
PRNT5000  COMPARE   "56",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,"| ",*7,PRTDESC,SP4,XPRTY,*37,"|",*39,TOTAL,*48,"|":
                    *51,XRNG1,*60,"|",*63,XRNG2,*72,"|",*75,XRNG3,*84,"|":
                    *87,XRNG4,*96,"|",*98,XRNG5,*108,"|",*111,XRNG6,*120,"|":
                    *123,XIAAS,*132,"|"
          ADD       ONE,CLNO
.
          ADD       XRNG1,GRNG1      * add totals to grand totals
          ADD       XRNG2,GRNG2  
          ADD       XRNG3,GRNG3 
          ADD       XRNG4,GRNG4
          ADD       XRNG5,GRNG5  
          ADD       XRNG6,GRNG6 
          ADD       XIAAS,GIAAS
          ADD       TOTAL,GTOTAL
          GOTO      PRNT1000 
.
PRNT9999  RETURN
+
.**********************************************************************
.*                         DNAM0000                                   *
.*                     Pack Doctor's Name                             *
.**********************************************************************
DNAM0000  MOVE      SP1,ANS
          MOVE      DGNAME,ANS      * get doctors first initial
.
          ENDSET    DTITL           * find the end of doctors title
DNAM1000  CMATCH    SP1,DTITL
          GOTO      DNAM2000 IF NOT EQUAL
          BUMP      DTITL BY -1
          GOTO      DNAM2000 IF EOS
          GOTO      DNAM1000
.
DNAM2000  LENSET    DTITL
          RESET     DTITL
.
.         Pack DOCDESC with title, first initial and surname
.
          CLEAR     DOCDESC
          APPEND    DTITL,DOCDESC
          APPEND    DOT,DOCDESC
          APPEND    ANS,DOCDESC
          APPEND    DOT,DOCDESC
          APPEND    DSNAME,DOCDESC
          RESET     DOCDESC
.
          RETURN
+
.**********************************************************************
.*                         HEAD0000                                   *
.*                      Print Headings                                *
.**********************************************************************
HEAD0000  COMPARE   ZERO,CPAGENO
          GOTO      HEAD1000 IF EQUAL
          PRINT     *1,"|",*37,"|",*48,"|",*60,"|",*72,"|",*84,"|":
                    *96,"|",*108,"|",*120,"|",*132,"|"
          CALL      PGE0000
.
HEAD1000  CLOCK     TIME,CTIMEIS
          CALL      HEAD132
.
          PRINT     *40,"Period : ",PDATE,*59,DRGLDSC:
                    *N,*59,STRDATE," to ",ENDDATE,*N
.
          CALL      PGE0000
.
          PRINT     *1,"|",*37,"|",*48,"|",*55,"< ",RNG1,*60,"|":
                    *64,RNG1," - ",RNG2,*72,"|",*76,ERNG2," - ",RNG3:
                    *84,"|",*88,ERNG3," - ",RNG4,*96,"|":
                    *100,ERNG4," - ",RNG5,*108,"|",*115,"> ",RNG5:
                    *120,"|",*132,"|":
                    *N,*1,"| Doctor",*37,"|",*42,"Total",*48,"|":
                    *53,"Months",*60,"|",*65,"Months",*72,"|",*77,"Months":
                    *84,"|",*89,"Months",*96,"|",*101,"Months",*108,"|":
                    *113,"Months",*120,"|",*128,"IAA",*132,"|"
.
          CALL      PGE0000
          MOVE      TEN,CLNO
.
HEAD9999  RETURN
+
.**********************************************************************
.*                         PGE0000                                    *
.*                        Print Line                                  *
.**********************************************************************
PGE0000   PRINT     *1,"*--------------------------------------------------":
                    "---------------------------------------------------":
                    "-----------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.**********************************************************************
.*                         CREA0000                                   *
.*                  Create Temporary File                             *
.**********************************************************************
CREA0000  DISPLAY   *P1:24,*EL,*P25:24,*V2LON:
                    "*** Creating Temp Index file ***";
.
          CALL      KILL0000
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    ANATMP,CMDLINE
          APPEND    " 45 u1-6,7-9",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      ANATMPXX,ANATMP
          RETURN
+
.**********************************************************************
.*                         KILL0000                                   *
.*                  Remove Temporary File                             *
.**********************************************************************
KILL0000  CLOSE     ANATMPXX
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    ANATMP,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.**********************************************************************
.*              I/O For Temporary File                                *
.**********************************************************************
RDSTMP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      ANATMPXX,KEY9;;
          RETURN
.
RDKTMP1   MOVE      ZERO,OVRCD
          READKS    ANATMPXX;XDOCT,XPRTY,XRNG1,XRNG2,XRNG3,XRNG4:
                             XRNG5,XRNG6,XIAAS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTMP1    MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      ANATMPXX,KEY9;XDOCT,XPRTY,XRNG1,XRNG2,XRNG3,XRNG4:
                             XRNG5,XRNG6,XIAAS
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMP1    UPDATE    ANATMPXX;XDOCT,XPRTY,XRNG1,XRNG2,XRNG3,XRNG4:
                             XRNG5,XRNG6,XIAAS
          RETURN
.
WRTMP1    RESET     KEY9
          WRITE     ANATMPXX,KEY9;KEY9,XRNG1,XRNG2,XRNG3,XRNG4:
                             XRNG5,XRNG6,XIAAS
          RETURN
+
.**********************************************************************
.*              I/O INCludes for files and general routines           *
.**********************************************************************
.
          INCLUDE   STD001IO
          INCLUDE   PATCOMN3
          INC       TFILENAM
.
          INC       IBASEQIO/INC
          INC       PATCODIO/INC       * Patient Codes file
          INC       PATDO1IO/INC       * Patient Doctors file
          INCLUDE   PATDRGIO/INC       * Period Date Range file
.
          INCLUDE   WATUDSIO/INC       * W/List Unit/Doctor Summary file
          INCLUDE   WATUNSIO/INC       * W/List Unit Summary file
          INC       WATSEAIO/INC
          INC       WATSEIIO/INC
          INC       WEBERRIO/INC
